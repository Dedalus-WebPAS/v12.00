. *****************************************************************************
. *                                                                           *
. * Routine Name  : GETVIMUL                                                  *
. *                                                                           *
. * Function      : To utilise the multi database search feature.  It will    *
. *                 loop over each of the databases Visit files for the UR    *
. *                 and extract all the visits to a temp file.                *
. *                                                                           *
. * External Rout :                                                           *
. *                 OTRF0000 - this routine will open the required file       *
. * Modifications :                                                           *
. *   V9.03.02  08/08/2003  Jill Habasque  SRF 41815                          *
. *             Changed reads of bokdiaaf                                     *
. *   V9.03.01  10/07/2003  Jill Habasque  SRF 40342                          *
. *             Replaced CHEOCREF procedure with CREF0000                     *
. *   V5.09.01  10/09/2001  Steve Downing  SRF 12287                          *
. *             Added check if record already exists before performing WRTEMP2*
. *****************************************************************************
. *   V5.08.04  12/04/2001  Steve Downing  SRF 647911                         *
. *             Added check for other hospitals sharing patvisaf              *
. *   V5.08.03  05/07/2000  Steve Armstrong      srf 646877                   *
. *             Fixed setting of PVIDATE and PVISITE when calling ODOC0000    *
. *             in GBOK0000 and OREF0000.                                     *
. *   V5.08.02  22/06/2000  Steve Armstrong      srf 646877                   *
. *             Removed default of "Unknown clinic"                           *
. *   V5.08.01  12/03/2000  Steve Armstrong                                   *
. *             Mods for changes to OUTCLIFD (effective date)                 *
. *****************************************************************************
. *   V5.07.01  27/08/1999  Jill Habasque                                     *
. *             Commented out STCNDOCT so only the new Doctor file is opened  *
. *   V5.05.02 19/04/1998   Steve Armstrong                                   *
. *   Fixed bug in loading hospital number (XXHOS2)                           *
. *   Mods for 6th hospital database   (STH)                                  *
. *   Fixed display of Dr/Clinic in UnLinked Event Screen                     *
. *   V5.05.01 11/12/1997   Nick SRF 643605                                   *
. *   Fixed display of Dr/Clinic in UnLinked Event Screen                     *
. *****************************************************************************
. *   V5.04.01 28/1/97 Paul Howells                                           *
. *   Removed PATDPTHV/INC                                                    *
. *   V5.04.02 20/3/97 Paul Howells                                           *
. *   Move spaces to XXDDE1 (Doctor Desc) if non Outpatient referral          *
. *   Don't Display Other Referral Event.                                     *
. *****************************************************************************
          DEFPROC   GETVIMUL
.
          INC       AAEDE1FD/INC            * aae details file
          INC       BOKRC1FD/INC            * booking file
          INC       DISMISFD/INC            * district nursing file
          INC       OUTBOAFD/INC            * slots file
          INC       OUTBB1FD/INC            * booking file
          INC       OUTCLIFD/INC            * clinic id file
          INC       OUTCTYFD/INC            * clinic type file
          INC       OUTMA1FD/INC            * outpatient master file
          INC       OUTRF1FD/INC            * referrals file
          INC       OUTSITFD/INC            * site file
          INC       PATDO1FD/INC            * doctor file
          INC       PATHOSFD/INC            * database file
          INC       PATHSPFD/INC            * hospital file
          INC       PATMI1FD/INC            * admission  file
          INC       PATVISFD/INC            * visit  file
          INC       PATWR1FD/INC            * ward   file
          INC       WATTR1FD/INC            * waiting list file
          INC       OUTDIAFD/INC
          INC       BOKDIAFD/INC
.
IBCNMHOS  FORM      1
HOSPNUM   FORM      2
.
GETVI000  OPEN      CONTROLF,"controlf" 
          READ      CONTROLF,SIXTY;*129,MRCNNOHS  * number of data directories
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*244,CHOSPNUM
.
          IF        IBCNMHOS = ONE
            OPEN      PATHSPA1,"pathspaf"
          ENDIF
.
          DISPLAY   *P1:24,*EL:
                    *P20:24,"Searching...  Visit [        ]";
.
          MOVE      CHOSPNUM,HOSPNUM
          MOVE      ZERO,HOSPCNT            * clear the counter
          COMPARE   ZERO,MRCNNOHS
          GOTO      GETVI080 IF EQUAL
.
.         loop over each data directory
.
GETVI050  ADD       ONE,HOSPCNT             * increment hospital counter
          COMPARE   HOSPCNT,MRCNNOHS
          GOTO      GETVI999 IF LESS        * finished number of hospitals
.
          MOVE      HOSPCNT,HOSPNUM
.
GETVI080  PACK      XXDPATH,SP20,SP20,SP20
          LOAD      XXDPATH,HOSPCNT,HDPATH1,HDPATH2,HDPATH3,HDPATH4,HDPATH5,HDPATH6
          IF        HOSPCNT <> ZERO
            MATCH     SP30,XXDPATH
            GOTO      GETVI050 IF EQUAL       * no DPATH for hospital
          ENDIF
.
          MOVE      "patvisaf",OPFILE
          MOVE      TEN8,FILENUM  
          CALL      OMDFN000                * open appropriate visit file
          BRANCH    EXIT,GETVI700           * no visit file, try O/P file
.
          MOVE      ONE,COUNTR
          STORE     PATH,HOSPCNT,PATH1,PATH2,PATH3,PATH4,PATH5,PATH6
.
GETVI090  COMPARE   HOSPCNT,COUNTR
          IF        @LESS
            LOAD      OLDPATH,COUNTR,PATH1,PATH2,PATH3,PATH4,PATH5,PATH6
            MATCH     OLDPATH,PATH
            IF        @EQUAL
              GOTO      GETVI050    * same path, dont loop thru again
            ENDIF
            ADD       ONE,COUNTR
            GOTO      GETVI090
          ENDIF
.
          MOVE      ONE,FILENUM  
          MOVE      "patmi1af",OPFILE
          CALL      OMDFN000                * open appropriate visit file
          BRANCH    EXIT,GETVI700           * no visit file, try O/P file
.
          CALL      OPTWD000                * open the ward file
          BRANCH    EXIT,GETVI700           * no visit file, try O/P file
.
          CALL      OPTDC000                * doctor
          BRANCH    EXIT,GETVI700           * no visit file, try O/P file
.
          MOVE      "outsitaf",OPFILE       * op site 
          MOVE      FORTY,FILENUM  
          CALL      OMDFN000
          BRANCH    EXIT,GETVI700
.
          CALL      OAEDA000                * a&e details
          BRANCH    EXIT,GETVI700
.
          PACK      KEY24,PURNO,PTCNLEDT,SP30
          CALL      RDSVISA2
GETVI100  CALL      RDKVISA2
          BRANCH    OVRCD,GETVI500
.
          MATCH     PVIURNO,PURNO                 * same U/R No.?
          GOTO      GETVI500 IF NOT EQUAL
.
          BRANCH    PVITYPE,GETVI110,GETVI110,GETVI110:
                            GETVI100,GETVI100,GETVI110
          GOTO      GETVI100
.
GETVI110  DISPLAY   *P41:24,PVIBILL;
.
.         get extra details for each visit type
.
          PERFORM   PVITYPE,MLAAE000,MLOUT000,MLINP000:
                            MLDUM000,MLDUM000,MLCOM000
.
. ------- make sure the visit is not already linked ----------------
.
          PACK      KEY10,HOSPNUM,PVIBILL,SP10
          CALL      RDECLNK2
          BRANCH    OVRCD,GETVI200
.
. ------- check whether linked to this episode ---------------------
.
          PACK      KEY23,ECRFURNO,ECRFEPNO,HOSPNUM,PVIBILL,SP20
          CALL      RDECLNK1
          BRANCH    OVRCD,GETVI100                * not linked to this episode
.
. ------- linked to this episode ----------------------------------
.
          MOVE      PVIBILL,XXEVN2                * visit No.
          MOVE      PVIDATE,XXDAT2                * visit date
          MOVE      PVIDOCT,XXDOC2                * Doctor/Clinic
          MOVE      DOCTDESC,XXDDE2
          MOVE      HOSPNUM,XXHOS2  
          MOVE      PTHSHOSP,XXHSP2         * hospital code
          MOVE      ZERO,XXSEL2
          MOVE      VTYPE,XXTYP2
          MOVE      VDIAG,XXDIA2
.
          PACK      KEY18,XXDAT2,XXEVN2,XXHOS2,SP10
          CALL      WRTEMP3                * write a rec to temp file
          GOTO      GETVI100                * get next visit
.
GETVI200  MOVE      PVIBILL,XXEVN1                * visit No.
          MOVE      PVIDATE,XXDAT1                * visit date
          MOVE      PVIDOCT,XXDOC1                * Doctor/Clinic
          MOVE      DOCTDESC,XXDDE1
          MOVE      DOCTDESC,XXDDE2
          MOVE      HOSPNUM,XXHOS1  
          MOVE      PTHSHOSP,XXHSP1         * hospital code
          MOVE      ZERO,XXSEL1
          MOVE      VTYPE,XXTYP1
          MOVE      VDIAG,XXDIA1
.
          PACK      KEY18,XXDAT1,XXEVN1,XXHOS1,SP10
          CALL      WRTEMP2                * write a rec to temp file
          GOTO      GETVI100                * get next visit
.
GETVI500  CALL      OREF0000                * get Outpatient Referral Letters
          CALL      GBOK0000                * get future O/P bookings
          CALL      GDNA0000                * get district nursing
          CALL      IPBK0000                * get inpatient bookings
          CALL      WAIT0000                * get Waiting List Patients
.
GETVI700  COMPARE   ZERO,MRCNNOHS
          GOTO      GETVI050 IF NOT EQUAL   * using many directories
.
GETVI999  GOTO      ENDGETVI
+
.*********************************************************************
.         get the future OP bookings
.*********************************************************************
GBOK0000  IF        OTCNNSID = 1
            MOVE      "out",UID
            MOVE      "out",OSTFILE
            GOTO      GBOK2200
          ENDIF
.
          CALL      CTMPO000                * create temp file 
          MOVE      SP6,KEY6
          CALL      RDSSITA1
.
GBOK1000  CALL      RDKSITA1
          BRANCH    OVRCD,GBOK2000          * end if file
.
          MOVE      OSTFILE,KEY3
          READ      TEMPO,KEY3;ANS
          GOTO      GBOK1000 IF NOT OVER    * already on temp file
.
          WRITE     TEMPO,KEY3;OSTFILE
          GOTO      GBOK1000
.
.         loop over temp file to look at all bookings
.
GBOK2000  MOVE      SP3,KEY3
          READ      TEMPO,KEY3;;
.
GBOK2100  BRANCH    OTCNNSID,GBOK9999 
          READKS    TEMPO;OSTFILE
          GOTO      GBOK9500 IF OVER        * end of file
.
GBOK2200  PACK      OPFILE,OSTFILE,FILBOKA3
          MOVE      SEVEN,FILENUM
          CALL      OMDFN000                * open bookings file
          BRANCH    EXIT,GBOK2100
.
          CALL      OOTMA000                * op clinic master
          BRANCH    EXIT,GBOK2100
.
          PACK      OPFILE,OSTFILE,FILDIAG1
          MOVE      "69",FILENUM
          CALL      OMDFN000                * open diagnosis file
          BRANCH    EXIT,GBOK2100
.
          PACK      OPFILE,OSTFILE,FILCLIA2
          MOVE      THIRTY6,FILENUM
          CALL      OMDFN000                * op clinic id - index 2
          BRANCH    EXIT,GBOK2100
.
          PACK      KEY36,PURNO,PTCNLEDT,SP30,SP10
          CALL      RDSBOKA3
.
. loop over the bookings file for the patients UR number
.
GBOK3000  CALL      RDKBOKA3
          BRANCH    OVRCD,GBOK2100          * no more data
.
          MATCH     PURNO,OBAURNO
          GOTO      GBOK2100 IF NOT EQUAL   * no more data
.
          COMPARE   ONE,OBASTAT
          GOTO      GBOK3000 IF NOT EQUAL   * not booked
.
          DISPLAY   *P41:24,OBAOUTNO
.
. ------- make sure the visit is not already linked ----------------
.
          PACK      KEY10,HOSPNUM,OBAOUTNO,SP10
          CALL      RDECLNK2
          BRANCH    OVRCD,GBOK4000
.
. ------- check whether linked to this episode ---------------------
.
          PACK      KEY23,ECRFURNO,ECRFEPNO,HOSPNUM,OBAOUTNO,SP20
          CALL      RDECLNK1
          BRANCH    OVRCD,GBOK3000                * not linked to this episode
.
          MOVE      OBAOUTNO,XXEVN2 
          MOVE      HOSPNUM,XXHOS2  
          MOVE      OBADATE,XXDAT2
          MOVE      "12",XXTYP2 
          MOVE      OBACLIN,XXDOC2
          MOVE      OBACLIN,PVIDOCT
          MOVE      OBASITE,PVISITE
          MOVE      OBADATE,PVIDATE
          CALL      ODOC0000                     * doctor desc
          MOVE      DOCTDESC,XXDDE2
          MOVE      ZERO,XXSEL2
.
          MOVE      SP3,OTMAHOSP
          PACK      KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT
          CALL      RDMASA1
.
          MOVE      OTMAHOSP,XXHSP2
.
. ------- get the diagnosis ------------------------------------
.
          PACK      OPDIDESC,SP30,SP30
          PACK      KEY8,XXEVN2,SP10
          CALL      RDDIAG1
          MOVE      OPDIDESC,XXDIA2
.
          PACK      KEY18,XXDAT2,XXEVN2,XXHOS2,SP10
          CALL      WRTEMP3                * write a rec to temp file
          GOTO      GBOK3000
.
GBOK4000  MOVE      OBAOUTNO,XXEVN1 
          MOVE      HOSPNUM,XXHOS1  
          MOVE      OBADATE,XXDAT1
          MOVE      "12",XXTYP1 
          MOVE      OBACLIN,XXDOC1
          MOVE      OBACLIN,PVIDOCT
          MOVE      OBASITE,PVISITE
          MOVE      OBADATE,PVIDATE
          CALL      ODOC0000                     * doctor desc
          MOVE      DOCTDESC,XXDDE1
          MOVE      ZERO,XXSEL1
.
          MOVE      SP3,OTMAHOSP
          PACK      KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT
          CALL      RDMASA1
.
          MOVE      OTMAHOSP,XXHSP1
.
. ------- get the diagnosis ------------------------------------
.
          PACK      OPDIDESC,SP30,SP30
          PACK      KEY8,XXEVN1,SP10
          CALL      RDDIAG1
          MOVE      OPDIDESC,XXDIA1
.
          PACK      KEY18,XXDAT1,XXEVN1,XXHOS1,SP10
          CALL      RATEMP2
          IF        OVRCD=1
            CALL      WRTEMP2                * write a rec to temp file
          ENDIF
          GOTO      GBOK3000
.
GBOK9500  CALL      DTMPO000
.
GBOK9999  RETURN
.*********************************************************************
.         get the Outpatient Referrals which aren't booked
.*********************************************************************
OREF0000  CALL      OOTRF000
          BRANCH    EXIT,OREF9999
.
          PACK      KEY24,PURNO,PTCNLEDT,SP30
          CALL      RSOTRFL2
.
OREF1000  CALL      RKOTRFL2
          BRANCH    OVRCD,OREF9999          * no more details
          MATCH     PURNO,OTRLURNO
          GOTO      OREF9999 IF NOT EQUAL   * no more details
.
. see if the referral is booked or not
.
          DISPLAY   *P41:24,OTRLOUTN
          MATCH     SP8,OTRLBDTE
          GOTO      OREF1000 IF NOT EQUAL   * referral is booked
.
. ------- Do display if this is the Outpat external referral for this EOC ---
. ------- Don't display if this is an other external referral for this EOC ---
.
          COMPARE   ONE,OTRLTREF              Other referral ?
          GOTO      OREF1000 IF EQUAL         Yes
.
          MOVE      "16",XXTYP2 
          MOVE      "16",XXTYP1 
          MOVE      ECRFREFN,DIM8
          MATCH     OTRLOUTN,DIM8
          GOTO      OREF2000 IF EQUAL 
.
          MOVE      OTRLOUTN,KEY8
          CALL      CREF0000
          BRANCH    EXIT,OREF1000
          MOVE      "11",XXTYP2 
          MOVE      "11",XXTYP1 
.
. ------- make sure the referral is not already linked ----------------
.
OREF2000  PACK      KEY10,HOSPNUM,OTRLOUTN,SP10
          CALL      RDECLNK2
          BRANCH    OVRCD,OREF4000
.
. ------- check whether linked to this episode ---------------------
.
          PACK      KEY23,ECRFURNO,ECRFEPNO,HOSPNUM,OTRLOUTN,SP20
          CALL      RDECLNK1
          BRANCH    OVRCD,OREF1000                * not linked to this episode
.
. ------- Referral is linked to this episode ---------------------------
.
          MOVE      OTRLOUTN,XXEVN2 
          MOVE      HOSPNUM,XXHOS2  
          MOVE      OTRLDATE,XXDAT2
.
          MOVE      OTRLCONS,XXDOC2
          MOVE      OTRLCONS,PVIDOCT
          MOVE      OTRLDATE,PVIDATE
          MOVE      OTRLSITE,PVISITE
          MOVE      SP20,XXDDE2
.
. ------- outpatient referral ----------------------------------
.
          MOVE      "out",OSTFILE
          MOVE      OTRLSITE,KEY6
          CALL      RDSITA1
.
          PACK      OPFILE,OSTFILE,FILCLIA2
          MOVE      THIRTY6,FILENUM
          CALL      OMDFN000                * op clinic id - index 2
          BRANCH    EXIT,OREF1000
          CALL      ODOC0000                     * doctor desc
          MOVE      DOCTDESC,XXDDE2
.
          MOVE      ZERO,XXSEL2
          MOVE      SP10,XXHSP2
.
          MOVE      OTRLDIAG,XXDIA2
.
          PACK      KEY18,XXDAT2,XXEVN2,XXHOS2,SP10
          CALL      RATEMP3
          IF        OVRCD=1
            CALL      WRTEMP3                * write a rec to temp file
          ENDIF
          GOTO      OREF1000
.
. ------- Referral is Not Linked ---------------------------------------
.
OREF4000  MOVE      OTRLOUTN,XXEVN1 
          MOVE      HOSPNUM,XXHOS1  
          MOVE      OTRLDATE,XXDAT1
.
          MOVE      OTRLCONS,XXDOC1
          MOVE      OTRLCONS,PVIDOCT
          MOVE      OTRLDATE,PVIDATE
          MOVE      OTRLSITE,PVISITE
          MOVE      "out",OSTFILE
          MOVE      OTRLSITE,KEY6
          CALL      RDSITA1
.
          PACK      OPFILE,OSTFILE,FILCLIA2
          MOVE      THIRTY6,FILENUM
          CALL      OMDFN000                * op clinic id - index 2
          BRANCH    EXIT,OREF1000
          CALL      ODOC0000                     * doctor desc
          MOVE      DOCTDESC,XXDDE1
.
          MOVE      ZERO,XXSEL1
          MOVE      SP10,XXHSP1
.
          MOVE      OTRLDIAG,XXDIA1
.
          PACK      KEY18,XXDAT1,XXEVN1,XXHOS1,SP10
          CALL      RATEMP2
          IF        OVRCD=1
            CALL      WRTEMP2                * write a rec to temp file
          ENDIF
          GOTO      OREF1000
.
OREF9999  RETURN
.*********************************************************************
.         get the District Nursing admissions
.*********************************************************************
GDNA0000  MOVE      "dismisaf",OPFILE
          MOVE      SIXTY,FILENUM
          CALL      OMDFN000
          BRANCH    EXIT,GDNA9999           * not using district nursing
.
          MOVE      ONE,DISSTAT                  * try admitted status
.
GDNA1000  PACK      KEY17,DISSTAT,PURNO,SP10
          CALL      RSDSMIS2
.
. loop over the district admission file for the patients UR number
.
GDNA2000  CALL      RKDSMIS2
          BRANCH    OVRCD,GDNA2500          * end of details for current status
.
          MATCH     PURNO,DSMIURNO
          GOTO      GDNA3000 IF EQUAL       * have a patient
.
GDNA2500  MATCH     "3",DISSTAT
          GOTO      GDNA9999 IF EQUAL
.
          MOVE      THREE,DISSTAT           * try discharged status
          GOTO      GDNA1000
.
. write the District Nursing details to temp file
.
GDNA3000  DISPLAY   *P41:24,DSMIVIST
.
          MATCH     PTCNLEDT,DSMIDATE
          GOTO      GDNA2000 IF LESS
.
. ------- make sure the visit is not already linked ----------------
.
          PACK      KEY10,HOSPNUM,DSMIVIST,SP10
          CALL      RDECLNK2
          BRANCH    OVRCD,GDNA4000
.
. ------- check whether linked to this episode ---------------------
.
          PACK      KEY23,ECRFURNO,ECRFEPNO,HOSPNUM,DSMIVIST,SP20
          CALL      RDECLNK1
          BRANCH    OVRCD,GDNA2000                * not linked to this episode
.
          MOVE      DSMIVIST,XXEVN2               * visit No.
          MOVE      HOSPNUM,XXHOS2  
          MOVE      DSMIDATE,XXDAT2
          MATCH     "1",DISSTAT
          IF        @EQUAL
            MOVE      "13",XXTYP2 
          ELSE
            MOVE      "14",XXTYP2 
          ENDIF
          MOVE      DSMIATTD,XXDOC2
          MOVE      XXDOC2,KEY6
          CALL      GDOCN000
          MOVE      DOCTDESC,XXDDE2
          MOVE      ZERO,XXSEL2
          MOVE      SP3,XXHSP2
          MOVE      DSMIDIA1,XXDIA2
.
          PACK      KEY18,XXDAT2,XXEVN2,XXHOS2,SP10
          CALL      WRTEMP3                * write a rec to temp file
          GOTO      GDNA2000
.
GDNA4000  MOVE      DSMIVIST,XXEVN1               * visit No.
          MOVE      HOSPNUM,XXHOS1  
          MOVE      DSMIDATE,XXDAT1
          MATCH     "1",DISSTAT
          IF        @EQUAL
            MOVE      "13",XXTYP1 
          ELSE
            MOVE      "14",XXTYP1 
          ENDIF
          MOVE      DSMIATTD,XXDOC1
          MOVE      XXDOC1,KEY6
          CALL      GDOCN000
          MOVE      DOCTDESC,XXDDE1
          MOVE      ZERO,XXSEL1
          MOVE      SP3,XXHSP1
          MOVE      DSMIDIA1,XXDIA1
.
          PACK      KEY18,XXDAT1,XXEVN1,XXHOS1,SP10
          CALL      RATEMP2
          IF        OVRCD=1
            CALL      WRTEMP2                * write a rec to temp file
          ENDIF
          GOTO      GDNA2000
.
GDNA9999  RETURN
.*********************************************************************
.         get all the waiting list entries (unscheduled)
.*********************************************************************
WAIT0000  CALL      OWTWM000
          BRANCH    EXIT,WAIT9999           * no file
.
          PACK      KEY20,ONE,PURNO,SP20
          CALL      RDSTREA2
.
WAIT1000  CALL      RDKTREA2
          BRANCH    OVRCD,WAIT9999          * no more details
.
          COMPARE   ONE,WMSTAT
          GOTO      WAIT9999 IF NOT EQUAL   * no more details
.
          MATCH     PURNO,WMURNO
          GOTO      WAIT9999 IF NOT EQUAL   * no more details
.
          MATCH     PTCNLEDT,WMDATE1
          GOTO      WAIT1000 IF LESS
.
          DISPLAY   *P41:24,WMBOOK  
.
. ------- make sure the visit is not already linked ----------------
.
          PACK      KEY10,HOSPNUM,WMBOOK,SP10
          CALL      RDECLNK2
          BRANCH    OVRCD,WAIT4000
.
. ------- check whether linked to this episode ---------------------
.
          PACK      KEY23,ECRFURNO,ECRFEPNO,HOSPNUM,WMBOOK,SP20
          CALL      RDECLNK1
          BRANCH    OVRCD,WAIT1000                * not linked to this episode
.
          MOVE      WMBOOK,XXEVN2 
          MOVE      HOSPNUM,XXHOS2  
          MOVE      WMDATE1,XXDAT2
          MOVE      "9",XXTYP2 
          MOVE      WMDOCTOR,XXDOC2
          MOVE      XXDOC2,KEY6
          CALL      GDOCN000
          MOVE      DOCTDESC,XXDDE2
          MOVE      ZERO,XXSEL2
          MOVE      WTWMIHSP,XXHSP2
          MOVE      WMREASON,XXDIA2
.
          PACK      KEY18,XXDAT2,XXEVN2,XXHOS2,SP10
          CALL      WRTEMP3                * write a rec to temp file
          GOTO      WAIT1000
.
WAIT4000  MOVE      WMBOOK,XXEVN1 
          MOVE      HOSPNUM,XXHOS1  
          MOVE      WMDATE1,XXDAT1
          MOVE      "9",XXTYP1 
          MOVE      WMDOCTOR,XXDOC1
          MOVE      XXDOC1,KEY6
          CALL      GDOCN000
          MOVE      DOCTDESC,XXDDE1
          MOVE      ZERO,XXSEL1
          MOVE      WTWMIHSP,XXHSP1
          MOVE      WMREASON,XXDIA1
.
          PACK      KEY18,XXDAT1,XXEVN1,XXHOS1,SP10
          CALL      RATEMP2
          IF        OVRCD=1
            CALL      WRTEMP2                * write a rec to temp file
          ENDIF
          GOTO      WAIT1000
.
WAIT9999  RETURN
+
.*********************************************************************
.         get the inpatient bookings
.*********************************************************************
IPBK0000  CALL      OBKRC000
          BRANCH    EXIT,IPBK9999
.
          MOVE      "bokdiaaf",OPFILE
          MOVE      "68",FILENUM  
          CALL      OMDFN000                * open appropriate visit file
.
          PACK      KEY16,PURNO,SP8
          CALL      RSBKREC6
IPBK1000  CALL      RKBKREC6
          BRANCH    OVRCD,IPBK9999          * no more details
.
          MATCH     PURNO,BKREURNO
          GOTO      IPBK9999 IF NOT EQUAL   * no more details
.
          DISPLAY   *P41:24,BKREBOOK
.
          COMPARE   ZERO,BKRESTAT
          GOTO      IPBK1000 IF NOT EQUAL   * only want bookings
.
          MATCH     PTCNLEDT,BKREEDAT
          GOTO      IPBK1000 IF LESS
.
. ------- make sure the visit is not already linked ----------------
.
          PACK      KEY10,HOSPNUM,BKREBOOK,SP10
          CALL      RDECLNK2
          BRANCH    OVRCD,IPBK4000
.
. ------- check whether linked to this episode ---------------------
.
          PACK      KEY23,ECRFURNO,ECRFEPNO,HOSPNUM,BKREBOOK,SP20
          CALL      RDECLNK1
          BRANCH    OVRCD,IPBK1000                * not linked to this episode
.
          MOVE      BKREBOOK,XXEVN2               * visit No.
          MOVE      HOSPNUM,XXHOS2  
          MOVE      BKREEDAT,XXDAT2
          MOVE      "10",XXTYP2 
          MOVE      BKREADOC,XXDOC2
          MOVE      XXDOC2,KEY6
          CALL      GDOCN000
          MOVE      DOCTDESC,XXDDE2
          MOVE      ZERO,XXSEL2
.
. ------- get the Hospital Code (from ward) -------------------
.
          MOVE      SP3,PTWDHOSN
          PACK      KEY6,BKREWARD,SP3
          CALL      RDWARD1
          MOVE      PTWDHOSN,XXHSP2
.
. ------- get the diagnosis ------------------------------------
.
          PACK      BKDIDES1,SP30,SP30
          PACK      KEY18,XXEVN2,SP70
          CALL      RSBKDIA1
          CALL      RKBKDIA1
          BRANCH    OVRCD,IPBK3000
          MATCH     XXEVN2,DBKDIBOO
          IF        !@EQUAL
            MOVE      SP70,BKDIDES1
          ENDIF
.
IPBK3000  MOVE      BKDIDES1,XXDIA2
.
          PACK      KEY18,XXDAT2,XXEVN2,XXHOS2,SP10
          CALL      WRTEMP3                * write a rec to temp file
          GOTO      IPBK1000
.
IPBK4000  MOVE      BKREBOOK,XXEVN1               * visit No.
          MOVE      HOSPNUM,XXHOS1  
          MOVE      BKREEDAT,XXDAT1
          MOVE      "10",XXTYP1 
          MOVE      BKREADOC,XXDOC1
          MOVE      XXDOC1,KEY6
          CALL      GDOCN000
          MOVE      DOCTDESC,XXDDE1
          MOVE      ZERO,XXSEL1
.
. ------- get the Hospital Code (from ward) -------------------
.
          MOVE      SP3,PTWDHOSN
          PACK      KEY6,BKREWARD,SP3
          CALL      RDWARD1
          MOVE      PTWDHOSN,XXHSP1
.
. ------- get the diagnosis ------------------------------------
.
          PACK      BKDIDES1,SP30,SP30
          PACK      KEY18,XXEVN1,SP70
          CALL      RSBKDIA1
          CALL      RKBKDIA1
          BRANCH    OVRCD,IPBK5000
          MATCH     XXEVN1,DBKDIBOO
          IF        !@EQUAL
            MOVE      SP70,BKDIDES1
          ENDIF
.
IPBK5000  MOVE      BKDIDES1,XXDIA1
.
          PACK      KEY18,XXDAT1,XXEVN1,XXHOS1,SP10
          CALL      RATEMP2
          IF        OVRCD=1
            CALL      WRTEMP2                * write a rec to temp file
          ENDIF
          GOTO      IPBK1000
.
IPBK9999  RETURN
.
+
.*********************************************************************
.         dummy routine for visits types 4,5
.*********************************************************************
MLDUM000  MOVE      SP3,PTHSHOSP 
          RETURN
.*********************************************************************
.         get the hospital code for Emergency                        
.*********************************************************************
MLAAE000  MOVE      SP3,PTHSHOSP 
          MOVE      ONE,VTYPE                 * patient type
.
          PACK      ADADIAG,SP30,SP30
          MOVE      PVIBILL,KEY8
          CALL      RDDETA1
          MOVE      ADADIAG,VDIAG
.
          MOVE      PVIDOCT,KEY6
          CALL      GDOCN000
.
MLAAE999  RETURN
+
.*********************************************************************
. ODOC0000 - get doctor description
.*********************************************************************
ODOC0000  MOVE      SP6,OCADOCT
.
          PACK      KEY14,PVIDOCT,PVIDATE
          CALL      RDCLIA2
          IF        OVRCD = 1
            CALL      RDPCLIA2
            IF        OVRCD = 0
              MATCH     PVISITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     PVIDOCT,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      SP30,OCADESC       * srf 646877
          ENDIF
          MOVE      OCADESC,DOCTDESC     * Save the clinic description
          BRANCH    OVRCD,ODOC9999
.
          MATCH     SP6,OCADOCT
          IF        !@EQUAL
            MOVE      OCADOCT,KEY6   
            CALL      GDOCN000                * session id - consultant
          ENDIF
.
ODOC9999  RETURN
+
.*********************************************************************
.         get the Consultant name
.         Para's  : KEY6          the doctor code
.*********************************************************************
GDOCN000  MOVE      SP30,DOCTDESC
          MATCH     SP6,KEY6   
          GOTO      GDOCN999 IF EQUAL       * no doctor code
.
          MOVE      "Invalid Code",DOCTDESC
          CALL      RDDOCT1
          IF        OVRCD = ZERO
            MOVE      SP4,PACTITLE
            MOVE      DGNAME,PACGNAME
            MOVE      DSNAME,PACSNAME
            MOVE      TWO,PACFRMT 
            CALL      PACNAME
            MOVE      PACFNAME,DOCTDESC
          ENDIF
GDOCN999  RETURN
.*********************************************************************
.         get the hospital code for outpatients                      
.         Returns : OVRCD = 1     invalid details
.*********************************************************************
MLOUT000  MOVE      SP3,PTHSHOSP 
          MOVE      TWO,VTYPE                 * patient type
          MOVE      SP20,DOCTDESC
.
          IF        OTCNNSID = 1
            MOVE      "out",UID
            MOVE      "out",OSTFILE
          ELSE
            MOVE      PVISITE,KEY6
            CALL      RDSITA1
            BRANCH    OVRCD,MLOUT999
          ENDIF
.
          PACK      OPFILE,OSTFILE,FILBOKA2
          MOVE      "45",FILENUM
          CALL      OMDFN000                * open bookings file
          BRANCH    EXIT,MLOUT999
.
          CALL      OOTMA000                * op clinic master
          BRANCH    EXIT,MLOUT999
.
          CALL      OOTBB000                * op booking B
          BRANCH    EXIT,MLOUT999
.
          PACK      OPFILE,OSTFILE,FILDIAG1
          MOVE      "69",FILENUM
          CALL      OMDFN000                * open diagnosis file
          BRANCH    EXIT,MLOUT999
.
          PACK      OPFILE,OSTFILE,FILCLIA2
          MOVE      THIRTY6,FILENUM
          CALL      OMDFN000                * op clinic id - index 2
          BRANCH    EXIT,MLOUT999
.
. get the Consultant Name
.
          CALL      ODOC0000
.
. ------- get the diagnosis ------------------------------------
.
          PACK      OPDIDESC,SP30,SP30
          PACK      KEY8,PVIBILL,SP10
          CALL      RDDIAG1
          MOVE      OPDIDESC,VDIAG 
          
.         get the booking details
.
MLOUT050  MOVE      PVIBILL,KEY8
          CALL      RDBOKB1
          BRANCH    OVRCD,MLOUT999          * no details
.
          PACK      KEY35,PVISITE,OBBCTYP,FOUR,PVIDATE,SP30
          CALL      RDSBOKA2
.
MLOUT100  CALL      RDKBOKA2
          BRANCH    OVRCD,MLOUT999          * no boka details
.
          MATCH     OBASITE,PVISITE
          GOTO      MLOUT999 IF NOT EQUAL   * no boka details
.
          MATCH     OBACTYP,OBBCTYP
          GOTO      MLOUT999 IF NOT EQUAL   * no boka details
.
          MATCH     PVIDATE,OBADATE
          GOTO      MLOUT999 IF NOT EQUAL   * no boka details
.
          COMPARE   PVIBILL,OBAOUTNO
          GOTO      MLOUT100 IF NOT EQUAL   * different booking number
.
          PACK      KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT
          CALL      RDMASA1
          BRANCH    OVRCD,MLOUT999          * invalid clinic master
.
          MOVE      OTMAHOSP,PTHSHOSP
.
MLOUT999  RETURN
+
.*********************************************************************
.         get the hospital code                                   
.         Returns : OVRCD = 1     invalid details
.*********************************************************************
MLINP000  MOVE      SP10,PTHSHOSP 
.
          MOVE      PVIDOCT,KEY6
          CALL      GDOCN000
.
. get the hospital code
.
          MOVE      PVIBILL,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,MLINP999
.
          MOVE      ADIAG1,VDIAG
.
          LOAD      VTYPE,ASTAT,SEVEN,THREE,FIVE,SIX,FOUR
.
          IF        ASTAT = ONE
            PACK      KEY6,PTMIXWRD,SP10
          ELSE
            PACK      KEY6,AWARD,SP10
          ENDIF
          CALL      RDWARD1
          MOVE      PTWDHOSN,PTHSHOSP
.        
MLINP999  RETURN
.*********************************************************************
.         get the hospital code for community health                 
.*********************************************************************
MLCOM000  MOVE      SP3,PTHSHOSP 
          MOVE      EIGHT,VTYPE
          MOVE      SP20,VDIAG
          MOVE      SP20,DOCTDESC
MLCOM999  RETURN
.*********************************************************************
.         open the required file
.*********************************************************************
OTRF0000  IF        FILENUM = 1
            CLOSE     PATMI1A1
            OPEN      PATMI1A1,PATH
          ENDIF
          IF        FILENUM = 5
            CLOSE     AAEDE1A1
            OPEN      AAEDE1A1,PATH
          ENDIF
          IF        FILENUM = 6
            CLOSE     OUTBOKA1
            OPEN      OUTBOKA1,PATH
          ENDIF
          IF        FILENUM = 7
            CLOSE     OUTBOKA3
            OPEN      OUTBOKA3,PATH
          ENDIF
          IF        FILENUM = 8
            CLOSE     OUTBB1A1
            OPEN      OUTBB1A1,PATH
          ENDIF
          IF        FILENUM = 15
            CLOSE     PATWR1A1
            OPEN      PATWR1A1,PATH
          ENDIF
          IF        FILENUM = 18
            CLOSE     PATVISA2
            OPEN      PATVISA2,PATH
          ENDIF
          IF        FILENUM = 20
            CLOSE     PATVISA1
            OPEN      PATVISA1,PATH
          ENDIF
          IF        FILENUM = 36
            CLOSE     OUTCLIA2
            OPEN      OUTCLIA2,PATH
          ENDIF
          IF        FILENUM = 37
            CLOSE     PATDO1A1
            OPEN      PATDO1A1,PATH
          ENDIF
          IF        FILENUM = 38
            CLOSE     OUTMA1A1
            OPEN      OUTMA1A1,PATH
          ENDIF
          IF        FILENUM = 40
            CLOSE     OUTSITA1
            OPEN      OUTSITA1,PATH
          ENDIF
          IF        FILENUM = 45
            CLOSE     OUTBOKA2
            OPEN      OUTBOKA2,PATH
          ENDIF
          IF        FILENUM = 60
            CLOSE     DISMISA2
            OPEN      DISMISA2,PATH
          ENDIF
          IF        FILENUM = 61
            CLOSE     OUTRF1A1
            OPEN      OUTRF1A1,PATH
          ENDIF
          IF        FILENUM = 62
            CLOSE     WATTR1A2
            OPEN      WATTR1A2,PATH
          ENDIF
          IF        FILENUM = 63
            CLOSE     BOKRC1A6
            OPEN      BOKRC1A6,PATH
          ENDIF
          IF        FILENUM = 65
            CLOSE     OUTRF1A2
            OPEN      OUTRF1A2,PATH
          ENDIF
          IF        FILENUM = 68
            CLOSE     BOKDIAA1
            OPEN      BOKDIAA1,PATH
          ENDIF
          IF        FILENUM = 69
            CLOSE     OUTDIAG1
            OPEN      OUTDIAG1,PATH
          ENDIF
.
OTRF9999  RETURN
.*********************************************************************
.         open the O/P booking file
.*********************************************************************
OOTBB000  PACK      OPFILE,OSTFILE,FILBB1A1
          MOVE      EIGHT,FILENUM
          CALL      OMDFN000
OOTBB999  RETURN
.*********************************************************************
.         open the outpatient master file
.*********************************************************************
OOTMA000  PACK      OPFILE,OSTFILE,FILMA1A1
          MOVE      THIRTY8,FILENUM
          CALL      OMDFN000
OOTMA999  RETURN
.*********************************************************************
.         open the ward file
.*********************************************************************
OPTWD000  MOVE      "patwr1af",OPFILE 
          MOVE      TEN5,FILENUM
          CALL      OMDFN000
OPTWD999  RETURN
.*********************************************************************
.         open the waiting list details file
.*********************************************************************
OWTWM000  MOVE      "wattr1af",OPFILE
          MOVE      SIXTY2,FILENUM
          CALL      OMDFN000
OWTWM999  RETURN
+
.*********************************************************************
.         open the booking file
.*********************************************************************
OBKRC000  MOVE      "bokrc1af",OPFILE
          MOVE      SIXTY3,FILENUM
          CALL      OMDFN000
OBKRC999  RETURN
.*********************************************************************
.         open the A&E details file
.*********************************************************************
OAEDA000  MOVE      "aaede1af",OPFILE
          MOVE      FIVE,FILENUM
          CALL      OMDFN000
OAEDA999  RETURN
.*********************************************************************
.         open the outpatient referral file
.*********************************************************************
OOTRF000  MOVE      "outrf1af",OPFILE
          MOVE      SIXTY1,FILENUM
          CALL      OMDFN000
          MOVE      SIXTY5,FILENUM
          CALL      OMDFN000
OOTRF999  RETURN
+
.*********************************************************************
.         open the doctor file
.*********************************************************************
OPTDC000  MOVE      "patdo1af",OPFILE
          MOVE      THIRTY7,FILENUM
          CALL      OMDFN000
OPTDC999  RETURN
.
.****************************************************************************
.* CREF0000 - Check Whether the referral is an External Referral (EOC)
.****************************************************************************
CREF0000  OPEN      EOCREFA1,"eocrefaf"
          OPEN      EOCREFA2,"eocrefaf"
          MOVE      ZERO,EXIT
.
          PACK      KEY13,ECRFURNO,DECRFEPN,SP70
          PACK      KEY15,OTRLURNO,CHOSPNUM,SP30
          CALL      RSECREF2
CREF1000  CALL      RKECREF2
          BRANCH    OVRCD,CREF9999
.
          MATCH     OTRLURNO,ECRFURNO
          GOTO      CREF9999 IF NOT EQUAL
.
          COMPARE   CHOSPNUM,ECRFHOSP
          GOTO      CREF9999 IF NOT EQUAL
.
          MOVE      ECRFREFN,KEY8
          MATCH     OTRLOUTN,KEY8
          GOTO      CREF1000 IF NOT EQUAL
.
          MOVE      ONE,EXIT
.
CREF9999  CALL      RDECREF1
          RETURN
.
.
          INC       PATDPATH
          INC       IBAOVRIO/INC
          INC       PATDO1IO/INC
          INC       PATMI1IO/INC
          INC       PATVISIO/INC
          INC       BOKRC1IO/INC
          INC       OUTCLIIO/INC
          INC       OUTCTYIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTMA1IO/INC
          INC       OUTSITIO/INC
          INC       OUTRF1IO/INC
          INC       PATHOSIO/INC
          INC       PATHSPIO/INC
          INC       PATWR1IO/INC
          INC       WATTR1IO/INC
          INC       DISMISIO/INC
          INC       OUTDIAIO/INC
          INC       BOKDIAIO/INC
          INC       AAEDE1IO/INC
.
ENDGETVI  ENDPROC
