.*****************************************************************************
.*    Include Name : OPCHKCUR.PBL                                            *
.*    System       : Theatre System                                          *
.*    Description  : Check for current inpatients in session                 *
.*                                                                           *
.*    Author       : Rod Knowles                                             *
.*    Date         : 07/11/90                                                *
.*    Modifications: 23/01/91 ROD                                            *
.*                   Changed size of var PREV01..13 to DIM23                 *
.*                   11/03/91 J.Tan     SRF 108051                           *
.*                   Fixed displaying no U/R patients                        *
.*****************************************************************************
.*                                                                           *
.*    PARAMETERS   : CURSESS   - must be current session                     *
.*                               ie. Date, Time & Clinic/Surgeon             *
.*                                                                           *
.*    FILES NEEDED : OPRSESFD, OPRDEAFD, PATMA1FD, PATMI1FD, PATPR1FD        *
.*                   BOKRC1FD, OPR001FD                                      *
.*                                                                           *
.*****************************************************************************

.*************************************************************************** 
.*               display patients not in hospital                          *
.***************************************************************************
OPCHKCUR  CALL      CHKCU000                      * check for >= 1 pat not curr.
          BRANCH    EXIT,DISCS999                 * nothing to display
.
.  there is at least 1 non inpatient so proceed
.
. display headings
.
          DISPLAY   *P1:8,*EF:                    * erase screen
                    *P5:9,"The following patients are not currently ":
                          "in the hospital"
.
          DISPLAY   *P1:11,*V2LON,*ULON,"Case",*HOFF,SP1,*V2LON,*ULON:
                    "Patient Name                          ",*HOFF,SP1:
                    *V2LON,*ULON," U/R No.",*HOFF,SP1,*V2LON,*ULON:
                    "Phone       ",*HOFF,SP1,*V2LON,*ULON,"Status        "
.
          MOVE      TEN2,CVERT                    * init screen row
          MOVE      ONE,CCOL                      * init screen column
          MOVE      ONE,SCREEN                    * init screen no.
.
          PACK      KEY23,CURSESS,ZERO,SP20       * position at start
          CALL      RSOPDEA1
DISCS100  CALL      RKOPDEA1                      * read next record
          BRANCH    OVRCD,DISCS500                * no more records
.
. save key
.
          PACK      KEY23,OPDADATE,OPDATIME,OPDACLIN,OPDASTAT,OPDACASE
.
          MATCH     TEMPDATE,OPDADATE             * still same date?
          GOTO      DISCS500 IF NOT EQUAL
.
          MATCH     TEMPTIME,OPDATIME             * still same time?
          GOTO      DISCS500 IF NOT EQUAL
.          
          MATCH     TEMPCLIN,OPDACLIN             * still same session?
          GOTO      DISCS500 IF NOT EQUAL
.
          COMPARE   ZERO,OPDASTAT                 * booked?
          GOTO      DISCS500 IF NOT EQUAL
.
. now check if there is an admission record with ASTAT <> 2
.
          PACK      KEY8,OPDAADMN
          CALL      RDMISS1
          BRANCH    OVRCD,DISCS150               * no record exists
.
          COMPARE   TWO,ASTAT                     * inpatient?
          GOTO      DISCS100 IF EQUAL
.
. we have a patient not currently in hospital so proceed
.
. check if enough room for display
.
DISCS150  COMPARE   TWENTY3,CVERT   
          GOTO      DISCS300 IF LESS
.
. we need a new screen
.
DISCS160  CALL      S1TYP0000                     * ask question with Next scrn
          BRANCH    EXIT,DISCS200:                * Previous screen selected
                         DISCS250:                * Next screen selected
                         DISCS999                 * Review was selected
.
. previous screen was selected
. (allow for 13 screens)
DISCS200  LOAD      KEY23,SCREEN,PREV01,PREV02,PREV03,PREV04,PREV05,PREV06:
                          PREV07,PREV08,PREV09,PREV10,PREV11,PREV12,PREV13
.
          CALL      RSOPDEA1                      * pos on 1st key on screen
.
. read back x records until 1st key on previous screen is reached
.
          MOVE      ZERO,COUNTER                  * init counter
DISCS220  CALL      RPOPDEA1                      * read back 1 record
          ADD       ONE,COUNTER                   * increment counter
          COMPARE   TEN1,COUNTER                  * check if read far enough
          GOTO      DISCS220 IF NOT EQUAL
.
. we have read back far enough
.
          DISPLAY   *P1:12,*EF                    * erase screen
          MOVE      TEN2,CVERT                    * init screen row
          SUB       ONE,SCREEN                    * update screen no.
          GOTO      DISCS300
.
. Next screen was selected
.
DISCS250  ADD       ONE,SCREEN                    * increment screen no.
.
. save the current key
.
          STORE     KEY23,SCREEN,PREV01,PREV02,PREV03,PREV04,PREV05,PREV06:
                          PREV07,PREV08,PREV09,PREV10,PREV11,PREV12,PREV13
.
          DISPLAY   *P1:12,*EF                   * erase screen
.
          MOVE      TEN2,CVERT                    * init screen row
.
. display record
.
DISCS300  CALL      GDETS000                      * get details for display
          BRANCH    OVRCD,DISCS100                * missing record
.
          MOVE      PACFNAME,DESC40
          DISPLAY   *P1:CVERT,OPDACASE,*P6:CVERT,DESC40,*P45:CVERT,BKREURNO:
                    *P54:CVERT,PTELEP,*P67:CVERT,STATUSD
.
          ADD       ONE,CVERT                     * update row pos
          GOTO      DISCS100                      * get next record
.
. we have no more records to display
.
DISCS500  CALL     S2TYP0000
          BRANCH   EXIT,DISCS200:                 * Previous screen selected
                        DISCS999                  * Continue was selected
.
DISCS999  RETURN
+
.***************************************************************************
.*                                GDETS000            Called by: DISCS000  *
.*                   get details for display                               *
.***************************************************************************
GDETS000
.
. get the patients name and format it
.
          PACK      KEY8,OPDAADMN
          CALL      RDMISS1
          BRANCH    OVRCD,GDETS100
.
          MOVE      AURNO,BKREURNO          * U/R number for display
          LOAD      STATUSD,ASTAT,PREADMIS,ANS,DISCHARG,ONLEAVE,CANCELLD
          GOTO      GDETS200 
.
. there is no admission record so get UR no. from booking file (BOKRC1FD)
.
GDETS100  PACK      KEY8,OPDAADMN
          CALL      RDBKREC1
          BRANCH    OVRCD,GDETS999
.
          COMPARE   TWO,BKRESTAT                  * get status
          GOTO      GDETS150 IF EQUAL
          MOVE      BOOKED,STATUSD                * Status="Booked"
          GOTO      GDETS200
.
GDETS150  MOVE      CANCELLD,STATUSD              * Status="Cancelled"
.
. we have a UR no. so check if it is 0   
.
GDETS200  MATCH     ZEROUR,BKREURNO
          GOTO      GDETS300 IF NOT EQUAL
.
          PACK      KEY8,OPDAADMN
          CALL      RDPRAM1
          BRANCH    OVRCD,GDETS999
          GOTO      GDETS350
.
. UR no is not zero so get name from PATMA1FD
.
GDETS300  PACK      KEY8,BKREURNO
          CALL      RDMAST1
          BRANCH    OVRCD,GDETS999
.
GDETS350  MOVE      PGNAME,PACGNAME
          MOVE      PSNAME,PACSNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
.
GDETS999  RETURN
+
.**************************************************************************
.*                                STIK0000            Called by: DISCS0000 *
.*                 ask line 24 question with (N)ext                       *
.**************************************************************************
S1TYP000  
. check which screen we are on. If 1st dont' display (P)revious
.
          BRANCH    SCREEN,S1TYP050
.
. screen is not 1st screen so display "(P)revious"
.
          DISPLAY   *P1:24,*EL,"(",*V2LON:
                    "N",*HOFF,")ext screen, (",*V2LON,"P",*HOFF,")revious": 
                    " screen, (",*V2LON,"C",*HOFF,")ontinue ?"
.
          MOVE      "48",CCOL                 * keyin position
          GOTO      S1TYP100                      * get keyin
.
. screen is 1st screen so don't display "(P)revious"
. 
S1TYP050  DISPLAY   *P1:24,*EL,"(",*V2LON:
                    "N",*HOFF,")ext screen, (",*V2LON,"C",*HOFF,")ontinue ?"
.
          MOVE      "29",CCOL                   * keyin position
.
. now get keyin
.
S1TYP100  KEYIN     *PCCOL:24,*DV,UNDLN1:
                    *PCCOL:24,*V2LON,*+,ANS:
                    *PCCOL:24,*DV,ANS
.
          REP       UPPLOW,ANS                  * Change to lower case
.
          MATCH     ANSN,ANS                    * Next screen?
          GOTO      S1TYP500 IF EQUAL
.
          MATCH     ANSC,ANS                    * Continue ?
          GOTO      S1TYP600 IF EQUAL
.
          MATCH     ANSP,ANS                    * Previous screen?
          GOTO      S1TYP700 IF EQUAL
.
          BEEP 
          GOTO      S1TYP100
.
S1TYP500  MOVE      TWO,EXIT      * Next screen
          GOTO      S1TYP999
S1TYP600  MOVE      THREE,EXIT    * Continue
          GOTO      S1TYP999
.
. Previous was selected so check if it was asked
.
S1TYP700  BRANCH    SCREEN,S1TYP720
          MOVE      ONE,EXIT                    * Previous was selected
          GOTO      S1TYP9999
.
. Previous was selected but wasn't asked so error
.
S1TYP720  BEEP
          GOTO      S1TYP100
.
S1TYP999  RETURN
+
.**************************************************************************
.*                                S2TYP0000            Called by: DISCS0000 *
.*                 ask line 24 question without (N)ext                    *
.**************************************************************************
S2TYP000  
. check which screen we are on. If 1st dont' display (P)revious
.
          BRANCH    SCREEN,S2TYP050
.
. screen is not 1st screen so display "(P)revious"
.
          DISPLAY   *P1:24,*EL,"(",*V2LON:
                    "P",*HOFF,")revious screen, (",*V2LON:
                    "C",*HOFF,")ontinue ?"
.
          MOVE      "33",CCOL                   * keyin position
          GOTO      S2TYP100                      * get keyin
.
. screen is 1st screen so don't display "(P)revious"
. 
S2TYP050  DISPLAY   *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue ?"
.
          MOVE      "14",CCOL                   * keyin position
.
. now get keyin
.
S2TYP100  KEYIN     *PCCOL:24,*DV,UNDLN1:
                    *PCCOL:24,*V2LON,*+,ANS:
                    *PCCOL:24,*DV,ANS
.
          REP       UPPLOW,ANS              
.
          MATCH     ANSC,ANS                    * Continue ?
          GOTO      S2TYP600 IF EQUAL
.
          MATCH     ANSP,ANS                    * Previous screen?
          GOTO      S2TYP700 IF EQUAL
.
          BEEP 
          GOTO      S2TYP100
.
S2TYP600  MOVE      TWO,EXIT    * Continue
          GOTO      S2TYP999
.
. Previous was selected so check if it was asked
.
S2TYP700  BRANCH    SCREEN,S2TYP720
          MOVE      ONE,EXIT                    * Previous was selected
          GOTO      S2TYP9999
.
. Previous was selected but wasn't asked so error
.
S2TYP720  BEEP
          GOTO      S2TYP100
.
S2TYP999  RETURN
+
.***********************************************************************
.*                              CHKCU000          Called by: DISCS000  *
.*               check that there is least 1 non inpatient             *
.***********************************************************************
CHKCU000  PACK      KEY23,CURSESS,ZERO,SP20
          CALL      RSOPDEA1                      * positional read
CHKCU100  CALL      RKOPDEA1                      * read next record
          BRANCH    OVRCD,CHKCU900                * end of file
.
          UNPACK    CURSESS,TEMPDATE,TEMPTIME,TEMPCLIN
.
          MATCH     TEMPDATE,OPDADATE             * still same date?
          GOTO      CHKCU900 IF NOT EQUAL
.
          MATCH     TEMPTIME,OPDATIME             * still same time?
          GOTO      CHKCU900 IF NOT EQUAL
.          
          MATCH     TEMPCLIN,OPDACLIN             * still same session?
          GOTO      CHKCU900 IF NOT EQUAL
.
          COMPARE   ZERO,OPDASTAT                 * booked?
          GOTO      CHKCU900 IF NOT EQUAL
.
. now check if there is an admission record with ASTAT <> 2
.
          PACK      KEY8,OPDAADMN
          CALL      RDMISS1
          BRANCH    OVRCD,CHKCU800                * no record exists
.
          COMPARE   TWO,ASTAT                     * inpatient?
          GOTO      CHKCU100 IF EQUAL
.
. we have a patient not currently in hospital so proceed
.
CHKCU800  MOVE      FALSE,EXIT
          GOTO      CHKCU999
.
CHKCU900  MOVE      TRUE,EXIT
.
CHKCU999  RETURN
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-***-*-*-***-*-*-*-*-*-*-***-*-*-*-*-*-*-*
