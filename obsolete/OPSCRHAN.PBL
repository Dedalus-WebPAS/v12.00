.*******************************************************************************
.* System          : Theatre System
.* Include Function: OPSCRHAN.PBL
.* Description     : Many screen handling routines 
.*******************************************************************************
.* Date            : 15/10/1990 Almost the end of a good year's programming.
.* Author          : Bill Dew
.* Modifications   : 
.*        V5.08.01  17/05/2000  Steve Downing
.*                  Mods to display patient's age with date of birth
.*                  19/02/1991 Graeme Williams
.*                  Correct the testing for a full screen.
.*                  Correct the code for fulling the screen.
.*                  11/03/1991 J.Tan    SRF 108051
.*                  Fixed displaying cancelled pre-admission
.*                  28/05/1991 Justin Coates
.*                  If invalid patient details, display message for name
.*                  09/10/1991 Justin Coates
.*                  Added obtaining PMI details for current case.
.*                  Added display of booking number to screen.
.*                  11/01/93  Whirlpool
.*                  Dudley Address changes
.*                  22/04/93 DIG
.*                           Re-Compiled for changes to PATALERT.
.*                  12/10/93  whirlpool    440401
.*                  Bug in dcase routine     
.*******************************************************************************
.  
.    FUNCTION ROSTER  DESCRIPTION
.    ---------------  ------------------------------------------------------
.    OPDSPCUR         The function loops thru list of patients on the 
.                     CASE01-CASE13 session, and re-display the details
.                     and reload the current case's PMI details.
.
.    OPSETNXT         The function loops thru the oprdetaf to get the next 
.                     screen of data 
.
.    OPSETPRV         The function loops backwards thru the oprdetaf to get 
.                     the previous screen of data and adjust the CASE vars 
.                     accordingly.
.
.    OPSETFST         The function setups the screen vars for first screen 
.                     by calling OPSETNXT
.
.    OPSETCUR         The function re-calculate current screen after an 
.                     modification has been performed in session.
.
.    OPMOVNXT         Move to Next booking.
.
.    OPMOVPRV         Move to Previous booking.
.
.*******************************************************************************
.
.    INCLUDES AND FILES.
.
.    FD's  : OPR001FD, OPRCONFD, OPRDEAFD, OPRDEBFD, OPRDECFD
.                                1      
.            PATMA1FD, PATPR1FD, BOKMASFD, PATMI1FD
.    I/O's : PATIOMAS, PATIOPRA, BOKIOMAA, PATIOMIS
.    PBL's : STDOPR01 
.
.*******************************************************************************
.                                 OPDSPCUR
.
.    The function loops thru list of patients on the CASE01-CASE13 session,
.    and re-display the details.
.  
.    PARAMETERS.
.    CURSESS       : Must be set to session date, time and clinic/doctor
.    CURCASE       : Current case number
.    NUMCASE       : Number of cases on current screen
.    CASE01-CASE13 : Should sequentially contain the case numbers displayed
.
.    RETURN PARAMETERS.
.    EXIT = 0 : No problems, a successful booking transfer
.    EXIT = 1 : Unsuccessful transfer eg invalid parameters, record cases not
.               on file
.    LINE01-LINE13 : contain the screen line number for each case.
.
.*******************************************************************************
OPDSPCUR  MOVE      CURSESS,KEY19           * current session
          CALL      RDOPSES1                * read session file
          BRANCH    OVRCD,DSPCUR99
.
          MOVE      ZERO,CLNO          * set the case counter
          DISPLAY   *P1:8,*EF,*P1:9,*EL,*V2LON,*ULON,"Case":
                    *P6:9,"Patient Name             ":
                    *P32:9," U/R No.",*P41:9,"Booking ",*P50:9,"Start":
                    *P56:9,"Dur"
.
          IF      OPCNPHDB = 0
            DISPLAY  *P60:9,*V2LON,*ULON,"Phone (H)         "
          ELSE  
            DISPLAY  *P60:9,*V2LON,*ULON,"Date of Birth/Age"
          ENDIF
          MOVE      TEN,YPOS           * set screen display line position
          DISPLAY   *P1:YPOS,*EF
.
DSPCUR11  ADD       ONE,CLNO           * increment case counter
          COMPARE   "14",CLNO
          GOTO      DSPCUR90 IF EQUAL
          LOAD      FORM3,CLNO,CASE01,CASE02,CASE03,CASE04,CASE05,CASE06:
                               CASE07,CASE08,CASE09,CASE10,CASE11,CASE12,CASE13
.
          PACK      KEY23,CURSESS,ZERO,FORM3
          CALL      RDOPDEA1           * get operation detail record
          BRANCH    OVRCD,DSPCUR11
.
. get preferred accommodation
.
          MOVE      OPDAADMN,BKREBOOK
          PACK      BKREACCM,SP10
          PACK      KEY8,BKREBOOK
          CALL      RDBKREC1
.
          CALL      DCASE000           * display a case record
          STORE     YPOS,CLNO,LINE01,LINE02,LINE03,LINE04,LINE05,LINE06:
                              LINE07,LINE08,LINE09,LINE10,LINE11,LINE12,LINE13
          ADD       ONE,YPOS           * increment line counter
.
          MOVE      ZERO,MODEFLAG      * set DPROV to display
          CALL      DPROV000           * display provisional codes and op desc
          COMPARE   NUMCASE,CLNO       * test all cases been displayed
          GOTO      DSPCUR11 IF LESS
.
.         get the details of the current case afer redisplaying the screen
.
DSPCUR90  LOAD      CURCASE,CURPOS,CASE01,CASE02,CASE03,CASE04,CASE05:
                                   CASE06,CASE07,CASE08,CASE09,CASE10:
                                   CASE11,CASE12,CASE13
.
          PACK      KEY23,CURSESS,ZERO,CURCASE
          CALL      RDOPDEA1
          BRANCH    OVRCD,DSPCUR99     * invalid details
          CALL      SHPAT000           * obtain PMI details for CURCASE
.
DSPCUR99  RETURN
+
.*******************************************************************************
.                   DCASE000
.         Display the current case
.*******************************************************************************
DCASE000  CALL      SHPAT000           * get patient Details
          CALL      SHSTF000           * status status flag into REPLY
.
          COMPARE   CLNO,CURPOS        * test ypos=selection pointer
          GOTO      DCASE222 IF NOT EQUAL
          DISPLAY   *P1:YPOS,*HON,OPDACASE
.
          CALL      PATALERT           * display the patient alert data
          GOTO      DCASE333
.
DCASE222  DISPLAY   *P1:YPOS,*V2LON,OPDACASE
.
DCASE333  MOVE      OPDAEXPD,FORM4
          DISPLAY   *P5:YPOS,REPLY:
                    *P6:YPOS,DESC25:
                    *P32:YPOS,AURNO:
                    *P41:YPOS,OPDAADMN:
                    *P50:YPOS,OPDAEXPS:
                    *P55:YPOS,FORM4
.
          IF        OPCNPHDB = 0
            DISPLAY   *P60:YPOS,PTELEP
          ELSE
            UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            DISPLAY   *P60:YPOS,CPCDATE:
                      *P74:YPOS,PSAGE
          ENDIF
DCASE999  RETURN
+
.*******************************************************************************
.                   DPROV000
.    Display provisional item or operation descriptions and increment the 
.    line counter.  If OPCNSUMM is ON then routine does nothing.
.    Parameters :
.       MODEFLAG = 0 : Display desc'n and increment line counter (disp current)
.       MODEFLAG = 1 : Increment line counter only (when setting next&prev)
.    Returns :
.       YPOS         : The updated line counter
.*******************************************************************************
DPROV000  BRANCH    OPCNSUMM,DPROV999       * not displaying descriptions
.
. ------- check on provisional item 1 -------
.
          MATCH     SP9,OPDAPROV
          GOTO      DPROV100 IF NOT EQUAL   * Something to display
.
          MATCH     SP30,OPDADES1
          GOTO      DPROV100 IF NOT EQUAL   * Something to display
.
          GOTO      DPROV150
.
.         We have something to display
.
DPROV100  BRANCH    MODEFLAG,DPROV110       * dont display description
.
          DISPLAY   *P7:YPOS,OPDAPROV," ",OPDADES1
.
DPROV110  ADD       ONE,YPOS                * increment line counter
.
. ------- check on provisional item 2 -------
.
DPROV150  MATCH     SP9,OPDAPRV2
          GOTO      DPROV200 IF NOT EQUAL   * Something to display
.
          MATCH     SP30,OPDADES2
          GOTO      DPROV200 IF NOT EQUAL   * Something to display
.
          GOTO      DPROV250
.
.         We have something to display
.
DPROV200  BRANCH    MODEFLAG,DPROV210       * dont display description
.
          DISPLAY   *P7:YPOS,OPDAPRV2," ",OPDADES2
.
DPROV210  ADD       ONE,YPOS                * increment line counter
.
. ------- check on provisional item 3 -------
.
DPROV250  MATCH     SP9,OPDAPRV3
          GOTO      DPROV300 IF NOT EQUAL   * Something to display
.
          MATCH     SP30,OPDADES3
          GOTO      DPROV300 IF NOT EQUAL   * Something to display
.
          GOTO      DPROV999
.
.         We have something to display
.
DPROV300  BRANCH    MODEFLAG,DPROV310       * dont display description
.
          DISPLAY   *P7:YPOS,OPDAPRV3," ",OPDADES3
.
DPROV310  ADD       ONE,YPOS                * increment line counter
.
DPROV999  RETURN
+
.*******************************************************************************
.                   SHPAT000
.    Screen Handling Get Patient details.
.    First get patients UR number from reading the Admission file.  If No
.    admission record then read the booking record file.   If zero or 
.    invalid UR number then read PATPR1FD
.*******************************************************************************
SHPAT000  MOVE      SP8,AURNO
          PACK      KEY8,OPDAADMN,SP8
          CALL      RDMISS1                 * get patients UR number 
          COMPARE   ZERO,OVRCD              * test if record exist
          GOTO      SHPAT333 IF EQUAL       * valid admission number
.
.         not on admission file, try booking file
.
          CALL      RDBKREC1                * get admission from booking master
          BRANCH    OVRCD,SHPAT444          * invalid booking number
          MOVE      BKREURNO,AURNO          * copy UR number from booking 
.
.         read master file for patients name
.
SHPAT333  MATCH     SP8,AURNO 
          GOTO      SHPAT444 IF EQUAL       * try pre-admission file
.
          MATCH     ZEROUR,AURNO 
          GOTO      SHPAT444 IF EQUAL       * try pre-admission file
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1                 * get surname given name and title
          COMPARE   ZERO,OVRCD
          GOTO      SHPAT555 IF EQUAL       * valid UR number
.
.         read pre-admission file
.
SHPAT444  PACK      KEY8,OPDAADMN,SP8
          CALL      RDPRAM1 
          BRANCH    OVRCD,SHPAT777          * invalid visit number
.
          MOVE      ZEROUR,PURNO  
.
.         format the name
.
SHPAT555  MOVE      TWO,PACFRMT        * title name surname of doctor
          MOVE      PSNAME,PACSNAME    * use pacdate to fix doctors name
          MOVE      PGNAME,PACGNAME    * get first letter in given name
          MOVE      PTITL,PACTITLE
          CALL      PACNAME            * pack up doctors name
          MOVE      PACFNAME,DESC31
          MOVE      DESC31,DESC25
          GOTO      SHPAT999
.
.         invalid admission number
.
SHPAT777  MOVE      ZEROUR,AURNO       * if UR blank then set to zero
          CLEAR     DESC31
          APPEND    "Invalid Admission No : ",DESC31
          APPEND    OPDAADMN,DESC31
          APPEND    SP30,DESC31
          RESET     DESC31
          CLEAR     DESC25
          APPEND    "Invalid Adm No : ",DESC25
          APPEND    OPDAADMN,DESC25
          APPEND    SP30,DESC25
          RESET     DESC25
          MOVE      SP20,PTELEP
.
SHPAT999  RETURN
+
.*******************************************************************************
.                   SHSTF000
.         Determine the current session status flag
.         Returns : REPLY         the status flag
.*******************************************************************************
SHSTF000  MOVE      SP3,REPLY          * clear the status flag
.
.         check if have usage details
.
          PACK      KEY11,OPDAUNIQ,SP20
          CALL      RSOPDEC1
          CALL      RKOPDEC1           * check operation usage details
          BRANCH    OVRCD,SHSTF333
.
          MATCH     OPDAUNIQ,OPDCUNIQ
          GOTO      SHSTF333 IF NOT EQUAL   * no usage details
.
          MOVE      "U",REPLY               * have usage details
          GOTO      SHSTF999
.
.         check if on a different theatre
.
SHSTF333  MATCH     SP6,OPDATHET       * check if patient is in default theatre
          GOTO      SHSTF444 IF EQUAL
.
          MATCH     OPDATHET,OPSETHET
          GOTO      SHSTF444 IF EQUAL       * in same theatre as session default
.
          MOVE      "*",REPLY               * in a different theatre
          GOTO      SHSTF999
.
.         check if have operating team set up
.
SHSTF444  PACK      KEY11,OPDAUNIQ,SP20
          CALL      RSOPDEB1
          CALL      RKOPDEB1           * check special operating team 
          BRANCH    OVRCD,SHSTF555
.
          MATCH     OPDAUNIQ,OPDBUNIQ
          GOTO      SHSTF555 IF NOT EQUAL   * no team details
.
          MOVE      "T",REPLY               * have an operating team set up
          GOTO      SHSTF999
.
SHSTF555  MOVE      SP3,REPLY               * no status flag to display
.
SHSTF999  RETURN
+
.*******************************************************************************
.                   OPSETNXT
.         The function loops thru the oprdetaf to get the next screen of data 
.         PARAMETERS :
.           CURSESS  : Must be set to session date, time and clinic/doctor.
.           NUMCASE  : Number of cases on current screen
.         RETURNS :
.           ISNEXT = 0 : Still more data concerning current session
.           ISNEXT = 1 : This is last screen
.           NUMCASE    : Number of cases on screen
.           CASE01-CASE14 : sequentially contain the case numbers displayed
.           LINE01-LINE14 : Contain the screen lines numbers corresp. to case.
.           ISPREV = CURPOS = 1
.*******************************************************************************
OPSETNXT  MOVE      ZERO,ISPREV        * reset previous page flag
          MOVE      ONE,CURPOS         * set to first case
          MOVE      TEN,YPOS           * set the starting screen line 
.
.         get last case of previous screen
.
          LOAD      FORM3,NUMCASE,CASE01,CASE02,CASE03,CASE04,CASE05,CASE06:
                           CASE07,CASE08,CASE09,CASE10,CASE11,CASE12,CASE13
          PACK      KEY23,CURSESS,ZERO,FORM3
          MOVE      ZERO,NUMCASE       * reset number of cases 
          CALL      CLSVR000           * clear screen vars
.
          CALL      RSOPDEA1           * start reading from the last record
.
.         get the cases for the screen
.
SETNXT11  CALL      RKOPDEA1           * read next operation record
          BRANCH    OVRCD,SETNXT90
.
          PACK      KEY19,OPDADATE,OPDATIME,OPDACLIN,SP20
          MATCH     KEY19,CURSESS      * test for same session
          GOTO      SETNXT90 IF NOT EQUAL
          COMPARE   ZERO,OPDASTAT      * test for booked records only
          GOTO      SETNXT90 IF NOT EQUAL
.
.         Calculate the position for this record.
.
          ADD       ONE,YPOS           * increment line counter
          MOVE      ONE,MODEFLAG       * signal count only in DPROV
          CALL      DPROV000           * display provisional codes and op desc
.
.         Check if there is enough room for this record on the current screen
.
          CALL      CHKPG000           * check for a full page
          BRANCH    EXIT,SETNXT80      * page is full
.
          ADD       ONE,NUMCASE        * increment number case counter
          MOVE      OPDACASE,FORM3     * convert dim to form
          STORE     FORM3,NUMCASE,CASE01,CASE02,CASE03,CASE04,CASE05,CASE06:
                              CASE07,CASE08,CASE09,CASE10,CASE11,CASE12,CASE13
          GOTO      SETNXT11
SETNXT80
          MOVE      ZERO,ISNEXT        * signal next page exist
          GOTO      SETNXT99
SETNXT90
          MOVE      ONE,ISNEXT         * signal NO next page
.
SETNXT99  RETURN
+
.*******************************************************************************
.                   CHKPG000
.         Check for a full screen of data
.         EXIT=0 if screen is not full
.         EXIT=1 if screen is full      ( simple )
.*******************************************************************************
CHKPG000  COMPARE   "23",YPOS
          GOTO      CHKPG900 IF LESS
          GOTO      CHKPG990
.
CHKPG900  MOVE      ZERO,EXIT               * page is OK
          GOTO      CHKPG999
.
CHKPG990  MOVE      ONE,EXIT                * page is full
.
CHKPG999  RETURN
+
.*******************************************************************************
.                   CLSVR000
.         Clear the screen save variables (case numbers & line numbers)
.*******************************************************************************
CLSVR000  MOVE      ZERO,CLNO
.
CLSVR222  ADD       ONE,CLNO
          STORE     ZERO,CLNO,CASE01,CASE02,CASE03,CASE04,CASE05,CASE06:
                               CASE07,CASE08,CASE09,CASE10,CASE11,CASE12,CASE13
.
          STORE     ZERO,CLNO,LINE01,LINE02,LINE03,LINE04,LINE05,LINE06:
                              LINE07,LINE08,LINE09,LINE10,LINE11,LINE12,LINE13
          COMPARE   TEN4,CLNO
          GOTO      CLSVR222 IF LESS
.
CLSVR999  RETURN
+
.*******************************************************************************
.                   OPSETPRV
.         The function loops backwards thru the oprdetaf to get the previous 
.         screen of data and adjust the CASE vars accordingly.
.         PARAMETERS.
.           CURSESS  : Must be set to session date, time and clinic/doctor
.         RETURN PARAMETERS.
.           ISPREV = 0 : Still Another previous screen
.           ISPREV = 1 : This is last screen
.           NUMCASE    : Number of cases on screen
.           CASE01-CASE14 : sequentially contain the case numbers displayed
.           LINE01-LINE14 : Contain the screen lines numbers corresp. to case.
.           CURPOS     : The selecting pointer is set to first record on screen
.           ISNEXT     : Flag is set to 0 signal next screen exist.
.*******************************************************************************
OPSETPRV  MOVE      ZERO,NUMCASE       * reset number of cases 
          MOVE      ZERO,ISNEXT        * reset previous page flag
          MOVE      ONE,CURPOS
          MOVE      TEN4,CLNO          * index to CASE and LINE
          MOVE      TEN,YPOS           * set starting display LINE
.
.         get first item of current page and read backwards
.
          PACK      KEY23,CURSESS,ZERO,CASE01
          CALL      CLSVR000           * clear screen vars
          CALL      RSOPDEA1           * start reading from the last record
.
.         read previous case
.
SETPRV11  CALL      RPOPDEA1           * read next operation record
          BRANCH    OVRCD,SETPRV90
.
          PACK      KEY19,OPDADATE,OPDATIME,OPDACLIN,SP20
          MATCH     KEY19,CURSESS      * test for same session
          GOTO      SETPRV90 IF NOT EQUAL
          COMPARE   ZERO,OPDASTAT      * test for booked records only
          GOTO      SETPRV90 IF NOT EQUAL
.         
.         Calculate the position for this record.
.
          ADD       ONE,YPOS           * increment line counter
          MOVE      ONE,MODEFLAG       * signal count only in DPROV
          CALL      DPROV000           * display provisional codes and op desc
.
.         Check if there is enough room for this record on the current screen
.
          CALL      CHKPG000           * check for a full page
          BRANCH    EXIT,SETPRV80
.
.         have a case to display
.
          ADD       ONE,NUMCASE        * increment number case counter
          SUB       ONE,CLNO
          MOVE      OPDACASE,FORM3     * convert dim to form
          STORE     FORM3,CLNO,CASE01,CASE02,CASE03,CASE04,CASE05,CASE06:
                               CASE07,CASE08,CASE09,CASE10,CASE11,CASE12,CASE13
          GOTO      SETPRV11
.
.         finished and page is full so has a previous screen
.
SETPRV80
          CALL      REVRS000           * reverse the screen vars
          MOVE      ZERO,ISPREV        * has a previous screen
          MOVE      NUMCASE,CURPOS     * set current prosition to last record
          GOTO      SETPRV95
.
.         finished and no previous screen
.
SETPRV90
          CALL      REVRS000           * reverse the screen vars
          CALL      FILUP000           * fill up the screen arrays
          MOVE      NUMCASE,CURPOS     * set current prosition to last record
          MOVE      ONE,ISPREV         * no previous screen
.
SETPRV95
.
SETPRV99  RETURN
+
.*******************************************************************************
.                                 REVRS000
.    The actual name of this routine lies.
.    Used after a previous screen has been done due to way Prev. screen works
.    When this routine gets the CASE vars they are usually in following 
.    format :
.    CASE   1  2  3  4  5  6  7  9 10 11 12 13
.    Value  0  0  0  0  0  0  1  2  3  4  5  6  
.
.    The finish product.    
.    CASE   1  2  3  4  5  6  7  9 10 11 12 13
.    Value  1  2  3  4  5  6  0  0  0  0  0  0
.*******************************************************************************
REVRS000  COMPARE   ZERO,NUMCASE
          GOTO      REVRS999 IF EQUAL
.
          MOVE      ZERO,CLNO
          MOVE      ZERO,FORM2
REVRS111
          ADD       ONE,CLNO
          LOAD      FORM3,CLNO,CASE01,CASE02,CASE03,CASE04,CASE05,CASE06:
                               CASE07,CASE08,CASE09,CASE10,CASE11,CASE12,CASE13
          COMPARE   ZERO,FORM3
          GOTO      REVRS111 IF EQUAL
REVRS222
.         
          LOAD      FORM3,CLNO,CASE01,CASE02,CASE03,CASE04,CASE05,CASE06:
                               CASE07,CASE08,CASE09,CASE10,CASE11,CASE12,CASE13
          STORE     ZERO,CLNO,CASE01,CASE02,CASE03,CASE04,CASE05,CASE06:
                              CASE07,CASE08,CASE09,CASE10,CASE11,CASE12,CASE13
.
          ADD       ONE,CLNO
          ADD       ONE,FORM2
.
          STORE     FORM3,FORM2,CASE01,CASE02,CASE03,CASE04,CASE05,CASE06:
                               CASE07,CASE08,CASE09,CASE10,CASE11,CASE12,CASE13
.
          COMPARE   TEN4,CLNO
          GOTO      REVRS222 IF LESS
REVRS999  RETURN
+
.*******************************************************************************
.                                 FILUP000
.    Position the fptr on the last case record and read forward until the
.    Screen is full.
.*******************************************************************************
FILUP000
          LOAD      FORM3,NUMCASE,CASE01,CASE02,CASE03,CASE04,CASE05,CASE06:
                               CASE07,CASE08,CASE09,CASE10,CASE11,CASE12,CASE13
          PACK      KEY23,CURSESS,ZERO,FORM3
          CALL      RSOPDEA1
FILUP111
          CALL      RKOPDEA1           * read next operation record
          BRANCH    OVRCD,FILUP999
.
          PACK      KEY19,OPDADATE,OPDATIME,OPDACLIN,SP20
          MATCH     KEY19,CURSESS      * test for same session
          GOTO      FILUP999 IF NOT EQUAL
          COMPARE   ZERO,OPDASTAT      * test for booked records only
          GOTO      FILUP999 IF NOT EQUAL
.
.         Calculate the position for this record.
.
          ADD       ONE,YPOS           * increment line counter
          MOVE      ONE,MODEFLAG       * signal count only in DPROV
          CALL      DPROV000           * display provisional codes and op desc
.
.         Check if there is enough room for this record on the current screen
.
          CALL      CHKPG000           * check for a full page
          BRANCH    EXIT,FILUP999
.
          ADD       ONE,NUMCASE        * increment number case counter
          MOVE      OPDACASE,FORM3     * convert dim to form
          STORE     FORM3,NUMCASE,CASE01,CASE02,CASE03,CASE04,CASE05,CASE06:
                              CASE07,CASE08,CASE09,CASE10,CASE11,CASE12,CASE13
          GOTO      FILUP111
FILUP999  RETURN
+
.*******************************************************************************
.                                 OPSETFST
.    The function setups the screen vars for first screen by calling OPSETNXT
.  
.    PARAMETERS.
.    CURSESS  : Must be set to session date, time and clinic/doctor
.
.    RETURN PARAMETERS.
.    ISPREV   : Sets No previous page ISPREV=1
.    EXIT = 1 : If No case records for current session hence NO first page
.    EXIT = 0 : Case records exist.
.    ISNEXT = 0 : Still more data concerning current session
.    ISNEXT = 1 : This is last screen
.    NUMCASE    : Number of cases on screen
.    CASE01-CASE14 : sequentially contain the case numbers displayed
.    LINE01-LINE14 : Contain the screen lines numbers corresponding to case.
.    CURPOS   : The current selector pointer is set to one.
.*******************************************************************************
OPSETFST
          CALL      CLSVR000           * clear screen array
          MOVE      ONE,NUMCASE        * reset number of cases 
          MOVE      ZERO,CASE01        * Trickery
.
          CALL      OPSETNXT           * start getting the next session
          COMPARE   ZERO,NUMCASE       * test for No cases in session
          GOTO      SETFST90 IF EQUAL
.
          MOVE      ONE,ISPREV        * set NO previous page
          MOVE      ZERO,EXIT         
          GOTO      SETFST99
SETFST90
          MOVE      ONE,EXIT
SETFST99  RETURN
+
.*******************************************************************************
.                                 OPSETCUR 
.    The function re-calculate current screen after an modification has been
.    performed in session.
.    PARAMETERS.
.    CURSESS  : Must be set to session date, time and clinic/doctor
.
.    RETURN PARAMETERS.
.    EXIT = 0 : There is a current screen.
.    EXIT = 1 : There is no current screen.
.    ISNEXT   : Flag will be set 1 if no next screen
.    ISPREV   : Flag will be set 1 if no previous screen
.    NUMCASE  : Number of cases on screen
.    CASE01-CASE14 : sequentially contain the case numbers displayed
.
.*******************************************************************************
OPSETCUR  MOVE      ONE,NUMCASE        * reset number of cases 
          MOVE      CASE01,CASE14      * save the original CASE01 in CASE14
          SUB       ONE,CASE01
.
          CALL      OPSETNXT           * start getting the next session
.
          COMPARE   ZERO,NUMCASE       * test for No cases in session
          GOTO      SETCUR44 IF EQUAL
.
          COMPARE   ONE,CASE14         * test for first screen
          GOTO      SETCUR80 IF NOT EQUAL
          MOVE      ONE,ISPREV         * set NO previous page
          GOTO      SETCUR80
.------------------------------------------------------------------------------
SETCUR44  COMPARE   ONE,CASE14
          GOTO      SETCUR90 IF EQUAL
          MOVE      CASE14,CASE01      * copy the save value back into original
.
          CALL      OPSETPRV
          MOVE      ONE,ISNEXT         * set no next screen
.------------------------------------------------------------------------------
SETCUR80  MOVE      ZERO,EXIT         
          GOTO      SETCUR99
.
SETCUR90  MOVE      ONE,EXIT
.
SETCUR99  RETURN
+
.*******************************************************************************
.                                 OPMOVNXT
.    Move to next booking.
.    Function : Will move to the next case number on the current screen. If the
.               user is currently on the last case number of the screen, the
.               program will automatically move to the next screen. If there is
.               no next screen, the program will beep. The current case number
.               will be displayed in reverse video (*HON) to indicate which is
.               the current case number.
.  
.    PARAMETERS.
.    CASE01-CASE14 : Should sequentially contain the case numbers displayed
.    LINE01-LINE14 : contain the screen line number for each case next page.
.
.    RETURN PARAMETERS.
.    LINE01-LINE14 : Will be re-adjusted if a next page exist
.    CASE01-CASE14 : Will be re-adjusted if a next page exist .
.*******************************************************************************
OPMOVNXT  MOVE      CURPOS,SAVEF10     * temporarily save current position
          ADD       ONE,CURPOS         * Increment to next position
.
          COMPARE   CURPOS,NUMCASE     * test if still on screen page
          GOTO      MOVNXT44 IF LESS
.
          LOAD      FORM3,SAVEF10,CASE01,CASE02,CASE03,CASE04,CASE05,CASE06:
                          CASE07,CASE08,CASE09,CASE10,CASE11,CASE12,CASE13
.
          LOAD      YPOS,SAVEF10,LINE01,LINE02,LINE03,LINE04,LINE05,LINE06:
                         LINE07,LINE08,LINE09,LINE10,LINE11,LINE12,LINE13

          DISPLAY   *P1:YPOS,*V2LON,FORM3
.
          LOAD      FORM3,CURPOS,CASE01,CASE02,CASE03,CASE04,CASE05,CASE06:
                          CASE07,CASE08,CASE09,CASE10,CASE11,CASE12,CASE13
.
          LOAD      YPOS,CURPOS,LINE01,LINE02,LINE03,LINE04,LINE05,LINE06:
                         LINE07,LINE08,LINE09,LINE10,LINE11,LINE12,LINE13
.
          DISPLAY   *P1:3,*EL,*P1:YPOS,*HON,FORM3
.
.         Display the alert information for this patient
.
          PACK      KEY23,CURSESS,ZERO,FORM3
          CALL      RDOPDEA1
          BRANCH    OVRCD,MOVNXT80
.
          CALL      SHPAT000           * Get the PMI details
.
          CALL      PATALERT           * display the patient alert data
          GOTO      MOVNXT80
.
MOVNXT44
          BRANCH    ISNEXT,MOVNXT90    * check if a next page exist
.
          PACK      KEY23,SESNDATE,SESNTIME,SESNCODE,ZERO,Z70
          CALL      RSOPDEA1
.
          CALL      RPOPDEA1
.
          MOVE      OPDACASE,CCITEM
.
          COMPARE   CCITEM,CURCASE
          GOTO      MOVNXT90 IF NOT LESS
.
          CALL      OPSETNXT           * setup next screen vars
          MOVE      SP20,OLDOPTS1      * line 24 options for calling program
                                       * will be redisplayed
          CALL      OPDSPCUR           * display next screen patient operation
.
MOVNXT80  MOVE      ZERO,EXIT
          GOTO      MOVNXT99
MOVNXT90
          BEEP
          MOVE      SAVEF10,CURPOS
          MOVE      ONE,EXIT
MOVNXT99  RETURN
+
.*******************************************************************************
.                                 OPMOVPRV
.    Move to Previous booking.
.
.    Function : Move to the previous case number on the current screen. If the
.               user is currently on the first case number of the screen, the
.               program will automatically move to previous screen. If there is
.               no previous screen, give em beep. The current case number
.               will be displayed in reverse video (*HON) to indicate which is
.               the current case number.
.  
.    PARAMETERS.
.    CASE01-CASE14 : Should sequentially contain the case numbers displayed
.    LINE01-LINE14 : contain the screen line number for each case next page.
.
.    RETURN PARAMETERS.
.    LINE01-LINE14 : Will be re-adjusted if a next page exist
.    CASE01-CASE14 : Will be re-adjusted if a next page exist
.
.*******************************************************************************
OPMOVPRV
          MOVE      CURPOS,SAVEF10     * temporarily save current position
          SUB       ONE,CURPOS         * decrement to next position
          COMPARE   CURPOS,ZERO        * test if still on screen page
          GOTO      MOVPRV44 IF EQUAL
.
          LOAD      FORM3,SAVEF10,CASE01,CASE02,CASE03,CASE04,CASE05,CASE06:
                          CASE07,CASE08,CASE09,CASE10,CASE11,CASE12,CASE13
.
          LOAD      YPOS,SAVEF10,LINE01,LINE02,LINE03,LINE04,LINE05,LINE06:
                         LINE07,LINE08,LINE09,LINE10,LINE11,LINE12,LINE13

          DISPLAY   *P1:YPOS,*V2LON,FORM3
.
          LOAD      FORM3,CURPOS,CASE01,CASE02,CASE03,CASE04,CASE05,CASE06:
                          CASE07,CASE08,CASE09,CASE10,CASE11,CASE12,CASE13
.
          LOAD      YPOS,CURPOS,LINE01,LINE02,LINE03,LINE04,LINE05,LINE06:
                         LINE07,LINE08,LINE09,LINE10,LINE11,LINE12,LINE13
.
          DISPLAY   *P1:3,*EL,*P1:YPOS,*HON,FORM3
.
.         Display the alert information for this patient
.
          PACK      KEY23,CURSESS,ZERO,FORM3
          CALL      RDOPDEA1
          BRANCH    OVRCD,MOVPRV80
.
          CALL      SHPAT000           * Get the PMI details
.
          CALL      PATALERT           * display the patient alert data
          GOTO      MOVPRV80
MOVPRV44
          BRANCH    ISPREV,MOVPRV90    * check if a next page exist
.
          CALL      OPSETPRV           * setup next screen vars
          MOVE      SP20,OLDOPTS1      * line 24 options for calling program
                                       * will be redisplayed
          CALL      OPDSPCUR           * display next screen patient operation
MOVPRV80
          MOVE      ZERO,EXIT
          GOTO      MOVPRV99
MOVPRV90
          BEEP
          MOVE      SAVEF10,CURPOS
          MOVE      ONE,EXIT
MOVPRV99  RETURN
+
