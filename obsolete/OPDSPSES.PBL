.*******************************************************************************
.* System         : Theatre System
.* Include Name   : OPDSPSES.PBL
.* Description    : Display available sessions.
.*******************************************************************************
.* Date           : 22/11/1990
.* Author         : Rod Knowles
.* Modifications  : 13/12/90 Justin Coates
.*                  Allow entering of a new date (after current date)
.*                  10/01/91 Graeme Williams
.*                  Ignore cancelled sessions
.*                  09/10/1991    Justin Coates  (User Group Changes)
.*                  Added ``New (D)ate'' option 
.*******************************************************************************
.
.    FUNCTION DESCRIPTION.
.
.    The function Displays all sessions on a selected date.  The user can
.    opt to select a session displayed or Cancel.
.
.    INCLUDES AND FILES.
.
.    FD's  : OPR001FD, OPRCONFD, PATCODFD, PATDO1FD, OPRSESFD, OPRCLIFD
.    I/O's : PATIOCOD, PATIODOC, OPRCLIIO, OPRSESIO
.    PBL's : STDOPR01 
.  
.    PARAMETERS.
.  
.    Store the date selected in STRTDATE in ccyymmdd format.
.
.    RETURN PARAMETERS.
.
.    KEY19    : is Session chosen otherwise set blank if no session selected.
.    EXIT     : = 0  A session was selected
.               = 1  No session was selected
.
.*******************************************************************************
.                                 OPDSPSES
.*******************************************************************************
OPDSPSES  DISPLAY   *PSUBOPT1:2,*V2LON,"- (Same Day Sessions) ";
          MOVE      STRTDATE,KEY8           * save default date
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P1:4,*EF,"Session Date : ",*V2LON,CPCDATE
.
.         allow for entering of a new date
.
GTSES050  MOVE      "4",CVERT               * row
          MOVE      "16",CCOL               * column
          MOVE      ONE,CDEFDTE             * hit enter once
          MOVE      ZERO,CCANLDTE           * cant cancel default
          DISPLAY   *P16:4,*EF
.
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY   * set up default
          CALL      KEYDATE
          BRANCH    CQUEST,GTSES050         * ? entered -> invalid
          BRANCH    OVRCD,GTSES050          * nothing entered
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
.
          MATCH     TODAY,STRTDATE
          GOTO      GTSES060 IF NOT LESS    * after current date
.
          DISPLAY   *P1:24,*B,"Date must be on or after today's date.  ",*EL;
          CALL      HITENTER 
          GOTO      GTSES050
.
GTSES060  PACK      DESC10,OPCNODSC,SP10
          DISPLAY   *P5:6,*V2LON,*ULON,OPCNCDSC,SP10,SP5:
                    *P36:6,"Start":
                    *P42:6," End ":
                    *P48:6,"Pats":
                    *P53:6,"Time Left":
                    *P63:6,"Susp":
                    *P68:6,DESC10
.
          MOVE      SEVEN,CVERT                    * init screen row
          MOVE      ONE,CCOL                      * init screen column
          MOVE      ONE,CPAGENO                    * init screen no.
          MOVE      ONE,FORM2                  * init stock no.
.
          PACK      KEY19,STRTDATE,SP20           * position at start
          CALL      RSOPSES1
GTSES100  CALL      RKOPSES1                      * read next record
          BRANCH    OVRCD,GTSES500          * no more records
          PACK      KEY19,OPSEDATE,OPSETIME,OPSECLIN   * get key
.
          MATCH     OPSEDATE,STRTDATE       * test for correct date
          GOTO      GTSES500 IF NOT EQUAL
.
          COMPARE   ZERO,OPSESTAT           * cancelled session ?
          GOTO      GTSES100 IF EQUAL       * yes. ignore
.
. check if enough room for display
.
GTSES150  COMPARE   "17",FORM2   
          GOTO      GTSES300 IF LESS
.
. we need a new screen
.
GTSES160  CALL      QST1S000                * ask question with Next scrn
          BRANCH    EXIT,GTSES800:          * session selected          
                         GTSES900:          * Cancel selected
                         GTSES200:          * Previous screen selected
                         GTSES250:          * Next screen selected
                         GTSES050           * new (D)ate selected
.
. previous screen was selected
.
GTSES200  LOAD      KEY19,CPAGENO,SESS01,SESS02,SESS03,SESS04,SESS05,SESS06:
                          SESS07,SESS08,SESS09,SESS10
.
          CALL      RDOPSES1                      * pos on 1st key on screen
.
. read back x records until 1st key on previous screen is reached
.
          MOVE      ZERO,COUNTER                  * init counter
GTSES220  CALL      RPOPSES1                      * read back 1 record
          COMPARE   ZERO,OPSESTAT
          GOTO      GTSES220 IF EQUAL             * don't count cancelled sess.
          ADD       ONE,COUNTER                   * increment counter
          COMPARE   "16",COUNTER                  * check if read far enough
          GOTO      GTSES220 IF NOT EQUAL
.
. we have read back far enough
.
          DISPLAY   *P1:7,*EF                    * erase screen
          MOVE      SEVEN,CVERT                    * init screen row
          MOVE      ONE,CCOL                      * init screen column
          MOVE      ONE,FORM2                  * init screen disp counter
          SUB       ONE,CPAGENO                    * update screen no.
          GOTO      GTSES300
.
. Next screen was selected
.
GTSES250  ADD       ONE,CPAGENO                    * increment screen no.
.
. save the current key
.
          STORE     KEY19,CPAGENO,SESS01,SESS02,SESS03,SESS04,SESS05,SESS06:
                          SESS07,SESS08,SESS09,SESS10
.
          DISPLAY   *P1:7,*EF                     * erase screen
.
          MOVE      SEVEN,CVERT                    * init screen row
          MOVE      ONE,CCOL                      * init screen column
          MOVE      ONE,FORM2                  * init screen disp counter
.
. display record
.
GTSES300  PACK      KEY19,OPSEDATE,OPSETIME,OPSECLIN,SP20
.
          PACK      KEY6,OPSECLIN,SP6
          CALL      VDCLN000                      * get clinic or doctor desc
          MOVE      DESC40,DESC30                 * truncate desc to 30 chars
.
          PACK      KEY23,OPSEDATE,OPSETIME,OPSECLIN,ZERO,Z70
          CALL      RSOPDEA1
          CALL      RPOPDEA1          * read last record to get no. of cases
          PACK      KEY20,OPDADATE,OPDATIME,OPDACLIN,ZERO
          MATCH     KEY20,KEY23       * test if same session
          GOTO      GTSES301 IF EQUAL  * if it is keep value else set 0
          MOVE      "  0",OPDACASE      * set no. of cases to zero
GTSES301  
          MOVE      OPSEDURA,FORM4      * calculate time left
          SUB       OPSETIMB,FORM4      * duration - time booked
          SUB       OPSEBRKT,FORM4      * duration - time for breaks
.
          BRANCH    OPSESUSP,GTSES302   * check if session is suspended
          MOVE      "No ",KEY3
          GOTO      GTSES305
.
GTSES302  MOVE      "Yes",KEY3
.
GTSES305  DISPLAY   *P1:CVERT,*V2LON,FORM2,*HOFF,". ":
                    DESC30," ",OPSETIME," ",OPSEENDT:
                    *P48:CVERT,OPDACASE,*P55:CVERT,FORM4,*P64:CVERT,KEY3:
                    *P70:CVERT,OPSETHET
.
.    save each record as it is displayed (for selection purposes)
.
          STORE     KEY19,FORM2,SESNUM01,SESNUM02,SESNUM03,SESNUM04:
                                SESNUM05,SESNUM06,SESNUM07,SESNUM08:
                                SESNUM09,SESNUM10,SESNUM11,SESNUM12:
                                SESNUM13,SESNUM14,SESNUM15,SESNUM16
.
          ADD       ONE,CVERT                     * update row pos
          ADD       ONE,FORM2                  * increment session counter
          GOTO      GTSES100                      * get next record
.
. we have no more records to display
.
GTSES500  
. test if anything was displayed
.
          COMPARE   ONE,CPAGENO             * are we on 1st page?
          GOTO      GTSES501 IF NOT EQUAL
.
          COMPARE   ZERO,FORM2              * test none found
          GOTO      GTSES501 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"No Sessions on selected date.  ";
          CALL      HITENTER
          GOTO      GTSES900 
.
GTSES501  CALL      QST2S000
          BRANCH    EXIT,GTSES800:          * session selected
                         GTSES900:          * Cancel selected
                         GTSES200:          * Previous screen selected
                         GTSES250:          * Next screen selected
                         GTSES050           * new (D)ate selected
.
. a valid session was entered (so get record selected)
.
GTSES800  LOAD      KEY19,FORM2OPT,SESNUM01,SESNUM02,SESNUM03,SESNUM04:
                                   SESNUM05,SESNUM06,SESNUM07,SESNUM08:
                                   SESNUM09,SESNUM10,SESNUM11,SESNUM12:
                                   SESNUM13,SESNUM14,SESNUM15,SESNUM16
.
          CALL      RDOPSES1                * get session details
          MOVE      ZERO,EXIT
          GOTO      GTSES999
.
GTSES900  MOVE      SP20,KEY19                    * no sessions found
.
. make sure we still have the correct session details
.
          PACK      KEY19,STRTDATE,SP20           * position at start
          CALL      RSOPSES1
GTSES910  CALL      RKOPSES1                      * read next record
          COMPARE   ZERO,OPSESTAT                 * cancelled session ?
          GOTO      GTSES910 IF EQUAL             * yes. ignore
.
          MOVE      ONE,EXIT
.
GTSES999  RETURN
+
.**************************************************************************
.*                                STIK0000            Called by: GTSES000 *
.*                 ask line 24 question with (N)ext                       *
.**************************************************************************
QST1S000  
. check which screen we are on. If 1st dont' display (P)revious
.
          BRANCH    CPAGENO,QST1S050
.
. screen is not 1st screen so display "(P)revious"
.
          DISPLAY   *P1:24,*EL,"Select the session, (",*V2LON:
                    "N",*HOFF,")ext, (",*V2LON,"P",*HOFF,")revious, (",*V2LON:
                    "C",*HOFF,")ancel, New (",*V2LON,"D",*HOFF,")ate ? "
.
          MOVE      "64",CCOL                 * keyin position
          GOTO      QST1S100                      * get keyin
.
. screen is 1st screen so don't display "(P)revious"
. 
QST1S050  DISPLAY   *P1:24,*EL,"Select the session, (",*V2LON:
                    "N",*HOFF,")ext, (",*V2LON,"C",*HOFF,")ancel, New (":
                    *V2LON,"D",*HOFF,")ate ? "
.
          MOVE      "52",CCOL                   * keyin position
.
. now get keyin
.
QST1S100  KEYIN     *PCCOL:24,*DV,UNDLN2:
                    *PCCOL:24,*V2LON,*JR,DIM2OPT:
                    *PCCOL:24,*DV,DIM2OPT
.
          ENDSET    DIM2OPT
          APPEND    SP2,DIM2OPT
          RESET     DIM2OPT
.
          REP       UPPLOW,DIM2OPT                  * Change to upper case
.
          TYPE      DIM2OPT                         * was a numeric entered
          GOTO      QST1S120 IF EQUAL
.
. a character was entered
.
          REP       "C1P2N3D4",DIM2OPT      * valid letters
.
          TYPE      DIM2OPT
          GOTO      QST1S150 IF NOT EQUAL   * invalid letter entered
.
          MOVE      DIM2OPT,FORM2OPT
          BRANCH    FORM2OPT,QST1S200,QST1S700,QST1S400,QST1S660
.
          BEEP 
          GOTO      QST1S100
.
. a numeric was entered so validate
.
QST1S120  MOVE      DIM2OPT,FORM2OPT                * move to form field
          COMPARE   ONE,FORM2OPT                  * < 1 ?
          GOTO      QST1S150 IF LESS
          COMPARE   "17",FORM2OPT              * in range ?
          GOTO      QST1S650 IF LESS 
.
. error in input
.
QST1S150  BEEP  
          GOTO      QST1S100
.
QST1S200  MOVE      TWO,EXIT                * Cancel was selected
          GOTO      QST1S999
.
QST1S400  MOVE      FOUR,EXIT               * Next was selected
          GOTO      QST1S999
.
QST1S650  MOVE      ONE,EXIT                * a number was entered
          GOTO      QST1S999
.
QST1S660  MOVE      FIVE,EXIT               * new (D)ate chosen
          GOTO      QST1S999
.
. Previous was selected so check if it was asked
.
QST1S700  BRANCH    CPAGENO,QST1S720
          MOVE      THREE,EXIT              * Previous was selected
          GOTO      QST1S999
.
. Previous was selected but wasn't asked so error
.
QST1S720  BEEP
          GOTO      QST1S100
.
QST1S999  RETURN
+
.**************************************************************************
.*                                QST2S000            Called by: GTSES000 *
.*                 ask line 24 question without (N)ext                    *
.**************************************************************************
QST2S000  
. check which screen we are on. If 1st dont' display (P)revious
.
          BRANCH    CPAGENO,QST2S050
.
. screen is not 1st screen so display "(P)revious"
.
          DISPLAY   *P1:24,*EL,"Select the session, (",*V2LON:
                    "P",*HOFF,")revious, (",*V2LON:
                    "C",*HOFF,")ancel, New (",*V2LON,"D",*HOFF,")ate ? "
.
          MOVE      "56",CCOL                   * keyin position
          GOTO      QST2S100                      * get keyin
.
. screen is 1st screen so don't display "(P)revious"
. 
QST2S050  DISPLAY   *P1:24,*EL,"Select the session, (",*V2LON:
                    "C",*HOFF,")ancel, New (",*V2LON,"D",*HOFF,")ate ? "
.
          MOVE      "44",CCOL                   * keyin position
.
. now get keyin
.
QST2S100  KEYIN     *PCCOL:24,*DV,UNDLN2:
                    *PCCOL:24,*V2LON,*JR,DIM2OPT:
                    *PCCOL:24,*DV,DIM2OPT
.
          ENDSET    DIM2OPT
          APPEND    SP2,DIM2OPT
          RESET     DIM2OPT
.
          REP       UPPLOW,DIM2OPT              
.
          TYPE      DIM2OPT                         * was a numeric entered
          GOTO      QST2S120 IF EQUAL
.
. a character was entered
.
          REP       "C1P2D3",DIM2OPT
.
          TYPE      DIM2OPT                 * valid letter entered?
          GOTO      QST2S150 IF NOT EQUAL   * No.
.
          MOVE      DIM2OPT,FORM2OPT
          BRANCH    FORM2OPT,QST2S200,QST2S700,QST2S660
. 
          BEEP
          GOTO      QST2S100
.
. a numeric was entered so validate
.
QST2S120  MOVE      DIM2OPT,FORM2OPT        * move to form field
          COMPARE   ONE,FORM2OPT            * < 1 ?
          GOTO      QST2S150 IF LESS
          COMPARE   FORM2,FORM2OPT          * in range ?
          GOTO      QST2S650 IF LESS 
.
. error in input
.
QST2S150  BEEP  
          GOTO      QST2S100
.
QST2S200  MOVE      TWO,EXIT                * Cancel was selected
          GOTO      QST2S999
.
QST2S650  MOVE      ONE,EXIT                * a number was entered
          GOTO      QST2S999
.
QST2S660  MOVE      FIVE,EXIT               * new (D)ate chosen
          GOTO      QST2S999
.
. Previous was selected so check if it was asked
.
QST2S700  BRANCH    CPAGENO,QST2S720
          MOVE      THREE,EXIT              * Previous was selected
          GOTO      QST2S999
.
. Previous was selected but wasn't asked so error
.
QST2S720  BEEP
          GOTO      QST2S100
.
QST2S999  RETURN
...............................................................................
