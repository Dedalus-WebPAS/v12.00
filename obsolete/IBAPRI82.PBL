.***************************************************************************
.* System    :   Private Practice                                          *
.* Program   :   IBAPRI82                                                  *
.* Desc      :   Copy Commission Fees                                      *
.***************************************************************************
.* Date      :   18/02/2005                                                *
.* Author    :   Jeni Tan                                                  *
.* Function  :   Function to copy Commision fee from selected Medical Prac *
.*               and Associated doctor to other Medical Prac/Assc.Doct     *
.* Mods.     :                                                             *
.* V10.13.01  03/08/2018  Richa Phogat    TSK 0848506                      *
.*            Extended keylength for ADOCT,FRDOCT,TODOCT from 6 to 10      *
.*            Changed SKEY18 to SKEY22                                     * 
.*            Updated keylength for RSPRDO1,RDPRDO1,WRPRDO1 from KEY18 to  *
.*            KEY22                                                        *
.***************************************************************************
.
          INC       STDGENDF
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PRIPRAFD/INC                 * Medical Practice file
          INC       PRIDOCFD/INC                 * Med Prac Doctor file
          INC       PMSHCPFD/INC                 * Doctor file
.
. LOCAL VARIABLE DEFINITION
. -------------------------
SAVFEEP    FORM     3.2
ADOCT      DIM      10
FRPRAC     DIM      6
FRDOCT     DIM      10
MPRAC      DIM      6
.
SKEY22     DIM      22
TOPRAC     DIM      6
TODOCT     DIM      10
.
PRGID     INIT      "IBAPRI82"
PRGNAM    INIT      "Copy Commission Fee"
VERSION   INIT      "V10.13.01"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialization and open files
.
ML1000    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML2000         * Copy Commission fee
          GOTO      ML1000
.
ML2000    CALL      INPT0000               * get from/to medical prac/doctor
          BRANCH    EXIT,ML1000            * exit
.
          CALL      CONTQST                * see if O.K. to continue
          BRANCH    CEXIT,ML3000,ML2000,ML1000
.
.------ it is O.K. to continue ------
.
ML3000    CALL      COPY0000               * do copy Commission fee
          GOTO      ML1000
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                Open files and initialise variables                       *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"controlf"
.
          DISPLAY   *P64:24,"pripracf";          * medical practice file
          OPEN      PRIPRAC1,"pripracf"
.
          DISPLAY   *P64:24,"pridoctf";          * medical practice doctor file
          OPEN      PRIDOCT1,"pridoctf"
.
          DISPLAY   *P64:24,"pmshcpaf";          * doctor file
          OPEN      PMSHCPA1,"pmshcpaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    =       (0)  Valid option selected                  *
.*                              (1)  Exit option selected                   *
.****************************************************************************
.
. ---- display option screen ----
.
OPTN0000  HLINE     *G37,2,60,80
          DISPLAY   *P1:3,*EF,*P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = Copy Commission Fee";
.
OPTN0500  KEYIN     *P1:11,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:11,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * 0 entered ?
          GOTO      OPTN9000 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN1000             * Copy
.
          GOTO      OPTN0500                     * invalid option selected
.
OPTN1000  DISPLAY   *P60:2,*V2LON,"Copy Commission Fee"
          GOTO      OPTN8000
.
OPTN8000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9000  MOVE      ONE,EXIT                     * set EXIT = 1
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               INPT0000              Called by: ML0000  *
.*                       input Medical Practice and doctor code           *
.**************************************************************************
INPT0000  DISPLAY   *P1:3,*EF:
                    *P5:4,"From Medical Practice",*P27:4,COLON:
                    *P5:5,"     Associated Doctor",*P27:5,COLON:
                    *P5:7,"To   Medical Practice",*P27:7,COLON:
                    *P5:8,"     Associated Doctor",*P27:8,COLON;
.
          MOVE      FOUR,CVERT 
          CALL      PRAC0000                     * get from medical practice
          BRANCH    EXIT,INPT9000                * exitchar entered
          MOVE      MPRAC,FRPRAC
.
          MOVE      FIVE,CVERT 
          CALL      DOCT0000                     * get from associated doctor
          BRANCH    EXIT,INPT9000                * exitchar entered
          MOVE      ADOCT,FRDOCT
.
          MOVE      SEVEN,CVERT 
          CALL      PRAC0000                     * get to medical practice
          BRANCH    EXIT,INPT9000                * exitchar entered
          MOVE      MPRAC,TOPRAC
.
          MOVE      EIGHT,CVERT 
          CALL      DOCT0000                     * get to associated doctor
          BRANCH    EXIT,INPT9000                * exitchar entered
          MOVE      ADOCT,TODOCT
          GOTO      INPT9999
.
INPT9000  MOVE      ONE,EXIT
INPT9999  RETURN
+
.***************************************************************************
.         Keyin medical practice
.***************************************************************************
PRAC0000  MOVE      SP70,MPRAC
          MOVE      ZERO,EXIT
          KEYIN     *P29:CVERT,*V2LON,MPRAC
          ENDSET    MPRAC
          APPEND    SP70,MPRAC
          RESET     MPRAC
          MATCH     SP70,MPRAC
          GOTO      PRAC9999 IF NOT EQUAL
          MOVE      ONE,EXIT
PRAC9999  RETURN
.
.***************************************************************************
.         Keyin associated doctor
.***************************************************************************
DOCT0000  MOVE      SP70,ADOCT
          MOVE      ZERO,EXIT
          KEYIN     *P29:CVERT,*V2LON,ADOCT
          ENDSET    ADOCT
          APPEND    SP70,ADOCT
          RESET     ADOCT
          MATCH     SP70,ADOCT
          GOTO      PRAC9999 IF NOT EQUAL
          MOVE      ONE,EXIT
DOCT9999  RETURN
+
.****************************************************************************
.*                                 COPY0000                                 *
.*             Perform the Copy Commission fee                              *
.****************************************************************************
COPY0000  DISPLAY   *P1:24,*EL,"Scanning : ":
                    *P30:24,"Updating : ";
          PACK      KEY22,FRPRAC,FRDOCT,SP70
          CALL      RSPRDO1                 * position on the item file
.
.------ read through the item file ------
.
COPY1000  CALL      RKPRDO1
          BRANCH    OVRCD,COPY9999
.
          MATCH     FRPRAC,PRDOPRAC              * same medical practice
          GOTO      COPY9999 IF NOT EQUAL
.
          MATCH     FRDOCT,PRDODOCT              * same doctor?
          GOTO      COPY9999 IF NOT EQUAL
.
          MOVE      PRDOFEEP,SAVFEEP             * save Commission fee
          PACK      SKEY22,PRDOPRAC,PRDODOCT,PRDOCLAM,PRDOPIND,SP70
.
          DISPLAY   *P12:24,*V2LON,PRDOCLAM," - ",PRDOPIND;
.
          PACK      KEY22,TOPRAC,TODOCT,PRDOCLAM,PRDOPIND,SP70
          CALL      RDPRDO1
          IF        OVRCD=0
            MOVE      SAVFEEP,PRDOFEEP
            CALL      UPPRDO1
          ELSE
            MOVE      SKEY22,KEY22
            CALL      RDPRDO1
            BRANCH    OVRCD,COPY1000
.
            MOVE      TOPRAC,PRDOPRAC
            MOVE      TODOCT,PRDODOCT
            MOVE      SAVFEEP,PRDOFEEP
            PACK      KEY22,PRDOPRAC,PRDODOCT,PRDOCLAM,PRDOPIND,SP70
            CALL      WRPRDO1
          ENDIF
.
          MOVE      SKEY22,KEY22
          CALL      RDPRDO1                   * reposition
          GOTO      COPY1000
.
COPY9999  RETURN
+
. =========================================================================
.        I/O INCLUDES
.==========================================================================
.
          INC       STDGENCD 
.
          INC       PRIPRAIO/INC                 * Medical practice file
          INC       PRIDOCIO/INC                 * MP doctor file
          INC       PMSHCPIO/INC                 * doctor file
