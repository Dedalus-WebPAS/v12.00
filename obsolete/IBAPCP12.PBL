.*******************************************************************************
.* System        :  PATIENT CARE PLANS                                         *
.* Program       :  IBAPCP12                                                   *
.* Desc          :  Input Evalutations                                         *
.*******************************************************************************
.* Date          :  22/01/90                                                   *
.* Author        :  Paul Duncan                                                *
.* Modifications :                                                             *
.*        V10.12.01 22/03/2018  Richa Phogat      TSK 0261630                 *
.*                  Removed multiple instances of PORT                        *
.*******************************************************************************
.*        V10.02.01 25/06/2011 Steve Armstrong    CAR 240722                   *
.*                  Mods to cater for changes to PATLOCFD.                     *
.*                       - LSNAME & LGNAME extended to 40 chars.               *
.*                       - PTLCGNM2 added.                                     *
.*                  Added RDPMPX21 prior to calling PMIHEAD                    *
.*******************************************************************************
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                       *
.*                  Added pmsvx1af                                             *
.*******************************************************************************
.*        V9.02.00  30/12/2002 Tracey Nguyen srf 23497                         *
.*                  r5.10 Care Plans Port to V9                                *
.*******************************************************************************
.*        V5.08.01  23/08/2000  Caleb Sharman                                  *
.*                  Changed Health Fund table variables to 8 chars             *
.*       V5.07.B01  04/11/1998  Jim Stathopoulos                               *
.*                  507 Changes                                                *
.*******************************************************************************
.*        V5.01.02  09/02/90  Steve O'Gorman                                   *
.*                  Added Patient Alerts                                       *
.*        V5.01.03  07/05/90  Glen Hobby                                       *
.*                  Fixed up various bugs - mainly screen displays             *
.*        V5.01.121 20/11/92  ROD                                              *
.*                  Fixed I*N  (hadn't created temp file yet!)                 *
.*        V5.01.122 04/12/1992    Justin Coates                                *
.*                  when compelete care plan, also de-activate it              *
.*        V5.01.123 22/04/93 DIG                                               *
.*                  Re-Compiled for changes to PATALERT.                       *
.*        V5.01.124 15/12/1993  Glenn Berry                                    *
.*                  Fixed bug when writing date field                          *
.*        V5.01.125 13.01.1994  Sandra Barcham                                 *
.*                  Global recompile changes                                   *
.*        V5.01.126 30/03/1994  Rob Leonard  SRF 128076                        *
.*                  Convert to use new word processor                          *
.*        V5.01.127 24/05/1995  Matt Surridge                                  *
.*                  Fixed bugs for global recompile.                           *
.*******************************************************************************
.
.$$$$$$
.  Input Evalutations        
.  ------------------
.
.  - Initialization
.
.       -  Display screen header
.       -  Open files
.             "controlf" (CONTROLF)
.             "pcpdscaf" (PCPDSCA1)
.             "pcpdiaaf" (PCPDIAA1)
.             "pcppdsaf" (PCPPDSA1)
.             "pcpnuraf" (PCPNURA1)
.             "pcpdbsaf" (PCPDBSA1)
.             "pcpdisaf" (PCPDISA1)
.             "patteraf" (PATTERA1)
.             "patnursm" (PATNURS1)
.             "patlocaf" (PATLOCA1)
.             "pattranf" (PATTRAN2)
.             "patmi1af" (PATMI1A1)
.
.  - Process
.
.       - Step 1 = Input nursing code(ICOD0000)
.                  . input code.  Validate against the PCPNURFD file and
.                    display the description if valid
.                  . display nursing station if record exists in the PATTERFD
.                    file, in other words, nursing station restriction applies
.
.       - Step 2 = Input the surname of the patient(IPAT0000)
.                  . if no input, program will return to Step 1
.                  . Otherwise scan through the PATLOCFD file in surname order
.                    to locate every patient whose surname matches that of the
.                    input.  If nursing station restriction applies, patients
.                    who do not reside in a ward belonging to this nursing
.                    station will be ignored
.                  . display patients found(DPAT0000).  If no patient found,
.                    program will display no-patients message and return to
.                    start of Step 2
.                  . user will then select patient
.       
.       - Step 3 = User selects patient required(IPAT0000).The patients care
.                    plans are then displayed(DPLN0000) and the user selects
.                    which care plan they wish to add an evaluation to(GPAT0000)
.                    If nothing is entered, it will return to start of step 3.
.                  
.       - Step 4 = Evaluations for the selected care plan displayed(DEVA0000).
.                  . OK to continue?. Load word processor and input new
.                    evaluation if yes. Re-load word processor if no. Go back
.                    to step 3 if cancel.
.
.       - Step 5 = Read tempfile produced by wp and write appropriate records
.                    to the pcppdsaf and pcpevaaf files(WPRC0000)
.
.       - Step 6 = Go back to step 2
.
.       - end
.$$$$$$
          INC       STD001FD
.
          INC       PATDHEAD/INC
.
          INC       PATCALAG/INC
          INC       PCPCONFD/INC
.
          INC       PCPPDSFD/INC
          INC       PCPDSCFD/INC
          INC       PCPEVAFD/INC
          INC       PCPDIAFD/INC
          INC       PCPNURFD/INC
          INC       PCPDISFD/INC
          INC       PATMA1FD/INC
.
          INC       PATALRFD/INC
          INC       PATTERFD/INC
          INC       PATNURFD/INC
          INC       PATLOCFD/INC
          INC       PATTRNFD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDO1FD/INC
          INC       PATMI1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       PATNIDFD/INC
          INC       PATNOBFD/INC
          INC       PATDSCFD/INC
          INC       PATWR1FD/INC
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
+
.*******************************************************************************
.*                                CONSTANTS                                    *
.*******************************************************************************
.
CODET     INIT      "T "
.
.
SP33      INIT      "                                 "
TMPHDRA   INIT      "PCP12A"
Z6        INIT      "ZZZZZZ"
Z25       INIT      "ZZZZZZZZZZZZZZZZZZZZZZZZZ"
+
.*******************************************************************************
.*                             GLOBAL VARIABLES                                *
.*******************************************************************************
. ------  Keys used in FD/IO routines  ------
.                                           temp file 5
.                                           patdschf
.                                           pcpintaf; all Audit files
.
. ------  varibles for temporary index files  ------
.
CMDLINE   DIM       50                * command line
FNAMES1   DIM       8                 * file name for word processing file 1
.                                       and work file
. ------  temp. storage for Admission No. of patient displayed  ------
.
ADMNO01   FORM      8
ADMNO02   FORM      8
ADMNO03   FORM      8
ADMNO04   FORM      8
ADMNO05   FORM      8
ADMNO06   FORM      8
ADMNO07   FORM      8
ADMNO08   FORM      8
ADMNO09   FORM      8
ADMNO10   FORM      8
ADMNO11   FORM      8
ADMNO12   FORM      8
ADMNO13   FORM      8
ADMNO14   FORM      8
.
SELECT01  DIM       33
SELECT02  DIM       33
SELECT03  DIM       33
SELECT04  DIM       33
SELECT05  DIM       33
SELECT06  DIM       33
SELECT07  DIM       33
SELECT08  DIM       33
SELECT09  DIM       33
SELECT10  DIM       33
SELECT11  DIM       33
SELECT12  DIM       33
SELECT13  DIM       33
.
. ------  other variables  ------
.
BJDAY     FORM      3
CJDAY     FORM      3
CLINE     FORM      2           
.
DIAGDESC  DIM       60
.
FLAG1     FORM      1
FORM8     FORM      8           * temp. variable for loading admission no.
.
GOTNAME   FORM      1
.
LINE      FORM      2           * display line number
LINE60    DIM       60          * variable for word processor temp file
LINE80    DIM       80          * variable for word processor temp file
LINECNT1  FORM      2
.
LDIM88    DIM       88          * last display key for PATLOCFD file
SDIM88    DIM       88          * start display key for PATLOCFD file
LDIM33    DIM       33          * last display key
LDIM13    DIM       13          * last display key for PCPDSCFD file
LDIM14    DIM       14          * last display key for PCPPDSFD file
SDIM14    DIM       14          * start display key for PCPPDSFD file
SDIM13    DIM       13
SDIM33    DIM       33
.
NUMOPTN   DIM       33          * variable to load selected option
NURSCODE  DIM       6           * Nursing code input
PATNAME   DIM       46          * patient name output
PRDATE    DIM       10
PRSTAT    DIM       3
.
REPLYD    DIM       2           * user response - DIM field
REPLYF    FORM      2           * user response - FORM field
.
SCREEN    FORM      2           * screen counter
SCRNFLAG  FORM      1           * screen flag (0=EOF, 1=full screen)
STATION   DIM       3           * nursing station for display on screen
STVAR     FORM      2           * status variable for word processor
SURNAME   DIM       40          * patient surname input
.
UNIQUE    DIM       10
.
. ------  File Definition for temporary sequential files  ------
.
SEQTEMP   FILE      ASCII,FIXED=256
.
PRGID     INIT      "IBAPCP12"
PRGNAM    INIT      "Input Evaluations"
VERSION   INIT      "V10.12.01"
+
.*******************************************************************************
.*                                MAINLINE                                     *
.*                            controlling logic                                *
.*******************************************************************************
ML0000    CALL      INIT0000              * display screen headings & open files
.
ML1000    CALL      ICOD0000              * input & validate nursing code
          BRANCH    EXIT,ML9999
.
ML2000    CALL      IPAT0000              * input patient surname
          BRANCH    EXIT,ML1000
.
ML2500    CALL      GPAT0000              * get patient and display details
          BRANCH    GOTNAME,ML2000
          BRANCH    EXIT,ML2000           * 1=null input, 2=file error/
.                                                                 null input
ML3000    CALL      DEVA0000              * display evaluations
          BRANCH    EXIT,ML4000,ML5000,ML2000
.
ML4000    CALL      WPRC0000              * word processing
          GOTO      ML2000
.
ML5000    MOVE      ONE,GOTNAME           * already got name
          GOTO      ML2500
.
ML9999    CALL      KLLS0000              * kill tempfile
          CHAIN     PGM
+
.*******************************************************************************
.*                                INIT0000                                     *
.*        display screen headings and open files         called by : MAINLINE  *
.*******************************************************************************
INIT0000  CALL      DISPHEAD              * display screen headings
.
. ------  open files  ------
.
          DISPLAY   *P50:24,*EL,"Opening":
                    *P58:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P58:24,"pcpdscaf";
          OPEN      PCPDSCA1,"pcpdscaf"
.
          DISPLAY   *P58:24,"pcpdiaaf";
          OPEN      PCPDIAA1,"pcpdiaaf"
.
          DISPLAY   *P58:24,"pcppdsaf";
          OPEN      PCPPDSA1,"pcppdsaf"
.
          DISPLAY   *P58:24,"pcpnuraf";
          OPEN      PCPNURA1,"pcpnuraf"
.
          DISPLAY   *P58:24,"pcpdisaf";
          OPEN      PCPDISA1,"pcpdisaf"
.
          DISPLAY   *P58:24,"patteraf";
          OPEN      PATTERA1,"patteraf"
.
          DISPLAY   *P58:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P58:24,"patnursm";
          OPEN      PATNURS1,"patnursm"
.
          DISPLAY   *P58:24,"patlocaf";
          OPEN      PATLOCA1,"patlocaf"
.
          DISPLAY   *P58:24,"pattrnaf";
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P58:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P58:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P58:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P58:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P58:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P58:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P58:24,"patnobef";
          OPEN      PATNOBE1,"patnobef"
.
          DISPLAY   *P58:24,"pcpevaaf"
          OPEN      PCPEVAA1,"pcpevaaf"
.
          DISPLAY   *P58:24,"patwr1af";
          OPEN      PATWR1A4,"patwr1af"
. ------  access Sector 41 (PCPCONFD) of CONTROLF file  ------
.
          READ      CONTROLF,FORTY1;*77,PCCNSSEC:
                                    *79,PCCNUNIQ
          READ      CONTROLF,FIFTY9;*132,CALRDESC
.
          MOVE      "11111101",ALRTMASK
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMES1
          RESET     FNAMES1
.
INIT9999  RETURN
+
.*******************************************************************************
.*                                ICOD0000                                     *
.*        input and validate nursing code                called by : MAINLINE  *
.*******************************************************************************
ICOD0000  DISPLAY   *P67:2," Station : ",*P1:3,*EF:
                    *P5:4,"Input your nursing code : "
.
          MOVE      PORT,KEY2
          CALL      RDPTTER1              * read PATTERFD file for
.                                           nursing station
          BRANCH    OVRCD,ICOD1000        * if no record found, or current
          COMPARE   PCCNSSEC,SECLEV         user security level is >= security
          GOTO      ICOD1000 IF NOT LESS    level to ignore nursing station
.                                           restriction, do not display station
.
. ------  nursing station restriction applies  ------
.
          MOVE      PTTENSTN,STATION
          DISPLAY   *P77:2,*V2LON,*+,STATION,SP1
          GOTO      ICOD2000
.
. ------  nursing station restriction does not apply  ------
.
ICOD1000  MOVE      SP3,STATION             * set station flag = 1
.
ICOD2000  KEYIN     *P31:4,*DV,UNDLN6,*P31:4,*V2LON,NURSCODE
          ENDSET    NURSCODE
          APPEND    SP6,NURSCODE
          RESET     NURSCODE
.
          MATCH     SP6,NURSCODE
          GOTO      ICOD9000 IF EQUAL
.
          DISPLAY   *P31:4,*V2LON,NURSCODE
          MOVE      NURSCODE,KEY6
          CALL      RDPCNUR1
          BRANCH    OVRCD,ICOD3000
          DISPLAY   *P52:4,PCNUNAME,*P78:4,PCNUCLSS
          MOVE      FALSE,EXIT
          GOTO      ICOD9999
.
ICOD3000  DISPLAY   *P1:24,*EL,*B,"Invalid nursing code - ";
          CALL      HITENTER
          GOTO      ICOD2000
.
ICOD9000  MOVE      TRUE,EXIT
.
ICOD9999  RETURN
+
.*******************************************************************************
.*                                IPAT0000                                     *
.*        input patient surname                          called by : MAINLINE  *
.*******************************************************************************
IPAT0000  BRANCH    GOTNAME,IPAT1200
          BRANCH    EXIT,IPAT0500,IPAT0500
.
IPAT0500  CALL      RDSP0000
          DISPLAY   *P5:6,*EF,"Patient Surname         : "
.
.         We don't ENDSET APPEND to the SURNAME because we want to do partial
.         matches.
.
IPAT1000  KEYIN     *P31:6,*V2LON,SURNAME:
                    *P31:6,*DV,SURNAME
          RESET     SURNAME
.
          GOTO      IPAT9000 IF EOS          * Check for return
.
          DISPLAY   *P1:8,*ULON,"No.",*HOFF," ",*ULON,"Patient's Name",SP30:
                    "            ",*P55:8,*HOFF," ",*ULON,"U/R No.",*HOFF," ":
                    *ULON,"Adm Date",*HOFF," ",*ULON,"Sex",*HOFF," ",*ULON,"Age"
.
          MOVE      ONE,SCREEN                  * initialize no. of screens to 1
          PACK      SDIM88,SURNAME,SP70,SP70
          MOVE      SDIM88,KEY88
.
. ------  check if patient(s) with this surname exist  ------
.
          CALL      RDSLOCA1
          CALL      RDKLOCA1
          BRANCH    OVRCD,IPAT1100
          MATCH     SURNAME,LSNAME
          GOTO      IPAT1100 IF NOT EQUAL
          GOTO      IPAT1200
.
IPAT1100  DISPLAY   *P1:24,*EL,*B,"No patient found with this surname.  ";
          CALL      HITENTER
          GOTO      IPAT1000
. ----------------------------------------------------------
.
IPAT1200  MOVE      ZERO,GOTNAME
          CALL      DPAT0000                    * Display matching patients
          BRANCH    SCRNFLAG,IPAT4000           * check if full screen(1) or
.                                                 no more records(0)
. ------  only one screen  ------
.
          COMPARE   SCREEN,ONE
          GOTO      IPAT3000 IF LESS
IPAT2000  DISPLAY   *P1:24,*EL,"Input the number of the patient required :  "
          MOVE      "44",CCOL
.
IPAT2100  CALL      KEYI0000
          MOVE      ZERO,GOTNAME 
          BRANCH    EXIT,IPAT2200,IPAT2000,IPAT0000       * 1=invalid input,
          GOTO      IPAT8000                                2=invalid no.,
.                                                           3=null input
IPAT2200  BEEP
          GOTO      IPAT2100
.
. ------  more than one screen - on last screen  ------
.
IPAT3000  DISPLAY   *P1:24,*EL,"Input the number of the patient required, (":
                           *V2LON,"P",*HOFF,")revious screen : "
          MOVE      "63",CCOL
.
IPAT3100  CALL      KEYI0000
          BRANCH    EXIT,IPAT3200,IPAT3000,IPAT0000       * 1=alpha input,
          GOTO      IPAT8000                                2=invalid no.,
.                                                           3=null input
IPAT3200  BRANCH    REPLYF,IPAT3300,IPAT6000     * 2=previous screen
.
IPAT3300  BEEP
          GOTO      IPAT3100
.
. ------  more than one screen ------
.
.      *  on first screen
.
IPAT4000  COMPARE   SCREEN,ONE
          GOTO      IPAT4300 IF LESS
.
IPAT4050  DISPLAY   *P1:24,*EL,"Input the number of the patient required, (":
                           *V2LON,"N",*HOFF,")ext screen : "
          MOVE      "59",CCOL
.
IPAT4100  CALL      KEYI0000
          BRANCH    EXIT,IPAT4200,IPAT4050,IPAT0000       * 1=alpha input,
          GOTO      IPAT8000                                2=invalid no.,
.                                                           3=null input
IPAT4200  BRANCH    REPLYF,IPAT5000              * 1=next screen
          BEEP
          GOTO      IPAT4100
.
.      *  in-between screen(s)
.
IPAT4300  DISPLAY   *P1:24,*EL,"Input the number of the patient required, (":
                           *V2LON,"N",*HOFF,")ext, or (",*V2LON,"P",*HOFF:
                           ")revious screen : "
          MOVE      "73",CCOL
.
IPAT4400  CALL      KEYI0000
          BRANCH    EXIT,IPAT4500,IPAT4300,IPAT0000       * 1=alpha input,
          GOTO      IPAT8000                                2=invalid no.,
.                                                           3=null input
IPAT4500  BRANCH    REPLYF,IPAT5000,IPAT6000  * 1=next screen, 2=previous screen
          BEEP
          GOTO      IPAT4400
.
. ------  display next screen  ------
.
IPAT5000  MOVE      LDIM88,KEY88
          MOVE      LDIM88,SDIM88
          ADD       ONE,SCREEN
          GOTO      IPAT1200
.
. ------  display previous screen  ------
.
IPAT6000  MOVE      SDIM88,KEY88
          GOTO      IPAT1200
.
. ------  valid numeic value entered  ------
.
IPAT8000  MOVE      FALSE,EXIT              * set EXIT = 0
          GOTO      IPAT9999
.
. ------  no patient name input  ------
.
IPAT9000  MOVE      TRUE,EXIT              * set EXIT = 1
.
IPAT9999  RETURN
+
.*******************************************************************************
.*                                DPAT0000                                     *
.*        display patients with requested surname        called by : MAINLINE  *
.*******************************************************************************
DPAT0000  CALL      CL1V0000                * clear variables
.
          DISPLAY   *P1:9,*EF
          MOVE      NINE,CVERT
          CALL      RDSLOCA1
.
DPAT1000  CALL      RDKLOCA1
          BRANCH    OVRCD,DPAT9000
.
          MATCH     SURNAME,LSNAME
          GOTO      DPAT9000 IF NOT EQUAL
.
          MATCH     SP3,STATION             * no station?
          GOTO      DPAT3000 IF EQUAL
.
          PACK      KEY30,LADMNO,SP30
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,DPAT1000
.
          COMPARE   LADMNO,TADMN
          GOTO      DPAT1000 IF NOT EQUAL
          PACK      KEY6,PTTENSTN,TWARD
          CALL      RDPCNUR1
          BRANCH    OVRCD,DPAT1000
.
DPAT3000  COMPARE   TWENTY3,CVERT
          GOTO      DPAT4000 IF LESS
          MOVE      ONE,SCRNFLAG
          PACK      LDIM88,LSNAME,LGNAME,LADMNO
          GOTO      DPAT9999
.
. ------  display patient details  ------
.
.         set up patient name using the surname & given names varibles of the
.         PATLOCFD file
.
DPAT4000  MOVE      LSNAME,PATNAME
          STRIP     PATNAME
          ENDSET    PATNAME
          APPEND    ", ",PATNAME
          APPEND    LGNAME,PATNAME
          APPEND    SP70,PATNAME
          RESET     PATNAME
.
.         get and set up Admission date
.
          MOVE      SP8,ADATE
          REP       " 0",ADATE
          MOVE      LADMNO,KEY8
          CALL      RDMISS1
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
.         get patient's sex and age from the PATMA1FD file
.
DPAT5100  MOVE      AURNO,KEY8
          CALL      RDMAST1
.
.         display set-up details
.
          ADD       ONE,LINE
          DISPLAY   *P1:CVERT,*V2LON,LINE,*HOFF,SP2,PATNAME,SP2,LURNO,SP2:
                               CPCDATE,SP2,PSEX,SP2,PSAGE
          STORE     AADMNO,LINE,ADMNO01,ADMNO02,ADMNO03,ADMNO04,ADMNO05:
                                ADMNO06,ADMNO07,ADMNO08,ADMNO09,ADMNO10:
                                ADMNO11,ADMNO12,ADMNO13,ADMNO14
          ADD       ONE,CVERT
          GOTO      DPAT1000
.
. ------  no more records that matches requested surname or EOF has reached ----
.
DPAT9000  MOVE      ZERO,SCRNFLAG
.
DPAT9999  RETURN
+
.*******************************************************************************
.*                                CL1V0000                                     *
.*        clear variables                                called by : DPAT0000  *
.*******************************************************************************
CL1V0000  MOVE      ZERO,LINE
          MOVE      ONE,FORM2
.
CL1V1000  STORE     ZERO,FORM2,ADMNO01,ADMNO02,ADMNO03,ADMNO04,ADMNO05:
                               ADMNO06,ADMNO07,ADMNO08,ADMNO09,ADMNO10:
                               ADMNO11,ADMNO12,ADMNO13,ADMNO14
          COMPARE   TEN4,FORM2
          GOTO      CL1V9000 IF EQUAL
          ADD       ONE,FORM2
          GOTO      CL1V1000
.
CL1V9000  MOVE      ZERO,FORM2
.
CL1V9999  RETURN
+
.*******************************************************************************
.*                                KEYI0000                                     *
.*        validate keyin response to question on line 24 after displaying      *
.*        a screen full of patients that matches requested surname or EOF      *
.*        has reached                                                          *
.*                                                                             *
.*                         called by : GPAT0000, S1OP0000, S2OP0000            *
.*******************************************************************************
KEYI0000  KEYIN     *PCCOL:24,*DV,UNDLN2,*PCCOL:24,*JR,*V2LON,REPLYD
.
          ENDSET    REPLYD
          APPEND    SP2,REPLYD
          RESET     REPLYD
.
          MATCH     SP2,REPLYD              * null input?
          GOTO      KEYI9000 IF EQUAL
.
          REP       UPPLOW,REPLYD
          DISPLAY   *PCCOL:24,*V2LON,REPLYD
          REP       " 0",REPLYD
          TYPE      REPLYD
          GOTO      KEYI2000 IF EQUAL
.
          REP       "N1P2A3F4",REPLYD
          TYPE      REPLYD
          GOTO      KEYI1000 IF EQUAL
          BEEP
          GOTO      KEYI0000
.
KEYI1000  MOVE      REPLYD,REPLYF
          MOVE      ONE,EXIT
          GOTO      KEYI9999
.
. ------  numeric input  ------
.
KEYI2000  MOVE      REPLYD,REPLYF
          COMPARE   ONE,REPLYF
          GOTO      KEYI2100 IF LESS
          COMPARE   REPLYF,LINE
          GOTO      KEYI2100 IF LESS
          MOVE      ZERO,EXIT
          GOTO      KEYI9999
.
KEYI2100  DISPLAY   *P1:24,*EL,*B,"You must input one of the displayed":
                                  " numbers.  ";
          CALL      HITENTER
          MOVE      TWO,EXIT                   * invalid numeric input
          GOTO      KEYI9999
.
. ------  null input  ------
.
KEYI9000  MOVE      THREE,EXIT
.
KEYI9999  RETURN
          
+
.*******************************************************************************
.*                                GPAT0000                                     *
.*        get selected patient and display care plan     called by : MAINLINE  *
.*******************************************************************************
GPAT0000  BRANCH    GOTNAME,GPAT2000
.
          LOAD      FORM8,REPLYF,ADMNO01,ADMNO02,ADMNO03,ADMNO04,ADMNO05:
                                 ADMNO06,ADMNO07,ADMNO08,ADMNO09,ADMNO10:
                                 ADMNO11,ADMNO12,ADMNO13,ADMNO14
          MOVE      FORM8,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,GPAT1000
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,GPAT1100
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,GPAT1100
.
          GOTO      GPAT2000
.
. ------  error messages  -------------------------------------------------
.
GPAT1000  DISPLAY   *P1:24,*EL,*B,*V2LON,"Patient Admissions file error,":
                                  " please contact I.B.A.  ",*HOFF;
          CALL      HITENTER
          GOTO      GPAT9999
.
GPAT1100  DISPLAY   *P1:24,*EL,*B,*V2LON,"Patient Master file error,":
                                  "please contact I.B.A.  ",*HOFF;
          CALL      HITENTER
          GOTO      GPAT9999
. _________________________________________________________________________
.
GPAT2000  CALL      PATHEAD
          MOVE      ZERO,GOTNAME
          DISPLAY   *P1:7,"Admit. Diagnosis : ",*V2LON,ADIAG1:
                    *P7:9,"Date",SP5,"Diagnosis"
.
. ------  check if a care plan already exists for this patient  ------
.         go to input a new plan if does not exist
.
          PACK      KEY33,AADMNO,SP30
          CALL      RSPCDIA1
          CALL      RKPCDIA1
          BRANCH    OVRCD,GPAT2100
          COMPARE   AADMNO,PCDIADMN
          GOTO      GPAT2100 IF NOT EQUAL
          GOTO      GPAT2200
.
GPAT2100  DISPLAY   *P1:24,*EL,*B,"No care plan for this patient.  ":
                                  "Please input one.  ";
          CALL      HITENTER
          MOVE      ONE,GOTNAME
          GOTO      GPAT7000
.
. -------------------------------------------------------------------
.
GPAT2200  MOVE      ONE,SCREEN                * initialize no. of screens to 1
          PACK      SDIM33,AADMNO,Z25
          MOVE      SDIM33,KEY33
          MOVE      SP20,SDIM13
          MOVE      SDIM13,KEY13
          MOVE      SP20,SDIM14
          MOVE      SDIM14,KEY14
.
GPAT3000  CALL      DPLN0000
          BRANCH    SCRNFLAG,GPAT5000         * check if full screen(1) or
.                                               no more records(0)
. ------  only one screen  ------
.
          COMPARE   SCREEN,ONE
          GOTO      GPAT4000 IF LESS
GPAT3050  DISPLAY   *P1:24,*EL,"Select the care plan you want to use : __"
          MOVE      "40",CCOL
.
GPAT3100  CALL      KEYI0000
          BRANCH    EXIT,GPAT3200,GPAT3050,GPAT8000     * 1=alpha input,
.                                                         2=invalid no. input,
.                                                         3=null input
          GOTO      GPAT9000
.
GPAT3200  BRANCH    REPLYF,GPAT3300,GPAT3300,GPAT7000   * 3=add a new care plan
.
GPAT3300  BEEP
          GOTO      GPAT3100
.
. ------  more than one screen - on last screen  ------
.
GPAT4000  DISPLAY   *P1:24,*EL,"Select the care plan you want to use,":
                               "(",*V2LON,"P",*HOFF:
                               ")revious screen : ___"
          MOVE      "58",CCOL
.
GPAT4100  CALL      KEYI0000
          BRANCH    EXIT,GPAT4200,GPAT4000,GPAT8000    * 1=alpha input,
.                                                        2=invalid no. input,
.                                                        3=null input
          GOTO      GPAT9000
.
GPAT4200  BRANCH    REPLYF,GPAT4300,GPAT6100,GPAT7000   * 2=previous screen,
.                                                         3=add a new care plan
GPAT4300  BEEP
          GOTO      GPAT4100
.
. ------  more than one screen ------
.
.      *  on first screen
.
GPAT5000  COMPARE   SCREEN,ONE
          GOTO      GPAT5400 IF LESS
.
GPAT5050  DISPLAY   *P1:24,*EL,"Select the care plan you want to use,(":
                    *V2LON,"N",*HOFF,")ext screen : "
          MOVE      "54",CCOL
.
GPAT5100  CALL      KEYI0000
          BRANCH    EXIT,GPAT5200,GPAT5050,GPAT8000    * 1=alpha input,
.                                                        2=invalid no. input,
.                                                        3=null input
          GOTO      GPAT9000
.
GPAT5200  BRANCH    REPLYF,GPAT6000,GPAT5300,GPAT7000   * 1=next screen,
.                                                         3=add a new care plan
GPAT5300  BEEP
          GOTO      GPAT5100
.
.      *  in-between screen(s)
.
GPAT5400  DISPLAY   *P1:24,*EL,"Select the care plan you want to use,":
                               "(",*V2LON,"N",*HOFF:
                               ")ext or (",*V2LON,"P",*HOFF,")revious screen : "
          MOVE      "70",CCOL
.
GPAT5500  CALL      KEYI0000
          BRANCH    EXIT,GPAT5600,GPAT5400,GPAT8000    * 1=alpha input,
.                                                        2=invalid no. input
.                                                        3=null input
          GOTO      GPAT9000
.
GPAT5600  BRANCH    REPLYF,GPAT6000,GPAT6100,GPAT7000   * 1=next screen,
.                                                         2=previous screen,
.                                                         3=add a new care plan
          BEEP
          GOTO      GPAT5500
.
. ------  display next screen  ------
.
GPAT6000  MOVE      LDIM33,KEY33
          MOVE      LDIM13,KEY13
          MOVE      LDIM14,KEY14
          ADD       ONE,SCREEN
          GOTO      GPAT3000
.
. ------  display previous screen  ------
.
GPAT6100  MOVE      SDIM33,KEY33
          MOVE      SDIM13,KEY13
          SUB       ONE,SCREEN
          GOTO      GPAT3000
.
. ------  (A)dd a new care plan selected  ----
.
GPAT7000  MOVE      ONE,GOTNAME         * set EXIT = 0
          CALL      RDSU0000
          GOTO      GPAT9999
.
. ------  valid numeric value entered  ------
.
GPAT8000  MOVE      ONE,EXIT
          MOVE      ONE,GOTNAME
          CALL      RDSU0000
          GOTO      GPAT9999
.
GPAT9000  MOVE      ZERO,EXIT
          MOVE      ZERO,GOTNAME
.
GPAT9999  RETURN 
.          
.*******************************************************************************
.*                                DPLN0000                                     *
.*  Read the diagnosis file and display in reverse order.                      *
.*******************************************************************************
DPLN0000  CALL      CL2V0000
.
          DISPLAY   *P1:10,*EF:
                    *P1:24,"Getting Nursing Diagnosis for requested patient":
                    ". Please wait";
.
          MOVE      TEN,CVERT
.
          CALL      RSPCDIA1
DPLN1000  CALL      RPPCDIA1                 * READKP
.
          BRANCH    OVRCD,DPLN9000           * EOF
.
          COMPARE   AADMNO,PCDIADMN          * Have we read too far?
          GOTO      DPLN9000 IF NOT EQUAL
.
          MOVE      ZERO,FLAG1
.
.         Only display active care plans
.
          COMPARE   TWO,PCDISTAT
          GOTO      DPLN1000 IF EQUAL        * Non active
.
. ------  set up Date and Activity variables for display  ------
.
          UNPACK    PCDIDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
.         check if description number in PCPDIAFD file is blank or not.
.         if yes, program will get description from PCPDSCFD file.
.         if not, will get description from PCPPDSFD file.
.
DPLN1200  MATCH     SP4,PCDIDCOD
          GOTO      DPLN2000 IF NOT EQUAL
.
. ------  get Diagnosis description from the PCPDSCFD file  ------
.
          MOVE      SIX,FORM2
          MOVE      SIX,PCDSTYPE             * Nursing diagnosis
          MOVE      PCDIDIAG,PCDSCODE        * Description code
          PACK      KEY13,PCDSTYPE,PCDSCODE,SP20
          CALL      RSPCDSC1
.
DPLN1250  CALL      RKPCDSC1
          BRANCH    OVRCD,DPLN1000
.
          COMPARE   SIX,PCDSTYPE
          GOTO      DPLN1000 IF NOT EQUAL
.
          MATCH     PCDIDIAG,PCDSCODE
          GOTO      DPLN1000 IF NOT EQUAL
. 
          BRANCH    FLAG1,DPLN1260
.
          ADD       ONE,LINE
          DISPLAY   *P1:CVERT,*V2LON,LINE,*HOFF,". ",CPCDATE,SP1;
          MOVE      ONE,FLAG1
.
DPLN1260  DISPLAY   *P16:CVERT,PCDSDESC
.
          PACK      NUMOPTN,PCDIADMN,PCDIDATE,PCDITIME,PCDIDIAG
.
          STORE     NUMOPTN,LINE,SELECT01,SELECT02,SELECT03,SELECT04,SELECT05:
                                 SELECT06,SELECT07,SELECT08,SELECT09,SELECT10:
                                 SELECT11,SELECT12,SELECT13
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      DPLN1250 IF LESS
.
.         Have reached end of screen, so save current record pointer
.
          PACK      LDIM13,PCDSTYPE,PCDSCODE,PCDSLINE
          GOTO      DPLN9999
.
. ------  get Diagnosis descritpion from the PCPPDSFD file  ------
.
DPLN2000  MOVE      AADMNO,PCPDADMN
          MOVE      PCDIDCOD,PCPDDCOD
          PACK      KEY14,PCPDADMN,PCPDDCOD,SP20
          CALL      RSPCPDS1
.
DPLN2010  CALL      RKPCPDS1                 
.
          BRANCH    OVRCD,DPLN1000
.
          COMPARE   AADMNO,PCPDADMN
          GOTO      DPLN1000 IF NOT EQUAL
.
          MATCH     PCDIDCOD,PCPDDCOD
          GOTO      DPLN1000 IF NOT EQUAL
.
          BRANCH    FLAG1,DPLN2020
          ADD       ONE,LINE
.
          DISPLAY   *P1:CVERT,*V2LON,LINE,*HOFF,". ",CPCDATE,SP1;
          MOVE      ONE,FLAG1
.
DPLN2020  DISPLAY   *P16:CVERT,PCPDDESC
.
          PACK      NUMOPTN,PCDIADMN,PCDIDATE,PCDITIME,PCDIDIAG
.
          STORE     NUMOPTN,LINE,SELECT01,SELECT02,SELECT03,SELECT04,SELECT05:
                                 SELECT06,SELECT07,SELECT08,SELECT09,SELECT10:
                                 SELECT11,SELECT12,SELECT13
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      DPLN2010 IF LESS
.
.         Have reached end of screen so save current record pointer
.
          PACK      LDIM14,PCPDADMN,PCPDDCOD,PCPDLINE
          GOTO      DPLN9999
.
DPLN9000  MOVE      FALSE,SCRNFLAG
.
DPLN9999  RETURN
+
.*******************************************************************************
.*                                CL2V0000                                     *
.*        clear variables                                called by : DPLN0000  *
.*******************************************************************************
CL2V0000  MOVE      ZERO,LINE
          MOVE      ZERO,LINECNT1
          MOVE      ONE,FORM2
.
CL2V1000  STORE     SP33,FORM2,SELECT01,SELECT02,SELECT03,SELECT04,SELECT05:
                               SELECT06,SELECT07,SELECT08,SELECT09,SELECT10:
                               SELECT11,SELECT12,SELECT13
          COMPARE   TEN3,FORM2
          GOTO      CL2V9000 IF EQUAL
          ADD       ONE,FORM2
          GOTO      CL2V1000
.
CL2V9000  MOVE      ZERO,FORM2
.
CL2V9999  RETURN
.
.*******************************************************************************
.*                                RDSP0000                                     *
.*        re-display nursing code                                              *
.*                                                                             *
.*                          called by : GPAT0000, APLN0000                     *
.*******************************************************************************
RDSP0000  DISPLAY   *P1:3,*EF,*P5:4,"Input your nursing code : ",*V2LON:
                               NURSCODE,*HOFF,*P52:4,PCNUNAME,*P78:4,PCNUCLSS
.
RDSP9999  RETURN
.
.*******************************************************************************
.*                                RDSP0000                                     *
.*        re-display nursing code                                              *
.*                                                                             *
.*                          called by : GPAT0000, APLN0000                     *
.*******************************************************************************
RDSU0000  DISPLAY   *P1:3,*EF,*P5:4,"Input your nursing code : ",*V2LON:
                               NURSCODE,*HOFF,*P52:4,PCNUNAME,*P78:4,PCNUCLSS
.
          DISPLAY   *P5:6,"Patient Surname         : ",*V2LON,SURNAME,*HOFF
.
          DISPLAY   *P1:8,*ULON,"No.",*HOFF," ",*ULON,"Patient's Name",SP30:
                    "            ",*P55:8,*HOFF," ",*ULON,"U/R No.",*HOFF," ":
                    *ULON,"Adm Date",*HOFF," ",*ULON,"Sex",*HOFF," ",*ULON,"Age"
.
RDSU9999  RETURN
.
.*************************************************************************
.*                           DEVA0000                                    *
.*         Display evalutations related to chioce                        *
.*************************************************************************
DEVA0000  LOAD      KEY33,REPLYF,SELECT01,SELECT02,SELECT03,SELECT04,SELECT05:
                                 SELECT06,SELECT07,SELECT08,SELECT09,SELECT10:.
                                 SELECT11,SELECT12,SELECT13
.
          CALL      RDPCDIA1                * Read DIA file
.
          MOVE      PCDIUNIQ,UNIQUE
          UNPACK    PCDIDATE,CCENT,CYEAR,CMON,CDAY
          PACK      PRDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.         
          MOVE      NINE,CLINE              * initialize line

          DISPLAY   *P1:CLINE,*EF:
                    *P3:CLINE,*V2LON,"Date",*P12:CLINE,"Nursing Diagnosis"
          ADD       ONE,CLINE
.
          DISPLAY   *P1:CLINE,PRDATE;     * Date 
          ADD       ONE,CLINE
          DISPLAY   *P1:CLINE,"(",PCDITIME,")";   * Time 
          SUB       ONE,CLINE
.
.         Now display the nursing diagnosis.  In order to do this we need to 
.         check the description number in the PCPDIAFD file.  Check if it is 
.         blank.  If it is then get the description from the PCPDSCFD file,
.         else get the description from the PCPPDSFD file.
.
          MATCH     SP4,PCDIDCOD
          GOTO      DEVA1000 IF NOT EQUAL
.
.         Get the description from the PCPDSCFD file
.
          MOVE      SIX,PCDSTYPE
          MOVE      PCDIDIAG,PCDSCODE
          MOVE      ONE,PCDSLINE
          PACK      KEY13,PCDSTYPE,PCDSCODE,PCDSLINE
          CALL      RDPCDSC1                 * Record will exist on file
          MOVE      PCDSDESC,DIAGDESC
          GOTO      DEVA1500
.
.         Get the description from the PCPPDSFD file
.
DEVA1000  MOVE      AADMNO,PCPDADMN
          MOVE      PCDIDCOD,PCPDDCOD
          MOVE      ONE,PCPDLINE
          PACK      KEY14,PCPDADMN,PCPDDCOD,PCPDLINE
          CALL      RDPCPDS1                 * Record will exist on file
          MOVE      PCPDDESC,DIAGDESC
.
DEVA1500  DISPLAY   *P12:CLINE,DIAGDESC
          ADD       TWO,CLINE
.
          DISPLAY   *P12:CLINE,*V2LON,"Evaluations",*P71:CLINE,"Finished"
          ADD       ONE,CLINE 
          PACK      KEY26,PCDIUNIQ,Z25 
          CALL      RSPCEVA1                * position file pointer
.
DEVA2000  CALL      RPPCEVA1                * get latest evaluation
          BRANCH    OVRCD,DEVA5000  
.
          MATCH     PCDIUNIQ,PCEVUNIQ       * right code?
          GOTO      DEVA5000 IF NOT EQUAL
.
          COMPARE   "20",CLINE              * full page?
          GOTO      DEVA5000 IF NOT LESS
.
          BRANCH    PCEVSTAT,DEVA2100,DEVA2200
.
DEVA2100  MOVE      "No",PRSTAT             * set status to print
          GOTO      DEVA2300
.
DEVA2200  MOVE      "Yes",PRSTAT            * set status to print
.
DEVA2300  MOVE      PCEVNURS,PCNUCODE
          MOVE      PCNUCODE,KEY6
          CALL      RDPCNUR1                * get nurse description
.
          CALL      PRDT0000                * format date
          DISPLAY   *P1:CLINE,PRDATE,"  (",PCEVTIME,")",*P25:CLINE,PCNUNAME:
                    SP1,PCNUCLSS,*P74:CLINE,PRSTAT
.
          PACK      KEY14,PCDIADMN,PCEVDCOD,SP2
          CALL      RSPCPDS1
.
DEVA3000  ADD       ONE,CLINE
          CALL      RKPCPDS1               * get evaluation description
          BRANCH    OVRCD,DEVA2000 
.
          MATCH     PCEVDCOD,PCPDDCOD      * right code?
          GOTO      DEVA2000 IF NOT EQUAL
.
          DISPLAY   *P12:CLINE,PCPDDESC    * print desc
.
          COMPARE   "23",CLINE             * full page?
          GOTO      DEVA3000 IF LESS
.                    
DEVA5000  CALL      CONTQST                * ok to continue?
          BRANCH    CEXIT,DEVA7000,DEVA8000,DEVA9000
.
DEVA7000  MOVE      ONE,EXIT
          GOTO      DEVA9999
.
DEVA8000  MOVE      TWO,EXIT
          GOTO      DEVA9999
.
DEVA9000  MOVE      THREE,EXIT
          MOVE      ONE,GOTNAME
          CALL      RDSU0000
.
DEVA9999  RETURN
.
.******************************************************************************
.*                           PRDT0000                                         *
.*                Format date into printing format                            *
.******************************************************************************
PRDT0000  UNPACK    PCEVDATE,CCENT,CYEAR,CMON,CDAY
          PACK      PRDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          REP       " 0",PRDATE             * zero fill date
PRDT9999  RETURN         
.*****************************************************************************
.*                           WPRC0000                                        *
.*               Handle word processor and posting                           *
.*****************************************************************************
WPRC0000  CALL      WORD0000                * load wp
          BRANCH    EXIT,WPRC9999
.
WPRC2000  DISPLAY   *P1:24,*EL,"Is this care plan complete ( / ) ? _":
                    *P29:24,*V2LON,"Y",*P31:24,"N"
          KEYIN     *P36:24,*V2LON,ANS
          REP       "N1Y2",ANS
          MOVE      ZERO,PCEVSTAT
          MOVE      ANS,PCEVSTAT
          BRANCH    PCEVSTAT,WPRC3000,WPRC3000
          GOTO      WPRC2000
.
WPRC3000  DISPLAY   *P1:24,*EL,*P50:24,*V2LON,"*** POSTING ***"
          MOVE      ZERO,LINE
          MOVE      ZERO,FORM1
.
          PACK      KEY14,AADMNO,Z6
          CALL      RSPCPDS1
          CALL      RPPCPDS1                * read last evaluation number
          BRANCH    OVRCD,WPRC3050
.
          MATCH     PCPDDCOD,SP4
          GOTO      WPRC3050 IF EQUAL
.
          COMPARE   AADMNO,PCPDADMN        * right addmission number?
          GOTO      WPRC3050 IF NOT EQUAL
.
          MOVE      PCPDDCOD,FORM4         * increment evaluation number 
          ADD       ONE,FORM4
          MOVE      FORM4,PCPDDCOD
          GOTO      WPRC3100
.
WPRC3050  MOVE      ZERO,FORM4             * initialize evaluation to zero if
                                           * no previous evaluation
          ADD       ONE,FORM4
          MOVE      FORM4,PCPDDCOD
.
WPRC3100  PACK      LINE60,SP30,SP30
          READ      SEQTEMP,SEQ;LINE60     * read next line in tempfile 
          GOTO      WPRC3400 IF OVER
.
          ENDSET    LINE60
          APPEND    SP33,LINE60
          APPEND    SP33,LINE60
          RESET     LINE60
.
          MATCH     LINE60,SP10             * nothing on line?
          GOTO      WPRC3400 IF EQUAL
.
          GOTO      WPRC3300 
.
WPRC3300  MOVE      LINE60,PCPDDESC
          MOVE      AADMNO,PCPDADMN
          ADD       ONE,LINE
          MOVE      LINE,PCPDLINE
.
          PACK      KEY14,PCPDADMN,PCPDDCOD,PCPDLINE
          CALL      WRPCPDS1                * write to description file
          GOTO      WPRC3100
.
WPRC3400  MOVE      UNIQUE,PCEVUNIQ 
          
          CALL      IBACLOCK                * get current date for key
          PACK      PCEVDATE,CCC,CYY,CMM,CDD * set up date
          REP       " 0",PCEVDATE
          MOVE      CTIMEIS,PCEVTIME
          MOVE      PCPDDCOD,PCEVDCOD
          MOVE      NURSCODE,PCEVNURS
          PACK      KEY26,PCDIUNIQ,PCEVDATE,PCEVTIME
.
          CALL      WRPCEVA1                * write to evaluation file
.
          IF        PCEVSTAT = TWO
            MOVE      TWO,PCDISTAT            * de-activate careplan
            CALL      UPPCDIA1
          ENDIF
.
          MOVE      FALSE,EXIT
          GOTO      WPRC9999
.
WPRC9000  MOVE      TRUE,EXIT
.
WPRC9999  RETURN
+
.*******************************************************************************
.*                                WORD0000                                     *
.*        invoke word processor                          called by : DESC0000  *
.*******************************************************************************
WORD0000  CALL      CRTS0000             * set up temp file
.                                          before invoking word processor
.
. ------  execute the word processor command line  ------
.
WORD2000  CLEAR     CMDLINE
          APPEND     FNAMES1,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE              * cmdline contains file name
          MOVE      TEN,STVAR
          WORD      CMDLINE,999,9,12,69,23,STVAR
.
. ------  ask the "O.K. to post ?" question  ------
.
WORD3000  DISPLAY   *P1:24,*EF,"Do you want to use this evalutation ( / / )":
                    " ? _":
                    *P38:24,*V2LON,"Y",*P40:24,"N",*P42:24,"C";
          KEYIN     *P47:24,*V2LON,ANS
          REP       "Y1y1N2n2C3c3",ANS
          MOVE      ANS,FORM1
          BRANCH    FORM1,WORD8000,WORD0000,WORD9000     * 1=Yes, 2=No, 3=Cancel
          BEEP
          GOTO      WORD3000
.
. ------  Post option chosen  ------
.
WORD8000  MOVE      FALSE,EXIT
          GOTO      WORD9999
.
. ------  Cancel option chosen  ------
.
WORD9000  MOVE      TRUE,EXIT
.
WORD9999  RETURN
+
.*******************************************************************************
.*                                CRTS0000                                     *
.*      create and open temporary sequential file 1      called by : WORD0000  *
.*******************************************************************************
CRTS0000  CALL      KLLS0000
.
          PREPARE   SEQTEMP,FNAMES1
.
CRTS9999  RETURN
+
.*******************************************************************************
.*                                KLLS0000                                     *
.*      routine to delete temporary sequential file 1    called by : MAINLINE  *
.*                                                                   CRST0000  *
.*******************************************************************************
KLLS0000  CLOSE      SEQTEMP
          PREPARE    SEQTEMP,FNAMES1
          CLOSE      SEQTEMP
.
KLLS9999  RETURN
+
.*******************************************************************************
.
          INC       STD001IO
          INC       PATHEAD
          INC       PMIHEAD
          INC       PATALERT
.
          INC       PCPDIAIO/INC
          INC       PCPNURIO/INC
          INC       PCPEVAIO/INC
          INC       PCPDSCIO/INC
          INC       PCPDISIO/INC
.
          INC       PATALRIO/INC
          INC       PATTERIO/INC
          INC       PATLOCIO/INC
          INC       PATTRNIO/INC
          INC       PCPPDSIO/INC
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATNIDIO/INC
          INC       PATNOBIO/INC
          INC       PATDSCIO/INC
          INC       PATWR1IO/INC
          INC       CALCAGE
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
