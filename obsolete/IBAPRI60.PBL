.******************************************************************************
.* System    :   Privat Practice Bulk Billing                                 *
.* Program   :   IBAPRI60                                                     *
.* Function  :   Rejection Report                                             *
.******************************************************************************
.* Date      :   02.04.92                                                     *
.* Author    :   Peter Eddey                                                  *
.* Mods      :                                                                *
.* V10.13.01 02/08/2018  Richa Phogat      TSK 0848506                        *
.*           Updated keylength for PRBBDOCT from KEY6 to KEY10                *
.* V10.00.01 23/08/2010  Steve Armstrong   CAR 228847                         *
.*           Recompiled for changes to PATIN1IO                               *
.******************************************************************************
.*        V9.03.01  02/03/2004  Lina Vo       CAR 47921                       *
.*                  Removed use of PRIDBTFD & IO.                             *
.*        V9.02.01  02/10/2001  J.Tan                                         *
.*                  Mods to use PMSHCPFD instead of PATDO1FD                  *
.*        V5.08.02  25/10/2000  Dean Cramer   SRF 6710                        *
.*                  Recomplied for PATFNDDS                                   *
.*                  Changed for health fund table code size increase to       *
.*                  8 characters                                              *
.*        V5.08.01  16/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.02  12/10/1999  J Habasque                                    *
.*                  Recompiled for PRIBULFD                                   *
.*        V5.07.01  09/06/1999  Greg Horvat                                   *
.*                  Recompiled for PRIINVIO - Fixed problem with direct read  *
.*                                                                            *
.*               V6.00.02 07/10/92  Steve Armstrong                           *
.*                        Modified for alphanumeric debtor number             *
.*               V6.00.03 22/10/92  Steve Armstrong                           *
.*                        Fixed bug in printing of patient name and U/R       *
.*               V6.00.04 01/03/93  DIG       SRF 120122  Quote 7558          *
.*                        Re-Compiled for changes to Bulk Billing & addition  *
.*                        of Cash Deposits.                                   *
.*                                                                            *
.******************************************************************************
.
          INC       STD001FD
          INC       PRIBULFD/INC
..          INC       PRIDBTFD/INC
          INC       PRIINVFD/INC
          INC       PATCONFD/INC
          INC       PATCODFD/INC
          INC       PMSHCPFD/INC
          INC       PATGO1FD/INC
          INC       PATFN1FD/INC
          INC       PATIN1FD/INC
          INC       PATINVFD/INC
          INC       PATMA1FD/INC
          INC       PATVISFD/INC
          INC       RCPREGFD/INC
.
.*********************************************************************
.*                       Data Variables Required                     *
.*********************************************************************
.
AFUNDH    DIM       6
CATGB     INIT      "GB"
CODE      DIM       2
COUNTR    FORM      1
DATE      DIM       10
DIM3      DIM       3
DISPUR    DIM       9
DOCNAME   DIM       20
ENDCODE   DIM       3
ENDDESC   DIM       30 
HIGCODE   DIM       6
HIGDESC   DIM       30
INDICATR  DIM       1
KCOL      FORM      3
KVERT     FORM      2
OLDBULK   DIM       3
OLDINS    DIM       3 
NAME      DIM       18
SAVITEM   DIM       9
SAVREG    DIM       3
SDOCT     DIM       18
SINVOICE  DIM       8
SKEY38    DIM       38
STRTCODE  DIM       3
STRTDESC  DIM       30
TEMPINDC  DIM       1
ROW       FORM      2
.
DIM30     DIM       30
.
PRGID     INIT      "IBAPRI60"
PRGNAM    INIT      "Rejection Report"
VERSION   INIT      "V10.13.01"
.
.*********************************************************************
.*                         ML0000                                    *
.*                     Mainline Code                                 *
.*********************************************************************
.
ML0000    CALL      INIT0000             * initialization routine
          BRANCH    EXIT,ML9999      
ML0500
          CALL      MENU0000             * program menu
          BRANCH    EXIT,ML1000
          GOTO      ML9999
.
.***** Print listing by Bulk bill Code *****
.
ML1000
          CALL      DSCR0000             * display the screen
.
          CALL      IBUL0000             * input procedure codes
          BRANCH    EXIT,ML0500
.
          CALL      PRNT0000             * print the report
          GOTO      ML0500
.
ML9999
          CHAIN     PGM
.
.********************************************************************
.*                         INIT0000                                 *
.*                    Initialization routine                        *
.********************************************************************
.
INIT0000
          
          MOVE      ONE,CNEWDS
          CALL      DISPHEAD
.
          MOVE      ZERO,EXIT
.
INIT1000
.
          DISPLAY   *P56:24,*EL,"Opening pribulkf"
          OPEN      PRIBULK1,"pribulkf"
          OPEN      PRIBULK3,"pribulkf"
          
          DISPLAY   *P64:24,"patfn1af";      * Health Fund File
          OPEN      PATFN1A1,"patfn1af"
.
          DISPLAY   *P64:24,"patgo1af ";      * Government File
          OPEN      PATGO1A1,"patgo1af"
.
          DISPLAY   *P64:24,"patin1af ";      * Insurance File
          OPEN      PATIN1A1,"patin1af"
.
          DISPLAY   *P64:24,"patcodes";      * Codes file        
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patcodes";      * Codes file        
          OPEN      RCPREGA1,"rcpregaf"
.
          DISPLAY   *P64:24,"patcodes";      * Codes file        
          OPEN      PATVISA1,"patvisaf"
.
          DISPLAY   *P64:24,"patma1af";      * patient master file
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
..          DISPLAY   *P64:24,"pridebtf";      * PP debtor file    
..          OPEN      PRIDEBT1,"pridebtf"
.
          DISPLAY   *P64:24,"priinvof";      * PP invoice file   
          OPEN      PRIINVO1,"priinvof"
.
          DISPLAY   *P64:24,"patinvrf";      * Invoice file      
          OPEN      PATINVR1,"patinvrf"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"controlf";      
          OPEN      CONTROLF,"controlf"
.
INIT9999  RETURN
.
.********************************************************************
.*                         DMEN0000                                 *
.*                  Display the programs menu                       *
.********************************************************************
.
DMEN0000
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0 ",*HOFF,"= Exit":
                    *P1:5,*V2LON,"1 ",*HOFF,"= Print Rejection Report":    
                    *P1:7,"Select Option ? "
.
DMEN9999  RETURN
.
.********************************************************************
.*                         DSCR0000                                 *
.*                  Display the screen prompts                      *
.********************************************************************
.
DSCR0000
          DISPLAY   *P1:8,*EF:
                    *P1:10,"Bulk Bill Code From  :":  
                    *P1:11,"Bulk Bill Code To    :"
.
DSCR9999  RETURN
.********************************************************************
.*                         MENU0000                                 *
.*                  Handle the programs menu                        *
.********************************************************************
.
MENU0000
          CALL      DMEN0000             * display the menu
MENU1000
          KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,EXIT:  
                    *P17:7,*DV,EXIT  
.
          COMPARE   ZERO,EXIT            * test if exit selected
          GOTO      MENU9999 IF EQUAL
.
.**** Test for valid options 1 or 2 ****
.
          BRANCH    EXIT,MENU9999
.
          BEEP                           * invalid option so re-keyin
          GOTO      MENU1000
.
.
MENU9999  RETURN
.
.**********************************************************************
.*                       IBUL0000                                     *
.*             Input the required bulk bill codes                     *
.**********************************************************************
.
IBUL0000
          MOVE      ZERO,EXIT
          MOVE      "24",KCOL
.
          MOVE      TEN,KVERT                         * set the row
          CALL      PRIKYBUL                          * keyin the procedure
          BRANCH    EXIT,IBUL9000
          IF        EXIT = 2
            DISPLAY   *P24:10,*V2LON,"Start"          * start of file
            MOVE      SP3,STRTCODE
          ELSE
            MOVE      TDESC,STRTDESC
            MOVE      ACODE,STRTCODE
            DISPLAY   *P24:KVERT,*V2LON,STRTCODE,*P32:KVERT,*HOFF,STRTDESC
          ENDIF
.
          MOVE      TEN1,KVERT                        * set the row
          CALL      PRIKYBUL                          * keyin the procedure
          BRANCH    EXIT,IBUL9000
          IF        EXIT = 2
            DISPLAY   *P24:11,*V2LON,"Finish"         * end of file
            MOVE      "zzz",ENDCODE
            MOVE      ZERO,EXIT
          ELSE
            MOVE      ACODE,ENDCODE
            MOVE      TDESC,ENDDESC
            DISPLAY   *P24:KVERT,*V2LON,ENDCODE,*P32:KVERT,*HOFF,ENDDESC
          ENDIF
.
          CALL      CONTQST
          BRANCH    CEXIT,IBUL9999,IBUL5000,IBUL9000
.
IBUL5000  DISPLAY   *P24:11,*EL,*P24:10,*EL
          GOTO      IBUL0000
.
IBUL9000  MOVE      ONE,EXIT
.
IBUL9999  RETURN
.
.*********************************************************************
.*                        PRNT0000                                   *
.*                   Print the master listing                        *
.*********************************************************************
.
PRNT0000
          MOVE      ZERO,CPAGENO
          DISPLAY   *P1:24,*EL,*V2LON,"***** Printing Report *****"
          MOVE      SP3,OLDBULK
          CALL      HEAD0000                * print heading
          MOVE      SP3,PRBBBULK
          PACK      KEY38,THREE,STRTCODE,SP20,SP20
          CALL      RSPRBB3 
          CALL      RKPRBB3                 * get next record
          MOVE      PRBBBULK,OLDBULK        * get first bulk bill code
.
.------   Read through the rejected Records ------
.
          PACK      KEY38,THREE,STRTCODE,SP20,SP20
PRNT0500  CALL      RSPRBB3 
.
PRNT1000  CALL      RKPRBB3                 * get next record
          BRANCH    OVRCD,PRNT8000
.
          COMPARE   THREE,PRBBSTAT          * rejected record ?
          GOTO      PRNT8000 IF NOT EQUAL
.
          MATCH     PRBBBULK,ENDCODE        * test if past the required code to
          GOTO      PRNT8000 IF LESS          print to
.
          COMPARE   "55",CLNO               * test if the page is full
          GOTO      PRNT1200 IF LESS 
.
          MOVE      SP3,OLDBULK
.
PRNT1200  MATCH     OLDBULK,PRBBBULK        * test if bulk bill code has changed
          GOTO      PRNT1500 IF EQUAL
.
          MOVE      PRBBBULK,OLDBULK
          CALL      HEAD0000
          ADD       ONE,CLNO
PRNT1500  
          CALL      UND132
          CALL      LNCL0000                * print the current cliam line
.
.------   Save the key, invoice number, item, and the register -----
.
          PACK      SKEY38,PRBBSTAT,PRBBBULK,PRBBCODE,PRBBINVN,PRBBITMN:
                           PRBBCLAM,PRBBREGR
          MOVE      PRBBINVN,SINVOICE
          MOVE      PRBBREGR,SAVREG
          MOVE      PRBBITMN,SAVITEM
.
.------   Test for previous rejections & print first one if so -----
.
          PACK      KEY38,FOUR,PRBBBULK,PRBBCODE,PRBBINVN,PRBBITMN,SP30
          CALL      RSPRBB3
.
.------ get the first previous rejection record ------
.
PRNT2000  CALL      RKPRBB3
          IF        OVRCD = 1
            MOVE      SKEY38,KEY38          * no records left so restore key
            GOTO      PRNT0500
          ELSE
            IF        PRBBSTAT <> 4
              MOVE      SKEY38,KEY38        * no records left so restore key
              GOTO      PRNT0500
            ELSE
              MATCH     SINVOICE,PRBBINVN
              IF          !@EQUAL
                MOVE      SKEY38,KEY38        * no records left so restore key
                GOTO      PRNT0500
              ELSE
                MATCH     SAVITEM,PRBBITMN
                IF          !@EQUAL
                  MOVE      SKEY38,KEY38        * no records left so restore key
                  GOTO      PRNT0500
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.         
          MATCH     SAVREG,PRBBREGR           * see if we have the same 
          GOTO      PRNT2000 IF NOT EQUAL       register
.
          CALL      UBLN0000                * half underline , half blank
          ADD       ONE,CLNO
          CALL      LNFR0000                * Print first previous rejection lin
          ADD       ONE,CLNO
.
.------   Get next re-submission records for this invoice ------
.
PRNT5000  CALL      RKPRBB3
          IF        OVRCD = 1
            MOVE      SKEY38,KEY38          * no records left so restore key
            CALL      BLAN0000              * Print blank line
            ADD       ONE,CLNO
            GOTO      PRNT0500              * return to rejected record
          ENDIF
.
          MATCH     SINVOICE,PRBBINVN
          IF        !@EQUAL
            MOVE      SKEY38,KEY38          * Not the same invoice so
            CALL      BLAN0000              * Print blank line
            ADD       ONE,CLNO
            GOTO      PRNT0500                return to rejected records
          ENDIF
.
          MATCH     SAVITEM,PRBBITMN
          IF        !@EQUAL
            MOVE      SKEY38,KEY38          * Not the same item so
            CALL      BLAN0000              * Print blank line
            ADD       ONE,CLNO
            GOTO      PRNT0500                return to rejected records
          ENDIF
.
          IF        PRBBSTAT <> 4
            MOVE      SKEY38,KEY38          * End of re-submitted records so
            CALL      BLAN0000              * Print blank line
            ADD       ONE,CLNO
            GOTO      PRNT0500                return to rejected records
          ENDIF
.         
          MATCH     SAVREG,PRBBREGR         * see if we have the same 
          GOTO      PRNT5000 IF NOT EQUAL       register
.
          CALL      LNPR0000                * print previous rejection line
.
          COMPARE   "55",CLNO               * test if page is full
          GOTO      PRNT5000 IF LESS 
.
          CALL      HEAD0000
          GOTO      PRNT5000
.
.------   End of processing ------
.
PRNT8000
          CALL      UND132
          PRINT     " ",*N:
                    "***** End of Report *****"
          DISPLAY   *P1:24,*EL
.
PRNT9999  RETURN
+
.*********************************************************************
.*                         HEAD0000                                  *
.*                   Print the required heading                      *
.*********************************************************************
.*
HEAD0000
          CALL      IBACLOCK
          CALL      HEAD132
          PRINT     " "
          MATCH     SP3,STRTCODE
          IF        @EQUAL
            PRINT     *40,"Bulk Bill Code from : Start"
          ELSE 
            PRINT     *40,"Bulk Bill Code from : ",STRTCODE,SP2,STRTDESC
          ENDIF
          MATCH     "zzz",ENDCODE
          IF        @EQUAL
            PRINT     *40,"                 to : Finish",*N
          ELSE 
            PRINT     *40,"                 to : ",ENDCODE,SP2,ENDDESC,*N
          ENDIF
.
          CALL      UND132 
          PRINT     *1,"|",*2,"B.B.",*6,"|",*7,"Ins.",*14,"|",*15,"Invoice":
                    *24,"|",*25,"Item No.",*34,"|",*35,"U/R",*44,"|",*64,"|":
                    *84,"|",*85,"Claim",*94,"|":
                    *106,"|",*107,"Amount",*119,"|",*120,"Amount",*132,"|"
          PRINT     *1,"|",*2,"Code",*6,"|",*7,"Code",*14,"|",*15,"Number":
                    *24,"|",*25,"Number",*34,"|",*35,"Number",*44,"|":
                    *46,"Patient Name",*64,"|",*66,"Service Doctor":
                    *84,"|",*85,"Number",*94,"|",*96,"Date Sent":
                    *106,"|",*107,"Claimed",*119,"|",*120,"Paid",*132,"|"
HEAD9999 
          ADD       FIVE,CLNO
          RETURN
.
.*********************************************************************
.*                         LNFR0000                                  *
.*                   Print the First rejection line                  *
.*********************************************************************
. 
LNFR0000
          REP       " 0",PRBBDATE
          UNPACK    PRBBDATE,CCENT,CYEAR,CMON,CDAY
          PACK      DATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          PRINT     *1,"|",*46,"PREVIOUS REJECTIONS FOR THIS ITEM ":
                    *84,"|",*85,PRBBCLAM,*94,"|",*96,DATE:
                    *106,"|",*107,PRBBAMNT,*119,"|",*120,PRBBPAID,*132,"|"
LNFR9999  RETURN
.
.
.*********************************************************************
.*                         LNFR0000                                  *
.*                     Print a rejection line                        *
.*********************************************************************
. 
LNPR0000
          REP       " 0",PRBBDATE
          UNPACK    PRBBDATE,CCENT,CYEAR,CMON,CDAY
          PACK      DATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          PRINT     *1,"|",*84,"|",*85,PRBBCLAM,*94,"|",*96,DATE:
                    *106,"|",*107,PRBBAMNT,*119,"|",*120,PRBBPAID,*132,"|"
LNPR9999  RETURN
.
.*********************************************************************
.*                         LNCL0000                                  *
.*                   Print the current claim line                    *
.*********************************************************************
. 
LNCL0000
          CALL      GNAM0000                * get patient name and U/R
          CALL      FDOC0000                * format the doctors name
          REP       " 0",PRBBDATE
          UNPACK    PRBBDATE,CCENT,CYEAR,CMON,CDAY
          PACK      DATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          PRINT     *1,"|",*2,PRBBBULK,*6,"|",*7,PRBBCODE,*14,"|",*15,PRBBINVN:
                    *24,"|",*25,PRBBITMN,*34,"|",*35,DISPUR,*44,"|",*46,NAME:
                    *64,"|",*66,SDOCT,*84,"|",*85,PRBBCLAM,*94,"|",*96,DATE:
                    *106,"|",*107,PRBBAMNT,*119,"|",*120,PRBBPAID,*132,"|"
LNCL9999  RETURN
.
.*********************************************************************
.*                         BLAN0000                                  *
.*                   Print a blank line                              *
.*********************************************************************
. 
BLAN0000
          PRINT     *1,"|",*84,"|",*94,"|",*106,"|",*119,"|",*132,"|"
BLAN9999  RETURN
.
.
.
.*********************************************************************
.*                         UBLN0000                                  *
.*            Print half underline half blank line                   *
.*********************************************************************
. 
UBLN0000
          PRINT     *1,"|":
                    "----------------------------------------":
                    "------------------------------------------":
                    *84,"|---------",*94,"|-----------":
                    *106,"|------------",*119,"|-------------",*132,"|"
UBLN9999  RETURN
.
.*****************************************************************************
.*                             GNAM0000                                      *
.*                        Get the patients name                              *
.*****************************************************************************
.
GNAM0000
          MOVE      SP6,PURNO
          MOVE      SP30,NAME
          PACK      KEY3,PRBBREGR
          CALL      RDREGA1
          BRANCH    OVRCD,GNAM9999
.
          PACK      KEY8,PRBBINVN
          BRANCH    REGIBACD,GNAM1000,GNAM5000
          GOTO      GNAM9999
.
GNAM1000  CALL      RDINV1
          BRANCH    OVRCD,GNAM9999
.
          MOVE      PINVADM,PVIBILL
          MOVE      PVIBILL,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,GNAM9999
.
          MOVE      PVIURNO,PURNO
          GOTO      GNAM8000

GNAM5000  MOVE      "Unknown Patient",NAME
          CALL      RDPRIN1
          BRANCH    OVRCD,GNAM9999
.
          BRANCH    PRINSCAN,GNAM8000            * PMI patient
.
          PACK      DISPUR,SP1,PRINDEBT
..          PACK      KEY8,PRINDEBT                * debtor
..          CALL      RDPRDB1
..          BRANCH    OVRCD,GNAM9999
.
..          MOVE      PRDBGNAM,ANS
..          PACK      NAME,ANS,DOT,PRDBSNAM
          MOVE      "Unknown Debtor",NAME
          GOTO      GNAM9999
.
GNAM8000  PACK      DISPUR,ANSP,PRINDEBT
          MOVE      PRINDEBT,PURNO
          PACK      KEY8,PURNO
          CALL      RDMAST1
          BRANCH    OVRCD,GNAM9999
.
          MOVE      PGNAME,ANS
          PACK      NAME,ANS,DOT,PSNAME
        
GNAM9999  RETURN
.
.****************************************************************************
.*                              FDOC0000                                    *
.*                        Format the doctors name                           *
.****************************************************************************
.
FDOC0000
          MOVE      SP10,SDOCT
          PACK      KEY10,PRBBDOCT
          PACK      KEY10,PRBBDOCT,SP10
          CALL      RDPMHCP1
          BRANCH    OVRCD,FDOC9999
.
          MOVE      PMHCGNAM,ANS
.
          PACK      SDOCT,ANSD,ANSR,SP1,ANS,DOT,PMHCSNAM
.
FDOC9999  RETURN
.
.*********************************************************************
.*                 I/O and other includes required                   *
.*********************************************************************
.
          INC       STD001IO
          INC       PRIBULIO/INC
..          INC       PRIDBTIO/INC
          INC       PRIINVIO/INC
          INC       PATCODIO/INC
          INC       PATGO1IO/INC
          INC       PATFN1IO/INC
          INC       PATIN1IO/INC
          INC       PATINVIO/INC
          INC       PATMA1IO/INC
          INC       PATVISIO/INC
          INC       RCPREGIO/INC
          INC       PMSHCPIO/INC
          INC       PRIKYBUL
          INC       PATCODDS
          INC       PATINSDS
          INC       PATGOVDS
          INC       PATFNDDS
