.******************************************************************************
.* System         : PATIENT CARE PLANS                                        *
.* Program        : IBAPCP11                                                  *
.* Desc           : Input Patient Care Plans                                  *
.******************************************************************************
.* Date           : 15/12/89                                                  *
.* Author         : i chung                                                   *
.* NOTE           : as of version 5.03.05 most of the code for this is in the *
.*                  include pcpcplan.  this routine requires the admission of *
.*                  the required patient to be selected and then it will do   *
.*                  all of the Care Plan details.                             *
.* Modifications  :                                                           *
.* >>>> SEE PROGRAMMERS NOTES BELOW AT THE $$$$$ POSITION <<<<<<              *
.*        V10.02.01 25/06/2011 Steve Armstrong    CAR 240722                  *
.*                  Mods to cater for changes to PATLOCFD.                    *
.*                       - LSNAME & LGNAME extended to 40 chars.              *
.*                       - PTLCGNM2 added.                                    *
.******************************************************************************
.*        V10.10.01 26/05/2017 Ania P         CAR 261630                      *
.*                  Recompiled for PCPCPLAN                                   *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.******************************************************************************
.*        V9.03.01  20/11/2003  Steve Armstrong CAR 44631                     *
.*                  Recompiled for PCPCPLAN (Removed PI command)              *
.******************************************************************************
.*        V9.02.00  30/12/2002 Tracey Nguyen srf 23497                        *
.*                  r5.10 Care Plans Port to V9                               *
.******************************************************************************
.*        V5.08.01  23/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund table variables to 8 chars            *
.*       V5.07.01   27/09/1999  Jill Habasque  SRF 644907                     *
.*                  Recompiled for PCPCPLAN                                   *
.*       V5.07.B02  07/01/1999  Jim Stathopoulos                              *
.*                  Display Modifications                                     *
.*       V5.07.B01  04/11/1998  Jim Stathopoulos                              *
.*                  507 Changes                                               *
.******************************************************************************
.*        V5.04.01  10/09/1996  Steve Armstrong   STH Mods                    *
.*                  Recompiled for changes to PCPCPLAN                        *
.*        V5.03.08  06/12/1995    Whirlpool   srf 641021  I * D               *
.*                  Fixed copy option not to transfer word processor stuff    *
.*                  from a previous admission                                 *
.*        V5.03.07  02/11/1995    Whirlpool                                   *
.*                  made changes to PCPCPLAN so that it displays all word     *
.*                  processor data in ADD mode once you have entered it !!    *
.*                  IF YOU DO NOT UNDERSTAND THE SCREEN HANDLING DO NOT TOUCH *
.*                  ROUTINES LIKE D2OP0000, APSN0000 and DDDD0000 - see lofty *
.*                  or whirlpool or your life expectancy will suddenly decrease
.*        V5.03.06  06/10/1995    Whirlpool                                   *
.*                  Recompiled for small fix when deleting interventions in   *
.*                  PCPCPLAN - wasn't displaying the correct screen           *
.*        V5.03.05  08/08/1995    Justin Coates                               *
.*                  took the guts out of this program and into an include     *
.*                   PCPCPLAN which will also be used by IBAPOE26.            *
.*        V5.03.04  07/08/1995    Justin Coates  SRF 610948/SRF 137003        *
.*                  in change mode, display text from word processor and leave*
.*                     on same screen after change/delete (DDDD0000)          *
.*        V5.03.03  04/08/1995    Justin Coates  SRF 610948                   *
.*                  when the user enters text in the word processer in insert *
.*                    mode, if they select the item again, give the user the  *
.*                    option to Modify or Delete details (CHKS5000)           *
.*        V5.03.02  29/06/1995    Justin Coates  SRF 131551                   *
.*                  set up clinic condition for re-print option               *
.*        V5.03.01  28/06/1995    Justin Coates  SRF 130845                   *
.*                  worked out how the paging worked and hopefully fixed it   *
.******************************************************************************
.*        V5.02.03  10/05/1995  Whirlpool                                     *
.*                  Fixed bug with active flag                                *
.*        V5.02.02  19/04/1995  Whirlpool    Hawkes bay bugs                  *
.*                  fixed problem with not loading wp in related factors      *
.*        V5.02.01  20/03/1995  Greg Horvat      SRF 135557                   *
.*                  Changed to remain on the same screen when adding or       *
.*                  deleting an intervention care plan                        *
.*                  21/03/1995  Greg Horvat      SRF 135559                   *
.*                  Changed so that if a patient has no care plans for the    *
.*                  current admission you can copy a care plan from a previous*
.*                  admission for that patient                                *
.*                  21/03/1995  Greg Horvat      SRF 135560                   *
.*                  Changed to print nurse responsible on report              *
.******************************************************************************
.
.$$$$$
.    Programmers Notes 28/6/1995
.    -----------------
.    the displaying of data in this program is very scary !!!  This is because
.    it has to loop over the pcpdbsaf and the pcpdscaf files and the number of
.    records off pcpdscaf is variable.  as a result I have spent f..kin ages
.    trying to work out what it was doing and attempting to fix it.
.
.    There is one main display routine, D2OP0000.
.    This can be called 3 ways :
.     1 - from the user selecting (N)ext screen
.     2 - from the user selecting (P)revious screen
.     3 - from the user picking an item off the screen to select and then the
.         screen gets redisplayed to highlight/de-highlight the selection
.    
.    This routine sets up a few values and requires a few parameters ...
.    
.    The following variables should be initialised as follows for the very
.    first time into this routine (see AINT0000 as an example)
.         MOVE      ZERO,DISPHALF           * clear half displayed flag 
.         MOVE      ZERO,RDSPHALF           * clear redisplay half disp. flag
.         MOVE      SP20,DISPLAST           * clear last displayed key
.         MOVE      SP20,DISPFRST           * clear first displayed key
.
.    D2OP0000 requires the following parameters :
.    ------------------------------------------
.    KEY20    - the key to the pcpdbsaf file as it does a READ on entry
.    DISPHALF - this can be one or zero ...
.      = 0      a next read will be done on pcpdbsaf.  this means that the
.               first item to be displayed is from the pcpdscaf file
.      = 1      a next read wont be done on pcpdbsaf.  this means that the 
.               first item to be displayed is from the pcpdscaf file, it also
.               means DISPLAST must be set to the last item displayed on the 
.               previous screen. (NOTE: after a previous screen, this value
.               may be set to one when it shouldnt be, however, the routine
.               works itself out and seems to display everything OK when an
.               item is selected)
.    REDISP   - this can be zero or one
.      = 1      the routine was called as part of a redisplay because an item
.               was selected from the list, this means a previous read will be 
.               done after the direct read.
.
.    D2OP000 will set the following variables :
.    ----------------------------------------
.    DISPFRST this is the key to pcpdscaf for the first item displayed
.    DISPLAST this is the key to pcpdscaf for the last  item displayed
.    DISPHALF this will be set to ONE if the display is part of the way
.             through the pcpdscaf file and a new screen is needed
.    RDSPHALF this is a save value of DISPHALF for the previous screen so that
.             when the screen is redisplayed, the correct value of DISPHALF can
.             be set and the program act accordingly
.    LKEY33   this is the key to pcpdbsaf for the last item displayed
.
.    There is debug commented out in S2OP0000 which is helpful when determing
.    the first and last codes on the screen.
.$$$$$
.
          INC       STD001FD
          INC       PATALRFD/INC
          INC       PATCALAG/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATDHEAD/INC
          INC       PATDO1FD/INC
          INC       PATDSCFD/INC
          INC       PATLOCFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       PATNIDFD/INC
          INC       PATNOBFD/INC
          INC       PATNURFD/INC
          INC       PATTERFD/INC
          INC       PATTRNFD/INC
          INC       PATVISFD/INC
          INC       PATWR1FD/INC
          INC       PCPCONFD/INC
          INC       PCPDIAFD/INC
          INC       PCPNURFD/INC
.
.*******************************************************************************
.*                            CONSTANTS DECLARATION                            *
.*******************************************************************************
TILDA88   INIT      "~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~":
                    "~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~"
.
.*******************************************************************************
.*                         WORK VARIABLES DECLARATION                          *
.*******************************************************************************
ADMNO01   FORM      8           * variables used for saving admission numbers
ADMNO02   FORM      8             of patients being displayed that satisfy the
ADMNO03   FORM      8             surname the user specified.  This will allow
ADMNO04   FORM      8             care plan(s) of a particular patient (if any
ADMNO05   FORM      8             exist) to be retrieved when selected
ADMNO06   FORM      8
ADMNO07   FORM      8
ADMNO08   FORM      8
ADMNO09   FORM      8
ADMNO10   FORM      8
ADMNO11   FORM      8
ADMNO12   FORM      8
ADMNO13   FORM      8
ADMNO14   FORM      8
BJDAY     FORM      3
CJDAY     FORM      3
CODE      DIM       2           * patdscod
DISPHALF  FORM      1           * 0 = all of the descriptions displayed
FORM8     FORM      8           * temp. variable for loading admission no.
LINE      FORM      2           * display line number
LINECNT   FORM      2           * previous screen line count
LINECNT1  FORM      2           * previous screen line count
LKEY88    DIM       88          * last display key for PATLOCFD file
NUMLINES  FORM      2           * no. of lines var used in previous screen optn
NURSCODE  DIM       6           * Nursing code input
PATNAME   DIM       46          * patient name output
RDSPHALF  FORM      1           * what disphalf was on entering screen
REPLYD    DIM       2           * user response - DIM field
REPLYF    FORM      2           * user response - FORM field
REPLYFSV  FORM      2           * save replyf for screen restart
SCREENA   FORM      2           * screen counter for add care plan
SCRNFLAG  FORM      1           * screen flag (0=EOF, 1=full screen)
SP59      DIM       59          * 59 spaces
STATION   DIM       3           * nursing station for display on screen
SURNAME   DIM       40          * patient surname input
REACLEVL  FORM      1           * nurses reactivate security level
.
PRGID     INIT      "IBAPCP11"
PRGNAM    INIT      "Input Patient Care Plans"
VERSION   INIT      "V10.12.00"
.
.*******************************************************************************
.*                                MAINLINE                                     *
.*                            controlling logic                                *
.*******************************************************************************
ML0000    CALL      INIT0000              * display screen headings & open files
.
ML1000    CALL      ICOD0000              * input & validate nursing code
          BRANCH    EXIT,ML9999
.
ML2000    CALL      IPAT0000              * input patient surname
          BRANCH    EXIT,ML1000
.
ML2100    CALL      DPHD0000              * display header details
          BRANCH    EXIT,ML2000           * invalid details
.
          PACK      KEY22,PURNO,AADMNO,NURSCODE
          PROC      PCPCPLAN
          GOTO      ML2000
.
ML9999    CHAIN     PGM                   * return to calling menu
+
.*******************************************************************************
.* INIT0000  : initialization       
.*******************************************************************************
INIT0000  CALL      DISPHEAD              * display screen headings
          MOVE      ONE,CNEWDS
.
          DISPLAY   *P50:23,*EL,"Opening":
                    *P58:23,"controlf";
          OPEN      CONTROLF,"controlf"
.
. files needed for main body of this program
.
          DISPLAY   *P58:23,"patteraf"
          OPEN      PATTERA1,"patteraf"
          DISPLAY   *P58:23,"pcpnuraf"
          OPEN      PCPNURA1,"pcpnuraf"
          DISPLAY   *P58:23,"patlocaf"
          OPEN      PATLOCA1,"patlocaf"
          DISPLAY   *P58:23,"pattranf"
          OPEN      PATTRAN2,"pattranf"
          DISPLAY   *P58:23,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          DISPLAY   *P58:23,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P58:23,"patmx1af"
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P58:23,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          READ      CONTROLF,FORTY1;*77,PCCNSSEC
          READ      CONTROLF,FIFTY9;*132,CALRDESC
          READ      CONTROLF,TEN9;*75,CCNOTES
.
.
          PACK      SP59,SP30,SP30         * use to check for blank lines in W/P
.                                            temp file
.
          MOVE      "11111111",ALRTMASK    * set up the Alert Mask
          MOVE      SP20,CPHDROPT          * clear header
.
.
INIT9999  RETURN
+
.*******************************************************************************
.*                                ICOD0000                                     *
.*        input and validate nursing code                called by : MAINLINE  *
.*******************************************************************************
ICOD0000  DISPLAY   *P66:2,*EL,"Station : ":
                    *P1:3,*EF,*P5:4,"Input your user id : "
.
          OPEN      PATTERA1,"patteraf"
          MOVE      PORT,KEY2
          CALL      RDPTTER1            * read PATTERFD file for nursing station
          BRANCH    OVRCD,ICOD1000        * record not found
          CLOSE     PATTERA1
.
          COMPARE   PCCNSSEC,SECLEV       * current user security level >=
.                                           security level ?
          GOTO      ICOD1000 IF NOT LESS  * yes
.
. ------  nursing station restriction applies  ------
.
          MOVE      PTTENSTN,STATION
          DISPLAY   *P77:2,*V2LON,STATION
          GOTO      ICOD2000
.
. ------  nursing station restriction does not apply  ------
.
ICOD1000  MOVE      SP3,STATION
.
. ------  Nursing code input  ------
.
ICOD2000  KEYIN     *P26:4,*DV,UNDLN6:
                    *P26:4,*V2LON,NURSCODE
          ENDSET    NURSCODE
          APPEND    SP6,NURSCODE
          RESET     NURSCODE
.
          MATCH     SP6,NURSCODE                * null input ?
          GOTO      ICOD9000 IF EQUAL           * yes
          DISPLAY   *P26:4,*V2LON,NURSCODE      * no - display input
.
          MOVE      NURSCODE,KEY6
          CALL      RDPCNUR1                    * read PCPNURFD record
          BRANCH    OVRCD,ICOD3000              * record not found
.
          DISPLAY   *P52:4,PCNUNAME,*P78:4,PCNUCLSS
          MOVE      PCNUSECL,REACLEVL           * save nurses reactivate level
          MOVE      FALSE,EXIT                  * set EXIT = 0
          GOTO      ICOD9999                    * exit
.
ICOD3000  DISPLAY   *P1:24,*EL,*B,"Invalid User id.  ";
          CALL      HITENTER
          GOTO      ICOD2000
.
. ------  null input  ------
.
ICOD9000  MOVE      TRUE,EXIT                   * set EXIT = 1
.
ICOD9999  RETURN
+
.*******************************************************************************
.*                                IPAT0000                                     *
.*        input patient surname and display patient(s) that matches the        *
.*        surname if any.                                called by : MAINLINE  *
.*******************************************************************************
IPAT0000  DISPLAY   *P5:6,*EF,"Patient Surname    :"
.
IPAT1000  KEYIN     *P26:6,*V2LON,SURNAME
.
          RESET     SURNAME                   * <CR> ?
          GOTO      IPAT9000 IF EOS           * yes
.
          CALL      PSCH0000                  * patient search
          BRANCH    EXIT,IPAT1000             * no patient found
.
. ------  display patient(s) that matches requested surname  ------
.
          DISPLAY   *P23:6,*V2LON,SURNAME:
                    *P1:8,*ULON,"No.",*HOFF,SP1,*V2LON,*ULON,"Patients Name":
                    SP30,SP3,*P52:8," U/R No.",*HOFF,SP1,*V2LON,*ULON:
                    " Adm Date ",*HOFF,SP1,*V2LON,*ULON,"Sex",*HOFF,SP1:
                    *V2LON,*ULON,"Age"
.
          CALL      CL1V0000                  * clear storage array
          MOVE      ONE,SCREENA               * initialize screen counter to 1
.
IPAT1100  CALL      DPAT0000                  * display patient(s) routine
.
          BRANCH    SCRNFLAG,IPAT4000         * 1 = full screen
.                                               0 = end-of-file/no more records
. ------  no more records  ------
.
          COMPARE   SCREENA,ONE               * screen counter > 1 ?
          GOTO      IPAT3000 IF LESS          * yes
.
.         only one screen
.         ---------------
.
IPAT2000  DISPLAY   *P1:24,*EL,"Input the number of the patient required :  "
          MOVE      "44",CCOL
.
IPAT2100  CALL      KEYI0000                  * keyin routine
          MOVE      REPLYF,REPLYFSV
          BRANCH    EXIT,IPAT2200:            * 1 = invalid input,
                         IPAT2000:            * 2 = invalid numeric input,
                         IPAT0000             * 3 = null input
          GOTO      IPAT8000                  * 0 = valid numeric input
.
IPAT2200  BEEP
          GOTO      IPAT2100
.
.         more than one screen
.         --------------------
.
.         * on last screen
.
IPAT3000  DISPLAY   *P1:24,*EL,"Input the number of the patient required, (":
                           *V2LON,"P",*HOFF,")revious screen : "
          MOVE      "63",CCOL
.
IPAT3100  CALL      KEYI0000                  * keyin routine
          MOVE      REPLYF,REPLYFSV
          BRANCH    EXIT,IPAT3200:            * 1 = alpha input,
                         IPAT3000:            * 2 = invalid numeric input,
                         IPAT0000             * 3 = null input
          GOTO      IPAT8000                  * 0 = valid numeric input
.
IPAT3200  BRANCH    REPLYF,IPAT3300,IPAT6000  * 2 = previous screen
.
IPAT3300  BEEP
          GOTO      IPAT3100
.
. ------  full screen  ------
.
.         * on first screen
.
IPAT4000  COMPARE   SCREENA,ONE               * more than one screen ?
          GOTO      IPAT4300 IF LESS          * no
.
IPAT4050  DISPLAY   *P1:24,*EL,"Input the number of the patient required, (":
                           *V2LON,"N",*HOFF,")ext screen : "
          MOVE      "59",CCOL
.
IPAT4100  CALL      KEYI0000                  * keyin routine
          MOVE      REPLYF,REPLYFSV
          BRANCH    EXIT,IPAT4200:            * 1 = alpha input,
                         IPAT4050:            * 2 = invalid numeric input,
                         IPAT0000             * 3 = null input
          GOTO      IPAT8000                  * 0 = valid numeric input
.
IPAT4200  BRANCH    REPLYF,IPAT5000           * 1 = next screen
          BEEP
          GOTO      IPAT4100
.
.         *  in-between screen(s)
.
IPAT4300  DISPLAY   *P1:24,*EL,"Input the number of the patient required, (":
                           *V2LON,"N",*HOFF,")ext, or (",*V2LON,"P",*HOFF:
                           ")revious screen : "
          MOVE      "73",CCOL
.
IPAT4400  CALL      KEYI0000                  * keyin routine
          BRANCH    EXIT,IPAT4500:            * 1 = alpha input,
                         IPAT4300:            * 2 = invalid numeric input,
                         IPAT0000             * 3 = null input
          GOTO      IPAT8000                  * 0 = valid numeric input
.
IPAT4500  BRANCH    REPLYF,IPAT5000,IPAT6000  * 1=next screen, 2=previous screen
          BEEP
          GOTO      IPAT4400
.
. ------  display next screen  ------
.
IPAT5000  MOVE      LKEY88,KEY88
          ADD       ONE,SCREENA               * increment screen counter by 1
          GOTO      IPAT1100
.
. ------  display previous screen  ------
.
IPAT6000  CALL      PPSN0000                  * previous screen option routine 
          SUB       ONE,SCREENA               * decrease screen counter by 1
          GOTO      IPAT1100
.
. ------  valid numeric value entered  ------
.
IPAT8000  MOVE      FALSE,EXIT                * set EXIT = 0
          GOTO      IPAT9999
.
. ------  null patient surname input  ------
.
IPAT9000  MOVE      TRUE,EXIT                 * set EXIT = 1
.
IPAT9999  RETURN
+
.*******************************************************************************
.*                                PSCH0000                                     *
.*        patient search                                 called by : IPAT0000  *
.*******************************************************************************
. ------  check if patient with requested surname exist  ------
.
PSCH0000  PACK      KEY88,SURNAME,SP70,SP70
          CALL      RDSLOCA1                  * position read PATLOCFD file
          CALL      RDKLOCA1                  * read next PATLOCFD record
          BRANCH    OVRCD,PSCH1000            * end-of-file
.
          MATCH     SURNAME,LSNAME         * requested surname=patient surname ?
          GOTO      PSCH9000 IF EQUAL
.
. ------  no patient found  ------
.
PSCH1000  DISPLAY   *P1:24,*EL,*B,"No patient found with this surname.  ";
          CALL      HITENTER
          MOVE      TRUE,EXIT                  * set EXIT = 1
          GOTO      PSCH9999
.
. ------  patient found  ------
.
PSCH9000  MOVE      FALSE,EXIT                 * set EXIT = 0
.
PSCH9999  RETURN
+
.*******************************************************************************
.*                                DPAT0000                                     *
.*        display patient(s) with requested surname      called by : IPAT0000  *
.*******************************************************************************
DPAT0000  CALL      CL1V0000             * clear working variables
.
          DISPLAY   *P1:9,*EF            * clear screen from row 9
          MOVE      NINE,CVERT
          CALL      RDSLOCA1             * position read PATLOCFD file
.
DPAT1000  CALL      RDKLOCA1             * read next PATLOCFD record
          BRANCH    OVRCD,DPAT9100       * end-of-file
.
          MATCH     SURNAME,LSNAME       * requested surname = patient surname ?
          GOTO      DPAT8000 IF NOT EQUAL * no
.
          MATCH     SP3,STATION          * nursing station restriction applies ?
          GOTO      DPAT2000 IF EQUAL    * no
.
          PACK      KEY30,LADMNO,TILDA88
          CALL      RDSTRAN2             * position read PATTRNFD file
          CALL      RDPTRAN2             * read previous PATTRNFD record
          BRANCH    OVRCD,DPAT1000       * end-of-file
.
          COMPARE   LADMNO,TADMN         * same Admission Number ?
          GOTO      DPAT1000 IF NOT EQUAL * no
.
          PACK      KEY6,PTTENSTN,TWARD
          CALL      RDNURS1              * read PATNURFD record
          BRANCH    OVRCD,DPAT1000       * record not found
.
DPAT2000  COMPARE   TWENTY3,CVERT        * screen overflow ?
          GOTO      DPAT3000 IF LESS     * no
.
          MOVE      ONE,SCRNFLAG         * set screen flag on
          CALL      RDPLOCA1             * read previous PATLOCFD record 
          GOTO      DPAT9000
.
. ------  display patient details  ------
.
.         set up patient name using the surname & given names varibles of the
.         PATLOCFD file
.
DPAT3000  MOVE      LSNAME,PATNAME
          STRIP     PATNAME
          ENDSET    PATNAME
          APPEND    ", ",PATNAME
          APPEND    LGNAME,PATNAME
          APPEND    SP70,PATNAME
          RESET     PATNAME
.
.         get and set up Admission date
.
          MOVE      "ERROR  ",CPCDATE
          MOVE      SP8,ADATE
          MOVE      LADMNO,KEY8
          MOVE      LADMNO,AADMNO
          CALL      RDMISS1              * read PATMI1FD record
          IF        OVRCD = ZERO
            UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
          ENDIF
.
.         get patient's sex and age from the PATMA1FD file
.
DPAT4100  MOVE      AURNO,KEY8
          CALL      RDMAST1              * read PATMA1FD record
.
          CALL      IBACLOCK
          PACK      AGEDATE,CCC,CYY,CMM,CDD  
          CALL      CALCAGE
          MOVE      PSAGEYRS,PSAGE
.
.         display set-up details
.
          ADD       ONE,LINE
          DISPLAY   *P1:CVERT,*V2LON,LINE,*HOFF,SP2,PATNAME:
                    *P52:CVERT,LURNO:
                    *P61:CVERT,CPCDATE:
                    *P73:CVERT,PSEX:
                    *P76:CVERT,PSAGE;
.
.         save admission no. of patient being displayed
.
          STORE     AADMNO,LINE,ADMNO01,ADMNO02,ADMNO03,ADMNO04,ADMNO05:
                                ADMNO06,ADMNO07,ADMNO08,ADMNO09,ADMNO10:
                                ADMNO11,ADMNO12,ADMNO13,ADMNO14
.
.         increment counters
.
          ADD       ONE,CVERT            * increment vertical line count
          ADD       ONE,LINECNT1         * increment previous screen line count
          GOTO      DPAT1000
.
. ------  no more records that matches requested surname  ------
.
DPAT8000  MOVE      ZERO,SCRNFLAG        * set screen flag to off
.
.      +  full screen
.
DPAT9000  PACK      LKEY88,LSNAME,LGNAME,LADMNO  * save Key of last PATLOCFD rec
          GOTO      DPAT9999                       read
.
. ------  EOF of PATLOCFD file has reached  ------
.
DPAT9100  MOVE      ZERO,SCRNFLAG        * set screen flag to off
          PACK      LKEY88,TILDA88       * set last key read to highest value
.
DPAT9999  RETURN
+
.*******************************************************************************
.*                                CL1V0000                                     *
.*        clear variables                                called by : DPAT0000  *
.*******************************************************************************
CL1V0000  MOVE      ZERO,LINE            * initialize display line number
          MOVE      ZERO,LINECNT1        * initialize previous screen line count
          MOVE      ONE,FORM2
.
CL1V1000  STORE     ZERO,FORM2,ADMNO01,ADMNO02,ADMNO03,ADMNO04,ADMNO05:
                               ADMNO06,ADMNO07,ADMNO08,ADMNO09,ADMNO10:
                               ADMNO11,ADMNO12,ADMNO13,ADMNO14
          COMPARE   TEN4,FORM2
          GOTO      CL1V9000 IF EQUAL
          ADD       ONE,FORM2
          GOTO      CL1V1000
.
CL1V9000  MOVE      ZERO,FORM2
.
CL1V9999  RETURN
+
.*******************************************************************************
.*                                PPSN0000                                     *
.*    previous screen routine for displaying patients     called by : IPAT0000 *
.*******************************************************************************
PPSN0000  MOVE      ZERO,LINECNT              * initialize line count
          BRANCH    SCRNFLAG,PPSN1000         * last screen was full screen
.
          ADD       TEN4,LINECNT1
          MOVE      LINECNT1,NUMLINES         * set no. of lines to previous
          GOTO      PPSN1100                    screen line count
.
PPSN1000  MOVE      TWENTY6,NUMLINES          * set no. of lines to 26
.
PPSN1100  MOVE      LKEY88,KEY88
          CALL      RDSLOCA1                  * position read PATLOCFD file
.                                               using saved last key
PPSN2000  CALL      RDPLOCA1                  * read previous PATLOCFD record
          BRANCH    OVRCD,PPSN9000            * end-of-file
.
          MATCH     SURNAME,LSNAME            * same surname ?
          GOTO      PPSN8000 IF NOT EQUAL     * no
.
          MATCH     SP3,STATION               * no nursing station restriction ?
          GOTO      PPSN3000 IF EQUAL         * yes
.
          PACK      KEY30,LADMNO,TILDA88
          CALL      RDSTRAN2                  * position read PATTRNFD file
          CALL      RDPTRAN2                  * read previous PATTRNFD record
          BRANCH    OVRCD,PPSN2000            * end-of-file
.
          COMPARE   LADMNO,TADMN              * same Admission Number ?
          GOTO      PPSN2000 IF NOT EQUAL     * no
.
          PACK      KEY6,PTTENSTN,TWARD
          CALL      RDNURS1                   * read PATNURFD record
          BRANCH    OVRCD,PPSN2000            * record not found
.
PPSN3000  ADD       ONE,LINECNT               * increment line count
          COMPARE   LINECNT,NUMLINES          * line count > no. of lines ?
          GOTO      PPSN8000 IF LESS          * yes
          GOTO      PPSN2000                  * no
.
. ------  surname does not match/line count greater than no. of lines  ------
.
PPSN8000  PACK      KEY88,LSNAME,LGNAME,LADMNO
          GOTO      PPSN9999
.
. ------  end of PCPDSCFD file  ------
.
PPSN9000  PACK      KEY88,SP70,SP70
.
PPSN9999  RETURN
+
.*******************************************************************************
.*                                DPHD0000                                     *
.*        get selected patient and display care plan     called by : MAINLINE  *
.*******************************************************************************
DPHD0000  MOVE      ZERO,FORM8              * clear admission number
          MOVE      REPLYFSV,REPLYF
          LOAD      FORM8,REPLYF,ADMNO01,ADMNO02,ADMNO03,ADMNO04,ADMNO05:
                                 ADMNO06,ADMNO07,ADMNO08,ADMNO09,ADMNO10:
                                 ADMNO11,ADMNO12,ADMNO13,ADMNO14
          MOVE      FORM8,KEY8
          CALL      RDMISS1                   * get Admission record
          BRANCH    OVRCD,DPHD1000            * record not found
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1                   * get Patient Master record
          BRANCH    OVRCD,DPHD1100            * record not found
.
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,DPHD1100
.
          MOVE      ZERO,EXIT
          GOTO      DPHD9999
.
. ------  Admission and Patient Master files error messages  -------------------
.
DPHD1000  DISPLAY   *P1:24,*EL,*B,"Patient Admissions file error,":
                    " please contact I.B.A.  ";
          CALL      HITENTER
          GOTO      DPHD9000
.
DPHD1100  DISPLAY   *P1:24,*EL,*B,"Patient Master file error, ":
                    "please contact I.B.A.  ";
          CALL      HITENTER
          GOTO      DPHD9000
.
DPHD9000  MOVE      ONE,EXIT
DPHD9999  RETURN
+
.*******************************************************************************
.*                                KEYI0000                                     *
.*        validate keyin response to question on line 24 after displaying      *
.*        a screen full of patients that matches requested surname or EOF      *
.*        has reached                                                          *
.*               called by : GPLN0000, S1OP0000, S2OP0000, IPAT0000            *
.*******************************************************************************
KEYI0000  KEYIN     *PCCOL:24,*DV,UNDLN2:
                    *PCCOL:24,*JR,*V2LON,REPLYD
.
          ENDSET    REPLYD
          APPEND    SP2,REPLYD
          RESET     REPLYD
.
          MATCH     SP2,REPLYD              * null input ?
          GOTO      KEYI9000 IF EQUAL       * yes
.
          REP       UPPLOW,REPLYD           * replace lower case with upper case
          DISPLAY   *PCCOL:24,*V2LON,REPLYD
.
          REP       " 0",REPLYD             * replace blanks with zeros
          TYPE      REPLYD                  * is input numeric ?
          GOTO      KEYI2000 IF EQUAL       * yes
.
          REP       "N1P2A3F4",REPLYD       * replace alpha input with number
          TYPE      REPLYD                  * is input numeric now ?
          GOTO      KEYI1000 IF EQUAL       * yes - valid
          BEEP                              * no - invalid.  Beep and re-enter
          GOTO      KEYI0000
.
KEYI1000  MOVE      REPLYD,REPLYF           * move DIM input to FORM field
          MOVE      ONE,EXIT                * set EXIT = 1
          STORE     DISPHALF,REPLYF,RDSPHALF  * Next, set rdsphalf to disphalf
          GOTO      KEYI9999
.
. ------  numeric input  ------
.
KEYI2000  MOVE      REPLYD,REPLYF           * move DIM input to FORM field
          COMPARE   ONE,REPLYF              * input > or = 1 ?
          GOTO      KEYI2100 IF LESS        * no - invalid
.
          COMPARE   REPLYF,LINE             * input > display line number ?
          GOTO      KEYI2100 IF LESS        * yes - invalid
          MOVE      ZERO,EXIT               * no - set EXIT = 0
          GOTO      KEYI9999
.
KEYI2100  BEEP
          MOVE      TWO,EXIT                * invalid numeric input-set EXIT = 2
          GOTO      KEYI9999
.
. ------  null input  ------
.
KEYI9000  MOVE      THREE,EXIT              * set EXIT = 3
.
KEYI9999  RETURN
+
.
.
          INC       STD001IO
          INC       CALCAGE
          INC       PATALERT
          INC       PATALRIO/INC
          INC       PATCODKY/PBL            * ? for codes file
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PATDSCIO/INC
          INC       PATLOCIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATNOBIO/INC
          INC       PATNURIO/INC
          INC       PATTRNIO/INC
          INC       PATVISIO/INC
          INC       PATWR1IO/INC
          INC       PATNIDIO/INC
          INC       PATTERIO/INC
          INC       PCPCPLAN
          INC       PCPDIAIO/INC
          INC       PCPNURIO/INC
          INC       PMIHEAD
...............................................................................
