.***************************************************************************
.* System    :   Theatre System                                            *
.* Program   :   FXOPRDEA.PBL                                              *
.* Desc      :   Program to update oprdetaf.opdathet with oprsesaf.opsethet*
.***************************************************************************
.* Date      :   31/03/2005                                                *
.* Author    :   J.Tan                                                     *
.* Function  :   This program will loop thru the oprdetaf files and        *
.*               calls the procedure to update the theatre operation room  * 
.*=========================================================================*
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       OPRDEAFD/INC   * Operation detail file
          INC       OPRSESFD/INC   * Session file
.
RECNUM    FORM      6
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
.----------------------------------------------------------------------
PRGID     INIT      "FXOPRDEA"
PRGNAM    INIT      "Fixing Operating Room Code"
VERSION   INIT      "V11.03.00"
.----------------------------------------------------------------------
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD         * display heading
          OPEN      OPRDETA3,"oprdetaf"
          OPEN      OPRSESA1,"oprsesaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Fix Operating Room Code" 
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Fixing ..."
          MOVE      ZERO,RECNUM
          PACK      KEY10,SP70
          CALL      RSOPDEA3
PROC1000  CALL      RKOPDEA3
          BRANCH    OVRCD,PROC9999
          MATCH     SP70,OPDATHET           * Blank operating code?
          GOTO      PROC1000 IF NOT EQUAL
.
MMMM
          PACK      KEY19,OPDADATE,OPDATIME,OPDACLIN
          CALL      RDOPSES1
          BRANCH    OVRCD,PROC1000
          MOVE      OPSETHET,OPDATHET
          CALL      UPOPDEA3
.
          ADD       ONE,RECNUM
          IF        RECNUM%100=0
            DISPLAY      *P40:24,*V2LON,RECNUM
          ENDIF
          GOTO      PROC1000
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       OPRDEAIO/INC   * Operation detail file
          INC       OPRSESIO/INC   * Session file

          INC       STD001IO
.
