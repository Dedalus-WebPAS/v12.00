.******************************************************************************
.* System    :   Receipting                                                   *
.* Program   :   VALRCP78                                                     *
.* Desc      :   Deposit Upload from a pipe delimited file                    *
.******************************************************************************
.* Date      :   28/09/2017                                                   *
.* Author    :   J.Tan                                                        *
.* Mods      :                                                                *
.*        V10.13.01 26/07/2018  J.Tan            TSK 0848506                  *
.*                  Changed PP Doctor from DIM6 to DIM10                      *
.******************************************************************************
+
          INC       STD001FD            * command line
.         INC       STD002FD            * command line
.kkkk
          INC       CHKDIGVR/INC
.
          INC       PATCONFD/INC
          INC       RCPCONFD/INC
.
          INC       COMPOLFD/INC
          INC       RCPDSTFD/INC
          INC       RCPREGFD/INC
          INC       RCPBNKFD/INC
          INC       RCPBDTFD/INC
          INC       RCPDTEFD/INC
          INC       RCPCIDFD/INC
          INC       RCPCMNFD/INC
.
          INC       NHIETHFD/INC
          INC       NHIMASFD/INC
.
          INC       PATINVFD/INC
          INC       PATRE1FD/INC
          INC       PATVISFD/INC
          INC       PATDPTHV/INC
          INC       PATCODFD/INC
          INC       PATHSPFD/INC
          INC       PATHOSFD/INC
          INC       PATMA1FD/INC
          INC       PMSVX1FD/INC
          INC       PATMI1FD/INC
          INC       PATNIDFD/INC
          INC       PMSPX2FD/INC
          INC       PMSCEXFD/INC
          INC       PATFN1FD/INC
          INC       PATIN1FD/INC
          INC       PATGO1FD/INC
          INC       PMSHCPFD/INC
.
          INC       PRIHDBFD/INC
          INC       PRIINVFD/INC
          INC       PRIPRAFD/INC  
          INC       PRIHREFD/INC
.
          INC       FMSCHQFD/INC
          INC       DEBDBTFD/INC
          INC       DEBFDTFD/INC
          INC       DEBFTHFD/INC
          INC       DEBITMFD/INC
.
          INC       IBAPRNFD/INC
          INC       IBATMDFD/INC            * Template Tail
          INC       IBATMHFD/INC            * Template Head
          INC       WEBSECFD/INC
.
. ----- Input Text File Definitions -----
.
DUPLDTXT  FILE       HL7, FIXED=1000        * Upload file for Deposits
.
.Name     Type     Size     Start
.----     ----     ----     -----
XHOSID    DIM         3     Hospital Id
XURNUM    DIM         8     U/R Number
XVSNUM    DIM         8     Visit Number
XSFRID    DIM        18     SalesForce ID
XDAMNT    FORM       12.2   Deposit Amount
XDRAWR    DIM         6     Cash Drawer
XPASSW    DIM         6     Password
XCHQNO    DIM        15     Cheque Account
XUSRID    DIM        10     User ID
XREGIS    DIM         3     Register
XPAYMC    DIM         3     Payment Type (Cat Py)
.End of Record
.
. ----- Program Variables -----
.
XFLD0001  DIM       30
XFLD0002  DIM       30
XFLD0003  DIM       30
XFLD0004  DIM       30
XFLD0005  DIM       30
XFLD0006  DIM       30
XFLD0007  DIM       30
XFLD0008  DIM       30
XFLD0009  DIM       30
XFLD0010  DIM       30
XFLD0011  DIM       30
.
. Program Constants
. *****************
.
CODEIR    INIT      "I/R"
CODEUR    INIT      "U/R"
CODEPP    INIT      "P/P"
HFHEADER  INIT      "0000    "
.
. ----- Program Variables -----
.
AECNAERH  DIM       6          * AAE Receipt Header Code (templated)
AECNAERT  DIM       6          * AAE Receipt Tail Code (templated)
.
CMDLINE   DIM       50
CANCLMSG  DIM       35
CURDATE   DIM       8
CURTIME   DIM       8
ERRFIELD  DIM       15
ERRDATA   DIM       80
ERRFLAG   FORM      1
ERRCOUNT  FORM      2
FFILENAM  DIM       160
FORM3     FORM      3
FORM8     FORM      8
FORM12    FORM      12
FORM8P2   FORM      8.2
IBCNMHOS  FORM      1
.kkk
INVOICEN  DIM       12
RECEIPTN  DIM       12
REFERENC  DIM       20
HEADTAIL  FORM      1
.
TASKTYPE  DIM       3
TRANNUMB  FORM      3
UNIQID    DIM       20
XMLPATHN  DIM       60
XMLFLNAM  DIM       60
MRCNNOHS  FORM      2
MAXCLNO   FORM      2
MPGEFLAG  FORM      1
REF10     DIM       10
SHREGIS   DIM       26                      * short register decsription
SPRAFLAG  FORM      1
.
CURRROW   FORM      3
CRCPDATE  DIM       8
TMPLTFLG  FORM      1
TAILLENG  FORM      3
.
PATPROVD  DIM       20
PRFANAME  DIM       30
PRFAADD1  DIM       35
PRFAADD2  DIM       35
PRFAADD3  DIM       35
PRFAADD4  DIM       35
PRFAPOST  DIM       8
.
PPRAADD1  DIM       30
PPRAADD2  DIM       30
PPRASUBR  DIM       30
PPRAPOST  DIM       4
PRNTADD1  DIM       35
PRNTADD2  DIM       35
PRNTSUBR  DIM       35
PRNTADD4  DIM       35
PRNTPOST  DIM       4
PRNTNAME  DIM       30
PRNTRNAM  DIM       30
PRNTPNAM  DIM       30
PRNTPNUM  DIM       8
PRNTSTRT  FORM      3
PRNTPTYP  DIM       15
PRNTCTYP  DIM       3
XXDPATH   DIM       120
DIM5      DIM       5
DIM8      DIM       8
DIM11     DIM       11
DIM12     DIM       12
DIM40     DIM       40
FORM5     FORM      5
NMPNUMB   DIM       20
PATNAME   DIM       30                * formatted patient name
.
VARIABLE  DIM       127
WEBTITLE  DIM       80
.
. ----- Program Constants -----
.
PRGID     INIT      "VALRCP78"
PRGNAM    INIT      "Deposit Upload"
VERSION   INIT      "V10.14.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
MAIN0000  CALL      INIT0000                * Initialise variables & open files
          BRANCH    EXIT,MAIN9999
.
MAIN0500  CALL      KTYP0000               * Type of upload
          BRANCH    EXIT,MAIN9999          * nothing input
.
          CALL      KUID0000               * Keyin Uniq id (Date/time)
          BRANCH    EXIT,MAIN9999          * nothing input
.
          CALL      GFIL0000               * get filename
          BRANCH    EXIT,MAIN9999          * nothing input
.
          CALL      OPEN0000               * open text file
          BRANCH    EXIT,MAIN9999          * unable to open .txt file
.
          CALL      PROC0000               * process
.
MAIN9999  
          CHAIN     PGM
.kkkk
.         STOP
+
.******************************************************************************
.*                                  INIT0000                                  *
.*                      Initialise Variables & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
.
          OPEN      COMPOLA1,"compolaf"
          OPEN      COMPOLA2,"compolaf"
.
          OPEN      RCPCMNA1,"rcpcmnaf"
          OPEN      RCPDSTA1,"rcpdstaf"
          OPEN      RCPREGA1,"rcpregaf"
          OPEN      RCPBNKA1,"rcpbnkaf"
          OPEN      RCPBDTA1,"rcpbdtaf"
          OPEN      RCPDTEA1,"rcpdteaf"
          OPEN      RCPCIDA1,"rcpcidaf"
.
          OPEN      FMSCHQA1,"fmschqaf"
.
          OPEN      PATCODE1,"patcodes"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PATHOSP1,"pathospf"
          OPEN      PATHOSP2,"pathospf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      PATFN1A1,"patfn1af"
          OPEN      PATIN1A1,"patin1af"
          OPEN      PATGO1A1,"patgo1af"
          OPEN      PMSHCPA1,"pmshcpaf"
.
          OPEN      PATINVR1,"patinvrf"
          OPEN      PATINVR3,"patinvrf"
          OPEN      PATRE1A1,"patre1af"
.
          OPEN      PRIHDBT1,"prihdbtf"
          OPEN      PRIINVO1,"priinvof"
          OPEN      PRIPRAC1,"pripracf"
          OPEN      PRIHREF1,"prihreff"
.
          OPEN      WEBSECA1,"websecaf"
.
.         Open files for new account receivable
.
          IF        HSYS6=1
            OPEN      DEBDBTA1,"debdbtaf"
            OPEN      DEBFTHA1,"debfthaf"
            OPEN      DEBFTHA6,"debfthaf"
            OPEN      DEBFTHA7,"debfthaf"
            OPEN      DEBFDTA3,"debfdtaf"
            OPEN      DEBFDTA6,"debfdtaf"
            OPEN      DEBFDTA7,"debfdtaf"
            OPEN      DEBITMA1,"debitmaf"
          ENDIF
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*244,CHOSPNUM
          CALL      PATDPATH
.
          READ      CONTROLF,THIRTY6;*205,RCCNRCHD,RCCNRCTL
.
          OPEN      IBATMHA1,"ibatmhaf"
          OPEN      IBATDET1,"ibatmdaf"
.
          PACK      CRCPDATE,CCC,CYY,CMM,CDD,SP70
.
          READ      CONTROLF,THIRTY6;*219,RCCNRCLN
          LOAD      MAXCLNO,HFORMAT,RCCNRCLN,RCCNRCLN,ZERO,ZERO,ZERO:
                                    ZERO,ZERO,THIRTY1,ZERO
.
INIT9000  MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9500  MOVE      ONE,EXIT
INIT9999  RETURN
+
.*******************************************************************************
.*                          KTYP0000                 Called by: MAIN0000       *
.*                        Type of Task                                         *
.*******************************************************************************
KTYP0000  MOVE      ZERO,EXIT
          KEYIN     *P1:12,*EL,"Type of Task                : ":
                    *P28:12,TASKTYPE
          ENDSET    TASKTYPE
          APPEND    SP70,TASKTYPE
          RESET     TASKTYPE
.
          MATCH     SP70,TASKTYPE
          GOTO      KTYP9999 IF NOT EQUAL
          MOVE      ONE,EXIT
.
KTYP9999  RETURN
+
.*******************************************************************************
.*                          KUID0000                 Called by: MAIN0000       *
.*                 Keyin Unique ID                                             *
.*******************************************************************************
KUID0000  MOVE      ZERO,EXIT
          MOVE      SP70,UNIQID
          KEYIN     *P1:14,*EL,"Enter Unique ID (Date/Time) : ":
                    *P28:14,UNIQID
.
          ENDSET    UNIQID
          APPEND    SP70,UNIQID
          RESET     UNIQID
.
          MATCH     SP70,UNIQID
          GOTO      KUID9999 IF NOT EQUAL
          MOVE      ONE,EXIT
.
KUID9999  RETURN
+
.*******************************************************************************
.*                          GFIL0000                 Called by: MAIN0000       *
.* Keyin the eAdmission XML file name                                          *
.*******************************************************************************
.
GFIL0000  KEYIN     *P1:16,*EL,"Path name: ":
                    *P28:16,XMLPATHN
.
          PACK      XMLPATHN,XMLPATHN,SP30
          MATCH     SP30,XMLPATHN
          GOTO      GFIL9100 IF EQUAL
.
GFIL1000  KEYIN     *P1:17,*EL,"Filename : ":
                    *P28:17,XMLFLNAM
.
          PACK      XMLFLNAM,XMLFLNAM,SP30
          MATCH     SP30,XMLFLNAM
          GOTO      GFIL9100 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      GFIL9999
.
GFIL9100  MOVE      ONE,EXIT
.
GFIL9999  RETURN
+
.*******************************************************************************
.*                               OPEN0000                Called by: MAIN0000   *
.* Open the Deposit file for processing                                        *
.*******************************************************************************
OPEN0000  CALL      IBACLOCK
          PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE             * set current date
          MOVE      CTIMEIS,CURTIME
          REP       " 0",CURTIME             * set current time
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
.
          STRIP     XMLPATHN
          CLEAR     FFILENAM
          APPEND    XMLPATHN,FFILENAM
          APPEND    "/",FFILENAM
          APPEND    XMLFLNAM,FFILENAM
          APPEND    SP70,FFILENAM
          APPEND    SP70,FFILENAM
          RESET     FFILENAM
.
          OPEN      DUPLDTXT,FFILENAM
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"File not found.  ";
            CALL      HITENTER
            GOTO      OPEN9100
          ENDIF
.
          MOVE      "0",KEY1               * Waiting
.         CALL      CPOL0000               * Write to Polling table
.
          MOVE      ZERO,EXIT
          GOTO      OPEN9999
.
OPEN9100  MOVE      "2",KEY1               * Error
.         CALL      CPOL0000               * Write to Polling table
.
          MOVE      ONE,EXIT
          GOTO      OPEN9999
.
OPEN9999  RETURN
+
.*******************************************************************************
.*                               PROC0000                Called by: MAIN0000   *
.* Loop through the eAdmission temporary files and store data in tables        *
.*******************************************************************************
PROC0000  MOVE      SP70,XFLD0001
          MOVE      SP70,XFLD0002
          MOVE      SP70,XFLD0003
          MOVE      SP70,XFLD0004
          MOVE      SP70,XFLD0005
          MOVE      SP70,XFLD0006
          MOVE      SP70,XFLD0007
          MOVE      SP70,XFLD0008
          MOVE      SP70,XFLD0009
          MOVE      SP70,XFLD0010
          MOVE      SP70,XFLD0011
.
          READ      DUPLDTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008,XFLD0009,XFLD0010:
                                 XFLD0011
          GOTO      PROC9000 IF OVER
.
          PACK      XHOSID,XFLD0001,SP70
.
          MOVE      ZERO,FORM8
          SQUEEZE   XFLD0002
          MOVE      XFLD0002,FORM8
          MOVE      FORM8,XURNUM              * U/R number
.
          MOVE      ZERO,FORM8
          SQUEEZE   XFLD0003
          MOVE      XFLD0003,FORM8
          MOVE      FORM8,XVSNUM              * visit number
.
          PACK      XSFRID,XFLD0004,SP70
          SQUEEZE   XFLD0004                  * salesforce
.
          SQUEEZE   XFLD0005
          MOVE      XFLD0005,XDAMNT           * amount
.
          PACK      XDRAWR,XFLD0006,SP70
          PACK      XPASSW,XFLD0007,SP70
          PACK      XCHQNO,XFLD0008,SP70
          PACK      XUSRID,XFLD0009,SP70
          PACK      XREGIS,XFLD0010,SP70
          PACK      XPAYMC,XFLD0011,SP70
.
          MOVE      ZERO,ERRFLAG
          MOVE      ZERO,ERRCOUNT
          CALL      VALD0000                * validating fields
          BRANCH    ERRFLAG,PROC9100
.
          UNPACK    SP70,ERRFIELD,ERRDATA
          MOVE      "No Error found.",ERRDATA
          CALL      PERR0000
.
          PRINT     *N,*1,"Upload File : ",XMLFLNAM
.
          MOVE      ZERO,ERRCOUNT
          MOVE      ZERO,ERRFLAG
.
          MOVE      "processed",KEY10
          CALL      MFIL0000               * moving file
.
.         No error - ok to process
.
.         MOVE      "1",KEY1               * Processing
.         CALL      UDST0000               * Update deposit status file
.
.         CALL      WBNK0000               *  write to bank header file
.         CALL      WBDT0000               *  write to bank details file
.         CALL      WDTE0000               *  write to receipt details file
.
.         MOVE      "4",KEY1               * Processed
.         CALL      UDST0000               * Update to Deposit status table
.
.         MOVE      "1",KEY1               * Processed
.         CALL      CPOL0000               * Update status of Polling table
.
.         CALL      PRCP0000               * Print receipt
.kkkk
.
          GOTO      PROC9999
.
PROC9000  MOVE      "3",KEY1               * Error-Upload
.         CALL      UDST0000               * Update deposit status file
.
          MOVE      "errors",KEY10
          CALL      MFIL0000               * moving file
.
PROC9100  MOVE      "2",KEY1               * Error-validation
.         CALL      UDST0000               * Update to Deposit status table
.
          MOVE      "errors",KEY10
          CALL      MFIL0000               * moving file
.
          PRINT     *N,*1,"Upload File : ",FFILENAM:
                    *N,*N,*1,"Deposit can not be Processed - Errors: ",ERRCOUNT
.
          GOTO      PROC9999
.
PROC9999  RETURN
+
.----------------------------------------------------------------------
.         Write a record to Deposit polling table
.----------------------------------------------------------------------
CPOL0000  MOVE      "001",CMPLTYPE               * Deposit upload
          MOVE      UNIQID,CMPLUNIQ
          PACK      KEY23,CMPLTYPE,UNIQID,SP70
          CALL      RDCMPOL2
          BRANCH    OVRCD,CPOL1000
.
          MOVE      KEY1,CMPLSTAT                * Status
          CALL      UPCMPOL2
          GOTO      CPOL9999
.
CPOL1000  UNPACK    KEY23,CMPLTYPE,CMPLUNIQ
          MOVE      KEY1,CMPLSTAT                * Status
          MOVE      FFILENAM,CMPLFILN            * File name
          PACK      CMPLSPAR,SP70,SP70
          CALL      WRCMPOL2
.
CPOL9999  RETURN
_
.----------------------------------------------------------------------
.         set fields for Deposit status table
.----------------------------------------------------------------------
UDST0000  PACK      KEY20,CMPLUNIQ,SP70
          CALL      RDRCDST1
          BRANCH    OVRCD,UDST2000
.
          MOVE      KEY1,RCDSDSTA                * Processing status
          CALL      UPRCDST1
          GOTO      UDST9999
.
UDST2000  MOVE      CMPLUNIQ,RCDSUNIQ            * Unique ID
          MOVE      XURNUM,RCDSURNO              * UR
          MOVE      XVSNUM,RCDSVISN              * visit
          MOVE      XSFRID,RCDSSFID              * salesforce id
          MOVE      KEY1,RCDSDSTA                 * Processing status
          MOVE      XDAMNT,RCDSDAMN              * Deposit amount
.
          MOVE      CURDATE,RCDSUDAT
          MOVE      CURTIME,RCDSUTIM
          MOVE      XUSRID,RCDSUWID              * User ID
          PACK      RCDSSPAR,SP70,SP70
.
          CALL      WRRCDST1                     * Write a record
.
UDST9999  RETURN
+
.------------------------------------------------------------------------------
. Validate record
.------------------------------------------------------------------------------
VALD0000  MOVE      "60",CLNO
.
          MATCH     SP70,XHOSID
          IF        !@EQUAL
            MOVE      XHOSID,KEY3
            CALL      RDPTHSP1
            IF        OVRCD=1
              PACK      ERRFIELD,XHOSID,SP70
              PACK      ERRDATA,SP70,SP70
              MOVE      "Invalid Hospital ID.",ERRDATA
              CALL      PERR0000
            ENDIF
          ENDIF
.
          MOVE      XURNUM,KEY8
          CALL      RDMAST1
          IF        OVRCD=1
            PACK      ERRFIELD,XURNUM,SP70
            PACK      ERRDATA,SP70,SP70
            MOVE      "Invalid U/R No.",ERRDATA
            CALL      PERR0000
          ENDIF
.
          MOVE      XVSNUM,KEY8
          CALL      RDMISS1
          IF        OVRCD=1
            PACK      ERRFIELD,XVSNUM,SP70
            PACK      ERRDATA,SP70,SP70
            MOVE      "Invalid Visit No.",ERRDATA
            CALL      PERR0000
          ENDIF
.
          MOVE      XDRAWR,KEY6
          CALL      RDCIDA1
          IF        OVRCD=1
            PACK      ERRFIELD,XDRAWR,SP70
            PACK      ERRDATA,SP70,SP70
            MOVE      "Invalid Drawer",ERRDATA
            CALL      PERR0000
          ELSE
            PACK      RCPPASS,RCPPASS,SP70
            MATCH     XPASSW,RCPPASS
            IF        !@EQUAL
              PACK      ERRFIELD,XPASSW,SP70
              PACK      ERRDATA,SP70,SP70
              MOVE      "Invalid Drawer Password",ERRDATA
            CALL      PERR0000
            ENDIF
          ENDIF
.
          MOVE       XCHQNO,KEY15
          CALL       RDFMCH1
          IF         OVRCD=1
            PACK      ERRFIELD,XCHQNO,SP70
            PACK      ERRDATA,SP70,SP70
            MOVE      "Invalid Cheque Account",ERRDATA
            CALL      PERR0000
          ENDIF
.
          MOVE       XUSRID,KEY10
          CALL       RDWBSE1
          IF         OVRCD=1
            PACK      ERRFIELD,XUSRID,SP70
            PACK      ERRDATA,SP70,SP70
            MOVE      "Invalid User Id",ERRDATA
            CALL      PERR0000
          ENDIF
.
          MOVE       XREGIS,KEY3
          CALL       RDREGA1
          IF         OVRCD=1
            PACK      ERRFIELD,XREGIS,SP70
            PACK      ERRDATA,SP70,SP70
            MOVE      "Invalid Register",ERRDATA
            CALL      PERR0000
          ENDIF
.
          MOVE       "Py",KEY2
          MOVE       XPAYMC,KEY3
          PACK       KEY5,KEY2,KEY3
          CALL       RDCODE1
          IF         OVRCD=1
            PACK      ERRFIELD,XPAYMC,SP70
            PACK      ERRDATA,SP70,SP70
            MOVE      "Invalid Payment Type",ERRDATA
            CALL      PERR0000
          ENDIF
.
          MOVE      HRECPT,RECEIPTN
          CALL      CTMPL000               * Check Template Header/Tail
.
VALD9999  RETURN
+
.------------------------------------------------------------------------------
.         Write to rcpbnkaf
.------------------------------------------------------------------------------
WBNK0000  READ      CONTROLF,THIRTY6;*193,HRECPT
          ADD       ONE,HRECPT
          WRITAB    CONTROLF,THIRTY6;*193,HRECPT
          SUB       ONE,HRECPT
.
          MOVE      HRECPT,KEY12
          CALL      RDRCBNK1
          COMPARE   ZERO,OVRCD
          GOTO      WBNK0000 IF EQUAL
.
          MOVE      HRECPT,RCBNRECN
          MOVE      CURDATE,RCBNTDAT
          MOVE      XDAMNT,RCBNTOTP
          MOVE      XDRAWR,RCBNCDRW
          UNPACK    SP70,RCBNMHOS,RCBNMDHS
          IF        IBCNMHOS=1
            MOVE      XHOSID,RCBNMHOS
            MOVE      CHOSPNUM,RCBNMDHS
          ENDIF
          MOVE      "0",RCBNTTYP               * From patient
          MOVE      SP70,RCBNRCOD
          MOVE      ZERO,RCBNSTAT
          MOVE      SP70,RCBNBDAT
          MOVE      SP70,RCBNBTIM
          MOVE      SP70,RCBNRDAT
          MOVE      SP70,RCBNRTIM
          MOVE      XCHQNO,RCBNCHQA
          MOVE      CURDATE,RCBNCDAT
          MOVE      CURTIME,RCBNCTIM
          MOVE      XUSRID,RCBNCUID
          MOVE      SP70,RCBNUDAT
          MOVE      SP70,RCBNUTIM
          MOVE      SP70,RCBNUUID
          PACK      RCBNSPAR,SP70
.
          MOVE      HRECPT,KEY12
          CALL      WRRCBNK1
WBNK9999  RETURN
+
.------------------------------------------------------------------------------
.         Write to rcpbdtaf
.------------------------------------------------------------------------------
WBDT0000  PACK      KEY15,RCBNRECN,Z70
          CALL      RSRCBDT1
          CALL      RPRCBDT1
          BRANCH    OVRCD,WBDT1000
.
          MATCH     RCBDRECN,RCBNRECN
          GOTO      WBDT1000 IF NOT EQUAL
.
          MOVE      RCBDUNIQ,TRANNUMB     * additional bank detail record
          ADD       ONE,TRANNUMB
          GOTO      WBDT2000
.
WBDT1000  MOVE      ONE,TRANNUMB          * first bank detail record
.
WBDT2000  MOVE      CURDATE,RCBDTDAT
          MOVE      RCBNRECN,RCBDRECN
          MOVE      TRANNUMB,RCBDUNIQ
          UNPACK    SP70,RCBNMHOS,RCBNMDHS
          IF        IBCNMHOS=1
            MOVE      XHOSID,RCBDMHOS
            MOVE      CHOSPNUM,RCBDMDHS
          ENDIF
.
          MOVE      "1",RCBDPTYP          * Default to Cash
          MOVE      "Py",KEY2
          MOVE      XPAYMC,RCBDPCOD
          PACK      KEY5,KEY2,RCBDPCOD
          CALL      RDCODE1
          IF        OVRCD=0
            MATCH     SP70,PTCDNHCP
            IF        !@EQUAL
              UNPACK    PTCDNHCP,RCBDPTYP   * payment type
            ENDIF
          ENDIF
          MOVE      XDAMNT,RCBDPAMT
          MOVE      SP70,RCBDPAYD
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,RCBDPAYD
.
          MOVE      XCHQNO,RCBDCHQN
          MOVE      XDRAWR,RCBDDRAW
          UNPACK    SP70,RCBDBANK,RCBDBRCH
          UNPACK    SP70,RCBDCRDT,RCBDSTRF,RCBDEFTT
          MOVE      XSFRID,RCBDSTRF         * SalesForce ID
          PACK      RCBDCDAT,CCC,CYY,CMM,CDD
          REP       " 0",RCBDCDAT
          CLOCK     TIME,RCBDCTIM
          MOVE      XUSRID,RCBDCUID
          UNPACK    SP70,RCBDUDAT,RCBDUTIM,RCBDUUID
.
          PACK      KEY15,RCBDRECN,RCBDUNIQ,SP70
          CALL      WRRCBDT1
WBDT9999  RETURN
+
.------------------------------------------------------------------------------
.         Write to rcpdteaf
.------------------------------------------------------------------------------
WDTE0000  MOVE      HRECPT,KEY12
          PACK      KEY15,KEY12,Z70
          CALL      RSRCDTE1
          CALL      RPRCDTE1
          BRANCH    OVRCD,WDTE1000
.
          MATCH     RCDTRECN,KEY12
          GOTO      WDTE1000 IF NOT EQUAL
.
          MOVE      RCDTTCNT,TRANNUMB     * additional detail record
          ADD       ONE,TRANNUMB
          GOTO      WDTE2000
.
WDTE1000  MOVE      ONE,TRANNUMB          * first detail record
.
WDTE2000  MOVE      CURDATE,RCDTTDAT
          MOVE      KEY12,RCDTRECN        * Receipt number
          MOVE      TRANNUMB,RCDTTCNT
          MOVE      XREGIS,RCDTREGI
          MOVE      SP70,RCDTINVN
          MOVE      XVSNUM,RCDTVIST
          MOVE      XURNUM,RCDTURNO
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,RCDTNAME
          MOVE      PADD1,RCDTADD1
          MOVE      PADD2,RCDTADD2
          MOVE      PSUBRB,RCDTADD3
          MOVE      PADD4,RCDTADD4
          MOVE      PPOST,RCDTPOST
          MOVE      SP70,RCDTSFLG
          MOVE      SP70,RCDTREFR
          MOVE      PACFNAME,RCDTREFR
          UNPACK    SP70,RCDTINCA,RCDTBNKA
          CALL      GTACCT00              * get income and control accounts
          MOVE      XDAMNT,RCDTAMNT
          MOVE      ZERO,RCDTDISC
          UNPACK    SP70,RCDTMHOS,RCDTMDHS
          IF        IBCNMHOS=1
            MOVE      XHOSID,RCDTMHOS
            MOVE      CHOSPNUM,RCDTMDHS
          ENDIF
          UNPACK    SP70,RCDTGSTC,RCDTGSTA,RCDTALLO
          UNPACK    SP70,RCDTRELD,RCDTRELT,RCDTPRAC
.
          MOVE      SP70,RCDTDPTY                * Deposit type
          MOVE      XDRAWR,KEY6
          CALL      RDCIDA1
          IF        OVRCD=0
            MOVE      RCCIDEPR,RCDTDPTY
          ENDIF
.
          MOVE      CURDATE,RCDTCDAT
          MOVE      CURTIME,RCDTCTIM
          MOVE      XUSRID,RCDTCUID
          UNPACK    SP70,RCDTUDAT,RCDTUTIM,RCDTUUID,RCDTGLEX,RCDTBATN
          UNPACK    SP70,RCDTRELU,RCDTSPUI,RCDTSPDT,RCDTSPTM,RCDTSDOC
          UNPACK    SP70,RCDTSPAR
.
          PACK      KEY15,RCDTRECN,TRANNUMB,SP70
          CALL      WRRCDTE1
.
WDTE9999  RETURN
+
.------------------------------------------------------------------------------
.  Get Income account and Control Account
.------------------------------------------------------------------------------
GTACCT00  PACK      KEY3,XREGIS
          CALL      RDREGA1
          BRANCH    OVRCD,GTACCT99
.
.         Get Credit and Bank Account from Register record
.
          MOVE      REGGENDB,RCDTINCA       * Debtors Control account as Crd.Acc
          MOVE      REGGENBK,RCDTBNKA       * Debit account
.
GTACCT99 RETURN
+
.******************************************************************************
.*                                  HEAD0000                                  *
.*                          Print error Report Header                         *
.******************************************************************************
HEAD0000  MOVE      ONE,CNOUNDLN
          MOVE      "- Error report",CPHDROPT
          CALL      HEAD132                 * Print the report header
.
          PRINT     *1,"Visit No.",*12,"U/R No.":
                    *25,"Field",*42,"Error",*N:
                    *1,"---------",*12,"--------":
                    *25,"----------":
                    *42,"----------------------------------------------"
          MOVE      ZERO,CLNO
HEAD9999  RETURN
+
.******************************************************************************
.*                                  PERR0000                                  *
.*                            Print An Error Report                           *
.******************************************************************************
PERR0000  
          COMPARE   "56",CLNO
          CALL      HEAD0000 IF NOT LESS    * Print the report header
.
          IF        ERRCOUNT=0
            PRINT     *1,XVSNUM,*12,XURNUM;
          ENDIF
.
          RESET     ERRDATA
          PRINT     *25,ERRFIELD,*42,ERRDATA
.
          ADD       ONE,ERRCOUNT
          ADD       ONE,CLNO
          PACK      ERRDATA,SP70
          MOVE      ONE,ERRFLAG
.
PERR9999  RETURN
+
.******************************************************************************
.*                                  MFIL0000                                  *
.*                            Moving file                                     *
.******************************************************************************
MFIL0000  STRIP     KEY10
          STRIP     XMLPATHN
          STRIP     XMLFLNAM
.
          CLEAR     CMDLINE
          APPEND    "mv ",CMDLINE
          APPEND    XMLPATHN,CMDLINE
          APPEND    "/",CMDLINE
          APPEND    XMLFLNAM,CMDLINE
          APPEND    SP1,CMDLINE
.
          APPEND    XMLPATHN,CMDLINE
          APPEND    "/",CMDLINE
          APPEND    KEY10,CMDLINE
          APPEND    "/",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
.
MFIL9999  RETURN
+
.******************************************************************************
.*                                  PRCP0000                                  *
.*                            Print Receipt                                   *
.******************************************************************************
PRCP0000
          MOVE      HRECPT,RECEIPTN
          CALL      OPNPRINT
          CALL      PRTN0000               * Print Receipt
          CALL      CLSPRINT
.
PRCP9999  RETURN
+
.******************************************************************************
.*                                  CTMPL000                                  *
.*                      Check User defined Stationery Header/Tail             *
.******************************************************************************
CTMPL000  MOVE      ONE,TMPLTFLG
          MOVE      ZERO,TAILLENG
.
          READ      CONTROLF,THIRTY6;*205,RCCNRCHD,RCCNRCTL
          READ      CONTROLF,SEVENTY8;*52,AECNAERH,*58,AECNAERT
.
          CALL      CSYS0000              * check if this is an ED receipt
          BRANCH    EXIT,CTMPL020         * first record is not for EMR visit
.
          MATCH     "000000",AECNAERH     * Emergency Receipt header
          GOTO      CTMPL020 IF EQUAL
          MATCH     SP70,AECNAERH         * Emergency Receipt header
          GOTO      CTMPL020 IF EQUAL
.
          MATCH     "000000",AECNAERT     * Emergency Receipt tail
          GOTO      CTMPL020 IF EQUAL
          MATCH     SP70,AECNAERT         * Emergency Receipt tail
          GOTO      CTMPL020 IF EQUAL
.
          MOVE      AECNAERH,RCCNRCHD
          MOVE      AECNAERT,RCCNRCTL
          GOTO      CTMPL100
.
CTMPL020  MATCH     "000000",RCCNRCHD
          GOTO      CTMPL050 IF EQUAL
.
          MATCH     SP6,RCCNRCHD
          GOTO      CTMPL100 IF NOT EQUAL
.
CTMPL050  MATCH     "000000",RCCNRCTL
          GOTO      CTMPL999 IF EQUAL
.
          MATCH     SP6,RCCNRCTL
          GOTO      CTMPL999 IF EQUAL
.
CTMPL100  PACK      KEY6,RCCNRCHD,SP70
          CALL      RDIBTH1               * read header detail file
          BRANCH    OVRCD,CTMPL900
.
          PACK      KEY6,RCCNRCTL,SP70
          CALL      RDIBTH1               * read header detail file
          BRANCH    OVRCD,CTMPL910
.
          MOVE      IBTHLENG,TAILLENG
.
          GOTO      CTMPL990
.
.------ stationery code not on file ------
.
CTMPL900  PACK      ERRFIELD,RCCNRCHD,SP70
          PACK      ERRDATA,SP70,SP70
          MOVE      "User Defined Stationery Header Code not on File. ",ERRDATA
          CALL      PERR0000
          GOTO      CTMPL999
.
CTMPL910  PACK      ERRFIELD,RCCNRCTL,SP70
          PACK      ERRDATA,SP70,SP70
          MOVE      "User Defined Stationery Tail Code not on File. - ",ERRDATA
          CALL      PERR0000
          GOTO      CTMPL999
.
CTMPL990  MOVE      ZERO,TMPLTFLG
.
CTMPL999  RETURN
+
.*******************************************************************************
.* I/O Includes                                                                *
.*******************************************************************************
          INC       STD001IO
.         INC       STD002IO
.kkkk
          INC       RCPPRTCD
          INC       PMIGTNID
          INC       PATDPATH
          INC       IBAPRINT
          INC       GETALTID
.
          INC       COMPOLIO/INC
          INC       RCPDSTIO/INC
          INC       RCPREGIO/INC
          INC       RCPBNKIO/INC
          INC       RCPBDTIO/INC
          INC       RCPDTEIO/INC
          INC       RCPCIDIO/INC
          INC       RCPCMNIO/INC
.
          INC       NHIETHIO/INC
          INC       NHIMASIO/INC
.
          INC       PATINVIO/INC
          INC       PATRE1IO/INC
          INC       PATVISIO/INC
          INC       PATCODIO/INC
          INC       PATHSPIO/INC
          INC       PATHOSIO/INC
          INC       PATMA1IO/INC
          INC       PMSVX1IO/INC
          INC       PATMI1IO/INC
          INC       PATNIDIO/INC
          INC       PMSPX2IO/INC
          INC       PMSCEXIO/INC
          INC       PATFN1IO/INC
          INC       PATIN1IO/INC
          INC       PATGO1IO/INC
          INC       PMSHCPIO/INC
.
          INC       PRIHDBIO/INC
          INC       PRIINVIO/INC
          INC       PRIPRAIO/INC  
          INC       PRIHREIO/INC
.
          INC       FMSCHQIO/INC
          INC       DEBDBTIO/INC
          INC       DEBFTHIO/INC
          INC       DEBFDTIO/INC
          INC       DEBITMIO/INC
.
          INC       IBAPRNIO/INC
          INC       IBATMDIO/INC            * Template Tail
          INC       IBATMHIO/INC            * Template Head
          INC       WEBSECIO/INC
.
+
