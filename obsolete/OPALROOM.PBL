.*****************************************************************************
.*    Include Name : OPALROOM.PBL                                            *
.*    System       : Theatre System                                          *
.*    Description  : Allocate default theatre for a session                  *
.*                                                                           *
.*    Author       : Rod Knowles                                             *
.*    Date         : 07/11/90                                                *
.*    Modifications: 25/02/1999 Jill Habasque
.*                   Added test for Active/Inactive codes                    *
.*****************************************************************************
.*                                                                           *
.*    PARAMETERS   : OPDNODSC  - must be read in from controlf file          *
.*                               by calling program (Sector 40, Pos 81)      *
.*                   CURSESS   - must be current session                     *
.*                               ie. Date, Time & Clinic/Surgeon             *
.*                                                                           *
.*  FLAGS AFFECTED : KEY6                                                    *
.*                                                                           *
.*    FILES NEEDED : OPRSESFD, OPROPRFD/IO/, OPROPRDS (open 2nd index)       *
.*                                                                           *
.*    RETURNS      : EXIT  0 = Need to redisplay screen ("?" input)          *
.*                         1 = No need to redisplay screen                   *
.*                                                                           *
.*****************************************************************************
+
.*****************************************************************************
.                                 DFSES000
.    Get transfer theatre code from opropraf
.*****************************************************************************
OPALROOM  DISPLAY   *PSUBOPT1:2,"- ",*V2LON,"(Default ",*+,OPCNODSC:
                     " Allocation) ";
.
DFSES000  MOVE      CURSESS,KEY19                 * get default value
          CALL      RDOPSES1
          BRANCH    OVRCD,DFSES900
          MOVE      ONE,EXIT                      * set no "?" input flag
.
DFSES100  DISPLAY   *P1:24,*EL,"Please enter the ",*+,OPCNODSC," for this ":
                    "session : ",OPSETHET
.
          MOVELPTR  OPCNODSC,CCOL           * get length of sys parameter
          ADD       "38",CCOL               * get keyin position
          KEYIN     *PCCOL:24,*V2LON,KEY6
.
          ENDSET    KEY6
          APPEND    SP6,KEY6
          RESET     KEY6
.
          MATCH     SP6,KEY6           * test for return hit
          GOTO      DFSES999 IF EQUAL
          MATCH     EXITCHAR,KEY6      * test for exitchar hit
          GOTO      DFSES999 IF EQUAL
.
          MATCH     "?",KEY6
          GOTO      DFSES222 IF NOT EQUAL
          CALL      OPROPRDS           * call theatre room ? routine.
          MOVE      ZERO,EXIT          * a "?" was input
          GOTO      DFSES100           * goback and get code.
DFSES222
          CALL      RDOPOPR1           * validate code and get description
          BRANCH    OVRCD,DFSES800
          COMPARE   ONE,OPRMSTAT       * active/inactiv
          IF        @EQUAL
            DISPLAY   *P1:24,*EL,*V2LON,"Inactive Code ",*HOFF;
            CALL      HITENTER
            GOTO      DFSES100
          ENDIF
          CALL      UPSES000           * update session file
          GOTO      DFSES999
DFSES800
          DISPLAY   *P1:24,*B,*EL,"Invalid Operating Room code.  ";
          CALL      HITENTER
          GOTO      DFSES100
DFSES900
          DISPLAY   *P1:24,*EL,*B,"Session record has disappeared. No Update. ";
          CALL      HITENTER
DFSES999  RETURN
+
.****************************************************************************
.*                              UPSES000                Called by: DFSES000 *
.*              update session file with new default theatre                *
.****************************************************************************
UPSES000  MOVE      CURSESS,KEY19
          CALL      RDOPSES1
          BRANCH    OVRCD,UPSES500
          MOVE      "2",AUDIFLAG            * before change audit
          CALL      AUSES000                * write the audit
          MOVE      KEY6,OPSETHET
          CALL      UPOPSES1
          MOVE      "3",AUDIFLAG            * after change audit
          CALL      AUSES000                * write the audit
          GOTO      UPSES999
.
UPSES500  DISPLAY   *P1:24,*EL,*B,"Session record has disappeared. No Update. ";
          CALL      HITENTER
.
UPSES999  RETURN
