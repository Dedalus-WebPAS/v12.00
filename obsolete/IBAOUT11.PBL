.******************************************************************************
.* System         : Outpatients                                               *
.* Program        : IBAOUT11.PBL                                              *
.* Name           : Site Code Maintenance                                     *
.******************************************************************************
.* Date           : 21/06/88                                                  *
.* Function       : To maintain the Outpatients site file                     *
.* Modifications  :                                                           *
.*        V9.02.00  28.11.02  Lina Vo                                         *
.*                  Remove site specific open of outsitaf                     *
.*        V5.01.01  14.07.89  E.Phan                                          *
.*                  Referral Source cat defaults to category S.               *
.*                  19.07.89  E.Phan    SRF 101331                            *
.*                  Added PATIRNDS.                                           *
.*        V5.01.02  14/09/89 Graeme Williams                                  *
.*                  Added OSTMAXD                                             *
.*        V5.01.03  03/10/89 L.P.       SRF012514                             *
.*                  Recompiled with new OUTAUXFD.INC                          *
.*        V5.01.04  05/10/89 K.Peak                                           *
.*                  Replaced uppercase audits with lowercase  SRF 102601      *
.*        V5.01.05  04/02/1992    Justin Coates  (Southland changes)          *
.*                  Added keyin of OTSTXBOK and OTSTXATT (for extra screens)  *
.*        V5.01.06  13/03/1992    Justin Coates  (Southland changes)          *
.*                  Make it so extra screens can be any number > 03           *
.*        V5.01.07  21/09/1992  Graeme Williams                               *
.*                  Removed *FILL on KEYIN's                                  *
.*        V5.01.08  10/12/1992    Justin Coates                               *
.*                  Extra screens must be >= 10                               *
.*        V5.01.09  17/03/1994    Justin Coates                               *
.*                  use PATCODKY for system code                              *
.*        V5.01.10  20/04/1994    WHIRLPOOL                                   *
.*                  removed updflock label because now in ibaioovr            *
.******************************************************************************
.
          INC       STD001FD
          INC       OUTAUXFD/INC
          INC       OUTSITFD/INC            * Site code file
          INC       PATIRNFD/INC
          INC       PATCODFD/INC            * codes file
          INC       SCRMASFD/INC            * screen master file
.
HOAUDS    FORM      1
.
CHG       FORM      1
CODE      DIM       2
CODEO     INIT      "O "
.
FIELD     FORM      1
MODE      DIM       1
MODE1     DIM       1
.
DSYST     DIM       20
DCATG     DIM       20
DIM3      DIM       3
DIM30     DIM       30
.
.
OUT       INIT      "out"
.
PRGNAM    INIT      "Site Code Maintenance"
PRGID     INIT      "IBAOUT11"
VERSION   INIT      "V10.13.00"
+
.         Start of program
. 
          CALL      INIT0000                 * Open data files
.
MENU      HLINE     *G37,2,51,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Insert":
                    *P1:6,*V2LON,TWO,*HOFF," = Change":
                    *P1:7,*V2LON,THREE,*HOFF," = Delete"
BEGIN     KEYIN     *P1:9,*EL,"Please Select : ",*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      ENDIT IF EQUAL
.
REPEAT    BRANCH    OPTION,INSERT,CHANGE,DELETE
          BEEP
          GOTO      BEGIN
+
.------------------------------------------------------------------------------
.         Add a site code
.------------------------------------------------------------------------------
.
INSERT    CALL      DISPI                    * Display mode
          CALL      DISPSC                   * Display screen
          MOVE      ZERO,CHG                 * No changes yet
          MOVE      ANSI,MODE1               * Initial mode INSERT
          BRANCH    HOAUDS,KSITE
          MOVE      ANSI,AUMODE
          GOTO      KSITE                    * Keyin site
.
.------------------------------------------------------------------------------
.         Change a site code
.------------------------------------------------------------------------------
.
CHANGE    CALL      DISPC                    * Display mode
          CALL      DISPSC                   * Display screen
          MOVE      ZERO,CHG                 * No changes yet
          MOVE      ANSC,MODE1               * Initial mode CHANGE
          BRANCH    HOAUDS,KSITE
          MOVE      ANSC,AUMODE
          GOTO      KSITE                    * Keyin site
.
.------------------------------------------------------------------------------
.         Delete a site code
.------------------------------------------------------------------------------
.
DELETE    CALL      DISPD                    * Display mode
          CALL      DISPSC                   * Display screen
          MOVE      ANSD,MODE1
          BRANCH    HOAUDS OF KSITE
          MOVE      ANSD,AUMODE
          GOTO      KSITE                    * Keyin site
.
DELCS     CALL      DISPDA                   * Display site data
.
REKEY     KEYIN     *P1:24,*EL,"Correct SITE CODE (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF:
                    ") ? ",*V2LON,ANS;
          CMATCH    ANSN,ANS
          GOTO      REPEAT IF EQUAL
.
REKEY1    KEYIN     *P1:24,*EL,"Are you sure ":
                    "you want to delete this SITE CODE (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF:
                    ") ? ",*V2LON,ANS;
          CMATCH    ANSY,ANS
          GOTO      WARDEL IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      REPEAT IF EQUAL
          BEEP
          GOTO      REKEY1
.
WARDEL    MOVE      OSTSITE,KEY6
          CALL      DESITA1
.
          BRANCH    HOAUDS OF ENDDELT
.
          MOVE      " Site Code          ",AUDESC
          PACK      AUCONBEF WITH OSTSITE,SP30,SP30
          PACK      AUCONAFT WITH SP30,SP20
          CALL      WRITAUDX
          CALL      POSTAUDX
.
ENDDELT   DISPLAY   *P1:24,*EL,*P55:24,*V2LON,"** Deleted **"
          GOTO      REPEAT
.
.------------------------------------------------------------------------------
.                   KSITE
.         Input site code
.------------------------------------------------------------------------------
KSITE     KEYIN     *P25:4,*EL,*DV,UNDLN6:
                    *P25:4,*V2LON,OSTSITE:
                    *P25:4,*DV,OSTSITE
.
          ENDSET    OSTSITE
          APPEND    SP6,OSTSITE
          RESET     OSTSITE
          MATCH     SP6,OSTSITE
          GOTO      MENU IF EQUAL
.
          MOVE      MODE1,MODE
.
          MOVE      OSTSITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD OF ENDNO,INUSE
.
          PACK      AUITEM,OSTSITE,SP20
          CMATCH    ANSC,MODE
          GOTO      NEXTM IF NOT EQUAL
          CALL      DISPDA
          GOTO      SFIELD
.
NEXTM     CMATCH    ANSD,MODE
          GOTO      DELCS IF EQUAL
.
          DISPLAY   *P1:24,*B,"Site Code already exists.  ",*EL;
          CALL      HITENTER
          GOTO      KSITE
.
ENDNO     CMATCH    ANSI,MODE
          GOTO      KDESC IF EQUAL
.
          DISPLAY   *P1:24,*B,"Site Code does not exist.  ",*EL;
          CALL      HITENTER
          GOTO      KSITE
.
INUSE     DISPLAY   *P1:24,*B,"Site Code in use on another terminal.  ",*EL;
          CALL      HITENTER
          GOTO      KSITE
.
.------------------------------------------------------------------------------
.       Input description
.------------------------------------------------------------------------------
KDESC     MOVE      " Description        ",AUDESC
          PACK      AUCONBEF WITH OSTDESC,SP30,SP20
.
          KEYIN     *P25:6,*DV,UNDLN30:
                    *P25:6,*V2LON,OSTDESC:
                    *P25:6,*DV,OSTDESC
.
          ENDSET    OSTDESC
          APPEND    SP30,OSTDESC
          RESET     OSTDESC
.
          PACK      AUCONAFT WITH OSTDESC,SP30,SP20
.
          CMATCH    ANSC,AUMODE
          CALL      WRITAUDX IF EQUAL
.
          MOVE      ONE,CHG
          CMATCH    ANSC,MODE
          GOTO      SFIELD IF EQUAL
.
.------------------------------------------------------------------------------
.         Input password
.------------------------------------------------------------------------------
KPASS     MOVE      " Password           ",AUDESC
          PACK      AUCONBEF WITH OSTPASS,SP30,SP20
.
          KEYIN     *P25:7,*EL,*DV,UNDLN8:
                    *P25:7,*V2LON,OSTPASS:
                    *P25:7,*DV,OSTPASS
.
          PACK      AUCONAFT WITH OSTPASS,SP30,SP20
.
          CMATCH    ANSC,AUMODE
          CALL      WRITAUDX IF EQUAL
.
          MOVE      ONE,CHG
          CMATCH    ANSC,MODE
          GOTO      SFIELD IF EQUAL
.
.------------------------------------------------------------------------------
.       Input File Prefix
.------------------------------------------------------------------------------
KFILE     MOVE      " File Prefix        ",AUDESC
          PACK      AUCONBEF WITH OSTFILE,SP30,SP20
.
KKFILE    KEYIN     *P25:8,*EL,*DV,UNDLN3:
                    *P25:8,*V2LON,OSTFILE:
                    *P25:8,*DV,OSTFILE
.
          ENDSET    OSTFILE
          APPEND    SP3,OSTFILE
          RESET     OSTFILE
.
          SCAN      SP1,OSTFILE
          GOTO      INVFILE IF EQUAL
.
          RESET     OSTFILE
.
          PACK      AUCONAFT WITH OSTFILE,SP30,SP20
.
          CMATCH    ANSC,AUMODE
          CALL      WRITAUDX IF EQUAL
.
          MOVE      ONE,CHG
          CMATCH    ANSC,MODE
          GOTO      SFIELD IF EQUAL
.
          GOTO      KSYST
.
INVFILE   DISPLAY   *P1:24,*B,"3 Characters Required.  ",*EL;
          CALL      HITENTER
          GOTO      KKFILE
.
.------------------------------------------------------------------------------
.       Input SYST Prefix
.------------------------------------------------------------------------------
KSYST     MOVE      "System Prefix        ",AUDESC
          PACK      AUCONBEF WITH OSTSYST,SP30,SP20
          MOVE      "25",ECOL
          MOVE      "09",EVERT
          MOVE      CODEO,CODE
          MOVE      SP3,CKYIDEF3
          MOVE      ONE,CKYIMAND
.
          CALL      PATCODKY
          BRANCH    EXIT,KSYST,REPEAT
.
          MOVE      ACODE,OSTSYST
          MOVE      TDESC,DSYST
          DISPLAY   *P30:9,DSYST
          PACK      AUCONAFT WITH OSTSYST,SP30,SP20
.
          CMATCH    ANSC,AUMODE
          CALL      WRITAUDX IF EQUAL
.
          MOVE      ONE,CHG
          CMATCH    ANSC,MODE
          GOTO      SFIELD IF EQUAL
.
.------------------------------------------------------------------------------
.         Input invoice range
.------------------------------------------------------------------------------
KINVRG    KEYIN     *P25:10,*EL,*DV,UNDLN3:
                    *P25:10,*V2LON,OSTIRNG
          ENDSET    OSTIRNG
          APPEND    SP3,OSTIRNG
          RESET     OSTIRNG
          MATCH     SP3,OSTIRNG
          GOTO      INVRNG IF EQUAL
          CMATCH    QUEST,OSTIRNG
          GOTO      CHKRNG IF NOT EQUAL
.
          MOVE      ZERO,HLEF
          CALL      GETSCR00
          CALL      PATIRNDS
          CALL      PUTSCR00
          GOTO      KINVRG
.
CHKRNG    PACK      KEY3,OSTIRNG
          CALL      RDIRNG1
          BRANCH    OVRCD OF INVRNG
.
          DISPLAY   *P25:10,*EL,*V2LON,OSTIRNG,*HOFF,SP2,IRDESC
.
          MOVE      ONE,CHG
          CMATCH    ANSC,MODE
          GOTO      SFIELD IF EQUAL
          GOTO      KCHRG000
.
INVRNG    DISPLAY   *P1:24,*B,"Invalid Range.  ",*EL;
          CALL      HITENTER
          GOTO      KINVRG
.
.------------------------------------------------------------------------------
.         Keyin charge flag
.------------------------------------------------------------------------------
KCHRG000  KEYIN     *P25:11,*EL,*DV,UNDLN1:
                    *P25:11,*V2LON,ANS
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
          REP       UPPLOW,ANS
          CMATCH    ANSY,ANS
          GOTO      KCHRG100 IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      KCHRG200 IF EQUAL
          BEEP
          GOTO      KCHRG000
.
KCHRG100  MOVE      "Yes",DIM3
          MOVE      ANSC,OSTCHRG
          GOTO      KCHRG300
.
KCHRG200  MOVE      "No ",DIM3
          MOVE      SP1,OSTCHRG
.
KCHRG300  DISPLAY   *P25:11,*V2LON,DIM3
          MOVE      ONE,CHG
          CMATCH    ANSC,MODE
          GOTO      SFIELD IF EQUAL
.
.------------------------------------------------------------------------------
.         Keyin Source of Referral Category
.------------------------------------------------------------------------------
KREFER    KEYIN     *P25:12,*EL,*DV,ANSS,*DV,UNDLN1:
                    *P25:12,*V2LON,OSTCATG
          RESET     OSTCATG
          GOTO      KREFER90 IF EOS
          ENDSET    OSTCATG
          APPEND    SP2,OSTCATG
          RESET     OSTCATG
          MATCH     SP2,OSTCATG
          GOTO      KREFER80 IF EQUAL
KREFER10  DISPLAY   *P25:12,*V2LON,OSTCATG;
          PACK      KEY5,OSTCATG,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,KREFER80
          MATCH     OSTCATG,TCODE
          GOTO      KREFER80 IF NOT EQUAL
          DISPLAY   *P30:12,TDESC;
          MOVE      ONE,CHG
          CMATCH    ANSC,MODE
          GOTO      SFIELD IF EQUAL
          GOTO      KMAXD000
.
. ------- Error -------
.
KREFER80  DISPLAY   *P1:24,*EL,*B,"Category not on File.  ";
          CALL      HITENTER
          GOTO      KREFER
.
. ------- Default to Category S -------
.
KREFER90  PACK      OSTCATG,ANSS,SP1
          GOTO      KREFER10
.
.------------------------------------------------------------------------------
.         Keyin Maximum days for a boooking search
.------------------------------------------------------------------------------
KMAXD000  KEYIN     *P25:13,*EL,*DV,UNDLN3:
                    *P25:13,*V2LON,OSTMAXD:
                    *P25:13,*DV,OSTMAXD
.
          COMPARE   ZERO,OSTMAXD
          GOTO      KMAXD100 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid Maximum Days.  ";
          CALL      HITENTER
          GOTO      KMAXD000
.
KMAXD100  MOVE      ONE,CHG
          CMATCH    ANSC,MODE
          GOTO      SFIELD IF EQUAL
.
.------------------------------------------------------------------------------
.                   KXBOK000
.         Input Bookings extra screen Id - OTSTXBOK
.------------------------------------------------------------------------------
KXBOK000  DISPLAY   *P1:24,"Screen Id must be between 10 and 99",*EL;
          KEYIN     *P25:14,*DV,UNDLN2:
                    *P25:14,*V2LON,OTSTXBOK:
                    *P25:14,*DV,OTSTXBOK
.
          PACK      OTSTXBOK,OTSTXBOK,SP20
          MATCH     SP2,OTSTXBOK
          GOTO      KXBOK999 IF EQUAL       * nothing entered
.
          MATCH     "10",OTSTXBOK
          GOTO      KXBOK100 IF NOT LESS    * valid
.
          DISPLAY   *P1:24,*B,"Invalid Screen Id.  ",*EL;
          CALL      HITENTER
          GOTO      KXBOK000
.
KXBOK100  MOVE      "IBAOUT66",KEY8
          PACK      KEY10,KEY8,OTSTXBOK
          CALL      RDSCMA1
          COMPARE   ZERO,OVRCD
          GOTO      KXBOK999 IF EQUAL       * valid code
.
          DISPLAY   *P1:24,*B,"Screen Id. not set up.  ",*EL;
          CALL      HITENTER
          GOTO      KXBOK000
.
KXBOK999  MOVE      ONE,CHG
          DISPLAY   *P1:24,*EL;
          CMATCH    ANSC,MODE
          GOTO      SFIELD IF EQUAL
+
.------------------------------------------------------------------------------
.                   KXATT000
.         Input Attendees extra screen Id
.------------------------------------------------------------------------------
KXATT000  DISPLAY   *P1:24,"Screen Id must be between 10 and 99",*EL;
          KEYIN     *P25:15,*DV,UNDLN2:
                    *P25:15,*V2LON,OTSTXATT:
                    *P25:15,*DV,OTSTXATT
.
          PACK      OTSTXATT,OTSTXATT,SP20
          MATCH     SP2,OTSTXATT
          GOTO      KXATT999 IF EQUAL       * nothing entered
.
          MATCH     "10",OTSTXATT
          GOTO      KXATT100 IF NOT LESS    * valid
.
          DISPLAY   *P1:24,*B,"Invalid Screen Id.  ",*EL;
          CALL      HITENTER
          GOTO      KXATT000
.
.         validate id entered
.
KXATT100  MOVE      "IBAOUT66",KEY8
          PACK      KEY10,KEY8,OTSTXATT
          CALL      RDSCMA1
          COMPARE   ZERO,OVRCD
          GOTO      KXATT999 IF EQUAL       * valid code
.
          DISPLAY   *P1:24,*B,"Screen Id. not set up.  ",*EL;
          CALL      HITENTER
          GOTO      KXATT000
.
KXATT999  MOVE      ONE,CHG
          DISPLAY   *P1:24,*EL;
          CMATCH    ANSC,MODE
          GOTO      SFIELD IF EQUAL
+
.------------------------------------------------------------------------------
.         Select the field to update
.------------------------------------------------------------------------------
SFIELD    MOVE      ANSC,MODE                * Change mode
          CALL      MAINQST
          COMPARE   ZERO,CCITEM
          GOTO      POSTCNT IF EQUAL
          COMPARE   SEQ,CCITEM
          GOTO      CANC0000 IF EQUAL
.
          BRANCH    CCITEM,KDESC,KPASS,KFILE,KSYST,KINVRG:
                           KCHRG000,KREFER,KMAXD000,KXBOK000,KXATT000
.
          BEEP
          GOTO      SFIELD
.
.------------------------------------------------------------------------------
.         Cancel
.------------------------------------------------------------------------------
CANC0000  BRANCH    HOAUDS OF REPEAT
          CALL      CANCAUDX
          GOTO      REPEAT
.
.------------------------------------------------------------------------------
.         Post
.------------------------------------------------------------------------------
POSTCNT   CMATCH    ANSC,MODE1               * Initial mode CHANGE ?
          GOTO      WARWR IF NOT EQUAL       * No - write new record
.
          COMPARE   ZERO,CHG                 * Anything changed ?
          GOTO      REPEAT IF EQUAL          * No
.
          CALL      UPSITA1                  * Update changes
          GOTO      ENDPST
.
WARWR     MOVE      OSTSITE,KEY6
          CALL      WRSITA1
.
          BRANCH    HOAUDS OF ENDPST
.
          MOVE      " SITE CODE          ",AUDESC
          PACK      AUCONBEF WITH OSTSITE,SP30,SP20
          PACK      AUCONAFT WITH SP30,SP20
          CALL      WRITAUDX
.
ENDPST    CMATCH    SP1,AUMODE
          CALL      POSTAUDX IF NOT EQUAL
.
          DISPLAY   *P40:24,*EL,*V2LON:
                    "** Data Successfully Posted **"
          GOTO      REPEAT
.
+
.------------------------------------------------------------------------------
.         Headings for each mode
.------------------------------------------------------------------------------
.
DISPI     DISPLAY   *P51:2,*V2LON,"- Insert "
          RETURN
.
DISPC     DISPLAY   *P51:2,*V2LON,"- Change "
          RETURN
.
DISPD     DISPLAY   *P51:2,*V2LON,"- Delete "
          RETURN
.
.------------------------------------------------------------------------------
.                   DISPSC
.         Site screen
.------------------------------------------------------------------------------
DISPSC    DISPLAY   *P1:4,*EF:
                    *P1:04,"    Site code         : ",*V2LON:
                    *P1:06," 1",*HOFF,". Description       : ",*V2LON:
                    *P1:07," 2",*HOFF,". Password          : ",*V2LON:
                    *P1:08," 3",*HOFF,". File Prefix       : ",*V2LON:
                    *P1:09," 4",*HOFF,". System Type       : ",*V2LON:
                    *P1:10," 5",*HOFF,". Invoice Range     : ",*V2LON:
                    *P1:11," 6",*HOFF,". Charging Flag     : ",*V2LON:
                    *P1:12," 7",*HOFF,". Referral Category : ",*V2LON:
                    *P1:13," 8",*HOFF,". Max Days to Search: ",*V2LON:
                    *P1:14," 9",*HOFF,". Booking Screen Id : ",*V2LON:
                    *P1:15,"10",*HOFF,". Attendee Screen Id: ";
          RETURN
.
.------------------------------------------------------------------------------
.                   DISPDA
.         Display the data 
.------------------------------------------------------------------------------
DISPDA    DISPLAY   *P25:4,*V2LON,OSTSITE:
                    *P25:6,OSTDESC:
                    *P25:7,OSTPASS:
                    *P25:8,OSTFILE
.
          MOVE      SP30,DSYST
          MATCH     SP3,OSTSYST
          GOTO      DISPDA1 IF EQUAL
          PACK      KEY5,CODEO,OSTSYST
          CALL      RDCODE1
          BRANCH    OVRCD,DISPDA0
          MOVE      TDESC,DSYST
DISPDA0   DISPLAY   *P25:9,*V2LON,OSTSYST,*HOFF,*P30:9,DSYST
.
DISPDA1   MATCH     SP3,OSTIRNG
          GOTO      DISPDA2 IF EQUAL
          DISPLAY   *P25:10,*V2LON,OSTIRNG,*HOFF,*P30:10,IRDESC
.
DISPDA2   MOVE      "No ",DIM3
          CMATCH    ANSC,OSTCHRG
          GOTO      DISPDA3 IF NOT EQUAL
          MOVE      "Yes",DIM3
DISPDA3   DISPLAY   *P25:11,*V2LON,DIM3
.
          MOVE      SP20,DCATG
          MATCH     SP2,OSTCATG
          GOTO      DISPDA5 IF EQUAL
          PACK      KEY5,OSTCATG,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,DISPDA4
          MOVE      TDESC,DCATG
DISPDA4   DISPLAY   *P25:12,*V2LON,OSTCATG,*HOFF,*P30:12,DCATG:
                    *P25:13,*V2LON,OSTMAXD,*P25:14,OTSTXBOK,*P25:15,OTSTXATT
.
DISPDA5   RETURN
.
.------------------------------------------------------------------------------
.                   INIT0000
.         Open data files
.------------------------------------------------------------------------------
INIT0000  CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening outsitaf";
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA2,"outsitaf"
.
          DISPLAY   *P64:24,"patirnge";
          OPEN      PATIRNG1,"patirnge"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"scrmasaf";
          OPEN      SCRMASA1,"scrmasaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,THIRTY1;*69,HOAUDS
          CLOSE     CONTROLF
.
          MOVE      SP1,AUMODE
          BRANCH    HOAUDS,INIT9999
          MOVE      ANSS,AUTYPE
          REP       "Ss",AUTYPE
          CALL      STRTAUDX
.
INIT9999  RETURN
.
.
.         I/O includes
.
          INC       STD001IO
          INC       OUTAUXIO/INC
          INC       OUTSITIO/INC
          INC       PATCODKY
          INC       PATIRNIO/INC
          INC       PATIRNDS
          INC       PATCODIO/INC
          INC       SCRMASIO/INC            * screen master file
.
ENDIT     BRANCH    HOAUDS OF ABORT
          CALL      STOPAUDX
.
ABORT     CHAIN     PGM
          STOP
.
.---------------------------------------------------------------------
.                   AVSCR000
.         display available screen id's
.---------------------------------------------------------------------
AVSCR000  MOVE      FIVE,CVERT              * line counter
          MOVE      ONE,CCOL                * start on LHS of screen
          MOVE      "IBAOUT66",KEY8
          PACK      KEY10,KEY8,SP2          * key of blanks
          CALL      RSSCMA1                 * positional read
.
          DISPLAY   *P1:3,*EF,*V2LON,*P30:4,"SCREEN ID's":
                    *P1:5,*ULON,"Id",*P4:5,"          Description         "
.
. -------------------------------------
. ------- next read on item file ------
. -------------------------------------
.
AVSCR100  CALL      RKSCMA1   
          BRANCH    OVRCD,AVSCR900          * end of file
.
          MATCH     KEY8,SCMAPID
          GOTO      AVSCR900 IF NOT EQUAL   * different program
.
. ----------------------------------
. ------- increment counters -------
. ----------------------------------
.
AVSCR200  ADD       ONE,CVERT               * inc line counter
.
          COMPARE   "23",CVERT
          GOTO      AVSCR300 IF LESS        * will fit on the page
.
          ADD       "40",CCOL               * inc column counter
          MOVE      SIX,CVERT               * reset row position
.
          COMPARE   "80",CCOL  
          GOTO      AVSCR400 IF NOT LESS    * need a new page
.
          DISPLAY   *V2LON,*ULON,*P41:5,"Id":
                    *P44:5,"          Description         "
.
. -------------------------------------------
. ------- display all the information -------
. -------------------------------------------
.
AVSCR300  DISPLAY   *PCCOL:CVERT,*V2LON,SCMASID,*HOFF,SP1,SCMADES
          GOTO      AVSCR100
.
. -------------------------------
. ------- new page needed -------
. -------------------------------
.
AVSCR400  DISPLAY   *P1:24,"( )ontinue, ( )irst, ( )ext ? ",*EL:
                    *V2LON,*P2:24,ANSC,*P14:24,ANSF,*P23:24,ANSN
.
          KEYIN     *P31:24,*DV,UNDLN1:
                    *P31:24,*V2LON,ANS
.
          REP       UPPLOW,ANS
.
          MATCH     ANSC,ANS
          GOTO      AVSCR999 IF EQUAL       * Continue selected
.
          MATCH     ANSF,ANS
          GOTO      AVSCR000 IF EQUAL       * First selected
.
. --------------------------------------
. ------- (N)ext screen selected -------
. --------------------------------------
.
AVSCR700  DISPLAY   *P41:5,*EF;
          MOVE      SIX,CVERT               * first display row 
          MOVE      ONE,CCOL                * get back to LHS of screen
          GOTO      AVSCR300                * display the data
.
AVSCR900  DISPLAY   *P1:24,*EL;
          CALL      HITENTER
.
AVSCR999  RETURN
+
