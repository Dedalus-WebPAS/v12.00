.******************************************************************************
.* System         : Outpatients                                               *
.*                                                                            *
.* Include Name   : OUTPRN66.PBL                                              *
.*                                                                            *
.* Function       : Print a single clinic.                                    *
.*                                                                            *
.* Author         : Greame Williams (I.B.A)                                   *
.*                                                                            *
.* Date Written   : 11/09/1989                                                *
.*                                                                            *
.* Modifications  :                                                           *
.*        V10.02.01 04/04/2011  Jill Parkinson CAR 239574                     *
.*                  Removed open of ibaprntf                                  *
.*        V5.09.B01 05/01/2001  Greg Horvat                                   *
.*                  Replaced certain variables with KEY?? variables cause     *
.*                  OUT66 couldnt compile                                     *
.*     V5.08.01     16/03/2000  Steve Armstrong       5.08 Mods               *
.*                  Mods for changes to OUTCLIFD (effective date)             *
.******************************************************************************
.*     V5.07.00     16/10/1998  Jim Stathopoulos                              *
.*                  507 Changes                                               *
.******************************************************************************
.*                  19/11/1997  John Holmes   QTE 8912, 8913, 8911            *
.*                  Added comments & other shit                               *
.*                  23/05/1997  Steve Armstrong    SRF 642902                 *
.*                  Added open on IBAPRNT1                                    *
.******************************************************************************
.*                  02/08/1991    Justin Coates  Quote 6994  SRF 109232       *
.*                  Suppress display of extended slots if desired             *
.*                  07/12/1992  Graeme Williams            (UK Changes)       *
.*                  Added OTCNR49A to specify report format                   *
.*                  06/07/93 DIG                       SRF  123629 + 123541   *
.*                  Removed open of PATRE1A1 as it is now assumed it is open. *
.******************************************************************************
.
.------------------------------------------------------------------------------
.         Print records
.------------------------------------------------------------------------------
.
.         START OF PRINTING SESSION DETAILS
.
PRNTB000  CALL      OPNPRINT
.
          DISPLAY   *P1:24,*EL,*P20:24,*V2LON:
                    "** Printing Outpatient Session Details **",*W2;
.
.         Get the clinic type description
.
          PACK      CFNAME,OSTFILE,FILCTYA1
          OPEN      OUTCTYA1,CFNAME
.
          MOVE      "** Unknown Clinic Type **     ",OCTDESC
          PACK      KEY12 WITH VSITE,VCTYP
          CALL      RDCTYA1
.
          CLOSE     OUTCTYA1
.
.         Get the clinic id description
.
          PACK      KEY14,VCLIN,VDATE
          CALL      RDCLIA2
          IF        OVRCD = 1
            CALL      RDPCLIA2
            IF        OVRCD = 0
              MATCH     VSITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     VCLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          BRANCH    OVRCD,PRNTB030
.
          CALL      CIDSC000                * Get clinic desc. (from OUTPRG66)
          GOTO      PRNTB050
.
PRNTB030  MOVE      "** Unknown Clinic Id **       ",OCADESC
.
.         Get the day description
.
PRNTB050  LOAD      DDAY,WDAY,DMON,DTUE,DWED,DTHU,DFRI,DSAT,DSUN
.
.         Get the comments from the Clinic Master file
.
          PACK      KEY18,VSITE,VCLIN,WDAY,VTIME
          CALL      RDMASA1
.
          CALL      PRNHEAD
.
.         Now loop over the bookings file, and print each record
.
          PACK      KEY28,VSESS,SP3
          CALL      RDSBOKA1
.
PRNTB100  CALL      RDKBOKA1
          BRANCH    OVRCD,PRNTB900
.
          PACK      SESSION,OBASITE,OBACLIN,OBADATE,OBASTRT
.
          MATCH     VSESS,SESSION
          GOTO      PRNTB900 IF NOT EQUAL
.
.         check for suppression of extended slot
.
          COMPARE   ONE,OTCNSUPX
          GOTO      PRNTB110 IF NOT EQUAL   * dont check for suppression
.
          COMPARE   THREE,OBASTAT
          GOTO      PRNTB1100 IF NOT EQUAL  * not an extended slot
.
          MATCH     ANSY,OTMASUPX
          GOTO      PRNTB100 IF EQUAL       * suppress the extended slot
.
.         Clear variables
.
PRNTB110  MOVE      SP30,DSCVISIT           * Booking record status
          MOVE      SP30,PTITL
          MOVE      SP30,PSNAME
          MOVE      SP30,PGNAME
          MOVE      SP30,KEY30
          MOVE      SP30,PKNAME
          MOVE      SP30,TDESC
          MOVE      SP30,PADD1
          MOVE      SP30,PKADD1
          MOVE      SP30,PADD2
          MOVE      SP30,PTELEP
          MOVE      SP30,PKADD2
          MOVE      SP30,PSUBRB
          MOVE      SP30,PPOST
          MOVE      SP30,PMEDI
          MOVE      SP30,PKSUBR
          MOVE      SP30,PKPOST
          MOVE      SP30,PKTELEB
          MOVE      SP30,PBDATE
          MOVE      SP30,OBACOMM
          MOVE      SP30,OBACOMP
          MOVE      SP30,OBAINS
          MOVE      SP30,OBACLASS
          MOVE      SP1,DESCASEN            * Case notes indicator
.
.         If not booked, do not get details
.
          COMPARE   ZERO,OBASTAT
          GOTO      PRNTB200 IF EQUAL
.
.         Get the master details
.
          MATCH     ZEROUR,OBAURNO          * Do we have a U/R number ?
          GOTO      PRNTB130 IF EQUAL       * No. Use the patpramf file
.
.         Read in the PMI details from the master file
.
          MOVE      OBAURNO,PURNO
          MOVE      PURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PRNTB130
.
          LOAD      DESCASEN,PCASE,CCNOTES
          GOTO      PRNTB150
.
.         Read in the PMI details from the patpramf file
.
PRNTB130  COMPARE   ZERO,OBAOUTNO           * Do we have an outpatient number
          GOTO      PRNTB150 IF EQUAL       * No. No-one in this slot
.
          MOVE      OBAOUTNO,PURNO
          MOVE      PURNO,KEY8
          CALL      RDPRAM1
          BRANCH    OVRCD,PRNTB140          * No details available
          GOTO      PRNTB150
.
PRNTB140  MOVE      "** Missing PMI **   ",PSNAME
          MOVE      SP30,PGNAME
          MOVE      SP4,PTITL
.
.         Get the booking details
.
PRNTB150  MOVE      OBAOUTNO,KEY8
          CALL      RDBOKB1
.
.         Format the name
.
          CALL      FRMN0000                * Format the name (from OUTPRG66)
          MOVE      DIM40,KEY30
.
.         Do we have a source of referal ?
.
          MOVE      SP20,TDESC
          MATCH     OBASRCR,SP3
          GOTO      PRNTB170 IF EQUAL
.
          PACK      KEY5 WITH OSTCATG,OBASRCR
          CALL      RDCODE1
.
.         Check for a person responsible for the account
.
PRNTB170  COMPARE   ZERO,OBAOUTNO
          GOTO      PRNTB200 IF EQUAL
. 
          MOVE      OBAOUTNO,PKADMN
          MOVE      PKADMN,KEY8
          CALL      RDRESP1
          BRANCH    OVRCD,PRNTB200
          GOTO      PRNTB300
.
PRNTB200  MOVE      SP20,PKNAME
          MOVE      SP30,PKADD1
          MOVE      SP30,PKADD2
          MOVE      SP30,PKSUBR
          MOVE      SP30,PKPOST
          MOVE      SP30,PKTELEB
.
.         Set up the status indicator
.
PRNTB300  BRANCH    OBASTAT OF PRNTB310,PRNTB320,PRNTB330,PRNTB340,PRNTB350
.
          MOVE      "** NOT BOOKED **",DSCVISIT
          GOTO      PRNTB400
.  
PRNTB310  MOVE      KEY30,DSCVISIT
          GOTO      PRNTB400
.  
PRNTB320  MOVE      "** CANCELLED **",DSCVISIT
          GOTO      PRNTB400
.  
PRNTB330  MOVE      "** EXTENDED SLOT **",DSCVISIT
          GOTO      PRNTB400
.  
PRNTB340  MOVE      "** ATTENDED **",DSCVISIT
          GOTO      PRNTB400
.  
PRNTB350  MOVE      "** NON ATTENDANCE **",DSCVISIT
.  
PRNTB400  UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          BRANCH    OTCNR49A,PRNTB410
.
.         Get the comments         
.
          PACK      PCLINE,SP30,SP30,SP30,SP10
          MATCH     "1",OTMADCMB
          IF        @EQUAL
            COMPARE   ZERO,OBAOUTNO
            IF        !@EQUAL               
              OPEN      PATCMNT1,"patcmntf"
              PACK      KEY10,OBAOUTNO,SP2       * Get the 1st Comments Line
              CALL      RDSCMNT1
              CALL      RDKCMNT1
              IF        OVRCD = 0         
                COMPARE   PCADMN,OBAOUTNO          * Correct patient ?
                IF        !@EQUAL                 
                  PACK      PCLINE,SP30,SP30,SP30,SP10
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
.         Standard Format
.
          COMPARE   ZERO,OBASTAT
          GOTO      PRNTB405 IF EQUAL
.
          PRINT     "|",*3,OBASLOT,*7,OBATIME:
                    *13,OBAVISIT,*17,OBAURNO,DESCASEN,*27,CPCDATE:
                    *38,DSCVISIT,*63,KEY30,*101,PKNAME,*132,"|":
                    *N,"|",*41,TDESC,*63,PADD1:
                    *89,OBACOMP,*93,OBAINS,*97,OBACLASS:
                    *101,PKADD1,*132,"|":
                    *N,"|",*63,PADD2,*89,PTELEP,*101,PKADD2,*132,"|":
                    *N,"|",*38,OBACOMM,*63,PSUBRB,*79,PPOST,*84,PMEDI:
                    *101,PKSUBR,*119,PKPOST,*132,"|":
                    *N,"|",*101,PKTELEB,*132,"|"
.
          ADD       FIVE,CLNO
.
          MATCH     SP70,PCLINE
          IF        !@EQUAL
            PRINT     "|",*17,"Comments : ",PCLINE,*132,"|" 
            ADD       ONE,CLNO
          ENDIF
.
          GOTO      PRNTB800
.
. ------- Standard format not booked
.
PRNTB405  PRINT     "|",*3,OBASLOT,*7,OBATIME:
                    *13,OBAVISIT,*17,OBAURNO,DESCASEN,*27,CPCDATE:
                    *38,DSCVISIT,*63,KEY30,*101,PKNAME,*132,"|"
          ADD       ONE,CLNO
          GOTO      PRNTB800
.
.         Dudley Format
.
PRNTB410  PRINT     "|",*3,OBASLOT,*7,OBATIME,*17,OBAURNO,DESCASEN,*27,CPCDATE:
                    *60,KEY30,*96,PKNAME,*132,"|":
                    *N,"|",*7,OBAVISIT,*34,TDESC,*60,PADD1,*96,PKADD1:
                    *132,"|":
                    *N,"|",*34,OBACOMP,*38,OBAINS,*42,OBACLASS:
                    *46,OTBBTRAN,*50,OTBBLNGI:
                    *60,PADD2,*96,PKADD2,*132,"|":
                    *N,"|",*60,PSUBRB,*79,PPOST:
                    *96,PKSUBR,*119,PKPOST,*132,"|":
                    *N,"|",*34,OBACOMM,*60,PPOST," (P) ",PTELEP:
                    *96,PKPOST," (B) ",PKTELEB,*132,"|"
.
          ADD       FIVE,CLNO
.
PRNTB800  COMPARE   "55",CLNO
          CALL      ENDPAGE IF NOT LESS
.
          COMPARE   TEN2,CLNO          ** NEW PAGE DON'T NEED UNDLN PRINT
          GOTO      PRNTB100 IF EQUAL
.
          IF        OTCNR49A = 0
            PRINT     *1,"|",*63,UNDLN30,UNDLN30,UNDLN9,*132,"|"
          ENDIF
          IF        OTCNR49A = 1
            MOVE      UNDLN30,KEY12
            PRINT     *1,"|",*60,UNDLN30,UNDLN30,KEY12,*132,"|"
          ENDIF
          GOTO      PRNTB100
.
.         End of report
.
PRNTB900  PRINT     "|",*132,"|":
                    *N,"*------------------------------------------":
                       "-------------------------------------------":
                       "---------------------------------------------*":
                    *N,*N,"** End of Report **"
.
          DISPLAY   *P1:24,*EL,*V2LON,*P20:24:
                    "** Report Generation Completed **",*W1;
.
          CALL      CLSPRINT
          RETURN
.
.------------------------------------------------------------------------------
.         Print report header
.------------------------------------------------------------------------------
.
ENDPAGE   PRINT     "|",*132,"|":
                    *N,"*------------------------------------------":
                       "-------------------------------------------":
                       "---------------------------------------------*"
.
PRNHEAD   ADD       ONE,CPAGENO
          MOVE      ZERO,CLNO
.
          CLOCK     TIME,CTIMEIS
          UNPACK    VDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *F,PRGID,*35,CNAME,*116,"DATE : ",CDATE:
                    *N,VERSION,*35,OCTDESC," CLINIC LIST FOR ",OCADESC:
                    *116,"TIME : ",CTIMEIS:
                    *N,*116,"PAGE :",SP6,CPAGENO:
                    *N,*35,"ON ",DDAY,SP1,CPCDATE:
                    *61,"AT ",VTIME:
                    *N,*35,OMACOM1,SP1,OMACOM2,*N,*N
          MOVE      TEN1,CLNO
.
          BRANCH    OTCNR49A,PRNHA100
.
.         Standard Format
.
          CALL      UND132
          PRINT     "|",*132,"|":
                    *N,"|",*2,"Slot",*7,"Time ",*13,"Vis",*17,"U/R No.  ":
                    *29,"D.O.B.  ",*38,"Booking Name/ Referral":
                    *63,"PMI Details ":
                    *101,"Person Fin. Resp.":
                    *125,"Nxt/Bk",*132,"|":
                    *N,"|",*132,"|"
          CALL      UND132
          PRINT     "|",*132,"|"
.
          ADD       FOUR,CLNO
          RETURN
.
.         Dudley format
.
PRNHA100  CALL      UND132
          PRINT     *1,"|",*2,"Slot",*7,"Time ",*13,"U/R No.  ":
                    *25,"D.O.B.  ",*34,"Booking Details":
                    *60,"PMF Details ":
                    *96,"Person Fin. Resp.":
                    *125,"Nxt/Bk",*132,"|"
          CALL      UND132
          PRINT     "|",*132,"|"
.
          ADD       TWO,CLNO
          RETURN
............................................................................
