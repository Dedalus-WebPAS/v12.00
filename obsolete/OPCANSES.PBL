.*******************************************************************************
.    System          : Theatre System
.    Include Function: OPCANSES.PBL
.    Description     : Cancel the current session 
.*******************************************************************************
.    Date            : 11/01/1991
.    Author          : Graeme Williams
.    Modifications   : 
.         V5.07.01   Jill Habasque
.                    Added inactive code error message 
.*******************************************************************************
.
.    FUNCTION DESCRIPTION.
.
.    The function will ask for the cancellation code for the current session
.    on line 24. It will then cancel the session.
.
.    INCLUDES AND FILES.
.
.    FD's  : OPR001FD, PATCODFD, OPRSESFD
.                      1         1
.
.    I/O's : PATIOCOD, OPRSESIO
.  
.    PARAMETERS.
.  
.    CURSESS : The current session in format DATE/TIME/CODE
.
.    RETURN PARAMETERS.
.
.    EXIT    = 0 If session is cancelled.
.    EXIT    = 1 If session is not cancelled.
.    SCRNFLAG= 0 If screen needs to be re-displayed
.    SCRNFLAG= 1 If screen does not need to be re-displayed
.
.*******************************************************************************
.                                 OPCANSES
.*******************************************************************************
OPCANSES  CALL      GSCAN000            * Get the cancellation code for session
          BRANCH    EXIT,CANSS999       * Entry aborted
.
          CALL      CANSS000            * Cancel the session
          MOVE      ZERO,EXIT           * Session cancelled
.
CANSES99  RETURN
.*******************************************************************************
.                                 CANSS000             Called by OPCANSES
.  Cancel the session.
.
.  Parameters : CURSESS   Session to be cancelled
.               REPLY     Cancellation reason
.*******************************************************************************
CANSS000  PACK      KEY19,CURSESS,SP20  * Key for session
          CALL      RDOPSES1            * Get the session record
          BRANCH    OVRCD,CANSS900      * Session record has disappeared
.
          MOVE      TWO,AUDIFLAG        * Before changes
          CALL      AUSES000            * post to audit file
.
          MOVE      REPLY,OPSECANC      * Cancellation reason
          MOVE      ZERO,OPSESTAT       * Change status to "Cancelled"
.
          CALL      UPOPSES1
          MOVE      THREE,AUDIFLAG      * After changes
          CALL      AUSES000            * post to audit file
          GOTO      CANSS999
.
CANSS900  DISPLAY   *P1:24,*EL,*B,"Session record missing. ":
                    "Session not cancelled. ";
          CALL      HITENTER
.
CANSS999  RETURN
.*******************************************************************************
.                                 GSCAN000             Called by OPCANSES
.  Get the cancellation reason for the session.
.*******************************************************************************
GSCAN000  MOVE      ONE,SCRNFLAG        * flag to say redisplay no necessary
.
.         Get the reason
.
GSCAN010  DISPLAY   *P1:24,*EL,"Please enter cancellation code : ___"
          KEYIN     *P34:24,*V2LON,REPLY,*DV,*P34:24,REPLY
          ENDSET    REPLY
          APPEND    SP3,REPLY
          RESET     REPLY
.
          MATCH     SP3,REPLY          * test for return hit
          GOTO      GSCAN090 IF EQUAL
          MATCH     EXITCHAR,REPLY     * test for exitchar 
          GOTO      GSCAN090 IF EQUAL
.
          CMATCH    "?",REPLY
          GOTO      GSCAN060 IF NOT EQUAL
.
          PACK      CODE,ANSO,ANSC
          CALL      PATCODDS           * display ? routine cancellation codes
          MOVE      ZERO,SCRNFLAG      * set flag to redisplay
          GOTO      GSCAN010
.
GSCAN060  PACK      KEY5,ANSO,ANSC,REPLY,SP5
          CALL      RDCODE1            * read code file for entered code
          BRANCH    OVRCD,GSCAN080     * tell user his code not on file
          MATCH     "I",PTCOACTV  
          GOTO      GSCAN950 IF EQUAL  * Inactive code
          MOVE      ZERO,EXIT          * we have a valid code
          GOTO      GSCAN999
.
GSCAN080  DISPLAY   *P1:24,*B,*EL,"Invalid Cancellation code.  ";
          CALL      HITENTER
          GOTO      GSCAN010
.
GSCAN090  MOVE      ONE,EXIT
          GOTO      GSCAN999
.
GSCAN950  DISPLAY   *P1:24,*EL,*V2LON,"Inactive Code  ";
          CALL      HITENTER
          GOTO      GSCAN010
.
GSCAN999  RETURN
