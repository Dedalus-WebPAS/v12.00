.******************************************************************************
.* System         : Patient Care Plans                                        *
.*                                                                            *
.* Include Name   : PCPFRADS.PBL                                              *
.*                                                                            *
.* Function       : Display the codes from the Frequency file PCPFRAFD        *
.*                                                                            *
.* Author         : Justin Coates        20/11/1991                           *
.*                                                                            *
.* Index used     : PCPFRAA1,"pcpfraaf"                                       *
.*                                                                            *
.* Modifications  :                                                           *
.*      V9.02.00    30/12/2002  Tracey Nguyen  srf 23497                      *
.*                  r5.10 Care Plans Port to V9                               *
.******************************************************************************
.
PCPFRADS  MOVE      FIVE,CVERT              * line counter
          MOVE      ONE,CCOL                * start on LHS of screen
          MOVE      SP10,KEY9               * key of blanks
          CALL      RSPCFRA1                * positional read
.
          DISPLAY   *P1:3,*EF,*V2LON,*P30:4,"FREQUENCY CODES":
                    *P1:5,*ULON,"Code     ":
                    *P11:5,"Description              "
.
. -------------------------------------
. ------- next read on item file ------
. -------------------------------------
.
FRADS100  CALL      RKPCFRA1  
          BRANCH    OVRCD,FRADS999          * end of file
.
. ----------------------------------
. ------- increment counters -------
. ----------------------------------
.
FRADS200  ADD       ONE,CVERT               * inc line counter
.
          COMPARE   "23",CVERT
          GOTO      FRADS300 IF LESS        * will fit on the page
.
          ADD       "40",CCOL               * inc column counter
          MOVE      SIX,CVERT               * reset row position
.
          COMPARE   "80",CCOL  
          GOTO      FRADS400 IF NOT LESS    * need a new page
.
          DISPLAY   *P41:5,*V2LON,*ULON,"Code     ":
                    *P51:5,"Description              "
.
. -------------------------------------------
. ------- display all the information -------
. -------------------------------------------
.
FRADS300  MOVE      PCFADESC,DIM25
          DISPLAY   *PCCOL:CVERT,*V2LON,PCFACODE,*HOFF,SP1,DIM25
          GOTO      FRADS100
.
. -------------------------------
. ------- new page needed -------
. -------------------------------
.
FRADS400  DISPLAY   *P1:24,"( )ontinue, ( )irst, ( )ext ? ",*EL:
                    *V2LON,*P2:24,ANSC,*P14:24,ANSF,*P23:24,ANSN
.
          KEYIN     *P31:24,*DV,UNDLN1:
                    *P31:24,*V2LON,ANS
.
          REP       UPPLOW,ANS
.
          MATCH     ANSC,ANS
          GOTO      FRADS999 IF EQUAL       * Continue selected
.
          MATCH     ANSF,ANS
          GOTO      PCPFRADS IF EQUAL       * First selected
.
. --------------------------------------
. ------- (N)ext screen selected -------
. --------------------------------------
.
FRADS700  DISPLAY   *P41:5,*EF;
          MOVE      SIX,CVERT               * first display row 
          MOVE      ONE,CCOL                * get back to LHS of screen
          GOTO      FRADS300                * display the data
.
FRADS999  RETURN
+
