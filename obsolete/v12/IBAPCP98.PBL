.******************************************************************************
.* System         : Patient Care Plans System                                 *
.* Program        : IBAPCP98                                                  *
.* Description    : System Parameter Maintenance                              *
.******************************************************************************
.* Date           : 20/12/89                                                  *
.* Author         : PAUL DUNCAN                                               *
.* Modifications  :                                                           *
.*        V9.02.00  30/12/2002 Tracey Nguyen srf 23497                        *
.*                  r5.10 Care Plans Port to V9                               *
.*----------------------------------------------------------------------------*
.*        V5.01.02  09/05/90 i chung                                          *
.*                  Fixed IBA screen MAINQST problem                          *
.*                  and added description display for Category option.        *
.*        V5.01.03  30/04/91 Peter Eddey                                      *
.*                  added PCCNUREL (Using related factors) &                  *
.*                       PCCNUDEF (using defining characteristics)            *
.*                  to hospital screen                                        *
.*        V5.01.04  18/11/1991    Justin Coates                               *
.*                  Added keyin of PCCNREL to PCCNEINT                        *
.*        V5.01.05  14/12/1993  Glenn Berry      Hawkes Bay Changes           *
.*                  Added PCCNFEVA                                            *
.*        V5.03.01  04/01/1996  Greg Horvat                                   *
.*                  Removed the menu program, security key, IBA screen        *
.*                  password & data directory keyins & changed to use a       *
.*                  standard keyin for the IBA screen security password       *
.*        V5.04.01  09/09/1996  Steve Armstrong   STH Mods                    *
.*                  Added PCCNSECL                                            *
.*                                                                            *
.******************************************************************************
.
.$$$$$$
.
.  System Parameter Maintenance (IBAPCP98)
.  ---------------------------------------
.
.  - Program description.
.    This is a maintenance program which modifies system control parameters 
.    for the Patient Care Plans system.  All parameters are stored in CONTROL
.
.  - Initialisation
.      - display screen header
.      - open CONTROLF
. 
.  - Processing Options
. 
.      0 = Exit
.
.      1 = System Audits
.          - read and display audit parameters from CONTROLF.  Currently none
.            are used, there are 26 in two columns on one screen.
.          - user can select audit and turn yes or no.
.
.      2 = I.B.A. Controlled Screen 1
.          - read and display IBA parameters and use small individual routines
.            to display a description to the values
.          - user can select the IBA parameters and alter them within their
.            ranges.
.          - write any changes at users choice.
.
.      3 = User Screen 1
.          - read and display User parameters and display the appropiate 
.            description to the values 
.          - user can select which parameters to change and post if required.
. 
.  - END
.
.$$$$$$
.*****************************************************************************
          INC       STD001FD
          INC       IBACONFD/INC
          INC       PCPCONFD/INC

.*****************************************************************************
.
CAT1      INIT      "Type of Condition"
CAT2      INIT      "Unknown"
CAT3      INIT      "Health Pattern"
CAT4      INIT      "Response Pattern"
CAT5      INIT      "ABC PRN REST"
LABSIZE1  INIT      "37 Char Labels"
LABSIZE2  INIT      "42 Char Labels"
STANDARD  INIT      "Standard"
UNKNOWN   INIT      "Unknown"
.
.*****************************************************************************
.                           VARIABLE DECLARATIONS
.*****************************************************************************
.
COL       FORM      2
DANSX     DIM       3
DANS1     DIM       3
DANS2     DIM       3
DANS3     DIM       3
DANS4     DIM       3
DANS5     DIM       3
DANS6     DIM       3
DANS7     DIM       3
DANS8     DIM       3
DANS9     DIM       3
DANS10    DIM       3
DANS11    DIM       3
DESC7     DIM       20
DESC8     DIM       10
MEALFLAG  DIM       6
MEALCODE  FORM      1
FORM8     FORM      8
COUNT     FORM      2
LINE80    DIM       80
PASS      DIM       8
REPFLAG   FORM      1
VERT      FORM      2
.
PRGID     INIT      "IBAPCP98"
PRGNAM    INIT      "Patient Care Plans Parameters Maintenance"
VERSION   INIT      "V12.00.00"
.
.*****************************************************************************
.MAINLINE
.*****************************************************************************
ML0000    CALL      INIT0000           * display header,open required files
.
ML1111    CALL      SELN0000           * select 1,2,3, 0
          BRANCH    EXIT,ML2222,ML4444,ML6666
          CHAIN     PGM
.
.---- Do THE Audit Flags Screen ----
.
ML2222    CALL      DPAD0000           * display audits from control f 
          CALL      ADPS0000           * audit screen position keyin
          BRANCH    EXIT,ML1111
          CALL      WAUD0000           * write audits to control f
          GOTO      ML1111
.
.---- Do THE User Controlled Screen 1 ----
.
ML4444    CALL      DUSR0000           * read and display user screen 1
          CALL      GUSR0000           * get user screen 1
          BRANCH    EXIT,ML1111
          CALL      WUSR0000           * write user parameters
          GOTO      ML1111
.
.---- Do THE IBA  Controlled Screen 1 ----
.
ML6666    CALL      IBASECUR                * Keyin IBA screen security passwd
          BRANCH    EXIT,ML1111
.
          CALL      DIBA0000           * read and display IBA screen 1
          CALL      GIBA0000           * Get IBA options post cancel select
          BRANCH    EXIT,ML1111
          CALL      WIBA0000           * write IBA parameter options
          GOTO      ML1111
.
.*****************************************************************************
.                                  INIT0000
.*****************************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P60:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
INIT9999  RETURN
.
+
.*****************************************************************************
.                   SELN0000
.   select main menu options and store in EXIT and branch in main line
.   1. system audits
.   2. User controlled parameters
.   3. I.B.A. controlled paramters
.*****************************************************************************
SELN0000  HLINE     *G37,2,61,80
          MOVE      ZERO,REPFLAG
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = System Audits":
                    *P1:6,*V2LON,TWO,*HOFF," = User   Controlled Screen 1":
                    *P1:7,*V2LON,THREE,*HOFF," = I.B.A. Controlled Screen 1":
                    *P1:9,*EL,"Select Option : _"
.
SELN2222  KEYIN     *P17:9,*EL,*V2LON,EXIT
.
          COMPARE   ZERO,EXIT
          GOTO      SELN9999 IF EQUAL
          BRANCH    EXIT,SELN9999,SELN9999,SELN9999
          BEEP
          GOTO      SELN2222
.
SELN9999  RETURN
+
.*****************************************************************************
.                   DPAD0000
.         Display audits from control f file
.*****************************************************************************
DPAD0000  READ      CONTROLF,FORTY1;*51,PCCNAUDA,PCCNAUDB,PCCNAUDC,PCCNAUDD:
                    PCCNAUDE,PCCNAUDF,PCCNAUDG,PCCNAUDH,PCCNAUDI,PCCNAUDJ:
                    PCCNAUDK,PCCNAUDL,PCCNAUDM,PCCNAUDN,PCCNAUDO,PCCNAUDP:
                    PCCNAUDQ,PCCNAUDR,PCCNAUDS,PCCNAUDT,PCCNAUDU,PCCNAUDV:
                    PCCNAUDW,PCCNAUDX,PCCNAUDY,PCCNAUDZ
.
          DISPLAY   *P61:2,*V2LON,"- System Audits ",*P1:3,*EF,*HOFF:
                    *P1:4,*V2LON," 1",*HOFF,". Database File             :":
                    *P1:5,*V2LON," 2",*HOFF,". Database File Description :":
                    *P1:6,*V2LON," 3",*HOFF,". Clinic Condition Factors  :":
                    *P1:7,*V2LON," 4",*HOFF,". Nurses Diagnosis          :":
                    *P1:8,*V2LON," 5",*HOFF,". Patient Related Factors   :":
                    *P1:9,*V2LON," 6",*HOFF,". Pat. Def. Characteristics :":
                    *P1:10,*V2LON," 7",*HOFF,". Patient outcomes          :":
                    *P1:11,*V2LON," 8",*HOFF,". Nursing Interventions     :":
                    *P1:12,*V2LON," 9",*HOFF,". Patient Descriptions      :":
                    *P1:13,*V2LON,"10",*HOFF,". Nurse                     :":
                    *P1:14,*V2LON,"11",*HOFF,". Evaluation                :":
                    *P1:15,*V2LON,"12",*HOFF,". Discharges                :":
                    *P1:16,*V2LON,"13",*HOFF,". Location                  :":
                    *P41:4,*V2LON,"14",*HOFF,". Audit N (Not used)       :":
                    *P41:5,*V2LON,"15",*HOFF,". Audit O (Not used)       :":
                    *P41:6,*V2LON,"16",*HOFF,". Audit P (Not used)       :":
                    *P41:7,*V2LON,"17",*HOFF,". Audit Q (Not used)       :":
                    *P41:8,*V2LON,"18",*HOFF,". Audit R (Not used)       :":
                    *P41:9,*V2LON,"19",*HOFF,". Audit S (Not used)       :":
                    *P41:10,*V2LON,"20",*HOFF,". Audit T (Not Used)       :":
                    *P41:11,*V2LON,"21",*HOFF,". Audit U (Not used)       :":
                    *P41:12,*V2LON,"22",*HOFF,". Audit V (Not used)       :":
                    *P41:13,*V2LON,"23",*HOFF,". Audit W (Not used)       :":
                    *P41:14,*V2LON,"24",*HOFF,". Audit X (Not used)       :":
                    *P41:15,*V2LON,"25",*HOFF,". Audit Y (Not used)       :":
                    *P41:16,*V2LON,"26",*HOFF,". Audit Z (Not used)       :"
.
          MOVE      ZERO,FORM2
          MOVE      THREE,CVERT
          MOVE      "33",CCOL
.
DPAD1111  ADD       ONE,FORM2
          ADD       ONE,CVERT
          COMPARE   "27",FORM2
          GOTO      DPAD9999 IF EQUAL
.
          COMPARE   "17",CVERT
          GOTO      DPAD2222 IF LESS
.
          MOVE      FOUR,CVERT
          MOVE      "72",CCOL
.
DPAD2222  LOAD      COUNT USING FORM2 OF PCCNAUDA,PCCNAUDB,PCCNAUDC,PCCNAUDD:
                    PCCNAUDE,PCCNAUDF,PCCNAUDG,PCCNAUDH,PCCNAUDI,PCCNAUDJ:
                    PCCNAUDK,PCCNAUDL,PCCNAUDM,PCCNAUDN,PCCNAUDO,PCCNAUDP:
                    PCCNAUDQ,PCCNAUDR,PCCNAUDS,PCCNAUDT,PCCNAUDU,PCCNAUDV:
                    PCCNAUDW,PCCNAUDX,PCCNAUDY,PCCNAUDZ
          MOVE      DYES,DANSX
          LOAD      DANSX USING COUNT OF DNO
          DISPLAY   *PCCOL:CVERT,*V2LON,DANSX;
          GOTO      DPAD1111
.
DPAD9999  RETURN
.*****************************************************************************
.                                 ADPS0000
.    Audit Post and Select or cancel items
.*****************************************************************************
ADPS0000
          CALL      MAINQST            * post cancel or select item
          COMPARE   ZERO,CCITEM
          GOTO      ADPS7000 IF EQUAL
          COMPARE   "-1",CCITEM
          GOTO      ADPS7770 IF EQUAL
          COMPARE   "27",CCITEM
          GOTO      ADPS0000 IF NOT LESS
.
          MOVE      THREE,CVERT
          MOVE      "33",CCOL
          ADD       CCITEM,CVERT
          COMPARE   "17",CVERT
          GOTO      ADPS2222 IF LESS
.
          MOVE      "72",CCOL
          SUB       "13",CVERT
.
ADPS2222  
          LOAD      FORM1 USING CCITEM OF PCCNAUDA,PCCNAUDB,PCCNAUDC,PCCNAUDD:
                    PCCNAUDE,PCCNAUDF,PCCNAUDG,PCCNAUDH,PCCNAUDI,PCCNAUDJ:
                    PCCNAUDK,PCCNAUDL,PCCNAUDM,PCCNAUDN,PCCNAUDO,PCCNAUDP:
                    PCCNAUDQ,PCCNAUDR,PCCNAUDS,PCCNAUDT,PCCNAUDU,PCCNAUDV:
                    PCCNAUDW,PCCNAUDX,PCCNAUDY,PCCNAUDZ
          DISPLAY   *PCCOL:CVERT;
.
          ADD       ONE,FORM1          * set up toggle switch for audits
          LOAD      FORM1,FORM1,ONE,ZERO
          BRANCH    FORM1,ADPS4444
          DISPLAY   *V2LON,DYES
          GOTO      ADPS5555
ADPS4444  DISPLAY   *V2LON,DNO
.
ADPS5555  STORE     FORM1 USING CCITEM OF PCCNAUDA,PCCNAUDB,PCCNAUDC,PCCNAUDD:
                    PCCNAUDE,PCCNAUDF,PCCNAUDG,PCCNAUDH,PCCNAUDI,PCCNAUDJ:
                    PCCNAUDK,PCCNAUDL,PCCNAUDM,PCCNAUDN,PCCNAUDO,PCCNAUDP:
                    PCCNAUDQ,PCCNAUDR,PCCNAUDS,PCCNAUDT,PCCNAUDU,PCCNAUDV:
                    PCCNAUDW,PCCNAUDX,PCCNAUDY,PCCNAUDZ
          GOTO      ADPS0000
.
ADPS7000  MOVE      ZERO,EXIT 
          GOTO      ADPS9999
ADPS7770  MOVE      ONE,EXIT
ADPS9999  RETURN
+
.
.*****************************************************************************
.                                 DIBA0000
.    Read and display IBA parameters and descriptions
.*****************************************************************************
DIBA0000  READ      CONTROLF,FORTY1;*77,PCCNSSEC,PCCNDCAT,PCCNUNIQ,*95,PCCNFEVA
.
          MOVE      CAT2,DESC7
          LOAD      DESC7,PCCNDCAT,CAT1,CAT2,CAT3,CAT4,CAT5
.
          MOVE      DNO,DESC8
          LOAD      DESC8,PCCNFEVA,DYES
.
          DISPLAY   *P61:2,*V2LON,"- I.B.A Screen 1 ",*HOFF:
                    *P1:4,*V2LON," 1",*HOFF:
                           ". (No longer used)                            :":
                    *P1:5,*V2LON," 2",*HOFF:
                           ". (No longer used)                            :":
                    *P1:6,*V2LON," 3",*HOFF:
                           ". (No longer used)                            :":
                    *P1:7,*V2LON," 4",*HOFF:
                           ". (No longer used)                            :":
                    *P1:8,*V2LON," 5",*HOFF:
                           ". Next unique Key                             :":
                    *P1:9,*V2LON," 6",*HOFF:
                           ". Security level                              :":
                    *P1:10,*V2LON," 7",*HOFF:
                           ". Category                                    :":
                    *P1:11,*V2LON," 8",*HOFF:
                           ". Force input of Evaluation after Mods?       :":
                    *P51:8,*V2LON,PCCNUNIQ,*P51:9,PCCNSSEC:
                    *P51:10,PCCNDCAT,*P55:10,DESC7:
                    *P51:11,DESC8
.
DIBA9999  RETURN
+
.
.******************************************************************************
.******************************************************************************
.                                  GIBA0000
.    get any changes from user to IBA systems parameters
.******************************************************************************
GIBA0000
          CALL      MAINQST
.
          COMPARE   ZERO,CCITEM                      * Post = 0
          GOTO      GIBA9000 IF EQUAL
          COMPARE   "-1",CCITEM                      * Cancel = -1
          GOTO      GIBA9990 IF EQUAL
.
          MOVE      "51",CCOL
          BRANCH    CCITEM,GIBA10,GIBA20,GIBA30,GIBA40,GIBA50,GIBA60,GIBA70:
                           GIBA80
          BEEP
          GOTO      GIBA0000
.
GIBA10    GOTO      GIBA0000
.
GIBA20    GOTO      GIBA0000
.
GIBA30    GOTO      GIBA0000
.
GIBA40    GOTO      GIBA0000
.
GIBA50    DISPLAY   *P1:24,*EL

          KEYIN     *P51:8,*DV,UNDLN10:
                    *P51:8,*V2LON,PCCNUNIQ,*P51:8,*DV,PCCNUNIQ
.
          GOTO      GIBA0000
.
GIBA60    DISPLAY   *P1:24,*EL

          KEYIN     *P51:9,*DV,UNDLN1:
                    *P51:9,*V2LON,PCCNSSEC,*P51:9,*DV,PCCNSSEC
.
          GOTO      GIBA0000
.
GIBA70    DISPLAY   *P1:24,*EL,*V2LON," 1",*HOFF,"=Type of Condition, ":
                               *V2LON,"3",*HOFF,"=Health Pattern, ":
                               *V2LON,"4",*HOFF,"=Response Pattern, ":
                               *V2LON,"5",*HOFF,"=ABC PRN REST"

          KEYIN     *P51:10,*EL,*DV,UNDLN1:
                    *P51:10,*V2LON,PCCNDCAT,*P51:10,*DV,PCCNDCAT
          BRANCH    PCCNDCAT,GIBA71,GIBA75,GIBA72,GIBA73,GIBA74
          GOTO      GIBA75
.
GIBA71    DISPLAY   *P55:10,"Type of Condition"
          GOTO      GIBA0000
.
GIBA72    DISPLAY   *P55:10,"Health Pattern"
          GOTO      GIBA0000
.
GIBA73    DISPLAY   *P55:10,"Response Pattern"
          GOTO      GIBA0000
.
GIBA74    DISPLAY   *P55:10,"ABC PRN REST"
          GOTO      GIBA0000
.
GIBA75    DISPLAY   *P1:24,*EL,*B,"Invalid Diagnostic Category.  ";
          CALL      HITENTER
          GOTO      GIBA70
.
.--------------------------------------
. Force input of evaluation after mods?
.--------------------------------------
GIBA80    MOVE      "11",CVERT
          MOVE      PCCNFEVA,ANS
          CALL      KYND0000
          MOVE      EXIT,PCCNFEVA
          GOTO      GIBA0000
.
.--------------------------------------
GIBA9000  MOVE      ZERO,EXIT
          GOTO      GIBA9999
.
GIBA9990  MOVE      ONE,EXIT
.
GIBA9999  RETURN
.******************************************************************************
.                                    DUSR0000
.    Read and display all user system parameters from control file
.******************************************************************************
DUSR0000  READ      CONTROLF,FORTY1;*89,PCCNUREL,PCCNUDEF,PCCNEREL,PCCNEDEF:
                    PCCNEOUT,PCCNEINT,*96,PCCNSECL
.
          MOVE      DNO,DANS1
          LOAD      DANS1,PCCNUREL,DYES
          MOVE      DNO,DANS2
          LOAD      DANS2,PCCNUDEF,DYES
          MOVE      DNO,DANS3
          LOAD      DANS3,PCCNEREL,DYES
          MOVE      DNO,DANS4
          LOAD      DANS4,PCCNEDEF,DYES
          MOVE      DNO,DANS5
          LOAD      DANS5,PCCNEOUT,DYES
          MOVE      DNO,DANS6
          LOAD      DANS6,PCCNEINT,DYES
.
          DISPLAY   *P61:2,*V2LON,"- User Screen 1 ":
                    *P1:4,*V2LON,"1. ",*HOFF:
                    "Using related factors          (Y/N)                    :":
                    *P1:5,*V2LON,"2. ",*HOFF:
                    "Using defining characteristics (Y/N)                    :":
                    *P1:6,*V2LON,"3. ",*HOFF:
                    "Related factors/defining char. editting being used (Y/N):":
                    *P1:7,*V2LON,"4. ",*HOFF:
                    "Defining char./outcomes editting being used (Y/N)       :":
                    *P1:8,*V2LON,"5. ",*HOFF:
                    "Outcomes/Interventions editting being used (Y/N)        :":
                    *P1:9,*V2LON,"6. ",*HOFF:
                    "Interventions/frequencies editting being used (Y/N)     :":
                    *P1:10,*V2LON,"7. ",*HOFF:
                    "Security level required for Reactivate Option           :":
                    *V2LON,*P62:4,DANS1,*P62:5,DANS2,*P62:6,DANS3,*P62:7,DANS4:
                    *P62:8,DANS5,*P62:9,DANS6,*P62:10,PCCNSECL
.
DUSR9999  RETURN
.
.*******************************************************************************
.                                 GUSR0000
.    Get user system parameters for changes EXIT=0, POST  EXIT=-1 CANCEL
.******************************************************************************
GUSR0000  CALL      MAINQST
          COMPARE   ZERO,CCITEM
          GOTO      GUSR9000 IF EQUAL       * post
          GOTO      GUSR9990 IF LESS        * cancel  
.
          MOVE      "51",CCOL
          UNPACK    SP4,CHOUR,CMIN     * clear time vars
.
          BRANCH    CCITEM,GUSR1000,GUSR1500,GUSR2000,GUSR2500,GUSR3000:
                           GUSR3500,GUSR4000
.
          BEEP
          GOTO      GUSR0000
.
GUSR1000  KEYIN     *P62:4,*EL,*DV,UNDLN1,*EL:
                    *P62:4,*V2LON,ANS:
                    *P62:4,*DV,ANS
.
          REP       UPPLOW,ANS
.
          MATCH     ANSN,ANS
          GOTO      GUSR1100 IF NOT EQUAL
          MOVE      ZERO,PCCNUREL
          MOVE      DNO,DANS1
          DISPLAY   *P62:4,*V2LON,DANS1;
          GOTO      GUSR0000
.
GUSR1100  MATCH     ANSY,ANS
          GOTO      GUSR1400 IF NOT EQUAL
          MOVE      ONE,PCCNUREL
          MOVE      DYES,DANS1
          DISPLAY   *P62:4,*V2LON,DANS1;
          GOTO      GUSR0000
.
GUSR1400  BEEP
          GOTO      GUSR1000
.
.         PCCNUDEF
.
GUSR1500  KEYIN     *P62:5,*EL,*DV,UNDLN1,*EL:
                    *P62:5,*V2LON,ANS:
                    *P62:5,*DV,ANS
          REP       UPPLOW,ANS
.
          MATCH     ANSN,ANS
          GOTO      GUSR1550 IF NOT EQUAL
          MOVE      ZERO,PCCNUDEF
          MOVE      DNO,DANS2
          DISPLAY   *P62:5,*V2LON,DANS2;
          GOTO      GUSR0000
.
GUSR1550  MATCH     ANSY,ANS
          GOTO      GUSR1599 IF NOT EQUAL
          MOVE      ONE,PCCNUDEF
          MOVE      DYES,DANS2
          DISPLAY   *P62:5,*V2LON,DANS2;
          GOTO      GUSR0000
.
GUSR1599  BEEP
          GOTO      GUSR1500
.
.         related factors - PCCNEREL
.
GUSR2000  KEYIN     *P62:6,*DV,UNDLN1,*EL:
                    *P62:6,*V2LON,ANS
          REP       UPPLOW,ANS
          MATCH     "Y",ANS
          GOTO      GUSR2100 IF EQUAL       * Yes
.
          MATCH     "N",ANS
          GOTO      GUSR2200 IF EQUAL       * No  
.
          BEEP
          GOTO      GUSR2000
.
GUSR2100  MOVE      ONE,PCCNEREL            * set to 1
          MOVE      DYES,DANS3              * set to Yes
          GOTO      GUSR2300
.
GUSR2200  MOVE      ZERO,PCCNEREL           * set to 0
          MOVE      DNO,DANS3               * set to No 
.
GUSR2300  DISPLAY   *P62:6,*V2LON,DANS3
          GOTO      GUSR0000
.
.         defining characters - PCCNEDEF
.
GUSR2500  KEYIN     *P62:7,*DV,UNDLN1,*EL:
                    *P62:7,*V2LON,ANS
          REP       UPPLOW,ANS
          MATCH     "Y",ANS
          GOTO      GUSR2600 IF EQUAL       * Yes
.
          MATCH     "N",ANS
          GOTO      GUSR2700 IF EQUAL       * No  
.
          BEEP
          GOTO      GUSR2500
.
GUSR2600  MOVE      ONE,PCCNEDEF            * set to 1
          MOVE      DYES,DANS4              * set to Yes
          GOTO      GUSR2800
.
GUSR2700  MOVE      ZERO,PCCNEDEF           * set to 0
          MOVE      DNO,DANS4               * set to No 
.
GUSR2800  DISPLAY   *P62:7,*V2LON,DANS4
          GOTO      GUSR0000
.
.         Outcomes - PCCNEOUT
.
GUSR3000  KEYIN     *P62:8,*DV,UNDLN1,*EL:
                    *P62:8,*V2LON,ANS
          REP       UPPLOW,ANS
          MATCH     "Y",ANS
          GOTO      GUSR3100 IF EQUAL       * Yes
.
          MATCH     "N",ANS
          GOTO      GUSR3200 IF EQUAL       * No  
.
          BEEP
          GOTO      GUSR3000
.
GUSR3100  MOVE      ONE,PCCNEOUT            * set to 1
          MOVE      DYES,DANS5              * set to Yes
          GOTO      GUSR3300
.
GUSR3200  MOVE      ZERO,PCCNEOUT           * set to 0
          MOVE      DNO,DANS5               * set to No 
.
GUSR3300  DISPLAY   *P62:8,*V2LON,DANS5
          GOTO      GUSR0000
.
.         interventions - PCCNEINT
.
GUSR3500  KEYIN     *P62:9,*DV,UNDLN1,*EL:
                    *P62:9,*V2LON,ANS
          REP       UPPLOW,ANS
          MATCH     "Y",ANS
          GOTO      GUSR3600 IF EQUAL       * Yes
.
          MATCH     "N",ANS
          GOTO      GUSR3700 IF EQUAL       * No  
.
          BEEP
          GOTO      GUSR3500
.
GUSR3600  MOVE      ONE,PCCNEINT            * set to 1
          MOVE      DYES,DANS6              * set to Yes
          GOTO      GUSR3800
.
GUSR3700  MOVE      ZERO,PCCNEINT           * set to 0
          MOVE      DNO,DANS6               * set to No 
.
GUSR3800  DISPLAY   *P62:9,*V2LON,DANS6
          GOTO      GUSR0000
.
GUSR4000  KEYIN     *P62:10,*V2LON,*DE,PCCNSECL
          GOTO      GUSR0000
.
.         set exit flags
.
GUSR9000  MOVE      ZERO,EXIT
          GOTO      GUSR9999
.
GUSR9990  MOVE      ONE,EXIT
.
GUSR9999  RETURN
.
.******************************************************************************
.                                 WAUD0000
.******************************************************************************
WAUD0000  WRITAB    CONTROLF,FORTY1;*51,PCCNAUDA,PCCNAUDB,PCCNAUDC,PCCNAUDD:
                    PCCNAUDE,PCCNAUDF,PCCNAUDG,PCCNAUDH,PCCNAUDI,PCCNAUDJ:
                    PCCNAUDK,PCCNAUDL,PCCNAUDM,PCCNAUDN,PCCNAUDO:
                    PCCNAUDP,PCCNAUDQ,PCCNAUDR,PCCNAUDS,PCCNAUDT:
                    PCCNAUDU,PCCNAUDV,PCCNAUDW,PCCNAUDX,PCCNAUDY,PCCNAUDZ
          DISPLAY   *P60:24,*EL,*V2LON,"*** Posted ***",*W2;
          RETURN
+
.******************************************************************************
.                                  WIBA0000
.******************************************************************************
WIBA0000  WRITAB    CONTROLF,FORTY1;*77,PCCNSSEC,PCCNDCAT,PCCNUNIQ,*95,PCCNFEVA
          DISPLAY   *P60:24,*EL,*V2LON,"*** Posted ***",*W2;
          RETURN
+
.******************************************************************************
.                                  WUSR0000
.******************************************************************************
WUSR0000  WRITAB    CONTROLF,FORTY1;*89,PCCNUREL,PCCNUDEF,PCCNEREL,PCCNEDEF:
                    PCCNEOUT,PCCNEINT,*96,PCCNSECL
          DISPLAY   *P60:24,*EL,*V2LON,"*** Posted ***",*W2;
WUSR9999  RETURN
+
.
.------------------------------------------------------------------------------
.         Yes/No standard keyin  with   DEFAULTS !!!!!!!!!!!!!!
.         Requires ANS (0=No, 1=Yes)
.------------------------------------------------------------------------------
.
KYND0000  REP       "1Y0N",ANS
          MOVE      ANS,DIM1                      * save default
.         
KYND1000  MOVE      DIM1,ANS
          KEYIN     *PCCOL:CVERT,*DV,ANS:
                    *PCCOL:CVERT,*V2LON,*RV,ANS:
                    *PCCOL:CVERT,*DV,ANS;
.
          TYPE      ANS                      * check if a number was input
          GOTO      KYND9000 IF EQUAL         * yes, invalid
.
          REP       "Y1y1N0n0",ANS           * convert valid answers to numerics
.
          TYPE      ANS                      * check if now a number
          GOTO      KYND9000 IF NOT EQUAL     * no, invalid
.
          MOVE      ANS,EXIT                 * set exit to Y/N value
.
          MOVE      DNO,DANS1                * assume answer was "No "
          LOAD      DANS1,EXIT,DYES,DNO      * unless otherwise stated
.
          DISPLAY   *PCCOL:CVERT,*V2LON,DANS1
          GOTO      KYND9999
.
KYND9000  BEEP
          GOTO      KYND1000
.
KYND9999  RETURN
+
.******************************************************************************
          INC       STD001IO
          INC       IBASECUR                * Keyin IBA screen security passwd

.******************************************************************************
