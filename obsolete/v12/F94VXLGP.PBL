.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   F94VXLGP.PBL                                              *
.* Desc      :   Fixit for PMSVX1FD Local GP Variable (Vis extension table)*
.***************************************************************************
.* Date      :   09/05/2005                                                *
.* Author    :   Peter Vela                                                *
.* Function  :                                                             * 
.*               This program will loop through the Admission Table        * 
.*               (PATMI1FD) and for every Admission record, update the     *
.*               corresponding Local GP variables in the Vis extension     *
.*               table (PMSVX1FD)                                          *
.*                                                                         *
.* Mods      :                                                             *
.*               V9.04.00  06/05/2005  Peter Vela       CAR 58320          *
.*                         Created fix program                             *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC 
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
COUNTER   FORM      3                      * count no. of records updated
.
.
. LOCAL CONSTANT DEFINTIONS
. -------------------------
.
.
. -------------------------
.
PRGID     INIT      "F94VXLGP"
PRGNAM    INIT      "Fixit for PMSVX1FD Local GP Variables"
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with fixit
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD               * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af" 
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af" 
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                              *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
   DISPLAY   *P1:10,"This program will loop through the Admission Table":
             *P1:11,"(PATMI1FD) and for every Admission record, update the":
             *P1:12,"corresponding Local GP variables in the Vis extension":
             *P1:13,"table (PMSVX1FD)"
.
          DISPLAY   *P1:15,"Before running this fixit, make a backup of":
                    *P1:16,"pmsvx1af.dat and pmsvx1af.idx"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION           * exit selected ?
          GOTO      OPTN9500 IF EQUAL      * yes
.
          BRANCH    COPTION,OPTN9000       * run fixit 
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*        This section reads the patmi1af table from the beginning        * 
.*        and looping through every record and updates the equival record *
.*        in the Vis Extension Table's (pmsvx1af) Local GP variables      *
.*        if they are empty                                               *
.**************************************************************************
.
PROC0000  MOVE      ZERO,COUNTER
          MOVE      SP70,KEY8                    * position key at start 
          CALL      RSPTMIS1 
PROC1000  CALL      RKPTMIS1           
          BRANCH    OVRCD,PROC9000               * exit at end of file
.
          MATCH     SP70,PTMSRFGP
          GOTO      PROC2000 IF NOT EQUAL
.
          GOTO      PROC1000
.
.         Populate the values to insert to the extension file
.
PROC2000  PACK      KEY8,DAADMNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,PROC1000
.
          MATCH     SP70,PMVXRHC1
          GOTO      PROC1000 IF NOT EQUAL
.
          PACK      PMVXRHC1,PTMSRFGP,SP70        * Moved from a DIM8 to a DIM10
          PACK      PMVXRH1G,PTMSGPPC,SP70        * Moved from a DIM8 to a DIM10
          MOVE      PTMIGPPT,PMVXRH1C
.
          CALL      UPPMVX11
          ADD       ONE,COUNTER                  
          DISPLAY   *P1:24,*EL,"Writing : ",*V2LON,KEY8
.
          GOTO      PROC1000                     * loop back to read next rec
.
PROC9000  DISPLAY *P1:24,*EL,COUNTER," records have been updated.  "; 
          CALL    HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
+
