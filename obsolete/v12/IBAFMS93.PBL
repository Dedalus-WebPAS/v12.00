. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS93                                             *
. * Desc      :   Closing Balance Report                               *
. **********************************************************************
. * Date      :   22.11.1990                                           *
. * Author    :   Paul Duncan                                          *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
.$$$$$$
.        PROGRAM IBAFMS93
.             open files and initialise variables
.             WHILE user wants to Produce report DO
.                  get desired parametres
.             END WHILE
.             IF user wants to post data
.                  THEN Produce printout
.             END IF
.        END
.$$$$$$
. Standard FMS Accounting System Include
.---------------------------------------
          INC       FMSSTDDF
          INC       FMSAMAFD/INC       * account master
          INC       FMSFPSFD/INC       * financial period summary
          INC       FMSLMAFD/INC       * ledger master
          INC       FMSLMCFD/INC       * financial year file
          INC       FMSCSAFD/INC
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
.
.
.==============================================================================
.LOCAL CONSTANT DEFINITION
.-------------------------
ASSET     INIT      "Asset"
.
EQUITY    INIT      "Equity"
.
LIABILTY  INIT      "Liability"
.
EXPENDIT  INIT      "Expenditure"
INCOME    INIT      "Income"
UNKNOWN   INIT      "Unknown"
.
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
BANKNAME  DIM       35
CREDNAME  DIM       35
DEBTNAME  DIM       35
DISCNAME  DIM       35
PAYMNAME  DIM       35
AGSTNAME  DIM       35
CGSTNAME  DIM       35
.
ABALANCE  FORM      10.2
ACRLBALC  FORM      10.2
ACRLTOTL  FORM      10.2
.
CBALANCE  FORM      10.2
CASHBALC  FORM      10.2
CASHTOTL  FORM      10.2
.
FIRSTREC  FORM      1
.
FNAME     DIM       8
.
MODE      FORM      1
.
PREVTYPE  DIM       1
.
TYPEDESC  DIM       11
FORMAT    INIT      "(999,999,999.99 "
CPRT      DIM       16 
APRT      DIM       16
.
ATOTINCF  DIM       16
ATOTEXPF  DIM       16
ATOTASSF  DIM       16
ATOTLIBF  DIM       16
ATOTEQTF  DIM       16
ATOTSURF  DIM       16
ATOTASDF  DIM       16
CTOTINCF  DIM       16
CTOTEXPF  DIM       16
CTOTASSF  DIM       16
CTOTLIBF  DIM       16
CTOTEQTF  DIM       16
CTOTSURF  DIM       16
CTOTASDF  DIM       16
.
ATOTINC   FORM      10.2
ATOTEXP   FORM      10.2
ATOTASS   FORM      10.2
ATOTLIB   FORM      10.2
ATOTEQT   FORM      10.2
ATOTSUR   FORM      10.2
ATOTASD   FORM      10.2
CTOTINC   FORM      10.2
CTOTEXP   FORM      10.2
CTOTASS   FORM      10.2
CTOTLIB   FORM      10.2
CTOTEQT   FORM      10.2
CTOTSUR   FORM      10.2
CTOTASD   FORM      10.2
.
PRGID     INIT      "IBAFMS93"
PRGNAM    INIT      "Closing Balance Report"
VERSION   INIT      "V12.00.00"
.
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML1000    CALL      KPAR0000           * keyin parameters
          BRANCH    EXIT,ML9999,ML9999
          CALL      PRNT0000           * save data
          GOTO      ML1000
.
ML9999    CHAIN     PGM                * chain back to menu
.
.******************************************************************************
.* INIT - Open Files                             Called by ML0000             *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          MOVE      ZERO,ATOTINC
          MOVE      ZERO,ATOTEXP
          MOVE      ZERO,ATOTASS
          MOVE      ZERO,ATOTLIB
          MOVE      ZERO,ATOTEQT 
          MOVE      ZERO,ATOTSUR
          MOVE      ZERO,ATOTASD
          MOVE      ZERO,CTOTINC
          MOVE      ZERO,CTOTEXP
          MOVE      ZERO,CTOTASS
          MOVE      ZERO,CTOTLIB
          MOVE      ZERO,CTOTEQT
          MOVE      ZERO,CTOTSUR
          MOVE      ZERO,CTOTASD
          DISPLAY   *P56:24,"Opening fmsamaaf";  * ledger file
          OPEN      FMSAMAA2,"fmsamaaf"
.
          DISPLAY   *P64:24,"fmslmaaf";  * ledger file
          OPEN      FMSLMAA1,"fmslmaaf"
.
          DISPLAY   *P64:24,"fmslmcaf";  * ledger file
          OPEN      FMSLMCA1,"fmslmcaf"
.
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.************************************************************************
.* CLR - Clear variables                                                *
.************************************************************************
CLR0000   UNPACK    SP70,FMLALEDG,FMLADESC,FMLCYEAR
.
CLR9999   RETURN
.************************************************************************
.* KPAR - Keyin the print parametres         Called by ML1000           *
.************************************************************************
KPAR0000  CALL      CLR0000            * clear variables
          CALL      RLMAFM00           * display screen
          BRANCH    MODE,KPAR2000
.
KPAR1000  CALL      KLDG0000           * keyin ledger
          MOVE      ZERO,MODE
          BRANCH    EXIT,KPAR9000,KPAR9000 * exit program
          BRANCH    MODE,KPAR3000
.
KPAR2000  CALL      KFYR0000           * keyin Financial year
          BRANCH    EXIT,KPAR1000,KPAR9000 * exit program
.
KPAR3000  CALL      MQST0000           * select item post cancel
          COMPARE   "0",CCITEM         * post
          GOTO      KPAR8000 IF EQUAL
.
          COMPARE   "-1",CCITEM        * cancel
          GOTO      KPAR9000 IF EQUAL
.
          MOVE      ONE,MODE           * set to change mode
          BRANCH    CCITEM,KPAR1000,KPAR2000
          BEEP
          GOTO      KPAR3000
.
KPAR8000  MOVE      ZERO,EXIT          * do printout
          GOTO      KPAR9999
.
KPAR9000  MOVE      ONE,EXIT           * exit prog
.
KPAR9999  RETURN 
.*************************************************************************
.* KLDG - Keyin ledger code                 Called by KPAR1000           *
.*        Returns EXIT = 0 - valid entry 1 -null 2 exitchar              *
.*************************************************************************
KLDG0000  MOVE      "4",CVERT           * set up variables for standard keyin
          MOVE      "28",CCOL
          CALL      KLMAFM00            * keyin ledger
          BRANCH    EXIT,KLDG9999,KLDG9999
.
          DISPLAY   *P28:4,*V2LON,FMLALEDG,*HOFF,*P35:4,FMLADESC
          GOTO      KLDG9999
.
KLDG9999  RETURN
.*************************************************************************
.* KFYR - Keyin Financial year              Called by KPAR2000           *
.*        Returns EXIT = 0 - valid entry 1 -null 2 exitchar              *
.*************************************************************************
KFYR0000  MOVE      "5",CVERT           * set up variables for standard keyin
          MOVE      "28",CCOL
          CALL      KLMCFM00            * keyin ledger
          BRANCH    EXIT,KLDG9999,KLDG9999
.
          DISPLAY   *P28:5,*V2LON,FMLCYEAR
          CLEAR     FNAME
          APPEND    "fmsp",FNAME
          APPEND    FMLCYEAR,FNAME
          RESET     FNAME
          REP       " 0",FNAME
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO     * can't do report if file doesn't exist
          OPEN      FMSFPSA1,FNAME
          TRAPCLR   IO
          BRANCH    OVRCD,KFYR8000
          GOTO      KLDG9999
KFYR8000  DISPLAY   *P1:24,*B,*EL,"Financial period summary file does not":
                                  " exist. ";                                 
          CALL      HITENTER
          GOTO      KFYR0000
.
KFYR9999  RETURN
.************************************************************************
.* REDISPLAY ROUTINES FOR STANDARD KEYINS                               *
.************************************************************************
RLMAFM00   DISPLAY   *P1:4,*EF,*V2LON," 1",*HOFF,". Ledger",*P26:4,":":
                     *P1:5,*V2LON," 2",*HOFF,". Financial Year",*P26:5,":"
.
           PACK      FMLCYEAR,FMLCYEAR,SP4
           MATCH     FMLCYEAR,SP4          * any year yet
           GOTO      RLMAFM99 IF EQUAL
.
           DISPLAY   *P28:5,*V2LON,FMLCYEAR
.
RLMAFM99   RETURN                     
.
RLMCFM00   DISPLAY   *P1:4,*EF,*V2LON," 1",*HOFF,". Ledger",*P26:4,": ":
                     *V2LON,FMLALEDG,*HOFF,*P35:4,FMLADESC:
                     *P1:5,*V2LON," 2",*HOFF,". Financial Year",*P26:5,":"
.
RLMCFM99   RETURN                     
.************************************************************************
.* PRNT - Loop thru the account master printing required records        *
.*                   called by : M1000                                  *
.************************************************************************
PRNT0000  MOVE      ZERO,CPAGENO              * set head80 variables
          MOVE      ONE,FIRSTREC
          CLOCK     TIME,CTIMEIS
          MOVE      ONE,CNOUNDLN
          PACK      KEY15,FMLALEDG,ONE,SP20
          CALL      RSFMAM2                   * position account file
.
          DISPLAY   *P50:24,"Printing : "
.
PRNT1000  CALL      RKFMAM2                   * read next record
          BRANCH    EXIT,PRNT5000
.
          MATCH     FMLALEDG,FMAMLEDG         * right ledger
          GOTO      PRNT5000 IF NOT EQUAL
.
          DISPLAY   *P61:24,*V2LON,FMAMACCT
          MOVE      FIVE,DIM1
          MATCH     FMAMTYPE,DIM1             * valid account type
          GOTO      PRNT5000 IF LESS
.
          CALL      CCBL0000                  * calculate closing balances
.
          BRANCH    FIRSTREC,PRNT3000
          MATCH     FMAMTYPE,PREVTYPE         * change in account type?
          GOTO      PRNT2000 IF EQUAL
.
          CALL      TOTL0000                  * print total
          GOTO      PRNT3000
.
PRNT2000  COMPARE   "60",CLNO                 * full page
          GOTO      PRNT4000 IF LESS
.
PRNT3000  CALL      NEWP0000                  * new page thanks 
.
PRNT4000  CALL      PLIN0000                  * print a line of data
          MOVE      ZERO,FIRSTREC
          MOVE      FMAMTYPE,PREVTYPE
          GOTO      PRNT1000
.
PRNT5000  BRANCH    FIRSTREC,PRNT6000
          CALL      TOTL0000                  * print last total
          CALL      BALC0000                  * print balance
          PRINT     *N,"*** End of Report ***"
          GOTO      PRNT9999
PRNT6000  CALL      NEWP0000
          PRINT     *N,"*** End of Report ***"
.
PRNT9999  RETURN          
.************************************************************************
.* CCBL - Calculate the cloaing cash and accrual balances               *
.*                   called by : PRNT1000                               *
.************************************************************************
CCBL0000  MOVE      ZERO,CASHBALC
          MOVE      ZERO,ACRLBALC
          PACK      KEY14,FMLALEDG,FMAMACCT
          CALL      RDFMFP1
          BRANCH    OVRCD,CCBL9999
.
          MOVE      FMFPCBF,CASHBALC          * cash brought forward
          ADD       FMFPC01,CASHBALC
          ADD       FMFPC02,CASHBALC
          ADD       FMFPC03,CASHBALC
          ADD       FMFPC04,CASHBALC
          ADD       FMFPC05,CASHBALC
          ADD       FMFPC06,CASHBALC
          ADD       FMFPC07,CASHBALC
          ADD       FMFPC08,CASHBALC
          ADD       FMFPC09,CASHBALC
          ADD       FMFPC10,CASHBALC
          ADD       FMFPC11,CASHBALC
          ADD       FMFPC12,CASHBALC
          ADD       FMFPC13,CASHBALC
.
          MOVE      FMFPABF,ACRLBALC          * accrual brought forward
          ADD       FMFPA01,ACRLBALC
          ADD       FMFPA02,ACRLBALC
          ADD       FMFPA03,ACRLBALC
          ADD       FMFPA04,ACRLBALC
          ADD       FMFPA05,ACRLBALC
          ADD       FMFPA06,ACRLBALC
          ADD       FMFPA07,ACRLBALC
          ADD       FMFPA08,ACRLBALC
          ADD       FMFPA09,ACRLBALC
          ADD       FMFPA10,ACRLBALC
          ADD       FMFPA11,ACRLBALC
          ADD       FMFPA12,ACRLBALC
          ADD       FMFPA13,ACRLBALC
.
CCBL9999  RETURN 
.************************************************************************
.* TOTL - Print the total for this account type                         *
.*                   called by : PRNT4000                               *
.************************************************************************
TOTL0000  CALL      UND80 
          MOVE      FORMAT,CPRT
          MOVE      FORMAT,APRT
          FEDIT     CASHTOTL,CPRT
          FEDIT     ACRLTOTL,APRT
          PRINT     *16,"Total ",TYPEDESC,*49,CPRT,APRT:
                    *N,*49,"--------------------------------"
          MOVE      PREVTYPE,F1
          STORE     CASHTOTL,F1,CTOTINC,CTOTEXP,CTOTASS,CTOTLIB,CTOTEQT
          STORE     ACRLTOTL,F1,ATOTINC,ATOTEXP,ATOTASS,ATOTLIB,ATOTEQT
          ADD       CASHTOTL,CBALANCE
          ADD       ACRLTOTL,ABALANCE
          MOVE      ZERO,CASHTOTL
          MOVE      ZERO,ACRLTOTL
TOTL9999  RETURN 
.************************************************************************
.* BALC - Print the Balance of the report                               *
.*                   called by : PRNT4000                               *
.************************************************************************
BALC0000  
          MOVE      CTOTINC,CTOTSUR
          ADD       CTOTEXP,CTOTSUR
          MOVE      ATOTINC,ATOTSUR
          ADD       ATOTEXP,ATOTSUR
.
          MOVE      CTOTASS,CTOTASD
          ADD       CTOTLIB,CTOTASD
          MOVE      ATOTASS,ATOTASD
          ADD       ATOTLIB,ATOTASD
.
          MOVE      FORMAT,ATOTINCF
          MOVE      FORMAT,ATOTEXPF
          MOVE      FORMAT,ATOTASSF
          MOVE      FORMAT,ATOTLIBF
          MOVE      FORMAT,ATOTEQTF
          MOVE      FORMAT,ATOTSURF
          MOVE      FORMAT,ATOTASDF
          MOVE      FORMAT,CTOTINCF
          MOVE      FORMAT,CTOTEXPF
          MOVE      FORMAT,CTOTASSF
          MOVE      FORMAT,CTOTLIBF
          MOVE      FORMAT,CTOTEQTF
          MOVE      FORMAT,CTOTSURF
          MOVE      FORMAT,CTOTASDF
.
          FEDIT     ATOTINC,ATOTINCF
          FEDIT     ATOTEXP,ATOTEXPF
          FEDIT     ATOTASS,ATOTASSF
          FEDIT     ATOTLIB,ATOTLIBF
          FEDIT     ATOTEQT,ATOTEQTF
          FEDIT     ATOTSUR,ATOTSURF
          FEDIT     ATOTASD,ATOTASDF
          FEDIT     CTOTINC,CTOTINCF
          FEDIT     CTOTEXP,CTOTEXPF
          FEDIT     CTOTASS,CTOTASSF
          FEDIT     CTOTLIB,CTOTLIBF
          FEDIT     CTOTEQT,CTOTEQTF
          FEDIT     CTOTSUR,CTOTSURF
          FEDIT     CTOTASD,CTOTASDF
.
          PRINT     *N,*N,*20,"Closing Balance Report Summary",*N:
                    *20,"------------------------------",*N:
                    *46,"Cash          Accrual",*N:
                    *46,"----          -------",*N:
                 "Total Income                    : ",CTOTINCF,SP1,ATOTINCF,*N:
                 "Total Expenditure               : ",CTOTEXPF,SP1,ATOTEXPF,*N:
                    *36,"--------------- ----------------",*N:
                 "Surplus/Deficit Carried Forward : ",CTOTSURF,SP1,ATOTSURF:
                    *N,*N,*N:
                 "Total Assets                    : ",CTOTASSF,SP1,ATOTASSF,*N:
                 "Total Liabilities               : ",CTOTLIBF,SP1,ATOTLIBF,*N:
                    *36,"--------------- ----------------",*N:
                 "Accumulated Surplus/Deficit     : ",CTOTASDF,SP1,ATOTASDF,*N:
                    *36,"=============== ================",*N,*N,*N:
                 "Total Equity                    : ",CTOTEQTF,SP1,ATOTEQTF,*N:
                 "Plus Surplus/Deficit C/F        : ",CTOTSURF,SP1,ATOTSURF,*N:
                    *36,"--------------- ----------------"
          ADD       ATOTSUR,ATOTEQT
          ADD       CTOTSUR,CTOTEQT
          MOVE      FORMAT,ATOTEQTF
          MOVE      FORMAT,CTOTEQTF
          FEDIT     ATOTEQT,ATOTEQTF
          FEDIT     CTOTEQT,CTOTEQTF
          PRINT     "Accumulated Surplus/Deficit     : ",CTOTEQTF,SP1,ATOTEQTF:
                    *N,*36,"=============== ================"
.
          MOVE      ZERO,CBALANCE
          MOVE      ZERO,ABALANCE
          MOVE      ZERO,CTOTINC
          MOVE      ZERO,CTOTEXP
          MOVE      ZERO,CTOTASS
          MOVE      ZERO,CTOTLIB
          MOVE      ZERO,CTOTEQT
          MOVE      ZERO,ATOTINC
          MOVE      ZERO,ATOTEXP
          MOVE      ZERO,ATOTASS
          MOVE      ZERO,ATOTLIB
          MOVE      ZERO,ATOTEQT
.
BALC9999  RETURN 
.************************************************************************
.* NEWP - Print new page                          Called by PRNT2000    *       
.************************************************************************
NEWP0000  CALL     HEAD80
          PRINT    *N,*1,"Ledger : ",FMLALEDG,SP1,FMLADESC,SP3:
                   "Financial Year : ",FMLCYEAR
.
          MOVE     FMAMTYPE,FORM1
          LOAD     TYPEDESC,FORM1,INCOME,EXPENDIT,ASSET,LIABILTY,EQUITY
          PRINT    *22,"Account Type : ",TYPEDESC
          CALL     UND80
          PRINT    "Account      Description                                 ":
                   "  Cash         Accrual"
          CALL     UND80
          ADD      FOUR,CLNO
.
NEWP9999  RETURN 
.************************************************************************
.* PLIN - Print a line on the report           Called by PRNT5000       *
.************************************************************************
PLIN0000  
          COMPARE   ZERO,CASHBALC
          GOTO      PLIN1000 IF NOT EQUAL
          COMPARE   ZERO,ACRLBALC
          GOTO      PLIN9999 IF EQUAL
.
PLIN1000  MOVE      FORMAT,CPRT
          MOVE      FORMAT,APRT
          FEDIT     CASHBALC,CPRT
          FEDIT     ACRLBALC,APRT
          PRINT     FMAMACCT,SP1,FMAMDESC,CPRT,APRT
          ADD       ONE,CLNO
          ADD       CASHBALC,CASHTOTL
          ADD       ACRLBALC,ACRLTOTL
.
PLIN9999  RETURN 
.******************************************************************************
.***********************************************************************
.         Subroutine to SELECT ITEM, RUN REPORT, EXIT
.-----------------------------------------------------------------------
.
MQST0000  MOVE      ZERO,CCITEM
          MOVE      SP10,CCITEMD
          KEYIN     *P1:24,*EL,"Select Item, (",*V2LON,"R",*HOFF,")un ":
                    "Report, e(":
                    *V2LON,"X",*HOFF,")it ? ",*V2LON,*JR,CCITEMD;
.
          RESET     CCITEMD
          GOTO      MQST0000 IF EOS
.
          REP       UPPLOW,CCITEMD
.
          MATCH     "  X",CCITEMD
          GOTO      MQST1000 IF EQUAL
.
          MATCH     "  R",CCITEMD
          GOTO      MQST2000 IF EQUAL
.
          TYPE      CCITEMD
          GOTO      MQST0500 IF NOT EQUAL
.
          MOVE      CCITEMD,CCITEM
          COMPARE   ONE,CCITEM
          GOTO      MQST0500 IF LESS
.
          GOTO      MQST9999
.
MQST0500  BEEP
          GOTO      MQST0000
.
MQST1000  MOVE      "-1",CCITEM
          GOTO      MQST9999
.
MQST2000  MOVE      "0",CCITEM
.
MQST9999  RETURN
+
          INC       FMSSTDCD           * FMS Acc. standard routines
          INC       FMSAMAIO/INC       * account master
          INC       FMSFPSIO/INC       * financial period summary
          INC       FMSLMAIO/INC       * ledger master
          INC       FMSLMAKY
          INC       FMSLMCIO/INC       * financial year file
          INC       FMSLMCKY
