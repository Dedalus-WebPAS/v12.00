.*****************************************************************************
.*    Program      : F02MRBRH                                                *
.*    Description  : Conversion mrtbrhaf to new File layout                  *
.*                                                                           *
.*    Author       : Mike Laming                                             *
.*    Date         : 27/05/2011                                              *
.*    Modifications:                                                         *
.*        V10.03.02 26/03/2013  Ebon Clements CAR 283238                     *
.*                  Changed FINDLOCS to read mrtlocaf using index 4          *
.*        V10.03.01 06/06/2012  Steve Armstrong   CAR 266826                 *
.*                  Changes to only populate new field for both c-isam &     *
.*                  Oracle.                                                  *
.*****************************************************************************
.*        V10.02.00 27/05/2011  Mike Laming   CAR 240724                     *
.*                  Created Fixit to add MRBRRHOS, populate Hospital, change *
.*                  Index 3                                                  *
.*****************************************************************************
.
          INC       STD001FD
.
.         New file definition
.
MRTBRHA1   IFILE    SQL, FIXED=169, KEY="U1-10"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
MRBRRQNM  DIM       10     10       1     Request Number
MRBRDTRI  DIM       8      8        11    Date Required
MRBRTMRI  DIM       5      5        19    Time Required 
MRBRDTRE  DIM       8      8        24    Date Requested
MRBRTMRE  DIM       5      5        32    Time Requested      
MRBRLOCA  DIM       4      4        37    MR Required Location       
MRBRNUMR  FORM      3      3        41    Number of records requested
MRBRREQS  DIM       35     35       44    Person Requesting Record       
MRBRPHON  DIM       20     20       79    Requestor Phone No/Extension No
MRBRPAGR  DIM       20     20       99    Requestor Pager No     
MRBRRHOS  DIM       3      3        119   MR Required Hospital ID
MRBRSPAR  DIM       47     47       122   Spare Variable
.
.End of Record                      169
.
.
MRTLOCA4  IFILE     SQL, FIXED=165, KEY="U1-4,55-57"
.
.Name     Type      Length  Phys.  Start  Description
.-------  ----      ------  -----  -----  ------------------
MRLOCODE  DIM       4          4       1  Location Code
MRLODESC  DIM       30        30       5  Description
MRLOTYPE  DIM       3          3      35  Type (Cat LT)
MRLOINDC  DIM       1          1      38  Indicator   1 = Internal       
.                                                     2 = External       
MRLOSTAT  FORM      1          2      39  Status Indicator 0 = Active    
.                                                          1 = Non Active
MRLOUSAG  FORM      1          2      41  Usage Indicator  0 = Not Used  
.                                                          1 = Used      
MRLOPRNT  DIM       6          6      43  Printer                        
MRLOSTCD  DIM       6          6      49  Stationery Code                
MRLOHOSP  DIM       3          3      55  Hospital ID (pathspaf)         
MRLOPRLC  DIM       1          1      58  Priority location (1=yes)      
MRLOBPRN  DIM       6          6      59  Bulk Record Request Printer
MRLOTREC  DIM       1          1      65  Using Tracking Receipt For This
.                                         Location  0=No, 1=Yes
MRLOSPAR  DIM       99         99     66  Spare
.
.End of Record                        165
.
.
.
DISNUM    FORM      4
IBCNMHOS  FORM      1                       * Multi Hospital flag
RECNUM    FORM      8
.
.
PRGNAM    INIT      "CONVERSION MRTBRHFD"
PRGID     INIT      "F02MRBRH"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
MAIN1000  CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN2000              * run fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN2000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN1000:               * no
                          MAIN9999                * cancel
.
.         Running c-isam/oracle fix
.
MAIN5000  CALL      FFIX0000                      * run field fix
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.* Returns:  EXIT  0 = Ok to continue                               *
.*                 1 = Exit program                                 *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening ":
                    *P64:24,"mrtbrhaf"
          OPEN      MRTBRHA1,"mrtbrhaf"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          BRANCH    IBCNMHOS,INIT9000            * using multi-hospital
.
          DISPLAY   *P1:24,*EL,*B,"Fixit only required for multi-hospital.  ";
          CALL      HITENTER
          GOTO      INIT9100
.
INIT9000  DISPLAY   *P64:24,"mrtlocaf"
          OPEN      MRTLOCA4,"mrtlocaf"
.
          MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9100  MOVE      ONE,EXIT
.         
INIT9999  RETURN
.
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**********************************************************************
.*                               FINDLOCs                             *
.*        Read through Location records to get Hospital IDs           *
.**********************************************************************
.
FINDLOCS  PACK      KEY7,MRBRLOCA,SP70
          CALL      RSMRLOC4
FINDLO10  CALL      RKMRLOC4
          BRANCH    OVRCD,FINDLO99
.
          MATCH     MRLOCODE,MRBRLOCA       * same location code ?
          GOTO      FINDLO99 IF NOT EQUAL
.
          MOVE      MRLOHOSP,MRBRRHOS     * yes - set required hospital
.
FINDLO99  RETURN
+
.*****************************************************************************
.*                            FFIX0000                                       *
.*                    Oracle fix to "mrtbrhaf"                               *
.*****************************************************************************
.
FFIX0000  DISPLAY   *P1:20,*EL,"Record No. :";
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
.
          PACK      KEY10,SP70
          CALL      RSMRBRH1                      * position at start of file
FFIX0500  CALL      RKMRBRH1                      * read next record
          BRANCH    OVRCD,FFIX9000                * eof - finished
.
          ADD       ONE,RECNUM                    * increment record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MOVE      SP70,MRBRRHOS
          CALL      FINDLOCS                * get Hospital IDs for each Location
          CALL      UPMRBRH1
.
          GOTO      FFIX0500                     * get next record
.
FFIX9000  CLOSE     MRTBRHA1
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:24,*EL,"Update completed.  ";
          CALL      HITENTER
.
FFIX9999  RETURN
+
.*****************************************************************************
.
.         New mrtbrhaf I/O Routines
.
RSMRBRH1  RESET     KEY10
          READ      MRTBRHA1,KEY10;;
          RETURN
.
RKMRBRH1  MOVE      ZERO,OVRCD
          READKS    MRTBRHA1;MRBRRQNM,MRBRDTRI,MRBRTMRI,MRBRDTRE,MRBRTMRE:
                        MRBRLOCA,MRBRNUMR,MRBRREQS,MRBRPHON,MRBRPAGR,MRBRRHOS:
                        MRBRSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
UPMRBRH1  UPDATE    MRTBRHA1;MRBRRQNM,MRBRDTRI,MRBRTMRI,MRBRDTRE,MRBRTMRE:
                        MRBRLOCA,MRBRNUMR,MRBRREQS,MRBRPHON,MRBRPAGR,MRBRRHOS:
                        MRBRSPAR
          GOTO      UPDFLOCK IF LOCKED
          RETURN
.
.         mrtlocaf I/O routines
.
RSMRLOC4  MOVE     ZERO,OVRCD
          RESET    KEY7
          READ     MRTLOCA4,KEY7;;
          RETURN
.
.
RKMRLOC4  MOVE     ZERO,OVRCD
          READKS   MRTLOCA4;MRLOCODE,MRLODESC,MRLOTYPE,MRLOINDC,MRLOSTAT:
                            MRLOUSAG,MRLOPRNT,MRLOSTCD,MRLOHOSP,MRLOPRLC:
                            MRLOBPRN,MRLOTREC,MRLOSPAR
          GOTO     OVERCOND IF OVER
          RETURN
.
          INC       STD001IO
