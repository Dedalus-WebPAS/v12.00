.******************************************************************************
.* System         : Accident and Emergency                                    *
.* Program        : IBAAAE12.PBL                                              *
.* Name           : Patient Waiting Enquiry                                   *
.******************************************************************************
.* Date           : 02/09/1988                                                *
.* Author         : Malcom "I pooch dogs" Hayse                               *
.* Function       : Display all patients still waiting                        *
.* Modifications  :                                                           *
.*        V5.08.01  23/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.03.02  16/04/1996    Howellsy   5.04                             *
.*                  Changed Accident & Emergency to Emergency.                *
.*        V5.03.B0  16/05/1995  Paul Howells         SRF 134950               *
.*                  Changed Priority Code to a hospital controlled code       *
.*        V5.02.01  11/12/1994  Graeme Williams                               *
.*                  Added option of a fifth priority code                     *
.******************************************************************************
.
          INC       STD001FD
          INC       AAECONFD/INC
          INC       AAEDE1FD/INC
          INC       AAEDEBFD/INC
          INC       AAEPRIFD/INC
          INC       PATCODFD/INC
          INC       PATMA1FD/INC
.
.         Extra variables
.
CODEA1    INIT      "A1"
LN55      FORM      "55"
RECCOUNT  FORM      2
DIM30     DIM       30
DPRIT     DIM       12
DPRIT1    INIT      "Urgent    "
DPRIT2    INIT      "Moderate  "
DPRIT3    INIT      "Non Urgent"
MOREDISP  FORM      1
PRDESC    DIM       12
SCREEN    FORM      2
SAVKEY24  DIM       24
COUNT     FORM      2
NAME22    DIM       22
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
A1DESC    DIM       20
.
PRGID     INIT      "IBAAAE12"
PRGNAM    INIT      "Patient Waiting Enquiry"
VERSION   INIT      "V12.00.00"
+
.*********************************************************************
.         mainline code
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000                * run the report ??
          BRANCH    EXIT,ML9999
.
          BRANCH    OPTION,ML3000,ML5000    * which sequence?
.
ML3000    CALL      DISP0000                * display all patients
          BRANCH    EXIT,ML9999             * exit program
          GOTO      ML1000
.
ML5000    CALL      DDAT0000                * display in date sequence
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.         initilisation
.*********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,THIRTY;*207,AECNUVPT,AECNDXPA,AECNDXPB,AECNDXPC:
                                         AECNDXPD
          READ      CONTROLF,SEVENTY8;*6,AECNDXPE
.
          DISPLAY   *P64:24,"aaepriaf";
          OPEN      AAEPRIA1,"aaepriaf"
          OPEN      AAEPRIA2,"aaepriaf"
.
          DISPLAY   *P64:24,"aaede1af";
          OPEN      AAEDE1A1,"aaede1af"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          CALL      GPDX0000                * get priority desc
.
INIT9999  RETURN
+
.*********************************************************************
.         run the program
.*********************************************************************
OPTN0000  
          HLINE     *G37,2,52,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Priority/Date/Time Sequence":
                    *P1:6,*V2LON,TWO,*HOFF," = Reverse date Sequence"
.
OPTN1000  KEYIN     *P1:8,*EF,"Please Select : ",*V2LON,OPTION;
          MOVE      ONE,EXIT
          COMPARE   ZERO,OPTION
          GOTO      OPTN9999 IF EQUAL
          MOVE      ZERO,EXIT
          BRANCH    OPTION,OPTN9000,OPTN9100
          BEEP
          GOTO      OPTN1000
.
OPTN9000  DISPLAY   *P52:2,*V2LON,"- by Priority "
          GOTO      OPTN9999
.
OPTN9100  DISPLAY   *P52:2,*V2LON,"- by Date "
.
OPTN9999  RETURN
+
.*********************************************************************
.         display the data
.*********************************************************************
DISP0000  DISPLAY   *P1:3,*EF,*P1:5,*ULON,*V2LON:
                    *P1:4,"Priority",*P10:4,"   Date   ",*P22:4,"Time ":
                    *P30:4," U/R No ",*P39:4," Emerg No ":
                    *P50:4,"Patient Name / Complaint"
.
          MOVE      FIVE,CVERT
          MOVE      SP30,KEY24
          CALL      RDSPRIA1
.
DISP1000  CALL      RDKPRIA1
          BRANCH    OVRCD,DISP5000
.
          MOVE      ADPNUMB,KEY8
          CALL      RDDETA1
          BRANCH    OVRCD OF DISP1000
.
          MATCH     SP5,ADASEEN           ** DO NOT INCLUDE DISCHARGE
          GOTO      DISP1000 IF NOT EQUAL
.
          MOVE      ADAURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF DISP1000
.
          UNPACK    ADPDATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      PGNAME,ANS
          PACK      DIM30 WITH ANS,DOT,PSNAME,SP20
.
          DISPLAY   *P1:CVERT,SP3,ADAPRTY:
                    *P10:CVERT,CPCDATE,*P22:CVERT,ADPTIME:
                    *P30:CVERT,ADAURNO:
                    *P40:CVERT,ADANUMB:
                    *P50:CVERT,DIM30
          ADD       ONE,CVERT
.
          MATCH     SP3,ADAUSR1
          GOTO      DISP2000 IF EQUAL
.
          MOVE      SP30,A1DESC
          PACK      KEY5 WITH CODEA1,ADAUSR1
          CALL      RDCODE1
          BRANCH    OVRCD,DISP2000
.
          MOVE      ADADIAG,DIM30
          MOVE      TDESC,A1DESC
          DISPLAY   *P12:CVERT,A1DESC
.
DISP2000  MOVE      ADADIAG,DIM30
          DISPLAY   *P50:CVERT,DIM30
          ADD       ONE,CVERT
.
          COMPARE   TWENTY2,CVERT
          GOTO      DISP1000 IF LESS        * more room to display
.
DISP5000  COMPARE   FIVE,CVERT
          GOTO      DISP8900 IF EQUAL       * nothing displayed
.
          KEYIN     *P1:24,*EL," (":
                    *V2LON,*DV,ANSP,*HOFF,")rint, (":
                    *V2LON,*DV,ANSE,*HOFF,")xit, (":
                    *V2LON,*DV,ANSR,*HOFF,")efresh, (":
                    *V2LON,*DV,ANSM,*HOFF,")ore ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
.
          CMATCH    ANSR,ANS
          GOTO      DISP0000 IF EQUAL       * display from start
.
          CMATCH    ANSE,ANS
          GOTO      DISP9100 IF EQUAL       * exit program
.
          CMATCH    ANSP,ANS
          GOTO      DISP7000 IF EQUAL       * print details
.
          CMATCH    ANSM,ANS
          GOTO      DISP6000 IF EQUAL       * see more
.       
          GOTO      DISP9000                * return to menu
.
DISP6000  MOVE      FIVE,CVERT
          DISPLAY   *P1:CVERT,*EF
          GOTO      DISP1000
.
.         print the details
.
DISP7000  CALL      PRNT0000
          GOTO      DISP0000
.
DISP8900  DISPLAY   *P1:24,*EL,*B,"No Patients to be seen.  ";
          CALL      HITENTER
.
DISP9000  MOVE      ZERO,EXIT
          GOTO      DISP9999
.
DISP9100  MOVE      ONE,EXIT
.
DISP9999  RETURN
+
.*************************************************************************
.*  DDAT0000  :  Display (from temp file) in reverse date sequence       *
.*************************************************************************
DDAT0000  DISPLAY   *P1:3,*EF,*P1:5,*ULON,*V2LON:
                    *P1:4,"Date    ",*P12:4,"Time ",*P20:4,"U/R No. ":
                    *P30:4,"Emerg No ",*P42:4,"Patient Name/Complaint ":
                    *P68:4,"Priority"
.
          MOVE      ONE,RECCOUNT
          MOVE      ONE,SCREEN
          MOVE      ZERO,MOREDISP
          MOVE      FIVE,CVERT
          PACK      KEY24,Z30
          CALL      RDSPRIA2
DDAT1000  CALL      RDPPRIA2
          BRANCH    OVRCD,DDAT5000                * no more data to display
.
          MOVE      ADPNUMB,KEY8
          CALL      RDDETA1
          BRANCH    OVRCD OF DDAT1000
.
          MATCH     SP5,ADASEEN           ** DO NOT INCLUDE DISCHARGE
          GOTO      DDAT1000 IF NOT EQUAL
.
          MOVE      ADAURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF DDAT1000
.
. check if enough room on screen to display record
.
          COMPARE   "23",CVERT
          GOTO      DDAT1500 IF LESS
.
. the screen if full
.
          MOVE      ONE,MOREDISP                  * there is more data to disp
          CALL      MORE0000
          BRANCH    EXIT,DDAT9999:                * Exit
                         DDAT7000:                * Print
                         DDAT1500:                * Forward
                         DDAT1400                 * Backward
.
. backward was selected
.
DDAT1400  MOVE      ONE,COUNT
          MOVE      SAVKEY24,KEY24
          CALL      RDPRIA2                  * position on 1st rec on screen
          WHILE     COUNT<10
DDAT1450    CALL      RDKPRIA2
            BRANCH    OVRCD,DDAT1470
            MOVE      ADPNUMB,KEY8
            CALL      RDDETA1
            BRANCH    OVRCD,DDAT1450
            MATCH     SP5,ADASEEN           ** DO NOT INCLUDE DISCHARGE
            GOTO      DDAT1450 IF NOT EQUAL
            MOVE      ADAURNO,KEY8
            CALL      RDMAST1
            BRANCH    OVRCD,DDAT1450
            ADD       ONE,COUNT
          DO
.
. we have read back(forward) far enough
.
DDAT1470  MOVE      FIVE,CVERT
          DISPLAY   *P1:CVERT,*EF
          MOVE      ONE,RECCOUNT
.
. display the current record
.
DDAT1500  UNPACK    ADPDATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      PGNAME,ANS
          PACK      NAME22,ANS,DOT,PSNAME,SP20
.
          MOVE       SP20,TDESC
          MATCH      SP3,ADAPRTY
          IF         !@EQUAL
            PACK       KEY5,ANSA,ANSA,ADAPRTY
            CALL       RDCODE1
          ENDIF
          MOVE       TDESC,PRDESC
...          MOVE      ADAPRTY,FORM1
...          MOVE      ADAPRTY,PRDESC
...          LOAD      PRDESC,FORM1,AECNDXPA,AECNDXPB,AECNDXPC,AECNDXPD,AECNDXPE
.
          DISPLAY   *P1:CVERT,CPCDATE:
                    *P12:CVERT,ADPTIME:
                    *P20:CVERT,ADAURNO:
                    *P31:CVERT,ADANUMB:
                    *P42:CVERT,NAME22:
                    *P68:CVERT,PRDESC
          ADD       ONE,CVERT
.
          MATCH     SP3,ADAUSR1
          GOTO      DDAT2000 IF EQUAL
.
          MOVE      SP30,A1DESC
          PACK      KEY5,CODEA1,ADAUSR1
          CALL      RDCODE1
          BRANCH    OVRCD,DDAT2000
.
          MOVE      ADADIAG,DIM30
          MOVE      TDESC,A1DESC
          DISPLAY   *P12:CVERT,A1DESC
.
DDAT2000  MOVE      ADADIAG,DIM30
          DISPLAY   *P50:CVERT,DIM30
          ADD       ONE,CVERT
          IF        RECCOUNT=1
            PACK      SAVKEY24,ADPDATE,ADPTIME,ADPPRTY,ADPNUMB
          ENDIF
          ADD       ONE,RECCOUNT
          GOTO      DDAT1000
.
. no more data to display
.
DDAT5000  MOVE      ZERO,MOREDISP                 * there is more data to disp
          CALL      MORE0000
          BRANCH    EXIT,DDAT9999:                * Exit
                         DDAT7000:                * Print
                         DDAT1500:                * Forward
                         DDAT1400                 * Backward
.
.         print the details
.
DDAT7000  CALL      PRNT0000
          GOTO      DDAT0000
.
DDAT9999  RETURN
+
.**************************************************************************
.*  MORE0000  :  ask line 24 options                                      *
.**************************************************************************
MORE0000  BRANCH    SCREEN,MORE1000               * on 1st screen?
.
. we are not on the 1st screen
.
          IF        MOREDISP=1
            KEYIN     *P1:24,*EL," (":
                      *V2LON,*DV,ANSP,*HOFF,")rint, (":
                      *V2LON,*DV,ANSE,*HOFF,")xit, (":
                      *V2LON,*DV,ANSF,*HOFF,")orward, (":
                      *V2LON,*DV,ANSB,*HOFF,")ackward ? ",*V2LON,ANS;
          ELSE
            KEYIN     *P1:24,*EL," (":
                      *V2LON,*DV,ANSP,*HOFF,")rint, (":
                      *V2LON,*DV,ANSE,*HOFF,")xit, (":
                      *V2LON,*DV,ANSB,*HOFF,")ackward ? ",*V2LON,ANS;
          ENDIF
          GOTO      MORE2000
.
. we are on the first screen
.
MORE1000  IF        MOREDISP=1
            KEYIN     *P1:24,*EL," (":
                      *V2LON,*DV,ANSP,*HOFF,")rint, (":
                      *V2LON,*DV,ANSE,*HOFF,")xit, (":
                      *V2LON,*DV,ANSF,*HOFF,")orward ? ",*V2LON,ANS;
          ELSE
            KEYIN     *P1:24,*EL," (":
                      *V2LON,*DV,ANSP,*HOFF,")rint, (":
                      *V2LON,*DV,ANSE,*HOFF,")xit ? ",*V2LON,ANS;
          ENDIF
.
MORE2000  REP       UPPLOW,ANS
.
          MOVE      ONE,EXIT
          CMATCH    ANSE,ANS
          GOTO      MORE9999 IF EQUAL       * exit program
.
          MOVE      TWO,EXIT
          CMATCH    ANSP,ANS
          GOTO      MORE9999 IF EQUAL       * print details
.
          CMATCH    ANSF,ANS                * Forward
          IF        @EQUAL
            IF        MOREDISP<>1
              BEEP
              GOTO      MORE0000
            ELSE
              MOVE      FIVE,CVERT
              DISPLAY   *P1:CVERT,*EF
              MOVE      THREE,EXIT
              ADD       ONE,SCREEN
              MOVE      ONE,RECCOUNT
              GOTO      MORE9999
            ENDIF
          ENDIF
.
          CMATCH    ANSB,ANS                      * Backward
          IF        @EQUAL
            IF        SCREEN=1
              BEEP
              GOTO      MORE0000
            ELSE
              MOVE      FOUR,EXIT
              SUB       ONE,SCREEN
              GOTO      MORE9999
            ENDIF
          ENDIF
.       
          BEEP
          GOTO      MORE0000                * return to menu
.
MORE9999  RETURN
+
.*********************************************************************
.         print the data
.*********************************************************************
PRNT0000  CLOCK     TIME,CTIMEIS
          MOVE      ZERO,CPAGENO
          MOVE      "60",CLNO
          DISPLAY   *P1:24,*EL,*P10:24,"Printing Details : ";
          IF        OPTION=1
            MOVE      SP30,KEY24
            CALL      RDSPRIA1
PRNT1000    CALL      RDKPRIA1
            BRANCH    OVRCD,PRNT9000
          ELSE
            MOVE      Z30,KEY24
            CALL      RDSPRIA2
PRNT1100    CALL      RDPPRIA2
            BRANCH    OVRCD,PRNT9000
          ENDIF
.
          MOVE      ADPNUMB,KEY8
          CALL      RDDETA1
          BRANCH    OVRCD,PRNT8000
.
          MATCH     SP5,ADASEEN           ** DO NOT INCLUDE DISCHARGE
          GOTO      PRNT8000 IF NOT EQUAL
.
          MOVE      ADAURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PRNT8000
.
          CALL      PLIN0000
          DISPLAY   *P45:24,*V2LON,ADPPRTY,*P50:24,ADPDATE,SP1,ADPTIME
.
PRNT8000  BRANCH    OPTION,PRNT1000,PRNT1100
.
PRNT9000  PRINT     *N,*N,"***  END OF REPORT  ***"
          DISPLAY   *P1:24,*EL,*P15:24,*V2LON:
                    "*** Report Generation Completed ***"
PRNT9999  RETURN
+
.*********************************************************************
.         print a line of the report
.*********************************************************************
PLIN0000  COMPARE   LN55,CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      PGNAME,ANS
          PACK      DIM30 WITH ANS,DOT,PSNAME,SP20
          MOVE      DIM30,NAME22
.
          MATCH     SP3,ADAUSR1
          GOTO      PLIN1000 IF NOT EQUAL
.
          MOVE      SP30,A1DESC
.
PLIN1000  MOVE      SP30,A1DESC
          PACK      KEY5 WITH CODEA1,ADAUSR1
          CALL      RDCODE1
.
          UNPACK    ADPDATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          IF        OPTION=1
            PRINT     *1,SP3,ADPPRTY,*10,CPCDATE,*22,ADPTIME:
                      *30,ADAURNO,*40,ADANUMB,*50,DIM30:
                      *N,*10,A1DESC,*50,ADADIAG
          ELSE
.
            MOVE       SP20,TDESC
            MATCH      SP3,ADAPRTY
            IF         !@EQUAL
              PACK       KEY5,ANSA,ANSA,ADAPRTY
              CALL       RDCODE1
            ENDIF
            MOVE       TDESC,PRDESC
...            MOVE      ADAPRTY,FORM1
...            MOVE      ADAPRTY,PRDESC
...            LOAD      PRDESC,FORM1,AECNDXPA,AECNDXPB,AECNDXPC,AECNDXPD:
...                                   AECNDXPE
.
            PRINT   *1,CPDATE:
                    *12,ADPTIME:
                    *20,ADAURNO:
                    *31,ADANUMB:
                    *42,NAME22:
                    *68,PRDESC:
                    *N,*10,A1DESC,*50,ADADIAG
          ENDIF
          ADD       ONE,CVERT
          ADD       TWO,CLNO
PLIN9999  RETURN
+
.*********************************************************************
.         print new page
.*********************************************************************
HEAD0000  CALL      HEAD132
          IF        OPTION=1
            PRINT     *1,"Priority",*10,"  Date  ",*22,"Time ":
                      *30," U/R No",*39," Emerg No":
                      *50,"Patient Name / Complaint":
                      *N,"--------",*10,"--------",*22,"-----":
                      *30,"--------":
                      *40,"--------",*50,"------------------------------":
                      "--------------------",*N
          ELSE
            PRINT     *1,"  Date  ",*12,"Time ",*21,"U/R No. ":
                      *31," Emerg No",*42,"Patient Name/Complaint":
                      *68,"Priority":
                      *N,"--------",*12,"-----",*20,"--------":
                      *31,"--------":
                      *42,"------------------------",*68,"----------",*N
          ENDIF
.
          MOVE      SEVEN,CLNO
HEAD9999  RETURN
+
.*********************************************************************
.         get the priority descriptions
.*********************************************************************
GPDX0000  READ      CONTROLF,THIRTY;*208,AECNDXPA,AECNDXPB:
                                    AECNDXPC,AECNDXPD
          READ      CONTROLF,SEVENTY8;*6,AECNDXPE
.
          MATCH     "0000000000",AECNDXPA
          GOTO      GPDX5000 IF EQUAL       * nothing set up
          MATCH     SP10,AECNDXPA
          GOTO      GPDX8000 IF NOT EQUAL   * have a parameter
.
. no system parameters so use our standard ones
.
GPDX5000  MOVE      DPRIT1,AECNDXPA
          MOVE      DPRIT2,AECNDXPB
          MOVE      DPRIT3,AECNDXPC
          MOVE      SP10,AECNDXPD
          MOVE      SP10,AECNDXPE
.
. remove trailing blanks
.
GPDX8000  STRIP     AECNDXPA
          STRIP     AECNDXPB
          STRIP     AECNDXPC
          STRIP     AECNDXPD
          STRIP     AECNDXPE
.
GPDX9999  RETURN
.
+
          INC       STD001IO
          INC       AAEDE1IO/INC
          INC       AAEPRIIO/INC
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
...............................................................................
