MRTMASDS
.*  *********************************************************************
.*                                                                      *
.*      Include Name  : MRTMASDS.PBL                                    *
.*                                                                      *
.*      Function      : Routine to Identify a Medical Records for a     *
.*                    : Particular U/R Number by Displaying all Medical *
.*                    : Record for that U/R Number.                     *
.*                                                                      *
.*      Files Needed  : MRTMASFD.INC   Tracking Master File             *
.*                      MRTMASIO.INC                                    *
.*                      MRTLOCFD.INC   Tracking Location File           *
.*                      MRTLOCIO.INC                                    *
.*                      PATMA1FD.INC   Patient Master File              *
.*                      PATIOMAS.INC                                    *
.*                      PATGSRFD.INC   Surname/Given Name Index File    *
.*                      PATIOSUR.INC                                    *
.*                      PATCODFD.INC   Patient Code File                *
.*                      PATIOCOD.INC                                    *
.*                      PATOPTFD.INC   Program Option security file     *
.*                      PATOPTIO.INC                                    *
.*                      PATCOMM.INC    Special Variables                * 
.*                      MRTMASDS       This Include                     *
.*                      PATCOMN1       KURN Routine                     *
.*                      PATSNDX        Surname Sound-ex Search          *
.*                                                                      *
.*      Modifications :                                                 *
.*  V9.02.03  18/01/2002  Tonii  SRF 14184                              *
.*            Changed so that when a patient has only 1 record then the *
.*            user is given the option of creating or moving the record *
.*            NB change is only for IBAMRT13,IBAHOS31,IBAPAT34,IBAPAT36 *
.*  V9.02.02  24/10/2001  Steve Downing  SRF 12218                      *
.*            Fixed to display correct record when barcode is scanned   *
.*  V9.02.01  06/10/2001  J.Tan  (copied from 5.09)                     *
.*            Fixed bug causing "Record does not exist for this patient"*
.*            message to appear even when record does exist for patient *
.*            Mods so that :                                            *
.*              - when a barcode label is entered, by scanning or       *
.*                entering the full record ID only the volume scanned   *
.*                is displayed                                          *
.*              - If a UR has only one volume of records, then the prg  *
.*                automatically selects and displays that record        *
.************************************************************************
.************************************************************************
.*        V5.08.01  02/10/2000 J Habasque 509 Mods                      *
.*                  Added check for MRT13 & MRT14 to stop UR keyin      *
.*        V5.07.01  22/11/1999 J.Tan                                    *
.*                  Added option security to IBAMRT13                   *
.*        V5.07.00  13/10/1998  Davin                                   *
.*                  507 changes                                         *
.************************************************************************
.*        V5.06.01  14/08/1998  Steve Armstrong                         *
.*                  Fixed calculation of age for deceased patient       *
.************************************************************************
.  *                    V5.00  29/05/89  S.O'Gorman                     *
.  *                           Include Written                          *
.  *                    V5.01  29/06/89 DIG                             *
.  *                           Modified so you can exit routine rather  *
.  *                           than making it mandatory to select a     *
.  *                           chart.                                   *
.  *                    V5.02  05.03.90 D.Di.Paola  SRF 103348          *
.  *                           Fixed exit point so it no longer selected*
.  *                           the first record from the 2nd screen     *
.  *                           when chosen.                             *
.  *                           Put in display of heading when going to  *
.  *                           2nd screen.                              *
.  *                           When choosing Accept clear rest of line. *
.  *                 V5.01.03  02/12/92  DIG                            *
.  *                           Fixed the check for the security level   *
.  *                           on moving a medical record.              *
.*----------------------------------------------------------------------* 
.*        V5.02.00  18/10/1994  bez          SRF 131201                 *
.*                  Clear Medical Record Location Description if no     *
.*                  Medical records.                                    *
.*        V5.02.01  07/12/1994  Graeme Williams   SRF 133649            *
.*                  Changes to allow for MURN accepting national numbers*
.*        V5.02.02  28/04/1995  Paul Howells      SRF 134942            *
.*                  For Pat36 make sure Barcode U/R is same as U/R      *
.*                  originally entered.                                 *
.*        V5.03.01  26/09/1995  Whirlpool                               *
.*                  added SWAPV000 routine to swap volume numbers       *
.*                  to overcome problem of "1 " and " 1" VOL numbers    *
.*        V5.03.02  09/02/95 J.Tan    SRF 613962                        *
.*                  Mods to recalculate age                             *
.************************************************************************
.
.  ****************************************************************************
.  *                                                                          *
.  * MRTMASDS.PBL :           Get Patient's Medical Records                   *
.  *                                                                          *
.  * Parameters   : MRMAURNO    Patient's U/R Number                            *
.  *                                                                          *
.  * Returns      : MRMAURNO    Patient's U/R Number                            *
.  *                MRMAHLOC  Medical Records Home Location                   *
.  *                MRMADOTY  Type of Document                                *
.  *                MRMAVOLN  Volume Number                                   *
.  *                EXIT      0 = U/R Entered                                 *
.  *                          1 = No U/R Entered                              *
.  *                          2 = No Item Selected after display              *
.  *                OVRCD     0 = U/R entered exists, and MRTMASA record read *
.  *                          1 = U/R Entered does not exist on MRTMASA       *
.  *                                                                          *
.  * Open Files   : PATMA1A1  Patient Master File                             *
.  *                PATGSRN1  Surname/Given Name Index, Sound-ex order        *
.  *                PATGSRN2  Surname/Given Name Index, Strict Surname order  *
.  *                PATCODE1  Patient Codes File                              *
.  *                MRTMASA1  Medical Records Tracking Master File            *
.  *                MRTLOCA1  Medical Records Tracking Location File          *
.  *                                                                          *
.  * Variables    : CODELT     INIT    "LT"   RECORD1     DIM     17          *
.  *                CCNOTES    DIM     1      RECORD2     DIM     17          *
.  *                COUNT      FORM    2      RECORD3     DIM     17          *
.  *                DIM17      DIM     17     RECORD4     DIM     17          *
.  *                DIM47      DIM     47     RECORD5     DIM     17          *
.  *                FLAG       FORM    1      RECORD6     DIM     17          *
.  *                KEY4       DIM     4      RECORD7     DIM     17          *
.  *                KEY5       DIM     5      RECORD8     DIM     17          *
.  *                KEY6       DIM     6      RECORD9     DIM     17          *
.  *                KEY8       DIM     8      RECORD10    DIM     17          *
.  *                KEY17      DIM     17     RECORD11    DIM     17          *
.  *                KEY61      DIM     61     RECORD12    DIM     17          *
.  *                KEY71      DIM     71     RECORD13    DIM     17          *
.  *                VERTPOS    FORM    2      RECORD14    DIM     17          *
.  *                COND       FORM    1      RECORD15    DIM     17          *
.  *                CODETY     INIT    "TY"   FORM8       FORM8   8           *
.  *                SAVPURNO   DIM     8                                      *
.  ****************************************************************************
.
MRTD0000
          MOVE      ZERO,FLAG
          MOVE      ZERO,EXIT
          MOVE      ZERO,OVRCD

          MATCH     "IBAMRT13",PRGID
          IF        @EQUAL
            GOTO      MRTD0100
          ENDIF
.
          MATCH     "IBAMRT14",PRGID
          IF        @EQUAL
            GOTO      MRTD0100
          ENDIF
.
          MOVE      ZERO,MRMAURNO
          MOVE      ZERO,PURNO
          PACK      DIM47,SP30,SP30 
.
          MOVE      TWO,SRCHOPT
          MOVE      ZERO,SRCHSYS
.
          CALL      MURN
.
MRTD0100  MATCH     "IBAPAT36",PRGID
          IF        @EQUAL
            BRANCH    EXIT,MRTD9999
            MATCH     PURNO,SAVPURNO
            GOTO      MRTD8000 IF NOT EQUAL
          ENDIF
.
          BRANCH    OVRCD,MRTD9000               * Invalid U/R number



          BRANCH    EXIT,MRTD9999:               * Nothing input
                         MRTD1000                * Possible barcode
.
MRTD0500  MOVE      PURNO,MRMAURNO              * Load U/R Number into MRMAURNO
.
          CALL      CHECK000                     * Check for a Single Med. Rec.
.
          MATCH     "IBAMRT13",PRGID
          IF        @EQUAL
            IF        FLAG=1
              GOTO      MRTD9000                 * No M.R's for patient
            ELSE
              IF        FLAG = 2
                GOTO      MRTD2000                 * at least 1 M.R.
              ELSE  
                UNPACK    CMPURNO,KEY12,DIM8
                MATCH     SP10,KEY12
                IF        !@EQUAL
                  MOVE      TWO,EXIT                                    
                ENDIF
                GOTO      MRTD2000
              ENDIF     
            ENDIF
          ENDIF   
.
          MATCH     "IBAMRT14",PRGID
          IF        @EQUAL
            IF        FLAG=1
              GOTO      MRTD9000                 * No M.R's for patient
            ELSE
              GOTO      MRTD2000                 * at least 1 M.R.
            ENDIF
          ENDIF
.
.MRTD0800  BRANCH    FLAG,MRTD9000,MRTD2000       * No M.R's or More than 1
.
.          GOTO      MRTD9999                     * Only 1 M.R. for Patient
.
. ---- return from MURN indicates full record id input ----
.
MRTD1000  COMPARE   ONE,FLAG
          GOTO      MRTD2000 IF NOT EQUAL
.
          UNPACK    CMPURNO,KEY3,KEY17
          CALL      RDMRMAS1
.
.----- The SWAPV000 routine was added to cope with left and right justified ---
.----- volume numbers so that barcoded labels in particular would not have  ---
.----- to be reprinted due to a bug introduced that caused, for example     ---
.----- patients to have a volume "1 " and a " 1"                            ---
.
          IF        OVRCD = 1
            MOVE      ZERO,OVRCD
            UNPACK    KEY17,KEY15,KEY2
            CALL      SWAPV000                   * Swap Volume numbers around
            BRANCH    EXIT,MRTD9000                
            PACK      KEY17,KEY15,KEY2
            CALL      RDMRMAS1
            BRANCH    OVRCD,MRTD9000               * Invalid input
          ENDIF
.
.          MOVE      ZERO,OVRCD
.          MOVE      ZERO,EXIT
.          GOTO      MRTD9999
.
MRTD2000  IF        FLAG = 2 
            UNPACK    CMPURNO,KEY3,KEY17
            CALL      RDMRMAS1
            IF        OVRCD = 1
              UNPACK    KEY17,KEY15,KEY2
              CALL      SWAPV000                   * Swap Volume numbers around
              BRANCH    EXIT,MRTD2050
              PACK      KEY17,KEY15,KEY2
              CALL      RDMRMAS1
              IF        OVRCD = 1
MRTD2050        PACK      KEY17,PURNO,SP20
                CALL      RSMRMAS1               * Position to first rec for
                CALL      RKMRMAS1               * this U/R in MRTMASA file
                MOVE      ZERO,EXIT
              ELSE
                MOVE      ZERO,FLAG
                GOTO      MRTD2075
              ENDIF
            ELSE
              MOVE      ZERO,FLAG
              GOTO      MRTD2075
            ENDIF
          ELSE
            IF        FLAG = 0
                                            * treat barcode as single admission
              IF        EXIT = 2
                UNPACK    CMPURNO,KEY3,KEY17
                CALL      RDMRMAS1           * direct read for barcode/full key
                MOVE      ZERO,EXIT
                IF        OVRCD = 1
                  GOTO      MRTD2050
                ENDIF 
              ELSE
                PACK      KEY17,PURNO,SP20
                CALL      RDMRMAS1
                IF        OVRCD = 1
                  MATCH     "IBAMRT13",PRGID
                  IF        @EQUAL
                    MOVE      TWO,FLAG
                  ENDIF
                  MATCH     "IBAHOS31",PRGID
                  IF        @EQUAL
                    MOVE      TWO,FLAG
                  ENDIF
                  MATCH     "IBAPAT34",PRGID
                  IF        @EQUAL
                    MOVE      TWO,FLAG
                  ENDIF
                  MATCH     "IBAPAT36",PRGID
                  IF        @EQUAL
                    MOVE      TWO,FLAG
                  ENDIF
                  GOTO      MRTD2050
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
MRTD2075  MOVE      PSNAME,DIM47
          ENDSET    DIM47
MRTD2100                                         * Format Patient's name
          BUMP      DIM47,-1
          MOVE      DIM47,ANS
          CMATCH    SP1,ANS
          GOTO      MRTD2100 IF EQUAL
.
          APPEND    ", ",DIM47
          APPEND    PGNAME,DIM47
          RESET     DIM47
.
          COMPARE   ONE,PCEASE
          IF        @EQUAL
            UNPACK    PDECDTE,CCENT,CYEAR,CMON,CDAY
            PACK      AGEDATE,CCENT,CYEAR,CMON,CDAY
          ELSE
            PACK      AGEDATE,CCC,CYY,CMM,CDD
          ENDIF
          REP       " 0",AGEDATE
          UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY
          CALL      CALCAGE                       * Calculate patients age
          MOVE      PSAGEYRS,PSAGE
.
          MOVE      ZERO,COUNT
          IF        FLAG = 0
            CALL      CLRMRT00                     * Clear Record1-15 Values
            GOTO      MRTD3100
          ENDIF      
.
          DISPLAY   *P1:3,*EF:
                    *P1:4,"U/R Number : ",*V2LON,MRMAURNO,*HOFF:
                    *P1:5,"Name : ",*V2LON,DIM47,*HOFF:
                    *P1:6,"D.O.B. : ",*V2LON,XDAY,SLASH,XMON,SLASH:
                           XCENT,XYEAR,*HOFF:
                    *P23:6,"Age : ",*V2LON,PSAGE,*HOFF;
MRTD3050  DISPLAY   *P5:8,*ULON,*V2LON,"Home Location",SP10,SP7:
                    *P37:8,"Type of Document",SP4:
                    *P59:8,"Vol",*P64:8,"Current Location ";
.
          CALL      CLRMRT00                     * Clear Record1-15 Values
MRTD3100
          ADD       ONE,COUNT
          COMPARE   TEN5,COUNT
          GOTO      MRTD3200 IF NOT EQUAL
.
MRTD3150  CALL      PAGGE000                     * Paging on 15 Med. Records
          IF        COND=3
            GOTO      MRTD4200                   * an item selected
          ENDIF
          IF        COND=2
            MOVE      DIM47,PATNAME
            CALL      GETSCR00
            CALL      CRTM0000
            IF        EXIT=1
              CALL      PUTSCR00
              GOTO      MRTD3150
            ENDIF
.           BRANCH    EXIT,MRTMASDS                . ???????????????? who knows?
            PACK      DIM17,MRMAURNO,MRMAHLOC,MRMADOTY,MRMAVOLN
            CALL      FRESCR00
            GOTO      MRTD5000
          ENDIF
.        
          BRANCH    COND,MRTD9100,MRTD4000       * Exit or Create
          GOTO      MRTD3050
MRTD3200
          MOVE      EIGHT,VERTPOS
          ADD       COUNT,VERTPOS
.
          MOVE      SP30,MRLODESC
          PACK      KEY4,MRMAHLOC
          CALL      RDMRLOC1
.
          MOVE      SP30,TDESC
          PACK      KEY5,CODETY,MRMADOTY
          CALL      RDCODE1
.
          PROC      CURLOCTN                      * get current location
.
          IF        FLAG = 2
            DISPLAY   *P1:VERTPOS,*V2LON,COUNT,*HOFF,DOT:
                      *P5:VERTPOS,MRLODESC:
                      *P37:VERTPOS,TDESC:
                      *P59:VERTPOS,MRMAVOLN:
                      *P64:VERTPOS,CURRLOCN;
          ENDIF
.
          PACK      DIM17,MRMAURNO,MRMAHLOC,MRMADOTY,MRMAVOLN
          STORE     DIM17,COUNT,RECORD1,RECORD2,RECORD3,RECORD4,RECORD5:
                                  RECORD6,RECORD7,RECORD8,RECORD9,RECORD10:
                                  RECORD11,RECORD12,RECORD13,RECORD14,RECORD15
.
          CALL      RKMRMAS1
          BRANCH    OVRCD,MRTD4000
.
          MATCH     MRMAURNO,PURNO
          GOTO      MRTD4000 IF NOT EQUAL

          GOTO      MRTD3100
MRTD4000                                      * Accept Value at end of Rec's
          IF        FLAG = 0                 
            MOVE      ONE,COPTION
            GOTO      MRTD4100
          ENDIF
.
          MOVE      ZERO,OVRCD
          IF        MRCNSLCM > SECLEV
            GOTO      MRTD4040                   * invalid security clearance
          ENDIF
          MATCH     "IBAMRT13",PRGID
          IF        @EQUAL
MRTD4010    DISPLAY   *P1:24,*EL,"Select Field, (",*V2LON,"C",*HOFF,")reate : ";
MRTD4020    KEYIN     *P26:24,*DV,UNDLN2:
                      *P26:24,*V2LON,*JR,KEY2;
            REP       "cC",KEY2
            ENDSET    KEY2
            APPEND    SP2,KEY2
            RESET     KEY2
            TYPE      KEY2
            IF        @EQUAL
              COMPARE   PTOPOP1,SECLEV
              IF        @LESS
                DISPLAY   *P1:24,*EL,*B,"You are not Authorised to Use this":
                                        " Option. ";
                CALL      HITENTER
                GOTO      MRTD4010
              ENDIF
              MOVE      KEY2,COPTION
              MOVE      ZERO,EXIT
              GOTO      MRTD4050
            ENDIF
.
            MATCH       SP2,KEY2
            GOTO        MRTD4080 IF EQUAL
            MATCH       " C",KEY2
            IF          !@EQUAL
              BEEP
              GOTO        MRTD4020
            ELSE
              COMPARE   PTOPOP2,SECLEV
              IF        @LESS
                DISPLAY   *P1:24,*EL,*B,"You are not Authorised to Use this":
                                        " Option. ";
                CALL      HITENTER
                GOTO      MRTD4010
              ENDIF
            ENDIF
            MOVE      DIM47,PATNAME
            CALL      GETSCR00
            CALL      CRTM0000
            IF        EXIT=1
              CALL      PUTSCR00
              GOTO      MRTD4000
            ENDIF
            BRANCH    EXIT,MRTMASDS                . ???????????????? who knows?
            PACK      DIM17,MRMAURNO,MRMAHLOC,MRMADOTY,MRMAVOLN
            CALL      FRESCR00
            GOTO      MRTD5000
          ENDIF
.
MRTD4040  DISPLAY   *P1:24,*EL,"Select Field :   ";
          KEYIN     *P16:24,*V2LON,COPTION
.
MRTD4050  COMPARE   ONE,COPTION
          GOTO      MRTD4100 IF NOT LESS
.
MRTD4080  MOVE      TWO,EXIT
          MOVE      ZERO,OVRCD
          GOTO      MRTD9999
MRTD4100  ADD       ONE,COUNT                    * Increment for test of less
          COMPARE   COUNT,COPTION
          GOTO      MRTD4200 IF LESS
.
          SUB       ONE,COUNT
          BEEP
          GOTO      MRTD4000
MRTD4200  LOAD      DIM17,COPTION,RECORD1,RECORD2,RECORD3,RECORD4,RECORD5:
                                  RECORD6,RECORD7,RECORD8,RECORD9,RECORD10:
                                  RECORD11,RECORD12,RECORD13,RECORD14,RECORD15
.
MRTD5000  PACK      KEY17,DIM17
          CALL      RDMRMAS1
.
          CALL      REDISPS
          GOTO      MRTD9999
.
MRTD8000  DISPLAY   *P1:24,*EL,*B,"Invalid U/R Number. ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL,"Scan Barcode : ";
          GOTO      MRTD0000
.
MRTD9000
          MOVE      TRUE,OVRCD
          MOVE      ZERO,EXIT
          GOTO      MRTD9999
.
MRTD9100  MOVE      ZERO,OVRCD
          MOVE      TWO,EXIT
MRTD9999  RETURN
.
.
.*********************************************************************** 
.CHECK000 : Check if U/R Number has only one Medical Record  -----
. returns : FLAG = 0  one
.                = 1  none
.                = 2  more than one
.*********************************************************************** 
CHECK000  MOVE      FALSE,FLAG
.
          PACK      KEY17,MRMAURNO,SP20
          CALL      RSMRMAS1
          CALL      RKMRMAS1
          BRANCH    OVRCD,CHECK900           * none
.
          MATCH     PURNO,MRMAURNO
          GOTO      CHECK900 IF NOT EQUAL    * none
.
          CALL      RKMRMAS1
          BRANCH    OVRCD,CHECK100
.
          MATCH     PURNO,MRMAURNO
          GOTO      CHECK100 IF NOT EQUAL
.
          MOVE      TWO,FLAG                     * More than 1 M.R. for Patient
          GOTO      CHECK999
.
CHECK100  PACK      KEY17,PURNO,SP20
          CALL      RSMRMAS1
          CALL      RKMRMAS1
.
          GOTO      CHECK9999                     * only one
CHECK900  MOVE      TRUE,FLAG   *none!
CHECK999  RETURN
.
.
PAGGE000  MOVE      ZERO,COND
          DISPLAY   *P1:24,*EL,"Select Item, ":
                               "(",*V2LON,"M",*HOFF,")ore";
.
          IF        MRCNSLCM > SECLEV
            DISPLAY   " ? ";
            GOTO      PAGGE010                   * invalid security clearance
          ENDIF
          MATCH     "IBAMRT13",PRGID
          IF        @EQUAL
            DISPLAY   ", (",*V2LON,"C",*HOFF,")reate ? ";
          ELSE
            DISPLAY   " ? ";
          ENDIF
.
PAGGE010  KEYIN     *V2LON,*JR,DIM2;
          ENDSET    DIM2
          APPEND    SP2,DIM2
          RESET     DIM2
          REP       UPPLOW,DIM2
          MATCH     SP2,DIM2
          GOTO      PAGGE900 IF EQUAL
          TYPE      DIM2
          GOTO      PAGGE050 IF EQUAL
.
          REP       "M1C2",DIM2
          TYPE      DIM2
          GOTO      PAGGE040 IF NOT EQUAL
.
          MOVE      DIM2,FORM2
          BRANCH    FORM2,PAGGE100,PAGGE800
.
PAGGE040  BEEP
          GOTO      PAGGE000
.
PAGGE050  MOVE      DIM2,FORM2
          IF        (FORM2<1) | (FORM2>(COUNT-1))
            BEEP
            GOTO      PAGGE000
          ENDIF
          MOVE      DIM2,COPTION
          MOVE      ZERO,EXIT
          MOVE      THREE,COND
          GOTO      PAGGE999
.
.-------- (M)ore ------
.
PAGGE100
          MOVE      ZERO,COUNT                    * intialise count
          DISPLAY   *P1:7,*EF;                    * clear screen
          GOTO      PAGGE999
.
.-------- (C)reate -----
.
PAGGE800  IF        MRCNSLCM > SECLEV
            BEEP                                 * invalid security clearance
            GOTO      PAGGE000
          ENDIF
          MATCH     "IBAMRT13",PRGID
          IF        !@EQUAL
            BEEP
            GOTO      PAGGE000
          ENDIF
          MOVE      TWO,COND                     * set condition
          GOTO      PAGGE999
.
.-------- (E)xit -----
.
PAGGE900
          MOVE      TWO,EXIT                     * set exit
          MOVE      ONE,COND                     * set condition
PAGGE999
          RETURN
CLRMRT00
          MOVE      SP20,RECORD1
          MOVE      SP20,RECORD2
          MOVE      SP20,RECORD3
          MOVE      SP20,RECORD4
          MOVE      SP20,RECORD5
          MOVE      SP20,RECORD6
          MOVE      SP20,RECORD7
          MOVE      SP20,RECORD8
          MOVE      SP20,RECORD9
          MOVE      SP20,RECORD10
          MOVE      SP20,RECORD11
          MOVE      SP20,RECORD12
          MOVE      SP20,RECORD13
          MOVE      SP20,RECORD14
          MOVE      SP20,RECORD15
.
          RETURN
+
.************************************************************************
.*  CURLOCTN  :  Get current location                                   *
.*               Returns: CURRLOCN - current location (description)     *
.************************************************************************
          DEFPROC   CURLOCTN
.
          INC       MRTHISFD/INC
          INC       MRTLOCFD/INC
ZEDS30    INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
          OPEN      MRTHISA1,"mrthisaf"
          OPEN      MRTLOCA1,"mrtlocaf"
.
          MOVE      SP30,CURRLOCN
          PACK      KEY26,MRMAKEY,ZEDS30
          CALL      RSMRHIS1
          CALL      RPMRHIS1
          BRANCH    OVRCD,CURLO999
.
          MATCH     MRMAKEY,MRHIKEY
          GOTO      CURLO900 IF NOT EQUAL
.
          MOVE      SP30,MRLODESC
          PACK      KEY4,MRHILOC
          CALL      RDMRLOC1
          MOVE      MRLODESC,CURRLOCN
          GOTO      CURLO999
.
CURLO900  MOVE      SP30,CURRLOCN
          GOTO      CURLO999
.
          INC       MRTHISIO/INC
          INC       MRTLOCIO/INC
          INC       IBAOVRIO/INC
          INC       NZCOMPIO/INC                 * IO Include for NZHIS
.
CURLO999  ENDPROC
.
.*****************************************************************************
.*                               SWAPV000                                    *
.*       Routine to swap single digit volume numbers if the first read fails *
.*       Paremeters : KEY 2    Returns KEY2 and EXIT                         *
.*****************************************************************************
.
SWAPV000  MOVE      ZERO,EXIT
          SCAN      SP1,KEY2
          GOTO      SWAPV900 IF NOT EQUAL       * Two digit volume number
.
          RESET     KEY2
          UNPACK    KEY2,KEY1,DIM1
          PACK      KEY2,DIM1,KEY1              * Swap the digits
          GOTO      SWAPV999
.
SWAPV900  MOVE      ONE,EXIT
. 
SWAPV999 RETURN
.
