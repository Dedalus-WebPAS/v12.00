.******************************************************************************
.* System         : Patient Care Plans                                        *
.* Program        : IBAPCP34.PBL                                              *
.* Name           : Frequency Codes Master Listing                            *
.******************************************************************************
.* Date           : 20/11/1991                                                *
.* Author         : Justin Coates                                             *
.* Function       : Produce a master listing for PCPFRAFD and PCPFRBFD        *
.* Modifications  :                                                           *
.*        V9.02.00  30/12/2002 Tracey Nguyen srf 23497                        *
.*                  r5.10 Care Plans Port to V9                               *
.******************************************************************************
.
.$$$$$
.         IBAPCP34.PBL        Frequency Codes Master Listing
.         ============        ==============================
.         OPTN0000 - produce listing or not
.         FREQ0000 - enter frequency code range
.         REPT0000 - Produce report
.
.$$$$$
.
.         FD includes 
.
          INC       STD001FD                * standard variables
          INC       PATCODFD/INC            * codes file
          INC       PCPFRAFD/INC            * freq. desc file
          INC       PCPFRBFD/INC            * freq/shift file
.
.         program variables
.
CDFQSTRT  DIM       9         * starting code
CDFQSTOP  DIM       9         * ending code
CURRCODE  DIM       9         * current printing code
DIM25     DIM       25        * pcpfrads
DXFQSTRT  DIM       30        * starting description
DXFQSTOP  DIM       30        * ending description
ENCODE    DIM       9         * entered code
ENDESC    DIM       30        * entered desc
SCRNFLAG  FORM      1         * redisplay flag
.
.         program constants
.
CATDS     INIT      "DS"      * shift code
ZED9      INIT      "zzzzzzzzz"
.
PRGID     INIT      "IBAPCP34"
PRGNAM    INIT      "Frequency Codes Master Listing"
VERSION   INIT      "V12.00.00"
+
.*********************************************************************
.*                  ML0000                                           *
.*        Mainline Code / Controlling Logic                          *
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000
          BRANCH    EXIT,ML9999             * exit selected
.
          CALL      FREQ0000                * enter codes
          BRANCH    EXIT,ML1000             * exit selected
.
          CALL      REPT0000                * produce report
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.********************************************************************* 
.*                  INIT0000                    Called by : ML0000   *
.*        Initialization                                             *
.*********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CNEWDS
          DISPLAY   *P50:24,"Opening"
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"     * codes file
          DISPLAY   *P58:24,"pcpfraaf"
          OPEN      PCPFRAA1,"pcpfraaf"     * freq desc file
          DISPLAY   *P58:24,"pcpfrbaf"
          OPEN      PCPFRBA1,"pcpfrbaf"     * freq/shift file
.
INIT9999  RETURN
+
.*********************************************************************
.*                  OPTN0000                    Called by : ML1000   *
.*        See if to produce listing or not                           *
.*        Returns : EXIT = 1      exit selected                      * 
.*********************************************************************
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Produce master listing":
                    *P1:7,"Select option : "
.
OPTN1000  KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,MOPTION
.
          BRANCH    MOPTION,OPTN8000
.
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9000 IF EQUAL       * exit
.
          BEEP
          GOTO      OPTN1000
.
OPTN8000  MOVE      FALSE,EXIT
          GOTO      OPTN9999
.
OPTN9000  MOVE      TRUE,EXIT
.
OPTN9999  RETURN
+
.*********************************************************************
.*                  FREQ0000                    Called by : ML1000   *
.*        Enter the frequency codes range                            *
.*        Returns : EXIT = 1      exit selected                      *
.*********************************************************************
FREQ0000  MOVE      SP30,DXFQSTRT
          MOVE      SP30,DXFQSTOP
          MOVE      "*********",CDFQSTRT
          MOVE      "*********",CDFQSTOP
          DISPLAY   *P1:8,*EF:
                    *P1:9,"Starting Frequency code : ":
                    *P1:11,"Ending   Frequency code : "
.
.         enter starting code
.
          MOVE      "27",CCOL               * col
          MOVE      "9",CVERT               * row
          CALL      KFRQ0000                * enter frequency
          MOVE      ENCODE,CDFQSTRT         * starting code
          MOVE      ENDESC,DXFQSTRT         * starting description
          DISPLAY   *P27:9,*V2LON,CDFQSTRT,*HOFF,SP5,DXFQSTRT
.
          MATCH     SP9,ENCODE
          GOTO      FREQ2000 IF NOT EQUAL   * a code entered
.
          DISPLAY   *P27:9,*V2LON,"Start",*EL;
.
.         enter ending code
.
FREQ2000  MOVE      "11",CVERT               * row
          MOVE      "27",CCOL               * col
          CALL      KFRQ0000                * enter frequency
          MOVE      ENCODE,CDFQSTOP         * ending code
          MOVE      ENDESC,DXFQSTOP         * ending description
          DISPLAY   *P27:11,*V2LON,CDFQSTOP,*HOFF,SP5,DXFQSTOP
.
          MATCH     SP9,ENCODE
          GOTO      FREQ4000 IF NOT EQUAL   * a code entered
.
          MOVE      ZED9,CDFQSTOP           * set end code
          DISPLAY   *P27:11,*V2LON,"Finish",*EL;
.
.         check end code >= start code
.
FREQ4000  MATCH     CDFQSTRT,CDFQSTOP
          GOTO      FREQ6000 IF NOT LESS    * ending code is after start code
.
          DISPLAY   *P1:24,*B,"Ending code must be after Starting code.  ",*EL;
          CALL      HITENTER 
          GOTO      FREQ0000
.
.         O.K. to Continue (Y/N/C)
.
FREQ6000  CALL      CONTQST
          BRANCH    CEXIT,FREQ9000,FREQ0000,FREQ9100
.
FREQ9000  MOVE      ZERO,EXIT
          GOTO      FREQ9999
.
FREQ9100  MOVE      ONE,EXIT
.
FREQ9999  RETURN
+
.*********************************************************************
.*                  KFRQ0000                    Called by : FREQ0000 *
.*        Keyin the frequency code making sure it doesn't exist for  *
.*        insert mode and it does exist for change/delete mode       *
.*        Para's  : CVERT         the row                            *
.*        Returns : EXIT = 1      nothing entered                    *
.*********************************************************************
KFRQ0000  MOVE      ONE,SCRNFLAG
.  
KFRQ1000  KEYIN     *PCCOL:CVERT,*DV,UNDLN9:
                    *PCCOL:CVERT,*V2LON,ENCODE:
                    *PCCOL:CVERT,*DV,ENCODE
.
          ENDSET    ENCODE
          APPEND    SP9,ENCODE
          RESET     ENCODE
.
          MATCH     SP9,ENCODE
          GOTO      KFRQ7000 IF EQUAL       * nothing entered
.
          MATCH     QUEST,ENCODE
          GOTO      KFRQ2000 IF NOT EQUAL   * ? entered
.
.         ? entered
.
          CALL      PCPFRADS
          MOVE      "24",CVERT
          MOVE      "14",CCOL  
          MOVE      ZERO,SCRNFLAG
          DISPLAY   *P1:24,"Enter Code : ",*EL;
          GOTO      KFRQ1000
.
KFRQ2000  MOVE      ENCODE,KEY9
          CALL      RDPCFRA1
          BRANCH    OVRCD,KFRQ5000          * code is not on file
.
          MOVE      PCFADESC,ENDESC         * set description
          MOVE      ZERO,EXIT
          GOTO      KFRQ8000
.
KFRQ5000  DISPLAY   *P1:24,*B,"Invalid Frequency code.  ",*EL;
          CALL      HITENTER
          BRANCH    SCRNFLAG,KFRQ1000
          DISPLAY   *P1:24,"Enter Code : ",*EL;
          GOTO      KFRQ1000
.
.         nothing entered
.
KFRQ7000  MOVE      SP10,ENCODE
          MOVE      SP30,ENDESC
.
.         check for redisplay
.
KFRQ8000  BRANCH    SCRNFLAG,KFRQ9999       * no redisplay needed
.
          CALL      RDIS0000
.
KFRQ9999  RETURN
+
.*********************************************************************
.*                  RDIS0000                    Called by : KFRQ8000 *
.*        Redisplay the screen                                       *
.*********************************************************************
RDIS0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Produce master listing":
                    *P1:7,"Select option : ",*V2LON,MOPTION,*HOFF:
                    *P1:9,"Starting Frequency code : ":
                    *P1:11,"Ending   Frequency code : "
.
          MATCH     "*********",CDFQSTRT
          GOTO      RDIS9999 IF EQUAL       * no code entered yet
.
          DISPLAY   *P27:9,*V2LON,CDFQSTRT,SP5,*HOFF,DXFQSTRT
          MATCH     SP9,CDFQSTRT
          GOTO      RDIS9999 IF NOT EQUAL   * display codes
.
          DISPLAY   *P27:9,*V2LON,"Start",*EL;
.
RDIS9999  RETURN
+
.
.         I/O includes
.
          INC       STD001IO                * standard routine
          INC       PATCODIO/INC            * codes file
          INC       PCPFRADS/PBL            * ? for freq. desc file
          INC       PCPFRAIO/INC            * freq. desc file
          INC       PCPFRBIO/INC            * freq/shift file
................................................................................
.*********************************************************************
.*                  REPT0000                    Called by : ML1000   *
.*        Produce the report                                         *
.*********************************************************************
REPT0000  MOVE      ZERO,CPAGENO            * clear page counter
          CLOCK     TIME,CTIMEIS            * get the current time
          CALL      HEAD0000                * print heading
          MOVE      SP9,ENCODE              * store current code
.
          MOVE      CDFQSTRT,KEY9           * starting code
          CALL      RDPCFRA1                * positional read
          COMPARE   ZERO,OVRCD
          GOTO      REPT1100 IF EQUAL       * valid code
.
. ******* loop over pcpfraaf file ******
.
REPT1000  CALL      RKPCFRA1                * next read
          BRANCH    OVRCD,REPT9000          * end of report
.
REPT1100  MATCH     PCFACODE,CDFQSTOP
          GOTO      REPT9000 IF LESS        * end of report
.
          PACK      KEY12,PCFACODE,SP3
          CALL      RSPCFRB1
.
. ******* loop over pcpfrbaf for each frequency code *******
.
REPT2000  CALL      RKPCFRB1                * next read
          BRANCH    OVRCD,REPT1000          * finished file
.
          MATCH     PCFACODE,PCFBCODE
          GOTO      REPT1000 IF NOT EQUAL   * finished this code
.
          DISPLAY   *P50:24,PCFBCODE,SP1,PCFBSHFT
          CALL      PLIN0000                * print line
          GOTO      REPT2000
.
REPT9000  PRINT     *1,"*---------------------------------------":
                       "---------------------------------------*":
                    *N,*N,"*** End of Report ***",*N
.
REPT9999  RETURN
+
.*********************************************************************
.*                  HEAD0000                    Called by : REPT0000 *
.*        Print page heading                                         *
.*********************************************************************
HEAD0000  CALL      HEAD80
          PRINT     *20,"Starting Code : ";
          MATCH     SP9,CDFQSTRT
          GOTO      HEAD1000 IF EQUAL       * print start
. 
          PRINT     CDFQSTRT,SP2,DXFQSTRT
          GOTO      HEAD2000
.
HEAD1000  PRINT     "Start"
.
HEAD2000  PRINT     *20,"Ending   Code : ";
          MATCH     ZED9,CDFQSTOP
          GOTO      HEAD3000 IF EQUAL       * print stop
. 
          PRINT     CDFQSTOP,SP2,DXFQSTOP
          GOTO      HEAD4000
.
HEAD3000  PRINT     "Finish"
.
HEAD4000  PRINT     *N,"*---------------------------------------":
                       "---------------------------------------*":
                    *N,"| Frequency Code                           ":
                       "| Shift Code               ":
                       "| Amount |":
                    *N,"*---------------------------------------":
                       "---------------------------------------*"
          MOVE      "11",CLNO
.
HEAD9999  RETURN
+
.*********************************************************************
.*                  PLIN0000                    Called by : REPT2000 *
.*        Print a line of the report                                 *
.*********************************************************************
PLIN0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * print new heading
.
          MATCH     ENCODE,PCFBCODE
          GOTO      PLIN2000 IF EQUAL       * same code
.
.         print a line b/w each different code (except first one)
.
          MATCH     SP9,ENCODE
          GOTO      PLIN2000 IF EQUAL       * no code set up
.  
          PRINT     *1,"*---------------------------------------":
                       "---------------------------------------*"
          ADD       "1",CLNO
.
PLIN2000  MOVE      "Invalid Code",TDESC
          PACK      KEY5,CATDS,PCFBSHFT
          CALL      RDCODE1
.
          MATCH     PCFBCODE,ENCODE
          IF        @EQUAL
            PRINT     *1,"| ",SP9,SP1,SP30:
                      " | ",PCFBSHFT,SP1,TDESC:
                      " | ",PCFBNUMB,*80,"|"
          ELSE
            PRINT     *1,"| ",PCFACODE,SP1,PCFADESC:
                      " | ",PCFBSHFT,SP1,TDESC:
                      " | ",PCFBNUMB,*80,"|"
          ENDIF
          ADD       "1",CLNO
          MOVE      PCFBCODE,ENCODE         * set current code
.
PLIN9999  RETURN
+

