.-------------------------------------------------------
          INC       STDGENDF     * Required Include
.
VERSION   INIT      "V12.00.00"
.-------------------------------------------------------
. V9.02.01  09.07.2002  Glenn Saunders
.           Recompile for changes in includes shared with Southland, but with
.           previously different formats - now merged.  Includes effected :-
.           CCSCEAFD, CCSCONFD, CCSEPSFD, CCSHCDFD, CCSHLCFD, CCSIADFD
.-------------------------------------------------------
. V9.02.00  03.06.2002  Glenn Saunders
.           Export to new release i.e. v9.02 from vf.09
.-------------------------------------------------------
. V6.03.01  18.12.1995 B.G.Ackland
.           Fix Year Check for Report
. V6.03.00  02.10.1995 B.G.Ackland
.           Release 6.03
.-------------------------------------------------------
. V6.00.01  23.03.1994 B.G.Ackland
.           Fix Detail Report Format
.-------------------------------------------------------
COL01     DIM      13    * Report Columns
COL02     DIM      13
COL03     DIM      12
COL04     DIM      12
COL05     DIM      12
COL06     DIM      12
COL07     DIM      12
CUR01     DIM      13    * Report Columns
CUR02     DIM      13
CUR03     DIM      13
CURCST    FORM     12.2
CURCST1   FORM     12.2
CURCST2   FORM     12.2
CURCST3   FORM     12.2
CURCST4   FORM     12.2
CURCST5   FORM     12.2
CURQTY    FORM     12.2  * Service Provider Totals
ENDDATE   DIM      10
F12       FORM     12
F12P1     FORM     12.1
F12P2     FORM     12.2
FLAGPSC   FORM     1     * Falg Patient Service
GCURCST1  FORM     12.2
GCURCST2  FORM     12.2
GCURCST3  FORM     12.2
GCURCST4  FORM     12.2
GCURCST5  FORM     12.2
GCURQTY   FORM     12.2  * Report Totals
GYTDCST1  FORM     12.2
GYTDCST2  FORM     12.2
GYTDCST3  FORM     12.2
GYTDCST4  FORM     12.2
GYTDCST5  FORM     12.2
GYTDQTY   FORM     12.2
SCURCST1  FORM     12.2
SCURCST2  FORM     12.2
SCURCST3  FORM     12.2
SCURCST4  FORM     12.2
SCURCST5  FORM     12.2
SCURQTY   FORM     12.2  * Service Provider Totals
SYTDCST1  FORM     12.2
SYTDCST2  FORM     12.2
SYTDCST3  FORM     12.2
SYTDCST4  FORM     12.2
SYTDCST5  FORM     12.2
SYTDQTY   FORM     12.2
TMPPER    DIM      2
TMPYEAR   DIM      4
YTD01     DIM      13
YTD02     DIM      13
YTD03     DIM      13
YTDCST    FORM     12.2
YTDCST1   FORM     12.2
YTDCST2   FORM     12.2
YTDCST3   FORM     12.2
YTDCST4   FORM     12.2
YTDCST5   FORM     12.2
YTDQTY    FORM     12.2
.-------------------------------------------------------
.@@@@@ Start of Generated Code @@@@@
. NB - Code & Comments Included Before the above line 
.      will not be overwritten by the generation of a 
.      program. The STDGENDF file and VERSION must appear
.      before the line. 
.-----------------------------------------
.   Program       :   IBACEA32
.   Function      :   Patient Service Analysis                
.   Generated  On :   09/07/2002
.-----------------------------------------
.=========================
.File Description Includes
.-------------------------
          INC       CCSCONFD/INC
          INC       CEAPAAFD/INC
          INC       CEAPERFD/INC
          INC       CEAPSCFD/INC
          INC       CEASPCFD/INC
.=========================
.Local Variable Definition
.-------------------------
REPTTYPE  FORM      1 * Scr 00 Fld     8
REPTDESC  DIM      20 * Scr 00 Fld     9
.
ACCEPT    FORM      1
SACCEPT   FORM      1
.
PRGID     INIT      "IBACEA32"
PRGNAM    INIT      "Patient Service Analysis                "
.
.--------------------------------
. MAINLINE - Controlling Logic
.--------------------------------
          TOPIC     PRGID
          CLOCK     PORT,DIM2
          MOVE      DIM2,PORT
ML0000
          CALL      INIT0000
          CALL      SC00L000
          CHAIN     PGM
.---------------------------------------
. INIT - Open Files & Display Heading
.---------------------------------------
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CCENTRY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          DISPLAY   *P56:24,"Opening scrsbga1";
          OPEN      SCRSBGA1,"scrsbgaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA1,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA2,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrslta1";
          OPEN      SCRSLTA1,"scrsltaf"
          MOVE      "   64",AUDTSECT
          MOVE      " 39",AUDTPOST
          READ      CONTROLF,AUDTSECT;*AUDTPOST,AUDTFLAG
          IF        AUDTFLAG=1
            DISPLAY   *P56:24,"Opening ceaaudps";
            OPEN      CEAAUDPS,"ceaaudps"
          ENDIF
          DISPLAY   *P56:24,"Opening ceapaaaf";
          OPEN      CEAPAAA1,"ceapaaaf"
          DISPLAY   *P56:24,"Opening ceaperaf";
          OPEN      CEAPERA1,"ceaperaf"
          DISPLAY   *P56:24,"Opening ceaperaf";
          OPEN      CEAPERA2,"ceaperaf"
          DISPLAY   *P56:24,"Opening ceapscaf";
          OPEN      CEAPSCA1,"ceapscaf"
          DISPLAY   *P56:24,"Opening ceaspcaf";
          OPEN      CEASPCA1,"ceaspcaf"
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.
.------------------------------------
. Called for Print Screen Routine 
.------------------------------------
PRTSVARS
          MATCH     "00",SCRID
          IF         @EQUAL
            CALL       PS00L000   * Service Provider Analysis          
          ENDIF
          RETURN
.--------------------------------
. Screen - Service Provider Analysis          
.--------------------------------
SC00L000
SC00L100
          MOVE      "00",SCRID
          MOVE      ZERO,PRCOL
          MOVE      ZERO,PRVERT
          MOVE      "    1",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SCPSCOL,PRCOL
            MOVE     SCPSROW,PRVERT
          ENDIF
          CALL      SBACK000 * Display Background
.
SC00L200
          CALL      CF00L000 * Clear Fields
          CALL      MVACCEPT
SC00L300
          CALL      KY00L000 * Enter Key Fields
          BRANCH    EXIT,SC00L999,SC00L999
SC00L400
          CALL      WP00L000 * Load any WP Details
SC00L500
          CALL      LM00L000 * Load any Batch Details
SC00L600
          CALL      DF00L000 * Display Fields
SC00L700
          CALL      SF00L000 * Select Fields
          BRANCH    EXIT,SC00L100,SC00L100,SC00L200
SC00L800
SC00L900
SC00L999  RETURN
.--------------------------------------------------------
. Clear Fields for Screen : Service Provider Analysis          
.-------------------------------------------------------
CF00L000
          MOVE      SP70,CEPEYEAR
          MOVE      SP70,CEPEPER 
          MOVE      SP70,CEPEDES 
          MOVE      SP70,CEPEEND 
          MOVE      ZERO,REPTTYPE
          MOVE      SP70,REPTDESC
CF00L999  RETURN
.--------------------------------------------------------
. Enter Key Fields for Service Provider Analysis          
.--------------------------------------------------------
KY00L000
          MOVE      ZERO,EXIT
          GOTO      KY00L999
.
KY00L900  MOVE      ONE,EXIT
KY00L999  RETURN
.---------------------------------------------------------
. Load WP files for : Service Provider Analysis          
.---------------------------------------------------------
WP00L000
          RETURN
.---------------------------------------------------------
. Load Batch Screen for : Service Provider Analysis          
.---------------------------------------------------------
LM00L000
          RETURN
.---------------------------------------------------------
. Display Fields for : Service Provider Analysis          
.---------------------------------------------------------
DF00L000
          BRANCH   ACCEPT,DF00L999
          PACK     KEY15,PRGID,SCRID,SP70
          CALL     RSSCPS1
DF00L010
          CALL     RKSCPS1
          BRANCH   OVRCD,DF00L900
          MATCH    PRGID,SCPSPID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SCRID,SCPSSID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SP70,SCPSMTY
          GOTO     DF00L010 IF NOT EQUAL
.
          CALL     PS00L000 * Display Field
          GOTO     DF00L010
DF00L900
DF00L999
          RETURN
.---------------------------------------------------------
. Display/Print Fields for : Service Provider Analysis          
.---------------------------------------------------------
PS00L000   PACK      VAR,SP70,SP70
          PACK     PRINTVAR,SP70,SP70
          MOVE     SCPSITM,FIELDNO
          BRANCH    FIELDNO,PS00L999,PS00L999,PS00L999,PS00L101:
                            PS00L102,PS00L103,PS00L104,PS00L105:
                            PS00L106
          GOTO     PS00L999
.
PS00L101
          MOVE      CEPEYEAR,VAR     
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L102
          MOVE      CEPEPER,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L103
          MOVE      CEPEDES,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L104
          UNPACK    CEPEEND,CCENT,CYEAR,CMON,CDAY           
          CALL      PACDATE
          MOVE      CPCDATE,VAR
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L105
          MOVE      REPTTYPE,VAR     
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L106
          MOVE      REPTDESC,VAR     
          CALL      DISPITEM
          GOTO     PS00L999
PS00L999
          RETURN
.---------------------------------------------------------
. Selections for : Service Provider Analysis          
.---------------------------------------------------------
SF00L000  PACK     KEY13,PRGID,SCRID,SP70
          CALL     RSSCSL1
SF00L001  BRANCH   ACCEPT,SF00L020
.
SF00L005  CALL     MTSTQ000
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L006,SF00L101,SF00L102,SF00L103:
                            SF00L104,SF00L006,SF00L006,SF00L105:
                            SF00L006
SF00L006  BEEP
          GOTO     SF00L005
SF00L020  CALL     RKSCSL1
          BRANCH   OVRCD,SF00L090
          MATCH    PRGID,SCSLPID
          GOTO     SF00L090 IF NOT EQUAL
          MATCH    SCRID,SCSLSID
          GOTO     SF00L090 IF NOT EQUAL
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L020,SF00L020,SF00L020,SF00L103:
                            SF00L104,SF00L020,SF00L020,SF00L105:
                            SF00L020
          GOTO     SF00L020
SF00L090  MOVE     ZERO,ACCEPT
          GOTO     SF00L001
.
SF00L101
          MOVE      ZERO,EXIT
          GOTO      SF00L999
.
SF00L102
          CALL      REPT0000
          MOVE      ZERO,EXIT
          GOTO      SF00L999
.
SF00L103
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      KYEAR000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L103
            ENDIF
            BRANCH    EXIT,SF00L103,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      CEPEYEAR,VAR     
          CALL      DISPITEM
          GOTO     SF00L001
.
SF00L104
.
.
          MOVE      "    6",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,CEPEDES 
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
.
.
          MOVE      "    7",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,CEPEEND 
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      KCEPEA00
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L104
            ENDIF
            BRANCH    EXIT,SF00L104,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      CEPEPER,VAR      
          CALL      DISPITEM
.
.
          MOVE      "    6",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      CEPEDES,VAR      
            CALL      DISPITEM
          ENDIF
.
.
          MOVE      "    7",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            UNPACK    CEPEEND,CCENT,CYEAR,CMON,CDAY           
            CALL      PACDATE
            MOVE      CPCDATE,VAR
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L105
.
.
          MOVE      "    9",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,REPTDESC
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      KRTYP000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L105
            ENDIF
            BRANCH    EXIT,SF00L105,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      REPTTYPE,VAR     
          CALL      DISPITEM
.
.
          MOVE      "    9",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      REPTDESC,VAR     
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
SF00L990  MOVE      ONE,EXIT * Exit Char Restart
SF00L999  RETURN
.@@@@@ Function Restart Point @@@@@
.----------------------------------------
. Move one to accept to for insert mode
.----------------------------------------
MVACCEPT  MOVE      ONE,ACCEPT
          RETURN
.-------------------------------------------------------------
. Patient Service Summary Report
.-------------------------------------------------------------
REPT0000  MOVE      "   64",AUDTSECT
          READ      CONTROLF,AUDTSECT;*7,CCCODIR,*59,CCCOCD1,CCCOCD2,CCCOCD3:
                                      CCCOCD4,CCCOCD5
.
          PACK      KEY6,CEPEYEAR,CEPEPER
          UNPACK    KEY6,TMPYEAR,TMPPER
          CALL      RDCEPE1
          BRANCH    OVRCD,REPT9990
          UNPACK    CEPEEND,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,ENDDATE
          CLOCK     TIME,CTIMEIS
.
          MOVE      ZERO,GCURQTY
          MOVE      ZERO,GCURCST1
          MOVE      ZERO,GCURCST2
          MOVE      ZERO,GCURCST3
          MOVE      ZERO,GCURCST4
          MOVE      ZERO,GCURCST5
.
          MOVE      ZERO,GYTDQTY
          MOVE      ZERO,GYTDCST1
          MOVE      ZERO,GYTDCST2
          MOVE      ZERO,GYTDCST3
          MOVE      ZERO,GYTDCST4
          MOVE      ZERO,GYTDCST5
.
          MOVE      "99",CLNO
          MOVE      ZERO,CPAGENO
          PACK      KEY3,SP70
          CALL      RSCESP1
REPT1000  CALL      RKCESP1
          BRANCH    OVRCD,REPT9000
.
          COMPARE   "55",CLNO
          IF        @LESS
            CALL      UND132
            PRINT     "Service Provider : ",CESPSPC,SP1,CESPDES
            CALL      UND132
            ADD       ONE,CLNO
          ELSE
            CALL      HEAD0000
          ENDIF
.
          CALL      CLRSPC00       * Clear Servcie Provider Totals
.
          PACK      KEY7,SP70
          CALL      RSCEPS1
REPT1100  CALL      RKCEPS1        * Loop Through All Patient Services
          BRANCH    OVRCD,REPT1900
.
          MOVE      ONE,FLAGPSC
.
          PACK      KEY16,CESPSPC,CEPSPSC,TMPYEAR,SP70
          CALL      RSCEPA1
REPT1200  CALL      RKCEPA1
          BRANCH    OVRCD,REPT1290
          MATCH     CEPASPC,CESPSPC
          GOTO      REPT1290 IF NOT EQUAL
          MATCH     CEPAPSC,CEPSPSC
          GOTO      REPT1290 IF NOT EQUAL
          MATCH     CEPAYEAR,TMPYEAR
          GOTO      REPT1290 IF NOT EQUAL
          MATCH     CEPAPER,TMPPER
          GOTO      REPT1290 IF LESS
          IF        FLAGPSC=1
            CALL      CLRPSC00
            MOVE      ZERO,FLAGPSC
          ENDIF
.
          MATCH     CEPAPER,TMPPER
          IF        @EQUAL
            MOVE      CEPACST1,CURCST1
            MOVE      CEPACST2,CURCST2
            MOVE      CEPACST3,CURCST3
            MOVE      CEPACST4,CURCST4
            MOVE      CEPACST5,CURCST5
            MOVE      CEPAQTY,CURQTY
          ENDIF
.
          ADD       CEPACST1,YTDCST1
          ADD       CEPACST2,YTDCST2
          ADD       CEPACST3,YTDCST3
          ADD       CEPACST4,YTDCST4
          ADD       CEPACST5,YTDCST5
          ADD       CEPAQTY,YTDQTY
          GOTO      REPT1200
.
. Print Patient Service
.
REPT1290  BRANCH    FLAGPSC,REPT1100     * Nothing for this Patient Service
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          CALL      ADDTOT00   * Add Totals
.
          ASSIGN    YTDCST1+YTDCST2+YTDCST3+YTDCST4+YTDCST5,YTDCST
          ASSIGN    CURCST1+CURCST2+CURCST3+CURCST4+CURCST5,CURCST
          CALL      PRINT000
          GOTO      REPT1100
.
. Print Service Provider
.
REPT1900  COMPARE   "99",CLNO              * Check for Forced Page Break
          CALL      HEAD0000 IF EQUAL
.
          CALL      UND132
.
          MOVE      SCURCST1,CURCST1
          MOVE      SCURCST2,CURCST2
          MOVE      SCURCST3,CURCST3
          MOVE      SCURCST4,CURCST4
          MOVE      SCURCST5,CURCST5
          MOVE      SCURQTY,CURQTY
.
          MOVE      SYTDCST1,YTDCST1
          MOVE      SYTDCST2,YTDCST2
          MOVE      SYTDCST3,YTDCST3
          MOVE      SYTDCST4,YTDCST4
          MOVE      SYTDCST5,YTDCST5
          MOVE      SYTDQTY,YTDQTY
          MOVE      CESPSPC,CEPSPSC
          MOVE      CESPDES,CEPSDES
          ASSIGN    YTDCST1+YTDCST2+YTDCST3+YTDCST4+YTDCST5,YTDCST
          ASSIGN    CURCST1+CURCST2+CURCST3+CURCST4+CURCST5,CURCST
          CALL      PRINT000
          GOTO      REPT1000
.
REPT9000  CALL      UND132
.
          MOVE      GCURCST1,CURCST1
          MOVE      GCURCST2,CURCST2
          MOVE      GCURCST3,CURCST3
          MOVE      GCURCST4,CURCST4
          MOVE      GCURCST5,CURCST5
          MOVE      GCURQTY,CURQTY
.
          MOVE      GYTDCST1,YTDCST1
          MOVE      GYTDCST2,YTDCST2
          MOVE      GYTDCST3,YTDCST3
          MOVE      GYTDCST4,YTDCST4
          MOVE      GYTDCST5,YTDCST5
          MOVE      GYTDQTY,YTDQTY
          MOVE      SP70,CEPSPSC
          MOVE      "Report Total",CEPSDES
.
          ASSIGN    YTDCST1+YTDCST2+YTDCST3+YTDCST4+YTDCST5,YTDCST
          ASSIGN    CURCST1+CURCST2+CURCST3+CURCST4+CURCST5,CURCST
.
          CALL      PRINT000
.
          GOTO      REPT9900
.
REPT9900  CALL      UND132
          PRINT     *40,"**** End of Report ****"
          GOTO      REPT9999
.
REPT9990  MOVE      "Invalid Period - ",DISPMSG
          BEEP
          CALL      MESSAGE1
.
REPT9999  RETURN                      * Return to program.
.-----------------------------------------------------------
. Print Report Line
.-----------------------------------------------------------
PRINT000  BRANCH    REPTTYPE,PRINT100,PRINT200
.
PRINT100  MOVE      "99,999,999.99",CUR01
          MOVE      "99,999,999.99",CUR02
          MOVE      "99,999,999.99",CUR03
          MOVE      "99,999,999.99",YTD01
          MOVE      "99,999,999.99",YTD02
          MOVE      "99,999,999.99",YTD03
          FEDIT     CURQTY,CUR01
          FEDIT     CURCST,CUR02
          ASSIGN    CURCST/CURQTY,F12P2
          FEDIT     F12P2,CUR03
          FEDIT     YTDQTY,YTD01
          FEDIT     YTDCST,YTD02
          ASSIGN    YTDCST/YTDQTY,F12P2
          FEDIT     F12P2,YTD03
          PRINT     "| ",CEPSPSC,SP1,CEPSDES,SP4,"|":
                    CUR01,CUR02,CUR03," |":
                    YTD01,YTD02,YTD03," |"
          ADD       ONE,CLNO
          GOTO      PRINT999
.-----------------------------------------------------------
. Report Format 2
.-----------------------------------------------------------
PRINT200  MOVE      "99,999,999.99",COL01
          MOVE      "99,999,999.99",COL02
          MOVE      "9,999,999.99",COL03
          MOVE      "9,999,999.99",COL04
          MOVE      "9,999,999.99",COL05
          MOVE      "9,999,999.99",COL06
          MOVE      "9,999,999.99",COL07
          FEDIT     CURQTY,COL01
          FEDIT     CURCST,COL02
          FEDIT     CURCST1,COL03
          FEDIT     CURCST2,COL04
          FEDIT     CURCST3,COL05
          FEDIT     CURCST4,COL06
          FEDIT     CURCST5,COL07
          PRINT     "| ",CEPSPSC,SP1,CEPSDES,*70,"|",*132,"|"
.
          PRINT     "|",*28,"Current",*43,COL01,COL02," |":
                    COL03,COL04,COL05,COL06,COL07," |"
          MOVE      "99,999,999.99",COL01
          MOVE      "99,999,999.99",COL02
          MOVE      "9,999,999.99",COL03
          MOVE      "9,999,999.99",COL04
          MOVE      "9,999,999.99",COL05
          MOVE      "9,999,999.99",COL06
          MOVE      "9,999,999.99",COL07
          FEDIT     YTDQTY,COL01
          FEDIT     YTDCST,COL02
          FEDIT     YTDCST1,COL03
          FEDIT     YTDCST2,COL04
          FEDIT     YTDCST3,COL05
          FEDIT     YTDCST4,COL06
          FEDIT     YTDCST5,COL07
          PRINT     "|",*28,"Year to Date",*43,COL01,COL02," |":
                    COL03,COL04,COL05,COL06,COL07," |",*N
          ADD       FOUR,CLNO
          GOTO      PRINT999
.
PRINT999  RETURN
.------------------------------------------------------------
. Clear Service Provider Totals
.------------------------------------------------------------
CLRSPC00  MOVE      ZERO,SCURCST1
          MOVE      ZERO,SCURCST2
          MOVE      ZERO,SCURCST3
          MOVE      ZERO,SCURCST4
          MOVE      ZERO,SCURCST5
          MOVE      ZERO,SCURQTY
          MOVE      ZERO,SYTDCST1
          MOVE      ZERO,SYTDCST2
          MOVE      ZERO,SYTDCST3
          MOVE      ZERO,SYTDCST4
          MOVE      ZERO,SYTDCST5
          MOVE      ZERO,SYTDQTY
          RETURN
.------------------------------------------------------------
. Clear Patient Service Totals
.------------------------------------------------------------
CLRPSC00  MOVE      ZERO,CURCST1
          MOVE      ZERO,CURCST2
          MOVE      ZERO,CURCST3
          MOVE      ZERO,CURCST4
          MOVE      ZERO,CURCST5
          MOVE      ZERO,CURQTY
          MOVE      ZERO,YTDCST1
          MOVE      ZERO,YTDCST2
          MOVE      ZERO,YTDCST3
          MOVE      ZERO,YTDCST4
          MOVE      ZERO,YTDCST5
          MOVE      ZERO,YTDQTY
          RETURN
.------------------------------------------------------------
. Add Totals
.------------------------------------------------------------
ADDTOT00  ADD       CURCST1,GCURCST1
          ADD       CURCST2,GCURCST2
          ADD       CURCST3,GCURCST3
          ADD       CURCST4,GCURCST4
          ADD       CURCST5,GCURCST5
          ADD       CURQTY,GCURQTY
.
          ADD       YTDCST1,GYTDCST1
          ADD       YTDCST2,GYTDCST2
          ADD       YTDCST3,GYTDCST3
          ADD       YTDCST4,GYTDCST4
          ADD       YTDCST5,GYTDCST5
          ADD       YTDQTY,GYTDQTY
.
          ADD       CURCST1,SCURCST1
          ADD       CURCST2,SCURCST2
          ADD       CURCST3,SCURCST3
          ADD       CURCST4,SCURCST4
          ADD       CURCST5,SCURCST5
          ADD       CURQTY,SCURQTY
.
          ADD       YTDCST1,SYTDCST1
          ADD       YTDCST2,SYTDCST2
          ADD       YTDCST3,SYTDCST3
          ADD       YTDCST4,SYTDCST4
          ADD       YTDCST5,SYTDCST5
          ADD       YTDQTY,SYTDQTY
.
          RETURN
.------------------------------------------------------------
. Print Headings
.------------------------------------------------------------
HEAD0000  CALL      HEAD132
          DISPLAY   *P1:24,*EL,"Printing Page : ",*V2LON,CPAGENO
          PRINT     "Year : ",CEPEYEAR,"   Period : ",CEPEPER,SP1,CEPEDES:
                    "Ending ",ENDDATE
          CALL      UND132
          BRANCH    REPTTYPE,HEAD1000,HEAD2000
.
HEAD1000  PRINT     "| Patient Service                                |":
                    "              Current Period            |":
                    "                Year to Date            |"
          PRINT     "|                                                |":
                    "     Quantity    Std. Cost    Avg. Cost |":
                    "     Quantity    Std. Cost    Avg. Cost |"
          GOTO      HEAD9000
.
HEAD2000  PRINT     "| Patient Service                           ":
                    "                         |",*132,"|"
          PRINT     "|                                           ":
                    "   Quantity   Total Cost |":
                    SP2,CCCOCD1,SP2,CCCOCD2,SP2,CCCOCD3:
                    SP2,CCCOCD4,SP2,CCCOCD5:
                    *132,"|"
          GOTO      HEAD9000
.
HEAD9000  CALL      UND132
          PRINT     "Service Provider : ",CESPSPC,SP1,CESPDES
          CALL      UND132
.
          ADD       "4",CLNO
          RETURN
.
.-------------------------------------------------------------
. Enter Year
.-------------------------------------------------------------
KYEAR000  MOVE       CYY,CYEAR
          MOVE       CCC,CCENT
          PACK       C4YEAR WITH CCENT,CYEAR
          KEYIN      *PCCOL:CVERT,*V2LON,*DE,*JR,C4YEAR;
          RESET      C4YEAR
          GOTO       KYEAR900 IF EOS
.
          UNPACK     C4YEAR WITH XCENT,XYEAR
          MATCH      SP2,XCENT
          GOTO       KYEAR100 IF NOT EQUAL
          MOVE       CCENT,XCENT
.
KYEAR100  REP        " 0",XYEAR
          MOVE       XYEAR,IYEAR
          REP        " 0",XCENT
          MOVE       XCENT,ICENT
.
.         Check for a valid year
.
          COMPARE    ZERO,IYEAR
          IF         @LESS
            MOVE       "Invalid Year",DDISPMSG
            CALL       DATEMSGC
            GOTO       KYEAR000
          ENDIF
.
          PACK       CEPEYEAR,XCENT,XYEAR
          MOVE       ZERO,EXIT
          GOTO       KYEAR999
.
KYEAR900  MOVE       ONE,EXIT
          GOTO       KYEAR999
.
KYEAR999  RETURN
.-------------------------------------------------------------
. Enter Report Type
.-------------------------------------------------------------
KRTYP000  MOVE      SP70,DISPMSG
          CALL      MESSAGE2
          DISPLAY   *P1:24,*EL,"Report Format : ":
                    *V2LON,"1",*HOFF,". Summary, ":
                    *V2LON,"2",*HOFF,". Detailed"
KRTYP100  KEYIN     *PCCOL:CVERT,*V2LON,REPTTYPE
          BRANCH    REPTTYPE,KRTYP110,KRTYP120
          BEEP
          GOTO      KRTYP100
.
KRTYP110  MOVE      "Summary              ",REPTDESC
          GOTO      KRTYP999
.
KRTYP120  MOVE      "Detailed             ",REPTDESC
          GOTO      KRTYP999
.
KRTYP999  CALL      MESSAGE3
          RETURN
.
.-------------------------------
. Includes for I/O and Keyins
.-------------------------------
          INC       CEAPAAIO/INC
          INC       CEAPERIO/INC
          INC       CEAPERKY    
          INC       CEAPSCIO/INC
          INC       CEASPCIO/INC
.
          INC       STDGENCD
.
