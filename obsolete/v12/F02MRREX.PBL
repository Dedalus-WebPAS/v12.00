.*****************************************************************************
.*    Program      : F02MRREX                                                *
.*    Description  : Conversion mrtrexaf to new File layout                  *
.*                                                                           *
.*    Author       : Mike Laming                                             *
.*    Date         : 30/05/2011                                              *
.*    Modifications:                                                         *
.*        V10.03.03 26/03/2013  Ebon Clements CAR 283238                     *
.*                  Changed FINDLOCS to read mrtlocaf using index 4          *
.*        V10.03.02 06/06/2012  Steve Armstrong   CAR 266826                 *
.*                  Changes to only populate new field for both c-isam &     *
.*                  Oracle.                                                  *
.*        V10.03.01 25/05/2012  Steve Armstrong    CAR 266069                *
.*                  Mods to change program to only populate MRRXHOSF         *
.*                  and MRRXHOST for use with both c-isam and Oracle.        *
.*****************************************************************************
.*        V10.02.00 30/05/2011  Mike Laming   CAR 240724                     *
.*                  Fixit for "mrtrexaf" - added Home Hospital From & To     *
.*****************************************************************************
.
          INC       STD001FD
.
MRTREXA1   IFILE    SQL, FIXED=276, KEY="U1-4"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
MRRXEXID  DIM       4      4        1     Extract ID
MRRXDESC  DIM       35     35       5     Extract Description
MRRXDIFR  DIM       8      8        40    Discharge Date From
MRRXDITO  DIM       8      8        48    Discharge Date To
MRRXVIFR  DIM       8      8        56    Visit Date From
MRRXVITO  DIM       8      8        64    Visit Date To
MRRXUNIT  DIM       3      3        72    Discharge Unit
MRRXWARD  DIM       3      3        75    Discharge Ward
MRRXDOCT  DIM       6      6        78    Discharge Doctor
MRRXDECD  DIM       1      1        84    Include Deceased
MRRXNOEX  FORM      5      4        85    Number of Records Extracted
MRRXPROG  DIM       1      1        89    Extract in
MRRXDIA1  DIM       9      9        90    Diagnosis Code 1
MRRXDIA2  DIM       9      9        99    Diagnosis Code 2
MRRXDIA3  DIM       9      9        108   Diagnosis Code 3
MRRXPRO1  DIM       9      9        117   Procedure Code 1
MRRXPRO2  DIM       9      9        126   Procedure Code 2
MRRXPRO3  DIM       9      9        135   Procedure Code 3
MRRXPDRG  DIM       4      4        144   Provisional DRG
MRRXDISO  DIM       1      1        148   Discharge Patients Only Indicator
MRRXURFR  DIM       8      8        149   U/R Number From
MRRXURTO  DIM       8      8        157   U/R Number To
MRRXDTFR  DIM       3      3        165   Document Type From
MRRXDTTO  DIM       3      3        168   Document Type To
MRRXHLFR  DIM       4      4        171   MR Home Location From
MRRXHLTO  DIM       4      4        175   MR Home Location To
MRRXSMDY  DIM       1      1        179   Sameday
MRRXCOFR  DIM       4      4        180   Coder ID From
MRRXCOTO  DIM       4      4        184   Coder ID To
MRRXSNAG  DIM       3      3        188   Coder Snags (Category VN)
MRRXCDFR  DIM       8      8        191   Coding Date From
MRRXCDTO  DIM       8      8        199   Coding Date To
MRRXINCM  DIM       1      1        207   Coding Complete
MRRXAAGE  FORM      3      3        208   Age at Admission
MRRXGEND  DIM       1      1        211   Gender of Patient
MRRXPOST  DIM       8      8        212   Postcode of Patient at Visit Date
MRRXCOB   DIM       3      3        220   Country of Birth (Cat. C)
MRRXINVT  DIM       1      1        223   Exclude Visit Type 1
MRRXIPRI  DIM       1      1        224   Include Primary Disease Coding
MRRXINV1  DIM       1      1        225   Exclude Visit Type 2
MRRXINV2  DIM       1      1        226   Exclude Visit Type 3
MRRXBLFR  DIM       4      4        227   Procedure Block Number From
MRRXBLTO  DIM       4      4        231   Procedure Block Number To
MRRXDGFR  DIM       4      4        235   DRG From
MRRXDGTO  DIM       4      4        239   DRG To
MRRXHOSP  DIM       3      3        243   Research Hospital ID (pathspaf)
MRRXHOSF  DIM       3      3        246   Home Hospital ID From
MRRXHOST  DIM       3      3        249   Home Hospital ID To
MRRXSPAR  DIM       24     24       252   Spare
.
.End of Record                      276
.
.
MRTLOCA4  IFILE     SQL, FIXED=165, KEY="U1-4,55-57"
.
.Name     Type      Length  Phys.  Start  Description
.-------  ----      ------  -----  -----  ------------------
MRLOCODE  DIM       4          4       1  Location Code
MRLODESC  DIM       30        30       5  Description
MRLOTYPE  DIM       3          3      35  Type (Cat LT)
MRLOINDC  DIM       1          1      38  Indicator   1 = Internal
.                                                     2 = External
MRLOSTAT  FORM      1          2      39  Status Indicator 0 = Active
.                                                          1 = Non Active
MRLOUSAG  FORM      1          2      41  Usage Indicator  0 = Not Used
.                                                          1 = Used
MRLOPRNT  DIM       6          6      43  Printer
MRLOSTCD  DIM       6          6      49  Stationery Code
MRLOHOSP  DIM       3          3      55  Hospital ID (pathspaf)
MRLOPRLC  DIM       1          1      58  Priority location (1=yes)
MRLOBPRN  DIM       6          6      59  Bulk Record Request Printer
MRLOTREC  DIM       1          1      65  Using Tracking Receipt For This
.                                         Location  0=No, 1=Yes
MRLOSPAR  DIM       99         99     66  Spare
.
IBCNMHOS  FORM      1                       * Multi Hospital flag
DISNUM    FORM      4
RECNUM    FORM      8
.
.
PRGNAM    INIT      "CONVERSION MRTREXFD"
PRGID     INIT      "F02MRREX"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
MAIN1000  CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN2000              * run fixit
          GOTO      MAIN9999                      * exit selected 
.
MAIN2000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN1000:               * no
                          MAIN9999                * cancel
.
.         Running c-isam/oracle fix
.
MAIN5000  CALL      FFIX0000                      * run field fix
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.* Returns:  EXIT  0 = Ok to continue                               *
.*                 1 = Exit program                                 *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening ":
                    *P64:24,"mrtrexaf"
          OPEN      MRTREXA1,"mrtrexaf"
.         
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.         
          BRANCH    IBCNMHOS,INIT9000            * using multi-hospital
.         
          DISPLAY   *P1:24,*EL,*B,"Fixit only required for multi-hospital.  ";
          CALL      HITENTER
          GOTO      INIT9100
.         
INIT9000  DISPLAY   *P64:24,"mrtlocaf"
          OPEN      MRTLOCA4,"mrtlocaf"
.         
          MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9100  MOVE      ONE,EXIT
.         
INIT9999  RETURN
.
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**********************************************************************
.*                               FINDLOCs                             *
.*        Read through Location records to get Hospital IDs           *
.**********************************************************************
.
FINDLOCS  PACK      KEY7,MRRXHLFR,SP70
          CALL      RSMRLOC4
          CALL      RKMRLOC4
          BRANCH    OVRCD,FINDLO10
.
          MATCH     MRLOCODE,MRRXHLFR       * is Location = MRTREX.From Locn?
          GOTO      FINDLO10 IF NOT EQUAL
.
          MOVE      MRLOHOSP,MRRXHOSF     * set Hospital ID From
.
FINDLO10  PACK      KEY7,MRRXHLTO,SP70
          CALL      RSMRLOC4
          CALL      RKMRLOC4
          BRANCH    OVRCD,FINDLO99
.
          MATCH     MRLOCODE,MRRXHLTO       * is Location = MRTREX.To Locn?
          GOTO      FINDLO99 IF NOT EQUAL
.
          MOVE      MRLOHOSP,MRRXHOST     * set Hospital ID To 
.
FINDLO99  RETURN
+
.*****************************************************************************
.*                            FFIX0000                                       *
.*                    Oracle fix to "mrtrexaf"                               *
.*****************************************************************************
.
FFIX0000  DISPLAY   *P1:20,*EL,"Record No. :";
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
.
          PACK      KEY4,SP70
          CALL      RSMRREX1                      * position at start of file
FFIX0500  CALL      RKMRREX1                      * read next record
          BRANCH    OVRCD,FFIX9000                * eof - finished
.
          ADD       ONE,RECNUM                    * increment record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM 
          ENDIF     
.
          MOVE      SP70,MRRXHOSF           * set Hospital ID From
          MOVE      SP70,MRRXHOST           * set Hospital ID To
.
          CALL      FINDLOCS                * get Hospital IDs for each Location
          CALL      UPMRREX1
.
          GOTO      FFIX0500                     * get next record           
.
FFIX9000  CLOSE     MRTREXA1
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:24,*EL,"Update completed.  ";
          CALL      HITENTER
.
FFIX9999  RETURN    
+
.*****************************************************************************
.         
.         New mrtrexaf I/O Routines
.
RSMRREX1  RESET     KEY4
          READ      MRTREXA1,KEY4;;
          RETURN
.
RKMRREX1  MOVE      ZERO,OVRCD
          READKS    MRTREXA1;MRRXEXID,MRRXDESC,MRRXDIFR,MRRXDITO,MRRXVIFR:
                        MRRXVITO,MRRXUNIT,MRRXWARD,MRRXDOCT,MRRXDECD,MRRXNOEX:
                        MRRXPROG,MRRXDIA1,MRRXDIA2,MRRXDIA3,MRRXPRO1,MRRXPRO2:
                        MRRXPRO3,MRRXPDRG,MRRXDISO,MRRXURFR,MRRXURTO,MRRXDTFR:
                        MRRXDTTO,MRRXHLFR,MRRXHLTO,MRRXSMDY,MRRXCOFR,MRRXCOTO:
                        MRRXSNAG,MRRXCDFR,MRRXCDTO,MRRXINCM,MRRXAAGE,MRRXGEND:
                        MRRXPOST,MRRXCOB,MRRXINVT,MRRXIPRI,MRRXINV1,MRRXINV2:
                        MRRXBLFR,MRRXBLTO,MRRXDGFR,MRRXDGTO,MRRXHOSP,MRRXHOSF:
                        MRRXHOST,MRRXSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.                       
UPMRREX1  UPDATE    MRTREXA1;MRRXEXID,MRRXDESC,MRRXDIFR,MRRXDITO,MRRXVIFR:   
                        MRRXVITO,MRRXUNIT,MRRXWARD,MRRXDOCT,MRRXDECD,MRRXNOEX:
                        MRRXPROG,MRRXDIA1,MRRXDIA2,MRRXDIA3,MRRXPRO1,MRRXPRO2:
                        MRRXPRO3,MRRXPDRG,MRRXDISO,MRRXURFR,MRRXURTO,MRRXDTFR:
                        MRRXDTTO,MRRXHLFR,MRRXHLTO,MRRXSMDY,MRRXCOFR,MRRXCOTO:
                        MRRXSNAG,MRRXCDFR,MRRXCDTO,MRRXINCM,MRRXAAGE,MRRXGEND:
                        MRRXPOST,MRRXCOB,MRRXINVT,MRRXIPRI,MRRXINV1,MRRXINV2:
                        MRRXBLFR,MRRXBLTO,MRRXDGFR,MRRXDGTO,MRRXHOSP,MRRXHOSF:
                        MRRXHOST,MRRXSPAR
          GOTO      UPDFLOCK IF LOCKED
          RETURN
.
.         mrtlocaf I/O routines
.         
RSMRLOC4  MOVE     ZERO,OVRCD
          RESET    KEY7
          READ     MRTLOCA4,KEY7;;
          RETURN
.
.
RKMRLOC4  MOVE     ZERO,OVRCD
          READKS   MRTLOCA4;MRLOCODE,MRLODESC,MRLOTYPE,MRLOINDC,MRLOSTAT:
                            MRLOUSAG,MRLOPRNT,MRLOSTCD,MRLOHOSP,MRLOPRLC:
                            MRLOBPRN,MRLOTREC,MRLOSPAR
          GOTO     OVERCOND IF OVER
          RETURN
.
          INC       STD001IO

