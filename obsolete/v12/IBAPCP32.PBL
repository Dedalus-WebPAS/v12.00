.***************************************************************************
.* System        :  PATIENT CARE                                           *
.* Program       :  IBAPCP32                                               *
.* Desc          :  Diagnosis Category Listing                             *
.***************************************************************************
.* Date          :  19/12/89                                               *
.* Author        :  P DUNCAN                                               *
.* Version       :  1.0                                                    *
.* Modifications :                                                         *
.*        V9.02.00  30/12/2002 Tracey Nguyen srf 23497                     *
.*                  r5.10 Care Plans Port to V9                            *
.***************************************************************************
.
.$$$$$$
.  Diagnosis Category Listing (IBAPCP32)
.  -------------------------------------
.
.  - Initialization
.
.       - Display Header
.       - Open File
.            PCPDSCA1
.
.  - Process Options
.
.       - 0 = Exit
.       - 1 = Print Type of Clinic Category Listing
.       - 2 = Print Clinic Conditions Category Listing
.       - 3 = Print Health Patterns Category Listing
.       - 4 = Print Response Patterns Category Listing
.       - 5 = Print ABC PRN REST codes Category Listing
.
.           - O.K. to Continue?
.           - Print required records
.
.  - END
.$$$$$$
          INC       STD001FD
          INC       PCPDSCFD/INC
+
.***************************************************************************
.*                              CONSTANTS                                  *
.***************************************************************************
FINISH    INIT      "FINISH"
START     INIT      "START"
.
PRGID     INIT      "IBAPCP32"
PRGNAM    INIT      "Diagnosis Category Listing"
VERSION   INIT      "V12.00.00"
OPT1      INIT      "TYPE OF CLINIC CONDITIONS"
OPT2      INIT      "CLINIC CONDITIONS"
OPT3      INIT      "HEALTH PATTERNS"
OPT4      INIT      "RESPONSE PATTERNS"
OPT5      INIT      "ABC PRN REST codes"
+
.***************************************************************************
.*                          GLOBAL VARIABLES                               *
.***************************************************************************
TEMPCODE  DIM        9
HEADING   DIM       30
RECNO     FORM       6
+
.***************************************************************************
.*                               MAINLINE                                  *
.*                           Controlling Logic                             *
.***************************************************************************
ML0000    CALL      INIT0000                * display header &
.                                           * open file
.
ML1000    CALL      SEL0000                 * select listing or exit
          BRANCH    EXIT,ML9999
.
ML2000    CALL      CONT0000                * O.K. TO proceed
          BRANCH    EXIT,ML1000             * 1 = No
.                                           * 0 = Yes
          CALL      OTPT0000                * Print selected listing  
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.***************************************************************************
.*                               INIT0000                                  *
.*                     Display header and open file                        *
.***************************************************************************
INIT0000  CALL      DISPHEAD                * display screen header
          DISPLAY   *P50:24,"Opening pcpdscaf";
          OPEN      PCPDSCA1,"pcpdscaf"
.
INIT9999  RETURN
+
.***************************************************************************
.*                                SEL0000                                  *
.*                         Select option or exit                          *
.***************************************************************************
SEL0000   CALL      SCRN0000               * display screen selection
          MOVE      ZERO,RECNO
          MOVE      ZERO,TEMPCODE
.
SEL1000   DISPLAY   *P17:11,*EL,UNDLN2;
          KEYIN     *P17:11,*V2LON,OPTION;
          COMPARE   ZERO,OPTION             * test for 0
          GOTO      SEL2000 IF EQUAL
          BRANCH    OPTION,SEL3000,SEL3000,SEL3000,SEL3000,SEL3000
          BEEP                             * beep if not 0 to 5
          GOTO      SEL1000
.
SEL2000   MOVE      TRUE,EXIT              * 0 = exit
          GOTO      SEL9999
.
SEL3000   MOVE      FALSE,EXIT             * 1 to 5 = produce a printout
          LOAD      HEADING,OPTION,OPT1,OPT2,OPT3,OPT4,OPT5
.
SEL9999   RETURN
+
.***************************************************************************
.*                               SCRN0000                                  *
.*                   Display selection part of screen                      *
.***************************************************************************
SCRN0000  DISPLAY   *P1:4,*EF,*V2LON,ZERO,*P1:5,ONE,*P1:6,TWO:
                    *P1:7,THREE,*P1:8,FOUR,*P1:9,FIVE,*HOFF:
                    *P2:4," = Exit":
                    *P2:5," = Type of clinic Conditions":
                    *P2:6," = Clinic Conditions":
                    *P2:7," = Health Patterns":
                    *P2:8," = Response Patterns":
                    *P2:9," = ABC PRN REST codes":
                    *P1:11,"Select Option : ";
.
SCRN9999  RETURN
+
.***************************************************************************
.*                               OTPT0000                                  *
.*              Loop through file printing selected files                  *
.***************************************************************************
.
.
OTPT0000  MOVE      ZERO,CPAGENO              * set page no variable to 0
          CLOCK     TIME,CTIMEIS
          CALL      HEAD0000                  * perform heading routine
          DISPLAY   *P1:24,*EL,*P10:24,*V2LON,"code : ";
          MOVE      OPTION,PCDSTYPE           * set pcdstype to option chosen
          PACK      KEY13,PCDSTYPE,SP20       * set key13 to pcdstype and spaces
          CALL      RSPCDSC1                  * place at beginning of required
                                              * records
.
OTPT1000  CALL      RKPCDSC1                  * read next record
          BRANCH    OVRCD,OTPT3000
.
          COMPARE   OPTION,PCDSTYPE           * test if record is of required
                                              * type
          GOTO      OTPT3000 IF NOT EQUAL     * end report if not
.
.
          DISPLAY   *P17:24,*V2LON,PCDSCODE;  * display code of record being
                                              * printed
.
          CALL      PRNT0000                  * print out one record
          GOTO      OTPT1000                  * go back for next record

.
OTPT3000  CALL      DRAW0000                  * finish report
          PRINT     *N,*N,"Number of codes printed = ",RECNO
          PRINT     *N,*N,"*** END OF REPORT ***";
          GOTO      OTPT9999
.
OTPT9999  RETURN 
+
+
+
.***************************************************************************
.*                                 CONT0000                                *
.*              'OK to continue' sub-routine                               *
.***************************************************************************
CONT0000  DISPLAY   *P1:24,"OK to continue ( / ) ?":
                    *P17:24,*V2LON,"Y",*P19:24,"N",*HOFF,*P24:24,UNDLN1;
          KEYIN     *P24:24,*V2LON,ANS     *response to 'OK to continue
.
.
          MATCH     ANS,ANSY                      * test for 'Yes'
          GOTO      CONT1000 IF EQUAL
.
.
          MATCH     ANS,ANSN                      * test for 'No'
          GOTO      CONT2000 IF EQUAL
.
.
          BEEP                                    * BEEP if not Y or N
          GOTO      CONT0000                      * ask ,again
.
.-------  Yes option chosen -------
.
CONT1000  MOVE      FALSE,EXIT                    * set 'EXIT' to FALSE(0)
          GOTO      CONT9999
.
.-------  No option chosen  -------.
.
CONT2000  MOVE      TRUE,EXIT                     * set 'EXIT' to TRUE(1)
.
CONT9999  RETURN
.
.***************************************************************************
.*                               PRNT0000                                  *
.*                      Read file and print listing                        *
.***************************************************************************
PRNT0000  CALL     LINE0000           * Print out record
.
.
PRNT9999  RETURN
+
.***************************************************************************
.*                               DRAW0000                                  *
.*                      Draw common line on report                         *
.***************************************************************************
DRAW0000  PRINT     *N,"*---------------------------------------":
                       "---------------------------------------*";
.
DRAW9999  RETURN
+
.***************************************************************************
.*                               HEAD0000                                  *
.*                           Print page header                             *
.***************************************************************************
HEAD0000  CALL      HEAD80
          PRINT     *20,HEADING,*N
          ADD       TWO,CLNO
          CALL      UND80
           
.HEAD0000  ADD       ONE,CPAGENO                  * increment page no by 1
.         COMPARE   ONE,CPAGENO                  * test for page 1, perform
.         CALL      DRAW0000 IF NOT EQUAL        * line drawing routine if
.                                                  page is 2
.         CLOCK     TIME,CTIMEIS
.
.         PRINT     *F,*N,*N,PRGID,*22,CNAME,*66,"DATE : ",CDATE,*N:
.                   VERSION,*22,PRGNAM,*66,"TIME : ",CTIMEIS,*N,*22:
.                   HEADING,*66,"PAGE :      ",CPAGENO
.                   *+
.         CALL      DRAW0000
* Print column headings
          PRINT     "| Code      |",*27,"Description",*80,"|"
          CALL      UND80
          ADD       ONE,CLNO
.         CALL      DRAW0000
.         MOVE      TEN,CLNO        * print first record for page on line 10
.
HEAD9999  RETURN
+
.***************************************************************************
.*                               LINE0000                                  *
.*                         Print a detail record                           *
.***************************************************************************
LINE0000  COMPARE   "55",CLNO                    * test for page overflow
          CALL      HEAD0000 IF NOT LESS         * call routine for newpage
                                                 * and heading
.
          MATCH     PCDSCODE,TEMPCODE            * test if code is same as
                                                 * previously
          GOTO      LINE1000 IF EQUAL            * don't print code if equal
.
          PRINT     "| ",*3,PCDSCODE,*13,"|",*15,PCDSDESC,*80,"|"
          ADD       ONE,RECNO                    * add to number of records
          MOVE      PCDSCODE,TEMPCODE     * set tempcode to code last printed
                                                 * printed
          GOTO      LINE2000
.
.
LINE1000  PRINT     "| ",*13,"|",*15,PCDSDESC,*80,"|"
.
LINE2000  ADD       ONE,CLNO                     * add to current line number
                                                 * for printout 
.
LINE9999  RETURN
+
.
          INC       STD001IO
          INC       PCPDSCIO/INC
