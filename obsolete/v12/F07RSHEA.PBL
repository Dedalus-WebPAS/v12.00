.*****************************************************************************
.*    Program      : F04RSHEA.PBL                                            *
.*    Description  : Conversion resheaaf to new File layout                  *
.*                                                                           *
.*    Author       : Steve Armstrong                                         *
.*    Date         : 05/11/2015                                              *
.*    Modifications:                                                         *
.*        V10.07.00 Steve Armstrong  CAR 303363                              *
.*                  Created program (based on F04RSHED)                      *
.*****************************************************************************
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
.
.
OLDFILE1  IFILE    SQL, FIXED=387, KEY="U1-8,9-13,14-17"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.REHARDT  DIM       8      8        1     Result Date
.REHARTM  DIM       5      5        9     Result Time hh:mm
.REHARUN  DIM       4      4        14    Result Unique ID
.REHAFUR  DIM       1      1        18    PID is U/R 0=No 1=Yes
.REHAPID  DIM       16     16       19    Patient Identifier
.REHAVIS  DIM       8      8        35    Visit Number
.REHASID  DIM       10     10       43    Sending Application ID
.REHAMDT  DIM       8      8        53    Message Date
.REHAMTM  DIM       5      5        61    Message Time
.REHAMID  DIM       20     20       66    Message Control ID
.REHAPAP  DIM       6      6        86    Placer Application
.REHAPOR  DIM       20     20       92    Placer Order No
.REHAFAP  DIM       6      6        112   Filler Application
.REHAFOR  DIM       20     20       118   Filler Order No
.REHAUSC  DIM       12     12       138   Universal Service Code  OBR-04
.REHAUCS  DIM       12     12       150   Universal Coding System OBR-04
.REHANOR  DIM       1      1        162   Normal Result 0=No 1=Yes
.REHACDT  DIM       8      8        163   Collected Date
.REHACTM  DIM       5      5        171   Collected Time
.REHACID  DIM       40     40       176   Collected Identifier
.REHAFCI  DIM       1      1        216   Clinical Information 0=No 1=Yes
.REHASDT  DIM       8      8        217   Specimen Received Date
.REHASTM  DIM       5      5        225   Specimen Received Time
.REHAOID  DIM       8      8        230   Ordering Provider ID
.REHAOSN  DIM       25     25       238   Ordering Provider Surname
.REHAOGN  DIM       20     20       263   Ordering Provider Given Name
.REHARRD  DIM       8      8        283   Result Report Date
.REHARRT  DIM       5      5        291   Result Report Time
.REHADSS  DIM       10     10       296   Diagnostic Service Sector
.REHARED  DIM       8      8        306   Request Date
.REHARET  DIM       5      5        314   Request Time
.REHARST  DIM       2      2        319   Result Status
.REHAIBY  DIM       30     30       321   Interpreted by Name
.REHAADC  DIM       6      6        351   Attending Doctor
.REHALAB  DIM       3      3        357   Laboratory Code
.REHALOC  DIM       25     25       360   Location           
OLDSPARE  DIM       2      2        385   Spare
.
.End of Record                      387
.
.                                     
.
RESHEAA1   IFILE    SQL, FIXED=418, KEY="U1-8,9-13,14-17"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
REHARDT   DIM       8      8        1     Result Date
REHARTM   DIM       5      5        9     Result Time hh:mm
REHARUN   DIM       4      4        14    Result Unique ID
REHAFUR   DIM       1      1        18    PID is U/R 0=No 1=Yes
REHAPID   DIM       16     16       19    Patient Identifier
REHAVIS   DIM       8      8        35    Visit Number
REHASID   DIM       10     10       43    Sending Application ID
REHAMDT   DIM       8      8        53    Message Date
REHAMTM   DIM       5      5        61    Message Time
REHAMID   DIM       20     20       66    Message Control ID
REHAPAP   DIM       6      6        86    Placer Application
REHAPOR   DIM       20     20       92    Placer Order No
REHAFAP   DIM       6      6        112   Filler Application
REHAFOR   DIM       20     20       118   Filler Order No
REHAUSC   DIM       12     12       138   Universal Service Code  OBR-04
REHAUCS   DIM       12     12       150   Universal Coding System OBR-04
REHANOR   DIM       1      1        162   Normal Result 0=No 1=Yes
REHACDT   DIM       8      8        163   Collected Date
REHACTM   DIM       5      5        171   Collected Time
REHACID   DIM       40     40       176   Collected Identifier
REHAFCI   DIM       1      1        216   Clinical Information 0=No 1=Yes
REHASDT   DIM       8      8        217   Specimen Received Date
REHASTM   DIM       5      5        225   Specimen Received Time
REHAOID   DIM       8      8        230   Ordering Provider ID
REHAOSN   DIM       25     25       238   Ordering Provider Surname
REHAOGN   DIM       20     20       263   Ordering Provider Given Name
REHARRD   DIM       8      8        283   Result Report Date
REHARRT   DIM       5      5        291   Result Report Time
REHADSS   DIM       10     10       296   Diagnostic Service Sector
REHARED   DIM       8      8        306   Request Date
REHARET   DIM       5      5        314   Request Time
REHARST   DIM       2      2        319   Result Status
REHAIBY   DIM       30     30       321   Interpreted by Name
REHAADC   DIM       6      6        351   Attending Doctor
REHALAB   DIM       3      3        357   Laboratory Code
REHALOC   DIM       25     25       360   Location
REHARDE   DIM       3      3        385   Reason for Delete (Cat Rd)
REHASPA   DIM       30     30       388   Spare
.
.End of Record                      418
.
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       100
DEFPATH   DIM       60
DIM4      DIM       4
DIM8      DIM       8
DIM28     DIM       28
DIM60     DIM       60
DIM80     DIM       80
FYEAR     FORM      4
DYEAR     DIM       4
NEWFILE   DIM       60
NEWPATH   DIM       60
OLDPATH   DIM       60
RECNUM    FORM      8
SAVEFLG   FORM      1
.
. 
. PROGRAM CONSTANTS
. -----------------
SP60      INIT    "                                                            "
Y2051     FORM    4
.
.
PRGNAM    INIT      "CONVERSION RESHEAFD"
PRGID     INIT      "F04RSHEA"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                         MAIN0000                                          *
.*                      Mainline code                                        *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN1000,MAIN0000,MAIN9999 * Yes, no, cancel
.
MAIN1000  CALL      GDAT0000                      * get start date/time
.
MAIN1100  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN2000
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/compress saved file
.
MAIN2000  CALL      NYER0000                      * get next year
          BRANCH    EXIT,MAIN1100
.
MAIN9999  CHAIN     PGM
+
.*****************************************************************************
.*                          INIT0000               Called by: MAIN0000       *
.*  Display heading and check that the files needed exist & open them        *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      "2051",Y2051
          MOVE      "1950",FYEAR
          MOVE      FYEAR,DYEAR
.
INIT9999  RETURN
+
.*****************************************************************************
.*                          NYER0000               Called by: MAIN0000       *
.*                    Increment Year until finished                          *
.* Requires: FYEAR: year being processed                                     *
.*           EXIT:  0 = Completed all years                                  *
.*                  1 = Process next year                                    *
.*****************************************************************************
.
NYER0000  ADD       ONE,FYEAR                    * increment year
          MOVE      FYEAR,DYEAR
.
          COMPARE   Y2051,FYEAR                  * final year processed ?
          GOTO      NYER9100 IF LESS             * no
.
.         Finished all years
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended      : ",*V2LON,CDATE,SP1,CTIMEIS:
                    *P1:24,"Fixit completed.  ";
          CALL      HITENTER
.
          MOVE      ZERO,EXIT
          GOTO      NYER9999
.
NYER9100  MOVE      ONE,EXIT
.
NYER9999  RETURN
+
.*****************************************************************************
.*                          GDAT0000               Called by: MAIN0000       *
.*                      Get start date/time                                  *
.*****************************************************************************
.
GDAT0000  DISPLAY   *P1:24,*EL;
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started    : ",*V2LON,CDATE,SP1,CTIMEIS
.
GDAT9999  RETURN
+
.*****************************************************************************
.*                          KDIR0000               Called by: MAIN0000       *
.*                       Keyin directories                                   *
.*****************************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9000
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9100 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9100 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9000
.
KDIR9000  MOVE      ZERO,EXIT
          GOTO      KDIR9999
.
KDIR9100  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.*****************************************************************************
.*                          PREP0000               Called by: MAIN0000       *
.*                       Preparing files                                     *
.*****************************************************************************
.
PREP0000  CALL      CHEK0000             * old file found ok ?
          BRANCH    EXIT,PREP9000        * no - finish
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat ",CMDLINE
          APPEND    "reha",CMDLINE
          APPEND    DYEAR,CMDLINE
          APPEND    ".dat` ",CMDLINE 
          APPEND    OLDPATH,CMDLINE
          APPEND    "srha",CMDLINE
          APPEND    DYEAR,CMDLINE
          APPEND    ".dat",CMDLINE 
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat ",CMDLINE
          APPEND    "reha",CMDLINE
          APPEND    DYEAR,CMDLINE
          APPEND    ".idx` ",CMDLINE 
          APPEND    OLDPATH,CMDLINE
          APPEND    "srha",CMDLINE
          APPEND    DYEAR,CMDLINE
          APPEND    ".idx",CMDLINE 
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "reha",CMDLINE 
          APPEND    DYEAR,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
.           CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "srha",DIM60                                                     
          APPEND    DYEAR,DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
.           CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "reha",NEWFILE 
          APPEND    DYEAR,NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.
          APPEND    " 418 UC1-8,9-13,14-17",CMDLINE 
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.         Create index 2
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC112-117,118-137,1-8,9-13,14-17",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.         Create index 3
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC19-34,150-161,138-149,1-8,9-13,14-17",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      RESHEAA1,NEWFILE 
.
.         Set the flag to say we want to continue
.
          DISPLAY   *P1:20,"File       : ",*V2LON,NEWFILE
          MOVE      ZERO,EXIT
          GOTO      PREP9999
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
.         CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.*****************************************************************************
.*                               READ0000          Called by: MAIN0000       *
.*             Loop thru old file and write each record                      *
.*****************************************************************************
.
READ0000  DISPLAY   *P1:21,*EL,"Record No. : ";
          PACK      KEY8,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
.
          MOVE      SP3,REHARDE
          CALL      WRREHA1 
.
          IF        (RECNUM%1000) = 0 | RECNUM = 1
            DISPLAY   *P15:21,*V2LON,RECNUM
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     RESHEAA1                      * close new file 
          CLOSE     OLDFILE1                      * close old file
.
READ9999  RETURN
+
.*****************************************************************************
.*                               VALD0000          Called by: KDIR0000       *
.*                   Check if it a valid directory                           *
.*****************************************************************************
.
VALD0000  SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9100
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9100
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      VALD9999
.
VALD9100  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.*****************************************************************************
.*                               ENDP0000          Called by: MAIN0000       *
.*                    Remove or compress save file                           *
.*****************************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "srha",DIM60
          APPEND    DYEAR,DIM60
          APPEND    "*",DIM60 
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "gzip -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not compressed or removed.  ";
.           CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.         DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
.         CALL      HITENTER
.
ENDP9999  RETURN
+
.*****************************************************************************
.*                               DEFT0000          Called by: KDIR0000       *
.*                      Get the default directory                            *
.*****************************************************************************
.
DEFT0000  CLEAR     DIM28
          APPEND    "ldat ",DIM28
          APPEND    "reha",DIM28
          APPEND    DYEAR,DIM28
          APPEND    ".dat > temp.txt",DIM28
          RESET     DIM28
.
          EXECUTE   DIM28,TASKID 
.
.         Loop through web and tmp directory and find first RESHEAaf
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  CLOSE     FINDFILE
.
DEFT9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          MOVE      "reha",DIM4
          PACK      DIM80,OLDPATH,DIM4,DYEAR
          OPEN      OLDFILE1,DIM80                                                     
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9000
.
CHEK2000  MOVE      ONE,EXIT
          GOTO      CHEK9999
.
CHEK9000  MOVE      ZERO,EXIT
.
CHEK9999  RETURN
.
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
READSOLD  RESET     KEY17
          READ      OLDFILE1,KEY17;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;REHARDT,REHARTM,REHARUN,REHAFUR,REHAPID:
                        REHAVIS,REHASID,REHAMDT,REHAMTM,REHAMID,REHAPAP:
                        REHAPOR,REHAFAP,REHAFOR,REHAUSC,REHAUCS,REHANOR:
                        REHACDT,REHACTM,REHACID,REHAFCI,REHASDT,REHASTM:
                        REHAOID,REHAOSN,REHAOGN,REHARRD,REHARRT,REHADSS:
                        REHARED,REHARET,REHARST,REHAIBY,REHAADC,REHALAB:
                        REHALOC,OLDSPARE
          GOTO      OVERCOND IF OVER
          PACK      REHASPA,OLDSPARE,SP70,SP70 
          RETURN
.
WRREHA1   RESET     KEY17
          WRITE     RESHEAA1,KEY17;REHARDT,REHARTM,REHARUN,REHAFUR,REHAPID:
                        REHAVIS,REHASID,REHAMDT,REHAMTM,REHAMID,REHAPAP:
                        REHAPOR,REHAFAP,REHAFOR,REHAUSC,REHAUCS,REHANOR:
                        REHACDT,REHACTM,REHACID,REHAFCI,REHASDT,REHASTM:
                        REHAOID,REHAOSN,REHAOGN,REHARRD,REHARRT,REHADSS:
                        REHARED,REHARET,REHARST,REHAIBY,REHAADC,REHALAB:
                        REHALOC,REHARDE,REHASPA
          RETURN
+
...............................................................................
.
          INC       STD001IO
