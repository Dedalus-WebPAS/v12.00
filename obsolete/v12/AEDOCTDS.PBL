.******************************************************************************
.* System         : Accident and Emergency                                    *
.*                                                                            *
.* Include Name   : AEDOCTDS.PBL                                              *
.*                                                                            *
.* Function       : To only display A&E doctor codes from PATDO1FD            *
.*                  NOTE: the codes file (patcodes) must be open              *
.*                                                                            *
.* Author         : Justin Coates       09/09/1993                            *
.*                                                                            *
.* Modifications  : 21/09/94  Rob Leonard                                     *
.*                  Check TCINDC5 instead of THCSCOD for AE doc type          *
.******************************************************************************
AEDOCTDS  DISPLAY   *P1:3,*EF:
                    *P1:4,"Doctor Surname : ____________________"
.
          KEYIN     *P18:4,*V2LON,DSNAMXX:
                    *P18:4,*DV,DSNAMXX
.
AEDDS100  DISPLAY   *P1:6,*EF:
                    *ULON,"Code  ",*P9:6,"Name                        ":
                    *P41:6,"Code  ",*P49:6,"Name                        "
.
          MOVE      SEVEN,CVERT
          MOVE      ONE,CCOL
          PACK      KEY26,DSNAMXX,SP30
          CALL      RDSDOCT2
.
.         Loop over file
.
AEDDS200  CALL      RDKDOCT2
          BRANCH    OVRCD,AEDDS999
.
.         check if an A&E doctor
.
          PACK      KEY5,ANSD,ANST,DRTYPE
          CALL      RDCODE1
          BRANCH    OVRCD,AEDDS200          * invalid doctor type
.
          MATCH     "A",TCINDC5
          GOTO      AEDDS200 IF NOT EQUAL   * not an A&E doctor
.
          RESET     DSNAMXX
          GOTO      AEDDS250 IF EOS
.
          MATCH     DSNAMXX,SP20
          GOTO      AEDDS250 IF EQUAL
.
          MATCH     DSNAME,DSNAMXX
          GOTO      AEDDS999 IF NOT EQUAL
.
AEDDS250  BRANCH    DRSTAT,AEDDS200
.
          MOVE      DGNAME,ANS
          PACK      DIM3 WITH SP1,ANS,DOT
.
          CMATCH    SP1,ANS
          GOTO      AEDDS300 IF NOT EQUAL
.
          MOVE      SP3,DIM3
.
AEDDS300  DISPLAY    *PCCOL:CVERT,*V2LON,DCODE,SP1,*HOFF,DIM3,DSNAME
          ADD        ONE,CVERT
          COMPARE    TWENTY4,CVERT
          GOTO       AEDDS200 IF LESS
.
          MOVE       SEVEN,CVERT
          ADD        "40",CCOL
.
          COMPARE    "70",CCOL
          GOTO       AEDDS200 IF LESS
.
AEDDS400  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
          REP       UPPLOW,ANS
          CMATCH    ANSC,ANS
          GOTO      AEDDS999 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      AEDDS100 IF EQUAL
.
.         Clear the screen
.
AEDDS450  MOVE      SEVEN,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:CVERT,*EF
          GOTO      AEDDS200
.
AEDDS999  RETURN
