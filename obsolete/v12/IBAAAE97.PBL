. *************************************************************************
. *                       P R O G R A M  S U M M A R Y                    *
. *                       -------------  -------------                    *
. *                                                                       *
. *     PROGRAM NAME:     IBAAAE97                                        *
. *                                                                       *
. *     FUNCTION:         EMERGENCY FLAG CLEAR                            *
. *                                                                       *
. *     DATE WRITTEN:     01/06/88                                        *
. *                                                                       *
. *     AUTHOR :          MALCOLM HAYSE   (I.B.A)  N.S.                   *
. *                                                                       *
. *     MODIFICATIONS:    -                                               *
. *                       V10.01.01 10/02/2011  Jill Parkinson CAR 233088 *
. *                                 Added pmsvx1af                        *
. *                       V9.03.01  20/11/2003  Steve Armstrong CAR 44631 *
. *                                 Removed PI command                    *
. *************************************************************************
. *                       V5.08.01  16/08/2000  Caleb Sharman             *
. *                             Changed Health Fund variables to be 8 chars *
. *                       V5.01 10//03/89 J.Tan  (I.B.A.)    SRF   4868   *
. *                             Fixed AAEIODEA.                           *
. *                       V5.02 21/04/89 K.Peak  (I.B.A.)    SRF 100081   *
. *                             Added CONTROLF.                           *
. *                       V5.03 04/05/89 K.Peak  (I.B.A.)                 *
. *                             Added HNUMDES for N.Z                     *
. *                       V5.04 21/05/89 K.Peak  (I.B.A.)    SRF 100824   *
. *                             Added charge audit for N.Z                *
. *                             Fixed Audit Busy                          *
. *                    V5.01.02 24/08/89 K.Peak  (I.B.A.)                 *
. *                             Added U/R No clear option                 *
. *                    V5.01.03 07/05/90 J.Tan               SRF 104857   *
. *                             Add to reset Continous log printer        *
. *                    V5.01.04 06/06/90 D.Di.Paola          SRF 103773   *
. *                             Recompiled for changes to AAECONFD.....   *
. *                    V5.01.05 11/07/90 i. chung            SRF 105608   *
. *                             Fixed I*C for surname search and added    *
. *                             reading of CCNOTES from CONTROLF file     *
. *                    V5.01.06 24/07/90 Paul Duncan                      *
. *                             implemented second sur file               *
. *          V5.01.121  27/08/93  ROD                                     *
. *                     Recompile for NZHIS                               *
. *        V5.01.122 24/05/1995  Matt Surridge                            *
. *                  Fixed bugs for global recompile.                     *
. *************************************************************************
.
          INC       STD001FD
          INC       NZHISIFD/INC                 * Include for NZHIS vars
          INC       NHIMASFD/INC                 * Include for NZHIS vars
.
          INC       PATCOMM/INC
          INC       PATCALAG/INC
          INC       PATCONFD/INC
          INC       PATDHEAD/INC
          INC       PATMA1FD/INC

          INC       PATMI1FD/INC
          INC       PATPR1FD/INC
          INC       PATGSRFD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
.
          INC       OUTPREFD/INC      
          INC       BOKRC1FD/INC      
.
          INC       AAECONFD/INC      * A&E control file
          INC       AAEDE1FD/INC
          INC       AAEDEBFD/INC
          INC       AAEAUXFD/INC      * A&E audit file
.
.         Extra variables
.
.
BJDAY     FORM      3
CJDAY     FORM      3
COL       FORM      2
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
SCNDGNAM  DIM       40
VERT      FORM      2
VAR       FORM      1
NMPNUMB   DIM       20
DESC1     DIM       13
.
INPFILE   FILE      ASCII, FIXED=256
.
ADMN      FORM      8          * admission number keyed in
.
PRGID     INIT      "IBAAAE97"
PRGNAM    INIT      "Emergency Flag Clear"
VERSION   INIT      "V12.00.00"
+
.         Start of program
.
          CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening aaede1af";
          OPEN      AAEDE1A1,"aaede1af"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"            * required by surname search
.
          DISPLAY   *P64:24,"pmsvx1af";
          OPEN      PMSVX1A1,"pmsvx1af"            * required by surname search
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN3,"patgsrnf"
          DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN4,"patgsrnf"
          DISPLAY   *P64:24,"patgsrnf"
          OPEN      PATGSRN2,"patgsrnf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,ZERO;*2,CDD,CMM,CYY,CCC
          READ      CONTROLF,TEN;*138,CNOPORT
          READ      CONTROLF,TEN3;*186,CMBSOPR,*245,CHOSTYP
          READ      CONTROLF,TEN9;*75,CCNOTES
          READ      CONTROLF,THIRTY;*168,HNUMDES,*184,HLNOLOG
.
          RESET     HNUMDES,9
NEXT70    CMATCH    SP1,HNUMDES
          GOTO      NEXT71 IF NOT EQUAL
          BUMP      HNUMDES BY -1
          GOTO      NEXT71 IF EOS
          GOTO      NEXT70
.
NEXT71    MOVEFPTR  HNUMDES,VAR
          RESET     HNUMDES,VAR
          LENSET    HNUMDES
          RESET     HNUMDES
.
.
START     HLINE     *G37,2,54,80
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = ",*+,HNUMDES,*-," Files":
                    *P2:6,*V2LON,TWO,*HOFF," = U/R Number":
                    *P2:7,*V2LON,THREE,*HOFF," = Audit Files":
                    *P2:8,*V2LON,FOUR,*HOFF," = Resetting Continuous Log"
.
REKSTRT0  KEYIN     *P1:10,*EL,"Option : _",*P10:10,*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          DISPLAY   *P10:10,*V2LON,OPTION
          BRANCH    OPTION OF START1,START2,START3,START4
.
          BEEP
          GOTO      REKSTRT0
.
.............................................................................
.         Normal A & E files
.............................................................................
.
START1    DISPLAY   *P55:2,*V2LON," ",*+,HNUMDES,*-," Detail File ":
                    *P1:5,*EF,*P2:5,*V2LON,ONE,*HOFF," = Detail"
.
.
REKST1    KEYIN     *P1:7,*EL,"Option : _",*P10:7,*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      START IF EQUAL
.
          DISPLAY   *P10:7,*V2LON,OPTION
          BRANCH    OPTION OF KADMN
.
          BEEP
          GOTO      REKST1
.
.
.       Detail file
..............................................................................
.
.       Input ADMISSION code
.
KADMN     KEYIN     *P1:10,*EF," Enter ",*DV,HNUMDES," :":
                    *P21:10,*DV,UNDLN8,*P21:10,*V2LON,ADMN
.
          COMPARE   ZERO,ADMN
          GOTO      START IF EQUAL
.
          DISPLAY   *P21:10,*V2LON,ADMN
          MOVE      ADMN,ADANUMB
          MOVE      ADANUMB,KEY8
          CALL      RDDETA1
          BRANCH    OVRCD OF INVADMN
.
          MOVE      ADAURNO,KEY8
          CALL      RDMAST1
          DISPLAY   *P38:10,*V2LON,PSNAME,*W
.
          MATCH     SP2,ADALOCK
          GOTO      NOTBUSY IF EQUAL
.
          MOVE      ADANUMB,KEY8
          CALL      UUAEDEA1
          DISPLAY   *P38:10,*EL,*V2LON,"*** CLEARED ***",*W;
          GOTO      KADMN
.
INVADMN   DISPLAY   *P38:10,*B,*V2LON,"** Invalid ",*+,HNUMDES,*-," **",*W2;
          GOTO      KADMN
.
NOTBUSY   DISPLAY   *P38:10,*B,*V2LON:
                    "* ",*+,HNUMDES,*-," is not Busy, NOT Cleared *",*W2;
          GOTO      KADMN
.
..............................................................................
.         Input U/R Number
..............................................................................
.
START2    DISPLAY   *P60:2,*V2LON," U/R Number "
.
KURNO     DISPLAY   *P1:12,*EL,"U/R Number : ________";
.
          MOVE      TEN2,CVERT
          MOVE      TEN4,CCOL
          MOVE      TWO,SRCHOPT
          MOVE      ZERO,SRCHSYS
.
          CALL      KURN
.
          BRANCH    EXIT,START
          BRANCH    OVRCD,INVURNO
.
          MATCH     SP2,PPORT
          GOTO      NOTBUSM IF EQUAL
.
          MOVE      PURNO,KEY8
          CALL      UUPTMAS1
          DISPLAY   *P35:12,*V2LON,"*** CLEARED ***",*W;
          GOTO      KURNO
.
INVURNO   DISPLAY   *P35:12,*B,*V2LON:
                    "*** U/R Number does not Exist ***",*W;
          GOTO      KURNO
.
NOTBUSM   DISPLAY   *P35:12,*B,*V2LON:
                    "*** U/R Number is not Busy, NOT Cleared ***",*W2;
          GOTO       KURNO
.
REDISPS   DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = ",*+,HNUMDES,*-," Files":
                    *P2:6,*V2LON,TWO,*HOFF," = U/R Number":
                    *P2:7,*V2LON,THREE,*HOFF," = Audit Files":
                    *P2:8,*V2LON,FOUR,*HOFF," = Resetting Continuous Log":
                    *P1:10,*EL,"Option : ",*V2LON,OPTION,*HOFF:
                    *P1:12,*EL,"U/R Number : ",UNDLN8;
          RETURN
.
GETSVAR   RETURN
..............................................................................
.         Audit files
..............................................................................
.
START3    DISPLAY   *P60:2,*V2LON," Audit Files ",*P1:5,*EF:
                    *P2:5,*V2LON,ONE,*HOFF," = Detail Audit File":
                    *P2:6,*V2LON,TWO,*HOFF," = Waiting Audit File":
                    *P2:7,*V2LON,THREE,*HOFF," = Charge Audit File"
.
REKSTRT3  KEYIN     *P1:9,*EF,"Option : _",*P10:9,*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      START IF EQUAL
.
          DISPLAY   *P10:9,*V2LON,OPTION
          BRANCH    OPTION OF FILETYPA,FILETYPW,FILETYPC
.
          BEEP
          GOTO      REKSTRT3
.
FILETYPA  MOVE      ANSA,AUTYPE
          REP       "Aa",AUTYPE
          GOTO      KPORT
.
FILETYPC  MOVE      ANSC,AUTYPE
          REP       "Cc",AUTYPE
          GOTO      KPORT
.
FILETYPW  MOVE      ANSW,AUTYPE
          REP       "Ww",AUTYPE
.
.
KPORT     MOVE      SP2,PPORT
          KEYIN     *P1:12,*EF,"Terminal Port ## : __",*P19:12,*V2LON,PPORT
          MATCH     SP2,PPORT
          GOTO      REKSTRT3 IF EOS
.
          DISPLAY   *P58:24,*EL,"Opening :";
.
          MOVE      PPORT,AUPORT
          PACK      AUFILE WITH AUAAEAU,AUTYPE,AUPORT
          REP       " 0",AUFILE
.
          DISPLAY   *P68:24,*V2LON,AUFILE;
          TRAP      NOAUD IF IO
          OPEN      INPFILE,AUFILE
          TRAPCLR   IO
.
          MOVE      "AUX",PRXCODE                * set file to lock - 
          CALL      GETSLK00                     * lock file
          READ      INPFILE,ZERO;SECTOR,AUFLAG
          COMPARE   ZERO,AUFLAG
          GOTO      NOTBUSD IF EQUAL
.
          DISPLAY   *P35:12,*V2LON,"*** CLEARED ***",*W;
          MOVE      ZERO,AUFLAG
          WRITAB    INPFILE,ZERO;*7,AUFLAG
          CLOSE     INPFILE
          CALL      RELSLK00                     * release lock
          GOTO      KPORT
.
NOAUD     DISPLAY   *P35:12,*B,*V2LON:
                    "*** Audit File Does Not Exist ***",*W2;
          NORETURN
          GOTO      KPORT
.
NOPORT    DISPLAY   *P35:12,*B,*V2LON:
                    "*** Invalid Port Number ***",*W2;
          GOTO      KPORT
.
NOTBUSD   DISPLAY   *P35:12,*B,*V2LON:
                    "*** Audit File is NOT Busy, NOT Cleared ***",*W2;
          CLOSE     INPFILE
          CALL      RELSLK00                     * release lock
          GOTO      KPORT
.
..............................................................................
.         Resetting the continous log printer
..............................................................................
.
START4    DISPLAY   *P55:2,*V2LON," Resetting Continuous Log ",*HOFF:
                    *P1:12,"The Current Line number of Continuous Log is ":
                          *V2LON,HLNOLOG,*HOFF:
                    *P1:24,"Are you sure you want to Reset the line number (":
                          *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? "
.
REKSTRT4  KEYIN     *P56:24,*EL,*DV,UNDLN1,*P56:24,*V2LON,ANS;
          RESET     ANS
          GOTO      ERRSTRT4 IF EOS
.
          REP       UPPLOW,ANS
          DISPLAY   *P56:24,*V2LON,ANS
.
          MATCH     "N",ANS
          GOTO      START IF EQUAL
.
          MATCH     "Y",ANS
          GOTO      DORESET IF EQUAL
ERRSTRT4  BEEP
          GOTO      REKSTRT4
.
DORESET   MOVE      "60",HLNOLOG
          WRITAB    CONTROLF,THIRTY;*184,HLNOLOG
.
          DISPLAY   *P1:24,*EL:
                    *V2LON,"*** Posted Succesfully *** ";
          CALL      HITENTER
          GOTO      START
.
+
.
          INCLUDE   PATCOMN1
          INC       PATMA1IO/INC      * Patient master IO
          INC       PATMI1IO/INC      * Patient admission IO
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATPR1IO/INC      
          INC       PATGSRIO/INC      * Patient surname search IO
.
          INCLUDE   OUTPREIO/INC      
          INC       BOKRC1IO/INC      
.
          INC       AAEDE1IO/INC      * A&E detail IO
          INC       AAEAUXIO/INC      * A&E audit IO
.
          INCLUDE   STD001IO
          INCLUDE   PATSNDX
          INCLUDE   PATSNX2
.
ENDIT     CHAIN     PGM
          STOP
.
          INC       NZCOMPIO/INC
          INC       CALCAGE
AGECHK
CLPATMAS  RETURN
