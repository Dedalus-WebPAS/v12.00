. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS39                                             *
. * Desc      :   Input Unpresented Cheques/Receipts                   *
. **********************************************************************
. * Date      :   24.07.1991                                           *
. * Author    :   Neeriem Dye (IBA)                                    *
. * Mods      :                                                        *
. **********************************************************************
. *V10.03.01  27/02/2013  Ania P              CAR 281021               *
. *           RECOMPILED for FMSBNKKY                                  *
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
. * VF.00.04  28.04.2000 Sandra Barcham                                *
. *           Recompiled for FMSBNKKY                                  *
. * VF.00.03  12.01.2000 Sandra Barcham                                *
. *           Recompiled for FMSBNKKY                                  *
. * VF.00.02  05.01.2000 Charles Handaya                               *
. *           Recompiled for APSMASIO                                  *
. * VF.00.01  02.09.1999 Sandra Barcham                                *
. *           Add in Creditor Keyword Search                           *
. *           srf 631485                                               *
. **********************************************************************
.$$$$$$
.$$$$$$
. Standard FMS Accounting System Include
.---------------------------------------
          INC       FMSSTDDF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
.
          INC       APSMASFD/INC
          INC       APSMXRFD/INC
          INC       FMSBNKFD/INC       * bank rec
          INC       FMSCHQFD/INC       * cheque
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
.
ACCEPT    FORM      1        * accept mode (1=on)
CORREF    DIM       15
CREDITOR  DIM       5        * creditor
DUPLICATE FORM      1
PAYTO     DIM       35
SAMOUNT   FORM      12.2     * amount
STRING    DIM       40       * parameter for routine RMBL
TRANDATE  DIM       8        * transaction date
Z20       INIT      "ZZZZZZZZZZZZZZZZZZZZ"
REPUNIQ   INIT      "yzxywxvwuvtustrsqrpqopnomnlmkljkijhighfgefdecdbcabZa":
                    "YZXYWXVWUVTUSTRSQRPQOPNOMNLMKLJKIJHIGHFGEFDECDBCAB9A":
                    "8978675645342312 1"
.
PRGID     INIT      "IBAFMS39"
PRGNAM    INIT      "Input Unpresented Cheques/Receipts"
VERSION   INIT      "V12.00.00"
.
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML0010    CALL      KOPT0000           * get type to enter
          BRANCH    EXIT,ML9999        * exit program
.
ML0050    CALL      PROC0000           * get key
          BRANCH    EXIT,ML0010        * exit option
.
ML0100    CALL      PCHQ0000           * get key
          BRANCH    EXIT,ML0050        * exit option
.
          CALL      SELA0000           * perform screen A
          BRANCH    EXIT,ML0100        * exit option
.
          CALL      POST0000           * save data
          GOTO      ML0100
.
ML9999    CHAIN     PGM                * chain back to menu
.
.******************************************************************************
.* INIT - Open Files                             Called by ML0000             *
.******************************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CCENTRY
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
.
          DISPLAY   *P56:24,"Opening apsmasaf";
          OPEN      APSMASA1,"apsmasaf"
.
          DISPLAY   *P64:24,"Opening apsmxraf";
          OPEN      APSMXRA1,"apsmxraf"
.
          DISPLAY   *P64:24,"Opening fmsbnkaf";  * bank rec
          OPEN      FMSBNKA1,"fmsbnkaf"
          OPEN      FMSBNKA2,"fmsbnkaf"
          OPEN      FMSBNKA3,"fmsbnkaf"
.
          DISPLAY   *P64:24,"Opening fmschqaf";  * cheque
          OPEN      FMSCHQA1,"fmschqaf"
.
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.******************************************************************************
.  KOPT - Get desired function                    Called by ML                 
.        Returns  : CCHEQREC  (desired option 0=cheq, 1=rec)
.                   EXIT      (1=quit)
.******************************************************************************
KOPT0000  DISPLAY   *P1:3,*EF:             * display screen
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Enter Cheques":
                    *P1:6,*V2LON,"2",*HOFF," = Enter Receipts";
.
KOPT1000  KEYIN     *P1:8,"Select Option : _",*EL:
                    *P17:8,*V2LON,COPTION
          COMPARE   ZERO,COPTION
          GOTO      KOPT9500 IF EQUAL       * exit ?
          BRANCH    COPTION,KOPT8100,KOPT8200
.
          BEEP                              * illegal option, get another
          GOTO      KOPT1000
.
KOPT8100  DISPLAY   *P70:3,*V2LON,"Cheques";
          GOTO      KOPT9000
.
KOPT8200  DISPLAY   *P70:3,*V2LON,"Receipts";
          GOTO      KOPT9000
.
KOPT9000  MOVE      ZERO,EXIT
          LOAD      CCHEQREC,COPTION,ZERO,ONE
          LOAD      CFMBND,COPTION,CFMBND0,CFMBND1
          GOTO      KOPT9999
.
KOPT9500  MOVE      ONE,EXIT
.
KOPT9999  RETURN
.**********************************************************************
.  PROC - Keyin cheque account and number              Called By ML
.        Returns  - FMCHCHQ  (bank account)
.**********************************************************************
PROC0000  CALL      RCHQFM00           * display screen
          MOVE      "28",CCOL
          MOVE      "4",CVERT
          CALL      KCHQFM00                * get cheque account
          BRANCH    EXIT,PROC9500,PROC9500  * exit program ?
          DISPLAY   *P28:4,*V2LON,FMCHCHQ,*HOFF,SP2,FMCHDES;
.
PROC9000  MOVE      ZERO,EXIT          * continue
          GOTO      PROC9999
.
PROC9500  MOVE      ONE,EXIT           * quit
.
PROC9999  RETURN
.**********************************************************************
.  PCHQ - Keyin cheque account and number              Called By ML
.        Returns  - FMCHCHQ  (bank account)
.                   CRECNO   (cheque number)
.**********************************************************************
PCHQ0000  CALL      RBNKFM00           * display screen
          MOVE      "28",CCOL
          MOVE      "5",CVERT
          IF        COPTION=1
            CALL      XBNKFM00                * get cheque/receipt no
          ELSE
            CALL      XRECFM00
          ENDIF
          BRANCH    EXIT,PCHQ9500,PCHQ9500  * exit program ?
          DISPLAY   *P28:5,*V2LON,CRECNO;
          MOVE      ZERO,DUPLICATE
          PACK      KEY36,FMCHCHQ,CCHEQREC,CRECNO,SP70
          CALL      RSFMBN2                 * check if cheque already on file
.
PCHQ1000  CALL      RKFMBN2                 * get next record
          BRANCH    OVRCD,PCHQ9000          * no more records ?
          PACK      KEY31,FMBNBNK,FMBNTYP,FMBNREF,SP70
          MATCH     KEY36,KEY31
          GOTO      PCHQ9000 IF NOT EQUAL   * out of range ?
.
          UNPACK    FMBNDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P1:24,*V2LON,"WARNING",*HOFF,*B,*+:
                    " - ",CFMBND," Number Already on File for ",CPCDATE:
                    ". Continue (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ";
          MOVE      "N",DEFYESNO       * set default to no
          MOVE      "75",CCOL
          MOVE      "24",CVERT
          CALL      YORN0000
          DISPLAY   *P1:20,*EF;
          MOVE      ONE,DUPLICATE
          BRANCH    FYESNO,PCHQ9000
          GOTO      PCHQ0000
.
PCHQ9000  MOVE      ZERO,EXIT          * continue
          GOTO      PCHQ9999
.
PCHQ9500  MOVE      ONE,EXIT           * quit
.
PCHQ9999  RETURN
.******************************************************************************
.* SCRA - Display Screen A                       Called by SELA, redisps      *
.******************************************************************************
SCRA0000  DISPLAY   *P1:6,*EF,*V2LON:
                    *P1:7," 1",*HOFF,". Transaction Date     : ",*V2LON:
                    *P1:8," 2",*HOFF,". Amount               : ";
          COMPARE   "2",COPTION
          GOTO      SCRA1000 IF EQUAL
          DISPLAY   *P1:9,*V2LON," 3",*HOFF,". Creditor             : ";
.
SCRA1000  BRANCH    ACCEPT,SCRA9999         * accept mode ?
.
          UNPACK    TRANDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      SP70,APMACN1
          PACK      KEY5,CREDITOR,SP70
          CALL      RDAPMA1
          DISPLAY   *P28:7,*V2LON,CPCDATE:
                    *P28:8,*V2LON,SAMOUNT;
.
          COMPARE   "2",COPTION
          GOTO      SCRA9999 IF EQUAL
          DISPLAY   *P28:9,*V2LON,CREDITOR,*HOFF,SP2,APMACN1;
.
SCRA9999  RETURN
.******************************************************************************
.* SELA - Perform Screen A                       Called by ML                 *
.*        Returns : EXIT     (1=exit program)                                 *
.******************************************************************************
SELA0000  UNPACK    SP70,CPCDATE,CREDITOR
          MOVE      ZERO,SAMOUNT
          MOVE      ONE,ACCEPT         * in accept mode
          MOVE      ZERO,CCITEM        * reset item desired
          CALL      SCRA0000           * display screen A
.
SELA0100  ADD       ONE,CCITEM         * get next item
          COMPARE   ONE,ACCEPT
          CALL      MAINQST  IF NOT EQUAL   * get option if not in accept mode
.
          MOVE      "28",CCOL          * get screen address
          MOVE      "6",CVERT
          ADD       CCITEM,CVERT
          MOVE      CCITEM,F2          * adjust selection for quit/continue
          ADD       TWO,F2
          BRANCH    F2,SELA9500,SELA9000:              * quit, continue
                       SELA1100,SELA1200,SELA1300      * execute option
.
SELA0200  BRANCH    ACCEPT,SELA0500    * at end of accept mode ?
          BEEP                         * illegal option
          GOTO      SELA0100           * get another option
.
SELA0500  MOVE      ZERO,ACCEPT        * clear accept mode
          GOTO      SELA0100
.
.---- option 1 ----
.
SELA1100  PACK      TRANDATE,CCC,CYY,CMM,CDD      * get transaction date
          REP       " 0",TRANDATE
          UNPACK    TRANDATE,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE
          BRANCH    OVRCD,SELA1100
          PACK      TRANDATE,CCENT,CYEAR,CMON,CDAY
          GOTO      SELA0100
.
.---- option 2 ----
.
SELA1200  KEYIN     *PCCOL:CVERT,"____________.__",*EL:
                    *PCCOL:CVERT,*V2LON,SAMOUNT:
                    *PCCOL:CVERT,*DV,SAMOUNT
          COMPARE   SAMOUNT,ZERO
          GOTO      SELA0100 IF LESS
          BEEP
          GOTO      SELA1200
.
.---- option 3 ----
.
SELA1300  COMPARE   "2",COPTION
          GOTO      SELA0200 IF EQUAL
.
          PACK      PAYTO WITH SP30,SP30
          MOVE      ZERO,CKEYTYP
          CALL      KAPMAA00
          BRANCH    EXIT,SELA1400,SELA9500  * entry ok ? (1=blank, 2=EXITCHAR)
          PACK      CREDITOR,APMACRD
.
          DISPLAY   *P1:10,*EL,*PCCOL:CVERT,*V2LON,CREDITOR,*HOFF,SP2,APMACN1;
          GOTO      SELA0100
.
SELA1400  MOVE      SP5,CREDITOR
          PACK      PAYTO WITH SP30,SP30
          KEYIN     *P28:9,*EL,*P5:10,"Paid to",*P26:10,":":
                    *P28:10,*EL,*V2LON,PAYTO
          GOTO      SELA0100
.
SELA9000  MOVE      ZERO,EXIT          * continue
          GOTO      SELA9999
.
SELA9500  MOVE      ONE,EXIT           * quit
.
SELA9999  RETURN
.**********************************************************************
.  POST - Post data                                    Called By ML
.        Requires - DIM35    (reason)
.                   TRANDATE (transaction date)
.                   FMCHCHQ  (bank account)
.                   CRECNO   (cheque number)
.**********************************************************************
POST0000  BRANCH    DUPLICATE,POST0100
          CALL      GNID0000           * get next id
          UNPACK    KEY20,FMBNBNK,FMBNUNQ
.
POST0100  MOVE      CRECNO,FMBNREF        * set up vars
          MOVE      TRANDATE,FMBNDAT
          PACK      FMBNPDT,CCC,CYY,CMM,CDD,SP70
          REP       " 0",FMBNPDT
          MOVE      CCHEQREC,FMBNTYP
          MOVE      SAMOUNT,FMBNAMT
          MOVE      "0",FMBNPRE
          UNPACK    SP70,FMBNLED,FMBNACC,FMBNFYR,FMBNBCH,FMBNLNO
.
          MOVE      SP70,APMACN1
          PACK      FMBNCRE,CREDITOR,SP70
          PACK      KEY5,CREDITOR,SP70
          CALL      RDAPMA1
          BRANCH    OVRCD TO POST0300
.
          PACK      FMBNNAM,APMACN1,SP70
          GOTO      POST0400
.
POST0300  PACK      FMBNNAM,PAYTO,SP70
.
POST0400  BRANCH    DUPLICATE,POST0500
.
          CALL      WRFMBN1            * write record
          GOTO      POST9999
.
POST0500  PACK      KEY20,FMBNBNK,FMBNUNQ
          CALL      RAFMBN1
          BRANCH    OVRCD,POST9999
.
          CALL      UPFMBN1
.
POST9999  RETURN
.**********************************************************************
. Get next bank rec unique id
.**********************************************************************
GNID0000
          MOVE      "~~~~~",KEY5
          PACK      KEY20,FMCHCHQ,KEY5
          CALL      RSFMBN1
          CALL      RPFMBN1
          BRANCH    OVRCD,GNID1150
          MATCH     FMBNBNK,FMCHCHQ
          GOTO      GNID1150 IF NOT EQUAL
.
          UNPACK    FMBNUNQ,ANS,KEY4
          MOVE      ZERO,F4
          MOVE      KEY4,F4
.
GNID1100  
          COMPARE   "9999",F4          * at end of group ?
          GOTO      GNID1110 IF NOT EQUAL
.
          MATCH     "z",ANS
          GOTO      BADERROR IF EQUAL
.
          REP       REPUNIQ,ANS
          MOVE      ZERO,F4
.
GNID1110
          ADD       ONE,F4
          PACK      KEY20,FMCHCHQ,ANS,F4,SP70
          CALL      RDFMBN1
          BRANCH    OVRCD,GNID1200
          GOTO      GNID1100
.
.---- no records !! ----
.
GNID1150  MOVE      ONE,F4
          MOVE      " ",ANS
          PACK      KEY20,FMCHCHQ,ANS,F4,SP70
.
GNID1200  
.
GNID9999  RETURN
.
BADERROR  DISPLAY   *P1:24,"CONTACT IBA - fmsbnkaf full for ",FMCHCHQ," - ";
          CALL      HITENTER
          GOTO      ML9999
.******************************************************************************
.* Redisplays                                                                 *
.******************************************************************************
RCHQFM00  DISPLAY   *P1:4,"    Cheque Account       : ",*EF;
          RETURN
.
RBNKFM00  DISPLAY   *P1:4,"    Cheque Account       : ":
                    *P28:4,*V2LON,FMCHCHQ,*HOFF,SP2,FMCHDES,*EL:
                    *P1:5,*+,"    ",CFMBND," Number",*EF,*P26:5,": ";
          RETURN
.
RCRAIB00  DISPLAY   *P1:4,"    Cheque Account       : ":
                    *P28:4,*V2LON,FMCHCHQ,*HOFF,SP2,FMCHDES,*EL:
                    *P1:5,*+,"    ",CFMBND," Number",*EF,*P26:5,": ":
                    *V2LON,CRECNO;
          CALL      SCRA0000
          RETURN
.
.******************************************************************************
.* Keyin receipt number                                                       *
.******************************************************************************
.
XRECFM00  MOVE      ZERO,EXIT 
          KEYIN     *P28:5,*EL,"_______________":
                    *P28:5,*V2LON,CRECNO:
                    *P28:5,*V2LON,*DV,CRECNO
.
          ENDSET    CRECNO
          APPEND    SP70,CRECNO
          RESET     CRECNO
          MATCH     SP70,CRECNO
          GOTO      XRECFM99 IF NOT EQUAL
.
XRECFM95  MOVE      ONE,EXIT
XRECFM99  RETURN
.******************************************************************************
.* INCLUDES FOR I/O'S                                                         *
.******************************************************************************
.
          INC       APSMASIO/INC
          INC       APSMXRIO/INC
          INC       FMSBNKIO/INC       * bank rec
          INC       FMSCHQIO/INC       * cheque
          INC       FMSCHQKY           * cheque
          INC       FMSBNKKY           * cheque no.
          INC       APSCKIKY
          INC       APSMASKY           * creditor
          INC       FMSSTDCD           * FMS Acc. standard routines
