.******************************************************************************
.* Include Name   : KYAAESAW.PBL                                              *
.*                                                                            *
.* Description    : Keyin of a School/Work code from AAESAWFD                 *
.*                                                                            *
.* Author         : LOFTY                                                     *
.*                                                                            *
.* Parameters     : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF6 - default value (blank if no default)            *
.*                  AESWQTYP - type of code to enter E=employer, S=school     *
.*                                                                            *
.* Returns        : AESWCODE - the code                                       *
.*                  AAESAWFD - record                                         *
.*                  EXIT = 0   code selected                                  *
.*                       = 1   exit keyin                                     *
.*                       = 2   enter details only                             *
.*                                                                            *
.* On return      : You must display the description yourself.                *
.*                                                                            *
.******************************************************************************
KYAAESAW  MOVE      ONE,CQUEST              * set ? as not entered
          MOVE      ECOL,CCOL
          MOVE      EVERT,CVERT
.
KAESW100  MATCH     SP6,CKYIDEF6
          GOTO      KAESW120 IF EQUAL       * no default
.
          MOVE      CKYIDEF6,AESWCODE
          KEYIN     *PCCOL:CVERT,*V2LON,*RV,AESWCODE:
                    *PCCOL:CVERT,*DV,AESWCODE
          GOTO      KAESW150
.
KAESW120  KEYIN     *PCCOL:CVERT,*V2LON,AESWCODE:
                    *PCCOL:CVERT,*DV,AESWCODE
.
KAESW150  RESET     AESWCODE
          GOTO      KAESW610 IF EOS         * return entered
.
          PACK      AESWCODE,AESWCODE,SP6
          MATCH     SP6,AESWCODE
          GOTO      KAESW620 IF EQUAL       * space entered
          MATCH     QUEST,AESWCODE
          GOTO      KAESW200 IF NOT EQUAL   * ? not entered
.
.         ? entered
.
          IF        CQUEST = ONE
            MOVE      "0",HLEF                * save all of the screen
            CALL      GETSCR00                * save screen
            MOVE      ZERO,CQUEST             * set as ? performed
          ENDIF
.
          CALL      AAESAWDS                * display valid entries
          BRANCH    EXIT,KAESW610,KAESW620  * Exit, Details
.
.         validate what was entered
.
KAESW200  PACK      KEY7,AESWQTYP,AESWCODE
          CALL      RDAESAW1
          BRANCH    OVRCD,KAESW400          * invalid code
.
          MOVE      ZERO,EXIT               * set as valid code
          GOTO      KAESW800
.
.         invalid code
.
KAESW400  KEYIN     *P28:23,*B,"Invalid Code.  Hit <",*V2LON,"ENTER",*HOFF:
                    "> to continue",*EOFF,ANS;
          DISPLAY   *P28:23,SP20,SP20,SP10
          GOTO      KAESW100
.
.         set exit flags
.
KAESW610  MOVE      ONE,EXIT                * return entered/exit
          GOTO      KAESW690
KAESW620  MOVE      TWO,EXIT                * space/details entered
.
KAESW690  MOVE      SP6,AESWCODE            * clear code
          MOVE      SP30,AESWNAME           * clear desc
.
.         valid entry
.
KAESW800  IF        CQUEST = ZERO
            CALL      PUTSCR00                * redisplay screen
          ENDIF
          DISPLAY   *PECOL:EVERT,*V2LON,AESWCODE
.
KAESW999  RETURN
+
