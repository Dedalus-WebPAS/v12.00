.---------------------------------------------------------------------------
.   System    :   Accounts Payable System            
.   Include   :   APSNSCKY
.   Routine   :   KAPNSA00 Keyin One Off Creditor ID 
.   Parameter :   CKEYTYP 0 = Keyin , 1 = Accept, 2 = Either
.   Return    :   EXIT = 1 null entry, 2 EXITCHAR entered
.-------------------------------------------------------------------------------
KAPNSA00 TOPIC     "APNSUNI "
          MOVE      UNDLN70,APNSUNI 
          KEYIN     *PCCOL:CVERT,*DV,APNSUNI:                                   
                    *PCCOL:CVERT,*V2LON,*JR,*ZF,APNSUNI:                        
                    *PCCOL:CVERT,*DV,APNSUNI 
.
          ENDSET    APNSUNI 
          APPEND    SP70,APNSUNI 
          RESET     APNSUNI 
.
          MATCH     SP70,APNSUNI 
          GOTO      KAPNSA90 IF EQUAL
.
          ENDSET    APNSUNI 
          CMATCH    EXITCHAR,APNSUNI 
          GOTO      KAPNSA95 IF EQUAL
.
          CMATCH    QUEST,APNSUNI 
          GOTO      KAPNSA10 IF NOT EQUAL
.
          MOVE      CCITEM,SCCITEM
          CALL      HAPNSA00
          MOVE      SCCITEM,CCITEM
          BRANCH    OVRCD,KAPNSA00
.
          ENDSET    APNSUNI 
          APPEND    SP70,APNSUNI 
          RESET     APNSUNI 
.
          MATCH     SP70,APNSUNI 
          GOTO      KAPNSA00 IF EQUAL
.
          DISPLAY   *PCCOL:CVERT,*V2LON,APNSUNI                                 
KAPNSA10
          RESET     APNSUNI 
          PACK      KEY8,APNSBCH,APNSUNI                                        
          CALL      RAAPNS1 
          BRANCH    OVRCD,KAPNSA80
          BRANCH    CKEYTYP,KAPNSA75
          CALL      RDAPNS1 
KAPNSA70
          MOVE      ZERO,EXIT
          GOTO      KAPNSA99
KAPNSA75
          MOVE      "One Off Creditor ID Already Exist - ",DISPMSG                                   
          CALL      MESSAGE1
          GOTO      KAPNSA00
KAPNSA80
          MOVE      ZERO,EXIT
          BRANCH    CKEYTYP,KAPNSA99,KAPNSA99
          MOVE      "One Off Creditor ID Does Not Exist - ",DISPMSG                                  
          CALL      MESSAGE1
          GOTO      KAPNSA00
KAPNSA90
          MOVE      ONE,EXIT
          GOTO      KAPNSA99
KAPNSA95
          MOVE      TWO,EXIT
KAPNSA99 TOPIC    PRGID
          RETURN
.---------------------------------------------------------------------------
.   System    :   Accounts Payable System            
.   Include   :   APSNSCKY
.   Routine   :   HAPNSA00 Non Specify Creditor
.   Return    :   APNSUNI 
.-------------------------------------------------------------------------------
HAPNSA00
          MOVE      ZERO,CSCRSIZ
          IF        HTOP<3
            MOVE      "10",HTOP
            MOVE      "24",HBOT
            MOVE      "50",HLEF
            MOVE      "80",HRIG
          ENDIF
HAPNSA04
          CALL      GSCRN000
          BRANCH    EXIT,HAPNSA99
          MOVE      STDCOL00,STDCOL01
          ADD       " 5",STDCOL01
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON,"ID."                
.
          MOVE      STDCOL01,STDCOL02
          ADD       " 4",STDCOL02
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"Pay to Name"        
.
          MOVE      STDCOL02,STDCOL03
          ADD       "35",STDCOL03
HAPNSA08
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          MOVE      SP70,APNSUNI 
          PACK      KEY8,APNSBCH,APNSUNI,SP70                                   
          PACK      SAVKEY80,APNSBCH,APNSUNI,SP70                               
          SETLPTR   SAVKEY80,5                                                  
          CALL      RSAPNS1 
HAPNSA10
          CALL      RKAPNS1 
          BRANCH    OVRCD,HAPNSA15
          PACK      KEY8,APNSBCH,APNSUNI,SP70                                   
          MATCH     SAVKEY80,KEY8                                               
          GOTO      HAPNSA15 IF NOT EQUAL                                       
          BRANCH    CSEARCH,HAPNSA12
          CALL      HAPNSA60
          BRANCH    OVRCD,HAPNSA10
HAPNSA12
          ADD       ONE,XITEMNO
          ADD       ONE,STDROW
          STORE     KEY8,XITEMNO,ST1,ST2,ST3,ST4,ST5:                            
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,XITEMNO,DOT
          DISPLAY   *PSTDCOL01:STDROW,APNSUNI 
          DISPLAY   *PSTDCOL02:STDROW,APNSNA1 
          COMPARE   MAXITEM,XITEMNO
          GOTO      HAPNSA10 IF NOT EQUAL
HAPNSA15
          UNPACK    SAVKEY80,APNSBCH,APNSUNI                                    
          MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
HAPNSA20
          CALL      HSCRI000
          BRANCH    EXIT,HAPNSA30,HAPNSA35,HAPNSA40,HAPNSA45,HAPNSA04:
                              HAPNSA91,HAPNSA50
          GOTO      HAPNSA92
HAPNSA30
          CALL      RKAPNS1 
          BRANCH    OVRCD,HAPNSA85
          PACK      KEY8,APNSBCH,APNSUNI,SP70                                   
          MATCH     SAVKEY80,KEY8                                               
          GOTO      HAPNSA85 IF NOT EQUAL                                       
          CALL      RPAPNS1 
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HAPNSA10
HAPNSA35
          MOVE      ST1,KEY8 
          CALL      RDAPNS1 
          CALL      RPAPNS1 
          BRANCH    OVRCD,HAPNSA85
          PACK      KEY8,APNSBCH,APNSUNI,SP70                                   
          MATCH     SAVKEY80,KEY8                                               
          GOTO      HAPNSA85 IF NOT EQUAL                                       
          MATCH     SP70,APNSUNI                                                
          GOTO      HAPNSA85 IF EQUAL                                           
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
HAPNSA36
          CALL      RPAPNS1 
          BRANCH    OVRCD,HAPNSA37
          PACK      KEY8,APNSBCH,APNSUNI,SP70                                   
          MATCH     SAVKEY80,KEY8                                               
          GOTO      HAPNSA37 IF NOT EQUAL                                       
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HAPNSA36 IF NOT EQUAL
HAPNSA37
          MOVE      ZERO,XITEMNO
          GOTO      HAPNSA10
HAPNSA40
          MOVE      ST1,KEY8 
          CALL      RDAPNS1 
          CALL      RPAPNS1 
          BRANCH    OVRCD,HAPNSA85
          PACK      KEY8,APNSBCH,APNSUNI,SP70                                   
          MATCH     SAVKEY80,KEY8                                               
          GOTO      HAPNSA85 IF NOT EQUAL                                       
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HAPNSA08
HAPNSA45
          CALL      RKAPNS1 
          BRANCH    OVRCD,HAPNSA85
          PACK      KEY8,APNSBCH,APNSUNI,SP70                                   
          MATCH     SAVKEY80,KEY8                                               
          GOTO      HAPNSA85 IF NOT EQUAL                                       
          SUSPEND
          CLIP      1,1,CRIG,24
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,XITEMNO
          PACK      KEY8,SAVKEY80,Z70                                           
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
          CALL      RSAPNS1 
HAPNSA46
          CALL      RPAPNS1 
          BRANCH    OVRCD,HAPNSA47
          PACK      KEY8,APNSBCH,APNSUNI,SP70                                   
          MATCH     SAVKEY80,KEY8                                               
          GOTO      HAPNSA47 IF NOT EQUAL                                       
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HAPNSA46 IF NOT EQUAL
HAPNSA47
          MOVE      ZERO,XITEMNO
          GOTO      HAPNSA10
HAPNSA50  COMPARE   ZERO,CSEARCH
          GOTO      HAPNSA55 IF EQUAL
          MOVE      ZERO,CSEARCH
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          UNPACK    SP70,SCHFLD01,SCHFLD02,SCHFLD03,SCHFLD04,SCHFLD05
          UNPACK    SP70,SCHFLD06,SCHFLD07,SCHFLD08,SCHFLD09,SCHFLD10
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          DISPLAY   *PSTDCOL00:STDROW,*V2LON,*HON,"Sch"
          KEYIN     *PSTDCOL01:STDROW,*V2LON,SCHFLD01
          RESET     SCHFLD01
          KEYIN     *PSTDCOL02:STDROW,*V2LON,SCHFLD02
          RESET     SCHFLD02
          PACK      KEY8,SP70
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HAPNSA08
HAPNSA55  MOVE      ONE,CSEARCH
          PACK      KEY8,SP70
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HAPNSA08
HAPNSA60  MOVE      ZERO,OVRCD
          MATCH     SP70,SCHFLD01
          GOTO      HAPNSA61 IF EQUAL
          MATCH     SCHFLD01,APNSUNI 
          GOTO      HAPNSA72 IF NOT EQUAL
HAPNSA61
          MATCH     SP70,SCHFLD02
          GOTO      HAPNSA62 IF EQUAL
          MATCH     SCHFLD02,APNSNA1 
          GOTO      HAPNSA72 IF NOT EQUAL
HAPNSA62
HAPNSA63
HAPNSA64
HAPNSA65
HAPNSA66
HAPNSA67
HAPNSA68
HAPNSA69
HAPNSA70
          GOTO      HAPNSA75
HAPNSA72  MOVE      ONE,OVRCD
HAPNSA75  RETURN
HAPNSA85
          LOAD      KEY8,XITEMNO,ST1,ST2,ST3,ST4,ST5:                            
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY8,APNSBCH,APNSUNI                                        
          CALL      RDAPNS1 
HAPNSA90
          UNPACK    SAVKEY80,APNSBCH,APNSUNI                                    
          BEEP
          GOTO      HAPNSA20
HAPNSA91
          MOVE      SP70,APNSNA1 
          MOVE      SP70,APNSNA2 
          MOVE      SP70,APNSAD1 
          MOVE      SP70,APNSAD2 
          MOVE      SP70,APNSAD3 
          MOVE      SP70,APNSPC  
          MOVE      SP70,APNSSPA 
          MOVE      SP70,KEY8 
          MOVE      SP70,APNSUNI 
          GOTO      HAPNSA96
HAPNSA92
          MOVE      SP70,KEY8 
          MOVE      SP70,APNSUNI 
          LOAD      KEY8,CCITEM,ST1,ST2,ST3,ST4,ST5:                            
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY8,APNSBCH,APNSUNI                                        
HAPNSA96
          PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
HAPNSA99
          RETURN
