. ******************************************************************************
. *              P R O G R A M  S U M M A R Y                                  *
. *              -------------  -------------                                  *
. *                                                                            *
. *     PROGRAM NAME:     IBAAAE10                                             *
. *                                                                            *
. *     FUNCTION:         CHARGE MAINTENANCE (Maintain PATNIPFD)               *
. *                                                                            *
. *     DATE WRITTEN:     02/09/88                                             *
. *                                                                            *
. *     AUTHOR:           M.HAYSE                                              *
. *                                                                            *
. *     MODIFICATIONS:                                                         *
. *       V9.02.01  13/10/2001 J.Tan                                           *
. *                 Changed patnip file                                        *
. *       V5.01     21/04/89 K.Peak                                            *
. *                 Added CONTROLF SRF 100081                                  *
. *       V5.02     16/05/89 K.Peak                                            *
. *                 Clear Error Msg properly  SRF  100544                      *
. *       V5.03     31/05/89 K.Peak                                            *
. *                 Fixed Audit Busy SRF 100824                                *
. *       V5.01.02  06/06/90 D.Di.Paola                                        *
. *                 Recompiled for changes to AAECONFD...   SRF 103773         *
. *       V5.01.03  14/05/1991    Justin Coates       Quote 6943 SRF 108605    *
. *                 If AECNCHRS = 1 enter NBINDC (Cat ED) discharge status     *
. *       V5.01.04  20/05/1991    Steve O'Gorman                               *
. *                 Removed Un-used Code Found by the New Compiler             *
. ******************************************************************************
.
          INC       STD001FD
          INC       AAEAUXFD/INC
          INC       AAECONFD/INC
          INC       PATCODFD/INC
          INC       PATNIPFD/INC       * patients charge file
.
.         Extra variables
.
CATED     INIT      "ED"      * discharge status
CATCI     INIT      "CI"        **** CLINIC INDICATOR
CATCL     INIT      "CL"
CATFI     INIT      "FI"      * item code
CATT      INIT      "T "
CODE      DIM       2
.
CHG       FORM      1        * indicator to see if anything has changed
.
VERT      FORM      2
COL       FORM      2
.
FIELD     FORM      1
MODE1     DIM       1
MODE      DIM       1
.
DESCRESD  DIM       20        * resident status  - description
DESCCOMP  DIM       20        * comp code        - description
DESCDSTA  DIM       20        * discharge status - description
DESCITEM  DIM       20        * item code        - description
.
AMNT1     FORM      7.2
.
.
PRGID     INIT      "IBAAAE10"
PRGNAM    INIT      "Charge Maintenance"
VERSION   INIT      "V12.00.00"
+
.         Start of program
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patnipbf";
          OPEN      PATNIPB1,"patnipbf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,THIRTY;*196,AECNCHRS
          READ      CONTROLF,THIRTY1;*53,HEAUDC
.
          MOVE      SP1,AUMODE
          BRANCH    HEAUDC OF MENU
          MOVE      ANSC,AUTYPE
          REP       "Cc",AUTYPE
          CALL      STRTAUDX
.
MENU      DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Insert":
                    *P1:6,*V2LON,TWO,*HOFF," = Change":
                    *P1:7,*V2LON,THREE,*HOFF," = Delete"
.
BEGIN     KEYIN     *P1:9,*EL,"Please Select : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
REPEAT    BRANCH    OPTION OF INSERT,CHANGE,DELETE
.
          BEEP
          GOTO      BEGIN
.
.         Add a charge
.
INSERT    CALL      DISPI                            * menu for insert
          CALL      DISC0000                         * main menu
.
          MOVE      ANSI,MODE1
          MOVE      MODE1,MODE
          MOVE      ZERO,CHG                         * initialise CHG
.
          BRANCH    HEAUDC OF KRESD
          MOVE      ANSI,AUMODE
          GOTO      KRESD
.
.         Change a charge
.
CHANGE    CALL      DISPC
          CALL      DISC0000
.
          MOVE      ZERO,CHG
          MOVE      ANSC,MODE1
          MOVE      MODE1,MODE
.
          BRANCH    HEAUDC OF KRESD
          MOVE      ANSC,AUMODE
          GOTO      KRESD
.
.         Delete a Charge
.
DELETE    CALL      DISPD
          CALL      DISC0000
.
          MOVE      ANSD,MODE1
          MOVE      MODE1,MODE
.
          BRANCH    HEAUDC OF KRESD
          MOVE      ANSD,AUMODE
          GOTO      KRESD
.
DELCS     CALL      DDAT0000
.
REKEY1    KEYIN     *P1:24,*EL,"Are you sure ":
                    "you want to delete this ",*DV,"Rate  "," (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF:
                    ") ? ",*V2LON,ANS;
.
          CMATCH    ANSY,ANS
          GOTO      WARDEL IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      WRONG1 IF NOT EQUAL
.
          GOTO      REPEAT
.
WRONG1    BEEP
          GOTO      REKEY1
.
WARDEL    MOVE      ONE,NBSYST      * System - A&E
          MOVE      SP10,NBINDC
.
          PACK      KEY13 WITH NBSYST,NBINDC,NBRESD,NBCOMP,NBINDV
          CALL      DENIPB1
.
          BRANCH    HEAUDC OF ENDDELT
.
          PACK      AUCONBEF WITH KEY15,SP30,SP30
          MOVE      "syst/resd/comp/indc ",AUDESC
          PACK      AUCONAFT WITH SP30,SP20
          CALL      WRITAUDX
          CALL      POSTAUDX
.
ENDDELT   DISPLAY   *P1:24,*EL,*V2LON,"** Deleted ** ";
          CALL      HITENTER
          GOTO      REPEAT
.
.       Input resident status
.
KRESD     MOVE      ONE,NBSYST              * System - A&E
          MOVE      SP3,NBINDC              * Clinic indicator spaces
          MOVE      SP3,NBRESD              * resident status
          MOVE      SP3,NBCOMP              * compenstion code
          MOVE      SP3,NBINDV              * discharge status
          MOVE      SP3,NBITEM              * item code
          MOVE      SP30,NBDESC             * description
          MOVE      ZERO,NBAMNT             * amount
          MOVE      SP30,DESCRESD
          MOVE      SP30,DESCCOMP
          MOVE      SP30,DESCDSTA
          MOVE      SP30,DESCITEM
.
KKRESD    KEYIN     *P29:4,*EL,*DV,UNDLN3,*P29:4,*V2LON,NBRESD:
                    *P29:4,*DV,NBRESD
.
          ENDSET    NBRESD
          APPEND    SP5,NBRESD
          RESET     NBRESD
.
          MATCH     SP3,NBRESD
          GOTO      MENU IF EQUAL
.
          CMATCH    QUEST,NBRESD
          GOTO      KRESD1 IF NOT EQUAL
.
          MOVE      SP3,NBRESD
          MOVE      CATT,CODE
          CALL      PATCODDS
          CALL      DISC0000
          CALL      DDAT0000
          GOTO      KRESD
.
KRESD1    PACK      AUCONAFT WITH NBRESD,SP30,SP30
.
          PACK      KEY5 WITH CATT,NBRESD
          CALL      RDCODE1
          BRANCH    OVRCD OF ENDN4
.
          MOVE      TDESC,DESCRESD
          DISPLAY   *P40:4,DESCRESD
.
          PACK      AUITEM WITH NBRESD,SP20
          GOTO      KCOMP
.
ENDN1     DISPLAY   *P40:24,*EL,*B,*V2LON:
                    "** No Amount For This Combination **",*W2
          DISPLAY   *P40:24,*EL
          CALL      DISC0000
          GOTO      KRESD
.
ENDN4     DISPLAY   *P40:4,*EL,*B,*V2LON:
                    "** Resident Status Does Not Exist **",*W2
          GOTO      KRESD
.
ENDN2     DISPLAY   *P40:5,*EL,*B,*V2LON:
                    "** Compensation Does Not Exist **",*W2
          GOTO      KCOMP
.
.         Input compensation code
.
KCOMP     MOVE      SP3,NBCOMP
          MOVE      SP30,DESCCOMP
.
          KEYIN     *P29:5,*EL,*DV,UNDLN3,*P29:5,*V2LON,NBCOMP:
                    *P29:5,*DV,NBCOMP
.
          ENDSET    NBCOMP
          APPEND    SP5,NBCOMP
          RESET     NBCOMP
.
          MATCH     SP3,NBCOMP
          GOTO      KRESD IF EQUAL
.
          CMATCH    QUEST,NBCOMP
          GOTO      KCOMP1 IF NOT EQUAL
.
          MOVE      SP3,NBCOMP
          MOVE      CATCL,CODE
          CALL      PATCODDS
          CALL      DISC0000
          CALL      DDAT0000
          GOTO      KCOMP
.
KCOMP1    PACK      KEY5 WITH CATCL,NBCOMP
          CALL      RDCODE1
          BRANCH    OVRCD OF ENDN2
.
.         have a valid compensation code
.
          MOVE      TDESC,DESCCOMP
          DISPLAY   *P40:5,DESCCOMP
.
          COMPARE   ONE,AECNCHRS
          GOTO      KCOMP2 IF NOT EQUAL     * not using discharge status/NBINDV
.
.         must keyin discharge status
.
          CALL      KDIS0000                * enter discharge status
          BRANCH    EXIT,KCOMP              * invalid/nothing entered
.
KCOMP2    PACK      KEY13 WITH NBSYST,NBINDC,NBRESD,NBCOMP,NBINDV
          CALL      RDNIPB1
          BRANCH    OVRCD OF ENDNO
.
          CMATCH    ANSC,MODE
          GOTO      RDCHR IF EQUAL
.
          CMATCH    ANSD,MODE
          GOTO      DELCS IF EQUAL
.
          DISPLAY   *P41:7,*B,*V2LON:
                    "** Charge Code Already Exists **",*W2,*P41:7,*EL
          MOVE      SP3,NBITEM
          MOVE      ZERO,NBAMNT
          MOVE      SP30,NBDESC
          GOTO      KCOMP
.
ENDNO     CMATCH    ANSI,MODE
          GOTO      KITEM IF EQUAL
.
          DISPLAY   *P41:7,*B,*V2LON:
                    "** Charge Code Does Not Exist **",*W2,*P41:7,*EL
          GOTO      KCOMP
.
RDCHR     CALL      DDAT0000
          GOTO      SFIELD
.
.       Input "Item code"
.
KITEM     PACK      AUCONBEF WITH NBITEM,SP30,SP30
          MOVE      "Item Code          ",AUDESC
.
KITEM0    MOVE      SP3,NBITEM
          KEYIN     *P29:8,*DV,UNDLN3,*P29:8,*V2LON,NBITEM,*P29:8,*DV,NBITEM
.
          ENDSET    NBITEM
          APPEND    SP5,NBITEM
          RESET     NBITEM
.
          MATCH     SP3,NBITEM
          GOTO      INVITEM IF EQUAL
.
          CMATCH    QUEST,NBITEM
          GOTO      KITEM1 IF NOT EQUAL
.
          MOVE      SP3,NBITEM
          MOVE      CATFI,CODE
          CALL      PATCODDS
          CALL      DISC0000
          CALL      DDAT0000
          GOTO      KITEM0
.
KITEM1    PACK      KEY5 WITH CATFI,NBITEM
          CALL      RDCODE1
          BRANCH    OVRCD OF INVITEM
.
          MOVE      TDESC,DESCITEM
          DISPLAY   *P40:8,DESCITEM
          PACK      AUCONAFT WITH NBITEM,SP30,SP20
.
          CMATCH    ANSC,AUMODE
          CALL      WRITAUDX IF EQUAL
.
          MOVE      ONE,CHG
          CMATCH    ANSC,MODE
          GOTO      SFIELD IF EQUAL
          GOTO      KDESC
.
INVITEM   DISPLAY   *P40:8,*EL,*B,*V2LON:
                    "** Code Does Not Exist **",*W2:
                    *P40:8,*EL;
          GOTO      KITEM0
.
.       Input "Description   "
.
KDESC     PACK      AUCONBEF WITH NBDESC,SP30,SP30
          MOVE      "Description        ",AUDESC
.
          MOVE      SP30,NBDESC
          KEYIN     *P29:9,*DV,UNDLN20,*DV,UNDLN10:
                    *P29:9,*V2LON,NBDESC,*P29:9,*DV,NBDESC
.
          PACK      AUCONAFT WITH NBDESC,SP30,SP20
.
          CMATCH    ANSC,AUMODE
          CALL      WRITAUDX IF EQUAL
.
          MOVE      ONE,CHG
          CMATCH    ANSC,MODE
          GOTO      SFIELD IF EQUAL
.
.       Input "charged amount"
.
KAMNT     PACK      AUCONBEF WITH NBAMNT,SP30,SP30
          MOVE      "Charged amount     ",AUDESC
.
          MOVE      ZERO,NBAMNT
          KEYIN     *P29:10,*DV,UNDLN9:
                    *P29:10,*V2LON,NBAMNT,*P29:10,*DV,NBAMNT
.
          PACK      AUCONAFT WITH NBAMNT,SP30,SP20
.
          CMATCH    ANSC,AUMODE
          CALL      WRITAUDX IF EQUAL
.
          MOVE      ONE,CHG
          CMATCH    ANSC,MODE
          GOTO      SFIELD IF EQUAL
.
.       Select the field to update
.
SFIELD    KEYIN     *P1:24,*EL,"Select field (0 to post) ? ":
                    *V2LON,FIELD;
          COMPARE   ZERO,FIELD
          GOTO      POST IF EQUAL
          MOVE      ANSC,MODE
.
SBRCH     BRANCH    FIELD OF KITEM,KDESC,KAMNT
.
INVFLD    DISPLAY   *P40:24,*EL,*B,*V2LON,"** Invalid Field **",*W2;
          GOTO      SFIELD
.
POST      KEYIN     *P1:24,*EL,"Ok to post (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF:
                    *DV,SLASH,*V2LON,*DV,ANSC,*HOFF:
                    ") ? ",*V2LON,ANS;
.
          CMATCH    ANSN,ANS
          GOTO      SFIELD IF EQUAL
.
          CMATCH    ANSY,ANS
          GOTO      POSTCNT IF EQUAL
.
          CMATCH    ANSC,ANS
          GOTO      WRONG IF NOT EQUAL
.
          BRANCH    HEAUDC OF REPEAT
          CALL      CANCAUDX
          GOTO      REPEAT
.
WRONG     BEEP
          GOTO      POST
.
.       Check CHG indicator to see if anything was changed
.
POSTCNT   CMATCH    ANSC,MODE1
          GOTO      WARWR IF NOT EQUAL
.
          COMPARE   ZERO,CHG
          GOTO      REPEAT IF EQUAL
.
          CALL      UPNIPB1
          GOTO      ENDPST
.
WARWR     MOVE      ONE,NBSYST      * System - A&E
          MOVE      SP10,NBINDC
.
          PACK      KEY13 WITH NBSYST,NBINDC,NBRESD,NBCOMP,NBINDV
          CALL      WRNIPB1
          BRANCH    OVRCD OF TOOLATE
.
          BRANCH    HEAUDC OF ENDPST
.
          PACK      AUCONBEF WITH KEY15,SP30,SP30
          MOVE      "indc/Resd/comp/vist            ",AUDESC
.
          PACK      AUCONAFT WITH SP30,SP20
          CALL      WRITAUDX
.
ENDPST    CMATCH    SP1,AUMODE
          CALL      POSTAUDX IF NOT EQUAL
.
          DISPLAY   *P40:24,*EL,*V2LON:
                    "** Data Successfully Posted **",*W2;
          GOTO      REPEAT
.
TOOLATE   DISPLAY   *P40:24,*EL,*B,*V2LON:
                    "** Charge Already Exists For This Combination **",*W2;
          GOTO      REPEAT
.
.        Menus for each mode
.
DISPI     DISPLAY   *P49:2,*V2LON,"- Insert Mode "
          RETURN
.
DISPC     DISPLAY   *P49:2,*V2LON,"- Change Mode "
          RETURN
.
DISPD     DISPLAY   *P49:2,*V2LON,"- Delete Mode "
          RETURN
.
.*********************************************************************
.*                  DISC0000                    Called by : lots     *
.*        Display the main screen                                    *
.*********************************************************************
DISC0000  HLINE     *G37,2,49,80
          DISPLAY   *P1:4,*EF:
                    *P1:4,"    Resident status       :":
                    *P1:5,"    Compensation code     :"
.
          COMPARE   ONE,AECNCHRS
          GOTO      DISC1000 IF NOT EQUAL   * not using discharge status
.
          DISPLAY   *P1:6,"    Discharge Status      :"
.
DISC1000  DISPLAY   *P1:8," 1. Item                  :":
                    *P1:9," 2. Description           :":
                    *P1:10," 3. Charged amount      $ :"
.
DISC9999  RETURN
+
.
.*********************************************************************
.*                  DDAT0000                    Called by : lots     *
.*        Display all the data                                       *
.*********************************************************************
.
DDAT0000  MOVE      SP30,DESCRESD           * resident status
          MATCH     SP3,NBRESD
          GOTO      DDAT1000 IF EQUAL       * no code to display
.
          PACK      KEY5 WITH CATT,NBRESD
          CALL      RDCODE1
          BRANCH    OVRCD OF DDAT1000
.
          MOVE      TDESC,DESCRESD
.
DDAT1000  MOVE      SP30,DESCCOMP           * compensation code
          MATCH     SP3,NBCOMP
          GOTO      DDAT2000 IF EQUAL       * no code to display
.
          PACK      KEY5 WITH CATCL,NBCOMP
          CALL      RDCODE1
          BRANCH    OVRCD OF DDAT2000
.
          MOVE      TDESC,DESCCOMP
.
DDAT2000  MOVE      SP30,DESCDSTA           * discharge status
          COMPARE   ONE,AECNCHRS
          GOTO      DDAT3000 IF NOT EQUAL   * not using discharge status
.
          MATCH     SP3,NBINDV
          GOTO      DDAT3000 IF EQUAL       * no code to display
.
          PACK      KEY5 WITH CATED,NBINDV
          CALL      RDCODE1
          BRANCH    OVRCD OF DDAT3000
.
          MOVE      TDESC,DESCDSTA
.
DDAT3000  MOVE      SP30,DESCITEM           * item code
          MATCH     SP3,NBITEM
          GOTO      DDAT4000 IF EQUAL       * no code to display
.
          PACK      KEY5 WITH CATFI,NBITEM
          CALL      RDCODE1
          BRANCH    OVRCD OF DDAT4000
.
          MOVE      TDESC,DESCITEM
.
.         display the fields
.
DDAT4000  DISPLAY   *P29:4,*V2LON,NBRESD,*HOFF,*P40:4,DESCRESD:
                    *P29:5,*V2LON,NBCOMP,*HOFF,*P40:5,DESCCOMP:
                    *P29:6,*V2LON,NBINDV,*HOFF,*P40:6,DESCDSTA:
                    *P29:8,*V2LON,NBITEM,*HOFF,*P40:8,DESCITEM:
                    *P29:9,*V2LON,NBDESC:
                    *P29:10,NBAMNT
DDAT9999  RETURN
+
.*********************************************************************
.*                  KDIS0000                    Called by : KCOMP    *
.*        Keyin the discharge status                                 *
.*        Returns : EXIT = 1      nothing entered                    *
.*                  NBINDV        the code                           *
.*********************************************************************
KDIS0000  COMPARE   ONE,AECNCHRS
          GOTO      KDIS9100 IF NOT EQUAL   * not set up to enter status
.
          MOVE      SP3,NBINDV              * clear code
          MOVE      SP20,DESCDSTA           * clear desc
          MOVE      CATED,CODE              * category
.
.         keyin the code
.
KDIS1000  KEYIN     *P29:6,*DV,UNDLN3,*EL:
                    *P29:6,*V2LON,NBINDV:
                    *P29:6,*DV,NBINDV
.
          ENDSET    NBINDV
          APPEND    SP3,NBINDV
          RESET     NBINDV
.
          MATCH     SP3,NBINDV
          GOTO      KDIS9100 IF EQUAL       * nothing entered
.
          MATCH     QUEST,NBINDV
          GOTO      KDIS2000 IF NOT EQUAL   * code entered
.
.         ? entered
.
          CALL      PATCODDS                * display valid codes
          CALL      DISC0000                * display screen
          CALL      DDAT0000                * display data
          GOTO      KDIS1000                * re-enter code
.
.         validate the code 
.
KDIS2000  PACK      KEY5,CATED,NBINDV
          CALL      RDCODE1
          BRANCH    OVRCD,KDIS7000          * invalid code
.
          MOVE      TDESC,DESCDSTA          * the description
          DISPLAY   *P40:6,DESCDSTA         * display the code
.
.         now check if it is valid for the mode
.
          MOVE      ONE,NBSYST
          PACK      KEY13,NBSYST,NBINDC,NBRESD,NBCOMP,NBINDV
          CALL      RDNIPB1
          BRANCH    OVRCD,KDIS4000          * code not on file
.
.         combination of codes is on file
.
KDIS3000  MATCH     ANSI,MODE
          GOTO      KDIS9000 IF NOT EQUAL   * not in insert mode
.
          DISPLAY   *P1:24,*B,"Combination of codes already exists.  ",*EL;
          CALL      HITENTER 
          GOTO      KDIS1000
.
.         combination of codes is not on file
.
KDIS4000  MATCH     ANSI,MODE
          GOTO      KDIS9000 IF EQUAL       * in insert mode
.
          DISPLAY   *P1:24,*B,"Combination of codes doesn't exist.  ",*EL;
          CALL      HITENTER 
          GOTO      KDIS1000
.
.         invalid code entered
.
KDIS7000  DISPLAY   *P1:24,*B,"Invalid Discharge status.  ",*EL;
          CALL      HITENTER 
          GOTO      KDIS1000
.
KDIS9000  MOVE      ZERO,EXIT               * valid code
          GOTO      KDIS9999
.
KDIS9100  MOVE      ONE,EXIT                * nothing entered
          MOVE      SP3,NBINDV              * clear code
.
KDIS9999  RETURN
+
.
.         I/O includes needed
.
          INC       STD001IO
          INC       PATCODKY
          INC       PATCODIO/INC
          INC       PATNIPIO/INC
          INC       AAEAUXIO/INC
.
ENDIT     BRANCH    HEAUDC OF ABORT
          CALL      STOPAUDX
.
ABORT     CHAIN     PGM
          STOP
................................................................................
