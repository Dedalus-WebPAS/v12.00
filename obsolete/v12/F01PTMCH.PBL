.*****************************************************************************
.*    Program      : F01PTMCH                                                *
.*    Description  : Conversion patmchgf to new File layout                  *
.*                                                                           *
.*    Author       : Mike Laming                                             *
.*    Date         : 26/11/2010                                              *
.*    Modifications: V10.02.01 Peter Vela CAR 233046                         *
.*                   Added Duplicate Record header display line.             *
.*                   V10.01.02 J.Tan   CAR 233046                            *
.*                   Mods to print duplicate records                         *
.*****************************************************************************
.
.  Global change F01PTMCH  eg F57PTDOC
.  Global change PATMCHFD  eg PATDOCFD (new layout)
.  Global change patmchgf  eg patdoctf
.  Global change PATMCHG1  eg PATDOCT1
.  Global change KEY34   eg KEY8
.  Global change KEY29  eg KEY9
.  Global change WRMCHG1  eg WRDOCT1
.
.  vi Global change command  :%s/FromString/ToString/g
.
.
          INC       STD001FD
.
          INC       PATFN1FD/INC
.
FINDFILE  FILE      ASCII, FIXED=256
.
. Enter the first or the shortest index from the original FD and rename
. the index OLDPTMCH
.
OLDPTMCH   IFILE SQL, FIXED=147, KEY="U1-3,4-9,10-17,18-26,109-116"
.
.                                     ******** copy the old FD here *******
.                                     * comment out all continuing fields and
.                                     * prefix changing fields with "OLD" -
.                                     * remove old Indexes and Redefines     
. NAME     TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION              
. ----     ----   ------   --------  ----------   -----------              
..MCLMCOD    DIM         3         3         1      CLAIM CODE (Cat CL)      
..MHFUND     DIM         6         6         4      HEALTH FUND              
MHFTAB     DIM         8         8        10      HEALTH FUND TABLE        
..MCHARGE    DIM         9         9        18      CHARGE CODE              
..MDESC      DIM        30        30        27      DESCRIPTION
..MITMTYP    FORM        1         2        57      CHARGE TYPE 
.                                                 2 = THEATRE FEE
.                                                 3 = MISC. ITEM
.
..MMSCGRP    DIM         3         3        59      MISC CHRGE GROUP (CAT. FI)
..MPATPOR    FORM     12.2         8        62      PATIENT PORTION
..MHFPOR     FORM     12.2         8        70      REBATE  PORTION
..MNINV      FORM        1         2        78      NUMBER OF INVS - 1 or 2 ONLY
..MINDIC     FORM        1         2        80      INDICATOR FOR CHANGING DESC.
.                                                 0 = NO
.                                                 1 = YES
..PTMCGSTA   FORM        1         2        82      Is GST applicable to charge
.                                                 0 = GST free
.                                                 1 = GST payable
.                                                 2 = Either
..PTMCGSTC   DIM         6         6        84      GST payable code from GST
.                                                 rates table
..PTMCEDIE   DIM        10        10        90      EDI Equivalent
..PTMCCFCY   FORM        1         2       100      Casemix funding chargeable
..PTMCKREB   DIM         1         1       102      KEYIN REBATE PORTION
.                                                 Y = YES
.                                                 OTHERWISE NO
..PTMCCOBD   DIM         1         1       103      Charge on behalf of a doctor
.                                                 Y = Yes
.                                                 Otherwise No
..PTMCTACT   DIM         1         1       104      TAC type
.                                                   A = AMA item
.                                                   P = Paramedical item
.                                                   T = TAC item
..PTMCTACD   DIM         4         4       105      TAC duration (mins) - only
.                                                 required for paramedical items
..PTMCEFDT   DIM         8         8       109      Effective Date CCYYMMDD
..PTMCALGR   DIM         3         3       117      Alternate Misc Chrge Group(FN)
..                                                 for JH Invoice
..PTMCPRTR   DIM         1         1       120      Prosthetic for tracking
..                                                 0 = NO
..                                                 1 = YES
..PTMCECEQ   DIM        11        11       121      Eclipse Equivalent
..PTMCECTS   DIM         2         2       132      Eclipse Type of Service
MCSPARE     DIM        13        13       134      SPARE VARIABLE
.
.End of Record                           147
.
.
. Other field definitions
. -----------------------
..PTMCACTV   DIM         1         1                Active/Inactive Flag
.                                                   (Blank or 0 = Active)
.                                                   (1 = Inactive)
.
.                                     ******** copy the new FD here *******
.
PATMCHG1   IFILE SQL, FIXED=186, KEY="U1-3,4-9,10-12,13-21,104-111"
PATMCHG2   IFILE SQL, FIXED=186, KEY="U22-51,1-3,4-9,10-12,13-21,104-111"
PATMCHG3   IFILE SQL, FIXED=186, KEY="U1-3,4-9,10-12,54-56,104-111,13-21"
PATMCHG4   IFILE SQL, FIXED=186, KEY="U13-21,1-3,4-9,10-12,104-111"
.
. NAME     TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION
. ----     ----   ------   --------  ----------   -----------
MCLMCOD    DIM         3         3         1      CLAIM CODE (Cat CL)
MHFUND     DIM         6         6         4      HEALTH FUND
PTMCHFTT   DIM         3         3        10      H/F Table Type (Cat."HT")
MCHARGE    DIM         9         9        13      CHARGE CODE
MDESC      DIM        30        30        22      DESCRIPTION
MITMTYP    FORM        1         2        52      CHARGE TYPE
.                                                 2 = THEATRE FEE
.                                                 3 = MISC. ITEM
.
MMSCGRP    DIM         3         3        54      MISC CHRGE GROUP (CATEGORY FI)
MPATPOR    FORM     12.2         8        57      PATIENT PORTION
MHFPOR     FORM     12.2         8        65      REBATE  PORTION
MNINV      FORM        1         2        73      NUMBER OF INVS - 1 or 2 ONLY
MINDIC     FORM        1         2        75      INDICATOR FOR CHANGING DESC.
.                                                 0 = NO
.                                                 1 = YES
PTMCGSTA   FORM        1         2        77      Is GST applicable to charge
.                                                 0 = GST free
.                                                 1 = GST payable
.                                                 2 = Either
PTMCGSTC   DIM         6         6        79      GST payable code from GST
.                                                 rates table
PTMCEDIE   DIM        10        10        85      EDI Equivalent
PTMCCFCY   FORM        1         2        95      Casemix funding chargeable
PTMCKREB   DIM         1         1        97      KEYIN REBATE PORTION
.                                                 Y = YES
.                                                 OTHERWISE NO
PTMCCOBD   DIM         1         1        98      Charge on behalf of a doctor
.                                                 Y = Yes
.                                                 Otherwise No
PTMCTACT   DIM         1         1        99      TAC type
.                                                   A = AMA item
.                                                   P = Paramedical item
.                                                   T = TAC item
PTMCTACD   DIM         4         4       100      TAC duration (mins) - only
.                                                 required for paramedical items
PTMCEFDT   DIM         8         8       104      Effective Date CCYYMMDD
PTMCALGR   DIM         3         3       112      Alternate Misc Chrge Group(FN)
.                                                 for JH Invoice
PTMCPRTR   DIM         1         1       115      Prosthetic for tracking
.                                                 0 = NO
.                                                 1 = YES
PTMCECEQ   DIM        11        11       116      Eclipse Equivalent
PTMCECTS   DIM         2         2       127      Eclipse Type of Service
PTMCACTV   DIM         1         1       129      Active/Inactive Flag
.                                                   (Blank or 0 = Active)
.                                                   (1 = Inactive)
PTMCETDT   DIM         8         8       130      Effective to Date (CCYYMMDD)
PTMCSPAR   DIM        48        48       138      SPARE VARIABLE
.
.End of Record                           186
.
CMDLINE   DIM       100
DIM4      DIM       4
DIM4A     DIM       4
DIM6      DIM       6
DIM6A     DIM       6
DIM60     DIM       60
DDCENT    DIM       2
.
SAVKEY21  DIM       21
SAVEDATE  DIM       8
WORKDATE  DATE      8
.
INPFILE   DIM       8
RECNUM    FORM      8
NEWFILE   DIM       60
NEWPATH   DIM       60
OLDPATH   DIM       60
DEFPATH   DIM       60
SAVEFLG   FORM      1
SP50      INIT    "                                                  "
SP60      INIT    "                                                            "
.
PRGNAM    INIT      "CONVERSION PATMCHFD"
PRGID     INIT      "F01PTMCH"
VERSION   INIT      "V12.00.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000                      * init and open files
.
          CALL      OPTN0000                * Choose main option
          BRANCH    EXIT,MAIN9999           * EXIT
          BRANCH    OPTION,MAIN0500:
                           MAIN4000:
                           MAIN4000
.
MAIN0500  OPEN      PATFN1A1,"patfn1af"           * Open Health Fund File
.
          CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN1000,MAIN0000,MAIN9999 * Yes, no, cancel
.
MAIN1000  CALL      CPFL0000                      * copy patmchgf to v10mchgf
          BRANCH    EXIT,MAIN9999
.
          CALL      CMCH0000                      * Create new Misc.Charge file
          GOTO      MAIN9999
.
MAIN4000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000,MAIN4000,MAIN9999 * Yes, no, cancel
.
MAIN5000  CALL      READ0000           * read old records and write
          BRANCH    EXIT,MAIN9999
          CALL      ENDP0000           * end of processing
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
.
          OPEN      PATFN1A1,"patfn1af"           * Open Health Fund File
          
INIT9999  RETURN
.
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
KDIR0000  EXECUTE   "ldat patmchgf.dat > temp.txt",TASKID
          CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
.
KDIR1000  MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
.
          KEYIN     *P1:12,*EL,"Keyin path for saving v10mchgf file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
          ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          CPFL0000                                *
.*                       Copy patmchgf to v10mchgf                  *
.********************************************************************
CPFL0000  MOVE      "patmchgf",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,CPFL9000
.
          CLOSE     OLDPTMCH
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
. Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat patmchgf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "v10mchgf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      CPFL8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat patmchgf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "v10mchgf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      CPFL8000
          ENDIF
.
. remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "patmchgf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      CPFL9000
          ENDIF
.
. Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "v10mchgf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDPTMCH,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      CPFL9000
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      CPFL9999
.
CPFL8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
CPFL9000  MOVE      ONE,EXIT
.
CPFL9999  RETURN
+
.*****************************************************************************
.         Create the new file
.*****************************************************************************
CMCH0000
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "patmchgf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
          APPEND    " 186 UC1-3,4-9,10-12,13-21,104-111",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has 
.  to be repeated with for each individual index
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC22-51,1-3,4-9,10-12,13-21,104-111",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC1-3,4-9,10-12,54-56,104-111,13-21",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC13-21,1-3,4-9,10-12,104-111",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           OPEN      PATMCHG1,NEWFILE
.
CMCH9999   RETURN
+
.*****************************************************************************
.         Opening v10mchgf file after being copied
.*****************************************************************************
OMCH0000  CLEAR     DIM60
          APPEND    "v10mchgf",DIM60
          APPEND    SP70,DIM60
          RESET     DIM60
.
          COMPARE   THREE,OPTION
          GOTO      OMCH2000 IF EQUAL       * Oracle
.
          EXECUTE   "ldat v10mchgf.dat > temp.txt",TASKID
          CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,OMCH9000
          MOVE      DEFPATH,OLDPATH
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "v10mchgf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
OMCH2000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDPTMCH,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open v10mchgf file.  ";
            CALL      HITENTER
            GOTO      OMCH9999
          ENDIF
          GOTO      OMCH9999
.
OMCH9000  MOVE      ONE,OVRCD
OMCH9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
READ0000  CALL      OMCH0000                      * Open v10mchgf file
          BRANCH    OVRCD,READ9000
.
          DISPLAY   *P1:24,*EL;
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATMCHG1,"patmchgf"
          TRAPCLR   IO
          BRANCH    OVRCD,READ8000       * new file does not exist
.
          DISPLAY   *P1:20,*EL,"Record No. : ";
.
          MOVE      ZERO,CLNO
          PACK      KEY34,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ5000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
          UNPACK    SP70,PTMCHFTT,PTMCETDT   * clear new fields
          UNPACK    SP70,PTMCSPAR
          MOVE      SP70,KEY29
          PACK      KEY14,MHFUND,MHFTAB,SP20
          MATCH     SP70,KEY14
          GOTO      READ2000 IF EQUAL
.
          CALL      RDFUND1
          BRANCH    OVRCD,READ3500             * Invalid health fund
.                                              * find correct "Table Type"
          MOVE      PTHFBCAT,PTMCHFTT          * insert new "Table Type"
.                                              * Pack key here
READ2000  PACK      KEY29,MCLMCOD,MHFUND,PTMCHFTT,MCHARGE,PTMCEFDT,SP70
          CALL      RDAMCHG1
          BRANCH    OVRCD,READ4000
.
READ3000  CALL      PRLN0000                   * Print duplicate record
          GOTO      READ1000
.
READ3500  CALL      PRLD0000                   * Print missing record
          GOTO      READ1000
.
READ4000  CALL      WRMCHG1                    * write to new file
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      READ1000                      * get next record
.
.         Workout the end date of effective date
.
READ5000  MOVE      SP8,SAVEDATE
          PACK      SAVKEY21,SP70
          PACK      KEY29,Z70
          CALL      RDSMCHG1                   * position at start
READ5100  CALL      RDPMCHG1                   * read previous record
          BRANCH    OVRCD,READ6000
          PACK      KEY21,MCLMCOD,MHFUND,PTMCHFTT,MCHARGE,SP20
.
          MATCH     SP70,SAVKEY21
          GOTO      READ5800 IF EQUAL          * first time
.
          MATCH     KEY21,SAVKEY21
          GOTO      READ5800 IF NOT EQUAL      * first record of the combination
.
          MOVE      SAVEDATE,WORKDATE
          SUB       ONE,WORKDATE
.
          MOVE      WORKDATE,PTMCETDT          * move info to Effective To date
          CALL      UPMCHG1
.
READ5800  MOVE      PTMCEFDT,SAVEDATE           * save start date
          MOVE      KEY21,SAVKEY21
          GOTO      READ5100
.
READ6000  CLOSE     PATMCHG1                      * close new file
          CLOSE     OLDPTMCH                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
          MOVE      ZERO,EXIT
          GOTO      READ9999
.
READ8000  DISPLAY   *P1:23,*EL,"New Miscellaneous Charge file does not exist"
          CALL      HITENTER
.
READ9000  MOVE      ONE,EXIT
READ9999  RETURN
+
.**********************************************************************
.         Print duplicate record
.**********************************************************************
PRLN0000  COMPARE    "50",CLNO
          CALL       PRHD0000 IF NOT LESS
.
          PRINT      *1,MCLMCOD,*5,MHFUND,"/",MHFTAB,*21,PTMCHFTT,*25,MCHARGE:
                     *35,MMSCGRP,39,MPATPOR,*54,MHFPOR,*70,PTMCGSTC:
                     *74,MDESC
          ADD        ONE,CLNO
PRLN9999  RETURN
+
.**********************************************************************
.         Print missing record
.**********************************************************************
PRLD0000  COMPARE    "50",CLNO
          CALL       PRHD0000 IF NOT LESS
.
          PRINT      *1,"Health Fund/Table not found in patfn1af: ":
                     *50,MHFUND,"/",MHFTAB
          ADD        ONE,CLNO
PRLD9999  RETURN
+
.**********************************************************************
.         Print Heading
.**********************************************************************
PRHD0000  MOVE       ZERO,CLNO
.
          CALL       HEAD132
          PRINT      *1,"DUPLICATE RECORDS (SAME TABLE TYPE) DISPLAYED ON ":
                    "THIS REPORT WILL NOT BE WRITTEN TO THE MISC CHARGES FILE.":
                     *N
          PRINT      *1,"Claim",*5,"Health Fund",*21,"Type",*25,"Item":
                     *35,"Group",39,"Pat Amount",*54,"Rebate Amt":
                     *70,"GST Code",*79,"Description"
          CALL       UND132
          ADD        THREE,CLNO
PRHD9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
VALD0000  SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        compress v10mchgf file                                      *
.**********************************************************************
ENDP0000  COMPARE   THREE,OPTION
          GOTO      ENDP8000 IF EQUAL    * Oracle
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "v10mchgf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "compress -f ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          DISPLAY   *P1:24,*EL,"Compressing v10mchgf file.. ":
                    "Please wait !!  ";
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"v10mchgf file not compressed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
ENDP8000  DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.         Get the default directory
.**********************************************************************
DEFT0000  PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding 'patmchgf'.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
DEFT9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDPTMCH,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDPTMCH
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
            ELSE
              DISPLAY   "has caused an IO error - ";
            ENDIF
          ENDIF
          DISPLAY   "File will be bypassed"
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: MAIN0000 *
.*                                Select Option                               *
.******************************************************************************
.
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          HLINE     *G37,2,57,80
          DISPLAY   *P40:4,*EF:
                    *P1:3,*V2LON,"0",*HOFF," = Exit":
                    *P1:4,*V2LON,"1",*HOFF:
           " = Copy Current Miscellaneous Charge file to v10mchgf (CISAM only)":
                    *P1:5,*V2LON,"2",*HOFF:
                    " = Update the New Miscellaneous Charge file (CISAM)":
                    *P1:6,*V2LON,"3",*HOFF:
                    " = Update the New Miscellaneous Charge file (ORACLE)":
                    *P1:8,"Select Option : ";
.
OPTN1000  KEYIN     *P17:8,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000:
                           OPTN9000:
                           OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
READSOLD  RESET     KEY34
          READ      OLDPTMCH,KEY34;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDPTMCH;MCLMCOD,MHFUND,MHFTAB,MCHARGE,MDESC:
                             MITMTYP,MMSCGRP,MPATPOR,MHFPOR,MNINV:
                             MINDIC,PTMCGSTA,PTMCGSTC,PTMCEDIE:
                             PTMCCFCY,PTMCKREB,PTMCCOBD,PTMCTACT:
                             PTMCTACD,PTMCEFDT,PTMCALGR,PTMCPRTR:
                             PTMCECEQ,PTMCECTS,MCSPARE
          GOTO      OVERCOND IF OVER
          MOVE      MCSPARE,PTMCACTV
          RETURN
.
RDSMCHG1  RESET     KEY29
          READ      PATMCHG1,KEY29;;
          RETURN
.
RDPMCHG1  MOVE      ZERO,OVRCD
          READKP    PATMCHG1;MCLMCOD,MHFUND,PTMCHFTT,MCHARGE,MDESC:
                             MITMTYP,MMSCGRP,MPATPOR,MHFPOR,MNINV:
                             MINDIC,PTMCGSTA,PTMCGSTC,PTMCEDIE:
                             PTMCCFCY,PTMCKREB,PTMCCOBD,PTMCTACT:
                             PTMCTACD,PTMCEFDT,PTMCALGR,PTMCPRTR:
                             PTMCECEQ,PTMCECTS,PTMCACTV:
                             PTMCETDT,PTMCSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
WRMCHG1   RESET     KEY29
          WRITE     PATMCHG1,KEY29;MCLMCOD,MHFUND,PTMCHFTT,MCHARGE,MDESC:
                                   MITMTYP,MMSCGRP,MPATPOR,MHFPOR,MNINV:
                                   MINDIC,PTMCGSTA,PTMCGSTC,PTMCEDIE:
                                   PTMCCFCY,PTMCKREB,PTMCCOBD,PTMCTACT:
                                   PTMCTACD,PTMCEFDT,PTMCALGR,PTMCPRTR:
                                   PTMCECEQ,PTMCECTS,PTMCACTV:
                                   PTMCETDT,PTMCSPAR
          RETURN
.
UPMCHG1   MOVE      ZERO,OVRCD
          UPDATE    PATMCHG1;MCLMCOD,MHFUND,PTMCHFTT,MCHARGE,MDESC:
                             MITMTYP,MMSCGRP,MPATPOR,MHFPOR,MNINV:
                             MINDIC,PTMCGSTA,PTMCGSTC,PTMCEDIE:
                             PTMCCFCY,PTMCKREB,PTMCCOBD,PTMCTACT:
                             PTMCTACD,PTMCEFDT,PTMCALGR,PTMCPRTR:
                             PTMCECEQ,PTMCECTS,PTMCACTV:
                             PTMCETDT,PTMCSPAR
          RETURN
.
RDAMCHG1  MOVE      ZERO,OVRCD
          RESET     KEY29
          READ      PATMCHG1,KEY29;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
.
          INC       PATFN1IO/INC
          INC       STD001IO
