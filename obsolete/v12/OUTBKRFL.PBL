.******************************************************************************
.* System         : Outpatients                                               *
.*                                                                            *
.* Procedure Name : OUTBKRFL.PBL                                              *
.*                                                                            *
.* Function       : To enter a booking by displaying all the Referral Letters *
.*                  for the current clinic.                                   *
.*                                                                            *
.* Author         : Justin Coates      04/03/1992                             *
.*                                                                            *
.* Parameters     : VCLIN         the clinic/doctor code                      *
.*                  VTYPE         the clinic type                             *
.*                                                                            *
.* Returns        : EXIT = 1      no letter chosen                            *
.*                  EXIT = 0      letter chosen                               *
.*                  VURNO         UR number                                   *
.*                  VOUTNO        OP number                                   *
.*                  VNAME         Patient name                                *
.*                  VAGE          Patient age                                 *
.*                  REFSRCR       source of referral                          *
.*                  REFPRTY       priority                                    *
.*                  REFDIAG       diagnosis                                   *
.*                  REFDEPT       department                                  *
.*                  REFDATE       referral date                               *
.*                  REFECRN       ECR approval number                         *
.*                  REFGPFH       GPFH approval number                        *
.*                  REFGPCD       GP                                          *
.*                  REFGPPC       GP Practice                                 *
.*                  REFGPPT       GP Practice count                           *
.*                  REFRANK       Referral Patient Rank                       *
.*                                                                            *
.* Modifications  :                                                           *
.*        V5.09.B01 05/01/2001  Greg Horvat                                   *
.*                  Replaced certain variables with KEY?? variables cause     *
.*                  OUT66 couldnt compile                                     *
.*                  03/04/00  J Habasque SRF 646569 & 646607                  *
.*                  Added check of OTRLTREF                                   *
.*                  22/11/99 J.Tan                                            *
.*                  Added U/R number option                                   *
.*                  28/03/1998  Steve Armstrong  506 Mods                     *
.*                  Changed referral source to default from OTRLSRCR instead  *
.*                  of OTCNLSRF                                               *
.******************************************************************************
.*                  13/05/92  Steve O'Gorman                                  *
.*                  Check if Ref. Letter is booked                            *
.*                  Added save of depatment                                   *
.*                  29/07/92 J.Tan   SRF 116215                               *
.*                  Fixed displaying details for more than one page           *
.*                  20/08/92 J.Tan   SRF 116402                               *
.*                  Mods to display (M)ore option on second screen if required*
.*                  09/02/93 Whirlpool                                        *
.*                  Added saving of more refferal details so they will default*
.*                  05/04/1994  Steve Armstrong                               *
.*                  Mods for PATCODKY to PATCODKY                             *
.*                  28/07/1994  ROD                                           *
.*                  added check on nzibvnew - haven't select pat off Nat sys. *
.*                                                                            *
.******************************************************************************
.
          DEFPROC   OUTBKRFL
.
LETTER    DIM       24[12]
.
OUTBKRFL  COMPARE   ONE,OTCNULET
          GOTO      BKRFL900 IF NOT EQUAL   * not using ref. letters
.
          MOVE      SP30,KEY30
BKRFL010  MOVE      ZERO,URSLFLAG
          DISPLAY   *P1:24,"By (",*V2LON,ANSU,*HOFF,")/R Number, (":
                    *V2LON,ANSP,*HOFF,")riority ? ",*EL;
          
BKRFL012  KEYIN     *P32:24,*DV,UNDLN1,*P32:24,*V2LON,ANS;
          BRANCH    ABORT,BKRFL900          * abort
          RESET     ANS
          GOTO      BKRFL910 IF EOS         * exit
.
          REP       UPPLOW,ANS
          MATCH     ANSU,ANS
          GOTO      BKRFL020 IF EQUAL       * U/R Number entered
          MATCH     ANSP,ANS
          GOTO      BKRFL030 IF EQUAL       * Priority entered
          BEEP
          GOTO      BKRFL012
.
BKRFL020  MOVE      ONE,URSLFLAG
          CALL      GETU0000                * Get the U/R number
          BRANCH    ABORT,BKRFL910          * Abort
          BRANCH    EXIT,BKRFL010           * Nothing entered
          GOTO      BKRFL040
.
BKRFL030  CALL      KBPTY000                * enter priority
          BRANCH    EXIT,BKRFL010:          * nothing entered
                         BKRFL910           * exitchar entered
.
BKRFL040  CALL      TYPCL000                * clinic Type or Id Option
          BRANCH    EXIT,BKRFL010
.
BKRFL050  DISPLAY   *P1:9,*EF,*V2LON,*ULON:
                    *P4:10,"Date of Letter":
                    *P19:10,"U/R No. ":
                    *P28:10,"Patients Name            ":
                    *P56:10,"Diagnosis            "
.
          CALL      CLRBA000                * clear storage variables
          MOVE      ONE,CPAGENO             * set page
          MOVE      "10",CVERT              * starting line
          MOVE      ZERO,CNTITEM            * clear count
.
          IF        CTYPIDX = 0
            OPEN      OUTRF1A5,"outrf1af"     * open file
            PACK      KEY22,VCLIN,SP30
            CALL      RSOTRFL5
          ELSE
            OPEN      OUTRF1A7,"outrf1af"     * open file
            PACK      KEY22,VTYPE,SP30
            CALL      RSOTRFL7
          ENDIF
.
.
. ******* loop over file *******
.
. ------- this code assumes it is ok to make a referral from one site
. ------- and be able to book into a different site
.
BKRFL100  IF        CTYPIDX = 0
            CALL      RKOTRFL5
            BRANCH    OVRCD,BKRFL500          * end of file
.
            MATCH     VCLIN,OTRLCONS
            GOTO      BKRFL500 IF NOT EQUAL   * different consultant
          ELSE
            CALL      RKOTRFL7
            BRANCH    OVRCD,BKRFL500          * end of file
.
            MATCH     VTYPE,OTRLCTYP
            GOTO      BKRFL500 IF NOT EQUAL   * different consultant
          ENDIF
.
          MATCH     OTRLDATE,VDATE
          GOTO      BKRFL100 IF LESS        * letter date is after clinic date
.
          COMPARE   ZERO,OTRLTREF           * only display outpatient referrals
          GOTO      BKRFL100 IF NOT EQUAL
.
          IF        URSLFLAG =1
            MATCH     OBAURNO,OTRLURNO
            GOTO      BKRFL100 IF NOT EQUAL   * different U/R number
          ELSE
            MATCH     CODEPRTY,OTRLPRIO
            GOTO      BKRFL100 IF NOT EQUAL   * different priority
          ENDIF
.
          MATCH     SP6,OTRLBSIT
          GOTO      BKRFL100 IF NOT EQUAL   * letter already booked
.
. ******* increment counters *******
.
BKRFL200  ADD       ONE,CVERT
          COMPARE   "23",CVERT
          GOTO      BKRFL400 IF NOT LESS    * need a new page
          ADD       ONE,CNTITEM             * add to item counter
.
. ******* display data and store key *******
.
BKRFL300  CALL      GPATR000                * get patient details
          DISPLAY   *P1:CVERT,*V2LON,CNTITEM,*HOFF,".  ",CPCDATE:
                    *P19:CVERT,OTRLURNO,SP1,KEY25:
                    SP3,*+,KEY30
          IF        CTYPIDX = 0
            PACK      KEY22,OTRLCONS,OTRLDATE,OTRLOUTN
          ELSE
            PACK      KEY22,OTRLCTYP,OTRLDATE,OTRLOUTN
          ENDIF
          MOVE      KEY22,LETTER[CNTITEM]
          GOTO      BKRFL100
.
. ******* new screen needed *******
.
BKRFL400  BRANCH    CPAGENO,BKRFL450
.
.         Middle pages : (N)ext & (F)irst
.
BKRFL410  MOVE      ONE,CALLPOSN            * set position
          CALL      KYRFA000
          BRANCH    EXIT,BKRFL700,BKRFL050,OUTBKRFL
.                        More     First    Nothing
          GOTO      BKRFL600
.
.         first page : (N)ext
.
BKRFL450  MOVE      TWO,CALLPOSN
          CALL      KYRFA000
          BRANCH    EXIT,BKRFL700,BKRFL590,OUTBKRFL
.                        More     First    Nothing
          GOTO      BKRFL600
.
. ******* no more data to display *******
.
BKRFL500  COMPARE   ZERO,CNTITEM
          GOTO      BKRFL890 IF EQUAL       * no data displayed
.
          BRANCH    CPAGENO,BKRFL550
.
.         last page : (F)irst
.
BKRFL510  MOVE      THREE,CALLPOSN
          CALL      KYRFA000
          BRANCH    EXIT,BKRFL590,BKRFL050,OUTBKRFL
.                        More     First    Nothing
          GOTO      BKRFL600
.
.         first page :
.
BKRFL550  MOVE      FOUR,CALLPOSN
          CALL      KYRFA000
          BRANCH    EXIT,BKRFL590,BKRFL590,OUTBKRFL
.                        More     First    Nothing
          GOTO      BKRFL600
.
BKRFL590  BEEP   
BKRFL591  BRANCH    CALLPOSN,BKRFL410,BKRFL450,BKRFL510,BKRFL550
.
. ******* item on screen selected *******
.
BKRFL600  MOVE      LETTER[FORM2],KEY22
          IF        CTYPIDX = 0
            CALL      RDOTRFL5
          ELSE
            CALL      RDOTRFL7
          ENDIF
          BRANCH    OVRCD,BKRFL590          * invalid letter
.
          CALL      GPATR000                * get the patient details
          MOVE      PURNO,VURNO             * set return UR number
          MOVE      PURNO,OBAURNO           * set return UR number
          MOVE      KEY25,VNAME             * set name
          MOVE      PSAGE,VAGE              * set age
          MOVE      OTRLOUTN,VOUTNO         * set OP number
.
.         506 Mods - Changed to user referral letter referral source as
.         default referral source instead of parameter OTCNLSRF if OTRLSRCE
.         is not blank
.
          MATCH     SP3,OTRLSRCE
          IF        !@EQUAL
            MOVE      OTRLSRCE,REFSRCR      * source of referral
          ELSE
            MOVE      OTCNLSRF,REFSRCR      * source of referral
          ENDIF
          MOVE      CODEPRTY,REFPRTY        * priority
          MOVE      OTRLDIAG,REFDIAG        * diagnosis
          MOVE      OTRLDEPT,REFDEPT        * department
          MOVE      OTRLDATE,REFDATE        * referral date
          MOVE      OTRLECRN,REFECRN        * ECR approval number
          MOVE      OTRLGPFH,REFGPFH        * GPFH approval number
          MOVE      OTRLRFGP,REFGPCD        * GP
          MOVE      OTRLGPPC,REFGPPC        * GP Practice
          MOVE      OTRLGPPT,REFGPPT        * GP Practice count
          MOVE      OTRLRANK,REFRANK
          MOVE      ZERO,NZIBVNEW           * pat not selected off National Sys.
          GOTO      BKRFL900
.
. ******* set up for a new page *******
.
BKRFL700  MOVE      LETTER[LASTLETT],KEY22
          IF        CTYPIDX = 0
            CALL      RDOTRFL5                * position on last item
          ELSE
            CALL      RDOTRFL7                * position on last item
          ENDIF
BKRFL750  IF        CTYPIDX = 0
            CALL      RKOTRFL5                * get to first item next page
            BRANCH    OVRCD,BKRFL500
.
            MATCH     VCLIN,OTRLCONS
            GOTO      BKRFL500 IF NOT EQUAL   * different consultant
          ELSE
            CALL      RKOTRFL7                * get to first item next page
            BRANCH    OVRCD,BKRFL500
.
            MATCH     VTYPE,OTRLCTYP
            GOTO      BKRFL500 IF NOT EQUAL   * different consultant
          ENDIF
.
          MATCH     OTRLDATE,VDATE
          GOTO      BKRFL750 IF LESS        * letter date is after clinic date
.
          IF        URSLFLAG =1
            MATCH     OBAURNO,OTRLURNO
            GOTO      BKRFL750 IF NOT EQUAL   * different U/R number
          ELSE
            MATCH     CODEPRTY,OTRLPRIO
            GOTO      BKRFL750 IF NOT EQUAL   * different priority
          ENDIF
.
          MATCH     SP6,OTRLBSIT
          GOTO      BKRFL750 IF NOT EQUAL   * letter already booked
.
. there is a valid record for the next screen
.
          CALL      CLRBA000                * clear storage variables
          ADD       ONE,CPAGENO             * add to page counter
          MOVE      ONE,CNTITEM             * reset item counter
          MOVE      "11",CVERT              * set row
          DISPLAY   *P1:CVERT,*EF           * clear screen
          GOTO      BKRFL300
.
. ******* no letters to display *******
.
BKRFL890  DISPLAY   *P1:24,*B,"There are no Referral letters.  ",*EL;
          CALL      HITENTER 
          GOTO      OUTBKRFL
.
.         set exit flags
.
BKRFL900  MOVE      ZERO,EXIT
          GOTO      BKRFL950
.
BKRFL910  MOVE      ONE,EXIT
          DISPLAY   *P1:8,*EF;
.
BKRFL950  GOTO      BKRFL999
+
.*********************************************************************
.*                  KBPTY000                    Called by : OUTBKRFL *
.*        Enter the priority for the selected Letter                 *
.*        Returns : EXIT = 1      no priority entered                *
.*********************************************************************
KBPTY000  DISPLAY   *P1:8,*EF,"Priority    : ",*EL;
          MOVE      "8",EVERT
          MOVE      "16",ECOL
          PACK      CODE,ANSP,ANSR          * category
          MOVE      ZERO,CKYIMAND           * isn't mandatory
          MOVE      SP3,CKYIDEF3            * no default
.
          CALL      PATCODKY
          BRANCH    EXIT,KBPTY999:          * nothing
                         KBPTY999           * exitchar
.
          MOVE      ACODE,CODEPRTY          * code
          DISPLAY   *P16:8,*V2LON,CODEPRTY,*HOFF,SP2,TDESC,*EL;
.
KBPTY999  RETURN
+
.*********************************************************************
.*                  KYRFA000                    Called by :          *
.*        Keyin response for line 24 question                        *
.*        Para's  : CALLPOSN      which line 24 prompt               *
.*        Returns : EXIT = 0      item selected (FORM2)              *
.*                  EXIT = 1      Next screen selected               *
.*                  EXIT = 2      First screen selected              *
.*                  EXIT = 3      Nothing entered                    *
.*********************************************************************
KYRFA000  DISPLAY   *P1:24,*EL,"Select Letter";
          MOVE      "14",CCOL
          IF        (CALLPOSN = ONE)
            DISPLAY   *P14:24,", (",*V2LON,"M",*HOFF,")ore, (":
                      *V2LON,"F",*HOFF,")irst ? "
            ADD       "22",CCOL
          ENDIF
.
          IF        (CALLPOSN = TWO)
            DISPLAY   *P14:24,", (",*V2LON,"M",*HOFF,")ore ? "
            ADD       "12",CCOL
          ENDIF
.
          IF        (CALLPOSN = THREE)
            DISPLAY   *P14:24,", (",*V2LON,"F",*HOFF,")irst ? "
            ADD       "13",CCOL
          ENDIF
.
          IF        (CALLPOSN = FOUR)
            DISPLAY   *P14:24," ? "
            ADD       "4",CCOL
          ENDIF
.
KYRFA100  KEYIN     *PCCOL:24,*DV,UNDLN2:
                    *PCCOL:24,*V2LON,*JR,DIM2:
                    *PCCOL:24,*DV,DIM2
.
          RESET     DIM2
          GOTO      KYRFA430 IF EOS         * nothing entered
.
          TYPE      DIM2
          GOTO      KYRFA500 IF EQUAL       * number entered
.
.         letter entered
.
          MATCH     SP1,DIM2
          GOTO      KYRFA150 IF NOT EQUAL   * invalid entry
.
          UNPACK    DIM2,ANS,ANS            * get required letter in ANS
          REP       UPPLOW,ANS              * get as uppercase
          REP       "M1F2",ANS              * turn valid letters into number
          MOVE      ZERO,EXIT               * clear exit flag
          MOVE      ANS,EXIT                * set exit flag
          BRANCH    EXIT,KYRFA999,KYRFA999,KYRFA150,KYRFA999
.
KYRFA150  BEEP
          GOTO      KYRFA100
.
KYRFA430  MOVE      THREE,EXIT
          GOTO      KYRFA999
.
.         number entered
.
KYRFA500  MOVE      ZERO,EXIT               * set exit flag
          MOVE      DIM2,FORM2              * get as a number
.
          COMPARE   FORM2,ZERO
          GOTO      KYRFA150 IF NOT LESS    * negative entered
.
          COMPARE   FORM2,CNTITEM
          GOTO      KYRFA150 IF LESS        * number too large
.
KYRFA999  RETURN
+
.*********************************************************************
.*                  CLRBA000                    Called by :          *
.*        Clear the storage variables                                *
.*********************************************************************
CLRBA000  MOVE      LASTLETT,FORM2          * set last element number
          WHILE     FORM2
            MOVE      SP20,LETTER[FORM2]
            SUB       ONE,FORM2
          DO
.
CLRBA999  RETURN
+
.*********************************************************************
.*                  GPATR000                    Called by : xxxx0000 *
.*        Get the patient name                                       *
.*********************************************************************
GPATR000  MATCH     ZEROUR,OTRLURNO
          IF        @EQUAL
            MOVE      OTRLOUTN,KEY8
            CALL      RDPRAM1
            MOVE      ZEROUR,PURNO          * clear the UR number
          ELSE
            MOVE      OTRLURNO,KEY8
            CALL      RDMAST1
          ENDIF
.
          IF        OVRCD = ZERO
            MOVE      PSNAME,PACSNAME
            MOVE      PGNAME,PACGNAME
            MOVE      PTITL,PACTITLE
            MOVE      TWO,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,KEY25
          ELSE
            MOVE      "No PMI details",KEY25
          ENDIF
.
          UNPACK    OTRLDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      OTRLDIAG,KEY30          * shorten diagnosis
          SETLPTR   KEY30,21                * set display length
.
GPATR999  RETURN
.*********************************************************************
.*                  TYPCL000                    Called by : xxxx0000 *
.*        Clinic Type or Id Option                                   *
.*********************************************************************
TYPCL000  DISPLAY   *P1:24,"Display Clinic (",*V2LON,ANST,*HOFF,")ype or":
                    " Clinic (",*V2LON,ANSI,*HOFF,")d Referral Letters ? ",*EL;
          
TYPCL100  KEYIN     *P57:24,*DV,UNDLN1,*P57:24,*V2LON,ANS;
          RESET     ANS
          GOTO      TYPCL950 IF EOS         * exit
.
          REP       UPPLOW,ANS
          MATCH     ANST,ANS
          GOTO      TYPCL200 IF EQUAL       * U/R Number entered
.
          MATCH     ANSI,ANS
          GOTO      TYPCL300 IF EQUAL       * U/R Number entered
.
          BEEP
          GOTO      TYPCL100
.
TYPCL200  MOVE      ONE,CTYPIDX
          GOTO      TYPCL900
.
TYPCL300  MOVE      ZERO,CTYPIDX
.
TYPCL900  MOVE      ZERO,EXIT
          GOTO      TYPCL999
.
TYPCL950  MOVE      ONE,EXIT
TYPCL999  RETURN
+
BKRFL999  ENDPROC
...........................................................................
