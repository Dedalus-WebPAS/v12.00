.******************************************************************************
.* System         : Outpatients                                               *
.*                                                                            *
.* Include Name   : OTAUDXSC.PBL                                              *
.*                                                                            *
.* Function       : Maintain the Extra Screen Details file audits.            *
.*                                                                            *
.* Author         : Justin Coates                                             *
.*                                                                            *
.* Date Written   : 05/02/1992                                                *
.*                                                                            *
.* Index used     : OUTAUDXS,"outaudxs"                                       *
.*                                                                            *
.* Parameters     : HOAUDX        Audits being used; 1 = No; 0 = Yes          *
.*                  AUDIFLAG      which type of audit to do                   *
.*                     1 = 'ADD' audit                                        *
.*                     2 = 'BEFORE CHANGE' audit                              *
.*                     3 = 'AFTER CHANGE' audit                               *
.*                     4 = 'DELETE' audit                                     *
.*                     5 = delete 'before change' audit                       *
.*                                                                            *
.* Modifications  :                                                           *
.******************************************************************************
.
OTAUDXSC  BRANCH    HOAUDX,OTAXS999         * audits not on
.
          COMPARE   FIVE,AUDIFLAG
          GOTO      OTAXS700 IF EQUAL       * delete before change audit
.
          COMPARE   THREE,AUDIFLAG
          GOTO      OTAXS200 IF EQUAL       * after change - leave date/time
.
.         get the current date and time
.
          CALL      IBACLOCK                * get date and time
          PACK      OTXSAUDD,CCC,CYY,CMM,CDD
          REP       " 0",OTXSAUDD           * date
          MOVE      CTIMEIS,OTXSAUDT        * time
.
OTAXS200  MOVE      PORT,OTXSAUDP           * port
          MOVE      AUDIFLAG,OTXSAUDR       * copy audit type
          REP       "1A2B3C4D",OTXSAUDR     * get the correct letter
          MOVE      ONE,OTXSAUDS            * set as not printed
          MOVE      PASSCODE,OTXSAUDO       * operator id
.
          PACK      KEY19,OTXSAUDD,OTXSAUDT,OTXSAUDP,OTXSAUDR
          CALL      AWOTXSC                 * write the audit
          GOTO      OTAXS999
.
.         delete the before change audit
.
OTAXS700  PACK      KEY19,OTXSAUDD,OTXSAUDT,OTXSAUDP,ANSB
          CALL      ADOTXSC                 * delete the audit
.
OTAXS999  RETURN
+
