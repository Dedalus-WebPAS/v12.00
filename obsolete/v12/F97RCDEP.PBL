.***************************************************************************
.* System    :   Receipting                                                *
.* Program   :   F97RCDEP.PBL                                              *
.* Desc      :                                                             *
.***************************************************************************
.* Date      :   08/03/2007                                                *
.* Author    :   J.Tan       CAR 133052                                    *
.* Function  :   This program will Load Deposits from a text file          *
.*               to Receipting files                                       *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       COMDEPFD/INC  
          INC       PATVISFD/INC
          INC       PATMA1FD/INC
          INC       PMSVX1FD/INC
          INC       RCPBNKFD/INC  
          INC       RCPDTEFD/INC  
          INC       RCPBDTFD/INC  
          INC       RCPREGFD/INC  
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
ITEMFIL1  FILE      ASCII,FIXED=256
ITEMFILE  DIM       80
.
.Name     Type   Length   Start   Description
.----     ----   ------   -----   -----------
XADMNO    DIM       8        1     Admission Number
DXAMNT    DIM      15        9     Amount
XTDATE    DIM       8       24     Transaction date
XRECPT    DIM      12       32     Receipt Number
XPTYPE    DIM       1       44     Payment Method
XPDET1    DIM      30       45     Payment details 1
XPDET2    DIM      30       75     Payment details 2
XPDET3    DIM      30      105     Payment details 3
XDTYPE    DIM       3      135     Deposit type
. End of Record            138
XAMOUNT   FORM     12.2
.
TMPFILNM  DIM       8
TEMPFILE  IFILE     SQL,FIXED=122, KEY="UC1-12"
.
.Name     Type   Length   Physical   Start   Description
.----     ----   ------   --------   -----   -----------
TMPRECP   DIM      12       12         1     Receipt Number
TMPAMNT   FORM     12.2      8         13    Total payment
TMPDATE   DIM       8        8         21    Transaction date
TMPDET1   DIM      30       30         29    Payment detail 1
TMPDET2   DIM      30       30         59    Payment detail 2
TMPDET3   DIM      30       30         89    Payment detail 3
TMPPTYP   DIM       3        3        119    Payment Method
. End of Record                       122
.
CURDATE   DIM       8
CMDLINE   DIM       80
CASH      INIT      "CASH"
CHEQUE    INIT      "CHEQUE"
CREDIT    INIT      "CREDIT CARD"
.
FORM3     FORM      3
FORM12    FORM      12
.
PRGID     INIT      "F97RCDEP"
PRGNAM    INIT      "Automated Upload of Deposits "
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      GFIL0000               * Get file
          BRANCH    EXIT,ML0100,ML0100
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML1000:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
          CALL      CLER0000               * Clear tempfile
          CALL      KILL0000               * Kill tempfile
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      RCPBNKA1,"rcpbnkaf"
          OPEN      RCPBDTA1,"rcpbdtaf"
          OPEN      RCPDTEA1,"rcpdteaf"
          OPEN      RCPREGA1,"rcpregaf"
          OPEN      COMDEPA1,"comdepaf"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run                                    *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Load Deposits"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               GFIL0000              Called by: ML0000  *
.**************************************************************************
GFIL0000  PACK      ITEMFILE,SP70,SP70
          KEYIN     *P1:10,"Path and File Name  : ",*P23:10,*RV,*DV,ITEMFILE:
                    *P23:10,*EL,*V2LON,ITEMFILE
          GOTO      GFIL9200 IF EOS
          ENDSET    ITEMFILE
          APPEND    SP70,ITEMFILE
          RESET     ITEMFILE
          MATCH     SP70,ITEMFILE
          GOTO      GFIL9200 IF EQUAL        * blank
.
          STRIP     ITEMFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      ITEMFIL1,ITEMFILE
          TRAPCLR   IO
          BRANCH    OVRCD,GFIL9000
.
          MOVE      ZERO,EXIT
          GOTO      GFIL9999
.
GFIL9000  DISPLAY   *P1:23,*EL,"File does not exist. "
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      GFIL9999
.
GFIL9200  MOVE      TWO,EXIT
GFIL9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.**************************************************************************
PROC0000  CALL      DEPREG00                  * Get deposit register
          MOVE      "60",CLNO
          CALL      CTMF0000                  * Create tempfile
.
PROC1000  CALL      RDITEMN1                  * read item record
          BRANCH    OVRCD,PROC4000
.
          MATCH     SP70,XADMNO
          GOTO      PROC2000 IF EQUAL         * blank adission number
.
          MOVE      XADMNO,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,PROC2000            * no visit record
.
          MOVE      PVIURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PROC2000            * no master record
.
          MOVE      XADMNO,KEY8
          CALL      RDPMVX11
          IF        OVRCD=1
            MOVE      SP70,PMVXMHOS
          ENDIF
.
          CALL      WDEP0000                  * write comdepaf
          CALL      WDTE0000                  * write to rcpdteaf
          CALL      WBDT0000                  * write to rcpbdtaf
.
          CALL      WTMP0000                  * write to a tempfile
          GOTO      PROC1000
.
PROC2000  UNPACK    XDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      ZERO,FORM1
          MOVE      XPTYPE,FORM1              * Payment type
          LOAD      KEY12,FORM1,CASH,CHEQUE,CREDIT
.
          COMPARE   "60",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *1,XADMNO,*10,XAMOUNT,*25,CPCDATE,*37,XRECPT:
                    *52,KEY12,*66,XDTYPE:
                    *N,*1,XPDET1,*35,XPDET2,*65,XPDET3
          ADD       TWO,CLNO
          GOTO      PROC1000
.
.         Loop through tempfile and write records to rcpbnkaf and rcpbdtaf
.
PROC4000  PACK      KEY12,SP70
          CALL      RSTEMP1
PROC4200  CALL      RKTEMP1
          BRANCH    OVRCD,PROC9999
.
          PACK      KEY12,TMPRECP,SP70
          CALL      RDRCBNK1
          COMPARE   ZERO,OVRCD
          GOTO      PROC5000 IF EQUAL
.
          CALL      WBNK0000                * write to rcpbnkaf
          GOTO      PROC4200
.
PROC5000  UNPACK    TMPDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      ZERO,FORM1
          MOVE      XPTYPE,FORM1              * Payment type
          LOAD      KEY12,FORM1,CASH,CHEQUE,CREDIT
.
          PRINT     *1,"**BANKING***",*10,TMPAMNT,*25,CPCDATE,*37,TMPRECP:
                    *52,KEY12:
                    *N,*1,TMPDET1,*35,TMPDET2,*65,TMPDET3
          GOTO      PROC4200
.
PROC9999  RETURN
+
.**************************************************************************
.*                               CDEP0000              Called by: PROC0000*
.**************************************************************************
WDEP0000  MOVE      ONE,FORM3
          PACK      KEY15,XRECPT,Z70
          CALL      RSCMDEP1
          CALL      RPCMDEP1
          BRANCH    OVRCD,WDEP2000
          MATCH     XRECPT,CMDERECN
          GOTO      WDEP2000 IF NOT EQUAL     * different receipt number
          MOVE      CMDETCNT,FORM3
          ADD       ONE,FORM3
.
WDEP2000  MOVE      FORM3,CMDETCNT            * Transaction Count
          MOVE      XRECPT,CMDERECN           * receipt number
          MOVE      XTDATE,CMDETDAT           * transaction date
          PACK      CMDEDTYP,XDTYPE,SP70      * Deposit Type (Cat DP)
          MOVE      SP70,CMDERRFN             * Reason for Refund (Cat DR)
          MOVE      SP70,CMDEREFD             * Refund Date (CCYYMMDD)
          MOVE      SP70,CMDEREFT             * Refund Time (hh:mm:ss)
          MOVE      PVIURNO,CMDEURNO          * U/R Number
          MOVE      XADMNO,CMDEADMN           * Admission Number
          MOVE      "1",CMDESFLG              * Scan Flag (0=Debtor, 1=PMI)
          MOVE      "3",CMDESYST              * Visit Type
          MOVE      SP70,CMDEPRAC             * Medical Practice
          MOVE      CURDATE,CMDECDAT          * Date Record Created (CCYYMMDD)
          CALL      IBACLOCK
          CLOCK     TIME,CMDECTIM             * Time Record Created (hh:mm:ss)
          MOVE      PASSCODE,CMDECUID         * User Id Created (websecaf)
.
          MOVE      SP70,CMDEUDAT             * Date Record Updated (CCYYMMDD)
          MOVE      SP70,CMDEUTIM             * Time Record Updated (hh:mm:ss)
          MOVE      SP70,CMDEUUID             * Web User Id Updated (websecaf)
          MOVE      SP70,CMDESPAR             * Spare
.
          PACK      KEY15,CMDERECN,CMDETCNT,SP70
          CALL      WRCMDEP1
WDEP9999  RETURN
+
.**************************************************************************
.*                               WDTE0000              Called by: PROC0000*
.**************************************************************************
WDTE0000  MOVE      ONE,FORM3
          PACK      KEY15,XRECPT,Z70
          CALL      RSRCDTE1
          CALL      RPRCDTE1
          BRANCH    OVRCD,WDTE2000
          MATCH     XRECPT,RCDTRECN
          GOTO      WDTE2000 IF NOT EQUAL     * different receipt number
          MOVE      RCDTTCNT,FORM3
          ADD       ONE,FORM3
.
WDTE2000  MOVE      FORM3,RCDTTCNT            * Transaction Count
          MOVE      XTDATE,RCDTTDAT           * Transaction Date (CCYYMMDD)
          MOVE      XRECPT,RCDTRECN           * Receipt Number
          MOVE      REGCODE,RCDTREGI          * Register
          MOVE      SP70,RCDTINVN             * Invoice Number
          MOVE      XADMNO,RCDTVIST           * Visit Number
          MOVE      PVIURNO,RCDTURNO          * U/R Number
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME                   * Format the patients name
          PACK      RCDTNAME,PACFNAME,SP70
.
          MOVE      PADD1,RCDTADD1            * Address Line 1
          MOVE      PADD2,RCDTADD2            * Address Line 2
          MOVE      PSUBRB,RCDTADD3           * Address Line 3
          MOVE      PADD4,RCDTADD4            * Address Line 4
          MOVE      PPOST,RCDTPOST            * Post Code
.
          MOVE      "1",RCDTSFLG              * Scan Flag
          MOVE      SP70,RCDTREFR             * Reference
          MOVE      SP70,RCDTINCA             * Income Account
          MOVE      SP70,RCDTBNKA             * Bank Account
          MOVE      XAMOUNT,RCDTAMNT          * Amount
          MOVE      ZERO,RCDTDISC             * Discount
          MOVE      PMVXMHOS,RCDTMHOS         * Multi Hospital Id (pathspaf)
          MOVE      SP70,RCDTMDHS             * Multi Database Hosp Id(pathospf)
          MOVE      SP70,RCDTGSTC             * GST Code
          MOVE      SP70,RCDTGSTA             * GST Account
          MOVE      SP70,RCDTALLO             * Allocated 0=No 1=Yes
.
          CALL      IBACLOCK
          MOVE      CURDATE,RCDTRELD          * Release Date (CCYYMMDD)
          CLOCK     TIME,RCDTRELT             * Release Time (hh:mm:ss)
          MOVE      SP70,RCDTPRAC             * Practice
          MOVE      CURDATE,RCDTCDAT          * Date Record Created (CCYYMMDD)
          CLOCK     TIME,RCDTCTIM             * Time Record Created (hh:mm:ss)
          MOVE      PASSCODE,RCDTCUID         * User Id Created (websecaf)
          MOVE      SP70,RCDTUDAT             * Date Record Updated (CCYYMMDD)
          MOVE      SP70,RCDTUTIM             * Time Record Updated (hh:mm:ss)
          MOVE      SP70,RCDTUUID             * Web User Id Updated (websecaf)
          PACK      RCDTDPTY,XDTYPE,SP70      * Deposit Type (Category DP)
          MOVE      SP70,RCDTSPAR             * Spare
.
          PACK      KEY15,RCDTRECN,RCDTTCNT,SP70
          CALL      WRRCDTE1
WDTE9999  RETURN
+
.**************************************************************************
.*                               WBNK0000              Called by: PROC0000*
.**************************************************************************
WBNK0000  MOVE      TMPRECP,RCBNRECN           * Receipt Number
          MOVE      TMPDATE,RCBNTDAT           * Transaction Date (CCYYMMDD)
          MOVE      TMPAMNT,RCBNTOTP           * Total Payment
          MOVE      SP70,RCBNCDRW              * Cash Drawer
          MOVE      SP70,RCBNMHOS              * Multi Hospital Id (pathspaf)
          MOVE      SP70,RCBNMDHS              * Multi Database Hosp Id
          MOVE      "0",RCBNTTYP               * Received From Type (Patient)
          MOVE      SP70,RCBNRCOD              * Received From Code
          MOVE      "2",RCBNSTAT               * Status (Banked)
.
          CALL      IBACLOCK
          MOVE      CURDATE,RCBNBDAT           * Bank Deposit Date (CCYYMMDD)
          CLOCK     TIME,RCBNBTIM              * Bank Deposit Time (hh:mm:ss)
          MOVE      CURDATE,RCBNRDAT           * Reconciliation Date (CCYYMMDD)
          CLOCK     TIME,RCBNRTIM              * Reconciliation Time (hh:mm:ss)
          MOVE      SP70,RCBNCHQA              * Cheque Account Number
          MOVE      CURDATE,RCBNCDAT           * Date Record Created (CCYYMMDD)
          CLOCK     TIME,RCBNCTIM              * Time Record Created (hh:mm:ss)
          MOVE      PASSCODE,RCBNCUID          * User Id Created (websecaf)
          MOVE      SP70,RCBNUDAT              * Date Record Updated (CCYYMMDD)
          MOVE      SP70,RCBNUTIM              * Time Record Updated (hh:mm:ss)
          MOVE      SP70,RCBNUUID              * Web User Id Updated (websecaf)
          MOVE      SP70,RCBNSPAR              * Spare
.
          PACK      KEY12,RCBNRECN,SP70
          CALL      WRRCBNK1
WBNK9999  RETURN
+
.**************************************************************************
.*                               WBDT0000              Called by: PROC0000*
.**************************************************************************
WBDT0000  MOVE      ONE,FORM3
          PACK      KEY15,XRECPT,Z70
          CALL      RSRCBDT1
          CALL      RPRCBDT1
          BRANCH    OVRCD,WBDT2000
          MATCH     XRECPT,RCBDRECN
          GOTO      WBDT2000 IF NOT EQUAL     * different receipt number
          MOVE      RCBDUNIQ,FORM3
          ADD       ONE,FORM3
.
WBDT2000  MOVE      FORM3,RCBDUNIQ            * Unique number
          MOVE      SP70,RCBDMHOS
          MOVE      SP70,RCBDMHOS             * Multi Hospital Id (pathspaf)
          MOVE      XTDATE,RCBDTDAT           * Transaction Date (CCYYMMDD)
          MOVE      XRECPT,RCBDRECN           * Receipt Number
          MOVE      SP70,RCBDMDHS             * Multi Database Hosp Id(pathospf)
          MOVE      XPTYPE,RCBDPTYP           * Payment Type
          MOVE      XAMOUNT,RCBDPAMT          * Payment Amount
.
          MOVE      XPDET1,RCBDPAYD           * Payee Details
          MOVE      SP70,RCBDCHQN             * Cheque Number/Money Order
          MOVE      SP70,RCBDDRAW             * Drawer
          MOVE      SP70,RCBDBANK             * Bank
          MOVE      SP70,RCBDBRCH             * Branch
.
          MATCH     "1",XPTYPE
          IF        !@EQUAL
            MOVE      XPDET1,RCBDDRAW         * Drawer
            MOVE      XPDET2,RCBDBANK         * Bank
            MOVE      XPDET3,RCBDBRCH         * Branch
          ENDIF
.
          MOVE      SP70,RCBDCRDT             * Card Type
          MOVE      SP70,RCBDSTRF             * Statement Reference
.>>>>     MOVE      SP70,RCBDMONM             * No longer used
          MOVE      SP70,RCBDEFTT             * EFT Type
          MOVE      CURDATE,RCBDCDAT          * Date Record Created (CCYYMMDD)
          CALL      IBACLOCK
          CLOCK     TIME,RCBDCTIM             * Time Record Created (hh:mm:ss)
          MOVE      PASSCODE,RCBDCUID         * User Id Created (websecaf)
.
          MOVE      SP70,RCBDUDAT             * Date Record Updated (CCYYMMDD)
          MOVE      SP70,RCBDUTIM             * Time Record Updated (hh:mm:ss)
          MOVE      SP70,RCBDUUID             * Web User Id Updated (websecaf)
          MOVE      SP70,RCBDSPAR             * Spare
.
          PACK      KEY15,RCBDRECN,RCBDUNIQ,SP70
          CALL      WRRCBDT1
WBDT9999  RETURN
+
.**************************************************************************
.*                               DEPREG00              Called by: PROC0000*
.**************************************************************************
DEPREG00  PACK      KEY3,SP70
          CALL      RDSREGA1
DEPREG10  CALL      RDKREGA1
          BRANCH    OVRCD,DEPREG99
          COMPARE   "97",REGIBACD
          GOTO      DEPREG10 IF NOT EQUAL       * Not deposit register
          GOTO      DEPREG99
.
DEPREG90  MOVE      SP70,REGCODE
DEPREG99  RETURN
+
.**************************************************************************
.*                               WTMP0000              Called by: PROC0000*
.**************************************************************************
WTMP0000  MOVE      XRECPT,KEY12
          CALL      RDTEMP1
          BRANCH    OVRCD,WTMP4000
.
          MOVE      XTDATE,TMPDATE
          MOVE      XPDET1,TMPDET1
          MOVE      XPDET2,TMPDET2
          MOVE      XPDET3,TMPDET3
          MOVE      XPTYPE,TMPPTYP
          ADD       XAMOUNT,TMPAMNT
          CALL      UPTEMP1
          GOTO      WTMP9999
.
WTMP4000  MOVE      XRECPT,TMPRECP
          MOVE      XTDATE,TMPDATE
          MOVE      XPDET1,TMPDET1
          MOVE      XPDET2,TMPDET2
          MOVE      XPDET3,TMPDET3
          MOVE      XPTYPE,TMPPTYP
          MOVE      XAMOUNT,TMPAMNT
          MOVE      TMPRECP,KEY12
          CALL      WRTEMP1
WTMP9999  RETURN
+
.*************************************************************************
.*                             CTMF0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
CTMF0000  CALL      KILL0000
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TMPFILNM,CMDLINE
          APPEND    " 122 UC1-12",CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,TMPFILNM
.
CTMF9999  RETURN
+
.*************************************************************************
.*                             KILL0000             Called by:           *
.*                   erase temporary file                                *
.*************************************************************************
KILL0000  CLOSE     TEMPFILE
          MOVE      "tmprcdep",TMPFILNM
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TMPFILNM,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.*************************************************************************
.*                             CLER0000            Called by:            *
.*               close and erase the temporary file                      *
.*************************************************************************
CLER0000  PACK      KEY12,SP70
          CALL      RSTEMP1
          CALL      RKTEMP1
          BRANCH    OVRCD,CLER9999
.
          PACK      KEY12,TMPRECP,SP70
          CALL      DETEMP1
.
          GOTO      CLER0000
.
CLER9999  RETURN
+
.**************************************************************************
.*                               HEAD0000              Called by: PROC0000*
.**************************************************************************
HEAD0000  CALL      HEAD132
          CALL      UND132
          PRINT     *1,"Adm No",*10,"Amount",*25,"Tran.Date",*37,"Receipt No":
                    *52,"Pay.Type",*66,"Deposit Type";
          CALL      UND132
          ADD       TWO,CLNO
HEAD9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
RDITEMN1  MOVE      ZERO,OVRCD
          READ      ITEMFIL1,SEQ;XADMNO,DXAMNT,XTDATE,XRECPT,XPTYPE:
                                 XPDET1,XPDET2,XPDET3,XDTYPE
          GOTO      OVERCOND IF OVER
          SQUEEZE   XRECPT
          MOVE      XRECPT,FORM12
          MOVE      FORM12,XRECPT
          MOVE      DXAMNT,XAMOUNT
          RETURN
.
RSTEMP1   RESET     KEY12
          READ      TEMPFILE,KEY12;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;TMPRECP,TMPAMNT,TMPDATE:
                             TMPDET1,TMPDET2,TMPDET3,TMPPTYP
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY12
          READ      TEMPFILE,KEY12;TMPRECP,TMPAMNT,TMPDATE:
                                   TMPDET1,TMPDET2,TMPDET3,TMPPTYP
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY12
          WRITE     TEMPFILE,KEY12;TMPRECP,TMPAMNT,TMPDATE:
                                   TMPDET1,TMPDET2,TMPDET3,TMPPTYP
          RETURN
.
DETEMP1   RESET     KEY12
          DELETE    TEMPFILE,KEY12
          RETURN
.
UPTEMP1   UPDATE    TEMPFILE;TMPRECP,TMPAMNT,TMPDATE:
                             TMPDET1,TMPDET2,TMPDET3,TMPPTYP
          RETURN
+
. =========================================================================
+
.
          INC       STD001IO
          INC       COMDEPIO/INC  
          INC       PATVISIO/INC
          INC       PATMA1IO/INC
          INC       PMSVX1IO/INC
          INC       RCPBNKIO/INC  
          INC       RCPDTEIO/INC  
          INC       RCPBDTIO/INC  
          INC       RCPREGIO/INC  
