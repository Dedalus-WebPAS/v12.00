.***************************************************************************
.* System    :   CIAS                                                      *
.* Program   :   IBACCC51                                                  *
.* Desc      :   CIAS export file generation                               *
.***************************************************************************
.* Date      :   11/03/2004                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will create a comma delimited export file    *
.*                                                                         *
.***************************************************************************
.* Mods      :   V9.04.02 16/11/2005 Ebon Clements         CAR 81409       *
.*                        Added patient surname and given name to export   *
.*                        file at episode level - WR04E000                 *
.*               V9.04.01 16/11/2005 Ebon Clements         CAR 71242       *
.*                        Changed OPEN0000 to also check if the file       *
.*                        exists in the web extract directory              *
.*               V9.03.01 17/03/2004 Ebon Clements         CAR 45489       *
.*                        Fixed call to ibaccc51.us1                       *
.*               V9.03.00 11/03/2004 Ebon Clements         CAR 45488       *
.*                        Created program from IBACIA51                    *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       CCSCEAFD/INC
          INC       CCSCEBFD/INC
          INC       CCSCECFD/INC
          INC       CCSCEDFD/INC
          INC       CCSCEEFD/INC
          INC       CCSCEFFD/INC
          INC       CCSCEGFD/INC
          INC       CCSCONFD/INC
          INC       CCSEPSFD/INC
          INC       CCSHCDFD/INC
          INC       CCSIADFD/INC
          INC       CCSSTADF
          INC       CCSSTYFD/INC
          INC       CCSXHDFD/INC
          INC       WEBCONFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COSTDESC  DIM       10
DESC1     DIM       127
DESC2     DIM       127
DESC3     DIM       127
CMDLINE   DIM       200
CSVEXT    INIT      ".csv"
DOWNFILE  FILE      ASCII, FIXED=200
EXTPATH   DIM       127
EXTRCDIR  INIT      "extracts/"
FILENAME  DIM       8
INCODESC  DIM       10
LEV1DESC  DIM       35
LEV2DESC  DIM       35
LEV3DESC  DIM       35
QUOTE     INIT      "#""
.
EXPORTLV  FORM      1
DOWNNAME  DIM      40
LEVDESC   DIM      35
FROMDD    DIM      10
TODD      DIM      10
FROMAD    DIM      10
TOAD      DIM      10
VISITDES  DIM      15
CLV1DES   DIM      35
CLV2DES   DIM      35
CLV3DES   DIM      35
.
ACCEPT    FORM      1
SACCEPT   FORM      1
.
PRGID     INIT      "IBACCC51"
PRGNAM    INIT      "CIAS - Spreadsheet Analysis Generation "
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000
.
ML1000    CALL      EXTR0000              * Generate export file
          BRANCH    EXIT,ML0100
          GOTO      ML9999
.
ML9999    CHAIN     PGM                    * chain back to program
.
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          MOVE      ONE,CCENTRY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          MOVE      "   64",AUDTSECT
          MOVE      " 37",AUDTPOST
          READ      CONTROLF,AUDTSECT;*AUDTPOST,AUDTFLAG
          IF        AUDTFLAG=1
            DISPLAY   *P56:24,"Opening ccsaudep";
            OPEN      CCSAUDEP,"ccsaudep"
          ENDIF
          MOVE      "   64",AUDTSECT
          MOVE      " 38",AUDTPOST
          READ      CONTROLF,AUDTSECT;*AUDTPOST,AUDTFLAG
          IF        AUDTFLAG=1
            DISPLAY   *P56:24,"Opening ccsaudia";
            OPEN      CCSAUDIA,"ccsaudia"
          ENDIF
          DISPLAY   *P56:24,"Opening ccsceaaf";
          OPEN      CCSCEAA1,"ccsceaaf"
          DISPLAY   *P56:24,"Opening ccsepsaf";
          OPEN      CCSEPSA1,"ccsepsaf"
          DISPLAY   *P56:24,"Opening ccsepsaf";
          OPEN      CCSEPSA2,"ccsepsaf"
          DISPLAY   *P56:24,"Opening ccsepsaf";
          OPEN      CCSEPSA3,"ccsepsaf"
          DISPLAY   *P56:24,"Opening ccsepsaf";
          OPEN      CCSEPSA4,"ccsepsaf"
          DISPLAY   *P56:24,"Opening ccsepsaf";
          OPEN      CCSEPSA5,"ccsepsaf"
          DISPLAY   *P56:24,"Opening ccshcdaf";
          OPEN      CCSHCDA1,"ccshcdaf"
          DISPLAY   *P56:24,"Opening ccshcdaf";
          OPEN      CCSHCDA2,"ccshcdaf"
          DISPLAY   *P56:24,"Opening ccsiadaf";
          OPEN      CCSIADA1,"ccsiadaf"
          DISPLAY   *P56:24,"Opening ccsstyaf";
          OPEN      CCSSTYA1,"ccsstyaf"
          DISPLAY   *P56:24,"Opening ccsxhdaf";
          OPEN      CCSXHDA1,"ccsxhdaf"
          DISPLAY   *P56:24,"Opening ccsxhdaf";
          OPEN      CCSXHDA2,"ccsxhdaf"
          DISPLAY   *P1:24,*EL;
.
          CALL      CF00L000 * Clear Fields
.
          READ      CONTROLF,NINTY1;*179,CWEBROOT   * HTML Root directory
          READ      CONTROLF,HUND04;RSCNEXT  * Ouput Extract directory
          STRIP     CWEBROOT
          STRIP     RSCNEXT
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Create Excel Export File"
.
OPTN0500  KEYIN     *P1:16,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:16,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
+
.**************************************************************************
.*                               EXTR0000              Called by: ML1000  *
.* Generate Casemix Extract                                               *
.**************************************************************************
.
EXTR0000  CALL      EEID0000                     * Keyin extract ID
          BRANCH    EXIT,EXTR9999
.
          CALL      GETCME00
          CALL      CCSCOPOP                     * Open Files
.
          CALL      ELEVL000                     * keyin export level
.
          CALL      FILE0000                     * keyin export file name
.
          CALL      CONTQST                      * ok to continue
          BRANCH    CEXIT,EXTR5000,EXTR9999,EXTR9999    * 1=Yes, 2=No, 3=Cancel
.
EXTR5000  CALL      REPT0000                     * Create Export File
.
EXTR9999  RETURN
.
.------------------------------------------------------------
. Keyin extract ID
.------------------------------------------------------------
EEID0000  DISPLAY   *P1:4,*EF
          KEYIN     *P1:4,"Extract ID : ",*V2LON,CCEAEID
          ENDSET    CCEAEID
          APPEND    SP70,CCEAEID
          RESET     CCEAEID
.
          MATCH     SP70,CCEAEID
          GOTO      EEID9000 IF EQUAL
.
          PACK      KEY4,CCEAEID,SP70
          CALL      RDCCEA1
          IF        OVRCD=1
            BEEP
            DISPLAY  *P1:24,"Invalid Extract ID";
            CALL      HITENTER
            GOTO      EEID0000
          ENDIF
.
          DISPLAY   *P19:4,*V2LON,CCEADES
          MOVE      ZERO,EXIT
          GOTO      EEID9999
.
EEID9000  MOVE      ONE,EXIT
          GOTO      EEID9999
.
EEID9999  RETURN
.
.------------------------------------------------------------
. Enter Level
.------------------------------------------------------------
ELEVL000  KEYIN     *P1:5,"Export Level : ",*V2LON,EXPORTLV
          BRANCH    EXPORTLV,ELEVL110,ELEVL120,ELEVL130,ELEVL140
          BEEP
          GOTO      ELEVL000
.
ELEVL110
          MOVE      CLV1DES,LEVDESC
          GOTO      ELEVL999
.
ELEVL120
          MATCH     SP70,CCEALV2
          IF        @EQUAL
            BEEP
            DISPLAY  *P1:24,"Extract Not Valid for this level"
            CALL      HITENTER
            GOTO      ELEVL000
          ENDIF
          MOVE      CLV2DES,LEVDESC
          GOTO      ELEVL999
.
ELEVL130
          MATCH     SP70,CCEALV3
          IF        @EQUAL
            BEEP
            DISPLAY  *P1:24,"Extract Not Valid for this level"
            CALL      HITENTER
            GOTO      ELEVL000
          ENDIF
          MOVE      CLV3DES,LEVDESC
          GOTO      ELEVL999
.
ELEVL140  MOVE      "Patient Episodes    ",LEVDESC
          GOTO      ELEVL999
.
ELEVL999  RETURN
.
.------------------------------------------------------------
.Keyin export data file name
.------------------------------------------------------------
FILE0000  DISPLAY   *P1:6,*EF
          KEYIN     *P1:6,"Export Data File Name : ",*V2LON,DOWNNAME
          PACK      DOWNNAME,DOWNNAME,SP70
.
          MATCH     SP70,DOWNNAME
          IF        @EQUAL
            DISPLAY  *P1:24,"Export data file name is mandatory";
            CALL      HITENTER
            GOTO      FILE0000
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      FILE9999
.
FILE9000  MOVE      ONE,EXIT
          GOTO      FILE9999
.
FILE9999  RETURN
.
.--------------------------------------------------------
. Clear Fields for Screen : Options
.-------------------------------------------------------
CF00L000
          MOVE      SP70,CCEAEID
          MOVE      ZERO,EXPORTLV
          MOVE      SP70,DOWNNAME
          MOVE      SP70,LEVDESC
          MOVE      SP70,CCEADES
          MOVE      SP70,CCEAINC
          MOVE      SP70,CCEAVIS
          MOVE      SP70,CCEADAY
          MOVE      SP70,CCEALV1
          MOVE      SP70,CCEALV2
          MOVE      SP70,CCEALV3
          MOVE      SP70,FROMDD
          MOVE      SP70,TODD
          MOVE      SP70,FROMAD
          MOVE      SP70,TOAD
          MOVE      SP70,VISITDES
          MOVE      SP70,CCHCDES
          MOVE      SP70,CLV1DES
          MOVE      SP70,CLV2DES
          MOVE      SP70,CLV3DES
CF00L999  RETURN
.
.------------------------------------------------------------
. Print Report
.------------------------------------------------------------
REPT0000  CALL      OPEN0000       * Open Files
          BRANCH    EXIT,REPT9990
.
          CALL      GETEX000       * Get Extract Details
.
          DISPLAY  *P1:10,*EF
          MOVE      DOWNNAME,KEY80
          STRIP     KEY80
          ENDSET    KEY80
          APPEND    ".csv",KEY80
          RESET     KEY80
.
          DISPLAY  *P1:15,*EL,"Extracting Casemix Analysis File"
          DISPLAY  *P1:16,*EL,*V2LON,KEY80
.
          BRANCH    EXPORTLV,REPT1000,REPT2000,REPT3000,REPT4000
.
REPT1000  CALL      LV1EX000
          GOTO      REPT5000
.
REPT2000  CALL      LV2EX000
          GOTO      REPT5000
.
REPT3000  CALL      LV3EX000
          GOTO      REPT5000
.
REPT4000  CALL      LV4EX000
          GOTO      REPT5000
.
REPT5000  CALL      TRAN0000  * Transfer Files Now
          GOTO      REPT9990
.
REPT9990  MOVE      ZERO,EXIT            * Exit
          GOTO      REPT9999
.
REPT9999  RETURN
.
.------------------------------------------------------------
. Open/Create Downlaod File
.------------------------------------------------------------
OPEN0000  MATCH     SP70,DOWNNAME
          GOTO      OPEN9000 IF EQUAL
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      DOWNFILE,DOWNNAME            * Check dpath
          TRAPCLR   IO
.
          COMPARE   ZERO,OVRCD
          GOTO      OPEN0100 IF EQUAL
.
          STRIP     DOWNNAME
.
          PACK      EXTPATH,SP70,SP70
          PACK      EXTPATH,CWEBROOT,EXTRCDIR,DOWNNAME,CSVEXT,SP70
          SQUEEZE   EXTPATH
          RESET     DOWNNAME
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      DOWNFILE,EXTPATH             * Check extract dir
          TRAPCLR   IO
.
          COMPARE   ZERO,OVRCD
          GOTO      OPEN0100 IF EQUAL
.
          GOTO      OPEN1000
.
OPEN0100  BEEP
          KEYIN     *P1:24,*EL,"Export File Already Exists. Overwrite (":
                    *V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS
          REP       UPPLOW,ANS
          MOVE      ONE,EXIT
          MATCH     "N",ANS
          GOTO      OPEN9999 IF EQUAL
          MATCH     "Y",ANS
          GOTO      OPEN0100 IF NOT EQUAL
          CLOSE     DOWNFILE,DELETE
.
OPEN1000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PREP      DOWNFILE,DOWNNAME
          TRAPCLR   IO
          BRANCH    OVRCD,OPEN9100
          MOVE      ZERO,EXIT
          GOTO      OPEN9999
.
OPEN9000  BEEP
          MOVE      "Export Filename MUST be Specified - ",DISPMSG
          CALL      MESSAGE1
          MOVE      ONE,EXIT
          GOTO      OPEN9999
.
OPEN9100  BEEP
          MOVE      "Unable to Create Export File - ",DISPMSG
          CALL      MESSAGE1
          MOVE      ONE,EXIT
          GOTO      OPEN9999
.
OPEN9999  RETURN
.
.------------------------------------------------------------
. Get Extract Details
.------------------------------------------------------------
GETEX000  PACK      DESC1,SP70,SP70
          PACK      DESC2,SP70,SP70
          PACK      DESC3,SP70,SP70
          PACK      KEY3,CCEALV1
          CALL      RDCCST1
          IF        OVRCD=0
            MOVELPTR CCSTDES,F3
            SFORMAT  DESC1,F3
            MOVE     CCSTDES,DESC1
          ENDIF
          MATCH     SP70,DESC1
          IF        @EQUAL
            SFORMAT  DESC1,5
            MOVE     "Blank",DESC1
          ENDIF
.
          PACK      KEY3,CCEALV2
          CALL      RDCCST1
          IF        OVRCD=0
            MOVELPTR CCSTDES,F3
            SFORMAT  DESC2,F3
            MOVE     CCSTDES,DESC2
          ENDIF
          MATCH     SP70,DESC2
          IF        @EQUAL
            SFORMAT  DESC2,5
            MOVE     "Blank",DESC2
          ENDIF
.
          PACK      KEY3,CCEALV3
          CALL      RDCCST1
          IF        OVRCD=0
            MOVELPTR CCSTDES,F3
            SFORMAT  DESC3,F3
            MOVE     CCSTDES,DESC3
          ENDIF
          MATCH     SP70,DESC3
          IF        @EQUAL
            SFORMAT  DESC3,5
            MOVE     "Blank",DESC3
          ENDIF
.
          MOVE      "Income   ",INCODESC
          MOVE      "Cost     ",COSTDESC
.
          MATCH      SP70,CCEAIMOD
          IF         !@EQUAL
            MOVE       "Model ",KEY6
            PACK       INCODESC,KEY6,CCEAIMOD,SP70
          ENDIF
          MATCH      SP70,CCEACMOD
          IF         !@EQUAL
            MOVE       "Model ",KEY6
            PACK       COSTDESC,KEY6,CCEACMOD,SP70
          ENDIF
.
          MOVE      "   64",AUDTSECT
          READ      CONTROLF,AUDTSECT;*109,CCCOIA1,CCCOIA2,CCCOIA3:
                                           CCCOIM1,CCCOIM2,CCCOIM3:
                                      *177,CCCOSOPR:
                                      *183,CCCOTH1,CCCOTH2,CCCOTH3,CCCOTH4:
                                           CCCOTF1,CCCOTF2,CCCOTF3,CCCOTF4:
                                           CCCOTT1,CCCOTT2,CCCOTT3,CCCOTT4
          MATCH      SP70,CCEAITHE
          IF         !@EQUAL
            MOVE       CCEAITHE,F1
            LOAD       INCODESC,F1,CCCOTH1,CCCOTH2,CCCOTH3,CCCOTH4
          ENDIF
          MATCH      SP70,CCEACTHE
          IF         !@EQUAL
            MOVE       CCEACTHE,F1
            LOAD       COSTDESC,F1,CCCOTH1,CCCOTH2,CCCOTH3,CCCOTH4
          ENDIF
.
GETEX999  RETURN
.
.----------------------------------------------------------------------
. Extract Level 1
.------------------------------------------------------------
LV1EX000  DISPLAY   *P1:24,*EL,"Creating File       : ";
          CALL      HD01E000
          MOVE      ZERO,SECTOR
          PACK      KEY10,SP70
          CALL      RSCCEB1
LV1EX100  CALL      RKCCEB1
          BRANCH    OVRCD,LV1EX900
          ADD       ONE,SECTOR
          IF        (SECTOR%50=0)
            DISPLAY   *P23:24,SECTOR
          ENDIF
          CALL      WR01E000
          GOTO      LV1EX100
.
LV1EX900  MOVE      ZERO,EXIT
LV1EX999  RETURN
.------------------------------------------------------------
. Extract Level 2
.------------------------------------------------------------
LV2EX000  DISPLAY   *P1:24,*EL,"Creating File       : ";
          CALL      HD02E000
          MOVE      ZERO,SECTOR
          PACK      KEY20,SP70
          CALL      RSCCED1
LV2EX100  CALL      RKCCED1
          BRANCH    OVRCD,LV2EX900
          ADD       ONE,SECTOR
          IF        (SECTOR%50=0)
            DISPLAY   *P23:24,SECTOR
          ENDIF
          CALL      WR02E000
          GOTO      LV2EX100
LV2EX900
LV2EX999  RETURN
.------------------------------------------------------------
. Extract Level 3
.------------------------------------------------------------
LV3EX000  DISPLAY   *P1:24,*EL,"Creating File       : ";
          CALL      HD03E000
          MOVE      ZERO,SECTOR
          PACK      KEY30,SP70
          CALL      RSCCEE1
LV3EX100  CALL      RKCCEE1
          BRANCH    OVRCD,LV3EX900
          ADD       ONE,SECTOR
          IF        (SECTOR%50=0)
            DISPLAY   *P23:24,SECTOR
          ENDIF
          CALL      WR03E000
          GOTO      LV3EX100
LV3EX900  MOVE      ZERO,EXIT
LV3EX999  RETURN
.------------------------------------------------------------
. Extract Level 4
.------------------------------------------------------------
LV4EX000  DISPLAY   *P1:24,*EL,"Creating File       : ";
          CALL      HD04E000
          MOVE      ZERO,SECTOR
          PACK      KEY18,SP70
          CALL      RSCCEC1
LV4EX100  CALL      RKCCEC1
          BRANCH    OVRCD,LV4EX900
          ADD       ONE,SECTOR
          IF        (SECTOR%50=0)
            DISPLAY   *P23:24,SECTOR
          ENDIF
          PACK      KEY18,CCECHCD,CCECEPS
          CALL      RDCCEP1
          CALL      RDCCIA1
          CALL      WR04E000
          GOTO      LV4EX100
LV4EX900  MOVE      ZERO,EXIT
LV4EX999  RETURN
.
.------------------------------------------------------------
. Write Export File
.------------------------------------------------------------
WR01E000  MOVE      SP70,LEV1DESC
          PACK      KEY13,CCEALV1,CCEBLV1
          CALL      RDCCXH1
          IF        OVRCD=0
            MOVE      CCXHDES,LEV1DESC
          ENDIF
          WRITE     DOWNFILE,SEQ;QUOTE,CCEBLV1,QUOTE,COMMA:
                    QUOTE,LEV1DESC,QUOTE,COMMA:
                    CCEBNEP,COMMA:
                    CCEBLTO,COMMA:
                    CCEBFTC
.
WR01E999  RETURN
.------------------------------------------------------------
. Write Export File Level 1
.------------------------------------------------------------
HD01E000  WRITE     DOWNFILE,SEQ;QUOTE,"Casemix Code ",CCEALV1,QUOTE,COMMA:
                    QUOTE,DESC1,QUOTE,COMMA:
                    QUOTE,"Episodes",QUOTE,COMMA:
                    QUOTE,"Bed Days",QUOTE,COMMA:
                    QUOTE,INCODESC,QUOTE
          RETURN
.
.------------------------------------------------------------
. Print Line
.------------------------------------------------------------
WR02E000  MOVE      SP70,LEV1DESC
          MOVE      SP70,LEV2DESC
          PACK      KEY13,CCEALV1,CCEDLV1
          CALL      RDCCXH1
          IF        OVRCD=0
            MOVE      CCXHDES,LEV1DESC
          ENDIF
          PACK      KEY13,CCEALV2,CCEDLV2
          CALL      RDCCXH1
          IF        OVRCD=0
            MOVE      CCXHDES,LEV2DESC
          ENDIF
          WRITE     DOWNFILE,SEQ;QUOTE,CCEDLV1,QUOTE,COMMA:
                    QUOTE,LEV1DESC,QUOTE,COMMA:
                    QUOTE,CCEDLV2,QUOTE,COMMA:
                    QUOTE,LEV2DESC,QUOTE,COMMA:
                    CCEDNEP,COMMA:
                    CCEDLTO,COMMA:
                    CCEDFTC
WR02E999  RETURN
.------------------------------------------------------------
. Write Export File Level 2
.------------------------------------------------------------
HD02E000  WRITE     DOWNFILE,SEQ;QUOTE,"Casemix Code ",CCEALV1,QUOTE,COMMA:
                    QUOTE,DESC1,QUOTE,COMMA:
                    QUOTE,"Casemix Code ",CCEALV2,QUOTE,COMMA:
                    QUOTE,DESC2,QUOTE,COMMA:
                    QUOTE,"Episodes",QUOTE,COMMA:
                    QUOTE,"Bed Days",QUOTE,COMMA:
                    QUOTE,INCODESC,QUOTE
          RETURN
.
.------------------------------------------------------------
. Print Line
.------------------------------------------------------------
WR03E000  MOVE      SP70,LEV1DESC
          MOVE      SP70,LEV2DESC
          MOVE      SP70,LEV3DESC
          PACK      KEY13,CCEALV1,CCEELV1
          CALL      RDCCXH1
          IF        OVRCD=0
            MOVE      CCXHDES,LEV1DESC
          ENDIF
          PACK      KEY13,CCEALV2,CCEELV2
          CALL      RDCCXH1
          IF        OVRCD=0
            MOVE      CCXHDES,LEV2DESC
          ENDIF
          PACK      KEY13,CCEALV3,CCEELV3
          CALL      RDCCXH1
          IF        OVRCD=0
            MOVE      CCXHDES,LEV3DESC
          ENDIF
.
          WRITE     DOWNFILE,SEQ;QUOTE,CCEELV1,QUOTE,COMMA:
                    QUOTE,LEV1DESC,QUOTE,COMMA:
                    QUOTE,CCEELV2,QUOTE,COMMA:
                    QUOTE,LEV2DESC,QUOTE,COMMA:
                    QUOTE,CCEELV3,QUOTE,COMMA:
                    QUOTE,LEV3DESC,QUOTE,COMMA:
                    CCEENEP,COMMA:
                    CCEELTO,COMMA:
                    CCEEFTC
WR03E999  RETURN
.------------------------------------------------------------
. Write Export File Level 3
.------------------------------------------------------------
HD03E000  WRITE     DOWNFILE,SEQ;QUOTE,"Casemix Code ",CCEALV1,QUOTE,COMMA:
                    QUOTE,DESC1,QUOTE,COMMA:
                    QUOTE,"Casemix Code ",CCEALV2,QUOTE,COMMA:
                    QUOTE,DESC2,QUOTE,COMMA:
                    QUOTE,"Casemix Code ",CCEALV3,QUOTE,COMMA:
                    QUOTE,DESC3,QUOTE,COMMA:
                    QUOTE,"Episodes",QUOTE,COMMA:
                    QUOTE,"Bed Days",QUOTE,COMMA:
                    QUOTE,INCODESC,QUOTE
          RETURN
.
.------------------------------------------------------------
. Print Line
.------------------------------------------------------------
WR04E000  MOVE      SP70,LEV1DESC
          MOVE      SP70,LEV2DESC
          MOVE      SP70,LEV3DESC
          PACK      KEY13,CCEALV1,CCECLV1
          CALL      RDCCXH1
          IF        OVRCD=0
            MOVE      CCXHDES,LEV1DESC
          ENDIF
          PACK      KEY13,CCEALV2,CCECLV2
          CALL      RDCCXH1
          IF        OVRCD=0
            MOVE      CCXHDES,LEV2DESC
          ENDIF
          PACK      KEY13,CCEALV3,CCECLV3
          CALL      RDCCXH1
          IF        OVRCD=0
            MOVE      CCXHDES,LEV3DESC
          ENDIF
.
          PACK      CCEPURN,SP70
          PACK      KEY18,CCECHCD,CCECEPS,SP70
          CALL      RDCCEP1
.
          WRITE     DOWNFILE,SEQ;QUOTE,CCECLV1,QUOTE,COMMA:
                    QUOTE,LEV1DESC,QUOTE,COMMA:
                    QUOTE,CCECLV2,QUOTE,COMMA:
                    QUOTE,LEV2DESC,QUOTE,COMMA:
                    QUOTE,CCECLV3,QUOTE,COMMA:
                    QUOTE,LEV3DESC,QUOTE,COMMA:
                    QUOTE,CCECHCD,QUOTE,COMMA:
                    QUOTE,CCEPURN,QUOTE,COMMA:
                    QUOTE,CCECEPS,QUOTE,COMMA:
                    CCEPSEX,COMMA:
                    CCEPAGEY,COMMA:
                    CCEPADAT,COMMA:
                    CCEPDDAT,COMMA:
                    CCECLOS,COMMA:
                    CCIABA1,COMMA:
                    CCIABA2,COMMA:
                    CCIABA3,COMMA:
                    CCECFTC,COMMA:
                    CCIAITH,COMMA:
                    CCIAIA1,COMMA:
                    CCIAIA2,COMMA:
                    CCIAIA3,COMMA:
                    CCIAIAO,COMMA:
                    CCIAIM1,COMMA:
                    CCIAIM2,COMMA:
                    CCIAIM3,COMMA:
                    CCIAIMO,COMMA:
                    QUOTE,CCEPSUR,QUOTE,COMMA:
                    QUOTE,CCEPGIV,QUOTE
.
WR04E999  RETURN
.------------------------------------------------------------
. Write Export File Level 4
.------------------------------------------------------------
HD04E000  CALL      HD04EB00
          RETURN
.
HD04EB00  WRITE     DOWNFILE,SEQ;QUOTE,"Casemix Code ",CCEALV1,QUOTE,COMMA:
                    QUOTE,DESC1,QUOTE,COMMA:
                    QUOTE,"Casemix Code ",CCEALV2,QUOTE,COMMA:
                    QUOTE,DESC2,QUOTE,COMMA:
                    QUOTE,"Casemix Code ",CCEALV3,QUOTE,COMMA:
                    QUOTE,DESC3,QUOTE,COMMA:
                    QUOTE,"Hospital",QUOTE,COMMA:
                    QUOTE,"U/R Number",QUOTE,COMMA:
                    QUOTE,"Episode",QUOTE,COMMA:
                    QUOTE,"Gender",QUOTE,COMMA:
                    QUOTE,"Age",QUOTE,COMMA:
                    QUOTE,"Admission Date",QUOTE,COMMA:
                    QUOTE,"Discharge Date",QUOTE,COMMA:
                    QUOTE,"Bed Days",QUOTE,COMMA:
                    QUOTE,"Bed Days-",CCCOIA1,QUOTE,COMMA:
                    QUOTE,"Bed Days-",CCCOIA2,QUOTE,COMMA:
                    QUOTE,"Bed Days-",CCCOIA3,QUOTE,COMMA:
                    QUOTE,INCODESC,QUOTE,COMMA:
                    QUOTE,"Theatre",QUOTE,COMMA:
                    QUOTE,CCCOIA1,QUOTE,COMMA:
                    QUOTE,CCCOIA2,QUOTE,COMMA:
                    QUOTE,CCCOIA3,QUOTE,COMMA:
                    QUOTE,"Acc Other",QUOTE,COMMA:
                    QUOTE,CCCOIM1,QUOTE,COMMA:
                    QUOTE,CCCOIM2,QUOTE,COMMA:
                    QUOTE,CCCOIM3,QUOTE,COMMA:
                    QUOTE,"Misc Other",QUOTE,COMMA:
                    QUOTE,"Surname",QUOTE,COMMA:
                    QUOTE,"Given Name",QUOTE
          RETURN
.
.--------------------------------------------------------------------------
. Execute us1 Script
.--------------------------------------------------------------------------
TRAN0000  STRIP     DOWNNAME
.         
          CLEAR     CMDLINE
          APPEND    "ibaccc51.us1 ",CMDLINE
          APPEND    DOWNNAME,CMDLINE
          APPEND    ".txt",CMDLINE
          APPEND    SP1,CMDLINE
          APPEND    DOWNNAME,CMDLINE
          APPEND    SP1,CMDLINE
          APPEND    CCEAEID,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID   * ibaccc51.us1 fullpathname filename
.
TRAN9999  RETURN
.
.======================================================================
. Determine Display Values for a Valid Casemix Extract Code
.======================================================================
GETCME00  MATCH     SP70,CCEAFDD
          IF        @EQUAL
            MOVE      "Start     ",FROMDD
          ELSE
            UNPACK    CCEAFDD,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,FROMDD
          ENDIF
.
          MATCH     "99999999",CCEATDD
          IF        @EQUAL
            MOVE      "Finish    ",TODD
          ELSE
            UNPACK    CCEATDD,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,TODD
          ENDIF
.
          MATCH     SP70,CCEAFAD
          IF        @EQUAL
            MOVE      "Start     ",FROMAD
          ELSE
            UNPACK    CCEAFAD,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,FROMAD
          ENDIF
.
          MATCH     "99999999",CCEATAD
          IF        @EQUAL
            MOVE      "Finish    ",TOAD
          ELSE
            UNPACK    CCEATAD,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,TOAD
          ENDIF
.
          MOVE     CCEAVIS,F2
          BRANCH   F2,GETCME10,GETCME20,GETCME30,GETCME35
          MOVE     "All        ",VISITDES
          GOTO     GETCME40
GETCME10  MOVE     "A & E      ",VISITDES
          GOTO     GETCME40
GETCME20  MOVE     "Outpatients ",VISITDES
          GOTO     GETCME40
GETCME30  MOVE     "Inpatients",VISITDES
          GOTO     GETCME40
.
GETCME35  MOVE     "Allied Health",VISITDES
          GOTO     GETCME40
.
GETCME40  MOVE     SP70,CLV1DES
          MOVE     "Not Applicable",CLV2DES
          MOVE     "Not Applicable",CLV3DES
.
          MOVE     CCEALV1,KEY3
          CALL     RDCCST1
          MOVE     CCSTDES,CLV1DES
.
          MATCH    SP70,CCEALV2
          IF       !@EQUAL
            MOVE     CCEALV2,KEY3
            CALL     RDCCST1
            MOVE     CCSTDES,CLV2DES
          ENDIF
.
          MATCH    SP70,CCEALV3
          IF       !@EQUAL
            MOVE     CCEALV3,KEY3
            CALL     RDCCST1
            MOVE     CCSTDES,CLV3DES
          ENDIF
.
          MOVE     CCEAHCD,KEY2
          CALL     RDCCHC1
.
          RETURN
.
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       CCSCEAIO/INC
          INC       CCSCEBIO/INC
          INC       CCSCECIO/INC
          INC       CCSCEDIO/INC
          INC       CCSCEEIO/INC
          INC       CCSCOPCD
          INC       CCSEPSIO/INC
          INC       CCSHCDIO/INC
          INC       CCSIADIO/INC
          INC       CCSSTACD
          INC       CCSSTYIO/INC
          INC       CCSXHDIO/INC
