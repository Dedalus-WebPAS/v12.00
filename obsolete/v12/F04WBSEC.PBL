.*****************************************************************************
.*    Program      : F04WBSEC                                                *
.*    Description  : Conversion of websecaf to new file layout               *
.*                                                                           *
.*    Author       : Ania Pietrusewicz CAR 295007                            *
.*    Date         : 06/01/2014                                              *
.*    Modifications:                                                         *
.*                                                                           *
.*        V10.04.02 11/03/2014  Steve Armstrong  CAR 294083                  *
.*                  Made Index 7 a single field                              *
.*        V10.04.01 04/03/2014  Steve Armstrong  CAR 294083                  *
.*                  Mods to include new field WBSELOGN.                      *
.*****************************************************************************
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
.
OLDFILE1  IFILE     SQL, FIXED=456, KEY="U1-10"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.WBSEUID   DIM       10     10       1     WEB User ID                        
.WBSENAM   DIM       30     30       11    User Name                          
.WBSEDOC   DIM       6      6        41    Linked Doctor Code                 
.WBSEPCD   DIM       4      4        47    Passcode                           
.WBSEWRD   DIM       3      3        51    Ward                               
.WBSESIT   DIM       6      6        54    Outpatient Site                    
.WBSECLI   DIM       6      6        60    Outpatient Clinic                  
.WBSEOCG   DIM       3      3        66    Occupational Group (Cat w0)        
.WBSEDIR   DIM       40     40       69    Log in URL Link                    
.WBSEESC   DIM       3      3        109   Emergency Site Code                
.WBSEEUC   DIM       3      3        112   Emergency Unit Code                
.WBSEACT   DIM       1      1        115   Active 0=No 1=Yes                  
.WBSEEXP   DIM       8      8        116   Expiry Date                        
.WBSEPSE   DIM       1      1        124   Post Security Enabled              
.WBSECNT   DIM       2      2        125   Retry Count                        
.WBSEAUT   DIM       1      1        127   Auto Ageing (Y/N) 1=Yes 0=No       
.WBSEAGE   DIM       3      3        128   Age by Number of Days              
.WBSEDFM   DIM       8      8        131   Default Menu                       
.WBSEGPCD  DIM       10     10       139   GP Code                            
.WBSEGPPC  DIM       10     10       149   GP Practice Code                   
.WBSEGPCN  DIM       2      2        159   GP Practice Count                  
.WBSEMESA  DIM       1      1        161   Messages Available (Y/N) 1=Yes 0=No
.WBSELACD  DIM       8      8        162   Last Login Date (CCYYMMDD)         
.WBSELACT  DIM       5      5        170   Last Login Time (HH:MM)            
.WBSEDEPT  DIM       6      6        175   Printer Department (ibadptaf)      
.WBSECXA   DIM       1      1        181   Passwork Expiry User Alerted (1=Y) 
.WBSEDALL  DIM       3      3        182   Allied Health Department Code (CG) 
.WBSEDTYP  DIM       3      3        185   Document Type (Cat TY)             
.WBSEHLOC  DIM       4      4        188   MR Home Location                   
.WBSEVWSL  DIM       1      1        192   View Alert Security Level          
.WBSEADSL  DIM       1      1        193   Add Alert Security Level           
.WBSEDGSI  DIM       3      3        194   Day/General Surgery Ind (Cat YD)   
.WBSEFPCD  DIM       4      4        197   Finance Passcode                   
.WBSECASH  DIM       1      1        201   Cashier 1=Yes                      
.WBSEMRTS  DIM       8      8        202   MRT Security Number                
.WBSEHOSP  DIM       3      3        210   Logged into Hospital ID (pathspaf) 
.WBSELTYP  DIM       3      3        213   Clinic Location Type (Category CT) 
.WBSEFDEP  DIM       15     15       216   User Department (Free Format)      
.WBSENURS  DIM       10     10       231   Nurse Code                         
.WBSECDAT  DIM       8      8        241   Date Record Created (ccyymmdd)     
.WBSECTIM  DIM       8      8        249   Time Record Created (hh:mm:ss)     
.WBSECUID  DIM       10     10       257   WEB User Id Rec. Creator (websecaf)
.WBSEUDAT  DIM       8      8        267   Last Update Date (ccyymmdd)        
.WBSEUTIM  DIM       8      8        275   Last Update Time (hh:mm:ss:)       
.WBSEUUID  DIM       10     10       283   WEB User Id Rec. Update (websecaf) 
.WBSEBEDV  DIM       1      1        293   Bed Board View Number              
.WBSEGPAC  DIM       1      1        294   GP Access - blank=all 1=no override
.WBSEROLE  DIM       10     10       295   Role                               
.WBSESECT  DIM       1      1        305   Security Id Type 0=User Profile    
.WBSEVH1L  DIM       1      1        306   View H1 Security Level             
.WBSEAH1L  DIM       1      1        307   Add H1 Security Level              
.WBSEVH2L  DIM       1      1        308   View H2 Security Level             
.WBSEAH2L  DIM       1      1        309   Add H2 Security Level              
.WBSEVH3L  DIM       1      1        310   View H3 Security Level             
.WBSEAH3L  DIM       1      1        311   Add H3 Security Level              
.WBSEVH4L  DIM       1      1        312   View H4 Security Level             
.WBSEAH4L  DIM       1      1        313   Add H4 Security Level              
.WBSEVH5L  DIM       1      1        314   View H5 Security Level             
.WBSEAH5L  DIM       1      1        315   Add H5 Security Level              
.WBSEVH6L  DIM       1      1        316   View H6 Security Level             
.WBSEAH6L  DIM       1      1        317   Add H6 Security Level              
.WBSEVH7L  DIM       1      1        318   View H7 Security Level             
.WBSEAH7L  DIM       1      1        319   Add H7 Security Level              
.WBSEVH8L  DIM       1      1        320   View H8 Security Level             
.WBSEAH8L  DIM       1      1        321   Add H8 Security Level              
.WBSEVH9L  DIM       1      1        322   View H9 Security Level             
.WBSEAH9L  DIM       1      1        323   Add H9 Security Level              
.WBSEHHOS  DIM       3      3        324   MR Home Hospital Id. (pathspaf)    
.WBSEOPDU  DIM       3      3        327   Outpatient Direct User (Cat Tz)    
.WBSEUTLH  DIM       3      3        330   Usual Tracking Location Hospital   
.WBSEUTLC  DIM       4      4        333   Usual Tracking Location            
OLDSPARE   DIM       119    119      337   Spare                              
.
.End of Record                      456  
.
.
.Data File Definition
.--------------------
WEBSECA1   IFILE    SQL, FIXED=597, KEY="U1-10"                                 
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
WBSEUID   DIM       10     10       1     WEB User ID                        
WBSENAM   DIM       30     30       11    User Name                          
WBSEDOC   DIM       6      6        41    Linked Doctor Code                 
WBSEPCD   DIM       4      4        47    Passcode                           
WBSEWRD   DIM       3      3        51    Ward                               
WBSESIT   DIM       6      6        54    Outpatient Site                    
WBSECLI   DIM       6      6        60    Outpatient Clinic                  
WBSEOCG   DIM       3      3        66    Occupational Group (Cat w0)        
WBSEDIR   DIM       40     40       69    Log in URL Link                    
WBSEESC   DIM       3      3        109   Emergency Site Code                
WBSEEUC   DIM       3      3        112   Emergency Unit Code                
WBSEACT   DIM       1      1        115   Active 0=No 1=Yes                  
WBSEEXP   DIM       8      8        116   Expiry Date                        
WBSEPSE   DIM       1      1        124   Post Security Enabled              
WBSECNT   DIM       2      2        125   Retry Count                        
WBSEAUT   DIM       1      1        127   Auto Ageing (Y/N) 1=Yes 0=No       
WBSEAGE   DIM       3      3        128   Age by Number of Days              
WBSEDFM   DIM       8      8        131   Default Menu                       
WBSEGPCD  DIM       10     10       139   GP Code                            
WBSEGPPC  DIM       10     10       149   GP Practice Code                   
WBSEGPCN  DIM       2      2        159   GP Practice Count                  
WBSEMESA  DIM       1      1        161   Messages Available (Y/N) 1=Yes 0=No
WBSELACD  DIM       8      8        162   Last Login Date (CCYYMMDD)         
WBSELACT  DIM       5      5        170   Last Login Time (HH:MM)            
WBSEDEPT  DIM       6      6        175   Printer Department (ibadptaf)      
WBSECXA   DIM       1      1        181   Passwork Expiry User Alerted (1=Y) 
WBSEDALL  DIM       3      3        182   Allied Health Department Code (CG) 
WBSEDTYP  DIM       3      3        185   Document Type (Cat TY)             
WBSEHLOC  DIM       4      4        188   MR Home Location                   
WBSEVWSL  DIM       1      1        192   View Alert Security Level          
WBSEADSL  DIM       1      1        193   Add Alert Security Level           
WBSEDGSI  DIM       3      3        194   Day/General Surgery Ind (Cat YD)   
WBSEFPCD  DIM       4      4        197   Finance Passcode                   
WBSECASH  DIM       1      1        201   Cashier 1=Yes                      
WBSEMRTS  DIM       8      8        202   MRT Security Number                
WBSEHOSP  DIM       3      3        210   Logged into Hospital ID (pathspaf) 
WBSELTYP  DIM       3      3        213   Clinic Location Type (Category CT) 
WBSEFDEP  DIM       15     15       216   User Department (Free Format)      
WBSENURS  DIM       10     10       231   Nurse Code                         
WBSECDAT  DIM       8      8        241   Date Record Created (ccyymmdd)     
WBSECTIM  DIM       8      8        249   Time Record Created (hh:mm:ss)     
WBSECUID  DIM       10     10       257   WEB User Id Rec. Creator (websecaf)
WBSEUDAT  DIM       8      8        267   Last Update Date (ccyymmdd)        
WBSEUTIM  DIM       8      8        275   Last Update Time (hh:mm:ss:)       
WBSEUUID  DIM       10     10       283   WEB User Id Rec. Update (websecaf) 
WBSEBEDV  DIM       1      1        293   Bed Board View Number              
WBSEGPAC  DIM       1      1        294   GP Access - blank=all 1=no override
WBSEROLE  DIM       10     10       295   Role                               
WBSESECT  DIM       1      1        305   Security Id Type 0=User Profile    
WBSEVH1L  DIM       1      1        306   View H1 Security Level             
WBSEAH1L  DIM       1      1        307   Add H1 Security Level              
WBSEVH2L  DIM       1      1        308   View H2 Security Level             
WBSEAH2L  DIM       1      1        309   Add H2 Security Level              
WBSEVH3L  DIM       1      1        310   View H3 Security Level             
WBSEAH3L  DIM       1      1        311   Add H3 Security Level              
WBSEVH4L  DIM       1      1        312   View H4 Security Level             
WBSEAH4L  DIM       1      1        313   Add H4 Security Level              
WBSEVH5L  DIM       1      1        314   View H5 Security Level             
WBSEAH5L  DIM       1      1        315   Add H5 Security Level              
WBSEVH6L  DIM       1      1        316   View H6 Security Level             
WBSEAH6L  DIM       1      1        317   Add H6 Security Level              
WBSEVH7L  DIM       1      1        318   View H7 Security Level             
WBSEAH7L  DIM       1      1        319   Add H7 Security Level              
WBSEVH8L  DIM       1      1        320   View H8 Security Level             
WBSEAH8L  DIM       1      1        321   Add H8 Security Level              
WBSEVH9L  DIM       1      1        322   View H9 Security Level             
WBSEAH9L  DIM       1      1        323   Add H9 Security Level              
WBSEHHOS  DIM       3      3        324   MR Home Hospital Id. (pathspaf)    
WBSEOPDU  DIM       3      3        327   Outpatient Direct User             
WBSEUTLH  DIM       3      3        330   Usual Tracking Location Hospital   
WBSEUTLC  DIM       4      4        333   Usual Tracking Location            
WBSEEMAI  DIM       80     80       337   User Email Address                 
WBSELOGN  DIM       80     80       417   Long Login Name
WBSESPAR  DIM       100    100      497   Spare                              
.
.End of Record                      597  
.
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       200
.
DEFPATH   DIM       60
DIM60     DIM       60
DISNUM    FORM      4
.
INPFILE   DIM       8
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
SAVEFLG   FORM      1
.
.
. PROGRAM CONSTANTS
. -----------------
SP60      INIT    "                                                            "
.
.
.
PRGNAM    INIT      "CONVERSION WEBSECFD"
PRGID     INIT      "F04WBSEC"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000              * run c-isam fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
MAIN2000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN0000:               * no
                          MAIN9999                * cancel
.
.         Running c-isam fix
.
MAIN5000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/zip saved file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
          
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run c-isam fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
.
PREP0000  MOVE      "websecaf",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat websecaf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "swbsecaf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat websecaf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "swbsecaf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "websecaf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "swbsecaf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "websecaf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " 597 UC1-10",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC69-108,1-10",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC47-50,1-10",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC201-201,1-10",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC202-209,1-10",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC305-305,1-10",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC417-496",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      WEBSECA1,NEWFILE
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
            ELSE
              DISPLAY   "has caused an IO error - ";
            ENDIF
          ENDIF
          DISPLAY   "File will be bypassed"
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY10,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          PACK      WBSEEMAI,SP70,SP70
          PACK      WBSELOGN,WBSEUID,SP70,SP10
          PACK      WBSESPAR,SP70,SP70
          CALL      WRWBSE1                       * write to new file
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     WEBSECA1                      * close new file
          CLOSE     OLDFILE1                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS:
                    *P1:24;
          CALL      HITENTER
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
.
VALD0000  MOVE      ZERO,EXIT
.
          SQUEEZE   DIM60
          eNDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or zip save file                                     *
.**********************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "swbsecaf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "gzip ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not zipped or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:24,*EL,*B,"Finished processing  ",CDATE,SP1,CTIMEIS:
                    ".  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.*        DEFT0000 -  Get the default directory                       *
.**********************************************************************
.
DEFT0000  EXECUTE   "ldat websecaf.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding 'websecaf'.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
.         New I/O Routines
.
READSOLD  RESET     KEY10                                                       
          READ      OLDFILE1,KEY10;;                                            
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;WBSEUID,WBSENAM,WBSEDOC,WBSEPCD,WBSEWRD:           
                        WBSESIT,WBSECLI,WBSEOCG,WBSEDIR,WBSEESC,WBSEEUC:        
                        WBSEACT,WBSEEXP,WBSEPSE,WBSECNT,WBSEAUT,WBSEAGE:        
                        WBSEDFM,WBSEGPCD,WBSEGPPC,WBSEGPCN,WBSEMESA,WBSELACD:   
                        WBSELACT,WBSEDEPT,WBSECXA,WBSEDALL,WBSEDTYP,WBSEHLOC:   
                        WBSEVWSL,WBSEADSL,WBSEDGSI,WBSEFPCD,WBSECASH,WBSEMRTS:  
                        WBSEHOSP,WBSELTYP,WBSEFDEP,WBSENURS,WBSECDAT,WBSECTIM:  
                        WBSECUID,WBSEUDAT,WBSEUTIM,WBSEUUID,WBSEBEDV,WBSEGPAC:  
                        WBSEROLE,WBSESECT,WBSEVH1L,WBSEAH1L,WBSEVH2L,WBSEAH2L:  
                        WBSEVH3L,WBSEAH3L,WBSEVH4L,WBSEAH4L,WBSEVH5L,WBSEAH5L:  
                        WBSEVH6L,WBSEAH6L,WBSEVH7L,WBSEAH7L,WBSEVH8L,WBSEAH8L:  
                        WBSEVH9L,WBSEAH9L,WBSEHHOS,WBSEOPDU,WBSEUTLH,WBSEUTLC:  
                        OLDSPARE                                        
          GOTO      OVERCOND IF OVER
          RETURN
.
WRWBSE1   RESET     KEY10                                                       
          WRITE     WEBSECA1,KEY10;WBSEUID,WBSENAM,WBSEDOC,WBSEPCD,WBSEWRD:     
                        WBSESIT,WBSECLI,WBSEOCG,WBSEDIR,WBSEESC,WBSEEUC:        
                        WBSEACT,WBSEEXP,WBSEPSE,WBSECNT,WBSEAUT,WBSEAGE:        
                        WBSEDFM,WBSEGPCD,WBSEGPPC,WBSEGPCN,WBSEMESA,WBSELACD:   
                        WBSELACT,WBSEDEPT,WBSECXA,WBSEDALL,WBSEDTYP,WBSEHLOC:   
                        WBSEVWSL,WBSEADSL,WBSEDGSI,WBSEFPCD,WBSECASH,WBSEMRTS:  
                        WBSEHOSP,WBSELTYP,WBSEFDEP,WBSENURS,WBSECDAT,WBSECTIM:  
                        WBSECUID,WBSEUDAT,WBSEUTIM,WBSEUUID,WBSEBEDV,WBSEGPAC:  
                        WBSEROLE,WBSESECT,WBSEVH1L,WBSEAH1L,WBSEVH2L,WBSEAH2L:  
                        WBSEVH3L,WBSEAH3L,WBSEVH4L,WBSEAH4L,WBSEVH5L,WBSEAH5L:  
                        WBSEVH6L,WBSEAH6L,WBSEVH7L,WBSEAH7L,WBSEVH8L,WBSEAH8L:  
                        WBSEVH9L,WBSEAH9L,WBSEHHOS,WBSEOPDU,WBSEUTLH,WBSEUTLC:  
                        WBSEEMAI,WBSELOGN,WBSESPAR                                                
          RETURN
.
.
          INC       STD001IO
