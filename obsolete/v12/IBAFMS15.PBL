. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS15                                             *
. * Desc      :   Accounting Codes File Maintenance                    *
. **********************************************************************
. * Date      :   20.09.1990                                           *
. * Author    :   Neeriem Dye (IBA)                                    *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
.$$$$$$
. PROGRAM IBAFMS15
.    open files and initialise variables
.    WHILE user wants to add/modify/delete data DO
.       get desired option (maintain categories or codes)
.       CASE option OF
.          CATEGORY : WHILE user wants to add/modify category DO
.                        get desired option
.                        CASE option OF
.                           ADD     : WHILE user wants to add records DO
.                                        accept initial values
.                                        WHILE user wants modify screen data DO
.                                             get number of item to be modified
.                                             allow user to modify option
.                                        END WHILE
.                                        IF user wants to post data
.                                             THEN write new data
.                                        END IF
.                                     END WHILE
.                           CHANGE  : WHILE user wants to modify records DO
.                                        read in desired category
.                                        WHILE user wants modify screen data DO
.                                             get number of item to be modified
.                                             allow user to modify option
.                                        END WHILE
.                                        IF user wants to post data
.                                             THEN update data
.                                        END IF
.                                     END WHILE
.                      END CASE
.                   END WHILE
.          CODE     : WHILE user wants to add/modify/delete code DO
.                        get desired option
.                        CASE option OF
.                           ADD     : WHILE user wants to add records DO
.                                        accept initial values
.                                        WHILE user wants modify screen data DO
.                                             get number of item to be modified
.                                             allow user to modify option
.                                        END WHILE
.                                        IF user wants to post data
.                                             THEN write new data
.                                        END IF
.                                     END WHILE
.                           CHANGE  : WHILE user wants to modify records DO
.                                        read in desired category/code
.                                        WHILE user wants modify screen data DO
.                                             get number of item to be modified
.                                             allow user to modify option
.                                        END WHILE
.                                        IF user wants to post data
.                                             THEN update data
.                                        END IF
.                                     END WHILE
.                           DELETE  : WHILE user wants to delete records DO
.                                        read in desired category/code
.                                        IF user wants to post data
.                                             THEN update data
.                                        END IF
.                                     END WHILE
.                      END CASE
.                   END WHILE
.       END CASE
.    END WHILE
. END
.$$$$$$
+
          INC       FMSSTDDF
          INC       FMSCODFD/INC
.
MODELAB   INIT      "      "                     * mode labels
INSERT    INIT      "Insert"
CHANGE    INIT      "Change"
DELETE    INIT      "Delete"
.
CHOICE    FORM      1
DESCRIPT  DIM       20
SAVECATY  DIM       2
XTOP      FORM      1
CATDESC   DIM       20
.
PRGID     INIT      "IBAFMS15"
PRGNAM    INIT      "Accounting Codes Maintenance"
VERSION   INIT      "V12.00.00"
+
          CALL      OPEN0000                     *Open Files
          CALL      DISPHEAD                     *Screen Header
MAIN1000  CALL      SELN0000                     *Select: Categories or Codes
          COMPARE   ZERO,CHOICE                  *If Exit Chosen
          GOTO      MAIN9999 IF EQUAL
          BRANCH    CHOICE,MAIN2000,MAIN2000
          GOTO      MAIN1000
MAIN2000  CALL      MODE0000                     *Insert or Change
          COMPARE   ZERO,COPTION                 *If Exit Chosen
          GOTO      MAIN1000 IF EQUAL
          BRANCH    COPTION,MAIN3000,MAIN4000,MAIN5000
          GOTO      MAIN2000
MAIN3000  CALL      ADDN0000                     *Insert
          GOTO      MAIN2000
MAIN4000  CALL      CHAN0000                     *Change
          GOTO      MAIN2000
MAIN5000  CALL      DELT0000                     *Delete
          GOTO      MAIN2000
MAIN9999  CHAIN     PGM
+
. ***********************************************
. OPEN FILES           
. ***********************************************
OPEN0000  DISPLAY   *P1:24,*EL,"Opening fmscodaf";
          OPEN      FMSCODA1,"fmscodaf"
          OPEN      FMSCODA2,"fmscodaf"
.
OPEN9999  RETURN
.
. ***********************************************
. SELN0000 - Select Option 1.Category or 2.Codes
. ***********************************************
SELN0000  DISPLAY   *P1:3,*EF:
                    *P5:5,"Exit":
                    *P5:6,"Categories":
                    *P5:7,"Codes":
                    *P2:5,*V2LON,"0.":
                    *P2:6,"1.":
                    *P2:7,"2."
SELN1000  DISPLAY   *P1:9,*EL,"Please Select : ",UNDLN1
          KEYIN     *P17:9,*V2LON,CHOICE
          COMPARE   "3",CHOICE
          GOTO      SELN1000 IF NOT LESS
          GOTO      SELN9999
SELN9999  RETURN
.
. ***********************************************
. MODE0000 - Select either Insert or Change
. ***********************************************
MODE0000  DISPLAY   *P1:3,*EF:
                    *P70:3,*EL:
                    *P5:5,"Exit":
                    *P5:6,"Insert":
                    *P5:7,"Change":
                    *P2:5,*V2LON,"0.":
                    *P2:6,"1.":
                    *P2:7,"2."
          BRANCH    CHOICE,MODE1000
          DISPLAY   *P2:8,*V2LON,"3.",*HOFF:
                    *P5:8,"Delete"
          GOTO      MODE2000
MODE1000  DISPLAY   *P1:9,*EL,"Please Select : ",UNDLN1
          KEYIN     *P17:9,*V2LON,OPTION
.
          COMPARE   "3",OPTION
          GOTO      MODE1000 IF NOT LESS
          MOVE      OPTION,COPTION
          GOTO      MODE9999
MODE2000  DISPLAY   *P1:10,*EL,"Please Select : ",UNDLN1
          KEYIN     *P17:10,*V2LON,OPTION
.
          COMPARE   "4",OPTION
          GOTO      MODE2000 IF NOT LESS
          MOVE      OPTION,COPTION
          GOTO      MODE9999
MODE9999  LOAD      MODELAB,OPTION,INSERT,CHANGE,DELETE
          DISPLAY   *P70:3,*V2LON,MODELAB;
          RETURN
.
. ***********************************************
. Screen for Categories & Codes
. ***********************************************
RCODFM00 DISPLAY   *P1:4,*EF:
                    *P5:5,"Category              : ":
                    *P30:5,*EL,*V2LON,CATEGORY,SP2,CATDESC,*HOFF:
                    *P5:6,"Code                  : ":
                    *P5:8,"Description           : "
         RETURN
.
RCATFM00  
SCRN0000  DISPLAY   *P1:4,*EF:
                    *P5:5,"Category              : "
.
          BRANCH    CHOICE,SCRN1000
.
          DISPLAY   *P5:6,"Code                  : "
          DISPLAY   *P5:8,"Description           : "
          GOTO      SCRN9999
.  
SCRN1000  DISPLAY   *P5:8,"Description           : ";

SCRN9999  RETURN
.
. ***********************************************
. PROC0000 - Enter Category or Code
. ***********************************************
PROC0000  MOVE      FALSE,XTOP
          MOVE      "30",CCOL                    * Enter Category
          MOVE      "5",CVERT
.
          BRANCH    CHOICE,PROC1000              * modifying category ?
.
          CALL      KCATFM00                     * modifying codes
          GOTO      PROC2000
.
PROC1000  COMPARE   "1",COPTION                  * modifying category
          CALL      ACATFM00 IF EQUAL
          COMPARE   "1",COPTION
          CALL      KCATFM00 IF NOT EQUAL
.
PROC2000  MOVE      FMCDCATY,CATEGORY            * save category
          DISPLAY   *P30:5,*EL,*V2LON,CATEGORY;
          BRANCH    EXIT,PROC9000
          GOTO      PROC8000
.
PROC5000  PACK      KEY5,CATEGORY,SP3
          CALL      RDFMCD1
          DISPLAY   *P30:5,*EL,*V2LON,CATEGORY,SP2,FMCDDESC  
          MOVE      FMCDDESC,CATDESC
.
          MOVE      "30",CCOL                    * Enter Codes
          MOVE      "6",CVERT
          COMPARE   "1",COPTION
          CALL      ACODFM00 IF EQUAL
          COMPARE   "1",COPTION
          CALL      KCODFM00 IF NOT EQUAL
          DISPLAY   *P30:6,*EL,*V2LON,FMCDCODE;
          BRANCH    EXIT,PROC9000
.
PROC8000  MOVE      FALSE,XTOP
          GOTO      PROC9999
.
PROC9000  MOVE      TRUE,XTOP
          GOTO      PROC9999
.
PROC9999  RETURN
. ***********************************************
. ADDN0000 - Add Categories & Codes
. ***********************************************
ADDN0000  CALL      SCRN0000
          CALL      PROC0000
          BRANCH    XTOP,ADDN9999
          COMPARE   "2",CHOICE
          GOTO      ADDN1000 IF NOT EQUAL
ADDN0500  CALL      PROC5000
          BRANCH    XTOP,ADDN0000
ADDN1000  KEYIN     *P30:8,"____________________",*EL:
                    *P30:8,*V2LON,DESCRIPT,*EL;
          PACK      DESCRIPT,DESCRIPT,SP30
          MATCH     SP20,DESCRIPT
          GOTO      ADDN1000 IF EQUAL
          COMPARE   "2",CHOICE
          GOTO      ADDN2000 IF EQUAL
.
ADDN2000  DISPLAY   *P1:8,*V2LON," 1."
.
ADDN2500  CALL      MAINQST
          COMPARE   SEQ,CCITEM                   * Cancel
          GOTO      ADDN4000 IF EQUAL
          COMPARE   "0",CCITEM
          GOTO      ADDN3000 IF EQUAL 
          BRANCH    CCITEM,ADDN2510
          BEEP                                   * illegal option
          GOTO      ADDN2500
.
ADDN2510  KEYIN     *P30:8,"____________________",*EL:
                    *P30:8,*V2LON,DESCRIPT,*EL;
          PACK      DESCRIPT,DESCRIPT,SP30
          MATCH     SP20,DESCRIPT
          GOTO      ADDN2510 IF EQUAL
          GOTO      ADDN2500
.
ADDN3000  COMPARE   "2",CHOICE
          GOTO      ADDN3100 IF EQUAL
          PACK      KEY5,CATEGORY,SP3            * Post for Category
          MOVE      CATEGORY,FMCDCATY
          MOVE      SP3,FMCDCODE
          MOVE      DESCRIPT,FMCDDESC
          MOVE      SP20,FMCDSPAR
          CALL      WRFMCD1
          CALL      CLR0000
          GOTO      ADDN0000
.
ADDN3100  PACK      KEY5,CATEGORY,FMCDCODE       * Post for Cat/Code
          MOVE      CATEGORY,FMCDCATY
          MOVE      DESCRIPT,FMCDDESC
          MOVE      SP20,FMCDSPAR
          CALL      WRFMCD1
          CALL      CLR0000
          GOTO      ADDN0500
.
ADDN4000  CALL      CLR0000
          BRANCH    CHOICE,ADDN0000
          GOTO      ADDN0500
ADDN9999  RETURN
. ***********************************************
. DISP0000 - display data
. ***********************************************
DISP0000  DISPLAY   *P30:8,*EL,*V2LON,FMCDDESC;
.
DISP9999  RETURN
. ***********************************************
. CHAN0000 - Modify Category or Code
. ***********************************************
CHAN0000  CALL      SCRN0000
          CALL      PROC0000
          MOVE      FMCDDESC,DESCRIPT
          BRANCH    XTOP,CHAN9999
          COMPARE   "2",CHOICE
          GOTO      CHAN0500 IF NOT EQUAL
CHAN0400  CALL      PROC5000
          BRANCH    XTOP,CHAN0000
CHAN0500  DISPLAY   *P30:8,*EL,*V2LON,FMCDDESC
          COMPARE   ONE,CHOICE
          GOTO      CHAN0600 IF NOT EQUAL
          CALL      DISP0000
.        
CHAN0600  DISPLAY   *P1:8,*V2LON," 1."
.
CHAN1000  CALL      MAINQST
          COMPARE   SEQ,CCITEM                   * Cancel
          GOTO      CHAN4000 IF EQUAL
          COMPARE   "0",CCITEM
          GOTO      CHAN3000 IF EQUAL 
          BRANCH    CCITEM,CHAN1100
.
          BEEP                                   * illegal option
          GOTO      CHAN1000
.
CHAN1100  KEYIN     *P30:8,"____________________",*EL:
                    *P30:8,*V2LON,DESCRIPT,*EL;
          PACK      DESCRIPT,DESCRIPT,SP30
          MATCH     SP20,DESCRIPT
          GOTO      CHAN1100 IF EQUAL
          GOTO      CHAN1000
.
CHAN3000  COMPARE   "2",CHOICE
          GOTO      CHAN3100 IF EQUAL
          PACK      KEY5,CATEGORY,SP3            * Post for Category
          CALL      RAFMCD1
          MOVE      DESCRIPT,FMCDDESC
          CALL      UPFMCD1
          CALL      CLR0000
          GOTO      CHAN0000
.
CHAN3100  PACK      KEY5,CATEGORY,FMCDCODE       * Post for Cat/Code
          CALL      RAFMCD1
          MOVE      DESCRIPT,FMCDDESC
          CALL      UPFMCD1
          CALL      CLR0000
          GOTO      CHAN0400
.
CHAN4000  CALL      CLR0000
          BRANCH    CHOICE,CHAN0000
          GOTO      CHAN0400
.
CHAN9999  RETURN
.
. ***********************************************
. CHAN0000 - Modify Category or Code
. ***********************************************
CLR0000   DISPLAY   *P1:8,SP3:
                    *P30:8,*EF
CLR9999   RETURN
.
. ***********************************************
. DELT0000 - Add Categories & Codes
. ***********************************************
DELT0000  CALL      SCRN0000
          MOVE      TWO,COPTION
          CALL      PROC0000
          BRANCH    XTOP,DELT9999
DELT0500  CALL      PROC5000
          BRANCH    XTOP,DELT0000
          CALL      DISP0000
DELT2000  CALL      DELQST
          MATCH     "1",ANS                      * Yes
          GOTO      DELT3000 IF EQUAL
          MATCH     "2",ANS                      * No
          GOTO      DELT4000 IF EQUAL
          MATCH     "3",ANS                      * Cancel
          GOTO      DELT9999 IF EQUAL
          BEEP
          GOTO      DELT2000
.
DELT3000  PACK      KEY5,CATEGORY,FMCDCODE       * Post for Cat/Code
          CALL      DEFMCD1
DELT4000  CALL      CLR0000
          GOTO      DELT0500
DELT9999  RETURN
.
.---------------------------------
. Include Standard Accounting Code
.---------------------------------
          INC       FMSSTDCD
.
          INC       FMSCODIO/INC
          INC       FMSCODKY
