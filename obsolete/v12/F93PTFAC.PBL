.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   F93PTFAC                                                  *
.* Desc      :   Fix program for Patient Future Actions file (patfactf).   *
.***************************************************************************
.* Date      :   08/06/2004                                                *
.* Author    :   Sylvek Litewka  CAR 50371                                 *
.* Function  :   This program will populate new fields patfactf.PTFCURNO   *
.*               and patfactf.PTFCDTCP. The PTFCURNO will be populated     *
.*               with Patient UR number based on Patient Admission number. *
.*               The PTFCDTCP (Date Action Completed) will be set to the   *
.*               Future Action Date if Future Action Date is before the    *
.*               Current date where current date is the date this program  *
.*               is ran. In effect this will complete all Future Actions   *
.*               dated prior to the date of running this program.          *
.*               All Future Actions with date equal to or greater than     *
.*               current date will have date completed set to SPACES       *
.*               indicating that these actions are still outstanding.      *
.*                                                                         *
.* Modifications :                                                         *
.*     V9.03.00  08.06.2004  Sylvek Litewka  CAR 50371                     *
.*               Created fix program.                                      *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATFACFD/INC
          INC       PATVISFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
RECNUM    FORM      8                       * Record counter
CURRDATE  DIM       8                       * Current Date
.
PRGID     INIT      "F93PTFAC"
PRGNAM    INIT      "Fix program for patfactf"
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                * Display heading
.
          MOVE      ZERO,RECNUM             * Initialize Record counter
.
          CALL      IBACLOCK                * Get current date
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          OPEN      PATFACT1,"patfactf"
          OPEN      PATVISA1,"patvisaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run patfactf fix - populate":
                                           " UR Number and Date Completed":
                                           " fields."
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  PACK      KEY16,SP70
          CALL      RDSFACT1
PROC0100  CALL      RDKFACT1
          BRANCH    OVRCD,PROC9999
          ADD       ONE,RECNUM                   * Update record counter
.
          PACK      KEY8,DFADMNO,SP70            * Get UR number from Admis file
          CALL      RDVISA1
          BRANCH    OVRCD,PROC0100
.
          MOVE      PVIURNO,PTFCURNO             * Populate patfactf.PTFCURNO
.
          MATCH     CURRDATE,FADATE              * Is FADATE < CURRDATE?
          IF        @LESS
            MOVE      FADATE,PTFCDTCP            * Yes, set action as complete
          ELSE
            MOVE      SP70,PTFCDTCP              * No, clear Date Complete
          ENDIF
.
          MOVE      SP70,PTFCSPAR                * Clear SPARE variable
.
          CALL      UPFACT1                      * Update patfactf.
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      PROC0100
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATFACIO/INC
          INC       PATVISIO/INC
