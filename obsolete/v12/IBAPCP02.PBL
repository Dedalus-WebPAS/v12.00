.***************************************************************************
.*                                                                         * 
.*   Program       :  IBAPCP02                                             *
.*                                                                         *
.*   Description   :  Diagnosis category maintenance                       *
.*                                                                         * 
.*   Date          :  12/10/89                                             *
.*                                                                         *
.*   Programmer    :  Glen Hobby is a spasitc                              *
.*   Modifications :                                                       *
.*        V9.02.00  30/12/2002 Tracey Nguyen srf 23497                     *
.*                  r5.10 Care Plans Port to V9                            *
.*-------------------------------------------------------------------------*
.*       V5.02.01     02/02/1995  Whirlpool                                *
.*                    Fixed screen display                                 *
.***************************************************************************
.
.$$$$$$
.
.$$$$$$
          INC       STD001FD
          INC       PCPDSCFD/INC             * description file
          INC       PCPCONFD/INC             * control file
.
.***************************************************************************
.*                              CONSTANTS                                  *
.***************************************************************************
SP60      INIT    "                                                            "
.
PRGID     INIT      "IBAPCP02"               * program id
PRGNAM    INIT      "Diagnosis Category Maintenance" * program name
VERSION   INIT      "V12.00.00"
.
.***************************************************************************
.*                            GLOBAL VARIABLES                             *
.***************************************************************************
.
.***************************************************************************
.*                              MAINLINE                                   *
.*                            Controlling Logic                            *
.***************************************************************************
ML0000    CALL      INIT0000                 * display header and open files
.
ML1000    CALL      OPT0000                  * Insert, Change or Delete 
          BRANCH    OPTION,ML2000,ML3000,ML4000
          GOTO      ML9999                   * exit out 
.
. ------ Insert ------
.
ML2000    CALL      CLR0000                  * clear variables 
          CALL      SCR0000                  * display screen layout 
.
ML2100    CALL      RECD0000                 * get record type
          BRANCH    EXIT,ML1000              * user pressed return
          CALL      CATC0000                 * get category code
          BRANCH    EXIT,ML2100              * user pressed return
          CALL      CHCK0000                 * check for valid type & code
          BRANCH    EXIT,ML2150              * invalid entry
          CALL      DESC0000                 * get description
          BRANCH    EXIT,ML2200,ML2000       * Post or Cancel
.
ML2150    CALL      ERR0000                  * record already on file
          GOTO      ML2000
.
ML2200    CALL      AUDA0000 
          CALL      POST0000
          GOTO      ML2000
.
. ------ Change ------
.
ML3000    CALL      CLR0000                  * clear variables 
          CALL      SCR0000                  * display screen layout
.
ML3100    CALL      RECD0000                 * get record type
          BRANCH    EXIT,ML1000              * user pressed return
          CALL      CATC0000                 * get category code
          BRANCH    EXIT,ML3100              * user pressed return
          CALL      CHCK0000                 * check for valid type & code
          BRANCH    EXIT,ML3150
          CALL      AERR0000                 * record not on file
          GOTO      ML3000
.
ML3150    CALL      DISP0000                 * display description
          CALL      AUDB0000
          BRANCH    EXIT,ML3160,ML3200,ML3155 * Select item, Post or Cancel
.
ML3155    CALL      ADBD0000
          GOTO      ML3000
.
ML3160    CALL      DESC0000
          BRANCH    EXIT,ML3200,ML3155       * Post or Cancel
.
ML3200    CALL      DEL0000
          CALL      AUDC0000
          CALL      POST0000
          GOTO      ML3000
.
.
. ------ Delete ------
.
ML4000    CALL      CLR0000                  * clear variables 
          CALL      SCR0000                  * display screen layout
.
ML4100    CALL      RECD0000                 * get record type
          BRANCH    EXIT,ML1000              * user pressed return
          CALL      CATC0000                 * get category code
          BRANCH    EXIT,ML4100              * user pressed return
          CALL      CHCK0000                 * check for valid type & code
          BRANCH    EXIT,ML4150
          CALL      AERR0000                 * record not on file
          GOTO      ML4000
.
ML4150    CALL      DISP0000                 * display description
          BRANCH    EXIT,ML4200,ML4000,ML1000 * Delete or Cancel
.
ML4200    CALL      AUDD0000 
          CALL      DEL0000
          GOTO      ML4000
.
ML9999    CHAIN     PGM                      * chain back to program
.
.*************************************************************************
.*                                  INIT0000                             *
.*                 Display screen headings and open files                *
.*************************************************************************
INIT0000  CALL      DISPHEAD                 * display screen headings
.
          DISPLAY   *P60:24,"pcpdscaf";
          OPEN      PCPDSCA1,"pcpdscaf" 
.
          DISPLAY   *P60:24,*EL,"controlf.txt"
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,FORTY1;*52,PCCNAUDB
.
          BRANCH    PCCNAUDB,INIT9999
.
          DISPLAY   *P60:24,*EL,"pcpaudds";
          OPEN      PCPAUDDS,"pcpaudds";
.
.
INIT9999  RETURN
.
.**************************************************************************
.*                                  CLR0000                               *
.*  Called by : - ML2000       Clear variables                            *
.*                ML3000                                                  *
.*                ML4000                                                  *
.**************************************************************************
CLR0000  MOVE       ZERO,PCDSTYPE
         MOVE       SP60,PCDSDESC 
.
CLR9999  RETURN
.
.*************************************************************************
.*                                  OPT0000                              *
.*  Called by : - ML1000          Get option                             *
.*************************************************************************
.
OPT0000   HLINE     *G37,2,55,80
          DISPLAY   *P2:4,*EF,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = Insert":
                    *P2:6,*V2LON,TWO,*HOFF," = Change":
                    *P2:7,*V2LON,THREE,*HOFF," = Delete"
.
OPT1000   KEYIN     *P2:9,*EL,"Select Option : _",*P18:9,*V2LON,OPTION  
.
          COMPARE   ZERO,OPTION
          GOTO      OPT9999 IF EQUAL  
.
          BRANCH    OPTION,OPT9999,OPT9999,OPT9999
          BEEP   
          GOTO      OPT1000                  * incorrect keyin
.
OPT9999   RETURN
.
.************************************************************************
.*                                 SCR0000                              *
.*  Called by : - ML2000         Display screen layout                  *
.*                ML3000                                                *
.*                ML4000                                                *
.************************************************************************
SCR0000   BRANCH    OPTION,SCR1000,SCR2000,SCR3000
.
SCR1000   DISPLAY   *P55:2,*V2LON,"- Insert ";
          GOTO      SCR4000
.
SCR2000   DISPLAY   *P55:2,*V2LON,"- Change ";
          GOTO      SCR4000
.
SCR3000   DISPLAY   *P55:2,*V2LON,"- Delete ";
.
SCR4000   DISPLAY   *P1:3,*EF,*P4:4,"Record Type   :":
                    *P4:6,"Category Code :":
                    *P1:8,ONE,DOT," Description   :" 
.
SCR9999   RETURN
.
.****************************************************************************
.*                                 RECD0000                                 *
.*  Called by : - ML2100       Get record type                              *
.*                ML3100                                                    *
.*                ML4100                                                    *
.**************************************************************************** 
RECD0000  MOVE      ZERO,EXIT                * initialize 
          MOVE      ZERO,PCDSTYPE
          KEYIN     *P20:4,*EL,*DV,UNDLN1,*P20:4,*V2LON,ANS
.
          MATCH     QUEST,ANS
          CALL      QTMK0000 IF EQUAL
.
          MOVE      ANS,PCDSTYPE

          BRANCH    PCDSTYPE,RECD10000,RECD2000,RECD3000,RECD4000,RECD5000
.
          COMPARE   ZERO,PCDSTYPE
          GOTO      RECD9000 IF EQUAL        * user pressed return
.
          BEEP                               * incorrect keyin
          GOTO      RECD0000
.
RECD1000  DISPLAY   *P30:4,"Type of Clinic Condition"
          GOTO      RECD9999
.
RECD2000  DISPLAY   *P30:4,"Clinic Condition"
          GOTO      RECD9999
.
RECD3000  DISPLAY   *P30:4,"Health Pattern"
          GOTO      RECD9999
.
RECD4000  DISPLAY   *P30:4,"Response Pattern"
          GOTO      RECD9999
.
RECD5000  DISPLAY   *P30:4,"ABC PRN REST code"
          GOTO      RECD9999
.
RECD9000  MOVE      ONE,EXIT                 * set flag
.
RECD9999  RETURN
.
.**************************************************************************
.*                             QTMK0000                                   *
.*             Question mark routine for record type                      *
.**************************************************************************
.
QTMK0000  DISPLAY   *P1:3,*EF:
                    *P29:4,*V2LON,UNDLN12:
                    *P29:4,"RECORD TYPES":
                    *P25:6,*V2LON,ONE,*HOFF,". Type of Clinic Condition":
                    *P25:7,*V2LON,TWO,*HOFF,". Clinic Condition":
                    *P25:8,*V2LON,THREE,*HOFF,". Health Pattern":
                    *P25:9,*V2LON,FOUR,*HOFF,". Response Pattern":
                    *P25:10,*V2LON,FIVE,*HOFF,". ABC PRN REST code"
.
          DISPLAY   *P1:24,"Record Type : "
          KEYIN     *P15:24,PCDSTYPE
.
          COMPARE    ZERO,PCDSTYPE
          GOTO      QTMK9999 IF EQUAL
.
          DISPLAY   *P1:3,*EF:
                    *P4:4,"Record Type    :",PCDSTYPE:
                    *P4:6,"Category Code  :":
                    *P1:8,ONE,DOT," Description    :"
.
QTMK9999  RETURN
                              
 
.
.**************************************************************************
.*                             CATC0000                                   *
.*  Called by : - ML2100      Get category code                           *
.*                ML3100                                                  *
.*                ML4100                                                  *
.**************************************************************************
CATC0000  MOVE      ZERO,EXIT                * initialize 
          KEYIN     *P20:6,*DV,UNDLN9,*P20:6,*V2LON,PCDSCODE,*P20:6,*DV,PCDSCODE
.
          ENDSET    PCDSCODE                 * pad out with spaces
          APPEND    SP9,PCDSCODE
          RESET     PCDSCODE
.
          MATCH     SP9,PCDSCODE
          GOTO      CATC9999 IF NOT EQUAL
.
          MOVE      ONE,EXIT
.
CATC9999  RETURN
.
.*************************************************************************
.*                              CHCK0000                                 *
.*  Called by : - ML2100  Check if record type and category code are on  *
.*                        file.                                          *
.*************************************************************************
CHCK0000  MOVE      ZERO,EXIT               * initialize 
          MOVE      ONE,PCDSLINE
.
          PACK      KEY13,PCDSTYPE,PCDSCODE,PCDSLINE
.
          CALL      RAPCDSC1
          BRANCH    OVRCD,CHCK9999           * record is not on file
          MOVE      ONE,EXIT                 * record is on file
.
CHCK9999  RETURN
.
.**************************************************************************
.*                                 DESC0000                               *
.*  Called by : - ML2100      Get description                             *
.*                ML3100                                                  *
.*                ML4100                                                  *
.**************************************************************************
DESC0000  KEYIN     *P1:24,*EL,*P20:8,*DV,UNDLN30,*DV,UNDLN30:
                    *P20:8,*V2LON,PCDSDESC,*P20:8,*DV,PCDSDESC
.
          ENDSET    PCDSDESC                     * pad out with spaces
          APPEND    SP60,PCDSDESC
          RESET     PCDSDESC
.
          MATCH     SP60,PCDSDESC                * user pressed return
          GOTO      DESC1000 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"A description must be entered.  ";
          CALL      HITENTER
          GOTO      DESC0000
.
DESC1000  CALL      MAINQST
          BRANCH    CCITEM,DESC0000           * select item
          COMPARE   ZERO,CCITEM
          GOTO      DESC2000 IF EQUAL         * Post
          COMPARE   "-1",CCITEM               
          GOTO      DESC3000 IF EQUAL         * Cancel
          BEEP
          GOTO      DESC1000                  * Invalid entry
.
DESC2000  MOVE      ONE,EXIT
          GOTO      DESC9999
.
DESC3000  MOVE      TWO,EXIT
.
DESC9999  RETURN
.
.***************************************************************************
.*                               ERR0000                                   *
.*  Called by : - ML2150     Record on file error message                  *
.***************************************************************************
ERR0000   DISPLAY   *P1:24,*EL,*B,"This Record Type and Category Code is ":
                    "already on file.  ";
          CALL      HITENTER
.
ERR9999   RETURN
.
.***************************************************************************
.*                                POST0000                                 *
.*  Called by : - ML2200      Post away data                               *
.***************************************************************************
POST0000  DISPLAY   *P60:24,"*** POSTING ***"; 
          MOVE      ONE,PCDSLINE
          PACK      KEY13,PCDSTYPE,PCDSCODE,PCDSLINE
.
          CALL      WRPCDSC1
.
POST9999  RETURN
.
.***************************************************************************
.*                              AERR0000                                   *
.*  Called by : - ML3100     Record not on file error message              *
.***************************************************************************
AERR0000   DISPLAY   *P1:24,*EL,*B,"This Record Type and Category Code is ":
                    "not on file.  ";
          CALL      HITENTER
.
AERR9999   RETURN
.
.***************************************************************************
.*                               DISP0000                                  *
.*  Called by : - ML3100     Display description from file                 *
.***************************************************************************
DISP0000  PACK      KEY13,PCDSTYPE,PCDSCODE,PCDSLINE
          CALL      RDPCDSC1                 * OVRCD will not occur
. 
          DISPLAY   *P20:8,*V2LON,PCDSDESC
          BRANCH    OPTION,DISP1000,DISP1000,DISP2000
.
DISP1000  CALL      MAINQST                  * change mode
          COMPARE   ONE,CCITEM
          GOTO      DISP1100 IF EQUAL        * select item
          COMPARE   ZERO,CCITEM
          GOTO      DISP1200 IF EQUAL        * post
          COMPARE   "-1",CCITEM
          GOTO      DISP1300 IF EQUAL        * cancel
          BEEP      
          GOTO      DISP1000                 * incorrect keyin
.
DISP1100  MOVE      ONE,EXIT
          GOTO      DISP9999
.
DISP1200  MOVE      TWO,EXIT
          GOTO      DISP9999
.
DISP1300  MOVE      THREE,EXIT
          GOTO      DISP9999
.
DISP2000  CALL      DELQST                   * delete mode
          MOVE      ANS,EXIT
.
DISP9999  RETURN
.
.***************************************************************************
.*                              DEL0000                                    *
.*  Called by : - ML4200     Delete record                                 *
.***************************************************************************
DEL0000   DISPLAY   *P60:24,"*** DELETING ***"; 
          MOVE      ONE,PCDSLINE
          PACK      KEY13,PCDSTYPE,PCDSCODE,PCDSLINE
.
          CALL      DEPCDSC1
.
DEL9999   RETURN
.
.
.***************************************************************************
.*                              STRT0000                                   *
.*                    Set up variables for audit files                     *
.***************************************************************************
STRT0000  CALL      IBACLOCK
.
          MOVE      CCC,CCENT                * current century 
          MOVE      CYY,CYEAR                * current year
          MOVE      CMM,CMON                 * current month
          MOVE      CDD,CDAY                 * current day
          PACK      PCDSAUDD,CCENT,CYEAR,CMON,CDAY
.
          REP       " 0",PCDSAUDD            * zero fill
.
          CLOCK     TIME,PCDSAUDT            * time of change
          MOVE      PORT,PCDSAUDP            * port doing change
          MOVE      PASSCODE,PCDSAUDO        * operator id that did the change
.
STRT9999  RETURN
.
.**************************************************************************
.*                              AUDA0000                                  *
.*                        Write to audit file                             *
.**************************************************************************
AUDA0000  BRANCH    PCCNAUDB,AUDA9999        * audit facility not wanted
.
          CALL      STRT0000
.
          MOVE      ANSA,PCDSAUDR            * record type
.
          PACK      KEY19,PCDSAUDD,PCDSAUDT,PCDSAUDP,PCDSAUDR
.
          CALL      AWPCDSC
.
AUDA9999  RETURN
.
.**************************************************************************
.*                              AUDB0000                                  *
.*                          Audit facility                                *
.**************************************************************************
AUDB0000  BRANCH    PCCNAUDB,AUDB9999        * audit facility not wanted
.
          CALL      STRT0000
.
          MOVE      ANSB,PCDSAUDR            * record type
.
          PACK      KEY19,PCDSAUDD,PCDSAUDT,PCDSAUDP,PCDSAUDR
.
          CALL      AWPCDSC
.
AUDB9999  RETURN
.
.**************************************************************************
.*                              AUDC0000                                  *
.*                          Audit facility                                *
.**************************************************************************
AUDC0000  BRANCH    PCCNAUDB,AUDC9999        * audit facility not wanted
.
          CALL      STRT0000
.
          MOVE      ANSC,PCDSAUDR            * record type
.
          PACK      KEY19,PCDSAUDD,PCDSAUDT,PCDSAUDP,PCDSAUDR
.
          CALL      AWPCDSC
.
AUDC9999  RETURN
.
.**************************************************************************
.*                              AUDD0000                                  *
.*                          Audit facility                                *
.**************************************************************************
AUDD0000  BRANCH    PCCNAUDB,AUDD9999        * audit facility not wanted
.
          CALL      STRT0000
.
          MOVE      ANSD,PCDSAUDR            * record type
.
          PACK      KEY19,PCDSAUDD,PCDSAUDT,PCDSAUDP,PCDSAUDR
.
          CALL      AWPCDSC
.
AUDD9999  RETURN
.
.**************************************************************************
.*                              ADBD0000                                  *
.*  This subroutine will delete an entry in the audit file that was       *
.*  written by AUDB0000.  This is necessary if the user chooses to cancel *
.*  in the change option rather than post away the changes.               *
.*  There is no need to PACK KEY19 because it will not have been changed  *
.*  from what it was set to in AUDB0000.                                  *
.*                                                                        *
.**************************************************************************
ADBD0000  BRANCH    PCCNAUDB,ADBD9999        * audit facility not wanted
.
          CALL      ADPCDSC
.
ADBD9999  RETURN
.
.----------------------------------------------------------------------------
          INC       STD001IO
          INC       PCPDSCIO/INC
