.******************************************************************************
.* System         : Operating Rooms    Release 5.01                           *
.*                                                                            *
.* Include Name   : OPINPREC.PBL                                              *
.*                                                                            *
.* Function       : Input Operation records for a booking and post them to    *
.*                  the OPRRECFD file                                         *
.*                                                                            *
.* Author         : Justin Coates  20/11/1990                                 *
.*                                                                            *
.* Parameters     : Current session record (OPRSESFD) -> CURSESS              *
.*                : Current case number               -> CURCASE              *
.*                                                                            *
.* Includes Req'd : OPRSESFD/IO : OPRRECFD/IO : OPRDEBFD/IO : OPR001FD/INC    *
.*                  OPRDEAFD/IO : PATMA1FD/IO : PATMI1FD/IO : BOKRC1FD/IO     *
.*                  OPRDEBDS/PBL: OPPRIREC/PBL:                               *
.*                                                                            *
.* Open files     : OPRSESA1 : OPRRECA1 : OPRDETA1 : OPRDETB1 : PATMA1A1      *
.*                  PATMI1A1 : BOKRC1A1 :                                     *
.*                                                                            *
.* On Return      : Redisplay the screen                                      *
.*                                                                            *
.*   Modifications :                                                          *
.*   V11.03.01 06/03/2023  Ebon Clements     TSK 0909393                      *
.*             Added hospital to oprsesaf(1-6) and oprdetfa(1&5) indexes      *
.*                : 14/02/91 Graeme Williams                                  *
.*                  Added print of operation records                          *
.*                  03/04/1991    Justin Coates                               *
.*                  Changed to use OPPRIREC for printing of operation records *
.******************************************************************************
.
.$$$$$
.         OPINPREC.PBL        Input Operation records for a booking
.         ------------        -------------------------------------
.         GTEAM000 - Get the operating team
.         SETWF000 - Set up the work file
.         WPROC000 - Invoke the word processor
.         TRCOM000 - Transfer the comments to oprrecaf
.         OPPRIREC - Print operation records
.$$$$$
.
OPINPREC  DISPLAY   *PSUBOPT1:2,*V2LON,"- (Input Operation Records) ";
.
IPREC000  CALL      GTEAM000                * get the team to input for
          BRANCH    EXIT,IPREC999           * invalid details
.
          CALL      SETWF000                * set up the work file
          CALL      WPROC000                * execute the word processor
          CALL      TRCOM000                * transfer comments to oprrecaf
          CALL      OPPRIREC                * print operation records
.
IPREC999  RETURN
+
.*********************************************************************
.*                  GTEAM000                    Called by : IPREC000 *
.*        Get the team number to input records for                   *
.*        Returns : OPDBTEAM      the team number                    *
.*                  EXIT = 1      invalid details                    *
.*********************************************************************
GTEAM000  MOVE      ONE,OPDBTEAM            * default team
.
          PACK      KEY26,CURSESS,ZERO,CURCASE
          CALL      RDOPDEA1                * read the details file
          BRANCH    OVRCD,GTEAM900          * invalid case number
.
.         check visit number is valid and get the patients name 
.
          MOVE      OPDAADMN,KEY8           * admission number is key
          CALL      RDMISS1                 * read admission file
          BRANCH    OVRCD,GTEAM010          * try booking file
.
          GOTO      GTEAM020                * valid visit number
.
.         read booking file for UR number
.
GTEAM010  CALL      RDBKREC1                * read booking master file
          BRANCH    OVRCD,GTEAM920          * invalid admission number
          MOVE      BKREBOOK,AADMNO         * set admission number
          MOVE      BKREURNO,AURNO          * set up UR number
          MOVE      BKREWARD,AWARD          * set up ward
.
.         see if patient on master or pre-admission file
.
GTEAM020  MATCH     ZEROUR,AURNO
          GOTO      GTEAM030 IF EQUAL       * on pre-admission file
.
.         on master file
.
          MOVE      AURNO,KEY8              * UR number is key
          CALL      RDMAST1                 * rad master file
          BRANCH    OVRCD,GTEAM930          * invalid UR number
          GOTO      GTEAM040
.
.         on pre-admission file
.
GTEAM030  CALL      RDPRAM1                 * pre-admission file
          BRANCH    OVRCD,GTEAM920          * invalid admission number
          MOVE      ZEROUR,PURNO            * clear UR number
.
GTEAM040  MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME        * patients name
.
. ------- obtain a valid operating team -------
.
          PACK      KEY11,OPDAUNIQ,TWO      * check if a team two
          CALL      RDOPDEB1                * read team file
          COMPARE   ZERO,OVRCD
          GOTO      GTEAM100 IF EQUAL       * have more than one team
.
          PACK      KEY11,OPDAUNIQ,ONE      * check if a team one
          CALL      RDOPDEB1
          BRANCH    OVRCD,GTEAM600          * use session team
          GOTO      GTEAM800                * valid team
.
.         have more than one team so select the team
.
GTEAM100  MOVE      ONE,SCRNFLAG            * set redisplay flag
.
GTEAM200  DISPLAY   *P1:24,"Enter the operating team : ",*EL;
.
GTEAM300  KEYIN     *P28:24,*DV,UNDLN1:
                    *P28:24,*V2LON,ANS:
                    *P28:24,*DV,ANS
.
          RESET     ANS
          GOTO      GTEAM990 IF EOS         * nothing entered
.
          MATCH     QUEST,ANS
          GOTO      GTEAM400 IF NOT EQUAL   * validate what was entered
.
.         ? entered
.
          CALL      OPRDEBDS                * ? routine
          MOVE      ZERO,SCRNFLAG
          GOTO      GTEAM200
.
.         validate what was entered
.
GTEAM400  PACK      KEY11,OPDAUNIQ,ANS      * team entered
          CALL      RDOPDEB1                * read team file
          BRANCH    OVRCD,GTEAM500
.
          GOTO       GTEAM800               * valid team found
.
.         Check for team 1
.
GTEAM500  MATCH     "1",ANS
          GOTO      GTEAM700 IF NOT EQUAL   * invalid team
.
.         use default team from session file
.
GTEAM600  MOVE      CURSESS,KEY22
          CALL      RDOPSES1                * read session file
.
          PACK      CMDLINE,SP30,SP30
          PACK      CMDLINE,OPSETYD1,OPSETYD2,OPSETYD3,OPSETYD4:
                            OPSETYD5,OPSETYD6,OPSETYD7,OPSETYD8
          UNPACK    CMDLINE,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                            OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
.
          PACK      CMDLINE,SP30,SP30
          PACK      CMDLINE,OPSEDCC1,OPSEDCC2,OPSEDCC3,OPSEDCC4:
                            OPSEDCC5,OPSEDCC6,OPSEDCC7,OPSEDCC8
          UNPACK    CMDLINE,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                            OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
.
          PACK      CMDLINE,SP30,SP30
          PACK      CMDLINE,OPSETYN1,OPSETYN2,OPSETYN3,OPSETYN4,OPSETYN5:
                            OPSETYN6
          UNPACK    CMDLINE,OPDBTYN1,OPDBTYN2,OPDBTYN3,OPDBTYN4,OPDBTYN5:
                            OPDBTYN6
.
          PACK      CMDLINE,SP30,SP30
          PACK      CMDLINE,OPSENUC1,OPSENUC2,OPSENUC3,OPSENUC4,OPSENUC5:
                            OPSENUC6
          UNPACK    CMDLINE,OPDBNUC1,OPDBNUC2,OPDBNUC3,OPDBNUC4,OPDBNUC5:
                            OPDBNUC6 
.
          MOVE      ONE,OPDBTEAM        * Team "1" is always valid
          GOTO      GTEAM800
.
GTEAM700  DISPLAY   *P1:24,*B,"Invalid team.  ",*EL;
          CALL      HITENTER 
          GOTO      GTEAM200
.
GTEAM800  MOVE      FALSE,EXIT              * have a valid team and its details
          GOTO      GTEAM999
.
.         ERROR: invalid data
.
GTEAM900  DISPLAY   *P1:24,*B,"Invalid Case number.  ",*EL;
          CALL      HITENTER 
          GOTO      GTEAM990
.
GTEAM920  DISPLAY   *P1:24,*B,"Invalid Admission number.  ",*EL;
          CALL      HITENTER 
          GOTO      GTEAM990
.
GTEAM930  DISPLAY   *P1:24,*B,"No PMI details for UR number ",*V2LON,KEY8,*HOFF:
                    ".  ",*EL;
          CALL      HITENTER 
.
GTEAM990  MOVE      TRUE,EXIT
.
GTEAM999  RETURN
+
.********************************************************************
.*                  SETWF000                   Called by : IPREC000 *
.*        Set up the temporary file to hold the screen header       *
.*        and any existing comments                                 *
.********************************************************************
SETWF000  DISPLAY   *P1:24,"Initializing the Wordprocessor - ":
                    *BLINKON,*V2LON,"Please Wait",*EL
.
          CLEAR     CFNAME
          APPEND    "oprrec",CFNAME
          APPEND    PORT,CFNAME
          RESET     CFNAME
          REP       " 0",CFNAME
.
          PREP      INPRECTP,CFNAME         * create the temp file
.
          UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY    * get the session date
          CALL      PACDATE
          REP       " 0",CPCDATE
.
          MOVE      SESNCODE,KEY6           * set up clinic/doct
          CALL      VDCLN000                * get clinic/doct desc OPCLDESC
.
.         write the header for the temp file 
.         NOTE: VERSION must be 9 characters long so PRGNAM starts at col 20
.
.         WRITE     INPRECTP,SEQ;PRGID,SP10,SP1,CNAME,SP10,SP7,CDATE
.         WRITE     INPRECTP,SEQ;VERSION,SP10,PRGNAM,SP1:
.                                 "- (Input Operation Records)"
.          WRITE     INPRECTP,SEQ;SP1
.          WRITE     INPRECTP,SEQ;SP4,OPCNCDSC," : ",SESNCODE,SP5,OPCLDESC
.          WRITE     INPRECTP,SEQ;"    Session Date    : ",CPCDATE,SP5;
.          MOVE      "Suspended",KEY9        * default to suspended
.
.          BRANCH    OPSESUSP,SETWF100       * session suspended
.
          MOVE      SP9,KEY9                * session not suspended
.
.SETWF100  WRITE     INPRECTP,SEQ;KEY9,SP4,"Session Time : ",OPSETIME
.          WRITE     INPRECTP,SEQ;SP1
.          WRITE     INPRECTP,SEQ;"    Admission Number: ",OPDAADMN,SP3:
.                    "Case Number : ",OPDACASE,SP8,"Team : ",OPDBTEAM
.          WRITE     INPRECTP,SEQ;"    U/R Number      : ",PURNO,SP3,PATNAME
.          WRITE     INPRECTP,SEQ;SP1
.
.         transfer existing comments to the work file
.
          PACK      KEY14,OPDAUNIQ,OPDBTEAM,SP3      * start of comment file
          CALL      RSOPREC1                * positional read
.
SETWF200  CALL      RKOPREC1                * read next record
          BRANCH    OVRCD,SETWF999          * end of comments
.
          MATCH     OPORTEAM,OPDBTEAM
          GOTO      SETWF999 IF NOT EQUAL   * different team
.
          MATCH     OPORUNIQ,OPDAUNIQ
          GOTO      SETWF999 IF NOT EQUAL   * different operation
.
          WRITE     INPRECTP,SEQ;OPORLINE   * write the comment line
          GOTO      SETWF200                * Get the next record
.
SETWF999  RETURN
+
.********************************************************************
.*                  WPROC000                   Called by : IPREC000 *
.*        Execute the word processor                                *
.********************************************************************
WPROC000  MOVE      "0",WORDFLAG
          CLEAR     CMDLINE
.
          CLOSE     INPRECTP
          APPEND    CFNAME,CMDLINE
.
          RESET     CMDLINE
.         EXECUTE   CMDLINE,TASKID          * invoke the word processor
          DISPLAY   *P1:24,*EL;
          WORD      CMDLINE,999,5,9,76,23,WORDFLAG
.
WPROC999  RETURN
+
.********************************************************************
.*                  TRCOM000                   Called by : IPREC000 *
.*        Post the comments from the temp file to oprrecaf          *
.********************************************************************
TRCOM000  MOVE      ZERO,OVRCD              * Assume temp file will exist
          TRAP      OVERCOND IF IO          * Trap just in case it doesn't
          OPEN      INPRECTP,CFNAME         * Open the comments temp file
          TRAPCLR   IO                      * Clear the IO trap
          BRANCH    OVRCD,TRCOM900          * No file -> no comments to post
.
          MOVE      ONE,CCITEM              * Starting line No. to post
.
.         Step 1: Delete any old comments from OPRRECFD
.
          PACK      KEY14,OPDAUNIQ,OPDBTEAM,SP3 
          CALL      RSOPREC1                * Position at start of old cmnts
.
TRCOM100  CALL      RKOPREC1                * Get the next comments record
          BRANCH    OVRCD,TRCOM200          * Finished deleting old comments
.
          MATCH     OPORTEAM,OPDBTEAM
          GOTO      TRCOM200 IF NOT EQUAL   * different team
.
          MATCH     OPDAUNIQ,OPORUNIQ
          GOTO      TRCOM200 IF NOT EQUAL   * different operation
.
          PACK      KEY14,OPDAUNIQ,OPDBTEAM,OPORCLND 
          CALL      DEOPREC1                * Delete these comments
          GOTO      TRCOM100                * Get the next record
.
.         Step 2: copy the entered comments from temp file to OPRRECFD
.
.         Loop over temp file and post to the main file
.
TRCOM200  READ      INPRECTP,SEQ;OPORLINE   * read the temp file
          GOTO      TRCOM900 IF OVER        * Finished loop on work file
.
          MOVE      CCITEM,OPORCLND         * get as correct variable
          PACK      KEY14,OPDAUNIQ,OPDBTEAM,OPORCLND * comment line
          CALL      WROPREC1                * Write the new record
          ADD       ONE,CCITEM              * Get the next line number
.
.         The next test is being a bit paranoid, but lets make sure the
.         word processor didn't leave more than 999 lines in the file
.
          COMPARE   ZERO,CCITEM             * Wrapped around to zero ?
          GOTO      TRCOM200 IF NOT EQUAL   * Get the next comment line
.
.         delete the temp file
.
TRCOM900  CLOSE     INPRECTP                * Close the comments temp file
          PREP      INPRECTP,CFNAME         * recreate the temp file
          CLOSE     INPRECTP                * kills the temp file
.
TRCOM999  RETURN
+
