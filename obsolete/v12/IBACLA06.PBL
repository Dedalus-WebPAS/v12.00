.******************************************************************************
.* System         : Clinical Audits                                           *
.* Program        : IBACLA06.PBL                                              *
.* Name           : Codes file maintenance                                    *
.******************************************************************************
.* Date           : 06/05/1992                                                *
.* Author         : Justin Coates                                             *
.* Function       : To maintain the CLACODFD                                  *
.* Modifications  :                                                           *
.******************************************************************************
.
          INC       STD001FD
          INC       CLACODFD/INC            * codes file
.
.         program variables
.
ACCEPT    FORM      1         * insert/change
DESROW    FORM      2         * row for desc
ENCATG    DIM       2         * category
ENCODE    DIM       3         * code
ENDESC    DIM       20        * description
.
.         program constants
.
CATEG     INIT      "Categories"
CODES     INIT      "Codes     "
INSERT    INIT      "Insert"
CHANGE    INIT      "Change"
DELETE    INIT      "Delete"
.
PRGID     INIT      "IBACLA06"
PRGNAM    INIT      "Codes File Maintenance"
VERSION   INIT      "V12.00.00"
+
.*********************************************************************
.                   ML0000
.         Mainline code / Controlling Logic
.*********************************************************************
ML0000    CALL      INIT0000 
.
ML1000    CALL      MENU0000                * menu cat/code
          BRANCH    EXIT,ML9999             * exit
.
          BRANCH    MOPTION,ML2000,ML5000   * categories/codes
.
. ******* category maintenance *******
.
ML2000    CALL      MENA0000                * menu insert/change
          BRANCH    EXIT,ML1000             * exit
.
          PERFORM   ACCEPT,ADDA0000,CHGA0000
          GOTO      ML2000
.
. ******* codes maintenance *******
.
ML5000    CALL      MENB0000                * menu insert/change/delete
          BRANCH    EXIT,ML1000
.
          PERFORM   ACCEPT,ADDB0000,CHGB0000,DELB0000
          GOTO      ML5000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.                   INIT0000                    Called by : ML0000 
.         open files
.*********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening clacodaf";
          OPEN      CLACODA1,"clacodaf"
.
INIT9999  RETURN
+
.*********************************************************************
.                   CLRV0000
.         Clear variables
.*********************************************************************
CLRV0000  UNPACK    SP30,ENCATG,ENCODE,ENDESC
CLRV9999  RETURN
.
.*********************************************************************
.                   MENU0000                    Called by : xxxx0000 
.         either by category/code
.*********************************************************************
MENU0000  HLINE     *G37,2,51,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Categories":
                    *P1:6,*V2LON,TWO,*HOFF," = Codes":
                    *P1:8,"Select Option : "
.
MENU1000  KEYIN     *P17:8,*DV,UNDLN1:
                    *P17:8,*V2LON,MOPTION
.
          COMPARE   ZERO,MOPTION
          GOTO      MENU9100 IF EQUAL       * exit
.
          BRANCH    MOPTION,MENU8000,MENU8000
.
          BEEP
          GOTO      MENU1000
.
MENU8000  MOVE      ZERO,EXIT
          LOAD      KEY10,MOPTION,CATEG,CODES
          STRIP     KEY10
          DISPLAY   *P51:2,*V2LON,"- ",*+,KEY10,SP1
          GOTO      MENU9999
.
MENU9100  MOVE      ONE,EXIT
.
MENU9999  RETURN
+
.*********************************************************************
.                   MENA0000                    Called by : ML2000
.         insert or change category
.*********************************************************************
MENA0000  MOVE      SIX,DESROW
          HLINE     *G37,2,65,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Insert":
                    *P1:6,*V2LON,TWO,*HOFF," = Change":
                    *P1:8,"Select Option : "
.
MENA1000  KEYIN     *P17:8,*DV,UNDLN1:
                    *P17:8,*V2LON,ACCEPT
.
          COMPARE   ZERO,ACCEPT
          GOTO      MENA9100 IF EQUAL       * exit
.
          BRANCH    ACCEPT,MENA8000,MENA8000
.
          BEEP
          GOTO      MENA1000
.
MENA8000  MOVE      ZERO,EXIT
          LOAD      KEY6,ACCEPT,INSERT,CHANGE
          DISPLAY   *P65:2," (",*V2LON,KEY6,*HOFF,") "
          GOTO      MENA9999
.
MENA9100  MOVE      ONE,EXIT
.
MENA9999  RETURN
+
.*********************************************************************
.                   MENB0000                    Called by : ML5000
.         insert or change or delete code
.*********************************************************************
MENB0000  MOVE      SEVEN,DESROW
          HLINE     *G37,2,65,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Insert":
                    *P1:6,*V2LON,TWO,*HOFF," = Change":
                    *P1:7,*V2LON,THREE,*HOFF," = Delete":
                    *P1:9,"Select Option : "
.
MENB1000  KEYIN     *P17:9,*DV,UNDLN1:
                    *P17:9,*V2LON,ACCEPT
.
          COMPARE   ZERO,ACCEPT
          GOTO      MENB9100 IF EQUAL       * exit
.
          BRANCH    ACCEPT,MENB8000,MENB8000,MENB8000
.
          BEEP
          GOTO      MENB1000
.
MENB8000  MOVE      ZERO,EXIT
          LOAD      KEY6,ACCEPT,INSERT,CHANGE,DELETE
          DISPLAY   *P65:2," (",*V2LON,KEY6,*HOFF,") "
          GOTO      MENB9999
.
MENB9100  MOVE      ONE,EXIT
.
MENB9999  RETURN
+
.*********************************************************************
.                   ADDA0000                    Called by : ML2000
.         insert a category
.*********************************************************************
ADDA0000  DISPLAY   *P1:3,*EF:
                    *P5:4,"Category    : ":
                    *V2LON,*P1:DESROW," 1",*HOFF,". Description : "
.
          CALL      CLRV0000                * clear variable
          CALL      KCAT0000                * keyin category
          BRANCH    EXIT,ADDA9999           * exit
.
ADDA1000  CALL      KDES0000                * enter desc
.
ADDA5000  CALL      MAINQST
          BRANCH    CCITEM,ADDA1000         * change desc
.
          COMPARE   ZERO,CCITEM
          GOTO      ADDA8000 IF EQUAL       * post
          GOTO      ADDA0000 IF LESS        * cancel
.
ADDA8000  CALL      WRIT0000                * write a record
          GOTO      ADDA0000
.
ADDA9999  RETURN
+
.*********************************************************************
.                   CHGA0000                    Called by : ML2000
.         change a category
.*********************************************************************
CHGA0000  DISPLAY   *P1:3,*EF:
                    *P5:4,"Category    : ":
                    *V2LON,*P1:DESROW," 1",*HOFF,". Description : "
.
          CALL      CLRV0000                * clear variable
          CALL      KCAT0000                * keyin category
          BRANCH    EXIT,CHGA9999           * exit
          DISPLAY   *P19:DESROW,*V2LON,ENDESC
.
CHGA5000  CALL      MAINQST
          BRANCH    CCITEM,CHGA5100         * change desc
.
          COMPARE   ZERO,CCITEM
          GOTO      CHGA8000 IF EQUAL       * post
          GOTO      CHGA0000 IF LESS        * cancel
.
CHGA5100  CALL      KDES0000
          GOTO      CHGA5000
.
CHGA8000  CALL      UPDT0000                * update a record
          GOTO      CHGA0000
.
CHGA9999  RETURN
+
.*********************************************************************
.                   ADDB0000                    Called by : ML2000
.         insert a code
.*********************************************************************
ADDB0000  DISPLAY   *P1:3,*EF:
                    *P5:4,"Category    : ":
                    *P5:5,"Code        : ":
                    *V2LON,*P1:DESROW," 1",*HOFF,". Description : "
.
ADDB1000  CALL      CLRV0000                * clear variable
          MOVE      ZERO,ACCEPT             * set to allow existing category
          CALL      KCAT0000                * keyin category
          BRANCH    EXIT,ADDB9999           * exit
          DISPLAY   *P30:4,ENDESC
.
ADDB1500  MOVE      ONE,ACCEPT              * set back to insert
          CALL      KCOD0000                * keyin code
          BRANCH    EXIT,ADDB1000           * exit
.
ADDB2000  CALL      KDES0000                * enter desc
.
ADDB5000  CALL      MAINQST
          BRANCH    CCITEM,ADDB2000         * change desc
.
          COMPARE   ZERO,CCITEM
          GOTO      ADDB8000 IF EQUAL       * post
          GOTO      ADDB0000 IF LESS        * cancel
.
ADDB8000  CALL      WRIT0000                * write a record
          GOTO      ADDB1500
.
ADDB9999  RETURN
+
.*********************************************************************
.                   CHGB0000                    Called by : ML2000
.         change a code
.*********************************************************************
CHGB0000  DISPLAY   *P1:3,*EF:
                    *P5:4,"Category    : ":
                    *P5:5,"Code        : ":
                    *V2LON,*P1:DESROW," 1",*HOFF,". Description : "
.
CHGB1000  CALL      CLRV0000                * clear variable
          CALL      KCAT0000                * keyin category
          BRANCH    EXIT,CHGB9999           * exit
          DISPLAY   *P30:4,ENDESC
.
CHGB1500  CALL      KCOD0000                * keyin code
          BRANCH    EXIT,CHGB1000           * exit
          DISPLAY   *P19:DESROW,*V2LON,ENDESC
.
CHGB5000  CALL      MAINQST
          BRANCH    CCITEM,CHGB5100         * change desc
.
          COMPARE   ZERO,CCITEM
          GOTO      CHGB8000 IF EQUAL       * post
          GOTO      CHGB0000 IF LESS        * cancel
.
CHGB5100  CALL      KDES0000
          GOTO      CHGB5000
.
CHGB8000  CALL      UPDT0000                * update a record
          GOTO      CHGB1500
.
CHGB9999  RETURN
+
.*********************************************************************
.                   DELB0000                    Called by : ML2000
.         delete a code
.*********************************************************************
DELB0000  DISPLAY   *P1:3,*EF:
                    *P5:4,"Category    : ":
                    *P5:5,"Code        : ":
                    *V2LON,*P1:DESROW," 1",*HOFF,". Description : "
.
DELB1000  CALL      CLRV0000                * clear variable
          CALL      KCAT0000                * keyin category
          BRANCH    EXIT,DELB9999           * exit
          DISPLAY   *P30:4,ENDESC
.
          CALL      KCOD0000                * keyin code
          BRANCH    EXIT,DELB1000           * exit
          DISPLAY   *P19:DESROW,*V2LON,ENDESC
.
DELB5000  CALL      DELQST
          BRANCH    CEXIT,DELB8000,DELB0000,DELB9999
.
DELB8000  CALL      DELT0000                * delete a record
          GOTO      DELB0000
.
DELB9999  RETURN
+
.*********************************************************************
.                   KCAT0000                    Called by : xxxx0000 
.         keyin the category
.*********************************************************************
KCAT0000  KEYIN     *P19:4,*DV,UNDLN2,*EL:
                    *P19:4,*V2LON,ENCATG:
                    *P19:4,*DV,ENCATG
.
          PACK      ENCATG,ENCATG,SP2
          MATCH     SP2,ENCATG
          GOTO      KCAT9100 IF EQUAL       * exit
.
          PACK      KEY5,ENCATG,SP3
          CALL      RDCACOD1
          BRANCH    OVRCD,KCAT5000          * code not on file
.
.         category is on file
.
          MOVE      CACDDESC,ENDESC         * description
          COMPARE   ONE,ACCEPT
          GOTO      KCAT9000 IF NOT EQUAL   * not insert mode so OK
.
          DISPLAY   *P1:24,*B,"Category is already on file.  ",*EL;
          CALL      HITENTER
          GOTO      KCAT0000
.
.         category is not on file
.
KCAT5000  COMPARE   ONE,ACCEPT
          GOTO      KCAT9000 IF EQUAL       * insert mode so OK
.
          DISPLAY   *P1:24,*B,"Invalid Category.  ",*EL;
          CALL      HITENTER
          GOTO      KCAT0000
.
KCAT9000  MOVE      ZERO,EXIT
          GOTO      KCAT9999
.
KCAT9100  MOVE      ONE,EXIT
.
KCAT9999  RETURN
+
.*********************************************************************
.                   KCOD0000                    Called by : xxxx0000 
.         keyin the code
.*********************************************************************
KCOD0000  KEYIN     *P19:5,*DV,UNDLN3:
                    *P19:5,*V2LON,ENCODE:
                    *P19:5,*DV,ENCODE
.
          PACK      ENCODE,ENCODE,SP3
          MATCH     SP3,ENCODE
          GOTO      KCOD9100 IF EQUAL       * exit
.
          PACK      KEY5,ENCATG,ENCODE
          CALL      RDCACOD1
          BRANCH    OVRCD,KCOD5000          * code not on file
.
.         code is on file
.
          MOVE      CACDDESC,ENDESC         * description
          COMPARE   ONE,ACCEPT
          GOTO      KCOD9000 IF NOT EQUAL   * not insert mode so OK
.
          DISPLAY   *P1:24,*B,"Code is already on file.  ",*EL;
          CALL      HITENTER
          GOTO      KCOD0000
.
.         code is not on file
.
KCOD5000  COMPARE   ONE,ACCEPT
          GOTO      KCOD9000 IF EQUAL       * insert mode so OK
.
          DISPLAY   *P1:24,*B,"Invalid Code.  ",*EL;
          CALL      HITENTER
          GOTO      KCOD0000
.
KCOD9000  MOVE      ZERO,EXIT
          GOTO      KCOD9999
.
KCOD9100  MOVE      ONE,EXIT
.
KCOD9999  RETURN
+
.*********************************************************************
.                   KDES0000                    Called by : xxxx0000 
.         keyin description
.*********************************************************************
KDES0000  KEYIN     *P19:DESROW,*DV,UNDLN20:
                    *P19:DESROW,*V2LON,ENDESC:
                    *P19:DESROW,*DV,ENDESC
          PACK      ENDESC,ENDESC,SP20
.
KDES9999  RETURN
+
.*********************************************************************
.                   WRIT0000                    Called by : xxxx0000 
.         write a new record to file
.*********************************************************************
WRIT0000  DISPLAY   *P1:24,*EL,"POSTING"
          MOVE      ENCATG,CACDCATG         * category
          MOVE      ENCODE,CACDCODE         * code
          MOVE      ENDESC,CACDDESC         * description
          MOVE      SP30,CACDSPAR
          CALL      WRCACOD1
          DISPLAY   *P19:DESROW,*EL,*P1:24,*EL;
WRIT9999  RETURN
+
.*********************************************************************
.                   UPDT0000                    Called by : xxxx0000 
.         change a record to file
.*********************************************************************
UPDT0000  DISPLAY   *P1:24,*EL,"UPDATING"
          MOVE      ENCATG,CACDCATG         * category
          MOVE      ENCODE,CACDCODE         * code
          MOVE      ENDESC,CACDDESC         * description
          MOVE      SP30,CACDSPAR
          CALL      UPCACOD1
          DISPLAY   *P19:DESROW,*EL,*P1:24,*EL;
UPDT9999  RETURN
+
.*********************************************************************
.                   DELT0000                    Called by : xxxx0000 
.         delete a record 
.*********************************************************************
DELT0000  DISPLAY   *P1:24,*EL,"DELETING"
          PACK      KEY5,ENCATG,ENCODE
          CALL      DECACOD1
          DISPLAY   *P19:DESROW,*EL,*P1:24,*EL;
DELT9999  RETURN
+
          INC       STD001IO
          INC       CLACODIO/INC            * codes file
.............................................................................
