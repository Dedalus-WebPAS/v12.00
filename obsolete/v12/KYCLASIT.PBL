.******************************************************************************
.* Include Name   : KYCLASIT.PBL                                              *
.* Description    : Keyin of a Clinical Audit Site code                       *
.* Author         : LOFTY                                                     *
.*                                                                            *
.* Returns        : CASTSITE    - Keyed in code                               *
.*                  CASTDESC    - Description                                 *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.******************************************************************************
KYCLASIT  DISPLAY   *P1:3,*EF:
                    *P22:10,"Site     : ":
                    *P22:12,"Password : "
          BOX       16,*V2LON,20,8,42,14
          MOVE      ONE,FORM1               * set ? as not entered
          MOVE      "33",CCOL
          MOVE      "10",CVERT
.
KCAST100  KEYIN     *PCCOL:CVERT,*DV,UNDLN6:      * no default used
                    *PCCOL:CVERT,*V2LON,CASTSITE:
                    *PCCOL:CVERT,*DV,CASTSITE
.
          PACK      CASTSITE,CASTSITE,SP6
          MATCH     SP6,CASTSITE
          GOTO      KCAST810 IF EQUAL       * nothing entered
          MATCH     EXITCHAR,CASTSITE
          GOTO      KCAST810 IF EQUAL       * exitchar entered
          MATCH     QUEST,CASTSITE
          GOTO      KCAST200 IF NOT EQUAL   * ? not entered
.
.         a "?" was input
.
          IF        FORM1=1
            MOVE      "0",HLEF                * save all of the screen
            CALL      GETSCR00                * save screen
          ENDIF
.
          CALL      CLASITDS
          DISPLAY   *P1:24,"Site : ",*EL;
          MOVE      "24",CVERT
          MOVE      "8",CCOL
          MOVE      ZERO,FORM1              * set ? as entered
          GOTO      KCAST100
.
.         validate what was entered
.
KCAST200  MOVE      CASTSITE,KEY6
          CALL      RDCASIT1
          BRANCH    OVRCD,KCAST700          * invalid code
.
.         redisplay screen if needed
.
          IF        FORM1=0
            CALL      PUTSCR00                * redisplay screen
            MOVE      ONE,FORM1               * set as ? not entered
          ENDIF
          DISPLAY   *P33:10,*V2LON,CASTSITE
          MOVE      "33",CCOL
          MOVE      "10",CVERT
.
.         enter the password
.
          MATCH     SP8,CASTPASS
          GOTO      KCAST800 IF EQUAL       * no password to enter
.
KCAST300  KEYIN     *P33:12,*ESON,KEY8;
          PACK      KEY8,KEY8,SP8
.
          MATCH     SP8,KEY8
          GOTO      KCAST100 IF EQUAL       * no password entered
.
          MATCH     KEY8,CASTPASS
          GOTO      KCAST800 IF EQUAL       * correct password
.
          DISPLAY   *P1:24,*EL,*B,"Invalid Password.  ";
          CALL      HITENTER
          GOTO      KCAST300
.
KCAST700  DISPLAY   *P1:24,*B,"Invalid site code.  ",*EL;
          CALL      HITENTER
          BRANCH    FORM1,KCAST100
          DISPLAY   *P1:24,"Site : ",*EL;
          GOTO      KCAST100
.
KCAST800  MOVE      FALSE,EXIT                    * valid input
          GOTO      KCAST999 
.
KCAST810  MOVE      ONE,EXIT                * Nothing or EXITCHAR entered
.
KCAST999  RETURN
+
