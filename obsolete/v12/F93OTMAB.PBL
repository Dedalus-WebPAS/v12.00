.******************************************************************************
.*    Program      : F93OTMAB                                                 *
.*    Description  : Conversion outmasbf to new File layout                   *
.*                                                                            *
.*    Author       : Ebon Clements                                            *
.*    Date         : 28/08/2003                                               *
.*    Modifications: V9.03.01 03/12/2003 Ebon Clements CAR 22361              *
.*                           Fixed I * D on WRMASB1                           *
.*                   V9.03.00 28/08/2003 Ebon Clements CAR 22361              *
.*                            Created program to convert outmasbf to          *
.*                            the new file layout                             *
.******************************************************************************
          INC       STD001FD
          INC       OUTMABFD/INC
          INC       OUTHEDFD/INC
          INC       OUTSITFD/INC
.
FINDFILE  FILE      ASCII, FIXED=256
.
. Enter the first or the shortest index from the original FD and rename
. the index OLDFILE
.
OLDFILE    IFILE    SQL, FIXED=40, KEY="U1-6,7-12,13-13,14-18,19-21"

.
CMDLINE   DIM       100
DIM4      DIM       4
DIM4A     DIM       4
DIM6      DIM       6
DIM6A     DIM       6
DIM60     DIM       60
DDCENT    DIM       2
.
RECNUM    FORM      8
NEWFILE   DIM       60
NEWPATH   DIM       60
OLDPATH   DIM       60
DEFPATH   DIM       60
SAVEFLG   FORM      1
SP50      INIT    "                                                  "
SP60      INIT    "                                                            "
ORACLEFL  FORM      1
VSITE     DIM       6
CFILEPRE  DIM       3   * Current Open File Prefix for Outpatient Files
SAVKEY31  DIM       31
.
PRGNAM    INIT      "CONVERSION OUTMABFD"
PRGID     INIT      "F93OTMAB"
VERSION   INIT      "V12.00.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
MAIN0100  CALL      OPTN0000                      * select options
          BRANCH    EXIT,MAIN9999                 * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN0200,MAIN0500     * proceed with clean up
          GOTO      MAIN9999
.
MAIN0200  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
MAIN0500  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN1000,MAIN0000,MAIN9999 * Yes, no, cancel
.
MAIN1000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
. Loop thru old file and write records to new file
.
          IF        ORACLEFL=1
            CALL      ORED0000                      * read master records 
          ELSE
            CALL      READ0000                      * read master records 
          ENDIF
.
          CALL      ENDP0000                      * save/compress saved file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      ZERO,ORACLEFL
          MOVE      SP70,VSITE
          MOVE      "out",CFILEPRE
.
          MOVE      IDDD,VSITE          * Initialize the current site
          MATCH     SP6,VSITE           * Valid site ?
          GOTO      INIT9000 IF EQUAL   * No. Initialization failure
.
          DISPLAY   *P56:24,"Opening outsitaf";
          OPEN      OUTSITA1,"outsitaf" * Open the site file
.
          MOVE      VSITE,KEY6          * Key for the site file
          CALL      RDSITA1             * Read the site record
          BRANCH    OVRCD,INIT9000      * Invalid site.
.
          MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
          GOTO      INIT9999
.
.         Invalid call to program. Common not set up with correct site info
.
INIT9000  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "Incorrect call to program - Site information wrong.    ";
          CALL      HITENTER
          MOVE      ONE,EXIT                * Initialization failure
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run fix program - CISAM ":
                    "- Build new files and populate":
                    *P1:6,*V2LON,TWO,*HOFF,". Run update program  - ORACLE ":
                    "- Populate new fields only":
                    *P1:10,*V2LON,"NOTE - outmabaf file will be saved as ":
                    "savmasbf regardless of the site prefix"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9100    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          MOVE      ZERO,ORACLEFL
          GOTO      OPTN9999
.
OPTN9100  MOVE      ZERO,EXIT
          MOVE      ONE,ORACLEFL
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
KDIR0000  CALL      DEFT0000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new outmasbf file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
PREP0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PACK      CFNAME,CFILEPRE,FILHEDA1
          OPEN      OUTHEDA1,CFNAME
          TRAPCLR   IO
          BRANCH    OVRCD,PREP7000       * Original file does not exist
.
          IF        ORACLEFL=1
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            PACK      CFNAME,CFILEPRE,FILMASB1
            OPEN      OUTMASB1,CFNAME 
            TRAPCLR   IO
            BRANCH    OVRCD,PREP7000       * Original file does not exist
            GOTO      PREP9999
          ENDIF
.
. open old file
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PACK      CFNAME,CFILEPRE,FILMASB1
          OPEN      OLDFILE,CFNAME
          TRAPCLR   IO
          BRANCH    OVRCD,PREP5000       * Original file does not exist
          CLOSE     OLDFILE
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
. Copy old file to the keyin directory
.
          PACK      CFNAME,CFILEPRE,FILMASB1
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat ",CMDLINE
          APPEND    CFILEPRE,CMDLINE
          APPEND    "masbf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "savmasbf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat ",CMDLINE
          APPEND    CFILEPRE,CMDLINE
          APPEND    "masbf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "savmasbf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
. remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    CFILEPRE,CMDLINE
          APPEND    "masbf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "savmasbf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    CFILEPRE,NEWFILE
          APPEND    "masbf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.      eg    APPEND    " 195 U9-16",CMDLINE
.
          APPEND    " 95 UC1-6,7-12,13-13,14-18,35-36,37-44,19-21",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has
.  to be repeated with for each individual index
.
.          PACK      CMDLINE,SP60,SP60
.          CLEAR     CMDLINE
.          APPEND    "isadd ",CMDLINE
.          APPEND    NEWFILE,CMDLINE
.          APPEND    " UC13-20,21-25,1-6,7-12,105-106,107-114",CMDLINE
.          APPEND    SP60,CMDLINE
.          RESET     CMDLINE
.          EXECUTE   CMDLINE,TASKID
.
          OPEN      OUTMASB1,NEWFILE
.
. set the flag to say we want to continue
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP7000  DISPLAY   *P1:24,*EL,*B,"outmasbf file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
PREP9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and WRMASB1 each record             *
.**********************************************************************
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          PACK      KEY21,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
          PACK      KEY28,OMBSITE,OMBCLIN,OMBDAY,OMBSTRT,SP70
          CALL      RDOTHED1
          COMPARE   ZERO,OVRCD
          GOTO      READ2500 IF EQUAL
.
          CALL      RKOTHED1
          BRANCH    OVRCD,READ2000
.
          MATCH     OTHESITE,OMBSITE              * Same site
          GOTO      READ2000 IF NOT EQUAL
.
          MATCH     OTHECLIN,OMBCLIN              * Same clinic
          GOTO      READ2000 IF NOT EQUAL
.
          MATCH     OTHEWDAY,DOMBDAY              * Same day
          GOTO      READ2000 IF NOT EQUAL
.
          MATCH     OTHESTRT,OMBSTRT             * Same start time
          GOTO      READ2000 IF NOT EQUAL
.
          GOTO      READ2500                      * OK to use this clinic master
.
READ2000  MOVE      SP70,OTMBSHNO
          MOVE      SP70,OTMBSDAT
          MOVE      SP70,OMBSPARE
          GOTO      READ3000
.
READ2500  MOVE      OTHESHNO,OTMBSHNO
          MOVE      OTHESDAT,OTMBSDAT
          MOVE      SP70,OMBSPARE
.
READ3000  PACK      KEY31,OMBSITE,OMBCLIN,OMBDAY,OMBSTRT:
                          OTMBSHNO,OTMBSDAT,OMBSLOT,SP70
.
          CALL      RDAMASB1
          IF        OVRCD=1
            CALL      WRMASB1                       * WRMASB1 to new file
          ELSE
            GOTO      READ1000                      * skip record
          ENDIF
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     OUTMASB1                      * close new file
          CLOSE     OLDFILE                       * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
READ9999  RETURN
+
.**********************************************************************
.*                               ORED0000                             *
.*             loop thru outmabaf file and update each record         *
.**********************************************************************
ORED0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          PACK      KEY31,SP60
          CALL      RDSMASB1                      * position at start
ORED1000  CALL      RDKMASB1                      * ORED next record
          BRANCH    OVRCD,ORED6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
          MATCH     SP70,OTMBSHNO                 * skip if populated
          GOTO      ORED1000 IF NOT EQUAL
.
          MATCH     SP70,OTMBSDAT                 * skip if populated
          GOTO      ORED1000 IF NOT EQUAL
.
          PACK      SAVKEY31,OMBSITE,OMBCLIN,OMBDAY,OMBSTRT:
                          OTMBSHNO,OTMBSDAT,OMBSLOT,SP70
.
          PACK      KEY28,OMBSITE,OMBCLIN,OMBDAY,OMBSTRT,SP70
          CALL      RDOTHED1
          COMPARE   ZERO,OVRCD
          GOTO      ORED2500 IF EQUAL
.
          CALL      RKOTHED1
          BRANCH    OVRCD,ORED2000
.
          MATCH     OTHESITE,OMBSITE              * Same site
          GOTO      ORED2000 IF NOT EQUAL
.
          MATCH     OTHECLIN,OMBCLIN              * Same clinic
          GOTO      ORED2000 IF NOT EQUAL
.
          MATCH     OTHEWDAY,DOMBDAY              * Same day
          GOTO      ORED2000 IF NOT EQUAL
.
          MATCH     OTHESTRT,OMBSTRT             * Same start time
          GOTO      ORED2000 IF NOT EQUAL
.
          GOTO      ORED2500                      * OK to use this clinic master
.
ORED2000  MOVE      SP70,OTMBSHNO
          MOVE      SP70,OTMBSDAT
          MOVE      SP70,OMBSPARE
          GOTO      ORED3000
.
ORED2500  MOVE      OTHESHNO,OTMBSHNO
          MOVE      OTHESDAT,OTMBSDAT
          MOVE      SP70,OMBSPARE
.
ORED3000  PACK      KEY31,OMBSITE,OMBCLIN,OMBDAY,OMBSTRT:
                          OTMBSHNO,OTMBSDAT,OMBSLOT,SP70
.
          CALL      UPMASB1                       * UPMASB1 to new file
.
          MOVE      SAVKEY31,KEY31
          CALL      RDSMASB1
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      ORED1000                      * get next record
.
ORED6000  CLOSE     OUTMASB1                      * close new file
          CLOSE     OLDFILE                       * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
ORED9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
VALD0000  MOVE      ZERO,EXIT
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.**********************************************************************
ENDP0000  CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.         Get the default directory
.**********************************************************************
DEFT0000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "ldat ",CMDLINE
          APPEND    CFILEPRE,CMDLINE
          APPEND    "masbf.dat > temp.txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding outmasbf.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
DEFT9999  RETURN
.
READSOLD RESET     KEY21
         READ      OLDFILE,KEY21;;
         RETURN
.
.
READKOLD MOVE      ZERO,OVRCD
         READKS    OLDFILE;OMBSITE,OMBCLIN,DOMBDAY,OMBSTRT:
                            DOMBSLOT,OMBVTYP,OMBTIME,OMBEXSL:
                            DOTMBRES,OMBSPARE
         GOTO      OVERCOND IF OVER
         MOVE      DOMBDAY,OMBDAY
         MOVE      DOMBSLOT,OMBSLOT
         MOVE      ZERO,OTMBRESV
         MOVE      DOTMBRES,OTMBRESV
         MOVE      OMBSITE,SOMBSITE
         MOVE      OMBCLIN,SOMBCLIN
         MOVE      OMBDAY,SOMBDAY
         MOVE      OMBSTRT,SOMBSTRT
         MOVE      OMBSLOT,SOMBSLOT
         RETURN
+
          INC       OUTMABIO/INC
          INC       OUTHEDIO/INC
          INC       OUTSITIO/INC
          INC       STD001IO
