. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS26                                             *
. * Desc      :   Committed Expenditure Listing                        *
. **********************************************************************
. * Date      :   23.07.1993                                           *
. * Author    :   Neeriem Dye (IBA)                                    *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
. * VF.01.01  23.10.2000 Sandra Barcham                                *
. *           Exlucde GST for OPTION 3 (LEDGER/ACCOUNT)                *
. *           srf 6681                                                 *
. **********************************************************************
. * VF.00.02  05.01.2000 Charles Handaya                               *
. *           Recompiled for APSMASIO                                  *
. * VF.00.01  02.09.1999 Sandra Barcham                                *
. *           Add in Creditor Keyword Search                           *
. *           srf 631485                                               *
. **********************************************************************
.$$$$$$
.        PROGRAM IBAFMS26
.             open FILEAsand initialise variables
.             WHILE user wants to print reports DO
.                  get desired order
.             END WHILE
.        END
.$$$$$$
. Standard FMS Accounting System Include
.---------------------------------------
          INC       FMSSTDDF
.
.==============================================================================
.FILEA ESCRIPTION INCLUDES
.-------------------------
.
          INC       APSCEAFD/INC       * commited expenditure
          INC       APSCEBFD/INC       * commited expenditure
          INC       APSCIOFD/INC       * commited expenditure
          INC       APSISLFD/INC       * supplier
          INC       APSMASFD/INC       * supplier
          INC       APSMXRFD/INC       * supplier
          INC       APSORDFD/INC       * commited expenditure
          INC       FMSAMAFD/INC       * account
          INC       FMSLMAFD/INC       * ledger
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
.
FORMAT    INIT      "(999,999,999,999.99 "
.
ALAB      INIT      "                            "   * scr A option descriptions
ALAB1     INIT      "Supplier       (Include GST)"
ALAB2     INIT      "Order Number   (Include GST)"
ALAB3     INIT      "Ledger/Account (Exclude GST)"
.
FINISH    INIT      "Finish"           * default labels and ranges
START     INIT      "Start"
TILDAS    INIT      "~~~~~~~~~~~~~~~~~~~~"
.
ACCEPT    FORM      1        * accept mode (1=on)
CODECURR  DIM       14       * current code
CODEFROM  DIM       14       * code to print from
CODELAST  DIM       14       * last code printed
CODETEMP  DIM       14       * code to print temp
CODETO    DIM       14       * code to print to
CURACC    DIM       12       * current account
CURDATE   DIM       12
CURLED    DIM       2        * current ledger
CURORD    DIM       7        * current order
CURSUPP   DIM       5        * current supplier
DESCFROM  DIM       35       * description to print from
DESCTEMP  DIM       35       * description to print temp
DESCTO    DIM       35       * description to print to
LABFROM   DIM       15       * code to print from label
LABTEMP   DIM       15       * code to print temp label
LABTO     DIM       15       * code to print to label
OPTIONA   FORM      1        * option on screen A
OPTIONB   FORM      1        * option on screen B
OPTIONC   FORM      1        * option on screen B
OUTFORM   DIM       20       * FEDIT output (For Spastic Floaters FMSCEXKY)
TEMPORD   DIM       7        * current order for printing
TOTAL1    FORM      12.2     * total
TOTAL2    FORM      12.2     * sub total
TOTAL3    FORM      12.2     * sub total
TOTDESC   DIM       14
TOTRASDS  INIT      "Total Raised  "
TOTRECDS  INIT      "Total Received"
RAISED    INIT      "Raised but not Received  "
RECEIVED  INIT      "Received but not Invoiced"
RRDESC    DIM       25
RASORREC  FORM      1     -  1-Raised  2-Received
SKEY8     DIM       8
.
PRGID     INIT      "IBAFMS26"
PRGNAM    INIT      "Committed Expenditure Listing"
VERSION   INIT      "V12.00.00"
.
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML0100    CALL      SELA0000           * perform screen A
          COMPARE   ZERO,OPTIONA
          GOTO      ML9999 IF EQUAL    * exit program ?
.
          CALL      SELB0000           * perform screen B
          BRANCH    EXIT,ML0100        * exit screen ?
.
          PERFORM   RASORREC,PRNTB000,PRNTA000  * print raised/received report
          GOTO      ML0100             * back to first menu
.
ML9999    CHAIN     PGM                * chain back to menu
.
.******************************************************************************
.* INIT - Open FILEAs                            Called by ML0000             *
.******************************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,CKEYTYP
.
          DISPLAY   *P56:24,"Opening apsordaf";  * commited expenditure
          OPEN      APSORDA1,"apsordaf"
          OPEN      APSORDA2,"apsordaf"
.
          DISPLAY   *P64:24,"apscioaf";
          OPEN      APSCIOA1,"apscioaf"
          OPEN      APSCIOA2,"apscioaf"
.
          DISPLAY   *P64:24,"apscebaf";  * commited expenditure
          OPEN      APSCEBA1,"apscebaf"
          OPEN      APSCEBA2,"apscebaf"
.
          DISPLAY   *P64:24,"apsceaaf";
          OPEN      APSCEAA1,"apsceaaf"
          OPEN      APSCEAA2,"apsceaaf"
.
          DISPLAY   *P64:24,"fmsamaaf";  * account
          OPEN      FMSAMAA1,"fmsamaaf"
          OPEN      FMSAMAA2,"fmsamaaf"
          OPEN      FMSAMAA3,"fmsamaaf"
.
          DISPLAY   *P64:24,"fmslmaaf";  * ledger
          OPEN      FMSLMAA1,"fmslmaaf"
.
          DISPLAY   *P64:24,"apsislaf";  * inv system
          OPEN      APSISLA1,"apsislaf"
.
          DISPLAY   *P64:24,"apsmasaf";  * supplier
          OPEN      APSMASA1,"apsmasaf"
          OPEN      APSMASA2,"apsmasaf"
          OPEN      APSMXRA1,"apsmxraf"
          OPEN      APSMXRA2,"apsmxraf"
.
          MOVE      ONE,CNOUNDLN
          MOVE      "1",CACCTIND       * posting accounts only for acct keyin
.
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.******************************************************************************
.* SELA - Display Screen A                       Called by ML0000             *
.*        Returns : OPTIONA  (0=exit, other=selection)                        *
.******************************************************************************
SELA0000  DISPLAY   *P46:3,*EF:                  * display screen
                    *P1:4,*V2LON," 0",*HOFF,". Exit":
                    *P1:5,*V2LON," 1",*HOFF,". List by ",ALAB1:
                    *P1:6,*V2LON," 2",*HOFF,". List by ",ALAB2:
                    *P1:7,*V2LON," 3",*HOFF,". List by ",ALAB3:
                    *P1:9,"Select Option : ";
.
SELA1000  KEYIN     *P17:9,"_",*P17:9,*V2LON,OPTIONA
          COMPARE   ZERO,OPTIONA
          GOTO      SELA9999 IF EQUAL            * exit program ?
          MOVE      SP70,ALAB
          LOAD      ALAB,OPTIONA,ALAB1,ALAB2,ALAB3
          MATCH     SP70,ALAB
          GOTO      SELA9999 IF NOT EQUAL        * legal option ?
          BEEP
          GOTO      SELA1000
.
SELA9999  RETURN
.******************************************************************************
.* SCRB - Display Screen B                       Called by SELB               *
.******************************************************************************
SCRB0000  DISPLAY   *P46:3,*EF,*V2LON," - by ",ALAB:    * display screen
                    *P1:4,*V2LON," 1",*HOFF,". Raised or Received   : ":
                         *V2LON,YESNO
.
           IF        OPTIONA=2
             DISPLAY   *P1:5,*V2LON," 2",*HOFF,". Inventory System Code: ":
                            *V2LON,APISISC,*HOFF,SP2,APISDES:
                       *P1:6,*V2LON," 3",*HOFF,". First ",ALAB,*P26:6,": ":
                            *V2LON,LABFROM,*HOFF,SP2,DESCFROM:
                       *P1:7,*V2LON," 4",*HOFF,". Last  ",ALAB,*P26:7,": ":
                            *V2LON,LABTO,*HOFF,SP2,DESCTO:
                       *P1:8,*V2LON," 5",*HOFF,". Summary Only (Y/N)   : ":
                            *V2LON,YESNO;
           ELSE
             DISPLAY   *P1:5,*V2LON," 2",*HOFF,". First ",ALAB,*P26:5,": ":
                            *V2LON,LABFROM,*HOFF,SP2,DESCFROM:
                       *P1:6,*V2LON," 3",*HOFF,". Last  ",ALAB,*P26:6,": ":
                            *V2LON,LABTO,*HOFF,SP2,DESCTO:
                       *P1:7,*V2LON," 4",*HOFF,". Summary Only (Y/N)   : ":
                            *V2LON,YESNO;
          ENDIF
.
SCRB9999  RETURN
.******************************************************************************
.* SELB - Display Screen B                       Called by ML0000             *
.*        Returns : OPTIONB  (0=exit, other=selection)                        *
.******************************************************************************
SELB0000  MOVE      SP70,LABFROM       * clear variables
          MOVE      SP70,LABTO
          MOVE      SP70,CODEFROM
          MOVE      SP70,CODETO
          MOVE      SP70,YESNO
          MOVE      SP70,DESCFROM
          MOVE      SP70,DESCTO
          MOVE      SP70,RRDESC
          MOVE      SP70,APISISC
          MOVE      SP70,APISDES
          MOVE      ONE,ACCEPT
          MOVE      ZERO,OPTIONB
          MOVE      SP70,YESNO
          CALL      SCRB0000           * display screen
.
SELB0100  ADD       ONE,OPTIONB
          BRANCH    ACCEPT,SELB0200    * in accept mode ?
.
          KEYIN     *P1:24,"Select Field, (",*V2LON,"R",*HOFF,")un Report, e(":
                    *V2LON,"X",*HOFF,")it : _",*EL,*P38:24,*V2LON,ANS;
          PACK      ANS,ANS,SP70
          REP       UPPLOW,ANS
          MATCH     "X",ANS
          GOTO      SELB9500 IF EQUAL  * exit ?
          MATCH     "R",ANS
          GOTO      SELB9000 IF EQUAL  * run report ?
.
          MOVE      ZERO,OPTIONB
          MOVE      ANS,OPTIONB
          IF        OPTIONA=1  &  OPTIONB=1
            MOVE      SP70,LABFROM       * clear variables
            MOVE      SP70,LABTO
            MOVE      SP70,CODEFROM
            MOVE      SP70,CODETO
            MOVE      SP70,YESNO
            MOVE      SP70,DESCFROM
            MOVE      SP70,DESCTO
            MOVE      SP70,RRDESC
            MOVE      ONE,ACCEPT
            MOVE      ZERO,OPTIONB
            MOVE      SP70,YESNO
            GOTO      SELB0100
          ENDIF
.
SELB0200  MOVE      "28",CCOL
          MOVE      "3",CVERT
          ASSIGN    OPTIONB-1,OPTIONC
          ADD       OPTIONB,CVERT
          IF        OPTIONA=2
            BRANCH    OPTIONB,SELB1300,SELB1500,SELB1100,SELB1100,SELB1400 
          ELSE
            BRANCH    OPTIONB,SELB1300,SELB1100,SELB1100,SELB1400 
          ENDIF
.
          COMPARE   ONE,ACCEPT
          CALL      BEEP0000 IF NOT EQUAL                  * illegal option
          MOVE      ZERO,ACCEPT                            * clear accept mode
          GOTO      SELB0100
.
.---- Range Option ----
.
SELB1100  CALL      GDAT0000           * get data
          BRANCH    EXIT,SELB9500      * EXITCHAR entered ?
.
          IF        OPTIONA=2
            STORE     LABTEMP,OPTIONC,ANS,LABFROM,LABTO
            STORE     CODETEMP,OPTIONC,ANS,CODEFROM,CODETO
            STORE     DESCTEMP,OPTIONC,ANS,DESCFROM,DESCTO
          ELSE
            STORE     LABTEMP,OPTIONC,LABFROM,LABTO
            STORE     CODETEMP,OPTIONC,CODEFROM,CODETO
            STORE     DESCTEMP,OPTIONC,DESCFROM,DESCTO
          ENDIF
          CALL      RANG0000           * valid range ?
          BRANCH    EXIT,SELB1100
          GOTO      SELB0100
.
.---- Raised or Received ----
.
SELB1300  CALL      RORV0000           * get R/V    
          IF        OPTIONA=2 
            MOVE      ONE,ACCEPT
          ENDIF
          GOTO      SELB0100
.
.---- Summary Only ----
.
SELB1400  MOVE      YESNO,DEFYESNO     * get default
          REP       " _",DEFYESNO
          CALL      YORN0000           * get yes/no
          GOTO      SELB0100
.
.---- Inventory System Linkage ----
.
SELB1500  CALL      KAPISA00           * get system    
          BRANCH    EXIT,SELB1500,SELB9500
.
          IF        OPTIONA=2 
            MOVE      ONE,ACCEPT
          ENDIF
          GOTO      SELB0100
.
SELB9000  MOVE      ZERO,EXIT          * continue
          GOTO      SELB9999
.
SELB9500  MOVE      ONE,EXIT           * exit
.
SELB9999  RETURN
.*****************************************************************************
. RORV - get "yes" or "no"                            Called by lots
.    Requires - CCOL, CVERT  (screen address to get input)
.               DEFYESNO     (Default answer. Set to "_" if no default)
.    Returns  - FYESNO       (1=yes entered, 0=no entered)
.               YESNO        (string var of entry ie. "Yes" or "No"
.*****************************************************************************
RORV0000  
          DISPLAY   *P1:24,"(",*V2LON,"R",*HOFF,")aised, Recei(":
                                *V2LON,"V",*HOFF,")ed",*EL;
          MOVE      "V",ANS                 * get default
          KEYIN     *PCCOL:CVERT,*V2LON,*RV,*EL,ANS;
          PACK      ANS,ANS,SP70
          REP       "1x2xR1r1V2v2",ANS
          MOVE      ZERO,RASORREC
          MOVE      ANS,RASORREC
          MOVE      SP70,RRDESC
          LOAD      RRDESC,RASORREC,RAISED,RECEIVED          * get description
          MATCH     SP70,RRDESC
          GOTO      RORV9000 IF NOT EQUAL        * legal option ?
.
          BEEP
          GOTO      RORV0000
.
RORV9000  LOAD      TOTDESC,RASORREC,TOTRASDS,TOTRECDS
          DISPLAY   *PCCOL:CVERT,*V2LON,RRDESC:
                    *P1:24,*EL;
          MOVE      ZERO,EXIT
.
RORV9999  RETURN
.**********************************************************************
. GDAT - Get print parameter                          Called By SELB
.        Requires - CVERT, CCOL        (screen address)
.        Returns  - LABTEMP, CODETEMP (inputted data)
.                   EXIT               (1=EXITCHAR entered)
.**********************************************************************
GDAT0000  BRANCH    OPTIONA,GDAT1000,GDAT2000,GDAT3000
.
.---- range type 1 ----
.
GDAT1000  CALL      KAPMAA00           * get code
          BRANCH    EXIT,GDAT1500,GDAT9500
.
          CLEAR     CODETEMP
          PACK      CODETEMP,APMACRD
          CLEAR     LABTEMP
          PACK      LABTEMP,APMACRD
          PACK      DESCTEMP,APMACN1,SP70
          GOTO      GDAT1900
.
GDAT1500  CLEAR     CODETEMP           * nothing entered
          LOAD      CODETEMP,OPTIONC,SP70,TILDAS
          CLEAR     LABTEMP
          LOAD      LABTEMP,OPTIONC,START,FINISH
          CLEAR     DESCTEMP
.
GDAT1900  DISPLAY   *PCCOL:CVERT,*V2LON,LABTEMP,*HOFF,SP2,DESCTEMP;
          GOTO      GDAT9000
.
.---- range type 2 ----
.
GDAT2000  IF        RASORREC=1
            MOVE      APISISC,APEAISC
            CALL      KAPEAA00           * get code
            BRANCH    EXIT,GDAT2500,GDAT9500
            CLEAR     CODETEMP
            PACK      CODETEMP,APEAORD,SP70
            CLEAR     LABTEMP
            PACK      LABTEMP,APEAORD,SP70
            PACK      DESCTEMP,SP70
          ELSE
            MOVE      APISISC,APCIISC
            CALL      KAPCIA00           * get code
            BRANCH    EXIT,GDAT2500,GDAT9500
            CLEAR     CODETEMP
            PACK      CODETEMP,APCIORD,SP70
            CLEAR     LABTEMP
            PACK      LABTEMP,APCIORD,SP70
            PACK      DESCTEMP,SP70
          ENDIF
          GOTO      GDAT2900
.
GDAT2500  CLEAR     CODETEMP           * nothing entered
          LOAD      CODETEMP,OPTIONC,SP70,SP70,TILDAS
          CLEAR     LABTEMP
          LOAD      LABTEMP,OPTIONC,SP70,START,FINISH
          CLEAR     DESCTEMP
.
GDAT2900  DISPLAY   *PCCOL:CVERT,*V2LON,LABTEMP,*HOFF,SP2,DESCTEMP;
          GOTO      GDAT9000
.
.---- range type 3 ----
.
GDAT3000  MOVE      "28",CCOL
          CALL      KLMAFM00           * get code
          BRANCH    EXIT,GDAT3500,GDAT9500
          DISPLAY   *P28:CVERT,*V2LON,FMLALEDG,SLASH;
.
          MOVE      "31",CCOL
          CALL      KAMAFM00           * get code
          BRANCH    EXIT,GDAT3000,GDAT9500
.
          CLEAR     CODETEMP
          PACK      CODETEMP,FMAMLEDG,FMAMACCT
          CLEAR     LABTEMP
          PACK      LABTEMP,FMAMLEDG,SLASH,FMAMACCT
          PACK      DESCTEMP,FMAMDESC,SP70
          GOTO      GDAT3900
.
GDAT3500  CLEAR     CODETEMP           * nothing entered
          LOAD      CODETEMP,OPTIONC,SP70,TILDAS
          CLEAR     LABTEMP
          LOAD      LABTEMP,OPTIONC,START,FINISH
          CLEAR     DESCTEMP
.
GDAT3900  DISPLAY   *P28:CVERT,*V2LON,LABTEMP,*HOFF,SP2,DESCTEMP;
          GOTO      GDAT9000
.
GDAT9000  MOVE      ZERO,EXIT          * continue
          GOTO      GDAT9999
.
GDAT9500  MOVE      ONE,EXIT           * quit
.
GDAT9999  RETURN
.**********************************************************************
. RANG - Check that print range is valid              Called By SELB
.        Requires - CODEFROM, CODETO   (print range)
.        Returns  - EXIT               (1=range invalid)
.**********************************************************************
RANG0000  MATCH     SP70,CODETO
          GOTO      RANG9000 IF EQUAL  * end not yet entered  ?
          MATCH     CODEFROM,CODETO
          GOTO      RANG9500 IF LESS   * illegal range ?
.
RANG9000  MOVE      ZERO,EXIT          * continue
          GOTO      RANG9999
.
RANG9500  DISPLAY   *P1:24,*EL,*B,"Illegal Range Entered - ";
          CALL      HITENTER
          MOVE      ONE,EXIT           * quit
.
RANG9999  RETURN
.**********************************************************************
. BEEP - Beep                                         Called By SELB
.**********************************************************************
BEEP0000  BEEP
.
BEEP9999  RETURN
.**********************************************************************
. PRNTA  Print Report                                 Called By ML
.        Requires - CODEFROM, CODETO, FYESNO  (print parameters)
.**********************************************************************
PRNTA000  DISPLAY   *P49:24,"Printing : ",*EL;
          MOVE      ZERO,CPAGENO                 * reset page no.
          CLOCK     TIME,CTIMEIS                 * get current time
          MOVE      "999",CLNO                   * set line number
          UNPACK    SP70,CODELAST,CODECURR       * clear last code printed
          MOVE      ZERO,TOTAL1                  * clear totals
          MOVE      ZERO,TOTAL2
          MOVE      ZERO,TOTAL3
.
          CALL      FILEA000                    * go to correct position in file
          CALL      NRECA000                    * go to correct position in file
          BRANCH    OVRCD,PRNTA900               * end of FILEA 
          ADD       APODAMT,TOTAL1               * add to grand total
.
PRNTA100  MOVE      CODECURR,CODELAST            * save data for next print
          MOVE      APODORD,TEMPORD              * save ord no. for next print
          MOVE      APCICRD,CURSUPP
          MOVE      APODORD,CURORD
          MOVE      APODLED,CURLED
          MOVE      APODACC,CURACC
          UNPACK    APCIDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,CURDATE
          MOVE      APODAMT,TOTAL2               * add to grand total
          MOVE      ZERO,TOTAL3
          COMPARE   ONE,FYESNO
          CALL      LINA0000 IF NOT EQUAL        * WRITAeheader line for det rep
.
PRNTA200  CALL      NRECA000                    * go to correct position in file
          BRANCH    OVRCD,PRNTA900               * end of FILEA 
          ADD       APODAMT,TOTAL1               * add to grand total
          MATCH     CODELAST,CODECURR
          GOTO      PRNTA300 IF NOT EQUAL        * at start of next item ?
.
          ADD       APODAMT,TOTAL2               * add to grand total
          COMPARE   ONE,FYESNO
          CALL      LINB0000 IF NOT EQUAL        * WRITAedata line for det rep
          GOTO      PRNTA200
.
PRNTA300  CALL      WRITA000                     * print summary details
          COMPARE   ONE,EXIT
          GOTO      PRNTA100 IF NOT EQUAL        * go back to top of loop ?
.
          MOVE      CODECURR,CODELAST            * save data for next print
          MOVE      APODORD,TEMPORD              * save ord no. for next print
          CALL      LINB0000                     * WRITAedata line for det rep
          GOTO      PRNTA200
.
PRNTA900  MOVE      SP70,APCICRD                * clear current supplier
          COMPARE   ZERO,CPAGENO
          CALL      WRITB000 IF NOT EQUAL        * print details if available
          COMPARE   ZERO,CPAGENO
          CALL      HEAD0000 IF EQUAL            * print head if nothing printed
.
          CALL      PTOT0000                     * print totals
          PRINT     *N,"****   End of Report   ****"
.
PRNTA999  RETURN
.**********************************************************************
. LINA -  Print first line of detailed report section  Called By PRNT
.**********************************************************************
LINA0000  MOVE      FORMAT,OUTFORM
          FEDIT     APODAMT,OUTFORM
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * at end of page ?
          BRANCH    OPTIONA,LINA1000,LINA2000,LINA3000
.
.---- print by type 1 ----
.
LINA1000  MOVE      SP70,APMACN1
          PACK      KEY5,APCICRD,SP70
          CALL      RDAPMA1
          PRINT     APCICRD,SP2,APMACN1,SP7,APODORD,SP3,APODLED,SLASH:
                    APODACC,SP1,APODDIS,SP2,APODRES,SP1,APODDES,*113,OUTFORM
          ADD       ONE,CLNO
          MOVE      SP70,TEMPORD       * clear order no. for future printing
          GOTO      LINA9999
.
.---- print by type 2 ----
.
LINA2000  MOVE      SP70,APMACN1
          UNPACK    APCIDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      KEY5,APCICRD,SP70
          CALL      RDAPMA1
          MOVE      APMACN1,KEY28
          PRINT     APODORD,SP1,APCICRD,SP1,KEY28,SP1,CPCDATE,SP1:
                    APODLED,SLASH:
                    APODACC,SP1,APODDIS,SP2,APODRES,SP1,APODDES,*113,OUTFORM
          ADD       ONE,CLNO
          GOTO      LINA9999
.
.---- print by type 3 ----
.
LINA3000  MOVE      SP70,APMACN1
          PACK      KEY5,APCICRD,SP70
          CALL      RDAPMA1
          MOVE      SP70,FMAMDESC
          PACK      KEY14,APODLED,APODACC,SP70
          CALL      RDFMAM1
          PRINT     APODLED,SLASH,APODACC,SP1,FMAMDESC:
                    *N,*17,APODORD,SP6,APCICRD,SP2,APMACN1:
                    SP1,APODDIS,SP2,APODRES,SP1,APODDES,*113,OUTFORM
          ADD       TWO,CLNO
          GOTO      LINA9999
.
LINA9999  RETURN
.**********************************************************************
. LINB -  Print data line of detailed report section   Called By PRNT
.**********************************************************************
LINB0000  MOVE      FORMAT,OUTFORM
          FEDIT     APODAMT,OUTFORM
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * at end of page ?
          BRANCH    OPTIONA,LINB1000,LINB2000,LINB3000
.
.---- print by type 1 ----
.
LINB1000  PRINT     *45,TEMPORD,SP3,APODLED,SLASH:
                    APODACC,SP1,APODDIS,SP2,APODRES,SP1,APODDES,*113,OUTFORM
          MOVE      SP70,TEMPORD       * clear order no. for future printing
          ADD       ONE,CLNO
          GOTO      LINB9999
.
.---- print by type 2 ----
.
LINB2000  PRINT     *55,APODLED,SLASH:
                    APODACC,SP1,APODDIS,SP2,APODRES,SP1,APODDES,*113,OUTFORM
          ADD       ONE,CLNO
          GOTO      LINB9999
.
.---- print by type 3 ----
.
LINB3000  MOVE      SP70,APMACN1
          PACK      KEY5,APCICRD,SP70
          CALL      RDAPMA1
          PRINT     *17,APODORD,SP6,APCICRD,SP2,APMACN1:
                    SP1,APODDIS,SP2,APODRES,SP1,APODDES,*113,OUTFORM
          ADD       ONE,CLNO
          GOTO      LINB9999
.
LINB9999  RETURN
.**********************************************************************
. PRNTB  Print Report                                 Called By ML
.        Requires - CODEFROM, CODETO, FYESNO  (print parameters)
.**********************************************************************
PRNTB000  DISPLAY   *P49:24,"Printing : ",*EL;
          MOVE      ZERO,CPAGENO                 * reset page no.
          CLOCK     TIME,CTIMEIS                 * get current time
          MOVE      "999",CLNO                   * set line number
          UNPACK    SP70,CODELAST,CODECURR       * clear last code printed
          MOVE      ZERO,TOTAL1                  * clear totals
          MOVE      ZERO,TOTAL2
          MOVE      ZERO,TOTAL3
.
          CALL      FILEB000                    * go to correct position in file
          CALL      NRECB000                    * go to correct position in file
          BRANCH    OVRCD,PRNTB900               * end of FILEA 
          ADD       APEBAMT,TOTAL1               * add to grand total
.
PRNTB100  MOVE      CODECURR,CODELAST            * save data for next print
          MOVE      APEBORD,TEMPORD              * save ord no. for next print
          MOVE      APEACRD,CURSUPP
          MOVE      APEBORD,CURORD
          MOVE      APEBLED,CURLED
          MOVE      APEBACC,CURACC
          UNPACK    APEADAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,CURDATE
          MOVE      APEBAMT,TOTAL2               * clear sub total
          MOVE      ZERO,TOTAL3
          COMPARE   ONE,FYESNO
          CALL      LINC0000 IF NOT EQUAL        * WRITAeheader line for det rep
.
PRNTB200  CALL      NRECB000                    * go to correct position in file
          BRANCH    OVRCD,PRNTB900               * end of FILEA 
          ADD       APEBAMT,TOTAL1               * add to grand total
          MATCH     CODELAST,CODECURR
          GOTO      PRNTB300 IF NOT EQUAL        * at start of next item ?
.
          ADD       APEBAMT,TOTAL2               * add to sub total
          COMPARE   ONE,FYESNO
          CALL      LIND0000 IF NOT EQUAL        * WRITAedata line for det rep
          GOTO      PRNTB200
.
PRNTB300  CALL      WRITB000                     * print summary details
          COMPARE   ONE,EXIT
          GOTO      PRNTB100 IF NOT EQUAL        * go back to top of loop ?
.
          MOVE      CODECURR,CODELAST            * save data for next print
          MOVE      APEBORD,TEMPORD              * save ord no. for next print
          CALL      LIND0000                     * WRITAedata line for det rep
          GOTO      PRNTB200
.
PRNTB900  MOVE      SP70,APEACRD                * clear current supplier
          COMPARE   ZERO,CPAGENO
          CALL      WRITB000 IF NOT EQUAL        * print details if available
          COMPARE   ZERO,CPAGENO
          CALL      HEAD0000 IF EQUAL            * print head if nothing printed
.
          CALL      PTOT0000                     * print totals
          PRINT     *N,"****   End of Report   ****"
.
PRNTB999  RETURN
.**********************************************************************
. LINC -  Print first line of detailed report section  Called By PRNT
.**********************************************************************
LINC0000  MOVE      FORMAT,OUTFORM
          FEDIT     APEBAMT,OUTFORM
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * at end of page ?
          BRANCH    OPTIONA,LINC1000,LINC2000,LINC3000
.
.---- print by type 1 ----
.
LINC1000  MOVE      SP70,APMACN1
          PACK      KEY5,APEACRD,SP70
          CALL      RDAPMA1
          PRINT     APEACRD,SP2,APMACN1,SP7,APEBORD,SP3,APEBLED,SLASH:
                    APEBACC,SP1,APEBDIS,SP2,APEBRES,SP1,APEBDES,*113,OUTFORM
          ADD       ONE,CLNO
          MOVE      SP70,TEMPORD       * clear order no. for future printing
          GOTO      LINC9999
.
.---- print by type 2 ----
.
LINC2000  MOVE      SP70,APMACN1
          UNPACK    APEADAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      KEY5,APEACRD,SP70
          CALL      RDAPMA1
          MOVE      APMACN1,KEY28
          PRINT     APISISC,APEBORD,SP1,APEACRD,SP1,KEY28,CPCDATE:
                    SP1,APEBLED,SLASH:
                    APEBACC,SP1,APEBDIS,SP2,APEBRES,SP1,APEBDES,*113,OUTFORM
          ADD       ONE,CLNO
          GOTO      LINC9999
.
.---- print by type 3 ----
.
LINC3000  MOVE      SP70,APMACN1
          PACK      KEY5,APEACRD,SP70
          CALL      RDAPMA1
          MOVE      SP70,FMAMDESC
          PACK      KEY14,APEBLED,APEBACC,SP70
          CALL      RDFMAM1
          PRINT     APEBLED,SLASH,APEBACC,SP1,FMAMDESC:
                    *N,*17,APEBORD,SP6,APEACRD,SP2,APMACN1:
                    SP1,APEBDIS,SP2,APEBRES,SP1,APEBDES,*113,OUTFORM
          ADD       TWO,CLNO
          GOTO      LINC9999
.
LINC9999  RETURN
.**********************************************************************
. LIND -  Print data line of detailed report section   Called By PRNT
.**********************************************************************
LIND0000  MOVE      FORMAT,OUTFORM
          FEDIT     APEBAMT,OUTFORM
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * at end of page ?
          BRANCH    OPTIONA,LIND1000,LIND2000,LIND3000
.
.---- print by type 1 ----
.
LIND1000  PRINT     *45,TEMPORD,SP3,APEBLED,SLASH:
                    APEBACC,SP1,APEBDIS,SP2,APEBRES,SP1,APEBDES,*113,OUTFORM
          MOVE      SP70,TEMPORD       * clear order no. for future printing
          ADD       ONE,CLNO
          GOTO      LIND9999
.
.---- print by type 2 ----
.
LIND2000  PRINT     *55,APEBLED,SLASH:
                    APEBACC,SP1,APEBDIS,SP2,APEBRES,SP1,APEBDES,*113,OUTFORM
          ADD       ONE,CLNO
          GOTO      LIND9999
.
.---- print by type 3 ----
.
LIND3000  MOVE      SP70,APMACN1
          PACK      KEY5,APEACRD,SP70
          CALL      RDAPMA1
          PRINT     *17,APEBORD,SP6,APEACRD,SP2,APMACN1:
                    SP1,APEBDIS,SP2,APEBRES,SP1,APEBDES,*113,OUTFORM
          ADD       ONE,CLNO
          GOTO      LIND9999
.
LIND9999  RETURN
.**********************************************************************
. HEAD - Print Header                            Called By lots
.**********************************************************************
HEAD0000  COMPARE   ZERO,CPAGENO
          CALL      LINE0000 IF NOT EQUAL        * first page ?
.
          BRANCH    FYESNO,HEAD2000              * summary or detailed ?
.
.---- detailed report ----
.
          CALL      HEAD132                      * print header
          PRINT     *+,"From ",ALAB,SP1,LABFROM," to ",LABTO:
                    SP5,RRDESC,*118,"Detailed Report"
          CALL      LINE0000                     * print line
          BRANCH    OPTIONA,HEAD1100,HEAD1200,HEAD1300
.
HEAD1100  PRINT     "Supplier                                   Order No.":
                    "  Ledger/Account   Diss  Resp Item Description":
                    *119,TOTDESC
          GOTO      HEAD1400
HEAD1200  PRINT     "Order No.  Supplier                                 ":
                    "  Ledger/Account   Diss  Resp Item Description":
                    *119,TOTDESC
          GOTO      HEAD1400
HEAD1300  PRINT     "Ledger/Account  Order No.    Supplier               ":
                    "                Diss  Resp Item Description":
                    *119,TOTDESC
          GOTO      HEAD1400
.
HEAD1400  CALL      LINE0000                     * print line
          ADD       "2",CLNO
          GOTO      HEAD9999
.
.---- summary report ----
.
HEAD2000  CALL      HEAD80                       * print header
          PRINT     *+,"From ",ALAB,SP1,LABFROM," to ",LABTO:
                    *61,"Summary Details Only",*N:
                    RRDESC 
          ADD       ONE,CLNO
          CALL      LINE0000                     * print line
          BRANCH    OPTIONA,HEAD2100,HEAD2200,HEAD2300
.
HEAD2100  PRINT     "Supplier",*62,TOTDESC
          GOTO      HEAD2400
HEAD2200  PRINT     "Order No.  Supplier",*62,TOTDESC
          GOTO      HEAD2400
HEAD2300  PRINT     "Ledger/Account",*62,TOTDESC
          GOTO      HEAD2400
.
HEAD2400  CALL      LINE0000                     * print line
          ADD       "2",CLNO
          GOTO      HEAD9999
.
HEAD9999  RETURN
.**********************************************************************
. LINE - Print Line                              Called By PRNT
.**********************************************************************
LINE0000  BRANCH    FYESNO,LINE1000    * summary or detailed ?
.
          PRINT     "--------------------------------------------":
                    "--------------------------------------------":
                    "--------------------------------------------"
          ADD       ONE,CLNO
          GOTO      LINE9999
.
LINE1000  PRINT     "----------------------------------------":
                    "----------------------------------------"
          ADD       ONE,CLNO
          GOTO      LINE9999
.
LINE9999  RETURN
.**********************************************************************
. PTOT - Print totals                            Called By PRNT
.**********************************************************************
PTOT0000  MOVE      FORMAT,OUTFORM
          FEDIT     TOTAL1,OUTFORM
          BRANCH    FYESNO,PTOT1000              * summary or detailed ?
.
          PRINT     *40,"Overall Total",*113,OUTFORM
          ADD       ONE,CLNO
          CALL      LINE0000
          GOTO      PTOT9999
.
PTOT1000  CALL      LINE0000
          PRINT     *20,"Overall Total",*59,OUTFORM
          ADD       ONE,CLNO
          CALL      LINE0000
          GOTO      PTOT9999
.
PTOT9999  RETURN
.**********************************************************************
. FILEA  Go to starting position on FILEA        Called By PRNT
.**********************************************************************
FILEA000  BRANCH    OPTIONA,FILEA100,FILEA200,FILEA300
.
.---- print by type 1 ----
.
FILEA100  
          UNPACK    Z70,APCIISC,APCIORD
          PACK      KEY21,CODEFROM,SP70
          CALL      RSAPCI2
          CALL      RKAPCI2
          PACK      KEY14,APCIISC,APCIORD,SP70
          CALL      RSAPOD1
          GOTO      FILEA999
.
.---- print by type 2 ----
.
FILEA200  PACK      KEY14,APISISC,CODEFROM,SP70
          CALL      RSAPOD1
          GOTO      FILEA999
.
.---- print by type 3 ----
.
FILEA300  PACK      KEY28,CODEFROM,SP70
          CALL      RSAPOD2
          GOTO      FILEA999
.
FILEA999  RETURN
.**********************************************************************
. NRECA   Get next record                          Called By PRNT
.         Returns : OVRCD     (1=no more records)
.**********************************************************************
NRECA000  BRANCH    OPTIONA,NRECA100,NRECA200,NRECA300
.
.---- print by type 1 ----
.
NRECA100  UNPACK    SP70,APODISC,APODORD
          CALL      RKAPOD1
.
          PACK      KEY8,APODISC,APODORD,SP70
          PACK      SKEY8,APCIISC,APCIORD,SP70
          MATCH     KEY8,SKEY8
          IF        !@EQUAL
            CALL      RKAPCI2
            BRANCH    OVRCD,NRECA950
            MATCH     APCICRD,CODETO
            GOTO      NRECA950 IF LESS
            UNPACK    APCIDAT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,CURDATE
            PACK      KEY14,APCIISC,APCIORD,SP70
            CALL      RSAPOD1
            GOTO      NRECA100
          ENDIF
.
          PACK      CODECURR,APCICRD,SP70
          BRANCH    FYESNO,NRECA900
          PACK      CODECURR,APCICRD,APODORD,SP70
          GOTO      NRECA900
.
.---- print by type 2 ----
.
NRECA200  CALL      RKAPOD1
          BRANCH    OVRCD,NRECA950     * no more records ?
          MATCH     APISISC,APODISC
          GOTO      NRECA950 IF NOT EQUAL
          MATCH     APODORD,CODETO
          GOTO      NRECA950 IF LESS
          PACK      CODECURR,APODISC,APODORD,SP70
          GOTO      NRECA900
.
.---- print by type 3 ----
.
NRECA300  CALL      RKAPOD2
          BRANCH    OVRCD,NRECA950     * no more records ?
          PACK      KEY14,APODLED,APODACC,SP70
          MATCH     KEY14,CODETO
          GOTO      NRECA950 IF LESS
          PACK      CODECURR,APODLED,APODACC,SP70
          IF        OPTIONA=3
            SUB       APODGSTA,APODAMT
          ENDIF
          GOTO      NRECA900
.
NRECA900  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * at end of page ?
.
          UNPACK    SP70,APCIDAT,APCICRD
          PACK      KEY8,APODISC,APODORD,SP70
          CALL      RDAPCI1
.
          MOVE      ZERO,OVRCD         * continue
          GOTO      NRECA999
.
NRECA950  MOVE      ONE,OVRCD          * exit
.
NRECA999  RETURN
.**********************************************************************
. FILEB  Go to starting position on FILEB        Called By PRNT
.**********************************************************************
FILEB000  BRANCH    OPTIONA,FILEB100,FILEB200,FILEB300
.
.---- print by type 1 ----
.
FILEB100  
          UNPACK    Z70,APEAISC,APEAORD
          PACK      KEY21,CODEFROM,SP70
          CALL      RSAPEA2
          CALL      RKAPEA2
          PACK      KEY14,APEAISC,APEAORD,SP70
          CALL      RSAPEB1
          GOTO      FILEB999
.
.---- print by type 2 ----
.
FILEB200  PACK      KEY14,APISISC,CODEFROM,SP70
          CALL      RSAPEB1
          GOTO      FILEB999
.
.---- print by type 3 ----
.
FILEB300  PACK      KEY28,CODEFROM,SP70
          CALL      RSAPEB2
          GOTO      FILEB999
.
FILEB999  RETURN
.**********************************************************************
. NRECB   Get next record                          Called By PRNT
.         Returns : OVRCD     (1=no more records)
.**********************************************************************
NRECB000  BRANCH    OPTIONA,NRECB100,NRECB200,NRECB300
.
.---- print by type 1 ----
.
NRECB100  UNPACK    SP70,APEBISC,APEBORD
          CALL      RKAPEB1
.
          PACK      KEY8,APEBISC,APEBORD,SP70
          PACK      SKEY8,APEAISC,APEAORD,SP70
          MATCH     KEY8,SKEY8
          IF        !@EQUAL
            CALL      RKAPEA2
            BRANCH    OVRCD,NRECB950
            MATCH     APEACRD,CODETO
            GOTO      NRECB950 IF LESS
            UNPACK    APEADAT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,CURDATE
            PACK      KEY14,APEAISC,APEAORD,SP70
            CALL      RSAPEB1
            GOTO      NRECB100
          ENDIF
.
          PACK      CODECURR,APEACRD,SP70
          BRANCH    FYESNO,NRECB9000
          PACK      CODECURR,APEACRD,APEBORD,SP70
          GOTO      NRECB900
.
.---- print by type 2 ----
.
NRECB200  CALL      RKAPEB1
          BRANCH    OVRCD,NRECB950     * no more records ?
          MATCH     APEBISC,APISISC
          GOTO      NRECB950 IF NOT EQUAL
          MATCH     APEBORD,CODETO
          GOTO      NRECB950 IF LESS
          PACK      CODECURR,APEBISC,APEBORD
          GOTO      NRECB900
.
.---- print by type 3 ----
.
NRECB300  CALL      RKAPEB2
          BRANCH    OVRCD,NRECB950     * no more records ?
          PACK      KEY14,APEBLED,APEBACC,SP70
          MATCH     KEY14,CODETO
          GOTO      NRECB950 IF LESS
          PACK      CODECURR,APEBLED,APEBACC,SP70
          IF        OPTIONA=3
            SUB       APEBGST,APEBAMT
          ENDIF
          GOTO      NRECB900
.
NRECB900  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS    * at end of page ?
.
          UNPACK    SP70,APEADAT,APEACRD
          PACK      KEY8,APEBISC,APEBORD,SP70
          CALL      RDAPEA1
.
          MOVE      ZERO,OVRCD         * continue
          GOTO      NRECB999
.
NRECB950  MOVE      ONE,OVRCD          * exit
.
NRECB999  RETURN
.**********************************************************************
. WRITA - print record                                Called By PRNT
.**********************************************************************
WRITA000  MOVE      FORMAT,OUTFORM
          FEDIT     TOTAL2,OUTFORM       * format amount
          BRANCH    FYESNO,WRITA200
          BRANCH    OPTIONA,WRITA110,WRITA120,WRITA130     * detailed report
.
.---- print by type 1 ----
.
WRITA110  DISPLAY   *P60:24,*EL,*V2LON,CURSUPP;
          PRINT     *11,"Purchase Order Date : ",CURDATE:
                    *99,"Order Total : ",*113,OUTFORM
          ADD       ONE,CLNO
          ADD       TOTAL2,TOTAL3
          MOVE      APODAMT,TOTAL2
          MATCH     APCICRD,CURSUPP
          GOTO      WRITA950 IF EQUAL  * still on same supplier ?
.
          MOVE      FORMAT,OUTFORM
          FEDIT     TOTAL3,OUTFORM     * format amount
          PRINT     *40,"Supplier Total",*113,OUTFORM
          ADD       ONE,CLNO
          CALL      LINE0000
          GOTO      WRITA900
.
.---- print by type 2 ----
.
WRITA120  DISPLAY   *P60:24,*EL,*V2LON,CURORD;
          PRINT     *11,"Purchase Order Date : ",CURDATE:
                    *99,"Order Total : ",*113,OUTFORM
          ADD       ONE,CLNO
          CALL      LINE0000
          GOTO      WRITA900
.
.---- print by type 3 ----
.
WRITA130  DISPLAY   *P60:24,*EL,*V2LON,CURLED,SLASH,CURACC;
          PRINT     *40,"Account Total",*113,OUTFORM
          ADD       ONE,CLNO
          CALL      LINE0000
          GOTO      WRITA900
.
.
WRITA200  BRANCH    OPTIONA,WRITA210,WRITA220,WRITA230     * summary report
.
.---- print by type 1 ----
.
WRITA210  DISPLAY   *P60:24,*EL,*V2LON,CURSUPP;
          MOVE      SP70,APMACN1
          PACK      KEY5,CURSUPP,SP70
          CALL      RDAPMA1
          PRINT     CURSUPP,SP2,APMACN1,*59,OUTFORM
          ADD       ONE,CLNO
          GOTO      WRITA900
.
.---- print by type 2 ----
.
WRITA220  DISPLAY   *P60:24,*EL,*V2LON,CURORD;
          MOVE      SP70,APMACN1
          PACK      KEY5,CURSUPP,SP70
          CALL      RDAPMA1
          PRINT     CURORD,SP4,CURSUPP,SP2,APMACN1,*59,OUTFORM
          ADD       ONE,CLNO
          GOTO      WRITA900
.
.---- print by type 3 ----
.
WRITA230  DISPLAY   *P60:24,*EL,*V2LON,CURLED,SLASH,CURACC;
          MOVE      SP70,FMAMDESC
          PACK      KEY14,CURLED,CURACC,SP70
          CALL      RDFMAM1
          PRINT     CURLED,SLASH,CURACC,SP2,FMAMDESC,*59,OUTFORM
          ADD       ONE,CLNO
          GOTO      WRITA900
.
WRITA900  MOVE      ZERO,EXIT          * go back to top of print loop
          GOTO      WRITA999
.
WRITA950  MOVE      ONE,EXIT           * dont go back to top of print loop
.
WRITA999  RETURN
.**********************************************************************
. WRITB - print record                                Called By PRNT
.**********************************************************************
WRITB000  MOVE      FORMAT,OUTFORM
          FEDIT     TOTAL2,OUTFORM       * format amount
          BRANCH    FYESNO,WRITB200
          BRANCH    OPTIONA,WRITB110,WRITB120,WRITB130     * detailed report
.
.---- print by type 1 ----
.
WRITB110  DISPLAY   *P60:24,*EL,*V2LON,CURSUPP;
          PRINT     *11,"Purchase Order Date : ",CURDATE:
                    *99,"Order Total : ",*113,OUTFORM
          ADD       ONE,CLNO
          ADD       TOTAL2,TOTAL3
          MOVE      APEBAMT,TOTAL2
          MATCH     APEACRD,CURSUPP
          GOTO      WRITB950 IF EQUAL  * still on same supplier ?
.
          MOVE      FORMAT,OUTFORM
          FEDIT     TOTAL3,OUTFORM     * format amount
          PRINT     *40,"Supplier Total",*113,OUTFORM
          ADD       ONE,CLNO
          CALL      LINE0000
          GOTO      WRITB900
.
.---- print by type 2 ----
.
WRITB120  DISPLAY   *P60:24,*EL,*V2LON,CURORD;
          PRINT     *11,"Purchase Order Date : ",CURDATE:
                    *99,"Order Total : ",*113,OUTFORM
          ADD       ONE,CLNO
          CALL      LINE0000
          GOTO      WRITB900
.
.---- print by type 3 ----
.
WRITB130  DISPLAY   *P60:24,*EL,*V2LON,CURLED,SLASH,CURACC;
          PRINT     *40,"Account Total",*113,OUTFORM
          ADD       ONE,CLNO
          CALL      LINE0000
          GOTO      WRITB900
.
.
WRITB200  BRANCH    OPTIONA,WRITB210,WRITB220,WRITB230     * summary report
.
.---- print by type 1 ----
.
WRITB210  DISPLAY   *P60:24,*EL,*V2LON,CURSUPP;
          MOVE      SP70,APMACN1
          PACK      KEY5,CURSUPP,SP70
          CALL      RDAPMA1
          PRINT     CURSUPP,SP2,APMACN1,*59,OUTFORM
          ADD       ONE,CLNO
          GOTO      WRITB900
.
.---- print by type 2 ----
.
WRITB220  DISPLAY   *P60:24,*EL,*V2LON,CURORD;
          MOVE      SP70,APMACN1
          PACK      KEY5,CURSUPP,SP70
          CALL      RDAPMA1
          PRINT     CURORD,SP4,CURSUPP,SP2,APMACN1,*59,OUTFORM
          ADD       ONE,CLNO
          GOTO      WRITB900
.
.---- print by type 3 ----
.
WRITB230  DISPLAY   *P60:24,*EL,*V2LON,CURLED,SLASH,CURACC;
          MOVE      SP70,FMAMDESC
          PACK      KEY14,CURLED,CURACC,SP70
          CALL      RDFMAM1
          PRINT     CURLED,SLASH,CURACC,SP2,FMAMDESC,*59,OUTFORM
          ADD       ONE,CLNO
          GOTO      WRITB900
.
WRITB900  MOVE      ZERO,EXIT          * go back to top of print loop
          GOTO      WRITB999
.
WRITB950  MOVE      ONE,EXIT           * dont go back to top of print loop
.
WRITB999  RETURN
.******************************************************************************
.* Redisplays                                                                 *
.******************************************************************************
RAMAFM00  
          CALL      SCRB0000           * display screen B
          DISPLAY   *P28:CVERT,*V2LON,FMLALEDG,SLASH;
          RETURN
.
RCEXFM00  
RCRAIB00  
RCRBIB00  
RLMAFM00  
          CALL      SCRB0000           * display screen B
          RETURN
.******************************************************************************
.* INCLUDES FOR I/O'S                                                         *
.******************************************************************************
.
          INC       FMSAMAIO/INC       * account
          INC       APSORDIO/INC       * commited expenditure
          INC       APSCIOIO/INC       * commited expenditure
          INC       APSCEAIO/INC       * commited expenditure
          INC       APSCEBIO/INC       * commited expenditure
          INC       APSISLIO/INC       * supplier
          INC       APSMASIO/INC       * supplier
          INC       APSMXRIO/INC       * supplier
          INC       FMSLMAIO/INC       * ledger
.
          INC       FMSAMAKY           * account
          INC       APSCKIKY
          INC       APSCIOKY           * commited expenditure
          INC       APSCEAKY           * commited expenditure
          INC       APSISLKY           * commited expenditure
          INC       APSMASKY           * supplier
          INC       FMSLMAKY           * ledger
.
          INC       FMSSTDCD           * FMS Acc. standard routines
