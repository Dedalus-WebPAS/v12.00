.
. *********************************************************
. *                                                       *
. * Include Name :  PATNOTES.PBL                          *
. *                                                       *
. * Function     :  Subroutine Include for                *
. *                 Case Notes Display                    *
. *                                                       *
. * Subroutines  :  PATNOTES : Display the case note locs.*
. *                 MAINNOTE : Normal Select item, (P)ost *
. *                            (C)ancel routine, with an  *
. *                            extra Case (N)otes option  *
. *                                                       *
. * Files needed :  IBACOMM.INC    IBA Special Variables  *
. *                 PATHOSFD.INC   Hospital File          *
. *                                                       *
. * Modifications:  V5.00    22/08/89 Graeme Williams     *
. *                          Include Written              *
. *                 V5.01.01 06/08/91 ROD                 *
. *                          Uses PTCNUCSL if ON (patuslaf*
. *                 V5.01.02 24/08/94 Rob Leonard         *
. *                          Make keyin case incencitive  *
. *                                                       *
. *********************************************************
.
. *****************************************************************************
. * PATNOTES   : Display the available case notes locations                   *
. *                                                                           *
. * Parameters : PHCNOTES Case notes indicators (from PMI record)             *
. *              NOTEDTYP = 0 If the display is to have a keyin to exit       *
. *                         1 If the display is to just exit. This option is  *
. *                           used in the enquiry programs.                   *
. *              PTCNUCSL  Sec 20 Pos 186  Using Case Note Location file?     *
. *              CVERT    Position to start display from                      *
. *****************************************************************************
.
PATNOTES  OPEN      PATHOSP2,"pathospf"      * Open the hospital file
          MOVE      ZERO,HOSINDX             * Start from the beginning
.
          COMPARE   ONE,PTCNUCSL             * using case notes location file?
          GOTO      NOTES050 IF NOT EQUAL    * No.
.
          OPEN      PATCSLA1,"patcslaf"      * Open case notes location file
          OPEN      PATCSLA2,"patcslaf"      * Open case notes location file
.
          PACK      KEY11,PURNO,SP20         * check if pat. has records
          CALL      RSPTCSL1
          CALL      RKPTCSL1
          BRANCH    OVRCD,NOTES800           * no records for this Patient
.
          MATCH     PURNO,PTCLURNO           * same U/R Number?
          GOTO      NOTES800 IF NOT EQUAL
.
          PACK      KEY41,PURNO,SP20         * check if pat. has records
          CALL      RSPTCSL2
          GOTO      NOTES060
.
NOTES050  RESET      PHCNOTES                * Reset the case notes indicators
          MATCH     "00000000000000000000",PHCNOTES
          GOTO      NOTES800 IF EQUAL        * No case notes indicators set.
.
NOTES060  MOVE      CVERT,NOTEVERT           * Save the line to start from
.
          DISPLAY   *P1:CVERT,*EF,*P20:CVERT:
                    *V2LON,*ULON," Hospitals with Case Notes for ":
                    PURNO,SP1
.
          ADD       ONE,CVERT                * Initialise starting line
          MOVE      ONE,CCOL                 * Initialise starting column
.
NOTES100  COMPARE   ONE,PTCNUCSL             * using case notes location file?
          GOTO      NOTES300 IF NOT EQUAL    * No.
.
          CALL      RKPTCSL2
          BRANCH    OVRCD,NOTES900           * no records for this Patient
.
          MATCH     PURNO,PTCLURNO           * same U/R Number?
          GOTO      NOTES900 IF NOT EQUAL
          GOTO      NOTES400
.
.         Try the next position in the case notes indicator variable
.
NOTES300  ADD       ONE,HOSINDX              * Try the next position
          COMPARE   HOSINDX,TWENTY           * Have we finished ?
          GOTO      NOTES900 IF LESS         * Yes. Exit the include
.
.         Check if this hospital has case notes
.
          RESET     PHCNOTES,HOSINDX         * Reset to the appropriate place
          CMATCH    "1",PHCNOTES             * Do we have case notes ?
          GOTO      NOTES300 IF NOT EQUAL    * No. Try the next position
.
.         Get the hospital name and code that corresponds to this position
.
          MOVE      HOSINDX,KEY2             * Key for hospital file
          CALL      RDHOSP2                  * Check for a record
          BRANCH    OVRCD,NOTES300           * No record. Ignore data
.
.         We have a record ready to display. Now check if there is room on
.         the screen for the data
.
NOTES400  COMPARE   CVERT,TWENTY2            * Only display up to line 22
          GOTO      NOTES700 IF NOT LESS     * Okay. Go and display data
.
.         This column is full. Try a second column
.
          ADD       "40",CCOL                * Shift across to the next column
          MOVE      NOTEVERT,CVERT           * Restore starting line number
          ADD       ONE,CVERT                * Drop down one line from heading
.
.         Check if the screen is completely full (2 columns of data)
.
          COMPARE   "70",CCOL                * Are we past column 70 for disp ?
          GOTO      NOTES700 IF LESS         * No. Go and display data
.
.         Screen is full. Check what the user wants to do
.
          KEYIN     *P1:24,*EL,"(",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"N",*HOFF,")ext Screen ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
.
          CMATCH    ANSE,ANS                 * Do they want to exit
          GOTO      NOTES999 IF EQUAL        * Yes. Leave include.
.
          MOVE      ONE,CCOL                 * Reposition starting column
          DISPLAY   *P1:CVERT,*EF            * Clear the screen
.
.         Display the data for this record
.
NOTES700  COMPARE   ONE,PTCNUCSL             * using case notes location file?
          GOTO      NOTES710 IF NOT EQUAL    * No.
.
          DISPLAY   *PCCOL:CVERT,PTCLDESC;
          ADD       ONE,CVERT                * Position for next display
          GOTO      NOTES100
.
NOTES710  DISPLAY   *PCCOL:CVERT,*V2LON,HOSCODE,*HOFF:
                    SP3,HOSDESC;
          ADD       ONE,CVERT                * Position for next display
          GOTO      NOTES100
.
.         No case notes indicators set
.
NOTES800  DISPLAY   *P1:CVERT,*EF,*V2LON,*P10:CVERT:
                    "*** No Case Notes Available for U/R Number ":
                    PURNO," ***"
.
.         We have finished the include. Check if the user wants a
.         "Hit <ENTER> to continue ... " message or not
.
NOTES900  BRANCH    NOTEDTYP,NOTES999        * No message wanted
.
          KEYIN     *P1:24,*EL,"Hit ",*V2LON,"<ENTER>",*HOFF:
                               " to continue ",*EOFF,ANS;
.
NOTES999  CLOSE     PATHOSP2                 * Close the hospital file
          CLOSE     PATCSLA1                 * close case note location file
          CLOSE     PATCSLA2                 * close case note location file
          RETURN                             * Return from whence thou comest
.
. *****************************************************************************
. * MAINNOTE   : Select Item, (P)ost, (C)ancel, Case (N)otes                  *
. *                                                                           *
. * Parameters : CUSENOTE = 0 if we are using the standard MAINQST            *
. *                         1 if we are using the above question              *
. * Returns    : CCITEM   = 0  If (P)ost selected                             *
. *                         -1 If (C)ancel option selected.                   *
. *                         -2 If Case (N)otes option selected                *
. *****************************************************************************
MAINNOTE  MATCH     ZEROUR,PURNO
          GOTO      MNOTE100 IF EQUAL
.
          BRANCH    CUSENOTE,MNOTE200
.
.         Use the standard question
.
MNOTE100  CALL      MAINQST
          COMPARE   "-2",CCITEM
          GOTO      MAINNOTE IF EQUAL
.
          GOTO      MNOTE999
.
.         Use the new question
.
MNOTE200  MOVE      ZERO,CCITEM
          MOVE      SP10,CCITEMD
          KEYIN     *P1:24,*EL,"Select Item, (",*V2LON,"P",*HOFF,")ost, (":
                    *V2LON,"C",*HOFF,")ancel, Case (":
                    *V2LON,"N",*HOFF,")otes ? ",*V2LON,*JR,CCITEMD;
          REP       UPPLOW,CCITEMD
          RESET     CCITEMD
          GOTO      MNOTE200 IF EOS
.
          MATCH     "  C",CCITEMD
          GOTO      MNOTE300 IF EQUAL
.
          MATCH     "  P",CCITEMD
          GOTO      MNOTE400 IF EQUAL
.
          MATCH     "  N",CCITEMD
          GOTO      MNOTE500 IF EQUAL
.
          TYPE      CCITEMD
          GOTO      MNOTE200 IF NOT EQUAL
.
          MOVE      CCITEMD,CCITEM
.
          COMPARE   ONE,CCITEM
          GOTO      MNOTE200 IF LESS
.
          GOTO      MNOTE999
.
MNOTE300  MOVE      "-1",CCITEM
          GOTO      MNOTE999
.
MNOTE400  MOVE      "0",CCITEM
          GOTO      MNOTE999
.
MNOTE500  MOVE      "-2",CCITEM
.
MNOTE999  RETURN
