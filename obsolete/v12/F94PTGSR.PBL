.***************************************************************************
.* System    :   Patient Management System                                 *
.* Program   :   F93PTGSR                                                  *
.* Desc      :   Update Soundex Surname key for names with "TZ" (CAR 52359)*
.***************************************************************************
.* Date      :   25/10/2004                                                *
.* Author    :   Steve Armstrong                                           *
.* Function  :   This program will loop through patgsrnf looking for       *
.*               patient's who have a surname containing "TZ".  For any    *
.*               matching record found, the patient's surname soundex key  *
.*               will be recalculated and updated.                         *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATGSRFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      8
.
.
PRGID     INIT      "F93PTGSR"
PRGNAM    INIT      "Soundex Surname Fixit for TZ"
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with fixit
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patgsrnf";
          OPEN      PATGSRN1,"patgsrnf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                              *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing:";
.
          MOVE      ZERO,COUNTER                 * initialise counter
.
          PACK      KEY70,SP30,SP30,SP10
          CALL      RSPTGSR1                     * position at start of file
PROC0500  CALL      RKPTGSR1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          ADD       ONE,COUNTER
          IF        (COUNTER%1000) = 0|COUNTER = 1
            DISPLAY   *P13:24,*V2LON,GSRURNO;
          ENDIF
.
          SCAN      SCANTZ,GSRSNAM               * does surname contain "TZ"
          GOTO      PROC0500 IF NOT EQUAL        * no - ignore
.
.         Surname contains "TZ", so determine new surname soundex key
.
          CALL      SOUNDEX
          CALL      UPPTGSR1                     * update record
          GOTO      PROC0500                     * get next record
.
PROC9000  DISPLAY   *P1:24,*EL,"Fixit completed.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATSNDX
.
          INC       PATGSRIO/INC
