.******************************************************************************
.* Include Name   : KYOPROPR.PBL                                              *
.*                                                                            *
.* Description    : Keyin of a Theatre code from OPROPRFD                     *
.*                                                                            *
.* Author         : LOFTY                                                     *
.*                                                                            *
.* Parameters     : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF6 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                                                                            *
.* Returns        : OPRMROOM - Keyed in code                                  *
.*                  OPRMDESC - Description                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.* On return      : You must display the description yourself.                *
.*                                                                            *
.******************************************************************************
KYOPROPR  MOVE      ONE,FORM1               * set ? as not entered
          MOVE      ECOL,CCOL
          MOVE      EVERT,CVERT
.
KOPRM100  MATCH     SP6,CKYIDEF6
          GOTO      KOPRM120 IF EQUAL       * no default
.
          MOVE      CKYIDEF6,OPRMROOM
          KEYIN     *PCCOL:CVERT,*DV,OPRMROOM:
                    *PCCOL:CVERT,*V2LON,*RV,OPRMROOM:
                    *PCCOL:CVERT,*DV,OPRMROOM
          GOTO      KOPRM150
.
KOPRM120  KEYIN     *PCCOL:CVERT,*DV,UNDLN6:
                    *PCCOL:CVERT,*V2LON,OPRMROOM:
                    *PCCOL:CVERT,*DV,OPRMROOM
.
KOPRM150  PACK      OPRMROOM,OPRMROOM,SP6
          MATCH     SP6,OPRMROOM
          GOTO      KOPRM790 IF EQUAL       * nothing entered
          MATCH     EXITCHAR,OPRMROOM
          GOTO      KOPRM820 IF EQUAL       * exitchar entered
          MATCH     QUEST,OPRMROOM
          GOTO      KOPRM200 IF NOT EQUAL   * ? not entered
.
.         ? entered
.
          IF        FORM1 = ONE
            MOVE      "0",HLEF                * save all of the screen
            CALL      GETSCR00                * save screen
            MOVE      ZERO,FORM1              * set as ? performed
          ENDIF
.
          CALL      OPROPRDS                * display valid entries
.
          IF        CNEWDS = ZERO
            CALL      PUTSCR00                * redisplay screen
            MOVE      ONE,FORM1               * set as ? not performed
            MOVE      ECOL,CCOL
            MOVE      EVERT,CVERT
          ELSE
            DISPLAY   *P1:24,"Input the code : ",*EL;
            MOVE      "24",CVERT
            MOVE      "18",CCOL
            MOVE      ZERO,FORM1              * set ? as entered
          ENDIF
          GOTO      KOPRM100
.
.         validate what was entered
.
KOPRM200  MOVE      OPRMROOM,KEY6
          CALL      RDOPOPR1
          BRANCH    OVRCD,KOPRM400          * invalid code
.
          MOVE      ZERO,EXIT               * set as valid code
          GOTO      KOPRM800
.
.         invalid code
.
KOPRM400  DISPLAY   *P1:24,*B,"Invalid Code.  ",*EL;
          CALL      HITENTER
          BRANCH    FORM1,KOPRM100
          DISPLAY   *P1:24,"Input the code : ",*EL;
          GOTO      KOPRM100
.
.         nothing entered
.
KOPRM790  MOVE      SP30,OPRMDESC            * clear desc
          MOVE      ONE,EXIT                * set exit flag
          COMPARE   ZERO,CKYIMAND
          GOTO      KOPRM800 IF EQUAL       * valid entry
.
.         field is mandatory
.
          DISPLAY   *P1:24,*B,"Field is mandatory.  ",*EL;
          CALL      HITENTER
          BRANCH    FORM1,KOPRM100
          DISPLAY   *P1:24,"Input the code : ",*EL;
          GOTO      KOPRM100
.
.         valid entry
.
KOPRM800  IF        FORM1 = ZERO
            CALL      PUTSCR00                * redisplay screen
          ENDIF
          DISPLAY   *PECOL:EVERT,*V2LON,OPRMROOM
          GOTO      KOPRM999 
.
.         exitchar entered
.
KOPRM820  MOVE      TWO,EXIT                * EXITCHAR entered
          MOVE      SP6,OPRMROOM            * clear code
          MOVE      SP30,OPRMDESC            * clear desc
          IF        FORM1 = ZERO
            CALL      FRESCR00                * free the screen
          ENDIF
.
KOPRM999  RETURN
+
