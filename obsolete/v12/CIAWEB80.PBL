.----------------------------------------------------------------------
. Program       : CIAWEB80
.
. Function      : CIAS XML Remote Scripting Code Validation Service
.
. Modifications : 
.------------------------------------------------------------------------------
. V10.08.01 23/08/2016  Peter McMullen  0823848
.           Add lower case login search to XMLRED00
. V10.03.01 16/07/2013  Jill Parkinson CAR 287923
.           Added include of PATCONFD
. V9.04.04  17/11/2005 Ebon Clements CAR 71910
.           Added CHKGEN00 - Check if extract files are generated
. V9.04.03  16/11/2005 Ebon Clements CAR 71242
.           Changed CHKFIL00 to also check if the file exists in the dpath
. V9.04.02  20/09/2005 Ebon Clements CAR 76986
.           Changed to use XMLRED00 
. V9.04.01  11/01/2005 Lina Vo
.           Fixed display of error message in VALEPS00.
. V9.03.03  15/03/2004 Ebon Clements                  CAR 45489
.           Added report 6 and 7                    
. V9.03.02  12/03/2004 Ebon Clements                  CAR 45488
.           Added report 5                          
.           26/02/2004 Sylvek Litewka                 CAR 46508
.           Recompiled for CCSEPSFD changes.
. V9.03.01  12/01/2004 Ebon Clements                  CAR 45487
.           Added report 4 - Casemix type and code validation
.           Added report 3 - Casemix type validation
. V9.03.00  22/12/2003 Ebon Clements                  CAR 45473
.           Created Program
.----------------------------------------------------------------------
          INC       IBAWEBDF    
.
          INC       PATCONFD/INC
          INC       CCSEPSFD/INC
          INC       CCSSTYFD/INC
          INC       CCSXHDFD/INC
          INC       CCSCEAFD/INC
          INC       CCSCEBFD/INC
          INC       CCSCECFD/INC
          INC       CCSCEDFD/INC
          INC       CCSCEEFD/INC
          INC       CCSCEFFD/INC
          INC       CCSCEGFD/INC
          INC       CCSPPYFD/INC
.
LOADFILE  FILE      ASCII, FIXED=350
DOWNFILE  FILE      ASCII, FIXED=200
VALDCODE  DIM       70
VALDTYPE  DIM       70
CSVEXT    INIT      ".csv"
EXTRCDIR  INIT      "extracts/"
EXTPATH   DIM       127
.
.------------------------------------------------------------
PRGID     INIT      "CIAWEB80"
VERSION   INIT      "V12.00.00"
PRGNAM    INIT      "CIAS XML Remote Code Validation Service"
.------------------------------------------------------------
.
          CALL      WEBINT00                * Initialise Fifo Etc.
          CALL      INIT0000                * Open Files
.
MAIN1000  CALL      XMLRED00       * Read Fifo WEBPID and USERID
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200,MAIN1300,MAIN1400:
                             MAIN1500,MAIN1600,MAIN1700,MAIN1800
.
          PACK      WEBTITLE,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          MOVE      "INVALID OPTION",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      VALEPS00           * Validate Episode
          GOTO      MAIN1000
.
MAIN1200  CALL      VALASC00           * Validate Ascii File Name
          GOTO      MAIN1000
.
MAIN1300  CALL      VALCTY00           * Validate Casemix Type     
          GOTO      MAIN1000
.
MAIN1400  CALL      VALCTC00           * Validate Casemix Type and Code    
          GOTO      MAIN1000
.
MAIN1500  CALL      CHKFIL00           * Check if download file is there
          GOTO      MAIN1000
.
MAIN1600  CALL      VALEXT00           * Validate Extract Id
          GOTO      MAIN1000
.
MAIN1700  CALL      VALPOW00           * Validate PowerPlay Extract Id
          GOTO      MAIN1000
.
MAIN1800  CALL      CHKGEN00           * Check extract file generation status
          GOTO      MAIN1000
.
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  OPEN      CONTROLF,"controlf"
.
          OPEN      CCSEPSA1,"ccsepsaf"
          OPEN      CCSSTYA1,"ccsstyaf"
          OPEN      CCSXHDA1,"ccsxhdaf"
          OPEN      CCSCEAA1,"ccsceaaf"
          OPEN      CCSPPYA1,"ccsppyaf"
.
          READ      CONTROLF,NINTY1;*179,CWEBROOT   * HTML Root directory
          READ      CONTROLF,HUND04;RSCNEXT  * Ouput Extract directory
          STRIP     CWEBROOT
          STRIP     RSCNEXT
.
INIT9999  RETURN
.
. Note Following Labels Not Used in Service
.
PROFLD00  RETURN 
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      SP70,VALDCODE
          MOVE      SP70,VALDTYPE
.
CLRPAR99  RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "valdcode=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VALDCODE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "valdtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,VALDTYPE
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.
.------------------------------------------------------------
. Validate CIAS Episode
.------------------------------------------------------------
VALEPS00  CALL      XMLHED00
          BRANCH    EXIT,VALEPS99
.
          MOVE     "1 ",KEY2                 * Default Hospital
.
          PACK      KEY18,KEY2,VALDCODE,SP70
          CALL      RDCCEP1
          BRANCH    OVRCD,VALEPS90
.
VALEPS80  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             "OK":
                             "</RETURN_VALUE>"
          GOTO      VALEPS98
.
VALEPS90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Invalid Episode - ",KEY18:
                             "</RETURN_VALUE>"
          GOTO      VALEPS98
.
VALEPS98  CALL      XMLEND00
VALEPS99  RETURN
.
.------------------------------------------------------------
. Validate ASCII File Name
.------------------------------------------------------------
VALASC00  CALL      XMLHED00
          BRANCH    EXIT,VALASC99
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      LOADFILE,VALDCODE
          TRAPCLR   IO
          BRANCH    OVRCD,VALASC90
.
VALASC80  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             "OK":
                             "</RETURN_VALUE>"
          GOTO      VALASC98
.
VALASC90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Invalid ASCII File Name - ",VALDCODE:
                             "</RETURN_VALUE>"
          GOTO      VALASC98
.
VALASC98  CALL      XMLEND00
VALASC99  RETURN
.
.
.------------------------------------------------------------
. Validate Casemix Type
.------------------------------------------------------------
VALCTY00  CALL      XMLHED00
          BRANCH    EXIT,VALCTY99
.
          PACK      KEY3,VALDCODE,SP70
          CALL      RDCCST1
          BRANCH    OVRCD,VALCTY90
.
VALCTY80  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             CCSTDES:
                             "</RETURN_VALUE>"
          GOTO      VALCTY98
.
VALCTY90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Invalid Casemix Type ",VALDCODE:
                             "</RETURN_VALUE>"
          GOTO      VALCTY98
.
VALCTY98  CALL      XMLEND00
VALCTY99  RETURN
.
.------------------------------------------------------------
. Validate Casemix Type and Code
.------------------------------------------------------------
VALCTC00  CALL      XMLHED00
          BRANCH    EXIT,VALCTC99
.
          MOVE      VALDTYPE,CCXHSTY
.
          PACK      KEY13,CCXHSTY,VALDCODE,SP70
          CALL      RDCCXH1
          BRANCH    OVRCD,VALCTC90
.
VALCTC80  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             CCXHDES:
                             "</RETURN_VALUE>"
          GOTO      VALCTC98
.
VALCTC90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Invalid Casemix Type/Code ",VALDCODE:
                             "</RETURN_VALUE>"
          GOTO      VALCTC98
.
VALCTC98  CALL      XMLEND00
VALCTC99  RETURN
.
.------------------------------------------------------------
. Check if a extract download file exists
.------------------------------------------------------------
CHKFIL00  CALL      XMLHED00
          BRANCH    EXIT,CHKFIL99
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      DOWNFILE,VALDCODE            * Check in dpath
          TRAPCLR   IO
.
          COMPARE   ZERO,OVRCD      
          GOTO      CHKFIL90 IF EQUAL
.
          STRIP     VALDCODE
          ENDSET    VALDCODE
          APPEND    CSVEXT,VALDCODE
          RESET     VALDCODE
.
          PACK      EXTPATH,SP70,SP70
          PACK      EXTPATH,CWEBROOT,EXTRCDIR,VALDCODE,SP70
          SQUEEZE   EXTPATH
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      DOWNFILE,EXTPATH             * Check extract dir
          TRAPCLR   IO
.
          COMPARE   ZERO,OVRCD      
          GOTO      CHKFIL90 IF EQUAL
.
CHKFIL80  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             "OK":
                             "</RETURN_VALUE>"
          GOTO      CHKFIL98
.
CHKFIL90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Download file already exists - ",VALDCODE:
                             "</RETURN_VALUE>"
          GOTO      CHKFIL98
.
CHKFIL98  CALL      XMLEND00
CHKFIL99  RETURN
.
.------------------------------------------------------------
. Validate Extract Id
.------------------------------------------------------------
VALEXT00  CALL      XMLHED00
          BRANCH    EXIT,VALEXT99
.
          PACK      KEY4,VALDCODE,SP70
          CALL      RDCCEA1
          BRANCH    OVRCD,VALEXT90
.
          PACK      KEY3,CCEALV1,SP70
          CALL      RDCCST1
          IF        OVRCD=1
            MOVE     SP70,CCSTDES
          ENDIF
.
VALEXT80  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             CCEADES,"|",CCSTDES:
                             "</RETURN_VALUE>"
          GOTO      VALEXT98
.
VALEXT90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Invalid Casemix Extract Id - ",VALDCODE:
                             "</RETURN_VALUE>"
          GOTO      VALEXT98
.
VALEXT98  CALL      XMLEND00
VALEXT99  RETURN
.
.------------------------------------------------------------
. Validate PowerPlay Extract Id
.------------------------------------------------------------
VALPOW00  CALL      XMLHED00
          BRANCH    EXIT,VALPOW99
.
          PACK      KEY8,VALDCODE,SP70
          CALL      RDCCPP1
          BRANCH    OVRCD,VALPOW90

VALPOW80  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "PowerPlay Extract Id Exists - ",VALDCODE:
                             "</RETURN_VALUE>"
          GOTO      VALPOW98
.
VALPOW90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             "</RETURN_VALUE>"
          GOTO      VALPOW98
.
.
VALPOW98  CALL      XMLEND00
VALPOW99  RETURN
.
.------------------------------------------------------------
. Check to see if the extract files have been generated.
.------------------------------------------------------------
CHKGEN00  CALL      XMLHED00
          BRANCH    EXIT,CHKGEN99
.
          PACK      KEY4,VALDCODE,SP70
          CALL      RDCCEA1
          BRANCH    OVRCD,CHKGEN91
.
          MATCH     "1",CCEASTAT                      * Not Processed
          GOTO      CHKGEN90 IF EQUAL
.
          MATCH     "2",CCEASTAT                      * Process Error
          GOTO      CHKGEN92 IF EQUAL
.
          CALL      CCSCOPOP                          * Open Casemix Files
          BRANCH    EXIT,CHKGEN90
.
          CALL      CCSCOPCL                          * Close Casemix Files
.
CHKGEN80  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             "0":                     * Generation complete
                             "</RETURN_VALUE>"
          GOTO      CHKGEN98
.
CHKGEN90  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             "1":                     * Generation in progress
                             "</RETURN_VALUE>"
          GOTO      CHKGEN98
.
CHKGEN91  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=ERROR>":
                             "Invalid Casemix Extract Id - ",VALDCODE:
                             "</RETURN_VALUE>"
          GOTO      CHKGEN98
.
CHKGEN92  WRITE     HTMLFILE;"<RETURN_VALUE TYPE=SIMPLE>":
                             "2":                     * Generation error
                             "</RETURN_VALUE>"
          GOTO      CHKGEN98
.
CHKGEN98  CALL      XMLEND00
.
CHKGEN99  RETURN
.
          INC       IBAWEBCD
          INC       CCSCOPCD
.
          INC       CCSEPSIO/INC
          INC       CCSSTYIO/INC
          INC       CCSXHDIO/INC
          INC       CCSCEAIO/INC
          INC       CCSCEBIO/INC
          INC       CCSCECIO/INC
          INC       CCSCEDIO/INC
          INC       CCSCEEIO/INC
          INC       CCSCEFIO/INC
          INC       CCSCEGIO/INC
          INC       CCSPPYIO/INC
