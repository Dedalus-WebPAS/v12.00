.-------------------------------------------------------
. VF.01.00 03.05.2000 Charles Handaya
.-------------------------------------------------------
. File            : APS30CD.PBL
. Function        : Include to Print Recipient Created Tax Invoice
. Parameters      : LINEUPYN = 0 - No
.                              1 - Yes
.-------------------------------------------------------
PRCTI000  MOVE      ZERO,CPAGENO
          CALL      FNACD000
          MOVE      ZERO,GTOTAMT1
          MOVE      ZERO,GTOTAMT2
          MOVE      ZERO,GTOTAMT3
.
          UNPACK    APIMIDT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
.
          CALL      PHEAD000
.
. Print Details
.
          BRANCH    LINEUPYN,PRCTI700
.
          PACK      KEY51,TEMABCH,TEMACRD,TEMAREF,TEMADOC,SP70
          CALL      RSAPIN1
PRCTI200  CALL      RKAPIN1
          BRANCH    OVRCD,PRCTI800
.
          MATCH     TEMABCH,APINBCH
          GOTO      PRCTI800 IF NOT EQUAL
.
          MATCH     TEMACRD,APINCRD
          GOTO      PRCTI800 IF NOT EQUAL
.
          MATCH     TEMAREF,APINREF
          GOTO      PRCTI800 IF NOT EQUAL
.
          MATCH     TEMADOC,APINDOC
          GOTO      PRCTI800 IF NOT EQUAL
.
          COMPARE   "17",CLNO
          CALL      PCFWD000 IF NOT LESS
.
          PACK      DIM19,APINDES
          UNPACK    APINORD,DIM1,DIM7
          MOVE      APINQTY,F7
PRCTI300  PRINT     DIM7,*9,F7,*17,DIM19;
.
          MOVE      FTOT9P2,TOTOUT14
          FEDIT     APINAMT,TOTOUT14
          PRINT     *37,TOTOUT14;
          ADD       APINAMT,GTOTAMT1
.
          MOVE      FTOT9P2,TOTOUT14
          FEDIT     APINGST,TOTOUT14
          PRINT     *52,TOTOUT14;
          ADD       APINGST,GTOTAMT2
.
          IF        LINEUPYN=0
            ASSIGN    (APINAMT+APINGST),TOTALAMT
          ENDIF
          MOVE      FTOT9P2,TOTOUT14
          FEDIT     TOTALAMT,TOTOUT14
          PRINT     *67,TOTOUT14
          ADD       TOTALAMT,GTOTAMT3
.
          ADD       ONE,CLNO
          BRANCH    LINEUPYN,PRCTI800
          GOTO      PRCTI200
.
PRCTI700  PACK      DIM7,X10,X10
          MOVE      "9999999",F7
          PACK      DIM19,X10,X10
          MOVE      "999999999.99",APINAMT
          MOVE      "999999999.99",APINGST
          MOVE      "999999999.99",TOTALAMT
          GOTO      PRCTI300
.
PRCTI800  IF        LINEUPYN=1
            MOVE      ONE,CLNO
          ENDIF
PRCTI850  COMPARE   "17",CLNO
          GOTO      PRCTI900 IF EQUAL
          GOTO      PRCTI900 IF NOT LESS
          PRINT     " "
          ADD       ONE,CLNO
          GOTO      PRCTI850
.
PRCTI900  IF        LINEUPYN=1
            MOVE      "999999999.99",GTOTAMT1
            MOVE      "999999999.99",GTOTAMT2
            MOVE      "999999999.99",GTOTAMT3
          ENDIF
          PRINT     *N,*N,*29,"Total";
          MOVE      FTOT9P2,TOTOUT14
          FEDIT     GTOTAMT1,TOTOUT14
          PRINT     *37,TOTOUT14;
.
          MOVE      FTOT9P2,TOTOUT14
          FEDIT     GTOTAMT2,TOTOUT14
          PRINT     *52,TOTOUT14;
.
          MOVE      FTOT9P2,TOTOUT14
          FEDIT     GTOTAMT3,TOTOUT14
          PRINT     *67,TOTOUT14,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N
.
PRCTI999  RETURN
.----------------------------------------------------------------------
. Print Header
.----------------------------------------------------------------------
PHEAD000  BRANCH    LINEUPYN,PHEAD800
.
          ADD       ONE,CPAGENO
          MOVE      ZERO,CLNO
.
PHEAD100  PRINT     *N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N:
                    "Recipient Created Tax Invoice",*N:
                    *N,*N,*N,*N,*N,*N,*N:
                    *10,NADLINE1,*N:
                    *10,NADLINE2,*N:
                    *10,NADLINE3,*N:
                    *10,NADLINE4,*N:
                    *10,NADLINE5,*N:
                    *N,"Department Payment":
                    *50,"Supplier ABN  : ",*66,APMAABN,*N:
                    "Contact   : ",*22,APMACON,*N:
                    "Telephone : ",*22,APMACTEL:
                    *50,"Recipient ABN : ",*66,IBCNABNN,*N:
                    *N,*N:
                    *4,"Date",*30,"Document No.":
                    *46,"Vendor No.",*61,"Batch No.",*N:
                    CPCDATE,*30,APIMREF,*46,APIMCRD,*61,APIMBCH:
                    *N,*N,*N:
                    "Activity",*13,"Qty",*17,"Description":
                    *45,"Amount",*63,"GST",*76,"Total"
.
          GOTO      PHEAD999
.
PHEAD800  MOVE      "999",CPAGENO
          MOVE      "99/99/9999",CPCDATE
          MOVE      "99999999999",APMAABN
          PACK      APMACON,X10,X10,X10,X10
          PACK      APMACTEL,X10,X10,X10
          MOVE      "99999999999",IBCNABNN
          PACK      APIMREF,X10,X10,X10
          PACK      APIMCRD,X10,X10
          PACK      APIMBCH,X10,X10
.
          GOTO      PHEAD100
.
PHEAD999  RETURN
.----------------------------------------------------------------------
. Print Carried Forward
.----------------------------------------------------------------------
PCFWD000  PRINT     *N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N
          CALL      PHEAD000
.
PCFWD999  RETURN
.----------------------------------------------------------------------
. Format Name & Address Lines
. Returns Creditor Name & Address formatted into 5 Lines
. NADLINE1, NADLINE3, NADLINE3, NADLINE4, NADLINE5
.----------------------------------------------------------------------
FNACD000  BRANCH    LINEUPYN,FNACD900
.
          MOVE      SP70,NADLINE1
          MOVE      SP70,NADLINE2
          MOVE      SP70,NADLINE3
          MOVE      SP70,NADLINE4
          MOVE      SP70,NADLINE5
.
          MOVE      APMACN1,NADLINE1
          MATCH     SP70,APMACN2
          GOTO      FNACD100 IF EQUAL
          MOVE      APMACN2,NADLINE2
.
          MATCH     SP70,APMACA1
          GOTO      FNACD200 IF EQUAL
          MOVE      APMACA1,NADLINE3
.
          MATCH     SP70,APMACA2
          GOTO      FNACD300 IF EQUAL
          MOVE      APMACA2,NADLINE4
.
          MATCH     SP70,APMACA3
          GOTO      FNACD800 IF EQUAL
          MOVE      APMACA3,NADLINE5
.
          GOTO      FNACD800
.
. Blank Name Line 2
.
FNACD100  MOVE      APMACA1,NADLINE2
          MOVE      APMACA2,NADLINE3
          MOVE      APMACA3,NADLINE4
          GOTO      FNACD800
.
. Blank Address Line 1
.
FNACD200  MOVE      APMACA2,NADLINE3
          MOVE      APMACA3,NADLINE4
          GOTO      FNACD800
.
. Blank Address Line 2
.
FNACD300  MOVE      APMACA3,NADLINE4
          GOTO      FNACD800
.
FNACD800  MATCH     SP8,APMACPC
          GOTO      FNACD999 IF EQUAL
.
          MATCH     SP70,NADLINE5
          IF        @EQUAL
            MATCH     SP70,NADLINE4
            IF        @EQUAL
              STRIP   NADLINE3
              ENDSET  NADLINE3
              APPEND  "  ",NADLINE3
              APPEND  APMACPC,NADLINE3
              APPEND  SP70,NADLINE3
              RESET   NADLINE3
            ELSE
              STRIP   NADLINE4
              ENDSET  NADLINE4
              APPEND  "  ",NADLINE4
              APPEND  APMACPC,NADLINE4
              APPEND  SP70,NADLINE4
              RESET   NADLINE4
            ENDIF
          ELSE
            STRIP   NADLINE5
            ENDSET  NADLINE5
            APPEND  "  ",NADLINE5
            APPEND  APMACPC,NADLINE5
            APPEND  SP70,NADLINE5
            RESET   NADLINE5
          ENDIF
.
          GOTO      FNACD999
.
FNACD900  PACK      NADLINE1,X10,X10,X10,X10
          PACK      NADLINE2,X10,X10,X10,X10
          PACK      NADLINE3,X10,X10,X10,X10
          PACK      NADLINE4,X10,X10,X10,X10
          PACK      NADLINE5,X10,X10,X10,X10
.
FNACD999  RETURN
.
