. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS51                                             *
. * Desc      :   Financial Reporting                                  *
. **********************************************************************
. * Date      :   15.02.1991                                           *
. * Author    :   Neeriem Dye                                          *
. * Mods      :                                                        *
. **********************************************************************
. * V9.03.01  05.11.2003  Mike Laming  CAR 37633                       *
. *           Recompile for PRCFMSPR - Change Projection FAMT6000      *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
. * VF.09.B01 30.10.2000 Charles Handaya                               *
. *           Added Column Type : 7-Statistical Units                  *
. *           srf 6934                                                 *
. **********************************************************************
. * VF.01.01  23.10.2000 Sandra Barcham                                *
. *           When you choose use subjective also do for stat posting  *
. *           srf 1852                                                 *
. **********************************************************************
. * VF.00.03  11.01.2000 Charles Handaya                               *
. *           Recompiled for Audit File date                           *
. * VF.00.02  23.12.1999 Sandra Barcham                                *
. *           Recompiled for FMSCCAFD                                  *
. *           srf 646145                                               *
. * VF.00.01  02.11.1999 Sandra Barcham                                *
. *           Change ITEMNO to XITEMNO                                 *
. **********************************************************************
.$$$$$$
.        PROGRAM IBAFMS51
.             open files and initialise variables
.             WHILE user wants to add/modify/delete DO
.                  get desired option (add, modify or delete)
.                  CASE option OF
.                       ADD     : WHILE user wants to add records DO
.                                      accept initial values
.                                      WHILE user wants modify screen data DO
.                                           get number of item to be modified
.                                           allow user to modify option
.                                      END WHILE
.                                      IF user wants to post data
.                                           THEN write new data
.                                      END IF
.                                 END WHILE
.                       MODIFY  : WHILE user wants to modify records DO
.                                      get and display desired record
.                                      WHILE user wants modify screen data DO
.                                           get number of item to be modified
.                                           allow user to modify option
.                                      END WHILE
.                                      IF user wants to post data
.                                           THEN update data
.                                      END IF
.                                 END WHILE
.                       DELETE  : WHILE user wants to delete records DO
.                                      get and display desired record
.                                      IF user wants to delete data
.                                           THEN delete data
.                                      END IF
.                                 END WHILE
.                  END CASE
.             END WHILE
.        END
.$$$$$$
. Standard FMS Accounting System Include
.---------------------------------------
          INC       FMSSTDDF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
          INC       FMSAMAFD/INC
          INC       FMSBTYFD/INC
          INC       FMSCSAFD/INC
          INC       FMSGURFD/INC
          INC       FMSLEVFD/INC
          INC       FMSLMAFD/INC
          INC       FMSLMBFD/INC
          INC       FMSLMCFD/INC
          INC       FMSRDFFD/INC
          INC       FMSUDRFD/INC
          INC       IBAESCFD/INC
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
ABUDDESC  DIM       35
ADESC     DIM       15
ADESC0    INIT      "Normal A/c Name"
ADESC1    INIT      "Use Cost Centre"
ADESC2    INIT      "Use Subjective "
ACCEPT    FORM      1        * accept mode (1=on)
BUDDESC   DIM       35
DIM52     DIM       52
DISPFLAG  FORM      1
FILENAME  DIM       8
MAXPERS   FORM      2
OPTIONB   FORM      2
PRTWARN   FORM      1
SP80      INIT      "                                        ":
                    "                                        "
SAVCOSTC  DIM       12
SAVCHEAD  DIM       12
.
KEYPERD   FORM      2
FYEAR     DIM       4
PERDFROM  FORM      2
PERDTO    FORM      2
BANKNAME  DIM       35
CREDNAME  DIM       35
DEBTNAME  DIM       35
DISCNAME  DIM       35
PAYMNAME  DIM       35
AGSTNAME  DIM       35
CGSTNAME  DIM       35
.
SUPPTYPE  DIM       20
SUPPTYP0  INIT      "None"
SUPPTYP1  INIT      "Posting"
SUPPTYP2  INIT      "All"
SUPPTYP3  INIT      "Ign. Sub Ac Suppress"
.
PRGID     INIT      "IBAFMS51"
PRGNAM    INIT      "Financial Reporting (General Users)"
VERSION   INIT      "V12.00.00"
.
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML0100    CALL      KREP0000           * select report
          BRANCH    EXIT,ML9999        * exit program ?
.
          CALL      SELB0000           * print report
          GOTO      ML0100
.
ML9999    CHAIN     PGM                * chain back to menu
.
.******************************************************************************
.* INIT - Open Files                             Called by ML0000             *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening fmsrdfaf";  * Reporting file
          OPEN      FMSRDFA1,"fmsrdfaf"
.
          DISPLAY   *P64:24,"fmscsaaf";  * Account file
          OPEN      FMSCSAA1,"fmscsaaf"
.
          DISPLAY   *P64:24,"fmsamaaf";  * Account file
          OPEN      FMSAMAA1,"fmsamaaf"
.
          DISPLAY   *P64:24,"fmslmaaf";  * ledger
          OPEN      FMSLMAA1,"fmslmaaf"
.
          DISPLAY   *P64:24,"fmslmcaf";  * Report Sequences
          OPEN      FMSLMCA1,"fmslmcaf"
.
          DISPLAY   *P64:24,"fmslmbaf";  * Report Sequences
          OPEN      FMSLMBA1,"fmslmbaf"
.
          DISPLAY   *P64:24,"fmslevaf";  * Report level
          OPEN      FMSLEVA1,"fmslevaf"
.
          DISPLAY   *P64:24,"fmsudraf";  * Report Sequences
          OPEN      FMSUDRA1,"fmsudraf"
.
          DISPLAY   *P64:24,"fmsguraf";  * user report allocation
          OPEN      FMSGURA1,"fmsguraf"
.
          DISPLAY   *P64:24,"fmsbtyaf";  * budget file
          OPEN      FMSBTYA1,"fmsbtyaf"
.
          DISPLAY   *P64:24,"controlf";  * Budget Types
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"ibaescaf";  * Printer Types
          OPEN      IBAESCA1,"ibaescaf"
          OPEN      IBAESCA2,"ibaescaf"
.
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.
.******************************************************************************
.* SCRB - Display Screen B                       Called by SELB, redisps      *
.******************************************************************************
SCRB0000  DISPLAY   *P1:3,*EF;
          MATCH     SP70,FMGUREP
          GOTO      SCRB1000 IF EQUAL  * only display report if relevent
          DISPLAY   *P1:4,"    Report               : ":
                          *V2LON,FMRDCOD,*HOFF,SP2,FMRDDES;
.
SCRB1000  DISPLAY   *P1:6,*V2LON," 1",*HOFF,". Financial Year       : ":
                    *P1:7,*V2LON," 2",*HOFF,". From Period          : ":
                    *P1:8,*V2LON," 3",*HOFF,". To   Period          : ";
.
          BRANCH   ACCEPT,SCRB9999     * in accept mode
.
          DISPLAY   *P28:7,*V2LON,PERDFROM:
                    *P28:8,*V2LON,PERDTO;
.
SCRB9999  RETURN
.******************************************************************************
.* SELB - Perform Screen B                       Called by ML                 *
.******************************************************************************
SELB0000  PACK      KEY2,FMRDLED,SP70
          CALL      RDFMLA1            * get ledger data
          MOVE      "12",MAXPERS
          ADD       FMLAPERS,MAXPERS   * max no. of periods
.
          BRANCH    FMGUSTA,SELB0050   * if status = 0 & type = 1, do report
          BRANCH    FMGUTYP,SELB9000
.
SELB0050  MOVE      ONE,ACCEPT         * in accept mode
          MOVE      ZERO,OPTIONB       * reset item desired
          CALL      SCRB0000           * display screen B
.
SELB0100  ADD       ONE,OPTIONB        * get next item
          COMPARE   ONE,ACCEPT
          CALL      REPTQST IF NOT EQUAL   * get option if not in accept mode
.
          MOVE      "28",CCOL          * get screen address
          MOVE      "5",CVERT
          ADD       OPTIONB,CVERT
          MOVE      OPTIONB,F2          * adjust selection for quit/continue
          ADD       TWO,F2
          BRANCH    F2,SELB9999,SELB9000:              * quit, continue
                       SELB1100,SELB1200,SELB1300      * execute option
          BRANCH    ACCEPT,SELB0500    * at end of accept mode ?
          BEEP                         * illegal option
          GOTO      SELB0100           * get another option
.
SELB0500  MOVE      ZERO,ACCEPT        * clear accept mode
          GOTO      SELB0100
.
.---- option 1 ----
.
SELB1100  CALL      KYRR0000           * get year 
          BRANCH    EXIT,SELB9999
          BRANCH    ACCEPT,SELB0100    * in accept mode
.
          LOAD      PEREDAT,PERDFROM,FMLC01TO,FMLC02TO,FMLC03TO,FMLC04TO:
                            FMLC05TO,FMLC06TO,FMLC07TO,FMLC08TO,FMLC09TO:
                            FMLC10TO,FMLC11TO,FMLC12TO,FMLC13TO
          LOAD      PERDESC,PERDFROM,FMLC01DE,FMLC02DE,FMLC03DE,FMLC04DE:
                            FMLC05DE,FMLC06DE,FMLC07DE,FMLC08DE,FMLC09DE:
                            FMLC10DE,FMLC11DE,FMLC12DE,FMLC13DE
          UNPACK    PEREDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P28:7,*V2LON,PERDFROM,*HOFF,SP3,PERDESC," Ending ",CPCDATE
.
          LOAD      PEREDAT,PERDTO,FMLC01TO,FMLC02TO,FMLC03TO,FMLC04TO:
                            FMLC05TO,FMLC06TO,FMLC07TO,FMLC08TO,FMLC09TO:
                            FMLC10TO,FMLC11TO,FMLC12TO,FMLC13TO
          LOAD      PERDESC,PERDTO,FMLC01DE,FMLC02DE,FMLC03DE,FMLC04DE:
                            FMLC05DE,FMLC06DE,FMLC07DE,FMLC08DE,FMLC09DE:
                            FMLC10DE,FMLC11DE,FMLC12DE,FMLC13DE
          UNPACK    PEREDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P28:8,*V2LON,PERDTO,*HOFF,SP3,PERDESC," Ending ",CPCDATE
          GOTO      SELB0100
.
.---- option 2 ----
.
SELB1200  CALL      KPER0000           * get period
          BRANCH    EXIT,SELB1290
          MOVE      KEYPERD,PERDFROM
          GOTO      SELB0100
.
SELB1290  COMPARE   ONE,ACCEPT
          GOTO      SELB1200 IF NOT EQUAL
          SUB       TWO,OPTIONB        * go up one option
          GOTO      SELB0100
.
.---- option 3 ----
.
SELB1300  CALL      KPER0000           * get period
          BRANCH    EXIT,SELB1390
          MOVE      KEYPERD,PERDTO
          GOTO      SELB0100
.
SELB1390  COMPARE   ONE,ACCEPT
          GOTO      SELB1300 IF NOT EQUAL
          SUB       TWO,OPTIONB        * go up one option
          GOTO      SELB0100
.
.
SELB9000  CALL      RUNR0000           * run report
          BRANCH    EXIT,SELB0100      * report failed to run ?
          GOTO      SELB9999
.
SELB9999  RETURN
+
.**********************************************************************
.  RUNR - Run report                                   Called By SELB
.**********************************************************************
RUNR0000  COMPARE   PERDFROM,PERDTO
          GOTO      RUNR0100 IF NOT LESS
.
          DISPLAY   *P1:24,*B,*EF:
                    "From Period Cannot Be After To Period - ";
          CALL      HITENTER
          GOTO      RUNR9500
.
RUNR0100  LOAD      F1,PERDTO,FMLC01IN,FMLC02IN,FMLC03IN,FMLC04IN,FMLC05IN:
                               FMLC06IN,FMLC07IN,FMLC08IN,FMLC09IN,FMLC10IN:
                               FMLC11IN,FMLC12IN,FMLC13IN
          MOVE      ONE,PRTWARN        * clear print warning flag
.
          BRANCH    F1,RUNR0500        * period locked ?
          BRANCH    FMGUSTA,RUNR0400   * status ?
          BRANCH    FMGUTYP,RUNR0500   * internal FMS or external UNIX/SQL ?
          DISPLAY   *P1:23,*B,*EL:
                              "WARNING - Accounting period has not been locked":
                   *P1:24,*EL,"          This will be a interim report only - ";
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
          MOVE      ZERO,PRTWARN       * set print warning flag
          GOTO      RUNR0500
.
RUNR0400  DISPLAY   *P1:23,*B,*EL:
                              "ERROR - Accounting period has not been locked":
                   *P1:24,*EL,"        Report cannot be executed - ";
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
          GOTO      RUNR9500
.
RUNR0500  BRANCH    FMGUTYP,RUNR5000   * internal FMS or external UNIX/SQL ?
.
.---- internal FMS report ----
.
          DISPLAY   *P1:18,*EF
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:21,*EL,"    Start : ",*V2LON,CTIMEIS
.
          MOVE      FMRDREP,KEY2
          CALL      RDFMLB1
.
          MOVE      FMRDRS,KEY3
          CALL      RDFMUD1
.
          MATCH     SP70,FMRDPT
          GOTO      RUNR1000 IF EQUAL
          PACK      KEY5,FMRDPT,SP3
          CALL      RDIBES1
          COMPARE   ZERO,OVRCD
          GOTO      RUNR1100 IF EQUAL
.
RUNR1000  MOVE      SP70,IBESSEQ
.
RUNR1100  PACK      KEY3,FMRDLEV
          MATCH     "000",FMRDLEV
          GOTO      RUNR1200 IF EQUAL
          CALL      RDFMLV1
          COMPARE   ZERO,OVRCD
          GOTO      RUNR1300 IF EQUAL
.
RUNR1200  MOVE      SP70,FMLVDESC
.
RUNR1300  MOVE      ADESC0,ADESC
          LOAD      ADESC,FMRDDOP,ADESC1,ADESC2
.
          MOVE      FMRDCBT,KEY4
          CALL      RDFMBT1
          MOVE      FMBTDESC,BUDDESC
.
          MOVE      FMRDPBT,KEY4
          CALL      RDFMBT1
          MOVE      FMBTDESC,ABUDDESC
.
          PROC      FMSPR000           * run report
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:24,*EL,*P30:21,*B,"Completed : ",*V2LON,CTIMEIS," - ";
          CALL      HITENTER
          GOTO      RUNR9000
.
.---- external UNIX/SQL report ----
.
RUNR5000  DISPLAY   *P1:21;
          BRANCH    FMGUSTA,RUNR7000   * status ?
          EXECUTE   FMGUCMD,TASKID
          CALL      DISPHEAD
          GOTO      RUNR9000
.
RUNR7000  CLEAR     CMDLINE
          PACK      CMDLINE,FMGUCMD,SP1,CURYEAR,SP1,PERDFROM,SP1,PERDTO
          EXECUTE   CMDLINE,TASKID
          CALL      DISPHEAD
.
RUNR9000  MOVE      ZERO,EXIT
          GOTO      RUNR9999
.
RUNR9500  MOVE      ONE,EXIT
.
RUNR9999  RETURN
.******************************************************************************
.         Subroutine to Select Item, Save Report, Run Report or Exit   
.******************************************************************************
.
REPTQST   MOVE      ZERO,OPTIONB
          MOVE      SP10,CCITEMD
          KEYIN     *P1:24,*EL,"Select Item, (":
                    *V2LON,"R",*HOFF,")un Report, e(":
                    *V2LON,"X",*HOFF,")it : ",*V2LON,*JR,CCITEMD;
.
          RESET     CCITEMD
          GOTO      REPTQST IF EOS
.
          REP       UPPLOW,CCITEMD
.
          MATCH     "  X",CCITEMD
          GOTO      REPTQST1 IF EQUAL
.
          MATCH     "  R",CCITEMD
          GOTO      REPTQST2 IF EQUAL
.
          TYPE      CCITEMD
          GOTO      REPTQSTE IF NOT EQUAL
.
          MOVE      CCITEMD,OPTIONB
          COMPARE   ONE,OPTIONB
          GOTO      REPTQSTE IF LESS
.
          RETURN
.
REPTQSTE  BEEP
          GOTO      REPTQST
.
REPTQST1  MOVE      "-1",OPTIONB
          RETURN
.
REPTQST2  MOVE      "0",OPTIONB
          RETURN
+
.************************************************************************
.* KPER - Keyin the period to run the report                            *
.************************************************************************
KPER0000  KEYIN     *PCCOL:CVERT,*DV,*EL,UNDLN2:
                    *PCCOL:CVERT,*V2LON,FORM2,*EL;
          COMPARE   ZERO,FORM2           * exit ?
          GOTO      KPER9500 IF EQUAL
          DISPLAY   *PCCOL:CVERT,*V2LON,FORM2;
.
          COMPARE   ONE,FORM2            * cant be less than 1
          GOTO      KPER1000 IF LESS
          COMPARE   FORM2,MAXPERS        * cant be more than MAXPERS
          GOTO      KPER1000 IF LESS
.
          MOVE      FORM2,KEYPERD
          MOVE      FORM2,IDAY
          LOAD      PEREDAT,KEYPERD,FMLC01TO,FMLC02TO,FMLC03TO,FMLC04TO:
                                    FMLC05TO,FMLC06TO,FMLC07TO,FMLC08TO:
                                    FMLC09TO,FMLC10TO,FMLC11TO,FMLC12TO,FMLC13TO
          LOAD      PERDESC,KEYPERD,FMLC01DE,FMLC02DE,FMLC03DE,FMLC04DE:
                                    FMLC05DE,FMLC06DE,FMLC07DE,FMLC08DE:
                                    FMLC09DE,FMLC10DE,FMLC11DE,FMLC12DE,FMLC13DE
          UNPACK    PEREDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *PCCOL:CVERT,*V2LON,FORM2:
                    *HOFF,SP3,PERDESC," Ending ",CPCDATE;
          GOTO      KPER9000
.
KPER1000  BEEP
          GOTO      KPER0000
.
KPER9000  MOVE      ZERO,EXIT
          GOTO      KPER9999
.
KPER9500  MOVE      ONE,EXIT
.
KPER9999  RETURN
.************************************************************************
.* KYRR - Keyin the year to run the report                              *
.************************************************************************
KYRR0000  CALL      KLMCFM00             * keyin financial year
          BRANCH    EXIT,KYRR7000,KYRR9000
.
          MOVE      FMLCYEAR,CURYEAR
          LOAD      PEREDAT,MAXPERS,FMLC01TO,FMLC02TO,FMLC03TO,FMLC04TO:
                                    FMLC05TO,FMLC06TO,FMLC07TO,FMLC08TO:
                                    FMLC09TO,FMLC10TO,FMLC11TO,FMLC12TO,FMLC13TO
          UNPACK    PEREDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          DISPLAY   *PCCOL:CVERT,*V2LON,CURYEAR,*HOFF," Ending ",CPCDATE
          GOTO      KYRR8000
.
KYRR7000  BRANCH    ACCEPT,KYRR9000
          GOTO      KYRR0000
.
KYRR8000  MOVE      ZERO,EXIT
          GOTO      KYRR9999
.
KYRR9000  MOVE      ONE,EXIT
.
KYRR9999  RETURN 
.********************************************************************
.   KREP -  Keyin Report   - Returns a valid record
.********************************************************************
KREP0000  DISPLAY   *P1:4,*EF,*P1:5,*V2LON,*ULON,"Item":
                    *P8:5,"Description",*P45:5,"Report"
          MOVE      FIVE,STDROW
          MOVE      ZERO,XITEMNO
          MOVE      ZERO,CPAGENO
          PACK      KEY7,PASSCODE,SP70
          CALL      RSFMGU1
.
KREP0010  CALL      RKFMGU1
          BRANCH    OVRCD,KREP0020
          MATCH     PASSCODE,FMGUUID
          GOTO      KREP0020 IF NOT EQUAL        * at end of user's selections ?
.
          ADD       ONE,XITEMNO
          ADD       ONE,STDROW
          MOVE      SP70,FMRDDES
          PACK      KEY3,FMGUREP,SP70
          CALL      RDFMRD1
          PACK      KEY7,FMGUUID,FMGUSEQ
          STORE     KEY7,XITEMNO,ST1,ST2,ST3,ST4,ST5:
                                    ST6,ST7,ST8,ST9,ST10:
                                    ST11,ST12,ST13,ST14,ST15:
                                    ST16,ST17
          DISPLAY   *P2:STDROW,*V2LON,XITEMNO,DOT,*HOFF:
                    *P8:STDROW,FMGUDES,*P45:STDROW,FMRDDES
          COMPARE   "17",XITEMNO
          GOTO      KREP0010 IF NOT EQUAL
.
KREP0020  MOVE      ZERO,CCITEM
          MOVE      SP2,CCITEMD
          DISPLAY   *P1:24,*EL,"Select Item, ";
          COMPARE   "17",XITEMNO
          GOTO      KREP0025 IF NOT EQUAL
          DISPLAY   "(",*V2LON,"N",*HOFF,")ext page, ";
.
KREP0025  COMPARE   ZERO,CPAGENO
          GOTO      KREP0026 IF EQUAL
          DISPLAY   "(",*V2LON,"P",*HOFF,")revious page, ";
.
KREP0026  KEYIN     "e(",*V2LON,"X",*HOFF,")it ? ",*JR,*V2LON,CCITEMD;
.
          RESET     CCITEMD
          GOTO      KREP0030 IF EOS      * Next Page
          REP       UPPLOW,CCITEMD
          MATCH     "  N",CCITEMD        * Next Page
          GOTO      KREP0030 IF EQUAL
          MATCH     "  P",CCITEMD        * Previous Page
          GOTO      KREP0035 IF EQUAL
          MATCH     "  X",CCITEMD        * Exit
          GOTO      KREP0091 IF EQUAL
.
          TYPE      CCITEMD
          GOTO      KREP0090 IF NOT EQUAL  * Invalid Entry
.
          MOVE      CCITEMD,CCITEM
          COMPARE   ONE,CCITEM
          GOTO      KREP0090 IF LESS       * Invalid Entry
          COMPARE   CCITEM,XITEMNO
          GOTO      KREP0090 IF LESS       * Invalid Entry
          GOTO      KREP0092               * Item Selected
.
KREP0030  COMPARE   "17",XITEMNO            * Next Page Selected
          GOTO      KREP0090 IF NOT EQUAL  * No Next Page Invalid Selection
          ADD       ONE,CPAGENO
          MOVE      ZERO,XITEMNO            
          MOVE      FIVE,STDROW
          DISPLAY   *P1:6,*EF
          GOTO      KREP0010
.
KREP0035  COMPARE   ZERO,CPAGENO           * Previous Page Selected
          GOTO      KREP0090 IF EQUAL      * No Previous Page Invalid Selection
          DISPLAY   *P1:6,*EF
          SUB       ONE,CPAGENO
          COMPARE   "17",XITEMNO      
          GOTO      KREP0036 IF EQUAL 
          CALL      RPFMGU1            * Initial reverse read
          COMPARE   ZERO,XITEMNO      
          GOTO      KREP0037 IF EQUAL       * no data on screen ?
.
KREP0036  CALL      RPFMGU1
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      KREP0036 IF NOT EQUAL
          COMPARE   FIVE,STDROW
          GOTO      KREP0010 IF EQUAL   * Display Down The Page
KREP0037  MOVE      FIVE,STDROW
          MOVE      "17",XITEMNO
          GOTO      KREP0036
.
KREP0090  BEEP                          * invalid entry
          GOTO      KREP0020
.
KREP0091  MOVE      SP70,FMRDCOD
          MOVE      ONE,EXIT
          GOTO      KREP9999
.
KREP0092  LOAD      KEY7,CCITEM,ST1,ST2,ST3,ST4,ST5:
                                  ST6,ST7,ST8,ST9,ST10:
                                  ST11,ST12,ST13,ST14,ST15:
                                  ST16,ST17
          CALL      RDFMGU1
          PACK      KEY3,FMGUREP,SP70
          MOVE      SP70,FMRDREP
          CALL      RDFMRD1
          MOVE      ZERO,EXIT
.
KREP9999  RETURN
.
.******************************************************************************
.* Redisplays                                                                 *
.******************************************************************************
RESCIB00  
RLMCFM00  CALL      SCRB0000
          RETURN
.******************************************************************************
.* INCLUDES FOR I/O'S                                                         *
.******************************************************************************
          INC       FMSSTDCD           * FMS Acc. standard routines
          INC       PRCFMSPR           * Report Printing Procedure
.
          INC       FMSAMAIO/INC
          INC       FMSBTYIO/INC
          INC       FMSCSAIO/INC
          INC       FMSGURIO/INC
          INC       FMSLMAIO/INC
          INC       FMSLMCIO/INC
          INC       FMSLMBIO/INC
          INC       FMSLEVIO/INC
          INC       FMSUDRIO/INC
          INC       FMSRDFIO/INC       * Reporting
          INC       IBAESCIO/INC
.
          INC       FMSLMCKY
          INC       IBAESCKY
.
