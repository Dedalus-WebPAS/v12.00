.*****************************************************************************
.*    Program      : F03MHDAU                                                *
.*    Description  : Conversion mehaudda to new File layout                  *
.*                                                                           *
.*    Author       : Mike Laming                                             *
.*    Date         : 27/04/2010                                              *
.*    Modifications:                                                         *
.*        V10.03.01 05/12/2011  Mike Laming   CAR 242151                     *
.*                  Mods required after Audit File fix using F02MHDAU        *
.*        V10.03.00 24/11/2011  Mike Laming   CAR 242151                     *
.*                  Created - 4 fields from Spare, lengthen record to 500    *
.*****************************************************************************
.
.  Global change F03MHDAU  eg F57PTDOC
.  Global change MEHDLSFD  eg PATDOCFD (new layout)
.  Global change mehaudda  eg patdoctf
.  Global change MEHAUDDA  eg PATDOCT1
.  Global change KEY32   eg KEY8
.  Global change KEY32  eg KEY9
.  Global change AWMHDL  eg WRDOCT1
.  Global change smhaudda  eg sptdoctf
.
.  vi Global change command  :%s/FromString/ToString/g
.
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
.
. Enter the first or the shortest index from the original FD and rename
. the index OLDFILE1
.
.                                     ******** copy the old FD here *******
.                                     * comment out all continuing fields and
.                                     * prefix changing fields with "OLD" -
.                                     * remove old Indexes and Redefines     
.
..OLDFILE1  IFILE     SQL, FIXED=xxx, KEY="Uxx-xx"
OLDFILE1   IFILE    SQL, FIXED=487, KEY="D28-35,36-43,1-8,9-16"
.OLDFILE2   IFILE    SQL, FIXED=487, KEY="D1-8,9-16,28-35,36-43"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.MHDLMDAT  DIM       8      8        1     Date of Change
.MHDLMTIM  DIM       8      8        9     Time of Change
.MHDLRTYP  DIM       1      1        17    Record Type
.                                              A = Added
.                                              B = Before image
.                                              C = After Image
.                                              D = Delete (Inactive)
.MHDLMUID  DIM      10     10        18    WEB User ID Changed (websecaf)
.MHDLURNO DIM       8      8        28    U/R Number
.MHDLUNIQ DIM       8      8        36    Unique Number
.
.Data File Definition
.--------------------
.MEHDLSA1   IFILE    SQL, FIXED=460, KEY="U1-8,9-16,17-24,25-32"
.MEHDLSA2   IFILE    SQL, FIXED=460, KEY="U79-86,1-8,9-16,17-24,25-32"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.MHDLURNO  DIM       8      8        1     U/R Number
.MHDLUNIQ  DIM       8      8        9     Unique Number
.MHDLSDAT  DIM       8      8        17    Start Date (ccyymmdd)
.MHDLSTIM  DIM       8      8        25    Start Time (hh:mm:ss)
.MHDLACTV  DIM       1      1        33    Status 0-Active 1-Inactive
.MHDLSCOD  DIM       3      3        34    Section Code (category LS)
.MHDLSTAT  DIM       3      3        37    Review Status (category LR)
.MHDLRDAT  DIM       8      8        40    Review Date (ccyymmdd)
.MHDLRTIM  DIM       8      8        48    Review Time (hh:mm:ss)
.MHDLHCPC  DIM       10     10       56    HCP Code
.MHDLCASE  DIM       10     10       66    Case Team
.MHDLPOCC  DIM       3      3        76    Place of Occurrence (category PO)
.MHDLREMD  DIM       8      8        79    Reminder Date (ccyymmdd)
.MHDLDRIV  DIM       1      1        87    Is the patient allowed to drive?
..                                         0-No, 1-Yes
.MHDLRESD  DIM       1      1        88    Residential Admission?
..                                         0-No, 1-Yes
.MHDLRCOD  DIM       6      6       89    Place of Resident(meheoraf.mheotype=R)
.MHDLCUID  DIM       10     10       95    WEB User ID created (websecaf)
.MHDLCDAT  DIM       8      8        105   Date Record created (ccyymmdd)
.MHDLCTIM  DIM       8      8        113   Time Record created (hh:mm:ss)
.MHDLUUID  DIM       10     10       121   WEB User ID updated (websecaf)
.MHDLUDAT  DIM       8      8        131   Date record updated
.MHDLUTIM  DIM       8      8        139   Time record update
.MHDLCMNT  DIM       100    100      147   Comments
.MHDLSENT  DIM       1      1        247   Mental Health data sent
..                                         Y-Yes, N or blank-No
.MHDLEDAT  DIM       8      8        248   End Date (ccyymmdd)
.MHDLETIM  DIM       8      8        256   End Time (hh:mm:ss)
.MHDLSULS  DIM       3      3        264   Supp. Legal Status - Cat."LS"
.MHDLSLSD  DIM       8      8        267   Supp. Legal Status Effective Date
.MHDLRPDT  DIM       8      8        275   Date Review performed
.MHDLCNSD  DIM       8      8        283   Court Notices Sent Date
.MHDLTRDT  DIM       8      8        291   Tribunal Date
.MHDLLWCD  DIM       8      8        299   Lodged with Court Date
.MHDLCTCD  DIM       3      3        307   Court Time Code - Cat."xy"
.MHDLCMN2  DIM       100    100      310   Comments 2
OLDLSPRE  DIM       50     50       410   Spare Variable
.
.End of Record                      460
.
.                                     ******** copy the new FD here *******
.
.Audit File Definition
.---------------------
MEHAUDDA   IFILE    SQL, FIXED=527, KEY="D28-35,36-43,1-8,9-16"
MEHAUDDB   IFILE    SQL, FIXED=527, KEY="D1-8,9-16,28-35,36-43"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
MHDLMDAT  DIM       8      8        1     Date of Change
MHDLMTIM  DIM       8      8        9     Time of Change
MHDLRTYP  DIM       1      1        17    Record Type
.                                              A = Added
.                                              B = Before image
.                                              C = After Image
.                                              D = Delete (Inactive)
MHDLMUID  DIM      10     10        18    WEB User ID Changed (websecaf)
.MHDLURNO DIM       8      8        28    U/R Number
.MHDLUNIQ DIM       8      8        36    Unique Number
.
.Data File Definition                     ( created by File Generator )
.--------------------
MEHDLSA1   IFILE    SQL, FIXED=500, KEY="U1-8,9-16,17-24,25-32"
MEHDLSA2   IFILE    SQL, FIXED=500, KEY="U79-86,1-8,9-16,17-24,25-32"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
MHDLURNO  DIM       8      8        1     U/R Number
MHDLUNIQ  DIM       8      8        9     Unique Number
MHDLSDAT  DIM       8      8        17    Date Event Opened (ccyymmdd)
MHDLSTIM  DIM       8      8        25    Time Event Opened (hh:mm:ss)
MHDLACTV  DIM       1      1        33    Status 0-Active 1-Inactive
MHDLSCOD  DIM       3      3        34    Section Code (category LS)
MHDLSTAT  DIM       3      3        37    Review Status (category LR)
MHDLRDAT  DIM       8      8        40    Review Date (ccyymmdd)
MHDLRTIM  DIM       8      8        48    Review Time (hh:mm:ss)
MHDLHCPC  DIM       10     10       56    Review HCP Code
MHDLCASE  DIM       10     10       66    HCP Case Team
MHDLPOCC  DIM       3      3        76    Place of Occurrence (category PO)
MHDLREMD  DIM       8      8        79    Reminder Date (ccyymmdd)
MHDLDRIV  DIM       1      1        87    Is the patient allowed to drive?
.                                         0-No, 1-Yes
MHDLRESD  DIM       1      1        88    Residental Admission?
.                                         0-No 1-Yes
MHDLRCOD  DIM       6      6        89    Resident Code (meheoraf.mheotype=R)
MHDLCUID  DIM       10     10       95    WEB User ID created (websecaf)
MHDLCDAT  DIM       8      8        105   Date Record created (ccyymmdd)
MHDLCTIM  DIM       8      8        113   Time Record created (hh:mm:ss)
MHDLUUID  DIM       10     10       121   WEB User ID updated (websecaf)
MHDLUDAT  DIM       8      8        131   Date record updated
MHDLUTIM  DIM       8      8        139   Time record update
MHDLCMNT  DIM       100    100      147   Comments
MHDLSENT  DIM       1      1        247   Mental Health data sent
.                                         Y-Yes, N or blank-No
MHDLEDAT  DIM       8      8        248   End Date (ccyymmdd)
MHDLETIM  DIM       8      8        256   End Time (hh:mm:ss)
MHDLSULS  DIM       3      3        264   Supp. Legal Status - Cat."LS"
MHDLSLSD  DIM       8      8        267   Supp. Legal Status Effective Date
MHDLRPDT  DIM       8      8        275   Date Review performed
MHDLCNSD  DIM       8      8        283   Court Notices Sent Date
MHDLTRDT  DIM       8      8        291   Tribunal Date
MHDLLWCD  DIM       8      8        299   Lodged with Court Date
MHDLCTCD  DIM       3      3        307   Court Time Code - Cat."xy"
MHDLCMN2  DIM       100    100      310   Comments 2
MHDLDOCG  DIM       3      3        410   Pt Documentation Given (Cat "to")
MHDLSPSY  DIM       10     10       413   Supervising Psychiatrist
MHDLRESP  DIM       10     10       423   Responsible Practitioner
MHDLLDOC  DIM       10     10       433   Locum Doctor
MHDLSPRE  DIM       57     57       443   Spare
.
.End of Record                      500       ( end of File Generator data )
.
.
CMDLINE   DIM       100
DIM4      DIM       4
DIM4A     DIM       4
DIM6      DIM       6
DIM6A     DIM       6
DIM60     DIM       60
DDCENT    DIM       2
.
INPFILE   DIM       8
RECNUM    FORM      8
NEWFILE   DIM       60
NEWPATH   DIM       60
OLDPATH   DIM       60
DEFPATH   DIM       60
SAVEFLG   FORM      1
SP50      INIT    "                                                  "
SP60      INIT    "                                                            "
.
PRGNAM    INIT      "CONVERSION MEHAUDDA"
PRGID     INIT      "F03MHDAU"
VERSION   INIT      "V12.00.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN1000,MAIN0000,MAIN9999 * Yes, no, cancel
.
MAIN1000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
. Loop thru old file and write records to new file
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/compress saved file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          
INIT9999  RETURN
.
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
PREP0000 
. open old file
          MOVE      "mehaudda",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
. Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat mehaudda.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "smhaudda.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat mehaudda.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "smhaudda.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
. remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "mehaudda",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "smhaudda",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "mehaudda",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.      eg    APPEND    " 195 UC9-16",CMDLINE
.
....      APPEND    " 487 DC28-35,36-43,1-8,9-16",CMDLINE  old one
          APPEND    " 527 DC28-35,36-43,1-8,9-16",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has 
.  to be repeated with for each individual index
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " D1-8,9-16,28-35,36-43",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
          OPEN      MEHAUDDA,NEWFILE
.
. set the flag to say we want to continue
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
          ELSE
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
            ELSE
              DISPLAY   "has caused an IO error - ";
            ENDIF
          ENDIF
          DISPLAY   "File will be bypassed"
.
          IF        FORM2 < 17
            CALL      HITENTER
          ENDIF
          DISPLAY   *P1:23,*EF
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
.
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          PACK      KEY32,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
.  All necessary changes to the files field lengths, date changes
.  or any other sort of modification must be made here
.
.    eg    PACK      DSPARE,SP50
.
           PACK      MHDLDOCG,SP70
           PACK      MHDLSPSY,SP70
           PACK      MHDLRESP,SP70
           PACK      MHDLLDOC,SP70
           UNPACK    OLDLSPRE,MHDLDOCG,MHDLSPSY,MHDLRESP,MHDLLDOC
           PACK      MHDLSPRE,SP70
.                                               * Pack key here
READ5500  PACK      KEY32,MHDLURNO,MHDLUNIQ,MHDLMDAT,MHDLMTIM,SP70
          CALL      AWMHDL                    * write to new file
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     MEHAUDDA                      * close new file
          CLOSE     OLDFILE1                      * close old file
          DISPLAY   *P15:20,*V2LON,RECNUM
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
READ9999  RETURN
.
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
VALD0000  MOVE      ZERO,EXIT
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or compress save file                                *
.**********************************************************************
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "smhaudda*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "compress -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not compressed or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.         Get the default directory
.**********************************************************************
DEFT0000  EXECUTE   "ldat mehaudda.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding 'mehaudda'.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
DEFT9999  RETURN
+
.
.                  ******************************    OLD IO ROUTINES
.                  Copy the Positional Read (RS or RDS) from original I/O file,
.                  - change Label to READSOLD and the index name to OLDFILE1
.
.READSOLD  MOVE      ZERO,OVRCD
.          RESET     KEY6
.          PACK      KEY8,SP2,KEY6
.          READ      OLDFILE1,KEY8;;
.          RETURN
READSOLD  RESET     KEY32
          READ      OLDFILE1,KEY32;;
          RETURN
.
.                  Copy the Read Next (RK or RDK) from the original I/O file
.                  - change Label to READKOLD and the index name to OLDFILE1
.                  - change any fields that are changing to a prefix of OLD,
.                    eg. PTEDSPAR becomes OLDDSPAR
.
.READKOLD  MOVE      ZERO,OVRCD
.          READKS    OLDFILE1;DDURNO,DDADMNO:
.                             DIM6,DTIME,DSTAT,DDEST:
.                             PTDSVOGU,DSPARE
.          GOTO      OVERCOND IF OVER
.          MOVE      QDURNO,DURNO
.          RETURN
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;MHDLMDAT,MHDLMTIM,MHDLRTYP,MHDLMUID:
                        MHDLURNO,MHDLUNIQ,MHDLSDAT,MHDLSTIM,MHDLACTV,MHDLSCOD:
                        MHDLSTAT,MHDLRDAT,MHDLRTIM,MHDLHCPC,MHDLCASE,MHDLPOCC:
                        MHDLREMD,MHDLDRIV,MHDLRESD,MHDLRCOD,MHDLCUID,MHDLCDAT:
                        MHDLCTIM,MHDLUUID,MHDLUDAT,MHDLUTIM,MHDLCMNT,MHDLSENT:
                        MHDLEDAT,MHDLETIM,MHDLSULS,MHDLSLSD,MHDLRPDT,MHDLCNSD:
                        MHDLTRDT,MHDLLWCD,MHDLCTCD,MHDLCMN2,OLDLSPRE

          GOTO      OVERCOND IF OVER
          RETURN
.
.                  ******************************    NEW IO ROUTINES
.                  Copy the Write statement from the new I/O file           
.                  (no changes are needed to this routine)

AWMHDL    RESET     KEY32
          WRITE     MEHAUDDA,KEY32;MHDLMDAT,MHDLMTIM,MHDLRTYP,MHDLMUID:
                        MHDLURNO,MHDLUNIQ,MHDLSDAT,MHDLSTIM,MHDLACTV,MHDLSCOD:
                        MHDLSTAT,MHDLRDAT,MHDLRTIM,MHDLHCPC,MHDLCASE,MHDLPOCC:
                        MHDLREMD,MHDLDRIV,MHDLRESD,MHDLRCOD,MHDLCUID,MHDLCDAT:
                        MHDLCTIM,MHDLUUID,MHDLUDAT,MHDLUTIM,MHDLCMNT,MHDLSENT:
                        MHDLEDAT,MHDLETIM,MHDLSULS,MHDLSLSD,MHDLRPDT,MHDLCNSD:
                        MHDLTRDT,MHDLLWCD,MHDLCTCD,MHDLCMN2,MHDLDOCG,MHDLSPSY:
                        MHDLRESP,MHDLLDOC,MHDLSPRE
.
          RETURN
.
          INC       STD001IO
