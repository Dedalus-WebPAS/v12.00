.******************************************************************************
.*  Include Name  : PATMC1KY.PBL                                              *
.*  Description   : Keyin of miscellaneous charge codes                       *
.*  Author        : Ian Rutt                                                  *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF9 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMODE - is keyin mode                                  *
.*                  CLAIM    - Claim Code                                     *
.*                  HFUND    - Health Fund Code                               *
.*                  HFTAB    - Health Fund Table                              * 
.*                                                                            *
.*  Returns       : MCHARGE  - Keyed in code                                  *
.*                  MDESC   - Description                                     *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Modifications:                                                            *
.*  V9.03.02 25/09/2003  Lina Vo  CAR36486                                    *
.*           Changed misc charge validation.                                  *
.*  V9.03.01 08/09/2003  Lina Vo  CAR40497                                    *
.*           Changed index size of patmchgf.                                  *
.*                                                                            *
.******************************************************************************
PATMC1KY
PTMCKY00  MATCH     SP9,CKYIDEF9                  * using a default ?
          GOTO      PTMCKY10 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN9:      * no default used
                    *PECOL:EVERT,*V2LON,MCHARGE:
                    *PECOL:EVERT,*DV,MCHARGE
          GOTO      PTMCKY20
.
PTMCKY10  MOVE      CKYIDEF9,MCHARGE                 * default
          KEYIN     *PECOL:EVERT,*DV,MCHARGE:     * default used
                    *PECOL:EVERT,*V2LON,*RV,MCHARGE:
                    *PECOL:EVERT,*DV,MCHARGE
.
.         check what was entered
.
PTMCKY20  CALL      MCHCK000
          BRANCH    EXIT,PTMCKY25,PTMCKY31,PTMCKY41,PTMCKY95
.                        "?"      valid    nothing  exitchar
          GOTO      PTMCKY00
.
.         a "?" was input
.
PTMCKY25  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
PTMCKY26  CALL      PATMCHDS
          BRANCH    CNEWDS,PTMCKY27 
.
          CALL      PUTSCR00                * redisplay screen
          GOTO      PTMCKY00                * rekey code
.
.         now ask for code while leaving displayed codes on screen
.
PTMCKY27  MATCH     SP9,CKYIDEF9                  * using a default
          GOTO      PTMCKY28 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Misc. Charge Code : ":
                    *P21:24,*DV,UNDLN9:      * no default used
                    *P21:24,*V2LON,MCHARGE:
                    *P21:24,*DV,MCHARGE
          GOTO      PTMCKY29
.
PTMCKY28  MOVE      CKYIDEF9,MCHARGE
          KEYIN     *P1:24,*EL,*DV,"Misc. Charge Code : ":
                    *P21:24,*DV,MCHARGE:     * default used
                    *P21:24,*V2LON,*RV,MCHARGE:
                    *P21:24,*DV,MCHARGE
.
.         repeat the validation of the code
.         check what was entered
.
PTMCKY29  CALL      MCHCK000
          BRANCH    EXIT,PTMCKY26,PTMCKY30,PTMCKY40,PTMCKY90
.                        "?"      valid    nothing  exitchar
.
          GOTO      PTMCKY27
.
.         a valid code was entered 
. 
PTMCKY30  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,MCHARGE
.
PTMCKY31  MOVE      FALSE,EXIT                    * valid input
          GOTO      PTMCKY99 
.
.         nothing was input
.
PTMCKY40  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,MCHARGE
.
PTMCKY41  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      PTMCKY99 
.
.         EXITCHAR input
.
PTMCKY90  CALL      FRESCR00                * free the screen saved
.
PTMCKY95  MOVE      TWO,EXIT                * EXITCHAR
.
PTMCKY99  RETURN
+
.*********************************************************************
.*                  MCHCK000                    Called by : PATMC1KY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
MCHCK000  ENDSET    MCHARGE
          APPEND    SP9,MCHARGE
          RESET     MCHARGE
.**       MOVE      MCHARGE,SAVCHG
.
          MATCH     EXITCHAR,MCHARGE
          GOTO      MCHCK600 IF EQUAL       * exit char
.
          MATCH     SP9,MCHARGE
          GOTO      MCHCK700 IF EQUAL       * nothing
.
          MATCH     QUEST,MCHARGE
          GOTO      MCHCK800 IF EQUAL       * ? mark
.
.         a code was entered so validate
. 
MCHCK500  
...        BRANCH    CLMFLG,MCHCK510
...
...        BRANCH    CMCNOHF,MCHCK510
...
...        COMPARE   ZERO,CMCNOHF
...        GOTO      MCHCK510 IF EQUAL
...
...        BRANCH    FNDFLG,MCHCK510
.
          UNPACK    SP20,PTHFBCAT           * change "H/F Table" to "Table Type"
          PACK      KEY14,AFUNDH,AFNDTB,SP20
          CALL      RDFUND1                 * "Table Type code" now in PTHFBCAT
.
          MOVE      MCHARGE,KEY9            * (MCHARGE re-use in PATMCHRD)
          PACK      KEY29,CLAIM,HFUND,PTHFBCAT,KEY9,SP20
          UNPACK    KEY29,KEY21,KEY8
.
          IF        NEWFLG=1
            CALL      RDMC1G1
          ELSE
            CALL      RDMCHG1
          ENDIF
.
          IF        CKYIMODE=0
            COMPARE   ZERO,OVRCD
            GOTO      MCHGCK510 IF EQUAL
            IF        NEWFLG=1
              CALL    RDKMC1G1
            ELSE 
              CALL    RDKMCHG1
            ENDIF
            BRANCH    OVRCD,MCHCK900          * invalid code
            PACK      KEY21A,MCLMCOD,MHFUND,PTHFBCAT,MCHARGE,SP70
            MATCH     KEY21,KEY21A
            GOTO      MCHCK900 IF NOT EQUAL
          ELSE
            COMPARE   ZERO,OVRCD   
            GOTO      MCHCK900 IF EQUAL
          ENDIF  
.
MCHGCK510 MOVE      TWO,EXIT                * valid code
          GOTO      MCHCK999
.
...MCHCK510  PACK      KEY34,CLAIM,HFUND,HFTAB,MCHARGE,SP70
...          CALL      RDSMCHG1
...
...MCHCK520  CALL      RDKMCHG1 
...          BRANCH    OVRCD,MCHCK900
...
...          BRANCH    CLMFLG,MCHCK530
...          MATCH     SAVCLM,MCLMCOD
...          GOTO      MCHCK520 IF NOT EQUAL
...
...MCHCK530  BRANCH    CMCNOHF,MCHCK550
...          BRANCH    FNDFLG,MCHCK540
...          MATCH     SAVFND,MHFUND
...          GOTO      MCHCK520 IF NOT EQUAL
...
...MCHCK540  BRANCH    TABFLG,MCHCK550
...          MATCH     SAVTAB,MHFTAB
...          GOTO      MCHCK520 IF NOT EQUAL
...
...MCHCK550  MATCH     SAVCHG,MCHARGE
...          GOTO      MCHCK520 IF NOT EQUAL
...          MOVE      TWO,EXIT
...          GOTO      MCHCK999   
.
.         exitchar entered
.
MCHCK600  MOVE      FOUR,EXIT
          MOVE      SP9,MCHARGE
          MOVE      SP20,MDESC              * clear desc field
          GOTO      MCHCK999
.
.         nothing entered
.
MCHCK700  BRANCH    CKYIMAND,MCHCK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP9,MCHARGE
          MOVE      SP20,MDESC              * clear desc field
          GOTO      MCHCK999
.
.         "?" entered
.
MCHCK800  MOVE      ONE,EXIT
          GOTO      MCHCK999
.
.         invalid code entered
.
MCHCK900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
.
          IF        CKYIMODE = 0
            DISPLAY   *P1:24,*EL,*B,"Invalid Miscellaneous Charge Code. ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      MCHCK999
.
.         field is mandatory
.
MCHCK950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is Mandatory. ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
MCHCK999  RETURN
.
          INCLUDE   PATMCHDS
+
