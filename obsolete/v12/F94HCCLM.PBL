.***************************************************************************
.* System    :   Billing                                                   *
.* Program   :   F94HCCLM                                                  *
.* Desc      :   This program will move zero to adjustment amount          *
.***************************************************************************
.* Date      :   10/05/2004                                                *
.* Author    :   J.Tan         CAR 61440                                   *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       HICCLMFD/INC
.
RECNUM    FORM      8
. LOCAL VARIABLE DEFINITION
. -------------------------
.
PRGID     INIT      "F94HCCLM"
PRGNAM    INIT      "Add Adjustment amount to Claim file"
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      HICCLMA1,"hicclmaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,RECNUM
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
.
          PACK      KEY8,SP70
          CALL      RSHCCLM1
PROC1000  CALL      RKHCOLD1
          BRANCH    OVRCD,PROC9999
.
          ADD       ONE,RECNUM
          IF        (RECNUM%100) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MOVE      ZERO,HCCLADJM
          CALL      UPHCCLM1
.
          GOTO      PROC1000
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
RKHCOLD1  MOVE      ZERO,OVRCD
          READKS    HICCLMA1;HCCLCLMN,HCCLSTAT,HCCLBTCH,HCCLVISN,HCCLVTYP:
                        HCCLURNO,HCCLPMCI,HCCLPYEE,HCCLPSRV,HCCLMPRA,HCCLDATE:
                        HCCLADOC,HCCLSITE,HCCLCTYP,HCCLCLID,HCCLPIND,HCCLMITM:
                        HCCLSENT,HCCLRECP,HCCLPBAT,HCCLNBAT,HCCLCAMN,HCCLRCVA:
                        HCCLREJA,HCCLWOFF,HCCLTRIN,HCCLCDAT,HCCLCTIM:
                        HCCLCUID,HCCLUDAT,HCCLUTIM,HCCLUUID,HCCLSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
          INC       STD001IO
          INC       HICCLMIO/INC
