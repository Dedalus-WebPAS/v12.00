.*****************************************************************************
.*    Program      : F03PTALR                                                *
.*    Description  : Conversion patalrtf to new File layout                  *
.*                                                                           *
.*    Author       : Jill Parkinson                                          *
.*    Date         : 21/11/2011                                              *
.*    Modifications:                                                         *
.*        V10.03.01 10/04/2013  Steve Armstrong  CAR 283921                  *
.*                  Removed Oracle option as the oracle script has been      *
.*                  modified to initialise the counter field to "  1".       *
.*        V10.03.00 21/11/2011  Jill Parkinson   CAR 249362                  *
.*                  Created                                                  *
.*****************************************************************************
.
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
.
OLDFILE1   IFILE    SQL, FIXED=175, KEY="U1-8,9-10,11-13"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.PTALURNO  DIM       8      8        1     Patients UR Number
.PTALCATG  DIM       2      2        9     Codes File Category (H1-H9)
.PTALCODE  DIM       3      3        11    Codes File Code (for Category)
.PTALDATE  DIM       8      8        14    Alert Date (CCYYMMDD)
.PTALCDTE  DIM       8      8        22    Creation Date (CCYYMMDD)
.PTALLIFE  DIM       1      1        30    Life Threatening Alert?
.                                         PTCNLFCH = Yes, " " = No.
.PTALREAC  DIM       3      3        31    Reaction code (Category wn)
.PTALUSID  DIM       10     10       34    Web User ID
.PTALLSEV  DIM       1      1        44    Severity Level (0-9)
.PTALRCOM  DIM       20     20       45    Reaction Comment
.PTALRDTE  DIM       8      8        65    Review Date (ccyymmdd)
.PTALICDC  DIM       9      9        73    ICD10 Code
.PTALDTIN  DIM       8      8        82    Date Inactive (ccyymmdd)
.PTALEDAT  DIM       8      8        90    End Date/Date Cleared (ccyymmdd)
.PTALRQBY  DIM       10     10       98    Requested By (HCP Code)
.PTALHOSP  DIM       3      3        108   Initiating Hospital
.PTALCTIM  DIM       8      8        111   Time Record Created (hh:mm:ss)
.PTALUDAT  DIM       8      8        119   Date Record Updated (ccyymmdd)
.PTALUTIM  DIM       8      8        127   Time Record Updated (hh:mm:ss)
.PTALUUID  DIM       10     10       135   WEB User ID Updated (websecaf)
OLDLSPAR  DIM       30     30       145   Spare Variable
.
.End of Record                      175
.
.
.
.                                     ******** copy the new FD here *******
PATALRT1   IFILE    SQL, FIXED=175, KEY="U1-8,9-10,11-13,14-16"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
PTALURNO  DIM       8      8        1     Patients UR Number
PTALCATG  DIM       2      2        9     Codes File Category (H1-H9)
PTALCODE  DIM       3      3        11    Codes File Code (for Category)
PTALCNTR  DIM       3      3        14    Counter
PTALDATE  DIM       8      8        17    Alert Date (CCYYMMDD)
PTALCDTE  DIM       8      8        25    Creation Date (CCYYMMDD)
PTALLIFE  DIM       1      1        33    Life Threatening Alert?
.                                         PTCNLFCH = Yes, " " = No.
PTALREAC  DIM       3      3        34    Reaction code (Category wn)
PTALUSID  DIM       10     10       37    Web User ID
PTALLSEV  DIM       1      1        47    Severity Level (0-9)
PTALRCOM  DIM       20     20       48    Reaction Comment
PTALRDTE  DIM       8      8        68    Review Date (ccyymmdd)
PTALICDC  DIM       9      9        76    ICD10 Code
PTALDTIN  DIM       8      8        85    Date Inactive (ccyymmdd)
PTALEDAT  DIM       8      8        93    End Date/Date Cleared (ccyymmdd)
PTALRQBY  DIM       10     10       101   Requested By (HCP Code)
PTALHOSP  DIM       3      3        111   Initiating Hospital
PTALCTIM  DIM       8      8        114   Time Record Created (hh:mm:ss)
PTALUDAT  DIM       8      8        122   Date Record Updated (ccyymmdd)
PTALUTIM  DIM       8      8        130   Time Record Updated (hh:mm:ss)
PTALUUID  DIM       10     10       138   WEB User ID Updated (websecaf)
PTALSPAR  DIM       27     27       148   Spare Variable
.
.End of Record                      175
.
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       100
.
DEFPATH   DIM       60
DIM60     DIM       60
.
INPFILE   DIM       8
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
SAVEFLG   FORM      1
SAVKEY16  DIM       16
.
.
. PROGRAM CONSTANTS
. -----------------
SP60      INIT    "                                                            "
.
.
.
PRGNAM    INIT      "CONVERSION PATALRFD"
PRGID     INIT      "F13PTALR"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000              * run c-isam fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN0000:               * no
                          MAIN9999                * cancel
.
.         Running c-isam fix
.
MAIN5000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/compress saved file
          GOTO      MAIN9999
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
.
PREP0000  MOVE      "patalrtf",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat patalrtf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sptalrtf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat patalrtf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sptalrtf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "patalrtf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "sptalrtf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "patalrtf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.      eg    APPEND    " 195 UC9-16",CMDLINE
.
          APPEND    " 175 U1-8,9-10,11-13,14-16",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      PATALRT1,NEWFILE
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
            ELSE
              DISPLAY   "has caused an IO error - ";
            ENDIF
          ENDIF
          DISPLAY   "File will be bypassed"
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY13,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
.
          IF        (RECNUM%1000) = 0 | (RECNUM%1000) = 1
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          PACK      PTALCNTR,SP1,SP1,ONE
.                                               * Pack key here
          PACK      KEY16,PTALURNO,PTALCATG,PTALCODE,PTALCNTR,SP70
          CALL      WRPTALR1                    * write to new file
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     PATALRT1                      * close new file
          CLOSE     OLDFILE1                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
.
VALD0000  MOVE      ZERO,EXIT
.
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or compress save file                                *
.**********************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "sptalrtf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "compress -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not compressed or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.*        DEFT0000 -  Get the default directory                       *
.**********************************************************************
.
DEFT0000  EXECUTE   "ldat patalrtf.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding 'patalrtf'.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
READSOLD  MOVE      ZERO,OVRCD
          RESET     KEY13
          READ      OLDFILE1,KEY13;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;PTALURNO,PTALCATG,PTALCODE,PTALDATE,PTALCDTE:
                        PTALLIFE,PTALREAC,PTALUSID,PTALLSEV,PTALRCOM,PTALRDTE:
                        PTALICDC,PTALDTIN,PTALEDAT,PTALRQBY,PTALHOSP,PTALCTIM:
                        PTALUDAT,PTALUTIM,PTALUUID,OLDLSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
RSPTALR1  RESET     KEY16
          READ      PATALRT1,KEY16;;
          RETURN
.
RKPTALR1  MOVE      ZERO,OVRCD
          READKS    PATALRT1;PTALURNO,PTALCATG,PTALCODE,PTALCNTR,PTALDATE:
                        PTALCDTE,PTALLIFE,PTALREAC,PTALUSID,PTALLSEV,PTALRCOM:
                        PTALRDTE,PTALICDC,PTALDTIN,PTALEDAT,PTALRQBY,PTALHOSP:
                        PTALCTIM,PTALUDAT,PTALUTIM,PTALUUID,PTALSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
WRPTALR1  RESET     KEY16
          WRITE     PATALRT1,KEY16;PTALURNO,PTALCATG,PTALCODE,PTALCNTR,PTALDATE:
                        PTALCDTE,PTALLIFE,PTALREAC,PTALUSID,PTALLSEV,PTALRCOM:
                        PTALRDTE,PTALICDC,PTALDTIN,PTALEDAT,PTALRQBY,PTALHOSP:
                        PTALCTIM,PTALUDAT,PTALUTIM,PTALUUID,PTALSPAR
          RETURN
.
UPPTALR1  MOVE      ZERO,OVRCD
          UPDATE    PATALRT1;PTALURNO,PTALCATG,PTALCODE,PTALCNTR,PTALDATE:
                        PTALCDTE,PTALLIFE,PTALREAC,PTALUSID,PTALLSEV,PTALRCOM:
                        PTALRDTE,PTALICDC,PTALDTIN,PTALEDAT,PTALRQBY,PTALHOSP:
                        PTALCTIM,PTALUDAT,PTALUTIM,PTALUUID,PTALSPAR
          GOTO      UPDFLOCK IF LOCKED
          RETURN
.
          INC       STD001IO
