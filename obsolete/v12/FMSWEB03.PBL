. **********************************************************************
. * System    :   Web Finance System                                   *
. * Program   :   FMSWEB03                                             *
. * Desc      :   Financial Server - Account Summary                   *
. * Mods      :                                                        *
. **********************************************************************
. V10.03.02 16/07/2013  Jill Parkinson CAR 287923
.           Added include of PATCONFD
. * V10.03.01 25/02/2013 Ania P         CAR 281021                     *
. *           Removed unnecessary modification of constants.           *
. * V9.03.02  23.09.2003 Steve Downing  CAR 42285    (VF.11.01)        *
. *           Recompiled for FMSWEBCD                                  *
. * V9.03.01  21.10.2003 Ebon Clements  CAR 44461                      *
. *           Changed server to use the finance passcode WBSEFPCD      *
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
. * VF.01.01  26.10.2000 Sandra Barcham                                *
. *           Recompiled for version change                            *
. **********************************************************************
. * VF.00.05  11.01.2000 Charles Handaya                               *
. *           Recompiled for Audit Files date                          *
. * VF.00.04  23.12.1999 Sandra Barcham                                *
. *           Recompiled for FMSCCAFD.INC                              *
. *           srf 646145                                               *
. * VF.00.03  12.11.1999 B.G.Ackland                                   *
. *           Fix Links to Account Codes with Spaces included          *
. * VF.00.02  05.11.1999 B.G.Ackland                                   *
. *           Enable Annual Budget or Last Year Columns                *
. **********************************************************************
.
          INC       IBAWEBDF    
          INC       FMSWEBDF
.
          INC       PATCONFD/INC
          INC       FMSAMAFD/INC       * account
          INC       FMSBTYFD/INC       * budget data
          INC       FMSBUDFD/INC       * budget data
          INC       FMSCONFD/INC       * controlf
          INC       FMSFPSFD/INC       * period summary file
          INC       FMSLMAFD/INC       * ledger
          INC       FMSLMCFD/INC       * ledger financial calendar
          INC       FMSSBUFD/INC       * stat budget
          INC       FMSSMAFD/INC       * stat summary
          INC       FMSUWPFD/INC       * wp file
          INC       FMSCODFD/INC       * codes file
          INC       FMSLMBFD/INC       * report
          INC       FMSCCAFD/INC       * cost centre
          INC       FMSSBAFD/INC       * subjective
          INC       FMSRSFFD/INC       * enquiry security
          INC       FMSWASFD/INC       * enquiry security
          INC       FMSWBCFD/INC       * enquiry security comments
          INC       FMSWCFFD/INC       * Variance Flag
.
LASTCOL   FORM      1
SAVEACCT  DIM       12
FMTACCT   DIM       25
WRKKEY14  DIM       14
FLAGFRST  FORM      1
LOCKFLAG  FORM      1
MTH301    DIM       3
MTH302    DIM       3
MTH303    DIM       3
MTH304    DIM       3
MTH305    DIM       3
MTH306    DIM       3
MTH307    DIM       3
MTH308    DIM       3
MTH309    DIM       3
MTH310    DIM       3
MTH311    DIM       3
MTH312    DIM       3
MTH313    DIM       3
.
DSETNUM   DIM       1
DSETAXIS  DIM       1
NUMPER    FORM      2
TAGBEF    DIM       6
TAGAFT    DIM       6
PERIODNO  FORM      2                  * current period number
.
NEXTYEAR  DIM       4  
LASTYEAR  DIM       4  
NEXTPERD  DIM       2
LASTPERD  DIM       2
.
LEDGCODE  DIM       2
ACCTCODE  DIM       12
ACCOUNTN  DIM       45
BGRED     INIT      "#"##FF0000#""
BGGREEN   INIT      "#"##008000#""
BGGOOD    DIM       10
BGBAD     DIM       10
YTDBGC    DIM       9
CURBGC    DIM       9
.
CHARTOPT  FORM      1
ACCTKEYS  DIM       14
LINKUSER  DIM       4
FINCYEAR  DIM       4
FINCPERD  DIM       2
MAXVALUE  FORM      10
STEPVAL   FORM      10
PERNAME   DIM       12
SELNAME   DIM       12
SELPERD   FORM      2
SELYEAR   DIM       4
YEAREND   FORM      2
PERNAME3  DIM       3 
.
AMTCOL01  FORM      12.5
AMTCOL02  FORM      12.5
AMTCOL03  FORM      12.5
AMTCOL04  FORM      12.5
AMTCOL05  FORM      12.5
AMTCOL06  FORM      12.5
AMTCOL07  FORM      12.5
OUTSTR18  DIM       18
FMTCOL01  DIM       18
FMTCOL02  DIM       18
FMTCOL03  DIM       18
FMTCOL04  DIM       18
FMTCOL05  DIM       18
FMTCOL06  DIM       18
FMTCOL07  DIM       18
EDITTYPE  FORM      1
FORMATTY  FORM      1
.
EDITSTR0  DIM       18
EDITSTR1  DIM       18
EDITSTR2  DIM       18
EDITSTR3  DIM       18
EDITSTR4  DIM       18
EDITSTR5  DIM       18
.
EDITSTA0  INIT      "(9999,999,999,999 "
EDITSTA1  INIT      "(99,999,999,999.9 "
EDITSTA2  INIT      "(9,999,999,999.99 "
EDITSTA3  INIT      "(9999,999,999.999 " 
EDITSTA4  INIT      "(999,999,999.9999 " 
EDITSTA5  INIT      "(99,999,999.99999 "
.
EDITSTB0  INIT      "99,999,999,999,999"
EDITSTB1  INIT      "9,99,999,999,999.9"
EDITSTB2  INIT      "999,999,999,999.99"
EDITSTB3  INIT      "99,999,999,999.999" 
EDITSTB4  INIT      "9,999,999,999.9999" 
EDITSTB5  INIT      "9999,999,999.99999"
.
AMT12P0   FORM      12
AMT12P1   FORM      12.1
AMT12P2   FORM      12.2
AMT12P3   FORM      12.3
AMT12P4   FORM      12.4
AMT12P5   FORM      12.5
.
INCOMEFL  FORM      2
ACTUAL    FORM      12.2
BUDGET    FORM      12.2
ACTUALCP  FORM      12.2
BUDGETCP  FORM      12.2
VARIANCP  FORM      12.2
PERVARCP  FORM      6.1 
.
ACTUALLY  FORM      12.2
ACTUALYD  FORM      12.2
BUDGETYD  FORM      12.2
VARIANYD  FORM      12.2
PERVARYD  FORM      6.1 
.
INDEX2    FORM      2
JAVATMPN  DIM       30
JAVATMPF  FILE      ASCII, FIXED=250
SAVTYPE   FORM      1
AVYTD     FORM      1
.
PRGID     INIT      "FMSWEB03"
PRGNAM    INIT      "Financial Server - Account Summary"
VERSION   INIT      "V12.00.00"
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          MATCH     SP70,WBSEFPCD       
          IF        !@EQUAL
            MOVE      WBSEFPCD,PASSCODE     * Web security finance passcode
          ENDIF
.
          BRANCH    REPORTNO,MAIN1100
.
          MOVE      "INVALID OPTION",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
.
MAIN1100  CALL      ACCSUM00    * Summary of Allocated Accounts
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  OPEN      CONTROLF,"controlf"
          CALL      RDFMCO50
          CALL      RDFMCO51
          CALL      RDFMCO52
          CALL      RDFMCO65
          CALL      RDFMCO66
.
          OPEN      FMSAMAA1,"fmsamaaf"
          OPEN      FMSWASA1,"fmswasaf"
          OPEN      FMSWBCA1,"fmswbcaf"
          OPEN      FMSWCFA1,"fmswcfaf"
          OPEN      FMSLMAA1,"fmslmaaf"
          OPEN      FMSLMCA1,"fmslmcaf"
          OPEN      FMSUWPA1,"fmsuwpaf"
          OPEN      FMSCODA1,"fmscodaf"
          OPEN      FMSLMBA1,"fmslmbaf"
          OPEN      FMSCCAA1,"fmsccaaf"
          OPEN      FMSSBAA1,"fmssbaaf"
          OPEN      FMSRSFA2,"fmsrsfaf"
.
          RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      ZERO,CHARTOPT
          MOVE      SP70,ACCTKEYS
          MOVE      SP70,FINCYEAR
          MOVE      SP70,FINCPERD
          MOVE      SP70,LINKUSER
          MOVE      "000",TEMPLATE
          RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "acctkeys=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ACCTKEYS
            REP       "a&",ACCTKEYS
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "chartopt=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,CHARTOPT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fincyear=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FINCYEAR
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fincperd=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FINCPERD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "linkuser=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,LINKUSER
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.------------------------------------------------------------
. Account Summary
.------------------------------------------------------------
ACCSUM00  MOVE      "Financial Summary",WEBTITLE
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ACCSUM99
.
          MATCH     SP70,LINKUSER
          IF        !@EQUAL
            MOVE      LINKUSER,PASSCODE
          ENDIF
.
          CALL      SETPER00   * Set Period and Year
          CALL      OPENFS00   * Open Summary Files
.
          MOVE      PERIODNO,F2
          LOAD      PERNAME,F2,FMLC01DE,FMLC02DE,FMLC03DE,FMLC04DE,FMLC05DE:
                               FMLC06DE,FMLC07DE,FMLC08DE,FMLC09DE,FMLC10DE:
                               FMLC11DE,FMLC12DE
          LOAD      KEY8,PERIODNO,FMLC01TO,FMLC02TO,FMLC03TO,FMLC04TO:
                                  FMLC05TO,FMLC06TO,FMLC07TO,FMLC08TO:
                                  FMLC09TO,FMLC10TO,FMLC11TO,FMLC12TO,FMLC13TO
          LOAD      LOCKFLAG,F2,FMLC01IN,FMLC02IN,FMLC03IN,FMLC04IN,FMLC05IN:
                                FMLC06IN,FMLC07IN,FMLC08IN,FMLC09IN,FMLC10IN:
                                FMLC11IN,FMLC12IN
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          CALL      NXTLST00
.
          CALL      WEBBOD00   * Do Body of HTML File
.
          CALL      WEBEND00   * Do End of HTML File
.
ACCSUM99  RETURN
.------------------------------------------------------------
. Process Field Tags
.------------------------------------------------------------
PROFLD00  CALL      SUMP0000
          RETURN
.------------------------------------------------------------
. Summary Fields
.------------------------------------------------------------
SUMP0000  BRANCH    FLDITMNO,SUMP0100,SUMP0200,SUMP0300,SUMP0400,SUMP0500:
                             SUMP0600,SUMP0700,SUMP0800,SUMP0900,SUMP1000:
                             SUMP1100
          GOTO      SUMP9999
.
SUMP0100  MOVE      ZERO,LASTCOL * Last Column=Last Year
          CALL      SUMTAB00     * Table of Accounts
          GOTO      SUMP9999
.
SUMP0200  CALL      ADDCOM00    Comments
          GOTO      SUMP9999 
.
SUMP0300  IF        LOCKFLAG=0
            WRITE     HTMLFILE;"Interim Results";
          ENDIF
          GOTO      SUMP9999 
.
SUMP0400  WRITE     HTMLFILE;LASTPERD;
          GOTO      SUMP9999 
.
SUMP0500  WRITE     HTMLFILE;LASTYEAR;
          GOTO      SUMP9999 
.
SUMP0600  WRITE     HTMLFILE;NEXTPERD;
          GOTO      SUMP9999 
.
SUMP0700  WRITE     HTMLFILE;NEXTYEAR;
          GOTO      SUMP9999 
.
SUMP0800  WRITE     HTMLFILE;PERIODNO;
          GOTO      SUMP9999 
.
SUMP0900  WRITE     HTMLFILE;CURYEAR;
          GOTO      SUMP9999 
.
SUMP1000  CALL      SELPER00     * Select Period Options
          GOTO      SUMP9999 
.
SUMP1100  MOVE      ONE,LASTCOL * Last Column=Annual Budget
          CALL      SUMTAB00    * Table of Accounts
          GOTO      SUMP9999
.
SUMP9999  RETURN
.------------------------------------------------------------
. Summary Table
.------------------------------------------------------------
SUMTAB00  MOVE      ZERO,FORMATTY
          UNPACK    DIM127,KEY1,ANS,DIM127
          MOVE      ANS,FORMATTY
          PACK      KEY21,PASSCODE,SP70
          CALL      RSFMWA1                      * go to start of records
SUMTAB10  CALL      RKFMWA1                      * get next record 
          BRANCH    OVRCD,SUMTAB90       
          MATCH     PASSCODE,FMWASEC
          GOTO      SUMTAB90 IF NOT EQUAL
          PACK      KEY14,FMWALEDG,FMWAACCT,SP70
          UNPACK    KEY14,LEDGCODE,ACCTCODE
.
          MATCH     SP70,FMWABCK
          IF        @EQUAL
            WRITE     HTMLFILE;"<tr>";
          ELSE
            WRITE     HTMLFILE;"<tr bgcolor=",FMWABCK,">";
          ENDIF
.
          MOVE      SP70,TAGBEF
          MOVE      SP70,TAGAFT
          IF        FMWABLD=1
            MOVE      "<b>",TAGBEF
            MOVE      "</b>",TAGAFT
          ENDIF
.
          CALL      SUMDET00             * Output Row Details
.
          WRITE     HTMLFILE;"</tr>"
.
          MATCH     "1",FMWACHD
          CALL      COLHED00 IF EQUAL    * Insert Column Headings
.
          GOTO      SUMTAB10           
.
SUMTAB90  
SUMTAB99  RETURN
.------------------------------------------------------------
. Add Comments
.------------------------------------------------------------
ADDCOM00  MOVE      PERIODNO,F2
          PACK      KEY13,PASSCODE,CURYEAR,F2,SP70
          PACK      KEY10,PASSCODE,CURYEAR,F2,SP70
          CALL      RSFMWB1                      * go to start of records
ADDCOM10  CALL      RKFMWB1                      * get next record
          BRANCH    OVRCD,ADDCOM90       
          PACK      KEY13,FMWBUID,FMWBYR,FMWBPR,SP70
          MATCH     KEY10,KEY13
          GOTO      ADDCOM90 IF NOT EQUAL
          WRITE     HTMLFILE;FMWBCOM
          GOTO      ADDCOM10
ADDCOM90  
ADDCOM99  RETURN 
.------------------------------------------------------------
. Insert Column Heading
.------------------------------------------------------------
COLHED00  WRITE     HTMLFILE;"<TR>":
                             "<TD colspan=3 align=center>":
                             "<b>Current</b></TD>":
                             "<TD> &nbsp;</TD>":
                             "<TD colspan=4 align=center>":
                                  "<b>Year to Date</b></TD>":
                             "</TR>":
                             "<TR>":
                             "<TD align=right><b>Actuals</b></TD>":
                             "<TD align=right><b>Budget</b></TD>":
                             "<TD align=right><b>Variance</b></TD>":
                             "<TD><b>Account</b></TD>":
                             "<TD align=right><b>Actuals</b></TD>":
                             "<TD align=right><b>Budget</b></TD>":
                             "<TD align=right><b>Variance</b></TD>";
          IF        LASTCOL=0
            WRITE     HTMLFILE;"<TD align=right><b>Last Year</b></TD></TR>"
          ELSE
            WRITE     HTMLFILE;"<TD align=right><b>Annual Budget</b></TD></TR>"
          ENDIF
          RETURN
.------------------------------------------------------------
. Summary Details
.------------------------------------------------------------
SUMDET00  PACK      KEY14,LEDGCODE,ACCTCODE,SP70
          CALL      GETACC00                     * Get Account Details
.
          PACK      KEY14,LEDGCODE,ACCTCODE,SP70
          CALL      RDFMAM1
          MATCH     "7",FMAMTYPE
          GOTO      SUMDET90 IF EQUAL
.
          CALL      FINDET00           * Get Data
.
          MATCH     "1",FMCOBVAR
          IF        @EQUAL
            MOVE      BGGREEN,BGBAD
            MOVE      BGRED,BGGOOD
            ASSIGN    CURBYTD-CURAYTD,CURAYTDV
            ASSIGN    CURBPER-CURAPER,CURAPERV
          ELSE
            MOVE      BGGREEN,BGGOOD
            MOVE      BGRED,BGBAD
            ASSIGN    CURAYTD-CURBYTD,CURAYTDV
            ASSIGN    CURAPER-CURBPER,CURAPERV
          ENDIF
.
          PACK      KEY14,LEDGCODE,ACCTCODE,SP70
          CALL      RDFMWC1            * Check for reverse variance
          BRANCH    OVRCD,SUMDET05
.
          MOVE      BGGOOD,YTDBGC
          IF        CURAYTDV<=0
            MOVE      BGBAD,YTDBGC
          ENDIF
          MOVE      BGGOOD,CURBGC
          IF        CURAPERV<=0
            MOVE      BGBAD,CURBGC
          ENDIF
.
          GOTO      SUMDET10
.
SUMDET05  MOVE      BGGOOD,YTDBGC
          IF        CURAYTDV>=0
            MOVE      BGBAD,YTDBGC
          ENDIF
          MOVE      BGGOOD,CURBGC
          IF        CURAPERV>=0
            MOVE      BGBAD,CURBGC
          ENDIF
.
SUMDET10  CALL      MLINK000    * Make Link
.
          MOVE      CURAPER,AMTCOL01
          MOVE      CURBPER,AMTCOL02
          MOVE      CURAPERV,AMTCOL03
          MOVE      CURAYTD,AMTCOL04
          MOVE      CURBYTD,AMTCOL05
          MOVE      CURAYTDV,AMTCOL06
          IF        LASTCOL=0
            MOVE      LYRAYTD,AMTCOL07  * Last Year
          ELSE
            MOVE      CURB01,AMTCOL07   * Annual Budget
            ADD       CURB02,AMTCOL07
            ADD       CURB03,AMTCOL07
            ADD       CURB04,AMTCOL07
            ADD       CURB05,AMTCOL07
            ADD       CURB06,AMTCOL07
            ADD       CURB07,AMTCOL07
            ADD       CURB08,AMTCOL07
            ADD       CURB09,AMTCOL07
            ADD       CURB10,AMTCOL07
            ADD       CURB11,AMTCOL07
            ADD       CURB12,AMTCOL07
            ADD       CURB13,AMTCOL07
          ENDIF
.
          MULT      INCOMEFL,AMTCOL01
          MULT      INCOMEFL,AMTCOL02
          MULT      INCOMEFL,AMTCOL03
          MULT      INCOMEFL,AMTCOL04
          MULT      INCOMEFL,AMTCOL05
          MULT      INCOMEFL,AMTCOL06
          MULT      INCOMEFL,AMTCOL07
          MOVE      FMWADEC,EDITTYPE
.
          CALL      FORMAT00
.
          STRIP     HTMLLINK
          MOVELPTR  HTMLLINK,LENGTH
          COMPARE   ZERO,LENGTH
          IF        @EQUAL
            SFORMAT   VAR,1
            MOVE      SP70,VAR
          ELSE
            SFORMAT   VAR,LENGTH
            MOVE      HTMLLINK,VAR
          ENDIF
.
          WRITE     HTMLFILE;"<td align=right>",TAGBEF,FMTCOL01,TAGAFT,"</td>":
                             "<td align=right>",TAGBEF,FMTCOL02,TAGAFT,"</td>":
                             "<td align=right><font color=",CURBGC,">":
                             TAGBEF,FMTCOL03,TAGAFT,"</font></td>":
                             "<td>":
                             "<a href=#"javascript:",VAR,";#">":
                             TAGBEF,ACCOUNTN,TAGAFT,"</a></td>":
                             "<td align=right>",TAGBEF,FMTCOL04,TAGAFT,"</td>":
                             "<td align=right>",TAGBEF,FMTCOL05,TAGAFT,"</td>":
                             "<td align=right><font color=",YTDBGC,">":
                             TAGBEF,FMTCOL06,TAGAFT,"</FONT></TD>":
                             "<td align=right>",TAGBEF,FMTCOL07,TAGAFT,"</td>"
          SFORMAT   VAR,127
          GOTO      SUMDET99                     * get next record
.
. Heading Only Display Name
.
SUMDET90  MATCH     SP70,FMWAURL
          GOTO      SUMDET92 IF NOT EQUAL
          MATCH     SP70,FMWALPC
          GOTO      SUMDET95 IF EQUAL
          CLEAR     HTMLLINK
          APPEND    "userLink('",HTMLLINK
          APPEND    FMWAREP,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    KEY3,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    FMWALPC,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
          GOTO      SUMDET94
.
SUMDET92  CLEAR     HTMLLINK
          APPEND    "staticLink('",HTMLLINK
          STRIP     FMWAURL
          APPEND    FMWAURL,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
SUMDET94  WRITE     HTMLFILE;"<td colspan=8 align=center>":
                             "<a href=#"javascript:",HTMLLINK,";#">":
                             TAGBEF,ACCOUNTN,TAGAFT,"</a></td>"
          GOTO      SUMDET99
.
SUMDET95  WRITE     HTMLFILE;"<td colspan=8 align=center>",TAGBEF:
                             ACCOUNTN,TAGAFT,"</td>"
          GOTO      SUMDET99
.
SUMDET99  RETURN
.------------------------------------------------------------
. Make HTML Link
.------------------------------------------------------------
MLINK000  PACK      KEY14,LEDGCODE,ACCTCODE,SP70
          MOVE      FMWATMP,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
          MATCH     SP70,FMWAREP
          IF        @EQUAL
            MOVE      "01",FMWAREP
          ENDIF
.
          MATCH     SP70,FMWAURL
          GOTO      MLINK200 IF NOT EQUAL
.
          MATCH     SP70,FMWALPC
          GOTO      MLINK100 IF EQUAL
.
          CLEAR     HTMLLINK
          APPEND    "userLink('",HTMLLINK
          APPEND    FMWAREP,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    KEY3,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    FMWALPC,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
          GOTO      MLINK999 
.
.
MLINK100  MOVE      "fmsweb04",FMWAPRG
          MATCH     "6",FMAMTYPE
          IF        !@EQUAL
            MATCH     "02",FMWAREP
            IF        @EQUAL
              MOVE      "fmsweb01",FMWAPRG
            ENDIF
          ENDIF
.
          CLEAR     HTMLLINK
          APPEND    "accountLink('",HTMLLINK
          APPEND    FMWAPRG,HTMLLINK
          APPEND    ".pbl','",HTMLLINK
          APPEND    FMWAREP,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    KEY3,HTMLLINK
          APPEND    "','",HTMLLINK
.
          CALL      SETACC00
          STRIP     FMTACCT
          APPEND    FMTACCT,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
          GOTO      MLINK999
.
MLINK200  CLEAR     HTMLLINK
          APPEND    "staticLink('",HTMLLINK
          STRIP     FMWAURL
          APPEND    FMWAURL,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
MLINK999  RETURN
.------------------------------------------------------------
. Setup Account Key replace & with &AMP;
.------------------------------------------------------------
SETACC00  MOVE      KEY14,WRKKEY14
          STRIP     WRKKEY14
          CLEAR     FMTACCT
SETACC10  SCAN      "&",WRKKEY14
          GOTO      SETACC90 IF NOT EQUAL
          BUMP      WRKKEY14,-1
          MOVEFPTR  WRKKEY14,CCITEM          * another handly form field
          RESET     WRKKEY14                 * reset the form pointer
          SETLPTR   WRKKEY14,CCITEM          * set logical length to end of name
          APPEND    WRKKEY14,FMTACCT
          APPEND    "a",FMTACCT
          SETLPTR   WRKKEY14,14              * reset logical length
          ADD       TWO,CCITEM               * position of 1st blank
          RESET     WRKKEY14,CCITEM          * reset to this point
          PACK      KEY15,WRKKEY14,SP70      * reset form pointer
          MOVE      KEY15,WRKKEY14           * reset form pointer
          GOTO      SETACC10
.
SETACC90  APPEND    WRKKEY14,FMTACCT
          RESET     FMTACCT
          STRIP     FMTACCT
          RETURN
.------------------------------------------------------------
. Create Links to Next and Last Period
.------------------------------------------------------------
NXTLST00  MOVE      PERIODNO,F2
          MOVE      CURYEAR,F4
          SUB       ONE,F2
          IF        F2=0
            MOVE      "12",F2
            SUB       ONE,F4
          ENDIF
          MOVE      F2,LASTPERD
          REP       " 0",LASTPERD
          MOVE      F4,LASTYEAR
          REP       " 0",LASTYEAR
.
          MOVE      PERIODNO,F2
          MOVE      CURYEAR,F4
          ADD       ONE,F2
          IF        F2=13
            MOVE      "1",F2
            ADD       ONE,F4
          ENDIF
          MOVE      F2,NEXTPERD
          REP       " 0",NEXTPERD
          MOVE      F4,NEXTYEAR
          REP       " 0",NEXTYEAR
.
NXTLST99  RETURN
.------------------------------------------------------------
. Get Account Details Pass KEY14 Ledger,Account
.------------------------------------------------------------
GETACC00  MOVE      SP70,ACCOUNTN
          MOVE      SP70,FMAMDESC
          CALL      RDFMAM1
          MOVE      FMAMACCT,SAVEACCT
          MOVE      FMAMDESC,ACCOUNTN
          COMPARE   ZERO,FMWADTY
          GOTO      GETACC90 IF EQUAL
          COMPARE   "3",FMWADTY
          GOTO      GETACC90 IF EQUAL
          IF        FMWADTY=1|FMWADTY=4
            MOVE      FMSUUD4A,F2
            ADD       FMSUUD2A,F2
            ADD       ONE,F2
            RESET     FMAMACCT,F2
            PACK      KEY14,FMAMLEDG,FMAMACCT
            CALL      RDFMSA1
            IF        OVRCD=0
              MOVE      FMSADESC,ACCOUNTN
            ENDIF
            GOTO      GETACC90
          ENDIF
.
          IF        FMWADTY=2|FMWADTY=5
            RESET     FMAMACCT
            STORE     FMAMACCT,FMSUUD4A,KEY1,KEY2,KEY3,KEY4,KEY5,KEY6:
                                        KEY7,KEY8,KEY9,KEY10,KEY11,KEY12
            LOAD      KEY12,FMSUUD4A,KEY1,KEY2,KEY3,KEY4,KEY5,KEY6:
                                     KEY7,KEY8,KEY9,KEY10,KEY11,KEY12
            PACK      KEY14,FMAMLEDG,KEY12,SP70
            CALL      RDFMCC1
            IF        OVRCD=0
              MOVE      FMCCDESC,ACCOUNTN
            ENDIF
          ENDIF
.
GETACC90  CLEAR     DISPNAME
          MOVE      ACCOUNTN,NAME35
          CALL      NAMSTR00
          RESET     DISPNAME
          MOVE      DISPNAME,ACCOUNTN
          IF        FMWADTY=3
            PACK      ACCOUNTN,FMAMACCT,DISPNAME,SP70
          ENDIF
          IF        FMWADTY=4
            PACK      ACCOUNTN,FMSASUBJ,DISPNAME,SP70
          ENDIF
          IF        FMWADTY=5
            PACK      ACCOUNTN,FMCCCOCE,DISPNAME,SP70
          ENDIF
          MOVE      SAVEACCT,FMAMACCT
.
GETACC99  RETURN
.------------------------------------------------------------
. Format Amount Strings
.------------------------------------------------------------
FORMAT00  MOVE      EDITSTA0,EDITSTR0
          MOVE      EDITSTA1,EDITSTR1
          MOVE      EDITSTA2,EDITSTR2
          MOVE      EDITSTA3,EDITSTR3
          MOVE      EDITSTA4,EDITSTR4
          MOVE      EDITSTA5,EDITSTR5
          IF        FORMATTY=1
            IF        AMTCOL03<0
              MULT      SEQ,AMTCOL03
            ENDIF
            IF        AMTCOL06<0
              MULT      SEQ,AMTCOL06
            ENDIF
          ENDIF
          COMPARE   TWO,FORMATTY
          CALL      FMTBRK00 IF EQUAL
.
          BRANCH    EDITTYPE,FORMAT10,FORMAT20,FORMAT30,FORMAT40,FORMAT50
.
          MOVE      EDITSTR0,FMTCOL01
          MOVE      AMTCOL01,AMT12P0
          FEDIT     AMT12P0,FMTCOL01
.
          MOVE      EDITSTR0,FMTCOL02
          MOVE      AMTCOL02,AMT12P0
          FEDIT     AMT12P0,FMTCOL02
.
          MOVE      EDITSTR0,FMTCOL03
          MOVE      AMTCOL03,AMT12P0
          FEDIT     AMT12P0,FMTCOL03
.
          MOVE      EDITSTR0,FMTCOL04
          MOVE      AMTCOL04,AMT12P0
          FEDIT     AMT12P0,FMTCOL04
.
          MOVE      EDITSTR0,FMTCOL05
          MOVE      AMTCOL05,AMT12P0
          FEDIT     AMT12P0,FMTCOL05
.
          MOVE      EDITSTR0,FMTCOL06
          MOVE      AMTCOL06,AMT12P0
          FEDIT     AMT12P0,FMTCOL06
.
          MOVE      EDITSTR0,FMTCOL07
          MOVE      AMTCOL07,AMT12P0
          FEDIT     AMT12P0,FMTCOL07
.
          GOTO      FORMAT99
.
FORMAT10  MOVE      EDITSTR1,FMTCOL01
          MOVE      AMTCOL01,AMT12P1
          FEDIT     AMT12P1,FMTCOL01
.
          MOVE      EDITSTR1,FMTCOL02
          MOVE      AMTCOL02,AMT12P1
          FEDIT     AMT12P1,FMTCOL02
.
          MOVE      EDITSTR1,FMTCOL03
          MOVE      AMTCOL03,AMT12P1
          FEDIT     AMT12P1,FMTCOL03
.
          MOVE      EDITSTR1,FMTCOL04
          MOVE      AMTCOL04,AMT12P1
          FEDIT     AMT12P1,FMTCOL04
.
          MOVE      EDITSTR1,FMTCOL05
          MOVE      AMTCOL05,AMT12P1
          FEDIT     AMT12P1,FMTCOL05
.
          MOVE      EDITSTR1,FMTCOL06
          MOVE      AMTCOL06,AMT12P1
          FEDIT     AMT12P1,FMTCOL06
.
          MOVE      EDITSTR1,FMTCOL07
          MOVE      AMTCOL07,AMT12P1
          FEDIT     AMT12P1,FMTCOL07
          GOTO      FORMAT99
.
FORMAT20  MOVE      EDITSTR2,FMTCOL01
          MOVE      AMTCOL01,AMT12P2
          FEDIT     AMT12P2,FMTCOL01
.
          MOVE      EDITSTR2,FMTCOL02
          MOVE      AMTCOL02,AMT12P2
          FEDIT     AMT12P2,FMTCOL02
.
          MOVE      EDITSTR2,FMTCOL03
          MOVE      AMTCOL03,AMT12P2
          FEDIT     AMT12P2,FMTCOL03
.
          MOVE      EDITSTR2,FMTCOL04
          MOVE      AMTCOL04,AMT12P2
          FEDIT     AMT12P2,FMTCOL04
.
          MOVE      EDITSTR2,FMTCOL05
          MOVE      AMTCOL05,AMT12P2
          FEDIT     AMT12P2,FMTCOL05
.
          MOVE      EDITSTR2,FMTCOL06
          MOVE      AMTCOL06,AMT12P2
          FEDIT     AMT12P2,FMTCOL06
.
          MOVE      EDITSTR2,FMTCOL07
          MOVE      AMTCOL07,AMT12P2
          FEDIT     AMT12P2,FMTCOL07
          GOTO      FORMAT99
.
FORMAT30  MOVE      EDITSTR3,FMTCOL01
          MOVE      AMTCOL01,AMT12P3
          FEDIT     AMT12P3,FMTCOL01
.
          MOVE      EDITSTR3,FMTCOL02
          MOVE      AMTCOL02,AMT12P3
          FEDIT     AMT12P3,FMTCOL02
.
          MOVE      EDITSTR3,FMTCOL03
          MOVE      AMTCOL03,AMT12P3
          FEDIT     AMT12P3,FMTCOL03
.
          MOVE      EDITSTR3,FMTCOL04
          MOVE      AMTCOL04,AMT12P3
          FEDIT     AMT12P3,FMTCOL04
.
          MOVE      EDITSTR3,FMTCOL05
          MOVE      AMTCOL05,AMT12P3
          FEDIT     AMT12P3,FMTCOL05
.
          MOVE      EDITSTR3,FMTCOL06
          MOVE      AMTCOL06,AMT12P3
          FEDIT     AMT12P3,FMTCOL06
.
          MOVE      EDITSTR3,FMTCOL07
          MOVE      AMTCOL07,AMT12P3
          FEDIT     AMT12P3,FMTCOL07
          GOTO      FORMAT99
.
FORMAT40  MOVE      EDITSTR4,FMTCOL01
          MOVE      AMTCOL01,AMT12P4
          FEDIT     AMT12P4,FMTCOL01
.
          MOVE      EDITSTR4,FMTCOL02
          MOVE      AMTCOL02,AMT12P4
          FEDIT     AMT12P4,FMTCOL02
.
          MOVE      EDITSTR4,FMTCOL03
          MOVE      AMTCOL03,AMT12P4
          FEDIT     AMT12P4,FMTCOL03
.
          MOVE      EDITSTR4,FMTCOL04
          MOVE      AMTCOL04,AMT12P4
          FEDIT     AMT12P4,FMTCOL04
.
          MOVE      EDITSTR4,FMTCOL05
          MOVE      AMTCOL05,AMT12P4
          FEDIT     AMT12P4,FMTCOL05
.
          MOVE      EDITSTR4,FMTCOL06
          MOVE      AMTCOL06,AMT12P4
          FEDIT     AMT12P4,FMTCOL06
.
          MOVE      EDITSTR4,FMTCOL07
          MOVE      AMTCOL07,AMT12P4
          FEDIT     AMT12P4,FMTCOL07
          GOTO      FORMAT99
.
FORMAT50  MOVE      EDITSTR5,FMTCOL01
          MOVE      AMTCOL01,AMT12P5
          FEDIT     AMT12P5,FMTCOL01
.
          MOVE      EDITSTR5,FMTCOL02
          MOVE      AMTCOL02,AMT12P5
          FEDIT     AMT12P5,FMTCOL02
.
          MOVE      EDITSTR5,FMTCOL03
          MOVE      AMTCOL03,AMT12P5
          FEDIT     AMT12P5,FMTCOL03
.
          MOVE      EDITSTR5,FMTCOL04
          MOVE      AMTCOL04,AMT12P5
          FEDIT     AMT12P5,FMTCOL04
.
          MOVE      EDITSTR5,FMTCOL05
          MOVE      AMTCOL05,AMT12P5
          FEDIT     AMT12P5,FMTCOL05
.
          MOVE      EDITSTR5,FMTCOL06
          MOVE      AMTCOL06,AMT12P5
          FEDIT     AMT12P5,FMTCOL06
.
          MOVE      EDITSTR5,FMTCOL07
          MOVE      AMTCOL07,AMT12P5
          FEDIT     AMT12P5,FMTCOL07
          GOTO      FORMAT99
.
FORMAT99  RETURN
.----------------------------------------------------------------------
. Routine to format Red Variances as Negative and Green as Positive
.----------------------------------------------------------------------
FMTBRK00  MATCH     BGGREEN,CURBGC
          GOTO      FMTBRK10 IF NOT EQUAL
          IF        AMTCOL03<0
            MULT      SEQ,AMTCOL03
          ENDIF
          GOTO      FMTBRK20
.
FMTBRK10  IF        AMTCOL03>0
            MULT      SEQ,AMTCOL03
          ENDIF
.
FMTBRK20  MATCH     BGGREEN,YTDBGC
          GOTO      FMTBRK30 IF NOT EQUAL
          IF        AMTCOL06<0
            MULT      SEQ,AMTCOL06
          ENDIF
          GOTO      FMTBRK99
.
FMTBRK30  IF        AMTCOL06>0
            MULT      SEQ,AMTCOL06
          ENDIF
.
FMTBRK99  RETURN
.
          INC       FMSAMAIO/INC       * account
          INC       FMSBUDIO/INC       * budget data
          INC       FMSCONIO/INC       * controlf
          INC       FMSFPSIO/INC       * period summary file
          INC       FMSLMAIO/INC       * ledger
          INC       FMSLMCIO/INC       * ledger financial calendar
          INC       FMSSBUIO/INC       * stat budget
          INC       FMSSMAIO/INC       * stat summary
          INC       FMSUWPIO/INC       * wp file
          INC       FMSCODIO/INC       * codes file
          INC       FMSLMBIO/INC       * report
          INC       FMSCCAIO/INC       * cost centre
          INC       FMSRSFIO/INC       * subjective
          INC       FMSSBAIO/INC       * subjective
          INC       FMSWASIO/INC       * enquiry security
          INC       FMSWBCIO/INC       * enquiry security comments
          INC       FMSWCFIO/INC       * Variance Flag
.
          INC       FMSWEBCD
          INC       NAMSTRCD
          INC       IBAWEBCD    
.
.------------------------------------------------------------
. Create Select Period Options List
.------------------------------------------------------------
SELPER00  MOVE      "12",YEAREND
          ADD       FMLAPERS,YEAREND
          PACK      KEY6,FMLALEDG,CURYEAR
          CALL      RDFMLC1
          CALL      RPFMLC1
          BRANCH    OVRCD,SELPER10
.
          MOVE      ZERO,SELPERD
          CALL      ADDPER00      * Last Year
.
SELPER10  CALL      RKFMLC1
          BRANCH    OVRCD,SELPER99
.
          MOVE      PERIODNO,SELPERD
          CALL      ADDPER00     * Current Year
          
          CALL      RKFMLC1
          BRANCH    OVRCD,SELPER99
.
          MOVE      ZERO,SELPERD
          CALL      ADDPER00     * Next Year
.
          CALL      RPFMLC1
.
SELPER99  RETURN
.------------------------------------------------------------
. Add all Periods for a Year
.------------------------------------------------------------
ADDPER00  MOVE      ONE,F2
ADDPER10  LOAD      SELNAME,F2,FMLC01DE,FMLC02DE,FMLC03DE,FMLC04DE,FMLC05DE:
                               FMLC06DE,FMLC07DE,FMLC08DE,FMLC09DE,FMLC10DE:
                               FMLC11DE,FMLC12DE
          LOAD      KEY8,F2,FMLC01TO,FMLC02TO,FMLC03TO,FMLC04TO:
                                  FMLC05TO,FMLC06TO,FMLC07TO,FMLC08TO:
                                  FMLC09TO,FMLC10TO,FMLC11TO,FMLC12TO,FMLC13TO
          MOVE      KEY8,SELYEAR 
          PACK      KEY6,F2,FMLCYEAR
          REP       " 0",KEY6
          IF        F2=SELPERD
            WRITE     HTMLFILE;"<option value=#"",KEY6,"#" selected>":
                               SELNAME,SP1,SELYEAR,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=#"",KEY6,"#">":
                               SELNAME,SP1,SELYEAR,"</option>"
          ENDIF
          ADD       ONE,F2
          IF        F2<=YEAREND
            GOTO      ADDPER10
          ENDIF
ADDPER99  RETURN
.
