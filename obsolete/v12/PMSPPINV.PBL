.----------------------------------------------------------------------
.    File          : PMSPPINV.PBL
.    
.    Function      : Include to Private Practice Invoices for WEB
.
.    Modifications : 
.                    V11.04.01 11/04/2024  J.Tan         TSK 0941662
.                    Mod PRDTGSTM - GST of Total amount(Item amount x Quantity)
.                    V10.14.01 20/03/2019  J.Tan            TSK 0857392
.                    Changed RCP Transaction count from DIM3 to DIM5
.                    V10.13.01 26/07/2018  J.Tan            TSK 0848506
.                    Changed PP Doctor from DIM6 to DIM10
.                    V10.11.01 30/08/2017 Peter Vela TSK 0387619
.                    Added validation for IMC Details PPRINV50
.                    V9.03.02 03/02/2004 J.Tan   CAR 45047
.                    Mods to display Non-banked Cash total
.
.----------------------------------------------------------------------
PPRINV00  MOVE      URNUMBER,VDEBTOR
          MOVE      ONE,VSCANPMI
          BRANCH    F1,PPRINV40
.
          MOVE      SP30,DIM27
          MOVE      SP3,PRHRBULK
          PACK      KEY27,PRINUNIQ,PRINPRAC,PRINDOCT,SP30
          CALL      RSPRHR1
          CALL      RKPRHR1
          BRANCH    OVRCD,PPRINV10
.
          PACK      DIM27,PRHRUNIQ,PRHRPRAC,PRHRDOCT,SP30
          MATCH     KEY27,DIM27          
          IF        !@EQUAL
            MOVE      SP3,PRHRBULK
            GOTO      PPRINV10
          ENDIF
.
          CALL      CBUL0000
.
PPRINV10  MOVE      ZERO,EXIT
          MOVE      SP2,DIM2
          PACK      KEY24,VDEBTOR,VSCANPMI,PRINNUMB,SP10
          CALL      RSPRDT3                      * position in file
          CALL      RKPRDT3                      * read next record
          BRANCH    OVRCD,PPRINV20               * end of file
.
          MATCH     VDEBTOR,PRDTDEBT             * same debtor ?
          GOTO      PPRINV20 IF NOT EQUAL        * no
.
          COMPARE   VSCANPMI,PRDTSCAN            * same scan flag ?
          GOTO      PPRINV20 IF NOT EQUAL        * no
.
          MATCH     PRDTINVN,PRINNUMB            * same invoice number ?
          GOTO      PPRINV20 IF NOT EQUAL        * no
.
          PACK      KEY5,CODEGI,PRDTCODE
          CALL      RDCODE1
          BRANCH    OVRCD,PPRINV20
.
          MATCH     ANSI,TCINDC1
          IF        @EQUAL
             MOVE      "IP",DIM2
          ELSE
             MOVE      "OP",DIM2
          ENDIF
.
PPRINV20  MOVE      "Unknown code",TDESC         * get claim code description
          PACK      KEY5,CODECL,PRINCLAM
          CALL      RDCODE1
.
          MOVE      "Unknown practice",PRPRDESC  * get medical practice name
          PACK      KEY6,PRINPRAC
          CALL      RDPRPR1
.
          MOVE      "Unknown doctor",DOCNAME     * get serv. doctor name
          PACK      KEY10,PRINDOCT,SP10
          CALL      RDPMHCP1
          BRANCH    OVRCD,PPRINV30               * not on file
.
          MOVE      PMHCTITL,PACTITLE               * format doctor name
          MOVE      PMHCGNAM,PACGNAME
          MOVE      PMHCSNAM,PACSNAME
          MOVE      ONE,PACFRMT
.
          CALL      PACNAME
          MOVE      PACFNAME,DOCNAME
PPRINV30  UNPACK    PRINDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      ZERO,RECDISP                 * set rec/journ display flag
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
          WRITE     HTMLFILE;"<tr><td width=18%>":
                               "Person Responsible for Account</td>":
                               "<td>&nbsp;</td>":
                               "<td>Claim Type</td>":
                               "<td><b>",TDESC,"( ",DIM2,")</b></td></tr>":
.
                               "<tr><td>Name</td>":
                               "<td><b>",PRINNAMR,"</b></td>":
                               "<td>Medical Practice</td>":
                               "<td><b>",PRPRDESC,"</b></td></tr>":
.
                               "<tr><td>Address</td>":
                               "<td><b>&nbsp;",PRINPRA1,"</b>":
                               "<td>Service Doctor</td>":
                               "<td><b>&nbsp;",DOCNAME,"</b></td></tr>":
.
                               "<tr><td>&nbsp;</td>":
                               "<td><b>&nbsp;",PRINPRA2,"</b>":
                               "<br><b>&nbsp;",PRINPRA3,"</b>":
                               "<br><b>&nbsp;",PRINPOST,"</b></td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td></tr>"
          GOTO      PPRINV99
.
.         Display all the service items first, then the receipts and journals
.
PPRINV40 WRITE     HTMLFILE;"<tr>":
                             "<td class=HeadingCell>Date</td>":
                             "<td class=HeadingCell>Item</td>":
                             "<td class=HeadingCell>Description</br>":
                             "Number of Services</td>":
                             "<td align=right class=HeadingCell>GST Exc.</td>":
                            "<td align=right class=HeadingCell>GST Amount</td>":
                            "<td align=right class=HeadingCell>Amount</td>":
                            "</tr>"
.
. ------- Get the service item transactions for this invoice ---------------
.
          MOVE      ZERO,TOTINV                  * initialise totals
          MOVE      ZERO,TOTGST
          MOVE      ZERO,TOTPAID
          MOVE      ZERO,TOTOUT
.
          PACK      KEY24,VDEBTOR,VSCANPMI,PRINNUMB,SP10
          CALL      RSPRDT3                      * position in file
PPRINV50  CALL      RKPRDT3                      * read next record
          BRANCH    OVRCD,PPRINV70               * end of file
.
          MATCH     VDEBTOR,PRDTDEBT             * same debtor ?
          GOTO      PPRINV70 IF NOT EQUAL        * no
.
          COMPARE   VSCANPMI,PRDTSCAN            * same scan flag ?
          GOTO      PPRINV70 IF NOT EQUAL        * no
.
          MATCH     PRDTINVN,PRINNUMB            * same invoice number ?
          GOTO      PPRINV70 IF NOT EQUAL        * no
.
          COMPARE   ONE,PRDTRTYP                 * item entry ?
          GOTO      PPRINV50 IF NOT EQUAL        * no
.
          COMPARE   TWO,PRDTIFLG
          GOTO      PPRINV50 IF EQUAL
.
.         Valid service item transaction found, so display it
.
          UNPACK    PRDTSDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
          WRITE     HTMLFILE;"<tr>":
                             "<td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>";
.
          BRANCH    PRDTIFLG,PPRINV60            * AMA item
.
          MOVE      "-",ANS
          PACK      KEY11,ANSM,ANS,PRDTITMN
          WRITE     HTMLFILE;"<td>&nbsp;",KEY11,"</td>";
.
          MATCH     SP3,PRDTSUBN                 * subitem number ?
          GOTO      PPRINV64 IF EQUAL            * no
.
          MOVE      "(",ANS
          MOVE      ")",KEY1
          PACK      KEY5,ANS,PRDTSUBN,KEY1
          WRITE     HTMLFILE;"<td><b>&nbsp;",KEY5,"</b></td>";
          GOTO      PPRINV64
.
PPRINV60  MOVE      "-",ANS
          PACK      KEY11,ANSA,ANS,PRDTITMN
          WRITE     HTMLFILE;"<td>&nbsp;",KEY11,"</td>";
.
PPRINV64  MOVE      PRDTDESC,SHDESC
          WRITE     HTMLFILE;"<td>&nbsp;",SHDESC;
.
          WRITE     HTMLFILE;"<br>",PRDTSERV,"x",PRDTIAMT,"</td>";
.
          MOVE      ZERO,FORM8P2
          COMPARE   PRDTAMNT,ZERO      
          IF        !@EQUAL
            MOVE      PRDTGSTM,FORM8P2
            ADD       FORM8P2,TOTGST
            WRITE     HTMLFILE;"<td align=right>",PRDTAMNT,"</td>":
                               "<td align=right>",FORM8P2,"</td>";
            ADD       PRDTAMNT,FORM8P2
            WRITE     HTMLFILE;"<td align=right>",FORM8P2,"</td></tr>"
          ELSE
            WRITE     HTMLFILE;"<td align=right>",PRDTAMNT,"</td>":
                               "<td align=right>",FORM8P2,"</td>":
                               "<td align=right>",FORM8P2,"</td></tr>"
          ENDIF
.
          ADD       PRDTAMNT,TOTINV              * update running total
          GOTO      PPRINV50                     * get next transaction
.
. ------- No more service items to display so display the total ----------- 
.  
PPRINV70  MOVE      TOTINV,FORM8P2
          ADD       TOTGST,FORM8P2
          WRITE     HTMLFILE;"<tr><td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td><b>Invoice Total</b></td>":
                             "<td align=right><b>",TOTINV,"</b></td>":
                             "<td align=right><b>",TOTGST,"</b></td>":
                             "<td align=right><b>",FORM8P2,"</b></td>":
                             "</tr>"
          CALL      PRJR0000                     * Print Receipt/Journal
.
.         End of transactions display, so display totals
.
          COMPARE   ZERO,RECDISP                 * any recpts/journ. displayed ?
          GOTO      PPRINV80 IF EQUAL            * no
.
          WRITE     HTMLFILE;"<tr><td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td><b>Payment Total</b></td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td align=right><b>",TOTPAID,"</b></td>":
                             "</tr>"
.
PPRINV80  MOVE      TOTINV,TOTOUT                * display outstanding debt
          ADD       TOTGST,TOTOUT
          ADD       TOTPAID,TOTOUT
.
          WRITE     HTMLFILE;"<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td><b>Outstanding</b></td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>";
.
          IF        TOTOUT<0
            WRITE     HTMLFILE;"<td align=right><font color=#FF0000><b>":
                               TOTOUT,"</font></b></td>":
                               "</tr>"
          ELSE
            WRITE     HTMLFILE;"<td align=right><font color=#0000FF><b>":
                               TOTOUT,"</font></b></td>":
                               "</tr>"
          ENDIF
.
.         Check if this invoice is for a compensable claim.  If so,
.         see if the user wishes to view the claim details
.
          PACK      KEY5,CODECL,PRINCLAM
          CALL      RDCODE1                      * read claim code
          BRANCH    OVRCD,PPRINV99               * not on file
.         
          MATCH     SP1,TCINDC1                  * compensation code ?
          GOTO      PPRINV99 IF EQUAL            * no
.
          REP       COMCHAR,TCINDC1
          MOVE      TCINDC1,COMTYPE
.
.         CALL      CLAM0000
.
PPRINV99  RETURN
+
.****************************************************************************
.*                                 PRJR0000                                 *
.*            display header line for receipts/journals     DINV0000        *
.****************************************************************************
PRJR0000  MOVE      ZERO,JOURFLAG
. ------- Now get any receipt/journal transactions and display them --------
.
PRJR1000  PACK      KEY24,VDEBTOR,VSCANPMI,PRINNUMB,SP10
          CALL      RSPRDT3                      * position in file
PRJR3500  CALL      RKPRDT3                      * read next record
          BRANCH    OVRCD,PRJR8000               * end of file
.
          MATCH     VDEBTOR,PRDTDEBT             * same debtor ?
          GOTO      PRJR8000 IF NOT EQUAL        * no
.
          COMPARE   VSCANPMI,PRDTSCAN            * same scan flag ?
          GOTO      PRJR8000 IF NOT EQUAL        * no
.
          MATCH     PRDTINVN,PRINNUMB            * same invoice number ?
          GOTO      PRJR8000 IF NOT EQUAL        * no
.
          COMPARE   ONE,PRDTRTYP                 * receipt/journal entry ?
          GOTO      PRJR3500 IF EQUAL            * no
.
          COMPARE   TWO,IBCNUGST
          GOTO      PRJR3600 IF NOT EQUAL        * not using Aust.GST
.
.         For Aust.GST - display the Journal before Cash
.
          IF        JOURFLAG=0
            COMPARE   THREE,PRDTRTYP             * check for journal record
            GOTO      PRJR3500 IF NOT EQUAL
          ELSE
            COMPARE   TWO,PRDTRTYP               * check for cash record
            GOTO      PRJR3500 IF NOT EQUAL
          ENDIF
.
.         Valid receipt/journal transaction found
.
PRJR3600  BRANCH    RECDISP,PRJR4000             * receipt(s) found
.
          IF        IBCNUGST=2 & JOURFLAG=0
            MOVE      ONE,RECDISP                * set journal found flag
            GOTO      PRJR4000
          ELSE 
            CALL      THED0000                   * display receipt header line
          ENDIF
.
.                                                * format transaction date
PRJR4000  UNPACK    PRDTSDAT,CCENT,CYEAR,CMON,CDAY 
          CALL      PACDATE
.
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
          WRITE     HTMLFILE;"<tr>":
                             "<td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                             "<td>&nbsp;</td>":
                             "<td>",PRDTDESC,"</td>";
.
          COMPARE    THREE,PRDTRTYP              * journal ?
          GOTO       PRJR5000 IF EQUAL           * yes
.
.         Cash
.
          PACK      HOWPAID,SP10,SP3             * receipt
          LOAD      HOWPAID,PRDTMETH,RCASH,RCHEQUE,RCCARD,RMCARD,RMORDER
          WRITE     HTMLFILE;"<td align=right>",PRDTRECN,"</td>":
                             "<td>&nbsp;</td>";
          ADD       PRDTGSTM,PRDTAMNT
          GOTO      PRJR6200
.
.         Journals
.
PRJR5000  MOVE      ZERO,FORM8P2
          MOVE      ZERO,FORM82
          IF        PRDTAMNT=0
            MOVE      PRDTGSTM,FORM8P2              * GST Journal
          ELSE
            MOVE      PRDTAMNT,FORM82               * Normal Journal
          ENDIF
          MOVE      "Journal      ",HOWPAID 
          WRITE     HTMLFILE;"<td align=right>",FORM82,"</td>":
                             "<td align=right>",FORM8P2,"</td>";
          ADD       PRDTGSTM,PRDTAMNT
.
PRJR6200  WRITE     HTMLFILE;"<td align=right>",PRDTAMNT,"</td></tr>"
          ADD       PRDTAMNT,TOTPAID
          GOTO      PRJR3500                     * get next transaction
.
PRJR8000  COMPARE   TWO,IBCNUGST
          GOTO      PRJR9000 IF NOT EQUAL
.
          BRANCH    JOURFLAG,PRJR9000             * done Journal record, exit.
          MOVE      ONE,JOURFLAG
          GOTO      PRJR1000
.
PRJR9000  PACK      KEY12,SP4,PRINNUMB,SP70 * set up invoice number
          CALL      CHKP0000             * Get cash pending for the invoice
PRJR9999  RETURN
+
.****************************************************************************
.*                                 THED0000      Called by: FULL0000        *
.*            display header line for receipts/journals     DINV0000        *
.****************************************************************************
.
THED0000  WRITE     HTMLFILE;"<tr>":
                         "<td class=HeadingCell>Date</td>":
                         "<td class=HeadingCell>&nbsp;</td>":
                         "<td class=HeadingCell>Description</td>":
                         "<td align=right class=HeadingCell>Receipt No.</td>":
                         "<td align=right class=HeadingCell>&nbsp;</td>":
                         "<td align=right class=HeadingCell>Amount</td>":
                         "</tr>"
.
THED4000  MOVE      ONE,RECDISP                  * set receipt found flag
.
THED9999  RETURN
+
.****************************************************************************
.*                              CBUL0000                                    *
.*                       Check if bulk billing                              *
.****************************************************************************
.
CBUL0000  MOVE      ZERO,EXIT
          MOVE      SP30,BBDESC
          PACK      PRHRBULK,PRHRBULK,SP10
          MATCH     SP3,PRHRBULK
          GOTO      CBUL9000 IF EQUAL
.
          PACK      KEY5,CATGB,PRHRBULK
          CALL      RDCODE1
          BRANCH    OVRCD,CBUL9000
.
          MOVE      ONE,FORM1
.
          WHILE     FORM1 < SIX
             LOAD   ANS,FORM1,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
.
             TYPE   ANS                     * see if we have a number
             GOTO   CBUL1000 IF EQUAL
.
             REP    "C1W2V3H4S5M6",ANS      
.
             TYPE   ANS                     * see if we have a number
             GOTO   CBUL1000 IF NOT EQUAL
.
             MOVE   THCSCOD,ANS
.
             TYPE   ANS
             GOTO   CBUL9000 IF EQUAL
.
             REP    "H1I2G3",ANS
.
             TYPE    ANS
             GOTO    CBUL9000 IF NOT EQUAL
.
             MOVE    ZERO,EXIT 
             MOVE    TDESC,BBDESC
.
             GOTO   CBUL9999
.
.------ increment the counter and continue loop ------
.
CBUL1000     ADD    ONE,FORM1
.
          DO
.
CBUL9000  MOVE      ONE,EXIT      
.
CBUL9999  RETURN
+
.*******************************************************************************
.         Display Cash pending total
.         FORM=0 - Generate only, 1 - Print, 2 - Display
.*******************************************************************************
CHKP0000  MOVE      ZERO,CASHTOT
          MOVE      KEY12,SAVKEY12     * saved variable for invoice number
          MOVE      SP70,KEY12A        * saved variable for receipt number
          OPEN      RCPDTEA3,"rcpdteaf"
          OPEN      RCPBNKA1,"rcpbnkaf"
          PACK      KEY29,SAVKEY12,SP70
          CALL      RSRCDTE3
CHKP1000  CALL      RKRCDTE3
          BRANCH    OVRCD,CHKP3000
.
          MATCH     SAVKEY12,RCDTINVN       * Same invoice number?
          GOTO      CHKP3000 IF NOT EQUAL
.
          MATCH     KEY12A,RCDTRECN   * check receipt number
          GOTO      CHKP1000 IF EQUAL
.
          MOVE      RCDTRECN,KEY12A   * save receipt number
          PACK      KEY12,RCDTRECN,SP70
          CALL      RDRCBNK1
          BRANCH    OVRCD,CHKP1000
.
          MATCH     "0",RCBNSTAT        * Cash Not banked?
          GOTO      CHKP1000 IF NOT EQUAL
.
          MULT      "-1",RCBNTOTP
          ADD       RCBNTOTP,CASHTOT
          GOTO      CHKP1000
.
CHKP3000  COMPARE   ZERO,CASHTOT
          GOTO      CHKP9999 IF EQUAL        * No cash pending
.
          WRITE     HTMLFILE;"<tr><td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td><b>Non-banked Cash Total</b></td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td align=right><b>",CASHTOT,"</b></td></tr>"
          ADD       CASHTOT,TOTPAID
          GOTO      CHKP9999
.
CHKP9999  RETURN
+
