.******************************************************************************
.* System         : Accident and Emergency                                    *
.*                                                                            *
.* Include Name   : AAEEPEXT.PBL                                              *
.*                                                                            *
.* Function       : Keyin the related New visit for any follow up visit       *
.*                                                                            *
.* Author         : Justin Coates      17/09/1993                             *
.*                                                                            *
.* Parameters     : CKYIDEF3      previous A&E (adaprev)    (Cat EP)          *
.*                  PURNO         patients UR number                          *
.*                                                                            *
.* Returns        : CKYIDEF8      related new a&E visit                       *
.*                  CKYIDEF3      related new a&E episode number              *
.*                                                                            *
.* Modifications  :                                                           *
.*                  08/11/1993    LOFTY                                       *
.*                  changed so first three checks will exit properly          *
.******************************************************************************
          DEFPROC   AAEEPEXT
.
          INC       AAEDE1FD/INC
.
CALLPOSN  FORM      1
EPARRY    DIM       16[10]
EPCNT     FORM      2
NODATA    FORM      1
.
CLRLEF    FORM      "32"
CLRTOP    FORM      "14"
CLRRIG    FORM      "79"
CLRBOT    FORM      "21"
.
AEXEP000  MOVE      ZERO,EPCNT
          MATCH     SP3,CKYIDEF3
          GOTO      AEXEP910 IF EQUAL       * nothing entered
.
          PACK      KEY5,ANSE,ANSP,CKYIDEF3
          CALL      RDCODE1
          BRANCH    OVRCD,AEXEP910          * invalid code
.
.         see if any indicators set
.
          MATCH     "2",TCINDC1
          GOTO      AEXEP910 IF NOT EQUAL   * not a follow up attendance
.
.         display all the new attendances for the patient
.
          OPEN      AAEDE1A2,"aaede1af"
          MOVE      ONE,NODATA              * set to display
          MOVE      ZERO,EPCNT              * clear counter
          MOVE      ONE,CPAGENO             * set page
          PACK      KEY16,PURNO,SP8
          CALL      RDSDETA2
.
AEXEP100  CALL      RDKDETA2
          BRANCH    OVRCD,AEXEP500          * end of file
          MATCH     PURNO,ADAURNO
          GOTO      AEXEP500 IF NOT EQUAL   * different patient
.
          CALL      AEXVL000                * validate the visit as a NEW
          BRANCH    EXIT,AEXEP100           * not a new visit
.
AEXEP200  ADD       ONE,CVERT
          COMPARE   "23",CVERT
          GOTO      AEXEP400 IF NOT LESS    * need new page
          ADD       ONE,EPCNT               * add to item counter
.
. ******* display data and store key *******
.
AEXEP300  IF        NODATA = ONE
            MOVE      ZERO,NODATA
            MOVE      "31",HLEF
            MOVE      "10",HTOP
            MOVE      "79",HRIG
            MOVE      "24",HBOT
            CALL      GETSCR00
            MOVE      "PREVIOUS NEW ATTENDANCES",BOXHDR
            CALL      COMDRBOX
            DISPLAY   *P32:11,*V2LON,*ULON,"Item":
                      *P37:11,"   Date   ":
                      *P49:11,"Diagnosis                     "
            MOVE      TEN2,CVERT              * set row number
          ENDIF
          UNPACK    ADADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      ADADIAG,KEY30
          DISPLAY   *P33:CVERT,*V2LON,EPCNT,*HOFF,". ",CPCDATE,SP2,KEY30
          PACK      EPARRY[EPCNT],ADAURNO,ADANUMB
          GOTO      AEXEP100
.
. ******* new screen needed *******
.
AEXEP400  BRANCH    CPAGENO,AEXEP450
.
.         Middle pages : (N)ext & (P)revious
.
AEXEP410  MOVE      ONE,CALLPOSN            * set position
          CALL      KEYA0000
          BRANCH    EXIT,AEXEP700,AEXEP800,AEXEP910
.                        Next     Previous Exit
          GOTO      AEXEP600
.
.         first page : (N)ext
.
AEXEP450  MOVE      TWO,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,AEXEP700,AEXEP590,AEXEP910
.                        Next     Previous Exit
          GOTO      AEXEP600
.
. ******* no more data to display *******
.
AEXEP500  COMPARE   ZERO,EPCNT  
          GOTO      AEXEP910 IF EQUAL       * nothing on file
.
          BRANCH    CPAGENO,AEXEP550
.
.         last page : (P)revious
.
AEXEP510  MOVE      THREE,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,AEXEP590,AEXEP800,AEXEP910
.                        Next     Previous Exit
          GOTO      AEXEP600
.
.         first page :
.
AEXEP550  MOVE      FOUR,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,AEXEP590,AEXEP590,AEXEP910
.                        Next     Previous Exit
          GOTO      AEXEP600
.
AEXEP590  BEEP   
          BRANCH    CALLPOSN,AEXEP410,AEXEP450,AEXEP510,AEXEP550
.
. ******* item on screen selected *******
.
AEXEP600  MOVE      EPARRY[FORM2],KEY16
          CALL      RDDETA2
          BRANCH    OVRCD,AEXEP590
.
          MOVE      ADANUMB,CKYIDEF8        * set related event number
          MOVE      AEDANPNA,CKYIDEF3       * set related episode number
          MOVE      ZERO,EXIT
          GOTO      AEXEP995
.
. ******* set up for a new page *******
.
AEXEP700  MOVE      EPARRY[10],KEY16   * read the last item displayed
          CALL      RDDETA2
          ADD       ONE,CPAGENO             * add to page counter
          BOXCLR    CLRLEF,CLRTOP,CLRRIG,CLRBOT
          MOVE      ZERO,EPCNT              * reset item counter
          MOVE      TEN1,CVERT              * set row
          GOTO      AEXEP100
.
. ******* set up for previous screen *******
.
AEXEP800  MOVE      EPARRY[ONE],KEY16
          CALL      RDDETA2
          MOVE      ZERO,FORM2
.
AEXEP810  CALL      RDKDETA2
          BRANCH    OVRCD,AEXEP820          * end of file
          CALL      AEXVL000                * validate record
          BRANCH    EXIT,AEXEP810           * not a new visit
.
          ADD       ONE,FORM2
          COMPARE   TEN,FORM2
          GOTO      AEXEP810 IF LESS        * more reads to do
.
AEXEP820  SUB       ONE,CPAGENO             * sub from page counter
          MOVE      ONE,EPCNT               * reset item counter
          MOVE      TEN2,CVERT              * set row
          GOTO      AEXEP300
.
. ******* set exit flags *******
.
AEXEP910  MOVE      ONE,EXIT                * exit selected
          MOVE      ZEROUR,CKYIDEF8         * set to zero
          MOVE      "  0",CKYIDEF3          * set to zero
          COMPARE   ZERO,EPCNT  
          GOTO      AEXEP999 IF EQUAL       * nothing on file
.
AEXEP995  CALL      PUTSCR00
          GOTO      AEXEP999
+
.*********************************************************************
.*        Keyin response for line 24 question                        *
.*********************************************************************
KEYA0000  DISPLAY   *PCLRLEF:23,SP30,SP10,SP5:
                    *PCLRLEF:23,"Select item, (":
                    *V2LON,ANSE,*HOFF,")xit";
          ASSIGN    (CLRLEF+18),CCOL
          IF        CALLPOSN = ONE | CALLPOSN = TWO
            DISPLAY   ", (",*V2LON,"N",*HOFF,")ext";
            ADD       "8",CCOL
          ENDIF
          IF        CALLPOSN = ONE | CALLPOSN = THREE
            DISPLAY   ", (",*V2LON,"P",*HOFF,")revious";
            ADD       "12",CCOL
          ENDIF
          DISPLAY   " : ";
          ADD       FOUR,CCOL
.
KEYA1000  KEYIN     *PCCOL:23,*V2LON,*JR,DIM2:
                    *PCCOL:23,*DV,DIM2
.
          TYPE      DIM2
          GOTO      KEYA5000 IF EQUAL       * number entered
.
.         letter entered
.
          MATCH     SP1,DIM2
          GOTO      KEYA1500 IF NOT EQUAL   * invalid entry
.
          UNPACK    DIM2,ANS,ANS            * get required letter in ANS
          REP       UPPLOW,ANS              * get as uppercase
          REP       "N1P2E3",ANS            * turn valid letters into number
          MOVE      ZERO,EXIT               * clear exit flag
          MOVE      ANS,EXIT                * set exit flag
          BRANCH    EXIT,KEYA9999,KEYA9999,KEYA9999
.                        Next     Previous Exit
.
KEYA1500  BEEP
          GOTO      KEYA1000
.
.         number entered
.
KEYA5000  MOVE      ZERO,EXIT               * set exit flag
          MOVE      DIM2,FORM2              * get as a number
.
          COMPARE   FORM2,ZERO
          GOTO      KEYA1500 IF NOT LESS    * negative entered
.
          COMPARE   FORM2,EPCNT  
          GOTO      KEYA1500 IF LESS        * number too large
.
KEYA9999  RETURN
+
.*********************************************************************
.         validate that a new attendance
.*********************************************************************
AEXVL000  PACK      KEY5,ANSE,ANSP,ADAPREV
          CALL      RDCODE1
          BRANCH    OVRCD,AEXVL910          * invalid code
.
          MATCH     "1",TCINDC1
          GOTO      AEXVL910 IF NOT EQUAL   * not a NEW attendance
.
          MOVE      ZERO,EXIT               * valid record
          GOTO      AEXVL999
AEXVL910  MOVE      ONE,EXIT                * invalid record
AEXVL999  RETURN
+
          INC       AAEDE1IO/INC
          INC       IBAOVRIO/INC
.
AEXEP999  ENDPROC
+
