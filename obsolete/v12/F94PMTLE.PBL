.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   F94PMTLE                                                  *
.***************************************************************************
.* Date      :   10/06/2005                                                *
.* Author    :   Jill Habasque                                             *
.* Function  :   This program will copy titles from patma1af to pmstleaf   *
.***************************************************************************
.
          INC       STD001FD
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSTLEFD/INC
          INC       PMSRELFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
PRGID     INIT      "F94PMTLE"
PRGNAM    INIT      "Write titles to pmstleaf and relationships to pmsrelaf "
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      PMSTLEA1,"pmstleaf"
          OPEN      PMSRELA1,"pmsrelaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000 CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
.
          PACK      KEY8,SP70
          CALL      RDSMAST1
PROC1000  CALL      RDKMAST1
          BRANCH    OVRCD,PROC9999
          PACK      KEY8,PURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,PROC1000
.
          DISPLAY   *P15:20,PURNO
.
          PACK      KEY4,PTITL,SP70
          CALL      RDPMTLE1
          BRANCH    OVRCD,PROC2000
.
PROC1100  PACK      KEY10,PNKRELP,SP70
          CALL      RDPMREL1
          BRANCH    OVRCD,PROC3000
.
PROC1200  PACK      KEY10,PTMXECRE,SP70
          CALL      RDPMREL1
          BRANCH    OVRCD,PROC4000
.
PROC1300  PACK      KEY10,PTMXNRRE,SP70
          CALL      RDPMREL1
          BRANCH    OVRCD,PROC5000
.
PROC1400  PACK      KEY10,PMPXMREL,SP70
          CALL      RDPMREL1
          BRANCH    OVRCD,PROC6000
.
PROC1500  PACK      KEY10,PMPXFREL,SP70
          CALL      RDPMREL1
          BRANCH    OVRCD,PROC7000
.
PROC1600  GOTO      PROC1000
.
PROC2000  PACK      PMTLTITL,PTITL
          PACK      PMTLDESC,PTITL
          CALL      WRPMTLE1
          GOTO      PROC1100
.
PROC3000  PACK      PMRLRELS,PNKRELP
          PACK      PMRLDESC,PNKRELP
          CALL      WRPMREL1
          GOTO      PROC1200
.
PROC4000  PACK      PMRLRELS,PTMXECRE
          PACK      PMRLDESC,PTMXECRE
          CALL      WRPMREL1
          GOTO      PROC1300
.
PROC5000  PACK      PMRLRELS,PTMXNRRE
          PACK      PMRLDESC,PTMXNRRE
          CALL      WRPMREL1
          GOTO      PROC1400
.
PROC6000  PACK      PMRLRELS,PMPXMREL
          PACK      PMRLDESC,PMPXMREL
          CALL      WRPMREL1
          GOTO      PROC1500
.
PROC7000  PACK      PMRLRELS,PMPXFREL
          PACK      PMRLDESC,PMPXFREL
          CALL      WRPMREL1
          GOTO      PROC1600
.
PROC9999  CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:21,"Finished : ",*V2LON,CTIMEIS,*HOFF
          CALL      HITENTER
          RETURN
. =========================================================================
.         I/O Includes
. =========================================================================
.
	  INC       STD001IO
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC
          INC       PMSTLEIO/INC
          INC       PMSRELIO/INC
