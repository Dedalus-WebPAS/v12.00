.*************************************************************************
.
.       PRIREFDS : Display Referring Doctors on file for a claim
.       Date     : 26/01/1993
.       Author   : Steve Armstrong
.       Mods     :
.         V10.13.01 25/07/2018  J.Tan             TSK 0848506
.                   Extended PRPRPDOC from 6 to 10 chars
.         V9.02.01  02/10/2001  J.Tan
.                   Mods to use PMSHCPFD instead of PATDO1FD
.         V6.00.01  12/08/1993  Glenn Berry      SRF 122974
.                   Mods for changes to PRIHTRFD
.
.       NB.  Currently only called by IBAPRI01 where parameters passed are:
.                 DIM50
.                 UNIQUE
.                 MPRAC
.                 SDOCT
.                 PATIND
.                 Z30
.
.*************************************************************************
.
PRIREFDS  DISPLAY    *P1:4,*EF,*ULON,*V2LON,"Referring doctors for this invoice"
.
          MOVE       FIVE,CVERT
.
          PACK       KEY62,UNIQUE,MPRAC,SDOCT,PATIND,SP30,SP30
          CALL       RSPRHT1                     * position in file
REFDS100  CALL       RKPRHT1                     * read next record
          BRANCH     OVRCD,REFDS999              * end of file
.
          COMPARE    UNIQUE,PRHTUNIQ             * same unique id ?
          GOTO       REFDS999 IF NOT EQUAL       * no
.
          MATCH      MPRAC,PRHTPRAC              * same medical practice ?
          GOTO       REFDS999 IF NOT EQUAL       * no
.
          MATCH      SDOCT,PRHTDOCT              * same service doctor ?
          GOTO       REFDS999 IF NOT EQUAL       * no
.
          MATCH      PATIND,PRHTPIND             * same patient indicator ?
          GOTO       REFDS999 IF NOT EQUAL       * no
.
          ADD        ONE,CVERT                   * increment line count
          COMPARE    TWENTY3,CVERT               * page full ?
          GOTO       REFDS150 IF LESS            * no
          GOTO       REFDS200                    * yes
.
REFDS150  MATCH      SP6,PRHTREFD
          GOTO       REFDS160 IF NOT EQUAL
.
          MOVE       "No referring doctor",DIM50
          GOTO       REFDS170
.
REFDS160  MOVE       "Unknown Doctor",DIM50
          PACK       KEY10,PRHTREFD,SP10
          CALL       RDPMHCP1
          BRANCH     OVRCD,REFDS170
.
          MOVE       PMHCSNAM,PACSNAME
          MOVE       PMHCGNAM,PACGNAME
          MOVE       PMHCTITL,PACTITLE
          MOVE       ONE,PACFRMT
          CALL       PACNAME
          MOVE       PACFNAME,DIM50
.
REFDS170  DISPLAY    *P1:CVERT,*V2LON,PRHTREFD,*HOFF,SP2,DIM50
.
.         Reposition after the last item for the current referring doctor
.
          PACK       KEY62,UNIQUE,MPRAC,SDOCT,PATIND,PRHTREFD,Z30,Z30
          CALL       RSPRHT1
          GOTO       REFDS100                    * get next record
.
REFDS200  KEYIN      *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, ":
                     "(",*V2LON,"F",*HOFF,")irst Screen, ":
                     "(",*V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH     ANSC,ANS                    * continue ?
          GOTO       REFDS999 IF EQUAL
.
          CMATCH     ANSF,ANS                    * first screen ?
          GOTO       PRIREFDS IF EQUAL
.
          CMATCH     ANSN,ANS                    * next screen ?
          GOTO       REFDS400 IF EQUAL
.
          BEEP                                   * invalid option
          GOTO       REFDS200
.
REFDS400  MOVE       SIX,CVERT
          DISPLAY    *P1:CVERT,*EF;
.
          GOTO       REFDS150
.
REFDS999  RETURN
