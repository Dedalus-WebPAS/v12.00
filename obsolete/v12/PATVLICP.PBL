.***************************************************************************
.*     Program Name : VALIDIC                                              *
.*                                                                         *
.*     Author       : Matt Surridge & Ian Rutt                             *
.*                                                                         *
.*     Date         : 9/12/93                                              *
.*                                                                         *
.*     Returns      : EXIT        0 = Valid                                * 
.*                                1 = Invalid i/c number                   * 
.*                                1 = i/c number on file                   * 
.*                                                                         *
.*     Parameters   : KYNNMP - IC Number (Right Justified)                 * 
.*                                                                         *
.*     Modifications:                                                      *
.*                                                                         *
.*        V5.09.B01 05/01/2001  Greg Horvat                                *
.*                  Replaced certain variables with KEY?? variables cause  *
.*                  OUT66 couldnt compile                                  *
.*     V5.01.01     05/01/94 Matt Surridge Mt Alvernia                     *
.*                  Modified to do all checking and to check the           *
.*                  pre-admittance id file for numbers                     *
.***************************************************************************
.
PATVLICP
          MOVE      FALSE,EXIT
.
VALIDIC3  OPEN      PATNIDA2,"patnidaf"
          PACK      KEY30,KYNNMP,CHOSINDX,SP10 
          CALL      RSPTNID2
          CALL      RKPTNID2
          BRANCH    OVRCD,VALIDI3A
          CLOSE     PATNIDA2
.
          MATCH     KYNNMP,PTNINMPI
          GOTO      VALIDIC7 IF EQUAL
.
VALIDI3A  OPEN      PATPNIA2,"patpniaf"
          PACK      KEY30,KYNNMP,CHOSINDX,SP10 
          CALL      RSPTPNI2
          CALL      RKPTPNI2
          BRANCH    OVRCD,VALIDIC5
          CLOSE     PATPNIA2
.
          MATCH     KYNNMP,PTPNNMPI
          GOTO      VALIDIC5 IF NOT EQUAL
.
VALIDIC4  COMPARE   CHOSINDX,PTPNHNUM
          GOTO      VALIDIC9 IF NOT EQUAL * different hospital
.
          PACK      KEY8,AADMNO,SP10
          MATCH     KEY8,PTPNADNO
          GOTO      VALIDIC5 IF EQUAL     * same person
.
          DISPLAY   *P1:24,*B,*+,PTCNNMDX,*-," exists for Visit Number ":
                        *V2LON,PTPNADNO,*HOFF,".  ",*EL;
          CALL      HITENTER
.
          MOVE      TWO,EXIT
          GOTO      VALIDIC9 
.
VALIDIC5  COMPARE   ONE,FORM1 
          GOTO      VALIDIC9 IF NOT EQUAL 
.
          COMPARE   EIGHT,FORM2
          GOTO      VALIDIC8 IF NOT EQUAL
.
          UNPACK    KYNNMP,KEY12,ICNUMBER,KEY1
          TYPE      ICNUMBER 
          GOTO      VALIDIC8 IF NOT EQUAL
.
          TYPE      KEY1
          GOTO      VALIDIC8 IF EQUAL
.
          MOVE      "0",FORM3
          MOVE      "1",FORM1
          UNPACK     ICNUMBER,ICARAY[ONE],ICARAY[TWO],ICARAY[THREE]:
                     ICARAY[FOUR],ICARAY[FIVE],ICARAY[SIX],ICARAY[SEVEN]
          UNPACK     MAP,MAPARY[ONE],MAPARY[TWO],MAPARY[THREE]:
                     MAPARY[FOUR],MAPARY[FIVE],MAPARY[SIX],MAPARY[SEVEN]
.
. ----- Loop to calculate total 
VALIDIC6  MOVE       ICARAY[FORM1],ICNUM
          MOVE       MAPARY[FORM1],MPNUM
          ASSIGN     (ICNUM*MPNUM+FORM3),FORM3
          IF         FORM1<7
            ADD        ONE,FORM1
            GOTO       VALIDIC6
          ENDIF
.
. ------ Calculate Check Digit
          ASSIGN    FORM3%TEN1,FORM3
          ASSIGN    TEN1-FORM3,FORM2
.
          MOVE      ANSJ,ANS
          LOAD      ANS,FORM2,ANSA,ANSB,ANSC,ANSD,ANSE,ANSF,ANSG,ANSH:
                    ANSI,ANSZ  
          MATCH     ANS,KEY1
          IF        !@EQUAL
            GOTO      VALIDIC8 
          ENDIF
.         
          GOTO      VALIDIC9 
          
.  
VALIDIC7  COMPARE   CHOSINDX,PTNIHNUM
          GOTO      VALIDIC9 IF NOT EQUAL * different hospital
.
          MATCH     PURNO,PTNILNUM
          GOTO      VALIDIC5 IF EQUAL     * same person
.  
          DISPLAY   *P1:24,*B,*+,PTCNNMDX,*-," exists for U/R Number ":
                        *V2LON,PTNILNUM,*HOFF,".  ",*EL;
          CALL      HITENTER
          MOVE      TWO,EXIT
          GOTO      VALIDIC9 
.
VALIDIC8  MOVE      TRUE,EXIT
.
VALIDIC9  RETURN
