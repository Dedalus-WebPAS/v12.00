.*****************************************************************************
.*    Program      : F08OPPOC                                                *
.*    Description  : Conversion oprpocaf to new File layout                  *
.*                                                                           *
.*    Author       : Steve Armstrong                                         *
.*    Date         : 31/03/2016                                              *
.*    Modifications:                                                         *
.*                                                                           *
.*        V10.08.00  31/03/2016  Steve Armstrong   TSK 0321157               *
.*                   Created program                                         *
.*****************************************************************************
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
.
.         Old File Definition
.
OLDFILE1  IFILE    SQL, FIXED=300, KEY="U1-10,11-11"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.OPPOUNID  DIM       10     10       1     Unique ID.
.DOPPORTY  DIM       1      1        11    Record Type
.                                         1 = Pre-Operative Check List
.                                         2 = Arrival Check List
.
.         Pre-OPerative Check List
.
.OPPOPIDB  DIM       1      1        12    Patient ID Bracelet
.                                         1 = No
.                                         2 = Yes
.
.         Consent Forms
.
.OPPOSATF  DIM       1      1        13    Surgical Agreement to Treat Signed
.                                         1 = No
.                                         2 = Yes
.OPPOAATF  DIM       1      1        14    Anaesthetic Agreement to Treat Sign
.                                         1 = No
.                                         2 = Yes
.OPPOBATF  DIM       1      1        15    Blood/Product Agree to Treat Signed
.                                         1 = No
.                                         2 = Yes
.OPPOTBPF  DIM       1      1        16    Request Tissue/Body Part Signed
.                                         1 = No
.                                         2 = Yes
.
.         Clinical Details
.
.OPPOTNBM  DIM       8      8        17    Time Nil by Mouth Food (HH:MM:SS)
.OPPOTNBF  DIM       8      8        25    Time Nil by Fluid (HH:MM:SS)
.OPPONBMC  DIM       1      1        33    Nil by Mouth Food Checked
.                                         1 = No
.                                         2 = Yes
.OPPONBFC  DIM       1      1        34    Nil by Fluid Checked
.                                         1 = No
.                                         2 = Yes
.OPPOALLN  DIM       1      1        35    Allergies Noted
.                                         1 = No
.                                         2 = Yes
.OPPOECGR  DIM       1      1        36    ECG Results
.                                         1 = No
.                                         2 = Yes
.OPPOLABR  DIM       1      1        37    Laboratory Results
.                                         1 = No
.                                         2 = Yes
.OPPOCOMB  DIM       1      1        38    Complete Blood Form
.                                         1 = No
.                                         2 = Yes
.OPPOMEDC  DIM       1      1        39    Medication Chart
.                                         1 = No
.                                         2 = Yes
.OPPODRIN  DIM       1      1        40    Drug Infusion Chart
.                                         1 = No
.                                         2 = Yes
.OPPOFLUB  DIM       1      1        41    Fluid Balance Chart
.                                         1 = No
.                                         2 = Yes
.OPPOXRAY  DIM       1      1        42    X-Ray/Cat Scans
.                                         1 = No
.                                         2 = Yes
.OPPODIAC  DIM       1      1        43    Diabetic Chart
.                                         1 = No
.                                         2 = Yes
.
.         Physical Details
.
.OPPODENT  DIM       3      3        44    Dentures            (Category Oy)
.                                                   1 = Labeled Carton
.                                                   2 = Top
.                                                   3 = Bottom
.                                                   4 = Top/Bottom
.                                                   5 = Partial Plate
.OPPOVALS  DIM       3      3        47    Valuables           (Category Oz)
.                                                   1 = Rings Taped
.                                                   2 = Rings Removed
.                                                   3 = Property with Patient
.                                                   4 = Property on Ward
.                                                   5 = Property with Family
.OPPOSHAV  DIM       1      1        50    Shave Checked
.                                         1 = No
.                                         2 = Yes
.OPPOURVD  DIM       8      8        51    Time Urine Voided (HH:MM:SS)
.OPPOINTR  DIM       3      3        59    Interpreter Required(Category OQ)
.OPPOLABN  FORM      2      2        62    Labels Number Required
.OPPOUC01  DIM       3      3        64    User Defined Code 1 (Category oY)
.OPPOUC02  DIM       3      3        67    User Defined Code 2 (Category oZ)
.OPPOUC03  DIM       3      3        70    User Defined Code 3 (Category Oa)
.OPPOUC04  DIM       3      3        73    User Defined Code 4 (Category Ob)
.OPPOUC05  DIM       3      3        76    User Defined Code 5 (Category Oc)
.OPPOUC06  DIM       3      3        79    User Defined Code 6 (Category Od)
.OPPOUC07  DIM       3      3        82    User Defined Code 7 (Category Oe)
.OPPOUC08  DIM       3      3        85    User Defined Code 8 (Category Of)
.OPPOUC09  DIM       3      3        88    User Defined Code 9 (Category Og)
.OPPOUC10  DIM       3      3        91    User Defined Code 10(Category Oh)
.OPPOUC11  DIM       3      3        94    User Defined Code 11(Category Oi)
.OPPOUC12  DIM       3      3        97    User Defined Code 12(Category Oj)
.OPPOUC13  DIM       3      3        100   User Defined Code 13(Category Ok)
.OPPOUC14  DIM       3      3        103   User Defined Code 14(Category Ol)
.OPPOUC15  DIM       3      3        106   User Defined Code 15(Category Om)
.OPPOUC16  DIM       3      3        109   User Defined Code 16(Category On)
.OPPOUC17  DIM       3      3        112   User Defined Code 17(Category Oo)
.OPPOUC18  DIM       3      3        115   User Defined Code 18(Category Op)
.OPPOUC19  DIM       3      3        118   User Defined Code 19(Category Oq)
.OPPOUC20  DIM       3      3        121   User Defined Code 20(Category Or)
.OPPOUY01  DIM       1      1        124   User Defined Y/N 1
.                                         1 = No
.                                         2 = Yes
.OPPOUY02  DIM       1      1        125   User Defined Y/N 2
.                                         1 = No
.                                         2 = Yes
.OPPOUY03  DIM       1      1        126   User Defined Y/N 3
.                                         1 = No
.                                         2 = Yes
.OPPOUY04  DIM       1      1        127   User Defined Y/N 4
.                                         1 = No
.                                         2 = Yes
.OPPOUY05  DIM       1      1        128   User Defined Y/N 5
.                                         1 = No
.                                         2 = Yes
.OPPOUY06  DIM       1      1        129   User Defined Y/N 6
.                                         1 = No
.                                         2 = Yes
.OPPOUY07  DIM       1      1        130   User Defined Y/N 7
.                                         1 = No
.                                         2 = Yes
.OPPOUY08  DIM       1      1        131   User Defined Y/N 8
.                                         1 = No
.                                         2 = Yes
.OPPOUY09  DIM       1      1        132   User Defined Y/N 9
.                                         1 = No
.                                         2 = Yes
.OPPOUY10  DIM       1      1        133   User Defined Y/N 10
.                                         1 = No
.                                         2 = Yes
.OPPOUT01  DIM       8      8        134   User Defined Time Field 1
.OPPOUT02  DIM       8      8        142   User Defined Time Field 2
.OPPOUT03  DIM       8      8        150   User Defined Time Field 3
.OPPOUT04  DIM       8      8        158   User Defined Time Field 4
.OPPOUT05  DIM       8      8        166   User Defined Time Field 5
.OPPOUT06  DIM       8      8        174   User Defined Time Field 6
.OPPOUT07  DIM       8      8        182   User Defined Time Field 7
.OPPOUT08  DIM       8      8        190   User Defined Time Field 8
.OPPOUT09  DIM       8      8        198   User Defined Time Field 9
.OPPOUT10  DIM       8      8        206   User Defined Time Field 10
.OPPOUF01  FORM      6      4        214   User Defined Form Field 1
.OPPOUF02  FORM      6      4        218   User Defined Form Field 2
.OPPOUF03  FORM      6      4        222   User Defined Form Field 3
.OPPOUF04  FORM      6      4        226   User Defined Form Field 4
.OPPOUF05  FORM      6      4        230   User Defined Form Field 5
.OPPOUF06  FORM      6      4        234   User Defined Form Field 6
.OPPOUF07  FORM      6      4        238   User Defined Form Field 7
.OPPOUF08  FORM      6      4        242   User Defined Form Field 8
.OPPOUF09  FORM      6      4        246   User Defined Form Field 9
.OPPOUF10  FORM      6      4        250   User Defined Form Field 10
OLDSPARE  DIM       46     46       254   Spare Variable
.
.End of Record                      300
.
.
.         New File Definition
.
OPRPOCA1   IFILE    SQL, FIXED=484, KEY="U1-10,11-11"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
OPPOUNID  DIM       10     10       1     Unique ID.
DOPPORTY  DIM       1      1        11    Record Type
.                                         1 = Pre-Operative Check List
.                                         2 = Arrival Check List
.                                         3 = Pre-Induction of Anaesthesia
.                                         4 = Pre Skin Incision Timeout
.                                             Safety Checklist
.                                         5 = OR Sign Out Safety Checklist
.                                         6 = Recovery Room Handover Checklist
.
.         Pre-Operative Check List
.
OPPOPIDB  DIM       1      1        12    Patient ID Bracelet
.                                         1 = No
.                                         2 = Yes
.
.         Consent Forms
.
OPPOSATF  DIM       1      1        13    Surgical Agreement to Treat Signed
.                                         1 = No
.                                         2 = Yes
OPPOAATF  DIM       1      1        14    Anaesthetic Agreement to Treat Sign
.                                         1 = No
.                                         2 = Yes
OPPOBATF  DIM       1      1        15    Blood/Product Agree to Treat Signed
.                                         1 = No
.                                         2 = Yes
OPPOTBPF  DIM       1      1        16    Request Tissue/Body Part Signed
.                                         1 = No
.                                         2 = Yes
.
.         Clinical Details
.
OPPOTNBM  DIM       8      8        17    Time Nil by Mouth Food (HH:MM:SS)
OPPOTNBF  DIM       8      8        25    Time Nil by Fluid (HH:MM:SS)
OPPONBMC  DIM       1      1        33    Nil by Mouth Food Checked
.                                         1 = No
.                                         2 = Yes
OPPONBFC  DIM       1      1        34    Nil by Fluid Checked
.                                         1 = No
.                                         2 = Yes
OPPOALLN  DIM       1      1        35    Allergies Noted
.                                         1 = No
.                                         2 = Yes
OPPOECGR  DIM       1      1        36    ECG Results
.                                         1 = No
.                                         2 = Yes
OPPOLABR  DIM       1      1        37    Laboratory Results
.                                         1 = No
.                                         2 = Yes
OPPOCOMB  DIM       1      1        38    Complete Blood Form
.                                         1 = No
.                                         2 = Yes
OPPOMEDC  DIM       1      1        39    Medication Chart
.                                         1 = No
.                                         2 = Yes
OPPODRIN  DIM       1      1        40    Drug Infusion Chart
.                                         1 = No
.                                         2 = Yes
OPPOFLUB  DIM       1      1        41    Fluid Balance Chart
.                                         1 = No
.                                         2 = Yes
OPPOXRAY  DIM       1      1        42    X-Ray/Cat Scans
.                                         1 = No
.                                         2 = Yes
OPPODIAC  DIM       1      1        43    Diabetic Chart
.                                         1 = No
.                                         2 = Yes
.
.         Physical Details
.
OPPODENT  DIM       3      3        44    Dentures            (Category Oy)
.                                                   1 = Labeled Carton
.                                                   2 = Top
.                                                   3 = Bottom
.                                                   4 = Top/Bottom
.                                                   5 = Partial Plate
OPPOVALS  DIM       3      3        47    Valuables           (Category Oz)
.                                                   1 = Rings Taped
.                                                   2 = Rings Removed
.                                                   3 = Property with Patient
.                                                   4 = Property on Ward
.                                                   5 = Property with Family
OPPOSHAV  DIM       1      1        50    Shave Checked
.                                         1 = No
.                                         2 = Yes
OPPOURVD  DIM       8      8        51    Time Urine Voided (HH:MM:SS)
OPPOINTR  DIM       3      3        59    Interpreter Required(Category OQ)
OPPOLABN  FORM      2      2        62    Labels Number Required
OPPOUC01  DIM       3      3        64    User Defined Code 1 (Category oY)
OPPOUC02  DIM       3      3        67    User Defined Code 2 (Category oZ)
OPPOUC03  DIM       3      3        70    User Defined Code 3 (Category Oa)
OPPOUC04  DIM       3      3        73    User Defined Code 4 (Category Ob)
OPPOUC05  DIM       3      3        76    User Defined Code 5 (Category Oc)
OPPOUC06  DIM       3      3        79    User Defined Code 6 (Category Od)
OPPOUC07  DIM       3      3        82    User Defined Code 7 (Category Oe)
OPPOUC08  DIM       3      3        85    User Defined Code 8 (Category Of)
OPPOUC09  DIM       3      3        88    User Defined Code 9 (Category Og)
OPPOUC10  DIM       3      3        91    User Defined Code 10(Category Oh)
OPPOUC11  DIM       3      3        94    User Defined Code 11(Category Oi)
OPPOUC12  DIM       3      3        97    User Defined Code 12(Category Oj)
OPPOUC13  DIM       3      3        100   User Defined Code 13(Category Ok)
OPPOUC14  DIM       3      3        103   User Defined Code 14(Category Ol)
OPPOUC15  DIM       3      3        106   User Defined Code 15(Category Om)
OPPOUC16  DIM       3      3        109   User Defined Code 16(Category On)
OPPOUC17  DIM       3      3        112   User Defined Code 17(Category Oo)
OPPOUC18  DIM       3      3        115   User Defined Code 18(Category Op)
OPPOUC19  DIM       3      3        118   User Defined Code 19(Category Oq)
OPPOUC20  DIM       3      3        121   User Defined Code 20(Category Or)
OPPOUY01  DIM       1      1        124   User Defined Y/N 1
.                                         1 = No
.                                         2 = Yes
OPPOUY02  DIM       1      1        125   User Defined Y/N 2
.                                         1 = No
.                                         2 = Yes
OPPOUY03  DIM       1      1        126   User Defined Y/N 3
.                                         1 = No
.                                         2 = Yes
OPPOUY04  DIM       1      1        127   User Defined Y/N 4
.                                         1 = No
.                                         2 = Yes
OPPOUY05  DIM       1      1        128   User Defined Y/N 5
.                                         1 = No
.                                         2 = Yes
OPPOUY06  DIM       1      1        129   User Defined Y/N 6
.                                         1 = No
.                                         2 = Yes
OPPOUY07  DIM       1      1        130   User Defined Y/N 7
.                                         1 = No
.                                         2 = Yes
OPPOUY08  DIM       1      1        131   User Defined Y/N 8
.                                         1 = No
.                                         2 = Yes
OPPOUY09  DIM       1      1        132   User Defined Y/N 9
.                                         1 = No
.                                         2 = Yes
OPPOUY10  DIM       1      1        133   User Defined Y/N 10
.                                         1 = No
.                                         2 = Yes
OPPOUT01  DIM       8      8        134   User Defined Time Field 1
OPPOUT02  DIM       8      8        142   User Defined Time Field 2
OPPOUT03  DIM       8      8        150   User Defined Time Field 3
OPPOUT04  DIM       8      8        158   User Defined Time Field 4
OPPOUT05  DIM       8      8        166   User Defined Time Field 5
OPPOUT06  DIM       8      8        174   User Defined Time Field 6
OPPOUT07  DIM       8      8        182   User Defined Time Field 7
OPPOUT08  DIM       8      8        190   User Defined Time Field 8
OPPOUT09  DIM       8      8        198   User Defined Time Field 9
OPPOUT10  DIM       8      8        206   User Defined Time Field 10
OPPOUF01  FORM      6      4        214   User Defined Form Field 1
OPPOUF02  FORM      6      4        218   User Defined Form Field 2
OPPOUF03  FORM      6      4        222   User Defined Form Field 3
OPPOUF04  FORM      6      4        226   User Defined Form Field 4
OPPOUF05  FORM      6      4        230   User Defined Form Field 5
OPPOUF06  FORM      6      4        234   User Defined Form Field 6
OPPOUF07  FORM      6      4        238   User Defined Form Field 7
OPPOUF08  FORM      6      4        242   User Defined Form Field 8
OPPOUF09  FORM      6      4        246   User Defined Form Field 9
OPPOUF10  FORM      6      4        250   User Defined Form Field 10
OPPOUDR1  DIM       10     10       254   User Defined Doctor Field 1
OPPOUDR2  DIM       10     10       264   User Defined Doctor Field 2
OPPOUDR3  DIM       10     10       274   User Defined Doctor Field 3
OPPOUDR4  DIM       10     10       284   User Defined Doctor Field 4
OPPOUDR5  DIM       10     10       294   User Defined Doctor Field 5
OPPOUNR1  DIM       10     10       304   User Defined Nurse Field 1
OPPOUNR2  DIM       10     10       314   User Defined Nurse Field 2
OPPOUNR3  DIM       10     10       324   User Defined Nurse Field 3
OPPOUNR4  DIM       10     10       334   User Defined Nurse Field 4
OPPOUNR5  DIM       10     10       344   User Defined Nurse Field 5
OPPOUD01  DIM       8      8        354   User Defined Date Field 1
OPPOUD02  DIM       8      8        362   User Defined Date Field 2
OPPOUD03  DIM       8      8        370   User Defined Date Field 3
OPPOUD04  DIM       8      8        378   User Defined Date Field 4
OPPOUD05  DIM       8      8        386   User Defined Date Field 5
OPPOUD06  DIM       8      8        394   User Defined Date Field 6
OPPOUD07  DIM       8      8        402   User Defined Date Field 7
OPPOUD08  DIM       8      8        410   User Defined Date Field 8
OPPOUD09  DIM       8      8        418   User Defined Date Field 9
OPPOUD10  DIM       8      8        426   User Defined Date Field 10
OPPOSPAR  DIM       50     50       434   Spare Variable
.
.End of Record                      484
.
.Redefine FORM fields from KEY
.-----------------------------
OPPORTYP  FORM      1
.
.
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       200
.
DEFPATH   DIM       60
DIM60     DIM       60
DISNUM    FORM      4
.
INPFILE   DIM       8
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
SAVEFLG   FORM      1
.
.
. PROGRAM CONSTANTS
. -----------------
SP60      INIT    "                                                            "
.
.
.
PRGNAM    INIT      "CONVERSION OPRPOCFD"
PRGID     INIT      "F08OPPOC"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000              * run c-isam fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
MAIN2000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN0000:               * no
                          MAIN9999                * cancel
.
MAIN5000  BRANCH    COPTION,MAIN6000              * c-isam fixit
.
.         Running c-isam fix
.
MAIN6000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/zip saved file
          GOTO      MAIN9999
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
          
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run c-isam fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
.
PREP0000  MOVE      "oprpocaf",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat oprpocaf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "soppocaf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat oprpocaf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "soppocaf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "oprpocaf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "soppocaf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "oprpocaf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " 484 UC1-10,11-11",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC11-11,1-10",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      OPRPOCA1,NEWFILE
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
            ELSE
              DISPLAY   "has caused an IO error - ";
            ENDIF
          ENDIF
          DISPLAY   "File will be bypassed"
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY11,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MOVE      SP10,OPPOUDR1
          MOVE      SP10,OPPOUDR2
          MOVE      SP10,OPPOUDR3
          MOVE      SP10,OPPOUDR4
          MOVE      SP10,OPPOUDR5
          MOVE      SP10,OPPOUNR1
          MOVE      SP10,OPPOUNR2
          MOVE      SP10,OPPOUNR3
          MOVE      SP10,OPPOUNR4
          MOVE      SP10,OPPOUNR5
          MOVE      SP8,OPPOUD01
          MOVE      SP8,OPPOUD02
          MOVE      SP8,OPPOUD03
          MOVE      SP8,OPPOUD04
          MOVE      SP8,OPPOUD05
          MOVE      SP8,OPPOUD06
          MOVE      SP8,OPPOUD07
          MOVE      SP8,OPPOUD08
          MOVE      SP8,OPPOUD09
          MOVE      SP8,OPPOUD10
          MOVE      SP70,OPPOSPAR
.
          CALL      WROPPOC1                     * write to new file
.
          GOTO      READ1000                     * get next record
.
READ6000  CLOSE     OPRPOCA1                     * close new file
          CLOSE     OLDFILE1                     * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS:
                    *P1:24;
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
.
VALD0000  MOVE      ZERO,EXIT
.
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or zip save file                                     *
.**********************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "soppocaf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "gzip -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not zipped or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:24,*EL,*B,"Finished processing  ",CDATE,SP1,CTIMEIS:
                    ".  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.*        DEFT0000 -  Get the default directory                       *
.**********************************************************************
.
DEFT0000  EXECUTE   "ldat oprpocaf.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding 'oprpocaf'.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
.         Old I/O Routines
.
READSOLD  RESET     KEY11
          READ      OLDFILE1,KEY11;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;OPPOUNID,DOPPORTY,OPPOPIDB,OPPOSATF,OPPOAATF:
                        OPPOBATF,OPPOTBPF,OPPOTNBM,OPPOTNBF,OPPONBMC,OPPONBFC:
                        OPPOALLN,OPPOECGR,OPPOLABR,OPPOCOMB,OPPOMEDC,OPPODRIN:
                        OPPOFLUB,OPPOXRAY,OPPODIAC,OPPODENT,OPPOVALS,OPPOSHAV:
                        OPPOURVD,OPPOINTR,OPPOLABN,OPPOUC01,OPPOUC02,OPPOUC03:
                        OPPOUC04,OPPOUC05,OPPOUC06,OPPOUC07,OPPOUC08,OPPOUC09:
                        OPPOUC10,OPPOUC11,OPPOUC12,OPPOUC13,OPPOUC14,OPPOUC15:
                        OPPOUC16,OPPOUC17,OPPOUC18,OPPOUC19,OPPOUC20,OPPOUY01:
                        OPPOUY02,OPPOUY03,OPPOUY04,OPPOUY05,OPPOUY06,OPPOUY07:
                        OPPOUY08,OPPOUY09,OPPOUY10,OPPOUT01,OPPOUT02,OPPOUT03:
                        OPPOUT04,OPPOUT05,OPPOUT06,OPPOUT07,OPPOUT08,OPPOUT09:
                        OPPOUT10,OPPOUF01,OPPOUF02,OPPOUF03,OPPOUF04,OPPOUF05:
                        OPPOUF06,OPPOUF07,OPPOUF08,OPPOUF09,OPPOUF10,OLDSPARE
          GOTO      OVERCOND IF OVER
          MOVE      DOPPORTY,OPPORTYP
          RETURN
.
.         New I/O Routines
.
WROPPOC1  MOVE      OPPORTYP,DOPPORTY
          WRITE     OPRPOCA1,KEY11;OPPOUNID,DOPPORTY,OPPOPIDB,OPPOSATF,OPPOAATF:
                        OPPOBATF,OPPOTBPF,OPPOTNBM,OPPOTNBF,OPPONBMC,OPPONBFC:
                        OPPOALLN,OPPOECGR,OPPOLABR,OPPOCOMB,OPPOMEDC,OPPODRIN:
                        OPPOFLUB,OPPOXRAY,OPPODIAC,OPPODENT,OPPOVALS,OPPOSHAV:
                        OPPOURVD,OPPOINTR,OPPOLABN,OPPOUC01,OPPOUC02,OPPOUC03:
                        OPPOUC04,OPPOUC05,OPPOUC06,OPPOUC07,OPPOUC08,OPPOUC09:
                        OPPOUC10,OPPOUC11,OPPOUC12,OPPOUC13,OPPOUC14,OPPOUC15:
                        OPPOUC16,OPPOUC17,OPPOUC18,OPPOUC19,OPPOUC20,OPPOUY01:
                        OPPOUY02,OPPOUY03,OPPOUY04,OPPOUY05,OPPOUY06,OPPOUY07:
                        OPPOUY08,OPPOUY09,OPPOUY10,OPPOUT01,OPPOUT02,OPPOUT03:
                        OPPOUT04,OPPOUT05,OPPOUT06,OPPOUT07,OPPOUT08,OPPOUT09:
                        OPPOUT10,OPPOUF01,OPPOUF02,OPPOUF03,OPPOUF04,OPPOUF05:
                        OPPOUF06,OPPOUF07,OPPOUF08,OPPOUF09,OPPOUF10,OPPOUDR1:
                        OPPOUDR2,OPPOUDR3,OPPOUDR4,OPPOUDR5,OPPOUNR1,OPPOUNR2:
                        OPPOUNR3,OPPOUNR4,OPPOUNR5,OPPOUD01,OPPOUD02,OPPOUD03:
                        OPPOUD04,OPPOUD05,OPPOUD06,OPPOUD07,OPPOUD08,OPPOUD09:
                        OPPOUD10,OPPOSPAR
          RETURN
.
          INC       STD001IO
