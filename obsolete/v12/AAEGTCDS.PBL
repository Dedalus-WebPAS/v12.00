.******************************************************************************
.* System         : Accident and Emergency                                    *
.*                                                                            *
.* Include Name   : AAEGTCDS.PBL                                              *
.*                                                                            *
.* Function       : Display the codes from AAEGTCFD and AAEGTLFD.             *
.*                                                                            *
.* Author         : Justin Coates      03/08/1993                             *
.*                                                                            *
.* Returns        : KEY6               (AETCCODE+AETLCODE)                    *
.*                                                                            *
.* Modifications  :                                                           *
.*        V5.03.01  23/06/1995    Justin Coates  SRF 133250                   *
.*                  if a code is selected that has links, but the main code   *
.*                  is selected, set KEY6 to AETCCODE and spaces              *
.******************************************************************************
.
AAEGTCDS  MOVE      FIVE,CVERT              * line counter
          MOVE      ONE,CCOL                * start on LHS of screen
          MOVE      ZERO,AETCQCNT           * clear the count
          MOVE      ONE,CPAGENO             * set page number
          MOVE      SP4,KEY4                * key of blanks
          CALL      RSAEGTC1                * positional read
.
          DISPLAY   *P1:3,*EF,*V2LON,*P25:4,"TREATMENT CODES":
                    *P1:5,*ULON,"Item",*P6:5,"Code   ":
                    *P14:5,"Description         ":
                    *P41:5,"Item",*P46:5,"Code   ":
                    *P54:5,"Description         "
.
. ---------------------------------------------------------------
. ------- next read on diagnosis file and position on link ------
. ---------------------------------------------------------------
.
HAETC100  CALL      RKAEGTC1
          BRANCH    OVRCD,HAETC500          * end of file
.
          PACK      KEY6,AETCCODE,SP2
          CALL      RSAEGTL1
.
          PACK      CKYIDEF6,AETCCODE,SP6
          PACK      KEY22,AETCDESC,SP2
.
. ----------------------------------
. ------- increment counters -------
. ----------------------------------
.
HAETC200  ADD       ONE,CVERT               * inc line counter
.
          COMPARE   "23",CVERT
          GOTO      HAETC300 IF LESS        * will fit on the page
.
          ADD       "40",CCOL               * inc column counter
          MOVE      SIX,CVERT               * reset row position
.
          COMPARE   "80",CCOL  
          GOTO      HAETC400 IF NOT LESS    * need a new page
.
. -------------------------------------------
. ------- display all the information -------
. -------------------------------------------
.
HAETC300  ADD       ONE,AETCQCNT
          DISPLAY   *PCCOL:CVERT,*V2LON,SP1,AETCQCNT,*HOFF,DOT:
                                        SP1,CKYIDEF6,SP2,KEY22
          MOVE      CKYIDEF6,AETCQARR[AETCQCNT]
.
.         now loop over link file
.
          CALL      RKAEGTL1
          BRANCH    OVRCD,HAETC100          * no more codes
          MATCH     AETCCODE,AETLMAIN
          GOTO      HAETC100 IF NOT EQUAL   * different code
.
          PACK      CKYIDEF6,AETCCODE,AETLCODE,SP6
          PACK      KEY22,SP2,AETLDESC
          GOTO      HAETC200
.
. ------- new page needed -------
.
HAETC400  BRANCH    CPAGENO,HAETC450        * first page
.
HAETC410  MOVE      ONE,AETCQLIN            * (N)ext, (F)irst
          CALL      HAETCK00
          BRANCH    EXIT,HAETC999,AAEGTCDS,HAETC700
.                        exit     first    next
          GOTO      HAETC600
.
HAETC450  MOVE      TWO,AETCQLIN            * (N)ext only
          CALL      HAETCK00
          BRANCH    EXIT,HAETC999,HAETC590,HAETC700
.                        exit     first    next
          GOTO      HAETC600
.
. ------- end of data -------
.
HAETC500  BRANCH    CPAGENO,HAETC550        * first page
.
HAETC510  MOVE      THREE,AETCQLIN          * (F)irst
          CALL      HAETCK00
          BRANCH    EXIT,HAETC999,AAEGTCDS,HAETC590
.                        exit     first    next
          GOTO      HAETC600
.
HAETC550  MOVE      FOUR,AETCQLIN           * no paging
          CALL      HAETCK00
          BRANCH    EXIT,HAETC999,HAETC590,HAETC590
.                        exit     first    next
          GOTO      HAETC600
.
HAETC590  BEEP
          BRANCH    AETCQLIN,HAETC410,HAETC450,HAETC510,HAETC550
.
. ------- item selected -------
.
HAETC600  MOVE      AETCQARR[FORM2],KEY6
          UNPACK    KEY6,KEY4,KEY2
          CALL      RDAEGTC1
          BRANCH    OVRCD,HAETC590
.
          MATCH     ANSY,AETCSUBA           * using sub-analysis
          IF        @EQUAL
            MATCH     SP2,KEY2                * has a code been entered
            IF        !@EQUAL
              PACK      KEY6,AETCCODE,KEY2
              CALL      RDAEGTL1
              BRANCH    OVRCD,HAETC590          * invalid link
            ELSE 
              PACK      KEY6,AETCCODE,SP2         * clear link code
            ENDIF
          ELSE
            PACK      KEY6,AETCCODE,SP2         * clear link code
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      HAETC999
.
. ------- (N)ext screen selected -------
.
HAETC700  DISPLAY   *P1:6,*EF;
          MOVE      SIX,CVERT               * first display row 
          MOVE      ONE,CCOL                * get back to LHS of screen
          MOVE      ZERO,AETCQCNT           * reset count
          ADD       ONE,CPAGENO             * add to page count
          GOTO      HAETC300                * display the data
.
HAETC999  RETURN
+
.*********************************************************************
.         keyin 
.*********************************************************************
HAETCK00  DISPLAY   *P1:24,*EL,"Select item, (":
                    *V2LON,ANSE,*HOFF,")xit";
          MOVE      "19",CCOL
.
          IF        AETCQLIN = ONE | AETCQLIN = TWO
            DISPLAY   ", (",*V2LON,ANSN,*HOFF,")ext";
            ADD       EIGHT,CCOL
          ENDIF
          IF        AETCQLIN = ONE | AETCQLIN = THREE
            DISPLAY   ", (",*V2LON,ANSF,*HOFF,")irst";
            ADD       NINE,CCOL
          ENDIF
.
          DISPLAY   " : ";
          ADD       FOUR,CCOL
.
HAETCK10  KEYIN     *PCCOL:24,*V2LON,*JR,DIM2
          PACK      DIM2,DIM2,SP2
          TYPE      DIM2
          GOTO      HAETCK50 IF EQUAL       * number entered
.
          UNPACK    DIM2,ANS,ANS
          REP       UPPLOW,ANS
          MOVE      ZERO,EXIT
          REP       "E1F2N3",ANS
          MOVE      ANS,EXIT
          TYPE      ANS
          GOTO      HAETCK99 IF EQUAL       * valid number
.
HAETCK20  BEEP
          GOTO      HAETCK10
.
HAETCK50  MOVE      DIM2,FORM2
          MOVE      ZERO,EXIT
          COMPARE   FORM2,ZERO
          GOTO      HAETCK20 IF NOT LESS    * negative invalid
          COMPARE   FORM2,AETCQCNT
          GOTO      HAETCK20 IF LESS        * too high
.
HAETCK99  RETURN
+
