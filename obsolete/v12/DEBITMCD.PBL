.---------------------------------------------------------------------------
.   System    :   Accounts Receiveable               
.   Include   :   DEBITMKY
.   Routine   :   KDBITX00 Keyin Sales Item with Security
.   Parameter :   CKEYTYP 0 = Keyin , 1 = Accept, 2 = Either
.   Return    :   EXIT = 1 null entry, 2 EXITCHAR entered
.-------------------------------------------------------------------------------
KDBITX00  TOPIC     "DBITITM "
          MOVE      UNDLN70,DBITITM 
          KEYIN     *PCCOL:CVERT,*DV,DBITITM:                                   
                    *PCCOL:CVERT,*V2LON,DBITITM:                                
                    *PCCOL:CVERT,*DV,DBITITM 
.
          ENDSET    DBITITM 
          APPEND    SP70,DBITITM 
          RESET     DBITITM 
.
          MATCH     SP70,DBITITM 
          GOTO      KDBITX90 IF EQUAL
.
          CMATCH    EXITCHAR,DBITITM 
          GOTO      KDBITX95 IF EQUAL
.
          CMATCH    QUEST,DBITITM 
          GOTO      KDBITX10 IF NOT EQUAL
.
          MOVE      CCITEM,SCCITEM
          CALL      HDBITX00
          MOVE      SCCITEM,CCITEM
.
          ENDSET    DBITITM 
          APPEND    SP70,DBITITM 
          RESET     DBITITM 
.
          MATCH     SP70,DBITITM 
          GOTO      KDBITX00 IF EQUAL
.
          DISPLAY   *PCCOL:CVERT,*V2LON,DBITITM                                 
KDBITX10
          PACK      KEY8,DBITITM                                                
          CALL      RADBIT1 
          BRANCH    OVRCD,KDBITX80
          BRANCH    CKEYTYP,KDBITX75
          CALL      RDDBIT1 
.
          MOVE      DBITPTY,KEY8
          CALL      RDDBPT1
          BRANCH    OVRCD,KDBITX71
.
          PACK      KEY12,PASSCODE,DBPTDEP
          CALL      RDDBSE1
          BRANCH    OVRCD,KDBITX72
.
KDBITX70  MOVE      ZERO,EXIT
          GOTO      KDBITX99
.
KDBITX71  MOVE      "Sales Item has an Invalid Product Group - ",DISPMSG 
          BEEP
          CALL      MESSAGE1
          GOTO      KDBITX00
.
KDBITX72  MOVE      "Security Not Enabled for this Sales Items - ",DISPMSG 
          BEEP
          CALL      MESSAGE1
          GOTO      KDBITX00
.
KDBITX75  MOVE      "Sales Item Already Exist - ",DISPMSG 
          BEEP
          CALL      MESSAGE1
          GOTO      KDBITX00
.
KDBITX80  MOVE      ZERO,EXIT
          BRANCH    CKEYTYP,KDBITX99,KDBITX99
          MOVE      "Sales Item Does Not Exist - ",DISPMSG        
          CALL      MESSAGE1
          GOTO      KDBITX00
KDBITX90
          MOVE      ONE,EXIT
          GOTO      KDBITX99
KDBITX95
          MOVE      TWO,EXIT
KDBITX99 TOPIC    PRGID
          RETURN
.---------------------------------------------------------------------------
.   System    :   Accounts Receiveable               
.   Include   :   DEBITMKY
.   Routine   :   HDBITX00 Sales Item          
.   Return    :           
.-------------------------------------------------------------------------------
HDBITX00
          MOVE      ZERO,CSCRSIZ
          IF        HTOP<3
            MOVE      "10",HTOP
            MOVE      "24",HBOT
            MOVE      "01",HLEF
            MOVE      "80",HRIG
          ENDIF
HDBITX04
          CALL      GSCRN000
          BRANCH    EXIT,HDBITX99
          MOVE      STDCOL00,STDCOL01
          ADD       " 5",STDCOL01
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON,"Code"               
.
          MOVE      STDCOL01,STDCOL02
          ADD       " 9",STDCOL02
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"Sales Item Name"    
.
          MOVE      STDCOL02,STDCOL03
          ADD       "36",STDCOL03
          DISPLAY   *PSTDCOL03:STDROW,*V2LON,*ULON,"Product Type"       
.
          MOVE      STDCOL03,STDCOL04
          ADD       "36",STDCOL04
HDBITX08
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          MOVE      SP70,DBITITM 
          PACK      KEY8,DBITITM,SP70                                           
.                                                                               
.                                                                               
          CALL      RSDBIT1 
HDBITX10
          CALL      RKDBIT1 
          BRANCH    OVRCD,HDBITX15
.
          MOVE      DBITPTY,KEY8
          CALL      RDDBPT1
          BRANCH    OVRCD,HDBITX10
          PACK      KEY12,PASSCODE,DBPTDEP
          CALL      RDDBSE1
          BRANCH    OVRCD,HDBITX10
.
          PACK      KEY8,DBITITM,SP70                                           
.                                                                               
          BRANCH    CSEARCH,HDBITX12
          CALL      HDBITX60
          BRANCH    OVRCD,HDBITX10
HDBITX12
          ADD       ONE,XITEMNO
          ADD       ONE,STDROW
          STORE     KEY8,XITEMNO,ST1,ST2,ST3,ST4,ST5:
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,XITEMNO,DOT
          DISPLAY   *PSTDCOL01:STDROW,DBITITM 
          DISPLAY   *PSTDCOL02:STDROW,DBITDES 
          DISPLAY   *PSTDCOL03:STDROW,DBPTDES 
          COMPARE   MAXITEM,XITEMNO
          GOTO      HDBITX10 IF NOT EQUAL
HDBITX15
.                                                                               
          MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
HDBITX20
          CALL      HSCRI000
          BRANCH    EXIT,HDBITX30,HDBITX35,HDBITX40,HDBITX45,HDBITX04:
                              HDBITX91,HDBITX50
          GOTO      HDBITX92
HDBITX30
          CALL      RKDBIT1 
          BRANCH    OVRCD,HDBITX85
          MOVE      DBITPTY,KEY8
          CALL      RDDBPT1
          BRANCH    OVRCD,HDBITX30
          PACK      KEY12,PASSCODE,DBPTDEP
          CALL      RDDBSE1
          BRANCH    OVRCD,HDBITX30
.
          PACK      KEY8,DBITITM,SP70                                           
.                                                                               
          CALL      RPDBIT1 
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HDBITX10
HDBITX35
          MOVE      ST1,KEY8 
          CALL      RDDBIT1 
          CALL      RPDBIT1 
          BRANCH    OVRCD,HDBITX85
          PACK      KEY8,DBITITM,SP70                                           
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
HDBITX36  CALL      RPDBIT1 
          BRANCH    OVRCD,HDBITX37
          MOVE      DBITPTY,KEY8
          CALL      RDDBPT1
          BRANCH    OVRCD,HDBITX36
          PACK      KEY12,PASSCODE,DBPTDEP
          CALL      RDDBSE1
          BRANCH    OVRCD,HDBITX36
.
          PACK      KEY8,DBITITM,SP70                                           
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HDBITX36 IF NOT EQUAL
.
HDBITX37  MOVE      ZERO,XITEMNO
          GOTO      HDBITX10
HDBITX40  MOVE      ST1,KEY8 
          CALL      RDDBIT1 
          CALL      RPDBIT1 
          BRANCH    OVRCD,HDBITX85
          PACK      KEY8,DBITITM,SP70                                           
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HDBITX08
.
HDBITX45  CALL      RKDBIT1 
          BRANCH    OVRCD,HDBITX85
          PACK      KEY8,DBITITM,SP70                                           
          SUSPEND
          CLIP      1,1,CRIG,24
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,XITEMNO
          PACK      KEY8,Z70                                                    
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
          CALL      RSDBIT1 
HDBITX46
          CALL      RPDBIT1 
          BRANCH    OVRCD,HDBITX47
.
          MOVE      DBITPTY,KEY8
          CALL      RDDBPT1
          BRANCH    OVRCD,HDBITX46
          PACK      KEY12,PASSCODE,DBPTDEP
          CALL      RDDBSE1
          BRANCH    OVRCD,HDBITX46
.
          PACK      KEY8,DBITITM,SP70                                           
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HDBITX46 IF NOT EQUAL
HDBITX47  MOVE      ZERO,XITEMNO
          GOTO      HDBITX10
HDBITX50  COMPARE   ZERO,CSEARCH
          GOTO      HDBITX55 IF EQUAL
          MOVE      ZERO,CSEARCH
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          UNPACK    SP70,SCHFLD01,SCHFLD02,SCHFLD03,SCHFLD04,SCHFLD05
          UNPACK    SP70,SCHFLD06,SCHFLD07,SCHFLD08,SCHFLD09,SCHFLD10
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          DISPLAY   *PSTDCOL00:STDROW,*V2LON,*HON,"Sch"
          KEYIN     *PSTDCOL01:STDROW,*V2LON,SCHFLD01
          RESET     SCHFLD01
          KEYIN     *PSTDCOL02:STDROW,*V2LON,SCHFLD02
          RESET     SCHFLD02
          KEYIN     *PSTDCOL03:STDROW,*V2LON,SCHFLD03
          RESET     SCHFLD03
          PACK      KEY8,SP70
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HDBITX08
HDBITX55  MOVE      ONE,CSEARCH
          PACK      KEY8,SP70
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HDBITX08
HDBITX60  MOVE      ZERO,OVRCD
          MATCH     SP70,SCHFLD01
          GOTO      HDBITX61 IF EQUAL
          MATCH     SCHFLD01,DBITITM 
          GOTO      HDBITX72 IF NOT EQUAL
HDBITX61
          MATCH     SP70,SCHFLD02
          GOTO      HDBITX62 IF EQUAL
          MATCH     SCHFLD02,DBITDES 
          GOTO      HDBITX72 IF NOT EQUAL
HDBITX62
          MATCH     SP70,SCHFLD03
          GOTO      HDBITX63 IF EQUAL
          MATCH     SCHFLD03,DBPTDES 
          GOTO      HDBITX72 IF NOT EQUAL
HDBITX63
HDBITX64
HDBITX65
HDBITX66
HDBITX67
HDBITX68
HDBITX69
HDBITX70
          GOTO      HDBITX75
HDBITX72  MOVE      ONE,OVRCD
HDBITX75  RETURN
.
HDBITX85  LOAD      KEY8,XITEMNO,ST1,ST2,ST3,ST4,ST5:
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY8,DBITITM                                                
          CALL      RDDBIT1 
.
HDBITX90  BEEP
          GOTO      HDBITX20
.
HDBITX91  MOVE      SP70,DBITDES 
          MOVE      SP70,DBITIDE 
          MOVE      SP70,DBITUNT 
          MOVE      SP70,DBITPTY 
          MOVE      ZERO,DBITPRI 
          MOVE      ZERO,DBITMOD 
          MOVE      ZERO,DBITACT 
          MOVE      SP70,DBITLED 
          MOVE      SP70,DBITIAC 
          MOVE      SP70,DBITCAC 
          MOVE      ZERO,DBITTRT 
          MOVE      SP70,DBITTAC 
          MOVE      SP70,DBITTCA 
          MOVE      SP70,DBITUR1 
          MOVE      SP70,DBITUR2 
          MOVE      SP70,DBITUD1 
          MOVE      SP70,DBITUD2 
          MOVE      SP70,DBITUY1 
          MOVE      SP70,DBITUY2 
          MOVE      SP70,DBITUC1 
          MOVE      SP70,DBITUC2 
          MOVE      SP70,DBITUC3 
          MOVE      SP70,DBITUC4 
          MOVE      SP70,DBITGST 
          MOVE      SP70,DBITSPA 
          MOVE      SP70,KEY8 
          MOVE      SP70,DBITITM 
          GOTO      HDBITX96
HDBITX92
          MOVE      SP70,KEY8 
          MOVE      SP70,DBITITM 
          LOAD      KEY8,CCITEM,ST1,ST2,ST3,ST4,ST5:                            
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY8,DBITITM                                                
HDBITX96
          PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
HDBITX99
          RETURN
