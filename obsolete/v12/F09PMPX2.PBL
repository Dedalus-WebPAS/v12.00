.*****************************************************************************
.*    Program      : F09PMPX2                                                *
.*    Description  : Conversion pmspx2af to new File layout                  *
.*                                                                           *
.*    Author       : Davin Sloan                                             *
.*    Date         : 03/01/2017                                              *
.*    Modifications:                                                         *
.*                                                                           *
.*        V10.09.00  03/01/2017  Davin            TSK 0823516                *
.*                   Created program                                         *
.*****************************************************************************
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
.
.         Old File Definition
.
OLDFILE1   IFILE    SQL, FIXED=1806, KEY="U1-8"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.PMPXURNO  DIM       8      8        1     Patient U/R Number
.
.
.
.PMPXSPAR  DIM       9      9        1797  Spare
.
.End of Record                      1806
.
.         New File Definition
.
PMSPX2A1   IFILE    SQL, FIXED=2411, KEY="U1-8"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
PMPXURNO  DIM       8      8        1     Patient U/R Number
PMPXRHC1  DIM       10     10       9     Registered HCP 1 (pmshcpaf)
PMPXRH1G  DIM       10     10       19    Registered HCP 1 Group (pmshcgaf)
PMPXR1GC  DIM       2      2        29    Reg. HCP 1 Group Count (pmshclaf)
PMPXINTR  DIM       1      1        31    Interpreter Required?
.                                         0 = No
.                                         1 = Yes
.                                         U = Unknown (Not Stated)
PMPXLNG1  DIM       3      3        32    Preferred Language 1 (Cat LA)
PMPXLNG2  DIM       3      3        35    Preferred Language 2 (Cat LA)
PMPXLNG3  DIM       3      3        38    Preferred Language 3 (Cat LA)
PMPXPEML  DIM       80     80       41    Patient's Email Address
PMPXEDOB  DIM       1      1        121   Date of Birth Estimated? 0=NO,1=YES
PMPXCMOB  DIM       20     20       122   Emergency Contact Mobile Number
PMPXCEML  DIM       80     80       142   Emergency Contact Email Address
PMPXCCRP  DIM       1      1        222   Emer. Contact Corres. (0=NO,1=YES)
PMPXKMOB  DIM       20     20       223   NOK Mobile Number
PMPXKEML  DIM       80     80       243   NOK Email Address
PMPXKCRP  DIM       1      1        323   NOK Correspondence (0=NO,1=YES)
PMPXRMOB  DIM       20     20       324   Nearest Relative Mobile Number
PMPXREML  DIM       80     80       344   Nearest Relative Email Address
PMPXRCRP  DIM       1      1        424   Nearest Relative Corres. 0=NO,1=YES
PMPXMEDC  DIM       8      8        425   Medicare Card Valid to (CCYYMMDD)
PMPXDETY  DIM       3      3        433   Death Type (Cat DY)
PMPXPRIS  DIM       15     15       436   PRISM Number
PMPXCSER  DIM       15     15       451   Correctional Services Number
PMPXPHEI  FORM      6      4        466   Patient Height
PMPXPWEI  FORM      6      4        470   Patient Weight
PMPXDREC  DIM       8      8        474   Date Weight & Height Rec.(CCYYMMDD)
PMPXMSNA  DIM       20     20       482   Mother's Surname
PMPXMGNA  DIM       25     25       502   Mother's Given Name
PMPXMTLE  DIM       4      4        527   Mother's Title
PMPXMAD1  DIM       35     35       531   Mother's Address Line 1
PMPXMAD2  DIM       35     35       566   Mother's Address Line 2
PMPXMAD3  DIM       35     35       601   Mother's Address Line 3
PMPXMAD4  DIM       35     35       636   Mother's Address Line 4
PMPXMPOS  DIM       8      8        671   Mother's Address Post Code
PMPXMPNO  DIM       20     20       679   Mother's Private Number
PMPXMBNO  DIM       20     20       699   Mother's Business Number
PMPXMMNO  DIM       20     20       719   Mother's Mobile Number
PMPXMEML  DIM       80     80       739   Mother's Email Address
PMPXMCNT  DIM       3      3        819   Mother's Country of Birth (Cat C)
PMPXMCRP  DIM       1      1        822   Mother's Correspondence  0=NO,1=YES
PMPXMLAB  DIM       1      1        823   Mother's Name on Labels  0=NO,1=YES
PMPXMREL  DIM       10     10       824   Relationship to Patient
PMPXFSNA  DIM       20     20       834   Father's Surname
PMPXFGNA  DIM       25     25       854   Father's Given Name
PMPXFTLE  DIM       4      4        879   Father's Title
PMPXFAD1  DIM       35     35       883   Father's Address Line 1
PMPXFAD2  DIM       35     35       918   Father's Address Line 2
PMPXFAD3  DIM       35     35       953   Father's Address Line 3
PMPXFAD4  DIM       35     35       988   Father's Address Line 4
PMPXFPOS  DIM       8      8        1023  Father's Address Post Code
PMPXFPNO  DIM       20     20       1031  Father's Private Number
PMPXFBNO  DIM       20     20       1051  Father's Business Number
PMPXFMNO  DIM       20     20       1071  Father's Mobile Number
PMPXFEML  DIM       80     80       1091  Father's Email Address
PMPXFCNT  DIM       3      3        1171  Father's Country of Birth (Cat C)
PMPXFCRP  DIM       1      1        1174  Father's Correspondence 0=NO,1=YES
PMPXFLAB  DIM       1      1        1175  Father's Name on Labels 0=NO,1=YES
PMPXFREL  DIM       10     10       1176  Relationship to Patient
PMPXTDIG  DIM       2      2        1186  Terminal Digit (Last 2 chrs of U/R)
PMPXWBID  DIM       10     10       1188  WEB User Id re. creator (websecaf)
PMPXDVAC  DIM       3      3        1198  DVA Card Colour (Cat DX)
PMPXPNSD  DIM       8      8        1201  Pension Start Date (CCYYMMDD)
PMPXUYN4  DIM       1      1        1209  User Defined Y/N field 4 0=NO,1=YES
PMPXUYN5  DIM       1      1        1210  User Defined Y/N field 5 0=NO,1=YES
PMPXUYN6  DIM       1      1        1211  Not for Resusitation Y/N 0=NO,1=YES
PMPXABRG  DIM       3      3        1212  Aboriginality (Cat VA)
PMPXPRVI  DIM       3      3        1215  Privacy Indicator (Cat PV)
PMPXCRIN  DIM       1      1        1218  Consent Release of Info? 0=NO,1=YES
PMPXHOML  DIM       1      1        1219  Homeless? (0=NO,1=YES)
PMPXUDC1  DIM       50     50       1220  User Defined Comments 1
PMPXUDC2  DIM       50     50       1270  User Defined Comments 2
PMPXUDC3  DIM       50     50       1320  User Defined Comments 3
PMPXUSR7  DIM       3      3        1370  User Defined 7 (Category P7)
PMPXUSR8  DIM       3      3        1373  User Defined 8 (Category P8)
PMPXUSR9  DIM       3      3        1376  Advance Care Plan Flag(Category P9)
PMPXMSNO  DIM       10     10       1379  Medicare Safety Net Number
PMPXCONP  DIM       1      1        1389  Which contact for PRA
.                                         0=Patient
.                                         1=Mother (this file)
.                                         2=Father (this file)
.                                         3=Next of Kin (patma1af)
.                                         4=Emergency Contact (patmx1af)
.                                         5=Nearest Relative (patmx1af)
.                                         6=PRA (priinvof)
.                                         7=Postal
PMPXFLDR  DIM       3      3        1390  Folder Selection (Category FS)
PMPXIWI1  DIM       3      3        1393  Iwi 1 (Category iw)
PMPXIWI2  DIM       3      3        1396  Iwi 2 (Category iw)
PMPXIWI3  DIM       3      3        1399  Iwi 3 (Category iw)
PMPXIHOS  DIM       3      3        1402  Hospital which initiated UR
PMPXPFSN  DIM       20     20       1405  Preferred Surname
PMPXPFGN  DIM       25     25       1425  Preferred Given Name
PMPXSIN6  DIM       1      1        1450  Transport Required - 1=Yes
PMPXSIN7  DIM       1      1        1451  Medical Alert (wahealth) - 1=Yes
PMPXSIN8  DIM       1      1        1452  Micro Alert (wahealth) - 1=Yes
PMPXSIN9  DIM       1      1        1453  Risk Alert (wahealth) - 1=Yes
PMPXSN10  DIM       1      1        1454  PCEHR Flag
.                                         0 - Blank
.                                         1 - Not published / unknown PCEHR
.                                         2 - No / non advertised PCEHR
.                                         3 - Yes / advertised PCEHR
PMPXSN11  DIM       1      1        1455  Chronic Condition Alert (wah) 1=Yes
PMPXSN12  DIM       1      1        1456  Patient Debt Only - 1=Yes
PMPXSN13  DIM       1      1        1457  Set Payment Plan - 1=Yes
PMPXSN14  DIM       1      1        1458  Debt Collection Agency - 1=Yes
PMPXSN15  DIM       1      1        1459  Outstanding Debt Ind. 1=Yes
PMPXPFAX  DIM       20     20       1460  Patient Fax Number
PMPXFNAM  DIM       40     40       1480  Patient First Name
PMPXSNAM  DIM       40     40       1520  Patient Second Name
PMPXHFGN  DIM       40     40       1560  Health Fund First Given Name Alias
PMPXHFSN  DIM       40     40       1600  Health Fund Surname Alias
PMPXFUPI  DIM       2      2        1640  Health Fund UPI
PMPXVGPC  DIM       10     10       1642  Verified GP Code (pmshcpaf)
PMPXVGPP  DIM       10     10       1652  Verified GP Practice (pmshcgaf)
PMPXVGPG  DIM       2      2        1662  Verified GP Group Count (pmshclaf)
PMPXASNO  DIM       15     15       1664  Ambulance Subscriber No.
PMPXMAID  DIM       20     20       1679  Maiden Name
PMPXETHN  DIM       3      3        1699  Ethnicity (Cat EN)
PMPXHFCN  DIM       40     40       1702  Health Fund Contributor Name
PMPXLUBH  DIM       3      3        1742  Last Updated By Hospital (pathspaf)
PMPXPMCN  DIM       3      3        1745  Preferred Method of Contact (Cat Pn
PMPXPMCR  DIM       3      3        1748  Preferred Method of Correspondence
PMPXCRN1  DIM       1      1        1751  Consent Release of info to HCP1
.                                         0=No
.                                         1=Yes
PMPXCRN2  DIM       1      1        1752  Consent Release of info to HCP2
.                                         0=No
.                                         1=Yes
PMPXCRN3  DIM       1      1        1753  Consent Release of info to HCP3
.                                         0=No
.                                         1=Yes
PMPXCRN4  DIM       1      1        1754  Consent Release of info to HCP4
.                                         0=No
.                                         1=Yes
PMPXCRN5  DIM       1      1        1755  Consent Release of info to HCP5
.                                         0=No
.                                         1=Yes
PMPXOSMS  DIM       1      1        1756  Opt out of SMS
.                                         0=No
.                                         1=Yes
PMPXRPER  DIM       3      3        1757  Retention Period (Cat rt)
PMPXRDAT  DIM       8      8        1760  Retention Date (ccyymmdd)
PMPXUPRF  DIM       3      3        1768  Usual PRFA Coded (Cat tc)
PMPXUUID  DIM       10     10       1771  User Id who Last Updated Record
.                                         (websecaf)
PMPXUDAT  DIM       8      8        1781  Date Record Last Updated (ccyymmdd)
PMPXUTIM  DIM       8      8        1789  Time Record Last Updated (hh:mm:ss)
PMPXSN16  DIM       1      1        1797  Status Indicator 16
PMPXSN17  DIM       1      1        1798  Status Indicator 17
PMPXSN18  DIM       1      1        1799  Status Indicator 18
PMPXSN19  DIM       1      1        1800  Status Indicator 19
PMPXSN20  DIM       1      1        1801  Status Indicator 20
PMPXSN21  DIM       1      1        1802  Status Indicator 21
PMPXSN22  DIM       1      1        1803  Status Indicator 22
PMPXSN23  DIM       1      1        1804  Status Indicator 23
PMPXSN24  DIM       1      1        1805  Status Indicator 24
PMPXSN25  DIM       1      1        1806  Status Indicator 25
PMPXSN26  DIM       1      1        1807  Status Indicator 26
PMPXSN27  DIM       1      1        1808  Status Indicator 27
PMPXSN28  DIM       1      1        1809  Status Indicator 28
PMPXSN29  DIM       1      1        1810  Status Indicator 29
PMPXSN30  DIM       1      1        1811  Status Indicator 30
PMPXUDT1  DIM       8      8        1812  User Defined Date 1 (ccyymmdd)
PMPXUTM1  DIM       8      8        1820  User Defined Time 1 (hh:mm:ss)
PMPXUDT2  DIM       8      8        1828  User Defined Date 2 (ccyymmdd)
PMPXUTM2  DIM       8      8        1836  User Defined Time 2 (hh:mm:ss)
PMPXUDT3  DIM       8      8        1844  User Defined Date 3 (ccyymmdd)
PMPXUTM3  DIM       8      8        1852  User Defined Time 3 (hh:mm:ss)
PMPXUDT4  DIM       8      8        1860  User Defined Date 4 (ccyymmdd)
PMPXUTM4  DIM       8      8        1868  User Defined Time 4 (hh:mm:ss)
PMPXUDT5  DIM       8      8        1876  User Defined Date 5 (ccyymmdd)
PMPXUTM5  DIM       8      8        1884  User Defined Time 5 (hh:mm:ss)
PMPXUCC1  DIM       3      3        1892  User Defined Category Code 1
PMPXUCC2  DIM       3      3        1895  User Defined Category Code 2
PMPXUCC3  DIM       3      3        1898  User Defined Category Code 3
PMPXUCC4  DIM       3      3        1901  User Defined Category Code 4
PMPXUCC5  DIM       3      3        1904  User Defined Category Code 5
PMPXAHC1  DIM       10     10       1907  Additional HCP 1
PMPXAHG1  DIM       10     10       1917  Additional HCP Group 1
PMPXAGC1  DIM       2      2        1927  Additional HCP Group Count 1
PMPXAHC2  DIM       10     10       1929  Additional HCP 2
PMPXAHG2  DIM       10     10       1939  Additional HCP Group 2
PMPXAGC2  DIM       2      2        1949  Additional HCP Group Count 2
PMPXAUF1  DIM       1      1        1951  Additional User Defined Field 1
PMPXAUF2  DIM       1      1        1952  Additional User Defined Field 2
PMPXAUF3  DIM       1      1        1953  Additional User Defined Field 3
PMPXAUF4  DIM       1      1        1954  Additional User Defined Field 4
PMPXAUF5  DIM       1      1        1955  Additional User Defined Field 5
PMPXAUF6  DIM       1      1        1956  Additional User Defined Field 6
PMPXAUF7  DIM       1      1        1957  Additional User Defined Field 7
PMPXAUF8  DIM       1      1        1958  Additional User Defined Field 8
PMPXAUF9  DIM       1      1        1959  Additional User Defined Field 9
PMPXAU10  DIM       1      1        1960  Additional User Defined Field 10
PMPXATF1  DIM       80     80       1961  Additional Text Field 1
PMPXATF2  DIM       80     80       2041  Additional Text Field 2
PMPXATF3  DIM       80     80       2121  Additional Text Field 3
PMPXATF4  DIM       80     80       2201  Additional Text Field 4
PMPXATF5  DIM       80     80       2281  Additional Text Field 5
PMPXSPAR  DIM       50     50       2361  Spare
.
.End of Record                      2411
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       200
.
DEFPATH   DIM       60
DIM60     DIM       60
DISNUM    FORM      4
.
INPFILE   DIM       8
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
SAVEFLG   FORM      1
.
.
. PROGRAM CONSTANTS
. -----------------
SP60      INIT    "                                                            "
.
.
.
PRGNAM    INIT      "CONVERSION PMSPX2FD"
PRGID     INIT      "F09PMPX2"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000              * run c-isam fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
MAIN2000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN0000:               * no
                          MAIN9999                * cancel
.
MAIN5000  BRANCH    COPTION,MAIN6000              * c-isam fixit
.
.         Running c-isam fix
.
MAIN6000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/zip saved file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
          
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run c-isam fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
.
PREP0000  MOVE      "pmspx2af",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat pmspx2af.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "spmpx2af.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat pmspx2af.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "spmpx2af.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "pmspx2af",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "spmpx2af",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "pmspx2af",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " 2411 UC1-8",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      PMSPX2A1,NEWFILE
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
            ELSE
              DISPLAY   "has caused an IO error - ";
            ENDIF
          ENDIF
          DISPLAY   "File will be bypassed"
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY8,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MOVE      SP60,PMPXSN16
          MOVE      SP60,PMPXSN17
          MOVE      SP60,PMPXSN18
          MOVE      SP60,PMPXSN19
          MOVE      SP60,PMPXSN20
          MOVE      SP60,PMPXSN21
          MOVE      SP60,PMPXSN22
          MOVE      SP60,PMPXSN23
          MOVE      SP60,PMPXSN24
          MOVE      SP60,PMPXSN25
          MOVE      SP60,PMPXSN26
          MOVE      SP60,PMPXSN27
          MOVE      SP60,PMPXSN28
          MOVE      SP60,PMPXSN29
          MOVE      SP60,PMPXSN30
          MOVE      SP60,PMPXUDT1
          MOVE      SP60,PMPXUTM1
          MOVE      SP60,PMPXUDT2
          MOVE      SP60,PMPXUTM2
          MOVE      SP60,PMPXUDT3
          MOVE      SP60,PMPXUTM3
          MOVE      SP60,PMPXUDT4
          MOVE      SP60,PMPXUTM4
          MOVE      SP60,PMPXUDT5
          MOVE      SP60,PMPXUTM5
          MOVE      SP60,PMPXUCC1
          MOVE      SP60,PMPXUCC2
          MOVE      SP60,PMPXUCC3
          MOVE      SP60,PMPXUCC4
          MOVE      SP60,PMPXUCC5
          MOVE      SP60,PMPXAHC1
          MOVE      SP60,PMPXAHG1
          MOVE      SP60,PMPXAGC1
          MOVE      SP60,PMPXAHC2
          MOVE      SP60,PMPXAHG2
          MOVE      SP60,PMPXAGC2
          MOVE      SP60,PMPXAUF1
          MOVE      SP60,PMPXAUF2
          MOVE      SP60,PMPXAUF3
          MOVE      SP60,PMPXAUF4
          MOVE      SP60,PMPXAUF5
          MOVE      SP60,PMPXAUF6
          MOVE      SP60,PMPXAUF7
          MOVE      SP60,PMPXAUF8
          MOVE      SP60,PMPXAUF9
          MOVE      SP60,PMPXAU10
          PACK      PMPXATF1,SP60,SP60
          PACK      PMPXATF2,SP60,SP60
          PACK      PMPXATF3,SP60,SP60
          PACK      PMPXATF4,SP60,SP60
          PACK      PMPXATF5,SP60,SP60
          PACK      PMPXSPAR,SP60
.
          CALL      WRPMPX21                     * write to new file
.
          GOTO      READ1000                     * get next record
.
READ6000  CLOSE     PMSPX2A1                     * close new file
          CLOSE     OLDFILE1                     * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS:
                    *P1:24;
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
.
VALD0000  MOVE      ZERO,EXIT
.
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or zip save file                                     *
.**********************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "spmpx2af*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "gzip -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not zipped or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:24,*EL,*B,"Finished processing  ",CDATE,SP1,CTIMEIS:
                    ".  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.*        DEFT0000 -  Get the default directory                       *
.**********************************************************************
.
DEFT0000  EXECUTE   "ldat pmspx2af.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding 'pmspx2af'.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
.         Old I/O Routines
.
READSOLD  READ      OLDFILE1,KEY8;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;PMPXURNO,PMPXRHC1,PMPXRH1G,PMPXR1GC,PMPXINTR:
                        PMPXLNG1,PMPXLNG2,PMPXLNG3,PMPXPEML,PMPXEDOB,PMPXCMOB:
                        PMPXCEML,PMPXCCRP,PMPXKMOB,PMPXKEML,PMPXKCRP,PMPXRMOB:
                        PMPXREML,PMPXRCRP,PMPXMEDC,PMPXDETY,PMPXPRIS,PMPXCSER:
                        PMPXPHEI,PMPXPWEI,PMPXDREC,PMPXMSNA,PMPXMGNA,PMPXMTLE:
                        PMPXMAD1,PMPXMAD2,PMPXMAD3,PMPXMAD4,PMPXMPOS,PMPXMPNO:
                        PMPXMBNO,PMPXMMNO,PMPXMEML,PMPXMCNT,PMPXMCRP,PMPXMLAB:
                        PMPXMREL,PMPXFSNA,PMPXFGNA,PMPXFTLE,PMPXFAD1,PMPXFAD2:
                        PMPXFAD3,PMPXFAD4,PMPXFPOS,PMPXFPNO,PMPXFBNO,PMPXFMNO:
                        PMPXFEML,PMPXFCNT,PMPXFCRP,PMPXFLAB,PMPXFREL,PMPXTDIG:
                        PMPXWBID,PMPXDVAC,PMPXPNSD,PMPXUYN4,PMPXUYN5,PMPXUYN6:
                        PMPXABRG,PMPXPRVI,PMPXCRIN,PMPXHOML,PMPXUDC1,PMPXUDC2:
                        PMPXUDC3,PMPXUSR7,PMPXUSR8,PMPXUSR9,PMPXMSNO,PMPXCONP:
                        PMPXFLDR,PMPXIWI1,PMPXIWI2,PMPXIWI3,PMPXIHOS,PMPXPFSN:
                        PMPXPFGN,PMPXSIN6,PMPXSIN7,PMPXSIN8,PMPXSIN9,PMPXSN10:
                        PMPXSN11,PMPXSN12,PMPXSN13,PMPXSN14,PMPXSN15,PMPXPFAX:
                        PMPXFNAM,PMPXSNAM,PMPXHFGN,PMPXHFSN,PMPXFUPI,PMPXVGPC:
                        PMPXVGPP,PMPXVGPG,PMPXASNO,PMPXMAID,PMPXETHN,PMPXHFCN:
                        PMPXLUBH,PMPXPMCN,PMPXPMCR,PMPXCRN1,PMPXCRN2,PMPXCRN3:
                        PMPXCRN4,PMPXCRN5,PMPXOSMS,PMPXRPER,PMPXRDAT,PMPXUPRF:
                        PMPXUUID,PMPXUDAT,PMPXUTIM,PMPXSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
.         New I/O Routines
.
WRPMPX21  RESET     KEY8
          WRITE     PMSPX2A1,KEY8;PMPXURNO,PMPXRHC1,PMPXRH1G,PMPXR1GC,PMPXINTR:
                                  PMPXLNG1,PMPXLNG2,PMPXLNG3,PMPXPEML,PMPXEDOB:
                                  PMPXCMOB,PMPXCEML,PMPXCCRP,PMPXKMOB,PMPXKEML:
                                  PMPXKCRP,PMPXRMOB,PMPXREML,PMPXRCRP,PMPXMEDC:
                                  PMPXDETY,PMPXPRIS,PMPXCSER,PMPXPHEI,PMPXPWEI:
                                  PMPXDREC,PMPXMSNA,PMPXMGNA,PMPXMTLE,PMPXMAD1:
                                  PMPXMAD2,PMPXMAD3,PMPXMAD4,PMPXMPOS,PMPXMPNO:
                                  PMPXMBNO,PMPXMMNO,PMPXMEML,PMPXMCNT,PMPXMCRP:
                                  PMPXMLAB,PMPXMREL,PMPXFSNA,PMPXFGNA,PMPXFTLE:
                                  PMPXFAD1,PMPXFAD2,PMPXFAD3,PMPXFAD4,PMPXFPOS:
                                  PMPXFPNO,PMPXFBNO,PMPXFMNO,PMPXFEML,PMPXFCNT:
                                  PMPXFCRP,PMPXFLAB,PMPXFREL,PMPXTDIG,PMPXWBID:
                                  PMPXDVAC,PMPXPNSD,PMPXUYN4,PMPXUYN5,PMPXUYN6:
                                  PMPXABRG,PMPXPRVI,PMPXCRIN,PMPXHOML,PMPXUDC1:
                                  PMPXUDC2,PMPXUDC3,PMPXUSR7,PMPXUSR8,PMPXUSR9:
                                  PMPXMSNO,PMPXCONP,PMPXFLDR,PMPXIWI1,PMPXIWI2:
                                  PMPXIWI3,PMPXIHOS,PMPXPFSN,PMPXPFGN,PMPXSIN6:
                                  PMPXSIN7,PMPXSIN8,PMPXSIN9,PMPXSN10,PMPXSN11:
                                  PMPXSN12,PMPXSN13,PMPXSN14,PMPXSN15,PMPXPFAX:
                                  PMPXFNAM,PMPXSNAM,PMPXHFGN,PMPXHFSN,PMPXFUPI:
                                  PMPXVGPC,PMPXVGPP,PMPXVGPG,PMPXASNO,PMPXMAID:
                                  PMPXETHN,PMPXHFCN,PMPXLUBH,PMPXPMCN,PMPXPMCR:
                                  PMPXCRN1,PMPXCRN2,PMPXCRN3,PMPXCRN4,PMPXCRN5:
                                  PMPXOSMS,PMPXRPER,PMPXRDAT,PMPXUPRF,PMPXUUID:
                                  PMPXUDAT,PMPXUTIM,PMPXSN16,PMPXSN17,PMPXSN18:
                                  PMPXSN19,PMPXSN20,PMPXSN21,PMPXSN22,PMPXSN23:
                                  PMPXSN24,PMPXSN25,PMPXSN26,PMPXSN27,PMPXSN28:
                                  PMPXSN29,PMPXSN30,PMPXUDT1,PMPXUTM1,PMPXUDT2:
                                  PMPXUTM2,PMPXUDT3,PMPXUTM3,PMPXUDT4,PMPXUTM4:
                                  PMPXUDT5,PMPXUTM5,PMPXUCC1,PMPXUCC2,PMPXUCC3:
                                  PMPXUCC4,PMPXUCC5,PMPXAHC1,PMPXAHG1,PMPXAGC1:
                                  PMPXAHC2,PMPXAHG2,PMPXAGC2,PMPXAUF1,PMPXAUF2:
                                  PMPXAUF3,PMPXAUF4,PMPXAUF5,PMPXAUF6,PMPXAUF7:
                                  PMPXAUF8,PMPXAUF9,PMPXAU10,PMPXATF1,PMPXATF2:
                                  PMPXATF3,PMPXATF4,PMPXATF5,PMPXSPAR
          RETURN
.
          INC       STD001IO
