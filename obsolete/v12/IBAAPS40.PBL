. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAAPS40                                             *
. * Desc      :   Creditors/Supplier Label Printing                    *
. **********************************************************************
. * Date      :                                                        *
. * Author    :                                                        *
. * Mods      :                                                        *
. **********************************************************************
.  V10.02.01  07/04/2011 Mike Laming for Jill CAR 239574
.             Remove CALL to ' IBAPRNT1,"ibaprntf" '
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
. * VF.01.01  30.10.2000 Sandra Barcham                                *
. *           Recompiled for IBACODIO & IBAPRNIO                       *
. **********************************************************************
. * VF.00.02  05.01.2000 Charles Handaya                               *
. *           Recompiled for APSMASIO change                           *
. * VF.00.01  02.09.1999 Sandra Barcham                                *
. *           Add in Creditor Keyword Search                           *
. *           srf 631485                                               *
. **********************************************************************
.
          INC       FMSSTDDF
          INC       IBAPRNFD/INC           * Default Printers and Next Spool id
          INC       APSMASFD/INC
          INC       APSMXRFD/INC
.
ACCEPT    FORM      1
BLCNT     FORM      3
COLLUMN   FORM      1
COLPR     FORM      "40"
CREDSUPP  DIM       1
CRSUDSC   DIM       30
COUNTER   FORM      3
FIELD     FORM      1
FORM3     FORM      3
FSTCRED   DIM       5
FSTNAME   DIM       6
ISUCCRED  FORM      1
LABSPRTD  FORM      3
LACCROSS  FORM      3
LSTCRED   DIM       5
LSTNAME   DIM       6
NCSEQ     DIM       1
NCSEQDSC  DIM       13
NOLABS    FORM      3
PLINE1    DIM       35
PLINE2    DIM       35
PLINE3    DIM       35
PLINE4    DIM       35
PLINE5    DIM       35
PLLINE1   DIM       35
PLLINE2   DIM       35
PLLINE3   DIM       35
PLLINE4   DIM       35
PLLINE5   DIM       35
PRLINE1   DIM       35
PRLINE2   DIM       35
PRLINE3   DIM       35
PRLINE4   DIM       35
PRLINE5   DIM       35
POST4     DIM       4
POST6     DIM       6
SAVCESU   DIM       1
SAVSEQ    DIM       1
SEQTYPE   FORM      1
SP40      INIT      "                                        "
TMPPRTD   FORM      2
UNIQCNT   FORM      3
XTRAL     FORM      1
YESNOSC   DIM       3
.
XX40      INIT      "XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX"
.
PRGID     INIT      "IBAAPS40"
PRGNAM    INIT      "Creditors/Suppliers Label Printing"
VERSION   INIT      "V12.00.00"
+
.****************************************************************************
.*  ML0000  :  Mainline                                                     *
.****************************************************************************
ML0000    CALL      INIT0000                      * initialisation
          CALL      SELPRINT                      * select printer
.
ML0100    CALL      MENU0000                      * Main Menu
          BRANCH    EXIT,ML9999                   * Exit
.
          BRANCH    MOPTION,ML1000,ML2000         * execute option
.
.---- print labels ----
.
ML1000    CALL      LOPT0000                      * Keyin Printing Options
          BRANCH    EXIT,ML0100                   * Exit/Cancel selected
.
          CALL      OPNPRINT
.
          CALL      PLAB0000                      * print lineup
.
          CALL      CLSPRINT
          GOTO      ML0100
.
.---- print labels ----
.
ML2000    CALL      OPNPRINT
.
          CALL      SDAT0000                      * set for lineup print
          CALL      PRNL0000                      * print lineup
          CALL      CDAT0000                      * clear label data
.
          CALL      CLSPRINT
          GOTO      ML0100
.
ML9999
          CHAIN     PGM
+
.****************************************************************************
.*  INIT0000  :  Initialisation                                             *
.****************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P64:24,"apsmasaf";
          OPEN      APSMASA1,"apsmasaf"
.
          DISPLAY   *P64:24,"apsmasaf";
          OPEN      APSMASA3,"apsmasaf"
.
          DISPLAY   *P64:24,"apsmxraf";
          OPEN      APSMXRA1,"apsmxraf"
.
          DISPLAY   *P64:24,"ibacodef";
          OPEN      IBACODE1,"ibacodef"
.
...       DISPLAY   *P64:24,"ibaprntf";
...       OPEN      IBAPRNT1,"ibaprntf"                               *D-249574
.
          MOVE      ZERO,BLCNT
          CALL      CDAT0000                      * clear label data
          MOVE      ONE,COLLUMN                   * start at left collumn
.
INIT9999  RETURN
+
.****************************************************************************
.*  MENU0000  :  Main Menu                                                  *
.****************************************************************************
MENU0000  MOVE      ONE,COLLUMN                   * start at left collumn
.
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = Print Labels":
                    *P1:6,*V2LON," 2",*HOFF," = Lineup Print":
                    *P1:8,"Select Option ? ";
.
MENU1000  KEYIN     *P17:8,*DV,UNDLN1:
                    *P17:8,*V2LON,MOPTION
.
          BRANCH    MOPTION,MENU8000,MENU8000
.
          COMPARE   ZERO,MOPTION
          GOTO      MENU9000 IF EQUAL
.
          BEEP
          GOTO      MENU1000
.
MENU8000  MOVE      ZERO,EXIT                     * Print Labels Selected
          GOTO      MENU9999
.
MENU9000  MOVE      ONE,EXIT                      * Exit
.
MENU9999  RETURN
+
.***************************************************************************
.*  IVAR0000  :  Initialise Variables                                      *
.***************************************************************************
IVAR0000  MOVE      ZERO,ACCEPT                   * init to Insert mode
          MOVE      SP30,CRSUDSC
          MOVE      SP30,NCSEQDSC
          MOVE      SP30,FSTNAME
          MOVE      SP30,LSTNAME
          MOVE      SP3,YESNOSC
          MOVE      SP1,CREDSUPP
.
IVAR9999  RETURN
+
.***************************************************************************
.*  LOPT0000  :  Keyin Printing Options                                    *
.***************************************************************************
LOPT0000  CALL      IVAR0000                      * init variables
.
          CALL      SCRN0000                      * display screen layout
.
LOPT1000  CALL      CSLA0000                      * Keyin Creditor or Supplier?
          BRANCH    EXIT,LOPT9000                 * EXITCHAR
          BRANCH    ACCEPT,LOPT6000               * Ask Select Field.....?
.
LOPT2000  CALL      SEQU0000                      * Keyin Name or Code Sequence
          BRANCH    EXIT,LOPT9000                 * EXITCHAR
          BRANCH    ACCEPT,LOPT6000               * Ask Select Field.....?
.
LOPT3000  CALL      FROM0000                      * Keyin Start From?
          BRANCH    EXIT,LOPT9000                 * EXITCHAR
          BRANCH    ACCEPT,LOPT6000               * Ask Select Field.....?
.
LOPT4000  CALL      ENDA0000                      * Keyin End After?
          BRANCH    EXIT,LOPT9000                 * EXITCHAR
          BRANCH    ACCEPT,LOPT6000               * Ask Select Field.....?
.
LOPT5500  MOVE      ONE,ACCEPT                    * go to Change mode
.
. We are now in Change mode so ask Select Field...
.
LOPT6000  CALL      SELQ0000                      * Ask Select
          BRANCH    EXIT,LOPT8000:                * Print Labels
                         LOPT9000                 * Exit
.
. a field was selected so go to appropriate Keyin routine
.
          BRANCH    FIELD,LOPT1000,LOPT2000,LOPT3000,LOPT4000
.
LOPT8000  MOVE      ZERO,EXIT
          GOTO      LOPT9999

LOPT9000  MOVE      ONE,EXIT                      * Exit/EXITCHAR
.
LOPT9999  RETURN
+
.****************************************************************************
.*  SCRN0000  :  Display Screen layout                                      *
.****************************************************************************
SCRN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 1",*HOFF,". (",*V2LON,"C",*HOFF:
                    ")reditor or (",*V2LON,"S",*HOFF,")upplier Labels  :":
                    *P1:5,*V2LON," 2",*HOFF,". (",*V2LON,"S",*HOFF:
                    ")ort Key or (",*V2LON,"C",*HOFF,")ode Sequence",*P38:5,":":
                    *P1:6,*V2LON," 3",*HOFF,". Start From",*P38:6,":":
                    *P1:7,*V2LON," 4",*HOFF,". End  After",*P38:7,":"
.
SCRN9999  RETURN
+
.****************************************************************************
.*  SELQ0000  :  Ask Select Field...                                        *
.****************************************************************************
SELQ0000  DISPLAY   *P1:24,*EL,"Select Field, (",*V2LON,"P",*HOFF:
                    ")rint Labels, e(",*V2LON,"X",*HOFF,")it ? ";
.
SELQ1000  KEYIN     *P40:24,*DV,UNDLN1:
                    *P40:24,*V2LON,ANS
.
          TYPE      ANS                           * Form field input?
          GOTO      SELQ3000 IF EQUAL
.
          REP       UPPLOW,ANS
.
          MATCH     ANS,ANSP
          GOTO      SELQ8000 IF EQUAL
.
          MATCH     ANS,ANSX
          GOTO      SELQ9000 IF EQUAL
.
          BEEP
          GOTO      SELQ1000
.
SELQ3000  MOVE      ANS,FIELD                     * move to form field
          MOVE      ZERO,EXIT
          BRANCH    FIELD,SELQ9999,SELQ9999,SELQ9999,SELQ9999
          BEEP
          GOTO      SELQ1000
.
SELQ8000  MOVE      ONE,EXIT
          GOTO      SELQ9999
.
SELQ9000  MOVE      TWO,EXIT
.
SELQ9999  RETURN
+
.****************************************************************************
.*  CSLA0000  :  Keyin Creditor or Supplier?                                *
.****************************************************************************
CSLA0000  MOVE      CREDSUPP,SAVCESU              * save old value
.
          KEYIN     *P40:4,*EL,*DV,UNDLN1:
                    *P40:4,*V2LON,CREDSUPP
.
          REP       UPPLOW,CREDSUPP
.
          MATCH     ANSC,CREDSUPP
          GOTO      CSLA6000 IF EQUAL
.
          MATCH     ANSS,CREDSUPP
          GOTO      CSLA7000 IF EQUAL
.
          MATCH     EXITCHAR,CREDSUPP
          GOTO      CSLA9000 IF EQUAL
.
          BEEP
          GOTO      CSLA0000
.
CSLA6000  DISPLAY   *P40:4,*V2LON,"Creditor"
          MOVE      "Creditor",CRSUDSC            * save description
          GOTO      CSLA8000
.
CSLA7000  DISPLAY   *P40:4,*V2LON,"Supplier"
          MOVE      "Supplier",CRSUDSC            * save description
.
CSLA8000  MOVE      ZERO,EXIT
          GOTO      CSLA9999
.
CSLA9000  MOVE      ONE,EXIT                      * EXITCHAR
.
CSLA9999  RETURN
+
.****************************************************************************
.*  DSEQ0000  :  Erase fields                                               *
.****************************************************************************
DSEQ0000  DISPLAY   *P40:6,*EL,*P40:7,*EL,*P40:8,*EL,*P1:24,*EL
.
          MOVE      ZERO,ACCEPT
          MOVE      SP30,FSTNAME
          MOVE      SP30,LSTNAME
          MOVE      SP3,YESNOSC
          MOVE      SP5,LSTCRED
          MOVE      SP30,LSTNAME
.
DSEQ9999  RETURN
+
.****************************************************************************
.*  SEQU0000  :  Keyin Name or Code Sequence ?                              *
.****************************************************************************
SEQU0000  MOVE      NCSEQ,SAVSEQ                  * save sequence
.
          KEYIN     *P40:5,*EL,*DV,UNDLN1:
                    *P40:5,*V2LON,NCSEQ
.
          REP       UPPLOW,NCSEQ
.
          MATCH     ANSS,NCSEQ
          GOTO      SEQU6000 IF EQUAL
.
          MATCH     ANSC,NCSEQ
          GOTO      SEQU7000 IF EQUAL
.
          MATCH     EXITCHAR,NCSEQ
          GOTO      SEQU9000 IF EQUAL
.
          BEEP
          GOTO      SEQU0000
.
SEQU6000  DISPLAY   *P40:5,*V2LON,"Sort Key Sequence"
          MOVE      "Sort Key Sequence",NCSEQDSC           * save description
          MOVE      ZERO,EXIT
          GOTO      SEQU8000
.
SEQU7000  DISPLAY   *P40:5,*V2LON,"Code sequence"
          MOVE      "Code sequence",NCSEQDSC               * save description
          MOVE      ZERO,EXIT
.
SEQU8000  MATCH     NCSEQ,SAVSEQ                  * sequence changed?
          GOTO      SEQU9999 IF EQUAL
          CALL      DSEQ0000                      * erase fields
          GOTO      SEQU9999
.
SEQU9000  MOVE      ONE,EXIT                      * EXITCHAR
.
SEQU9999  RETURN
+
.****************************************************************************
.*  FROM0000  :  Keyin Start From                                           *
.****************************************************************************
FROM0000  MATCH     ANSC,NCSEQ                    * Code sequence
          GOTO      FROM5000 IF EQUAL
.
. Keyin Sort Key (Start)
.
          KEYIN     *P40:6,*DV,UNDLN6,*EL:
                    *P40:6,*V2LON,FSTNAME:
                    *P40:6,*DV,FSTNAME
.
          ENDSET    FSTNAME
          APPEND    SP6,FSTNAME
          RESET     FSTNAME
.
          MATCH     EXITCHAR,FSTNAME
          GOTO      FROM5600 IF EQUAL
.
          MOVE      ZERO,EXIT
          MATCH     SP6,FSTNAME                  * anything input?
          GOTO      FROM3000 IF NOT EQUAL
.
          DISPLAY   *P40:6,*V2LON,"Start"
          GOTO      FROM9999
.
. check if a valid range
.
FROM3000  MATCH     LSTNAME,SP6
          GOTO      FROM9999 IF EQUAL
          MATCH     FSTNAME,LSTNAME
          GOTO      FROM9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid Range.  ";
          CALL      HITENTER
          GOTO      FROM0000
.
. Keyin Supplier/Creditor code
.
FROM5000  MOVE      "40",CCOL
          MOVE      "6",CVERT
          CALL      KAPMAA00
          BRANCH    EXIT,FROM5200,FROM5600
.
          MATCH     SP5,LSTCRED
          GOTO      FROM5100 IF EQUAL
          MATCH     APMACRD,LSTCRED               * in range?
          GOTO      FROM5100 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid Range.  ";
          CALL      HITENTER
          GOTO      FROM5000
.
FROM5100  MOVE      APMACRD,FSTCRED                * valid code input
          MOVE      APMACN1,FSTNAME
          DISPLAY   *P40:6,*V2LON,APMACRD,*HOFF,SP4,APMACN1
          GOTO      FROM9999
.
FROM5200  MOVE      SP5,FSTCRED                   * nothing input
          DISPLAY   *P40:6,*V2LON,"Start"
          MOVE      ZERO,EXIT
          GOTO      FROM9999
.
FROM5600  MOVE      ONE,EXIT                      * EXITCHAR
          GOTO      FROM9999
.
FROM9999  RETURN
+
.****************************************************************************
.*  ENDA0000  :  Keyin End After                                            *
.****************************************************************************
ENDA0000  MATCH     ANSC,NCSEQ                    * Code sequence
          GOTO      ENDA5000 IF EQUAL
.
. Keyin Sort Key (End)
.
          KEYIN     *P40:7,*DV,UNDLN6,*EL:
                    *P40:7,*V2LON,LSTNAME:
                    *P40:7,*DV,LSTNAME
.
          ENDSET    LSTNAME
          APPEND    SP6,LSTNAME
          RESET     LSTNAME
.
          MATCH     EXITCHAR,LSTNAME              * EXITCHAR?
          GOTO      ENDA5600 IF EQUAL
.
          MOVE      ZERO,EXIT
          MATCH     SP6,LSTNAME                  * anything input?
          GOTO      ENDA3000 IF NOT EQUAL
.
          DISPLAY   *P40:7,*V2LON,"Finish"
          MOVE      "~~~~~~",LSTNAME
          GOTO      ENDA9999
.
. check if a valid range
.
ENDA3000  MATCH     FSTNAME,LSTNAME
          GOTO      ENDA9999 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid Range.  ";
          CALL      HITENTER
          GOTO      ENDA0000
.
. Keyin Supplier/Creditor code
.
ENDA5000  MOVE      "40",CCOL
          MOVE      "7",CVERT
          MOVE      ZERO,CKEYTYP
          CALL      KAPMAA00
          BRANCH    EXIT,ENDA5200,ENDA5600
.
          MATCH     FSTCRED,APMACRD                * in range?
          GOTO      ENDA5100 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"Invalid Range.  ";
          CALL      HITENTER
          GOTO      ENDA5000
.
ENDA5100  MOVE      APMACRD,LSTCRED                * valid code input
          DISPLAY   *P40:7,*V2LON,APMACRD,*HOFF,SP4,APMACN1
          GOTO      ENDA9999
.
ENDA5200  MOVE      "~~~~~",LSTCRED               * nothing input
          DISPLAY   *P40:7,*V2LON,"Finish"
          MOVE      ZERO,EXIT
          GOTO      ENDA9999
.
ENDA5600  MOVE      ONE,EXIT                      * EXITCHAR
          GOTO      ENDA9999
.
ENDA9999  RETURN
+
.****************************************************************************
.*  RCRAIB00  :  Redisplay routine for Cred/Supp code input routine         *
.****************************************************************************
RCRAIB00  CALL      SCRN0000
.
          DISPLAY   *P40:4,*V2LON,CRSUDSC:
                    *P40:5,NCSEQDSC
.
          MATCH     ANSC,NCSEQ                    * Code sequence
          GOTO      RCRAIB10 IF EQUAL
.
          DISPLAY   *P40:6,*V2LON,FSTNAME,*P40:7,LSTNAME
          GOTO      RCRAIB70
.
RCRAIB10  MATCH     SP5,FSTCRED
          GOTO      RCRAIB30 IF EQUAL
.
          DISPLAY   *P40:6,*V2LON,FSTCRED,SP4,*HOFF,FSTNAME
          GOTO      RCRAIB40
.
RCRAIB30  DISPLAY   *P40:6,*V2LON,"Start"
.
RCRAIB40  MATCH     "~~~~~",LSTCRED
          GOTO      RCRAIB50 IF EQUAL
.
          DISPLAY   *P40:7,*V2LON,LSTCRED,SP4,*HOFF,LSTNAME
          GOTO      RCRAIB70
.
RCRAIB50  DISPLAY   *P40:7,*V2LON,"Finish"
.
RCRAIB70  DISPLAY   *P40:8,*V2LON,YESNOSC
.
RCRAIB99  RETURN
+
.***************************************************************************
.*  PLAB0000  :  Print Labels                                              *
.***************************************************************************
PLAB0000  DISPLAY   *P1:24,*EL,"How many Labels do you want to print ? "
.
          MOVE      ZERO,UNIQCNT
          MOVE      ONE,NOLABS                    * default
          KEYIN     *P40:24,*DV,NOLABS:
                    *P40:24,*RV,NOLABS:
                    *P40:24,*DV,*V2LON,NOLABS
.
          COMPARE   ZERO,NOLABS
          GOTO      PLAB9999 IF EQUAL
.
. we desire to Print at least 1 label
.
PLAB0500  MATCH     ANSC,NCSEQ                    * Name or Code sequence?
          GOTO      PLAB1000 IF EQUAL
.
. --- Sort Key Sequence ---
.
PLAB0650  MOVE      ONE,SEQTYPE
          PACK      KEY11,FSTNAME,SP5
          CALL      RSAPMA3
.
PLAB0700  CALL      RKAPMA3
          BRANCH    OVRCD,PLAB9000
.
          MATCH     APMASKY,LSTNAME
          GOTO      PLAB9000 IF LESS
          GOTO      PLAB2000                      * go ahead and print
.
. --- Get Creditors in Code sequence ---
.
PLAB1000  MOVE      TWO,SEQTYPE
          PACK      KEY5,FSTCRED,SP5              * Loop thru Creditors
          CALL      RDAPMA1
          BRANCH    OVRCD,PLAB1200
          GOTO      PLAB1210
.
PLAB1200  CALL      RKAPMA1
          BRANCH    OVRCD,PLAB9000
.
PLAB1210  MATCH     FSTCRED,APMACRD                * In range?
          GOTO      PLAB1200 IF LESS
          MATCH     APMACRD,LSTCRED
          GOTO      PLAB9000 IF LESS
.
. we have a record so print x labels for current record
.
PLAB2000  CALL      GDAT0000
          BRANCH    EXIT,PLAB3200
.
. print desired number of labels
.
          MOVE      ZERO,COUNTER
.
PLAB3000  ADD       ONE,COUNTER
          COMPARE   COUNTER,NOLABS
          GOTO      PLAB3200 IF LESS
.
          STORE     PLINE1,COLLUMN,PLLINE1,PRLINE1    * Copy Line 1
          STORE     PLINE2,COLLUMN,PLLINE2,PRLINE2    * Copy Line 2
          STORE     PLINE3,COLLUMN,PLLINE3,PRLINE3    * Copy Line 3
          STORE     PLINE4,COLLUMN,PLLINE4,PRLINE4    * Copy Line 4
          STORE     PLINE5,COLLUMN,PLLINE5,PRLINE5    * Copy Line 5
.
          ADD       ONE,COLLUMN                       * move to next collumn
          COMPARE   "3",COLLUMN                       * ready to print ?
          GOTO      PLAB3000 IF LESS
.
          MOVE      ONE,COLLUMN                       * print labels
          CALL      PRNL0000
          CALL      CDAT0000                          * clear label data
          GOTO      PLAB3000
.
PLAB3200  BRANCH    SEQTYPE,PLAB0700,PLAB1200         * get next rec
.
PLAB9000  COMPARE   ONE,COLLUMN                   * anything waiting to print ?
          CALL      PRNL0000 IF NOT EQUAL
.
PLAB9999  RETURN
+
.****************************************************************************
.*  GDAT0000  :  Get data for label                                         *
.****************************************************************************
GDAT0000  MATCH     ANSC,CREDSUPP                 * Creditor or Suppliers?
          GOTO      GDAT2000 IF EQUAL
.
. Get Suppliers data
.
          UNPACK    APMASPC,POST6                 * move to shorter length
          CALL      SETP0000                      * set up post code
          PACK      PLINE1,SP2,APMASN1,SP3        * label
          PACK      PLINE2,SP2,APMASN2,SP3
          PACK      PLINE3,SP2,APMASA1,SP3
          PACK      PLINE4,SP2,APMASA2,SP3
          PACK      PLINE5,SP2,APMASA3,SP1,POST4,SP3
          GOTO      GDAT9000
.
. Get Creditors data
.
GDAT2000  
          UNPACK    APMACPC,POST6                 * move to shorter length
          CALL      SETP0000                      * set up post code
          PACK      PLINE1,SP2,APMACN1,SP3        * label
          PACK      PLINE2,SP2,APMACN2,SP3
          PACK      PLINE3,SP2,APMACA1,SP3
          PACK      PLINE4,SP2,APMACA2,SP3
          PACK      PLINE5,SP2,APMACA3,SP1,POST4,SP3
          GOTO      GDAT9000
.
GDAT9000  DISPLAY   *P1:24,"Printing : ",*V2LON,PLINE1,*EF;
.
          MATCH     SP70,PLINE1             * skip if nothing to print
          GOTO      GDAT9500 IF NOT EQUAL
          MATCH     SP70,PLINE2
          GOTO      GDAT9500 IF NOT EQUAL
          MATCH     SP70,PLINE3
          GOTO      GDAT9500 IF NOT EQUAL
          MATCH     SP70,PLINE4
          GOTO      GDAT9500 IF NOT EQUAL
          MATCH     SP70,PLINE5
          GOTO      GDAT9500 IF NOT EQUAL
.
          MOVE      ONE,EXIT
          GOTO      GDAT9999
.
GDAT9500  MOVE      ZERO,EXIT
.
GDAT9999  RETURN
+
.**********************************************************************
.  SETP - Abbreviate postcode                          Called By GDAT
.        Requires - POST6
.        Returns  - POST4
.**********************************************************************
SETP0000  MOVE      POST6,D2
          MATCH     "00",D2
          GOTO      SETP1000 IF EQUAL
          MATCH     "  ",D2
          GOTO      SETP1000 IF EQUAL
.
          UNPACK    POST6,POST4
          GOTO      SETP9999
.
SETP1000  UNPACK    POST6,ANS,ANS,POST4
.
SETP9999  RETURN
.**********************************************************************
.  CDAT - Clear label data                            Called By PLAB
.**********************************************************************
CDAT0000  MOVE      SP40,PLLINE1
          MOVE      SP40,PLLINE2
          MOVE      SP40,PLLINE3
          MOVE      SP40,PLLINE4
          MOVE      SP40,PLLINE5
.
          MOVE      SP40,PRLINE1
          MOVE      SP40,PRLINE2
          MOVE      SP40,PRLINE3
          MOVE      SP40,PRLINE4
          MOVE      SP40,PRLINE5
.
CDAT9999  RETURN
.**********************************************************************
.  SDAT - Set for lineup print                        Called By PLAB
.**********************************************************************
SDAT0000  MOVE      XX40,PLLINE1
          MOVE      XX40,PLLINE2
          MOVE      XX40,PLLINE3
          MOVE      XX40,PLLINE4
          MOVE      XX40,PLLINE5
.
          MOVE      XX40,PRLINE1
          MOVE      XX40,PRLINE2
          MOVE      XX40,PRLINE3
          MOVE      XX40,PRLINE4
          MOVE      XX40,PRLINE5
.
SDAT9999  RETURN
.****************************************************************************
.*  PRNL0000  :  Print 2 labels (LHS and RHS)                               *
.****************************************************************************
PRNL0000  PRINT     *1,PLLINE1,*COLPR,PRLINE1
          PRINT     *1,PLLINE2,*COLPR,PRLINE2
          PRINT     *1,PLLINE3,*COLPR,PRLINE3
          PRINT     *1,PLLINE4,*COLPR,PRLINE4
          PRINT     *1,PLLINE5,*COLPR,PRLINE5
          PRINT     *N,*N,*N
.
PRNL9999  RETURN
.
          INC       FMSSTDCD
          INC       APSCKIKY
          INC       APSMASKY
          INC       APSMASIO/INC
          INC       APSMXRIO/INC
.
          INC       IBAPRINT
          INC       IBAPRNIO/INC           * This IO includes IBAIOSPL/INC
.
