.*****************************************************************************
.*    Program      : FXPATCRD                                                *
.*    Description  : Conversion to new File layout                           *
.*                                                                           *
.*    Author       : Mike Laming   CAR 125991 - Singapore Cancer Reg.        *
.*    Date         : 17/05/2007                                              *
.*    Modifications:                                                         *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001FD
          INC       PATCRDFD/INC                        * new format
.
OLDFILE   IFILE     SQL, FIXED=682, KEY="U1-8,51-59,9-16"
.
CMDLINE   DIM       100
NEWLINNO  DIM       2
RECNUM    FORM      8
OLDSPAR1  DIM       115                           * decreasing to 100
OLDSPAR2  DIM       121                           * decreasing to 100
.
PRGNAM    INIT      "CONVERSION PATCRDFD"
PRGID     INIT      "FXPATCRD"
VERSION   INIT      "V12.00.00"
.*****************************************************************************
.
. Start of Program
.
          CALL      INIT0000                      * init and open files
.
MAIN1000  CALL      OPTN0000
          BRANCH    EXIT,MAIN9999
.
          CALL      OPEN0000
          BRANCH    EXIT,MAIN1000
.
. Loop thru old file and write records to new file
.
MAIN5000  CALL      READ0000                      * read old records and write
          GOTO      MAIN1000
.
MAIN9999  CHAIN     PGM
+
.******************************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.******************************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
.
          MOVE      SP35,PTCDDOCC
          MOVE      SP35,PTCDDOCN
          MOVE      SP35,PTCDNTDT
          MOVE      SP35,PTCDSNT1
          MOVE      SP35,PTCDSNT2
          MOVE      SP35,PTCDSNT3
          MOVE      SP35,PTCDBOD1
          MOVE      SP35,PTCDBOD2
          MOVE      SP35,PTCDBOD3
          MOVE      SP35,PTCDBOD4
          MOVE      SP35,PTCDBOD5
          MOVE      SP35,PTCDBOL5
          MOVE      SP35,PTCDBOD6
          MOVE      SP35,PTCDBOL6
          MOVE      SP35,PTCDBOD7
          MOVE      SP35,PTCDBOL7
          MOVE      SP35,PTCDGD01
          MOVE      SP35,PTCDGD02
          MOVE      SP35,PTCDGD03
          MOVE      SP35,PTCDGD04
          MOVE      SP35,PTCDGD05
          MOVE      SP35,PTCDGD06
          MOVE      SP35,PTCDGD07
          MOVE      SP35,PTCDGD08
          MOVE      SP35,PTCDGD09
          MOVE      SP35,PTCDPLOD
          MOVE      SP35,PTCDCOD1
          MOVE      SP35,PTCDCOD2
          MOVE      SP35,PTCDCSGT
          MOVE      SP35,PTCDCSGN
          MOVE      SP35,PTCDCSGM
          MOVE      SP35,PTCDCSSG
          MOVE      SP35,PTCDCSCM
          MOVE      SP35,PTCDTNON
          MOVE      SP35,PTCDTSUR
          MOVE      SP35,PTCDTSDT
          MOVE      SP35,PTCDTRAD
          MOVE      SP35,PTCDTRDT
          MOVE      SP35,PTCDTCHM
          MOVE      SP35,PTCDTCDT
          MOVE      SP35,PTCDTHOR
          MOVE      SP35,PTCDTHDT
          MOVE      SP35,PTCDTBIO
          MOVE      SP35,PTCDTBDT
          MOVE      SP35,PTCDRLDT
          MOVE      SP35,PTCDNDCL
          MOVE      SP35,PTCDTSAB
          MOVE      SP35,PTCDTOTH
          MOVE      SP35,PTCDTHOS
          MOVE      SP35,PTCDRST1
          MOVE      SP35,PTCDRST2
          MOVE      SP35,PTCDRST3
          MOVE      SP35,PTCDRST4
          MOVE      SP35,PTCDRST5
          PACK      PTCDSPAR,SP70,SP70
.
INIT9999  RETURN
.
.******************************************************************************
.*                          OPEN0000                                *
.*          Check that the files needed exist & open them           *
.******************************************************************************
OPEN0000  MOVE      ZERO,OVRCD
          DISPLAY   *P1:3,*EF;
.
. open old file
.
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE,"patcrdaf"
          TRAPCLR   IO
.
          BRANCH    OVRCD,OPEN5000                * check if Old file exists
.
. check if new file already exists
.
          MOVE      ZERO,OVRCD
.
          TRAP      OVERCOND IF IO
          OPEN      PATCRDA1,"newcrdaf"
          TRAPCLR   IO
.
          BRANCH    OVRCD,OPEN2000
          CLOSE     PATCRDA1
.
. File found. Tell users and check if they want to proceed
.
OPEN1000  KEYIN     *P1:10,*B,*EF,*V2LON,"New file already exists.":
                    *HOFF," Do you want to Proceed (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*V2LON,ANS
.
          REP       UPPLOW,ANS
.
          CMATCH    ANSY,ANS
          GOTO      OPEN2000 IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      OPEN9000 IF EQUAL
.
          GOTO      OPEN1000
.
. Create the new file
.
OPEN2000  EXECUTE   "iserase newcrdaf",TASKID
          CLEAR     CMDLINE
          APPEND    "isbuild newcrdaf 900 ",CMDLINE
          APPEND    "UC1-8,51-59,9-16",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      PATCRDA1,"newcrdaf"
.
. set the flag to say we want to continue
.
          MOVE      ZERO,EXIT
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:12,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:14,"Record : "
.
          GOTO      OPEN9999
.
. old file not found
.
OPEN5000  KEYIN     *P1:10,*B,*V2LON,"Old file not found.",*HOFF:
                    "  Hit ",*V2LON,"<ENTER>",*HOFF," to exit  ":
                    *EOFF,ANS
.
. we don't want to continue
.
OPEN9000  MOVE      ONE,EXIT
.
OPEN9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Run Conversion":
                    *P1:7,"Select Option :";
.
OPTN1000  KEYIN     *P17:7,*DV,UNDLN2,*P17:7,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.******************************************************************************
READ0000  PACK      KEY25,SP30
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
          PACK      KEY25,DPTCDDAD,PTCDPRMM,DPTCDCNR,SP30
          CALL      WRPTCRD1                      * write to new file
.
          IF        (RECNUM%50) = 0
            PACK      KEY8,DPTCDDAD,SP10
            DISPLAY   *P10:14,*V2LON,RECNUM,*P20:14,KEY8
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     PATCRDA1                      * close new file
          CLOSE     OLDFILE                       * close old file
          DISPLAY   *P10:14,*V2LON,RECNUM,*P20:14,KEY8
.
          EXECUTE   "cp `ldat patcrdaf.dat` savcrdaf.dat",TASKID
          EXECUTE   "cp `ldat patcrdaf.idx` savcrdaf.idx",TASKID
.
          CALL      IBACLOCK
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:18,"Ended   : ",CDATE,SP1,CTIMEIS
.
READ7000  KEYIN     *P1:23,*B,*EF,*V2LON,"Conversion is complete.":
                    *P1:24,*HOFF,"Copy back the new file (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*V2LON,ANS
          REP       UPPLOW,ANS
.
          CMATCH    ANSY,ANS
          GOTO      READ8000 IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      READ9000 IF EQUAL
.
READ8000  EXECUTE   "mv newcrdaf.dat `ldat patcrdaf.dat`",TASKID
          EXECUTE   "mv newcrdaf.idx `ldat patcrdaf.idx`",TASKID
.
READ9000  DISPLAY   *P1:23,*EF
.
READ9999  RETURN

.
. ---- OLD IO ROUTINES ----
.
READSOLD  RESET     KEY25                                                     
          READ      PATCRDA1,KEY25;;                                          
          RETURN 
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE;DPTCDDAD,DPTCDCNR,PTCDADTE,PTCDDDTE,PTCDTMRC:
                        PTCDMTSP,PTCDUKPR,PTCDPRMM,PTCDHTYP,PTCDMET1,PTCDMET2:
                        PTCDMET3,PTCDLATR,PTCDSTAG,PTCDBT01,PTCDBT02,PTCDBT03:
                        PTCDBT04,PTCDBT05,PTCDBT06,PTCDBT07,PTCDBT08,PTCDBT09:
                        PTCDBT10,PTCDOTDG,PTCDRFLG,PTCDPATH,PTCDDGPR,PTCDFCPR:
                        PTCDBO01,PTCDBO02,PTCDBO03,PTCDBO04,PTCDBO05,PTCDBO06:
                        PTCDBO07,PTCDBO08,PTCDBO09,PTCDDGOT,PTCDAUTP,PTCDBRLW:
                        PTCDMLIN,PTCDRPLB,PTCDBTSZ,PTCDCKLV,PTCDGLSC,PTCDCOMM:
                        PTCDDTEX,PTCDDTRC,PTCDTMCR,PTCDUSCR,PTCDDTUP,PTCDTMUP:
                        PTCDUSUP,PTCDBSDG,PTCDESTM,PTCDCELT,OLDSPAR1,OLDSPAR2

          GOTO      OVERCOND IF OVER
          MOVE      DPTCDDAD,PTCDDADN
          MOVE      DPTCDCNR,PTCDCNRG
          RETURN
.
          INC       PATCRDIO/INC
          INC       STD001IO
