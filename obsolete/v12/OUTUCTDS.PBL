.***********************************************************************
.*                          OUTUCTDS                                   *
.*               Display the valid Unit clinic types                   *
.***********************************************************************
OUTUCTDS  DISPLAY   *P1:4,*EF,*P30:4,*V2LON,*ULON,"Team Codes"          
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
.
          PACK      KEY18,IDDD,SP20
          CALL      RSOTUNI1                         * positional read
.
.  -----  read through group file  -----
.
PTUCT100   CALL      RKOTUNI1                         * read next record
           BRANCH    OVRCD,PTUCT800                    * end of file reached
           MATCH     IDDD,OTUISITE
           GOTO      PTUCT800 IF NOT EQUAL
.
PTUCT200   
          CLEAR     OCTDESC
          PACK      KEY12,IDDD,OTUITYPE
          CALL      RDCTYA1
          MOVE      OCTDESC,DIM30
.
.  -----  display codes and descriptions  -----
.
.
          DISPLAY   *PCCOL:CVERT,*V2LON,OTUITYPE,*HOFF,SP2,DIM30
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT                     *enough room on screen
          GOTO      PTUCT100 IF LESS                  *Yes
.
          COMPARE   FORTY,CCOL
          GOTO      PTUCT500 IF EQUAL
.
          MOVE      FORTY,CCOL
          MOVE      FIVE,CVERT
          GOTO      PTUCT100
.
.
.         Screen is full.Ask user what they want to do
.
PTUCT500  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"N",*HOFF,")ext Screen, (":
                    *V2LON,"F",*HOFF,")irst Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS                        * upper or lower case
          CMATCH    ANSC,ANS                           *continue
          GOTO      PTUCT800 IF EQUAL                  *Yes
.
          CMATCH    ANSF,ANS                           *first screen  
          GOTO      OUTUCTDS IF EQUAL                  *Yes  
.        
.         Anything else assume next screen
.
          MOVE      FIVE,CVERT                         *start at top of screen
          MOVE      ONE,CCOL
          DISPLAY   *P1:CVERT,*EF;                     *erase current screen
          GOTO      PTUCT100                           *get next record
.
.         Finished displaying file
.
PTUCT800  MOVE      FALSE,EXIT                         *data was displayed
          MOVE      ZERO,OVRCD
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
          GOTO      PTUCT999
.
.         No data on file
.
PTUCT900  DISPLAY   *P1:24,*EL,*B,"No data currently on file.   ";
          BEEP
          CALL      HITENTER
          MOVE      TRUE,EXIT                          *no data on file
.
PTUCT999  RETURN
.
.
