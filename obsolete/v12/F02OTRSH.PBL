.*****************************************************************************
.*    Program      : F02OTRSH                                                *
.*    Description  : Conversion outrshaf to new File layout                  *
.*                                                                           *
.*    Author       : Steve Armstrong                                         *
.*    Date         : 21/07/2011                                              *
.*    Modifications:                                                         *
.*        V10.02.00 21/07/2011  Steve Armstrong   CAR 246872                 *
.*                  Created program                                          *
.*****************************************************************************
.
          INC       STD001FD
.
          INC       OUTSITFD/INC
.
FINDFILE  FILE      ASCII, FIXED=256
.
.
.         Old File Definition
.
OLDFILE1   IFILE     SQL, FIXED=111, KEY="U34-41,42-44"
.
. NAME     TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION
. ----     ----   ------   --------  ----------   -----------
OPRSSITE  DIM        6         6          1       Site of booking
OPRSCLIN  DIM        6         6          7       Original clinic of booking
OPRSDATE  DIM        8         8         13       Original date of booking
OPRSSTRT  DIM        5         5         21       Original session start time
DOPRSSLO  DIM        3         3         26       Original session slot number
OPRSTIME  DIM        5         5         29       Original session slot time
DOPRSOUT  DIM        8         8         34       Outpatient number rescheduled
DOPRSUNI  DIM        3         3         42       Unique transaction number
OPRSRDTE  DIM        8         8         45       Date of rescheduling
OPRSRTIM  DIM        8         8         53       Time of rescheduling
OPRSREAS  DIM        3         3         61       Reason for reschedule (RR)
OPRSNCLI  DIM        6         6         64       New clinic of booking
OPRSNDAT  DIM        8         8         70       New date of booking
OPRSNSTR  DIM        5         5         78       New session start time
DOPRSNSL  DIM        3         3         83       New session slot number
OPRSNTIM  DIM        5         5         86       New session slot time
OPRSOPER  DIM        4         4         91       Operator Id.
OLDSPAR   DIM       16        16         95       Spare variable
. 
.End of Record                          111
.
. Redefine FORM fields from key          
. -----------------------------          
OPRSOUTN   FORM        8
OPRSUNIQ   FORM        3
OPRSSLOT   FORM        3
OPRSNSLO   FORM        3
.
.
.         New File Definition
.
OUTRSHA1   IFILE     SQL, FIXED=150, KEY="U34-41,42-44"
FILRSHA1   INIT      "rshaf"
.
. NAME    TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION
. ----    ----   ------   --------  ----------   -----------
.OPRSSITE DIM        6         6          1       Site of booking
.OPRSCLIN DIM        6         6          7       Original clinic of booking
.OPRSDATE DIM        8         8         13       Original date of booking
.OPRSSTRT DIM        5         5         21       Original session start time
.DOPRSSLO DIM        3         3         26       Original session slot number
.OPRSTIME DIM        5         5         29       Original session slot time
.DOPRSOUT DIM        8         8         34       Outpatient number rescheduled
.DOPRSUNI DIM        3         3         42       Unique transaction number
.OPRSRDTE DIM        8         8         45       Date of rescheduling
.OPRSRTIM DIM        8         8         53       Time of rescheduling
.OPRSREAS DIM        3         3         61       Reason for reschedule (RR)
.OPRSNCLI DIM        6         6         64       New clinic of booking
.OPRSNDAT DIM        8         8         70       New date of booking
.OPRSNSTR DIM        5         5         78       New session start time
.DOPRSNSL DIM        3         3         83       New session slot number
.OPRSNTIM DIM        5         5         86       New session slot time
.OPRSOPER DIM        4         4         91       Operator Id.
OPRSCLTY  DIM        6         6         95       Clinic Type (outctyaf)
OPRSVTYP  DIM        3         3        101       Visit Type (Cat CV)
OPRSCOMP  DIM        3         3        104       Claim Code (Cal CL)
OPRSNMDS  DIM        3         3        107       NMDS Clinic (Cat NM)
OPRSSRVD  DIM        3         3        110       Service Delivery (Cat sd)
OPRSCARE  DIM        3         3        113       Care Type (Cat CC)
OPRSSPAR  DIM       34        34        116       Spare variable
.
.End of Record                          150
.
.
. LOCAL VARIABLES
. ---------------
CFILEPRE  DIM       3   * Current Open File Prefix for Outpatient Files
CMDLINE   DIM       100
.
DEFPATH   DIM       60
DIM60     DIM       60
.
INPFILE   DIM       8
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
SAVEFLG   FORM      1
VSITE     DIM       6
.
.
. PROGRAM CONSTANTS
. -----------------
SP60      INIT    "                                                            "
.
.
.
PRGNAM    INIT      "CONVERSION OUTRSHFD"
PRGID     INIT      "F02OTRSH"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000              * run c-isam fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KSIT0000                      * Keyin OP Site Code
          BRANCH    EXIT,MAIN0000

          CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
MAIN2000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN0000:               * no
                          MAIN9999                * cancel
.
MAIN5000  BRANCH    COPTION,MAIN6000              * c-isam fixit
.
.         Running c-isam fix
.
MAIN6000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/compress saved file
          GOTO      MAIN9999
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
.
          DISPLAY   *P56:24,"Opening outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)":
                    *P1:20,*V2LON,"NOTE - xxxrshaf file will be saved as ":
                    "sotrshaf regardless of the site prefix"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run c-isam fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                                KSIT0000             Called by: MAIN0000   *
.*  Keyin outpatient Site code to get file prefix                            *
.*****************************************************************************
.
KSIT0000  MOVE      SP70,VSITE
          KEYIN     *P1:9,*EL,"Outpatient Site Code : ",VSITE;
.
          PACK      VSITE,VSITE,SP70
.
          MATCH     SP6,VSITE           * Valid site ? Blank
          GOTO      KSIT9100 IF EQUAL   * 
.
          MOVE      VSITE,KEY6          * Key for the site file
          CALL      RDSITA1             * Read the site record
          BRANCH    OVRCD,KSIT9000      * Invalid site.
.
          MOVE      OSTFILE,CFILEPRE    * Save Current File Prefix
.
          DISPLAY   *P35:9,"(Prefix: ",CFILEPRE,")";
          MOVE      ZERO,EXIT
          GOTO      KSIT9999
.
.         Invalid call to program. Common not set up with correct site info
.
KSIT9000  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "Invalid Site Code Entered. ";
          CALL      HITENTER
          GOTO      KSIT0000
.
KSIT9100  MOVE      ONE,EXIT
          GOTO      KSIT9999
.
KSIT9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
.
PREP0000  PACK      INPFILE,CFILEPRE,FILRSHA1,SP70
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat ",CMDLINE
          APPEND    CFILEPRE,CMDLINE
          APPEND    "rshaf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sotrshaf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat ",CMDLINE
          APPEND    CFILEPRE,CMDLINE
          APPEND    "rshaf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sotrshaf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    CFILEPRE,CMDLINE
          APPEND    "rshaf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "sotrshaf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    CFILEPRE,NEWFILE
          APPEND    "rshaf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " 150 UC34-41,42-44",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.         Index 2
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC1-6,7-12,13-20,21-25,34-41,42-44",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.         Index 3
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC1-6,45-52,34-41,42-44",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      OUTRSHA1,NEWFILE
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
            ELSE
              DISPLAY   "has caused an IO error - ";
            ENDIF
          ENDIF
          DISPLAY   "File will be bypassed"
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY11,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
.
          IF        (RECNUM%1000) = 0 | (RECNUM%1000) = 1
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MOVE      SP6,OPRSCLTY
          MOVE      SP3,OPRSVTYP
          MOVE      SP3,OPRSCOMP
          MOVE      SP3,OPRSNMDS
          MOVE      SP3,OPRSSRVD
          MOVE      SP3,OPRSCARE
          MOVE      SP70,OPRSSPAR
          CALL      WRRSHA1                       * write to new file
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     OUTRSHA1                      * close new file
          CLOSE     OLDFILE1                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
.
VALD0000  MOVE      ZERO,EXIT
.
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or compress save file                                *
.**********************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "sotrshaf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "compress -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not compressed or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.*        DEFT0000 -  Get the default directory                       *
.**********************************************************************
.
DEFT0000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "ldat ",CMDLINE
          APPEND    CFILEPRE,CMDLINE
          APPEND    "rshaf.dat > temp.txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding ",CFILEPRE,"rshaf.";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
.         Old I/O Routines
.
READSOLD  RESET     KEY11
          READ      OLDFILE1,KEY11;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;OPRSSITE,OPRSCLIN,OPRSDATE,OPRSSTRT:
                             DOPRSSLO,OPRSTIME,DOPRSOUT,DOPRSUNI:
                             OPRSRDTE,OPRSRTIM,OPRSREAS,OPRSNCLI:
                             OPRSNDAT,OPRSNSTR,DOPRSNSL,OPRSNTIM:
                             OPRSOPER,OLDSPAR
          GOTO      OVERCOND IF OVER
          MOVE      DOPRSSLO,OPRSSLOT
          MOVE      DOPRSOUT,OPRSOUTN
          MOVE      DOPRSUNI,OPRSUNIQ
          MOVE      DOPRSNSL,OPRSNSLO
          RETURN
.
.         New I/O Routines
.
WRRSHA1   MOVE      OPRSSLOT,DOPRSSLO
          MOVE      OPRSOUTN,DOPRSOUT
          MOVE      OPRSUNIQ,DOPRSUNI
          MOVE      OPRSNSLO,DOPRSNSL
          WRITE     OUTRSHA1,KEY11;OPRSSITE,OPRSCLIN,OPRSDATE,OPRSSTRT:
                             DOPRSSLO,OPRSTIME,DOPRSOUT,DOPRSUNI:
                             OPRSRDTE,OPRSRTIM,OPRSREAS,OPRSNCLI:
                             OPRSNDAT,OPRSNSTR,DOPRSNSL,OPRSNTIM:
                             OPRSOPER,OPRSCLTY,OPRSVTYP,OPRSCOMP:
                             OPRSNMDS,OPRSSRVD,OPRSCARE,OPRSSPAR
          RETURN
.
          INC       STD001IO
.
          INC       OUTSITIO/INC
