.-------------------------------------------------------------------
. Extract Casemix Code for an Episode
.             - Input  EXTRBRAN - Casemix Type
.                      Epsiode Record from ccsepsaf & ccsiadaf
.               Output KEY10    - Casemix Code (Normal or Summary)
.                      CCXHKEY  - Casemix Code (Normal)
.                      CCXHSUM  - Casemix Code (Summary)
.-------------------------------------------------------------------
GETCD000  MOVE      ZERO,EXTRSUMM
          IF        EXTRBRAN>200
            MOVE      ONE,EXTRSUMM      * Flag as Summary Casemix Type 
            SUB       "200",EXTRBRAN
          ENDIF
          MOVE      CCEPTIM,TIMHRS      * Hourly Intervals
          MOVE      CCEPTDP,TDPHRS
          MOVE      CCEPTIM,TIMTEN      * Ten Min Intervals
          MOVE      CCEPTDP,TDPTEN
          IF        ((EXTRBRAN>35)&(EXTRBRAN<45))
            CALL      GETAGE00
          ENDIF
          IF        ((EXTRBRAN>27)&(EXTRBRAN<32))
            CALL      SETICD00
          ENDIF
          IF        EXTRBRAN=56
            CALL      SETADY00     * Set-up Admission Day
          ENDIF
          IF        EXTRBRAN=57
            CALL      SETDDY00     * Set-up Discharge Day
          ENDIF
          LOAD      KEY10,EXTRBRAN,CCEPDRGC,CCEPMDCC:
                    CCEPDWAR,CCEPDSTA,CCEPDDES,CCEPDMBS,CCEPDDPT,CCEPDSER:
                    CCEPAWAR,CCEPASRC,CCEPACLS,CCEPACAR,CCEPAMBS,CCEPADPT:
                    CCEPASER,CCEPARDC,CCEPATDC,CCEPAADC,CCEPDADC,CCEPAHFD:
                    CCEPAHTB,CCEPOPCL,CCEPPDIA,CCEPSDIA,CCEPPOPP,CCEPSOPP:
                    CCEPVIST,PDIA01,SDIA01,POPP01,SOPP01,CCEPAGEY,CCEPSEX:
                    CCEPCOB,CCEPREL,AGERANGE,AGERANGE,AGERANGE,AGERANGE:
                    AGERANGE,AGERANGE,AGERANGE,AGERANGE,AGERANGE,CCEPPMBS:
                    CCEPSMBS,CCEPMU1,CCEPMU2,CCEPMU3,CCEPMU4,CCEPMU5:
                    CCEPMU6,CCEPMU7,CCEPPCD,CCEPCLM,WEEKDAY,WEEKDAY,THEBAND:
                    THEBAND,CCEPCLI,CCEPCTY,CCEPANA,CCEPOTY,CCEPTTR,CCEPOY6:
                    CCEPOY7,CCEPOY8,CCEPOY9,CCEPORM,CCEPOKI,CCEPOTP,CCEPTST:
                    CCEPINS,CCEPTIM,CCEPTDP,TIMHRS,TDPHRS,TIMTEN,TDPTEN:
                    CCEPAST,CCEPAMD,CCEPAU1,CCEPAU2,CCEPAU3,CCEPAU4,CCEPAU5:
                    CCEPAU6,CCEPAU7,CCEPAU8,CCEPAWT,CCEPAPR,CCEPAET,CCEPART:
                    CCEPAIN,CCEPAPL,CCEPAPY,CCEPOST,CCEPOCL,CCEPOCT,CCEPOVT:
                    CCEPODY,CCEPOSS,CCEPODS,CCEPOAT,CCEPOPR,CCEPOU1,CCEPOU2:
                    CCEPOU3,CCEPOU4,CCEPODP,CCEPOTR,CCEPOLG,CCEPOOT,CCEPMU8:
                    CCEPMU9,CCEPMU10,CCEPPHP,CCEPAGN
             
.
          PACK      KEY10,KEY10,SP70
          PACK      CCXHKEY,KEY10,SP70
          IF        EXTRSUMM=1
            MOVE      EXTRBRAN,KEY3
            REP       " 0",KEY3
            PACK      KEY13,KEY3,CCXHKEY
            CALL      RDCCXH1
            IF        OVRCD=0
              MOVE      CCXHSUM,KEY10
            ELSE
              MOVE      SP70,KEY10
            ENDIF
            ADD       "200",EXTRBRAN
          ENDIF
.
GETCD999  RETURN
.--------------------------------------------------
. Determine Age Range Code
.--------------------------------------------------
GETAGE00  MOVE     SP70,AGERANGE
          MOVE     EXTRBRAN,F3
          SUB      "35",F3
          MOVE     F3,F1
          MOVE     F1,KEY1
          PACK     KEY7,KEY1,CCEPAGEY,SP70
          CALL     RSCCAG2
          CALL     RKCCAG2
          BRANCH   OVRCD,GETAGE99
          MATCH    KEY1,CCAGDEF
          GOTO     GETAGE99 IF NOT EQUAL
          MOVE     CCAGGRP,AGERANGE
.
GETAGE99  RETURN
.--------------------------------------------------
. Setup Icd Codes Pre "."
.--------------------------------------------------
SETICD00  MOVE      CCEPPDIA,PDIA01
          MOVE      CCEPSDIA,SDIA01
          MOVE      CCEPPOPP,POPP01
          MOVE      CCEPSOPP,SOPP01
.
          SCAN      ".",PDIA01
          IF        @EQUAL
            BUMP      PDIA01,-1
            APPEND    SP70,PDIA01
          ENDIF
.
          SCAN      ".",SDIA01
          IF        @EQUAL
            BUMP      SDIA01,-1
            APPEND    SP70,SDIA01
          ENDIF
.
          SCAN      ".",POPP01
          IF        @EQUAL
            BUMP      POPP01,-1
            APPEND    SP70,POPP01
          ENDIF
.
          SCAN      ".",SOPP01
          IF        @EQUAL
            BUMP      SOPP01,-1
            APPEND    SP70,SOPP01
          ENDIF
.
          RESET     PDIA01
          RESET     SDIA01
          RESET     POPP01
          RESET     SOPP01
.
          RETURN
.------------------------------------------------------------
. Get Day of Admission
.------------------------------------------------------------
SETADY00  UNPACK     CCEPADAT,CCENT,CYEAR,CMON,CDAY
          CALL       WDAYCONV
SETADY99  RETURN
.------------------------------------------------------------
. Get Day of Discharge
.------------------------------------------------------------
SETDDY00  UNPACK     CCEPDDAT,CCENT,CYEAR,CMON,CDAY
          CALL       WDAYCONV
SETDDY99  RETURN
.------------------------------------------------------------
. WDAYCONV  : Convert a date to a day number : 1 Mon to 7 Sun  
. Variables : CDAY,CMON,CYEAR,CCENT 
. Returns   : WEEKDAY the day number of the week (1-7)
.------------------------------------------------------------
WDAYCONV  CALL      GFDAY000                    * get first day of given year
          BRANCH    EXIT,WDAYC999
.          
.------ First day of year exists ------
.
          MOVE      CDAY,DD
          MOVE      CMON,MM
          MOVE      CYEAR,YY
          MOVE      CCENT,CC
          CALL      DMYCON                      * convert to a julian date
.
.------ Calculate the day of the week ------
.
          MOVE      JULDAY,FSTJULDY
          DIV       SEVEN,FSTJULDY
          MULT      SEVEN,FSTJULDY
.
          SUB       FSTJULDY,JULDAY
.
          MOVE      JULDAY,FSTJULDY
          ADD       FSTDAY,FSTJULDY
          SUB       ONE,FSTJULDY
          MOVE      FSTJULDY,WEEKDAY
.
.------ Change to a number from 1 to 7 ------
.
          COMPARE   ZERO,FSTJULDY
          GOTO      WDAYC900 IF EQUAL
.
          COMPARE   EIGHT,FSTJULDY
          GOTO      WDAYC999 IF LESS
.
          SUB       SEVEN,FSTJULDY
          MOVE      FSTJULDY,WEEKDAY
          GOTO      WDAYC999
.
WDAYC900  MOVE      SEVEN,WEEKDAY
.
WDAYC999  RETURN
.**********************************************************************
.*                               GFDAY000                             *
.*        Routine to get the starting day of a given Year 1979-2013   *
.**********************************************************************
GFDAY000  PACK      KEY4,CCENT,CYEAR
          REP       " 0",KEY4
          MOVE      KEY4,F4
          MOVE      ZERO,EXIT
          SUB       "1978",F4
          BRANCH    F4,GFDAY100,GFDAY200,GFDAY400,GFDAY500,GFDAY600: * 79-83
                       GFDAY700,GFDAY200,GFDAY300,GFDAY400,GFDAY500: * 84-88
                       GFDAY700,GFDAY100,GFDAY200,GFDAY300,GFDAY500: * 89-93
                       GFDAY600,GFDAY700,GFDAY100,GFDAY300,GFDAY400: * 94-98
                       GFDAY500,GFDAY600,GFDAY100,GFDAY200,GFDAY300: * 99-03
                       GFDAY400,GFDAY600,GFDAY700,GFDAY100,GFDAY200: * 04-08
                       GFDAY400,GFDAY500,GFDAY600,GFDAY700,GFDAY200  * 09-13
          MOVE      ONE,EXIT
          GOTO      GFDAY999
GFDAY100  MOVE      ONE,FSTDAY      * Mon
          GOTO      GFDAY999
GFDAY200  MOVE      TWO,FSTDAY      * Tue
          GOTO      GFDAY999
GFDAY300  MOVE      THREE,FSTDAY    * Wed
          GOTO      GFDAY999
GFDAY400  MOVE      FOUR,FSTDAY     * Thu
          GOTO      GFDAY999
GFDAY500  MOVE      FIVE,FSTDAY     * Fri
          GOTO      GFDAY999
GFDAY600  MOVE      SIX,FSTDAY      * Sat
          GOTO      GFDAY999
GFDAY700  MOVE      SEVEN,FSTDAY    * Sun
          GOTO      GFDAY999
GFDAY999  RETURN
.
