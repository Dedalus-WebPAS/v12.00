.******************************************************************************
.* System         : Outpatients                                               *
.*                                                                            *
.* Procedure Name : OUTXSTAF.PBL                                              *
.*                                                                            *
.* Function       : To input the extra staff for a clinic master              *
.*                                                                            *
.* Author         : Justin Coates      23/10/1995                             *
.*                                                                            *
.* Parameters     : KEY18              key to outma1af                        *
.*                  CFNAMEX            temp file name                         *
.*                  FORM1              1 = enquiry, 0 = processing            *
.*                                                                            *
.* Modifications  :                                                           *
.******************************************************************************
. 
.*********************************************************************
.                   ML0000
.         MAINLINE CODE / CONTROLLING LOGIC
.*********************************************************************
OUTXSTAF  CALL      INIT0000
          CALL      PROC0000                * enter details
          CALL      DTPA0000                * delete temp file
          DISPLAY   *P1:7,*EF;
          RETURN
+
.*********************************************************************
.                   INIT0000                    Called by : ML0000
.         initilization
.*********************************************************************
INIT0000  MOVE      KEY18,MASAKEY
          MOVE      FORM1,ENQUIRY
          PACK      CFNAME,UID,FILMXSA1
          OPEN      OUTMXSA1,CFNAME
          OPEN      PATCODE1,"patcodes"
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATDO1A2,"patdo1af"
.
          MOVE      ONE,CNEWDS
          PACK      KEY5,CATDT,SP3
          CALL      RDCODE1
          MOVE      TDESC,CATDTDSC
          STRIP     CATDTDSC
.
INIT9999  RETURN
+
.*********************************************************************
.*                  CTPA0000                    Called by : xxxx0000 *
.*        Create a temp file                                         *
.*********************************************************************
CTPA0000  CALL      DTPA0000                * delete temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISBUILD,CFNAMEX,KEYA,SP30      * set command
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * build the temp file
          OPEN      TEMPA,CFNAMEX           * open the temp file
CTPA9999  RETURN
+
.*********************************************************************
.*                  DTPA0000                    Called by : CTPA0000 *
.*        Delete temp file A                                         *
.*********************************************************************
DTPA0000  CLOSE     TEMPA                   * close temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISERASE,CFNAMEX * set command line for erase
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * delete the temp file
DTPA9999  RETURN
+
.*********************************************************************
.*                  PROC0000                    Called by : ML3000   *
.*        Enter all the data for the patient                         *
.*********************************************************************
PROC0000  MOVE      ZERO,TOTITEM            * number of items entered
          DISPLAY   *P1:7,*EF,*P5:HDRLINE,*ULON,*V2LON:
                    "Health Care Professional",SP20,SP3:
                    *P55:HDRLINE,CATDTDSC,SP4
.
          CALL      DEFL0000                * see if details entered for patient
          BRANCH    EXIT,PROC5000           * have defaults so change
.
          IF        ENQUIRY = ONE
            DISPLAY   *P1:24,*B,"No extra staff details.  ",*EL;
            CALL      HITENTER
            GOTO      PROC9999
          ENDIF
.
.         insert new details
.
          MOVE      ONE,FORM2               * item number entering
          MOVE      "1",BCOL                * col number to input on
          MOVE      HDRLINEA,BVERT
.
PROC4000  MOVE      SP6,CKYIDEF6            * clear default value
          MOVE      SP6,CKYIDEF3            * clear default value
          MOVE      SP3,XSPEC               * clear specialty
          MOVE      ZERO,CHANGE             * insert mode
          CALL      KITM0000                * enter the item(s)
          BRANCH    EXIT,PROC9999           * exitchar
.
. ******* loop over temp file and display all the data *******
.
PROC5000  CALL      LTMP0000                * display from temp file
          BRANCH    EXIT,PROC9999,PROC4000  * cancel,add
.
. ******* Post all the data *******
.         first delete the existing data
.
PROC8000  DISPLAY   *P1:24,"Posting data - ",*V2LON,"Please wait",*EL;
          PACK      KEY20,MASAKEY,SP2
          CALL      RSOTMXS1
          CALL      RKOTMXS1
          BRANCH    OVRCD,PROC8500          * no more data
.
          PACK      KEY18,OTXSSITE,OTXSCLIN,OTXSDAY,OTXSTIME
          MATCH     MASAKEY,KEY18
          GOTO      PROC8500 IF NOT EQUAL   * no more data for patient
.
          PACK      KEY20,OTXSSITE,OTXSCLIN,OTXSDAY,OTXSTIME,OTXSCNT
          CALL      DEOTMXS1
          GOTO      PROC8000
.
.         post the new data from temp file
.
PROC8500  MOVE      SP6,KEY6
          CALL      RSTEMPA
          MOVE      ZERO,OTXSCNT            * clear counter
          UNPACK    MASAKEY,OTXSSITE,OTXSCLIN,KEY1,OTXSTIME
          MOVE      KEY1,OTXSDAY
.
PROC8600  CALL      RKTEMPA
          BRANCH    OVRCD,PROC9999
.
          ADD       ONE,OTXSCNT             * count
          MOVE      XDOCT,OTXSDOCT          * doctor
          MOVE      XSPEC,OTXSSPEC          * specialty (Cat A)
          CALL      WROTMXS1
          GOTO      PROC8600
.
PROC9999  RETURN
+
.*********************************************************************
.         See if there are details already on file
.         make sure TOTITEM is updated (the total amount of items entered)
.*********************************************************************
DEFL0000  CALL      CTPA0000                * create temp file
          MOVE      ZERO,TOTITEM            * number of items entered
          MOVE      " 1",KEY2
          PACK      KEY20,MASAKEY,KEY2
          CALL      RDOTMXS1
          BRANCH    OVRCD,DEFL9000          * nothing in file
.
.         have defaults so write to the temp file
.
DEFL1000  MOVE      OTXSDOCT,XDOCT          * doctor
          MOVE      OTXSSPEC,XSPEC          * specialty
          MOVE      XDOCT,KEY6
          CALL      WRTEMPA
          ADD       ONE,TOTITEM              * add to item counter
.
.         see if any more details
.
          CALL      RKOTMXS1
          BRANCH    OVRCD,DEFL3000          * end of file
          PACK      KEY18,OTXSSITE,OTXSCLIN,OTXSDAY,OTXSTIME
          MATCH     MASAKEY,KEY18
          GOTO      DEFL1000 IF EQUAL       * more data for session
.
DEFL3000  MOVE      ONE,EXIT
          GOTO      DEFL9999
.
DEFL9000  MOVE      ZERO,EXIT               * nothing on file
.
DEFL9999  RETURN
+
.*********************************************************************
.                   KITM0000                    Called by : PROC4000 
.         Enter the doctor and specialty
.         Para's  : FORM2         the item number inputting
.                   BCOL          column for input
.                   BVERT         row for input
.                   CHANGE        1 = change mode
.*********************************************************************
KITM0000  IF        BVERT > 22
            MOVE      HDRLINEA,BVERT
            MOVE      ONE,BCOL                * set back to col 1
            DISPLAY   *P1:BVERT,*EF;
          ENDIF
.
          DISPLAY   *PBCOL:BVERT,*V2LON,FORM2,*HOFF,". "
          MOVE      BCOL,SCOL               * save column
          MOVE      BVERT,SVERT             * save row
          ADD       "4",BCOL                * column
.
KITM0500  CALL      KDOCT000                * keyin item
          BRANCH    EXIT,KITM9990,KITM9991  * no item entered/exitchar
.
          MOVE      XDOCT,CKYIDEF6          * set the new default doctor
.
.         enter the specialty (Cat A)
.
KITM1000  CALL      KSPEC000
          BRANCH    EXIT,KITM0500,KITM9991
.
.         write to the temp file
.
KITM7000  MOVE      XDOCT,KEY6
          CALL      WRTEMPA
.
          BRANCH    CHANGE,KITM9990         * in change mode
.
.         add another item
.
          MOVE      SCOL,BCOL               * restore column
          MOVE      SVERT,BVERT             * restore row
          ADD       ONE,BVERT               * add to row
          ADD       ONE,TOTITEM             * add to number of items entered
          ASSIGN    (TOTITEM+1),FORM2       * set item number entering
          MOVE      SP3,XSPEC               * clear specialty
          MOVE      SP6,CKYIDEF6            * clear default item
          MOVE      SP6,CKYIDEF3            * clear default item
          GOTO      KITM0000
.
KITM9990  MOVE      ZERO,EXIT
          GOTO      KITM9999
.
KITM9991  MOVE      ONE,EXIT
.
KITM9999  RETURN
+
.*********************************************************************
.*                  LTMP0000                    Called by : PROC5000 *
.*        Loop over the temp file and display the data from start    *
.*        Returns : EXIT = 0      post                               *
.*                  EXIT = 1      cancel                             *
.*                  EXIT = 2      add                                *
.*********************************************************************
LTMP0000  DISPLAY   *P1:HDRLINEA,*EF;
          MOVE      ONE,CURRPAGE            * the current page
          MOVE      ZERO,FORM2              * total number entered
          MOVE      "1",BCOL                * set col
          MOVE      HDRLINE,BVERT           * set row
          MOVE      "1",LASTCOL             * set last col
          MOVE      HDRLINE,LASTROW         * set last row
          MOVE      SP8,KEY6
          CALL      RSTEMPA
.
LTMP1000  CALL      RKTEMPA
          BRANCH    OVRCD,LTMP5000
.
          ADD       ONE,BVERT               * add to row 
          ADD       ONE,FORM2               * total entered
          COMPARE   "23",BVERT
          GOTO      LTMP4000 IF NOT LESS    * need a new screen
.
LTMP2000  MOVE      XDOCT,KEY6
          CALL      RDDOCT1
          CALL      FDNAM000
          PACK      KEY5,CATDT,XSPEC
          CALL      RDCODE1
          DISPLAY   *P1:BVERT,*V2LON,FORM2,*HOFF,". ",*V2LON,XDOCT,*HOFF:
                    SP1,DESCDOCT,*P55:BVERT,*V2LON,XSPEC,*HOFF,SP1,TDESC
          MOVE      BCOL,LASTCOL            * set last displayed column
          MOVE      BVERT,LASTROW           * set last displayed row
          MOVE      XDOCT,LASTITEM          * set last displayed item
          GOTO      LTMP1000
.
.         need a new screen
.
LTMP4000  MOVE      TWO,CALLPOSN
          CALL      KEYB0000
          BRANCH    EXIT,LTMP9000,LTMP9100,LTMP7000,LTMP8000,LTMP0000,LTMP6000
.                        Post     Cancel   Next     Add      First    Delete
          GOTO      LTMP5050
.
.         no more data to display
.
LTMP5000  MOVE      ONE,CALLPOSN
          CALL      KEYB0000
          BRANCH    EXIT,LTMP9000,LTMP9100,LTMP5000,LTMP8000,LTMP0000,LTMP6000
.                        Post     Cancel   Next     Add      First    Delete
.
. ******* change an item - must position on the screen on that item *****
.
LTMP5050  MOVE      ONE,DELTEMP             * do delete
          CALL      CHGI0000                * set up the correct screen
          CALL      KITM0000                * keyin date/amount
          BRANCH    EXIT,LTMP9100           * exitchar
          BRANCH    CALLPOSN,LTMP5000       * without next
          GOTO      LTMP4000                * with next
.
. ******* delete an item *******
.
LTMP6000  CALL      DITM0000
          BRANCH    EXIT,LTMP0000           * item deleted
          BRANCH    CALLPOSN,LTMP5000       * without next
          GOTO      LTMP4000                * with next
.
.         (N)ext screen
.
LTMP7000  MOVE      LASTITEM,KEY6
          CALL      RDTEMPA
          CALL      RKTEMPA
          ASSIGN    (CURRPAGE*PGELIMA+1),FORM2   * item number
          ADD       ONE,CURRPAGE            * get to next screen
          MOVE      ONE,BCOL                * reset column
          MOVE      HDRLINEA,BVERT
          DISPLAY   *P1:BVERT,*EF;
          GOTO      LTMP2000
.
.         (A)dd item
.
LTMP8000  ASSIGN    (LASTROW+1),BVERT       * get to next line
          MOVE      LASTCOL,BCOL            * get to column
          ASSIGN    (TOTITEM+1),FORM2       * new item number to input
          MOVE      TWO,EXIT                * ADD
          GOTO      LTMP9999
.
.         set exit flags
.
LTMP9000  MOVE      ZERO,EXIT               * POST
          GOTO      LTMP9999
.
LTMP9100  MOVE      ONE,EXIT                * CANCEL
.
LTMP9999  RETURN
+
.*********************************************************************
.         delete 
.*********************************************************************
DITM0000  KEYIN     *P1:24,*EL,"Enter item to delete : ",*V2LON,CHGITEM
          MOVE      ZERO,EXIT
          COMPARE   ZERO,CHGITEM
          GOTO      DITM9999 IF EQUAL       * nothing deleted
          GOTO      DITM0000 IF LESS        * invalid option
          COMPARE   CHGITEM,TOTITEM
          GOTO      DITM0000 IF LESS        * invalid option
.
          MOVE      ZERO,DELTEMP            * dont delete temp file
          CALL      CHGI0000                * get to current item
          MOVE      ONE,DELTEMP             * do delete temp file
          ASSIGN    BCOL-1,ECOL
          DISPLAY   *PECOL:BVERT,*V2LON,*HON,SP1,CHGITEM,DOT
.
DITM1000  KEYIN     *P1:24,*EL,"OK to Delete (",*V2LON,*DV,ANSY,*HOFF,"/":
                    *V2LON,*DV,ANSN,*HOFF,") ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
          MATCH     ANSN,ANS
          GOTO      DITM5000 IF EQUAL       * dont delete
          MATCH     ANSY,ANS
          GOTO      DITM2000 IF EQUAL       * delete
          BEEP
          GOTO      DITM1000
.
.         delete the item
.
DITM2000  MOVE      CKYIDEF6,KEY6
          CALL      DETEMPA
          SUB       ONE,TOTITEM             * subtract from total items
          MOVE      ONE,EXIT
          GOTO      DITM9999
.
.         dont delete the item
.
DITM5000  DISPLAY   *PECOL:BVERT,SP1,*V2LON,CHGITEM,*HOFF,DOT
          GOTO      DITM0000
.
DITM9999  RETURN
+
.*********************************************************************
.*        Enter response to line 24 prompt                           *
.*********************************************************************
KEYB0000  IF        ENQUIRY = ZERO
            DISPLAY   *P1:24,"Select Item, (":
                      *V2LON,ANSP,*HOFF,")ost, (":
                      *V2LON,ANSC,*HOFF,")ancel, (":
                      *V2LON,ANSA,*HOFF,")dd, (":
                      *V2LON,ANSD,*HOFF,")elete";
            MOVE      "46",ECOL
          ELSE
            DISPLAY   *P1:24,"(",*V2LON,ANSE,*HOFF,")xit";
            MOVE      "6",ECOL
          ENDIF
          IF        CALLPOSN = TWO
            DISPLAY   ", (",*V2LON,ANSN,*HOFF,")ext";
            ADD       "8",ECOL
          ENDIF
.
          IF        CURRPAGE <> ONE
            DISPLAY   ", (",*V2LON,ANSF,*HOFF,")irst";
            ADD       "9",ECOL
          ENDIF
          DISPLAY   " : ";
          ADD       FOUR,ECOL
.
KEYB1000  KEYIN     *PECOL:24,*V2LON,*JR,DIM2:
                    *PECOL:24,*DV,DIM2
.
          TYPE      DIM2
          GOTO      KEYB5000 IF EQUAL       * number entered
.
.         letter entered
.
          MATCH     SP1,DIM2
          GOTO      KEYB1500 IF NOT EQUAL   * invalid letters
.
          UNPACK    DIM2,ANS,ANS            * get letter entered
          REP       UPPLOW,ANS              * turn to uppercase
          IF        ENQUIRY = ZERO
            REP       "P1C2N3A4F5D6",ANS      * get valid options
          ELSE
            REP       "E2N3F5",ANS            * get valid options
          ENDIF
          MOVE      ZERO,EXIT
          MOVE      ANS,EXIT                * get as a form
          BRANCH    EXIT,KEYB9999,KEYB9999,KEYB9999,KEYB9999,KEYB9999,KEYB9999
.
KEYB1500  BEEP
          GOTO      KEYB1000
.
.         number entered
.
KEYB5000  BRANCH    ENQUIRY,KEYB1500        * invalid for enquiry
.
          MOVE      DIM2,CHGITEM            * convert to a form
          MOVE      ZERO,EXIT               * set exit flag
          COMPARE   CHGITEM,ZERO
          GOTO      KEYB1500 IF NOT LESS    * invalid option
.
          COMPARE   CHGITEM,TOTITEM
          GOTO      KEYB1500 IF LESS        * invalid option
.
KEYB9999  RETURN
+
.*********************************************************************
.*                  CHGI0000                    Called by : LTMP5000 *
.*        Set up to change an item by displaying the screen it is on *
.*        and position the keyin on the correct position             *
.*        Returns : BVERT         keyin row                          *
.*                  BCOL          keyin column                       *
.*                  FORM2         item number to keyin               *
.*                  XITEM         default item                       *
.*                  CALLPOSN      which line 24 prompt (2=next)      *
.*********************************************************************
CHGI0000  MOVE      ONE,CHANGE              * set to change mode
          ASSIGN    ((CHGITEM-1)/PGELIMA),CPAGENO   * page number item is on
          ASSIGN    (CPAGENO*PGELIMA),CCITEM   * # of reads to 1st item on page
          ADD       ONE,CPAGENO             * get to actual page number
          MOVE      CCITEM,FORM2            * set item number to start display
.
.         position on the last item of previous screen
.
          MOVE      SP8,KEY6
          CALL      RSTEMPA                 * position at start of file
          WHILE     CCITEM
            CALL      RKTEMPA
            SUB       ONE,CCITEM
          DO
.
.         redisplay the new screen
.
          IF        CURRPAGE <> CPAGENO
            MOVE      ONE,CHGDISP             * set to redisplay
            DISPLAY   *P1:HDRLINEA,*EF;       * clear screen
          ELSE
            MOVE      ZERO,CHGDISP            * dont do redisplay
          ENDIF
.
          MOVE      ONE,CALLPOSN            * set call position
          MOVE      CPAGENO,CURRPAGE        * set the actual page
          MOVE      "1",CCOL                * set col
          MOVE      HDRLINE,CVERT           * set row
.
CHGI1000  CALL      RKTEMPA
          BRANCH    OVRCD,CHGI5000
.
          ADD       ONE,CVERT               * add to row 
          ADD       ONE,FORM2               * add to item counter
          COMPARE   "23",CVERT
          GOTO      CHGI4900 IF NOT LESS    * need a new screen
.
CHGI2000  IF        CHGDISP = 1
            MOVE      XDOCT,KEY6
            CALL      RDDOCT1
            DISPLAY   *P1:CVERT,*V2LON,FORM2,*HOFF,". ",*V2LON,XDOCT,*HOFF:
                      *P41:CVERT,*V2LON,XSPEC
            MOVE      XDOCT,LASTITEM          * set last displayed item
          ENDIF
          MOVE      CCOL,LASTCOL            * set last displayed column
          MOVE      CVERT,LASTROW           * set last displayed row
          IF        FORM2 = CHGITEM
            MOVE      CVERT,BVERT             * save keyin row
            MOVE      CCOL,BCOL               * save keyin col
            MOVE      XDOCT,CKYIDEF6          * save key to temp file
            MOVE      XSPEC,CKYIDEF3          * save key to temp file
          ENDIF
          GOTO      CHGI1000
.
.         the current page is full
.
CHGI4900  MOVE      TWO,CALLPOSN            * set call posn
.
.         set up to change the item
.
CHGI5000  MOVE      CHGITEM,FORM2           * item number to change
          MOVE      CKYIDEF6,KEY6           * read required record
          CALL      RDTEMPA
          IF        DELTEMP = ONE
            CALL      DETEMPA
          ENDIF
          MOVE      ZERO,EXIT
.
CHGI9999  RETURN
+
.*********************************************************************
.                   KDOCT000
.         enter the doctor code
.         Para's  : BCOL & BVERT  input column and row
.                   CKYIDEF6      default value
.                   CKYIMAND      1=mandatory
.*********************************************************************
KDOCT000  MOVE      FIVE,ECOL               * save col
          MOVE      BVERT,EVERT             * save row
          DISPLAY   *PECOL:EVERT,SP30,SP5
          MOVE      CHANGE,CKYIMAND         * 1 = mandatory
          CALL      PATDOCKY
          BRANCH    EXIT,KDOCT910,KDOCT900
.
KDOCT800  CALL      FDNAM000
          DISPLAY   *PECOL:EVERT,*V2LON,DCODE,*HOFF,SP1,DESCDOCT
          MOVE      DCODE,XDOCT
          MOVE      XDOCT,KEY6
          CALL      RDTEMPA
          BRANCH    OVRCD,KDOCT999
.
.>>>>>>>  BRANCH    CHANGE,KDOCT999
          DISPLAY   *P1:24,*B,"Doctor already entered.  ",*EL;
          CALL      HITENTER 
          GOTO      KDOCT000
.
.         exitchar entered
.
KDOCT900  MOVE      TWO,EXIT                * EXITCHAR entered
KDOCT910  MOVE      SP6,XDOCT
.
KDOCT999  RETURN
+
.*********************************************************************
.                   KSPEC000
.         enter the specialty/doctor type code
.         Para's  : BCOL & BVERT  input column and row
.                   CKYIDEF3      default value
.*********************************************************************
KSPEC000  MOVE      ZERO,CKYIMAND
          MOVE      CATDT,CODE
          MOVE      FIFTY5,ECOL             * save col
          MOVE      BVERT,EVERT             * save row
          DISPLAY   *PECOL:EVERT,*EL;
          CALL      PATCODKY
          BRANCH    EXIT,KSPEC910,KSPEC900
.
KSPEC800  DISPLAY   *PECOL:EVERT,*V2LON,ACODE,*HOFF,SP1,TDESC
          MOVE      ACODE,XSPEC
          MOVE      ONE,FORM2
          WHILE     FORM2 <= 5
            LOAD      KEY3,FORM2,DRTYPE,DRTYPE2,DRTYPE3,DRTYPE4,DRTYPE5
            MATCH     KEY3,XSPEC
            GOTO      KSPEC999 IF EQUAL       * matches one of doctors types
            ADD       ONE,FORM2
          DO
          DISPLAY   *P1:24,*B,*+,CATDTDSC," is invalid for H.C.P.  ",*EL;
          CALL      HITENTER
          GOTO      KSPEC000 
.
.         exitchar entered
.
KSPEC900  MOVE      TWO,EXIT                * EXITCHAR entered
KSPEC910  MOVE      SP6,XSPEC
          DISPLAY   *PECOL:EVERT,*EL;
.
KSPEC999  RETURN
+
.*********************************************************************
.         format the doctor name
.*********************************************************************
FDNAM000  MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,DESCDOCT
FDNAM999  RETURN
+
.*********************************************************************
.         temp file I/O
.*********************************************************************
RSTEMPA   READ      TEMPA,KEY6;;
          RETURN
.
RKTEMPA   MOVE      ZERO,OVRCD
          READKS    TEMPA;XDOCT,XSPEC
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMPA   MOVE      ZERO,OVRCD
          READ      TEMPA,KEY6;XDOCT,XSPEC
          GOTO      OVERCOND IF OVER
          RETURN
WRTEMPA   WRITE     TEMPA,KEY6;XDOCT,XSPEC
          RETURN
.
DETEMPA   DELETE    TEMPA,KEY6;;
          RETURN
..............................................................................
