. **********************************************************************
. * System    :   Personnel System                                     *
. * Program   :   IBAFMS61                                             *
. * Function  :   Accounting Codes Listing                             *
. **********************************************************************
. * Date      :   25.02.1991                                           *
. * Author    :   Neeriem Dye                                          *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
.$$$$$$
.        PROGRAM IBAFMS61
.             open files and initialise variables
.             WHILE user wants to print reports DO
.                  get desired category print range
.                  IF user confirms desire to print report
.                       THEN BEGIN
.                                 print report heading
.                                 FOR each category in print range DO
.                                      FOR each code record in category DO
.                                           get data
.                                           IF at end of page
.                                               THEN start new page and
.                                                    print heading
.                                           END IF
.                                           print code record
.                                      END FOR
.                                 END FOR
.                            END
.                  END IF
.             END WHILE
.        END
.$$$$$$
. Standard Personnel Include
.---------------------------
          INC       FMSSTDDF
          INC       FMSCODFD/INC       * codes
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
.
ALAB      INIT      "                "            * labels for screen A
ALAB1     INIT      "Category Listing"
ALAB2     INIT      " "
ALAB3     INIT      " "
ALAB4     INIT      " "
ALAB5     INIT      " "
ALAB6     INIT      " "
ALAB7     INIT      " "
ALAB8     INIT      " "
.
END       INIT      "End"                         * general constants
START     INIT      "Start"
DEFTFROM  INIT      "                    "
DEFTTO    INIT      "~~~~~~~~~~~~~~~~~~~~"
.
DESC      DIM       35
DESC1     DIM       35
DESC2     DIM       35
FROMCODE  DIM       2        * starting code
FROMLAB   DIM       6        * starting code label
LASTCAT   DIM       2        * last category
OPTIONA   FORM      1        * option on screen A
RANGE     DIM       2        * range entry variable
SKEY5     DIM       5        * save key for codes file
TOCODE    DIM       2        * ending code
TOLAB     DIM       6        * ending code label
.
PRGID     INIT      "IBAFMS61"
PRGNAM    INIT      "Accounting Codes Listing"
VERSION   INIT      "V12.00.00"
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      SELA0000      * display screen A and get "report by" option
          BRANCH    EXIT,ML9999   * user wants to exit screen
.
ML2000    CALL      SELB0000      * get reporting range
.
          CALL      CONTQST       * "Ok to Continue (Y/N/C) ?"
          BRANCH    CEXIT,ML3000,ML2000,ML1000  * user wants to exit screen ?
.
ML3000    CALL      PRNT0000      * print report
          GOTO      ML1000        * get another option
.
ML9999    CHAIN     PGM
.******************************************************************************
.* INIT - Open Files                                                          *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening fmscodaf";  * codes
          OPEN      FMSCODA1,"fmscodaf"
          OPEN      FMSCODA2,"fmscodaf"
.
          DISPLAY   *P1:24,*EL;
.
INIT9999  RETURN
.**********************************************************************
. SELA -  display screen A and get "print by" option   Called By ML
.         Returns : EXIT       (0=continue, 1=quit)
.**********************************************************************
SELA0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = ",ALAB1:
                    *P6:7,"Select Option :";
.
SELA1000  KEYIN     *P22:7,"_",*EL,*P22:7,*V2LON,OPTIONA; * read option
          COMPARE   OPTIONA,ZERO
          GOTO      SELA9500 IF EQUAL            * user wants to exit ?
.
          PACK      ALAB,SP70                    * clear screen label
          LOAD      ALAB,OPTIONA,ALAB1,ALAB2,ALAB3: * get screen label
                                 ALAB4,ALAB5,ALAB6,ALAB7,ALAB8
.
          MATCH     SP70,ALAB
          GOTO      SELA9000 IF NOT EQUAL        * legal option ?
.
          BEEP                                   * illegal option
          GOTO      SELA1000                     * get another option
.
SELA9000  MOVE      ZERO,EXIT                    * continue
          GOTO      SELA9999
.
SELA9500  MOVE      ONE,EXIT                     * quit
.
SELA9999  RETURN
.**********************************************************************
. SCRB -  display screen B               Called By SELB, REDISPLAYS
.**********************************************************************
SCRB0000  DISPLAY   *P1:3,*EF:
                    *P6:4,*V2LON,"Print ",ALAB,*HOFF:
                    *P6:6,"Starting Category : ",*V2LON,FROMLAB:
                    SP2,DESC1,*HOFF:
                    *P6:8,"Ending Category   : "
.
SCRB9999  RETURN
.**********************************************************************
. SELB -  display screen B and get report type       Called By ML
.         Returns : FROMCODE, TOCODE  (printing range)
.                   FROMLAB, TOLAB  (printing range labels)
.**********************************************************************
SELB0000  MOVE      SP70,FROMLAB       * clear label
          UNPACK    SP70,DESC1,DESC2   * clear description labels
          CALL      SCRB0000           * display screen
.
.---- get starting code ----
.
SELB1000  MOVE      DEFTFROM,FROMCODE  * clear from code
          CLEAR     FROMLAB            * clear from label
          MOVE      START,FROMLAB      * clear starting label
.
          MOVE      "6",CVERT          * set up screen address
          MOVE      "26",CCOL
          CALL      GCOD0000           * read in starting code
.
          BRANCH    EXIT,SELB2000      * print from start ?
          PACK      FROMCODE,RANGE,SP70 * save code as starting code
          MOVE      FROMCODE,FROMLAB   * save starting label
          MOVE      DESC,DESC1
.
.---- get ending code ----
.
SELB2000  DISPLAY   *PCCOL:CVERT,*V2LON,FROMLAB:
                    SP2,DESC1          * display from label
          MOVE      DEFTTO,TOCODE      * clear to code
          MOVE      END,TOLAB          * clear ending label
.
          MOVE      "8",CVERT          * set up screen address
          MOVE      "26",CCOL
          CALL      GCOD0000           * read in ending code
.
          BRANCH    EXIT,SELB3000      * print to end ?
          PACK      TOCODE,RANGE,SP70  * save code as ending code
          MOVE      TOCODE,TOLAB       * save ending label
          MOVE      DESC,DESC2
.
.---- valid range ? ----
.
SELB3000  DISPLAY   *PCCOL:CVERT,*V2LON,TOLAB:
                    SP2,DESC2          * display to label
          MATCH     FROMCODE,TOCODE
          GOTO      SELB9999 IF NOT LESS * to code not less than from code ?
.
          DISPLAY   *P1:24,*EL,*B,"Illegal Range Entered - ";
          CALL      HITENTER
          GOTO      SELB0000           * get another range
.
SELB9999  RETURN
.**********************************************************************
. GCOD - read in code                              Called By ML
.         Requires : CCOL,CVERT (screen address to get input)
.         Returns  : RANGE      (code entered)
.                    EXIT       (0=code entered, 1=nothing entered)
.**********************************************************************
GCOD0000  MOVE      SP70,DESC
          BRANCH    OPTIONA,GCOD1000,GCOD9999,GCOD9999,GCOD9999:
                            GCOD9999,GCOD9999,GCOD9999,GCOD9999
.
.---- print by type 1 ----
.
GCOD1000  CALL      KCATFM00           * read in category
          BRANCH    EXIT,GCOD9999      * nothing entered
          BRANCH    OVRCD,GCOD9000     * illegal category entered
          MOVE      FMCDCATY,RANGE     * move entry to output variable
          MOVE      FMCDDESC,DESC
          GOTO      GCOD9999
.
.---- illegal code entered ----
.
GCOD9000  DISPLAY   *P1:24,*EL,*B,"Illegal Category Entered - "; * illegal 
          CALL      HITENTER
          GOTO      GCOD0000           * get another entry
.
GCOD9999  RETURN
.********************************************************************
. PRNT - print report                               Called by ML
.         Requires : FROMLAB,TOLAB   (print range display labels)
.                    FROMCODE,TOCODE (print range)
.********************************************************************
PRNT0000  DISPLAY   *P50:24,"Printing :   ";
          MOVE      ZERO,CPAGENO                 * reset page no.
          CLOCK     TIME,CTIMEIS                 * get current time
          MOVE      SP70,LASTCAT                 * clear last category
          MOVE      "999",CLNO                   * init last line number
.
          CALL      FILE0000                    * go to correct position in file
.
PRNT1000  CALL      NREC0000                     * get next record
          BRANCH    OVRCD,PRNT9000               * end of file ?
.
          DISPLAY   *P61:24,*V2LON,FMCDCODE,*EL;
          COMPARE   "55",CLNO                    * end of page ?
          GOTO      PRNT2000 IF LESS
          MOVE      SP70,LASTCAT                 * clear last category
.
PRNT2000  MATCH     FMCDCATY,LASTCAT             * next category ?
          CALL      HEAD0000 IF NOT EQUAL        * start new page
.
          MOVE      FMCDCATY,LASTCAT             * set last category
          MATCH     FMCDCODE,SP70                * category record ?
          GOTO      PRNT1000 IF EQUAL            * get next record
.
          CALL      WRIT0000                     * print details
          GOTO      PRNT1000                     * get next record
.
PRNT9000  COMPARE   ZERO,CPAGENO
          CALL      HEAD0000 IF EQUAL          * print header if nothing printed
          CALL      UND80                        * print line
          PRINT     "****   End of Report   ****"
.
PRNT9999  RETURN
.**********************************************************************
.  FILE - Set file pointer                             Called By PRNT
.**********************************************************************
FILE0000  BRANCH    OPTIONA,FILE0100,FILE9999,FILE9999,FILE9999:
                            FILE9999,FILE9999,FILE9999,FILE9999
.
.---- print by type 1 ----
.
FILE0100  PACK      KEY5,FROMCODE,SP70
          CALL      RSFMCD1
          GOTO      FILE9999
.
FILE9999  RETURN
.**********************************************************************
.  NREC - Get next record                              Called By PRNT
.**********************************************************************
NREC0000  BRANCH    OPTIONA,NREC0100,NREC9999,NREC9999,NREC9999:
                            NREC9999,NREC9999,NREC9999,NREC9999
.
.---- print by type 1 ----
.
NREC0100  CALL      RKFMCD1
          BRANCH    OVRCD,NREC9500
.
          MATCH     FMCDCATY,TOCODE
          GOTO      NREC9500 IF LESS             * out of range ?
          GOTO      NREC9000
.
NREC9000  MOVE      ZERO,OVRCD         * continue
          GOTO      NREC9999
.
NREC9500  MOVE      ONE,OVRCD          * quit
.
NREC9999  RETURN
.********************************************************************
. HEAD - print report                               Called by PRNT
.         Requires : FROMLAB,TOLAB   (print range display labels)
.                    FROMCODE,TOCODE (print range)
.********************************************************************
HEAD0000  CALL      HEAD80                       * print heading
          PACK      SKEY5,FMCDCATY,FMCDCODE,SP5  * save current key
          MOVE      SP70,FMCDDESC                * clear description
          PACK      KEY5,FMCDCATY,SP70           * set up key
          CALL      RDFMCD1                      * read category data
.
          ADD       "4",CLNO
          PRINT     *20,"Print ",ALAB:
                    *N,*20,"From : ",FROMLAB,SP2,DESC1:
                    *N,*20,"To   : ",TOLAB,SP2,DESC2,*N:
                    "Category : ",FMCDCATY,SP2,FMCDDESC  * print description
          CALL      UND80                        * print line
          PRINT     "| Code | Description          | Notes",*80,"|"
          ADD       "1",CLNO
          CALL      UND80                        * print line
.                                                * re-read current record
          MOVE      SP20,FMCDDESC                * clear description
          PACK      KEY5,SKEY5,SP5               * set up key
          CALL      RDFMCD1                      * read codes file
.
HEAD9999  RETURN
.********************************************************************
. WRIT - print data line                            Called by PRNT
.         Requires : FROMLAB,TOLAB (print range display labels)
.                    FROMCODE,TOCODE (print range)
.********************************************************************
WRIT0000  PRINT     "| ",FMCDCODE,"  | ",FMCDDESC," | ",*80,"|"
          ADD       "1",CLNO           * add one to line number
.
WRIT9999  RETURN
.******************************************************************************
.  Redisplay for standard keyins
.******************************************************************************
RCATFM00  
RCODFM00  
          CALL      SCRB0000           * display screen B
          RETURN
.******************************************************************************
.  INCLUDES FOR I/O'S
.******************************************************************************
.
.--------------------------------
. Standard personnel Code include
.--------------------------------
          INC       FMSSTDCD
          INC       FMSCODIO/INC       * codes
          INC       FMSCODKY           * codes
.
