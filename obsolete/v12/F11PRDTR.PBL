.*****************************************************************************
.*    Program      : F11PRDTR                                                *
.*    Description  : Conversion pridtraf to new File layout                  *
.*                                                                           *
.*    Author       : Steve Armstrong                                         *
.*    Date         :                                                         *
.*    Modifications:                                                         *
.*        V10.11.01 28/08/2017  Steve Armstrong   TSK 0837619                *
.*                  Added new fields PRDTLVIS                                *
.*        V10.11.00 17/07/2017  Steve Armstrong   TSK 0837619                *
.*                  Created program.                                         *
.*****************************************************************************
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
LOGFILE   FILE      ASCII, FIXED=256
.
.         Old File Definition
.
OLDFILE1   IFILE SQL, FIXED=372, KEY="U1-8,9-16,17-22"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
DPRDTUNQ  DIM       8      8        1     Unique Identifier
PRDTINVN  DIM       8      8        9     Invoice Number
DPRDTTRN  DIM       6      6        17    Transaction Number
.
PRDTDEBT  DIM       8      8        23    Debtor Number
DPRDTSCN  DIM       2      2        31    Scan Flag
.
PRDTSDAT  DIM       8      8        33    Service Date
PRDTSTIM  DIM       6      6        41    Service Time
.
PRDTDESC  DIM       30     30       47    Description (Item/Receipt/Journal)
PRDTAMNT  FORM     12.2    8        77    Total Amount (Item/Receipt/Journal)
.
PRDTPRAC  DIM       6      6        85    Medical Practice Code
PRDTDOCT  DIM       6      6        91    Service Doctor
PRDTSERV  FORM      5      4        97    Number of Services
PRDTREFD  DIM       6      6        101   Referring Doctor
PRDTREFT  DIM       50     50       107   Referring Details
PRDTCLAM  DIM       3      3        157   Claim Code (Category CL)
PRDTCODE  DIM       3      3        160   Patient Indicator (Category GI)
PRDTMESS  DIM       3      3        163   Message Code (PATMESG1)
.
PRDTRTYP  FORM      1      2        166   Transaction Record Type
.                                              1 = Item
.                                              2 = Cash Receipt
.                                              3 = Journals
.
PRDTIFLG  FORM      2      2        168   For Item Transactions
.                                            Item Flag
.                                              0 = MBS
.                                              1 = AMA
.                                         For Receipt Transactions
.                                            Receipt Type
.                                              1 = From Patient
.                                              2 = From Health Fund
.                                              3 = From Insurance Company
.                                              4 = From Govt. Dept
PRDTITMN  DIM       9      9        170   For item transactions
.                                            Item Number
.                                         For Receipt Transactions
.                                            First 6 chars. = code for
.                                            health fund or ins. company
PRDTSUBN  DIM       3      3        179   Subitem number
PRDTSETC  DIM       3      3        182   Item Set Code (Misc. Item Grp-Cat FI)
.
PRDTRECN  DIM       8      8        185   Receipt Number
.
PRDTMETH  FORM      1      2        193   Payment Method
.                                              0 = Not Applicable
.                                              1 = Cash
.                                              2 = Cheque
.                                              3 = Credit Card
.                                              4 = Merchant Card (eg AMEX)
.                                              5 = Money Order
.                                              6 = EFTPOS
.                                              7 = BPAY
.                                              8 = Virtual Terminal (HICAPS)
.                                              9 = Medicare
.
PRDTTTYP  DIM       3      3        195   Transaction Type
.                                              BD = Bad Debt
.                                              CR = Credit
.                                              WO = Write Off
.                                              PY = Payments
.                                              DB = Debit
PRDTINVP  FORM      1      2        198   Invoice Print Status
.                                              0 = Not Yet Printed
.                                              1 = Printed
.
.  The next two fields are automatically updated by the I/O for every
.  write and update
.
PRDTCDAT  DIM       8      8        200  Date This Record Last Changed(CCYYMMDD)
PRDTCTIM  DIM       8      8        208  Time This Record Last Changed(HH:MM:SS)
.
PRDTBATC  DIM       8      8        216   Batch Number (Item,Receipt,Journal)
.
PRDTUDF1  DIM       3      3        224   User Defined Field 1 (Category G1)
PRDTUDF2  DIM       3      3        227   User Defined Field 2 (Category G2)
PRDTUDF3  DIM       3      3        230   User Defined Field 3 (Category G3)
PRDTUDF4  DIM       3      3        233   User Defined Field 4 (Category G4)
.
PRDTADMN  DIM       3      3        236   Admission Class (Cat A)
PRDTMISG  DIM       3      3        239   For Receipt Transactions
.                                            Merchant Card Code (if applicable)
PRDTRDTE  DIM       8      8        242   Referral Date (CCYYMMDD)
PRDTIAMT  FORM     12.2    8        250   Item Amount
PRDTOPS   DIM       20     20       258   Operation Codes
PRDTMFLG  FORM      1      2        278   Multiple Service Code Flag
.                                            0 = No
.                                            1 = Yes
PRDTGLT   DIM       1      1        280   Transferred to G/L 1=Yes
PRDTMEDA  DIM       10     10       281   Medicare Assign Form Number
PRDTGSTA  FORM      1      2        291   Is GST Applicable to this item
PRDTGSTC  DIM       6      6        293   GST Payable Code
PRDTGSTM  FORM      12.2   8        299   GST Amount
DPRDTAPA  DIM       1      1        307   Accounts Payable flag
.                                         0 = Not applicable
.                                         1 = Invoice not balanced
.                                         2 = Waiting to be sent (balanced)
.                                         3 = Sent to Accounts payable
PRDTRFPD  DIM       3      3        308   Referral Period (Cat RF)
PRDTREFN  DIM       8      8        311   Referral/Encounter Number
PRDTENCN  DIM       8      8        319   Encounter Number
PRDTCNST  DIM       1      1        327   Credit Note Status
.                                         0-No Credit Note
.                                         1-Fully Credited
.                                         2-Credit by Item
PRDTPFLG  FORM      1      2        328   Pathology Flag
.                                         0 = No
.                                         1 = Yes
PRDTS4B3  FORM      1      2        330   S4B3 Flag
.                                         0 = No
.                                         1 = Yes
PRDTEFFD  DIM       8      8        332   Date Item assigned to Fees Invoiced
PRDTPCOD  DIM       3      3        340   Payment Code (Cat Py)
PRDTACOI  DIM       1      1        343   After Care Override Indicator
.                                          0 or Blank = No
.                                          1 = Yes
PRDTTDUR  DIM       3      3        344   Time Duration (mins)
OLDSPARE  DIM       25     25       347   Spare Variable
.
.End of Record                      372
.
.
.         New File Definition
.
PRIDTRA1   IFILE SQL, FIXED=529, KEY="U1-8,9-16,17-22"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.DPRDTUNQ DIM       8      8        1     Unique Identifier
.PRDTINVN DIM       8      8        9     Invoice Number
.DPRDTTRN DIM       6      6        17    Transaction Number
.
.PRDTDEBT DIM       8      8        23    Debtor Number
.DPRDTSCN DIM       2      2        31    Scan Flag
.
.PRDTSDAT DIM       8      8        33    Service Date
.PRDTSTIM DIM       6      6        41    Service Time
.
.PRDTDESC DIM       30     30       47    Description (Item/Receipt/Journal)
.PRDTAMNT FORM     12.2    8        77    Total Amount (Item/Receipt/Journal)
.
.PRDTPRAC DIM       6      6        85    Medical Practice Code
.PRDTDOCT DIM       6      6        91    Service Doctor
.PRDTSERV FORM      5      4        97    Number of Services
.PRDTREFD DIM       6      6        101   Referring Doctor
.PRDTREFT DIM       50     50       107   Referring Details
.PRDTCLAM DIM       3      3        157   Claim Code (Category CL)
.PRDTCODE DIM       3      3        160   Patient Indicator (Category GI)
.PRDTMESS DIM       3      3        163   Message Code (PATMESG1)
.
.PRDTRTYP FORM      1      2        166   Transaction Record Type
.                                              1 = Item
.                                              2 = Cash Receipt
.                                              3 = Journals
.
.PRDTIFLG FORM      2      2        168   For Item Transactions
.                                            Item Flag
.                                              0 = MBS
.                                              1 = AMA
.                                              2 = IMC Details
.                                         For Receipt Transactions
.                                            Receipt Type
.                                              1 = From Patient
.                                              2 = From Health Fund
.                                              3 = From Insurance Company
.                                              4 = From Govt. Dept
.PRDTITMN DIM       9      9        170   For item transactions
.                                            Item Number
.                                         For Receipt Transactions
.                                            First 6 chars. = code for
.                                            health fund or ins. company
.PRDTSUBN DIM       3      3        179   Subitem number
.PRDTSETC DIM       3      3        182   Item Set Code (Misc. Item Grp-Cat FI)
.
.PRDTRECN DIM       8      8        185   Receipt Number
.
.PRDTMETH FORM      1      2        193   Payment Method
.                                              0 = Not Applicable
.                                              1 = Cash
.                                              2 = Cheque
.                                              3 = Credit Card
.                                              4 = Merchant Card (eg AMEX)
.                                              5 = Money Order
.                                              6 = EFTPOS
.                                              7 = BPAY
.                                              8 = Virtual Terminal (HICAPS)
.                                              9 = Medicare
.
.PRDTTTYP DIM       3      3        195   Transaction Type
.                                              BD = Bad Debt
.                                              CR = Credit
.                                              WO = Write Off
.                                              PY = Payments
.                                              DB = Debit
.PRDTINVP FORM      1      2        198   Invoice Print Status
.                                              0 = Not Yet Printed
.                                              1 = Printed
.
.  The next two fields are automatically updated by the I/O for every
.  write and update
.
.PRDTCDAT DIM       8      8        200  Date This Record Last Changed(CCYYMMDD)
.PRDTCTIM DIM       8      8        208  Time This Record Last Changed(HH:MM:SS)
.
.PRDTBATC DIM       8      8        216   Batch Number (Item,Receipt,Journal)
.
.PRDTUDF1 DIM       3      3        224   User Defined Field 1 (Category G1)
.PRDTUDF2 DIM       3      3        227   User Defined Field 2 (Category G2)
.PRDTUDF3 DIM       3      3        230   User Defined Field 3 (Category G3)
.PRDTUDF4 DIM       3      3        233   User Defined Field 4 (Category G4)
.
.PRDTADMN DIM       3      3        236   Admission Class (Cat A)
.PRDTMISG DIM       3      3        239   For Receipt Transactions
.                                            Merchant Card Code (if applicable)
.PRDTRDTE DIM       8      8        242   Referral Date (CCYYMMDD)
.PRDTIAMT FORM     12.2    8        250   Item Amount
.PRDTOPS  DIM       20     20       258   Operation Codes
.PRDTMFLG FORM      1      2        278   Multiple Service Code Flag
.                                            0 = No
.                                            1 = Yes
.PRDTGLT  DIM       1      1        280   Transferred to G/L 1=Yes
.PRDTMEDA DIM       10     10       281   Medicare Assign Form Number
.PRDTGSTA FORM      1      2        291   Is GST Applicable to this item
.PRDTGSTC DIM       6      6        293   GST Payable Code
.PRDTGSTM FORM      12.2   8        299   GST Amount
.DPRDTAPA DIM       1      1        307   Accounts Payable flag
.                                         0 = Not applicable
.                                         1 = Invoice not balanced
.                                         2 = Waiting to be sent (balanced)
.                                         3 = Sent to Accounts payable
.PRDTRFPD DIM       3      3        308   Referral Period (Cat RF)
.PRDTREFN DIM       8      8        311   Referral/Encounter Number
.PRDTENCN DIM       8      8        319   Encounter Number
.PRDTCNST DIM       1      1        327   Credit Note Status
.                                         0-No Credit Note
.                                         1-Fully Credited
.                                         2-Credit by Item
.PRDTPFLG FORM      1      2        328   Pathology Flag
.                                         0 = No
.                                         1 = Yes
.PRDTS4B3 FORM      1      2        330   S4B3 Flag
.                                         0 = No
.                                         1 = Yes
.PRDTEFFD DIM       8      8        332   Date Item assigned to Fees Invoiced
.PRDTPCOD DIM       3      3        340   Payment Code (Cat Py)
.PRDTACOI DIM       1      1        343   After Care Override Indicator
.                                          0 or Blank = No
.                                          1 = Yes
.PRDTTDUR DIM       3      3        344   Time Duration (mins)
PRDTRPER  DIM       2      2        347   Referral Period (months, 1-98)
.                                          (R-Justified)
PRDTROVR  DIM       3      3        349   Referral Override (Cat ro)
PRDTMPOV  DIM       1      1        352   Multi Procedure Override
.                                          0 or Blank = No
.                                          1 = Yes
PRDTDSOV  DIM       1      1        353   Duplicate Service Override
.                                          0 or Blank = No
.                                          1 = Yes
PRDTSTXT  DIM       100    100      354   Service Text
PRDTASPF  DIM       1      1        454   Alternate Service Provider Flag
.                                          0 or Blank = No
.                                          1 = Yes
PRDTASPC  DIM       10     10       455   Alternate Service Provider HCP Code
.                                          (pmshcpaf)
PRDTAICF  DIM       1      1        465   Alternate IFC Issue Code Flag
.                                          0 or Blank = No
.                                          1 = Yes
PRDTAICV  DIM       3      3        466   Alternate IFC Issue Code Value
.                                          (Cat ic)
PRDTNMPT  DIM       2      2        469   Number of Patients
.                                          (R-Justified numeric)
PRDTLSPN  DIM       6      6        471   Location Specific Practice No. Value
PRDTSDCD  DIM       2      2        477   Self Deem Code (Cat Sd)
.                                          SD = Self Deemed
.                                          SS = Substituted Service
.                                          N  = Not Self Deemed
PRDTLVIS  DIM       8      8        479   Linked Visit Number
PRDTSPAR  DIM       42     42       487   Spare Variable
.
.End of Record                      529
.
.
. Redefine FORM fields from key
. -----------------------------
PRDTUNIQ  FORM      8
PRDTTRAN  FORM      6
PRDTSCAN  FORM      2
PRDTAPAY  FORM      1
.
.
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       200
.
DEFPATH   DIM       60
DIM60     DIM       60
DISNUM    FORM      4
.
ERORTEXT  DIM       80
.
INPFILE   DIM       8
.
MINUS3    FORM      "-3"
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
SAVEFLG   FORM      1
.
.
. PROGRAM CONSTANTS
. -----------------
CREATEXT  INIT    "Log file created "
DASHLINE  INIT      "--------------------------------------------------":
                    "------------------------------"
ENDGTEXT  INIT    "Ending   fixit (Option "
OPENTEXT  INIT    "Log file opened "
SP60      INIT    "                                                            "
STARTTXT  INIT    "Starting fixit (Option "
.
.
.
PRGNAM    INIT      "CONVERSION PRIDTRFD"
PRGID     INIT      "F11PRDTR"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
.
          CALL      OLOG0000                      * create/open log file
MAIN0500  CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000              * run c-isam fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9000
.
MAIN2000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN6000:               * yes
                          MAIN0500:               * no
                          MAIN9000                * cancel
.
.         Running c-isam fix
.
MAIN6000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9000
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/zip saved file
.
MAIN9000  CALL      CLOG0000                      * close log file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
          
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run c-isam fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
.
PREP0000  CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
.
          CALL      SLOG0000                      * write start to log file
.
          MOVE      "pridtraf",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat pridtraf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sprdtraf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat pridtraf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sprdtraf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "pridtraf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Unable to remove the original file.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "sprdtraf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            MOVE      "Unable to open saved original file.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "pridtraf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " 529 UC1-8,9-16,17-22",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.         Add Index 2
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC33-40,1-8,9-16,17-22",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.         Add Index 3
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC23-30,31-32,9-16,17-22",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.         Add Index 4
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC9-16,33-40,17-22",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.         Add Index 5
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC280-280,33-40,1-8,9-16,17-22",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.         Add Index 6
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC307-307,23-30,31-32,9-16,17-22",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      PRIDTRA1,NEWFILE
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          GOTO      PREP9999
.
PREP5000  MOVE      "Old file not found.  ",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  MOVE      "Unable to copy original file.  (Error: ",ERORTEXT
          ENDSET    ERORTEXT
          APPEND    TASKID,ERORTEXT
          APPEND    ")  ",ERORTEXT
          RESET     ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
            MOVE      "File does not exist",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
              MOVE      "File has already been converted",ERORTEXT
              WRITE     LOGFILE,SEQ;ERORTEXT
            ELSE
              DISPLAY   "has caused an IO error - ";
              MOVE      "File has caused an IO Error",ERORTEXT
              WRITE     LOGFILE,SEQ;ERORTEXT
            ENDIF
          ENDIF
          MOVE      "File will be bypassed",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *+,ERORTEXT
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY22,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
.         Load new field variables
.
          MOVE      SP2,PRDTRPER
          MOVE      SP3,PRDTROVR
          MOVE      SP1,PRDTMPOV
          MOVE      SP1,PRDTDSOV
          PACK      PRDTSTXT,SP70,SP70
          MOVE      SP1,PRDTASPF
          MOVE      SP10,PRDTASPC
          MOVE      SP1,PRDTAICF
          MOVE      SP3,PRDTAICV
          MOVE      SP2,PRDTNMPT
          MOVE      SP6,PRDTLSPN
          MOVE      SP2,PRDTSDCD
          MOVE      SP8,PRDTLVIS
          MOVE      SP70,PRDTSPAR
.
          PACK      KEY22,PRDTUNIQ,PRDTINVN,PRDTTRAN
          CALL      WRPRDT1                       * write to new file
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     PRIDTRA1                      * close new file
          CLOSE     OLDFILE1                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS:
                    *P1:24;
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
.
VALD0000  MOVE      ZERO,EXIT
.
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            MOVE      "Directory path must end with a slash (/) ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Directory does not exist ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or zip save file                                     *
.**********************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "sprdtraf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "gzip -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Saved file not zipped or removed.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:24,*EL,*B,"Finished processing  ",CDATE,SP1,CTIMEIS:
                    ".  ";
          CALL      HITENTER
.
          CALL      ELOG0000                     * write an end log entry
.
ENDP9999  RETURN
+
.**********************************************************************
.*        DEFT0000 -  Get the default directory                       *
.**********************************************************************
.
DEFT0000  EXECUTE   "ldat pridtraf.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  MOVE      "Error finding 'pridtraf'.  ",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.*                            OLOG0000                                       *
.*                    Open the log file                                      *
.*****************************************************************************
.
OLOG0000  CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      LOGFILE,"f11prdtr.log"       * open log file
          TRAPCLR   IO
          IF        OVRCD = 1
            PREP      LOGFILE,"f11prdtr.log"     * create and open log file
            WRITE     LOGFILE,MINUS3;DASHLINE
            WRITE     LOGFILE,SEQ;CREATEXT,CDATE,SP1,CTIMEIS
          ELSE
            WRITE     LOGFILE,MINUS3;DASHLINE
            WRITE     LOGFILE,SEQ;OPENTEXT,CDATE,SP1,CTIMEIS
          ENDIF
.
OLOG9999  RETURN
+
.*****************************************************************************
.*                            CLOG0000                                       *
.*                    Close the log file                                     *
.*****************************************************************************
.
CLOG0000  CLOSE     LOGFILE
.
CLOG9999  RETURN
+
.*****************************************************************************
.*                            SLOG0000                                       *
.*                    Write a start log to the log file                      *
.*****************************************************************************
.
SLOG0000  WRITE     LOGFILE,SEQ;DASHLINE
          WRITE     LOGFILE,SEQ;STARTTXT,COPTION,RBRACK,SP1,CDATE,SP1:
                                   CTIMEIS
.
SLOG9999  RETURN
+
.*****************************************************************************
.*                            ELOG0000                                       *
.*                    Write an end log to the log file                       *
.*****************************************************************************
.
ELOG0000  WRITE     LOGFILE,SEQ;ENDGTEXT,COPTION,RBRACK,SP1,CDATE,SP1:
                                   CTIMEIS
ELOG9999  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
.         Old File I/O Routines
.
READSOLD  RESET     KEY22
          READ      OLDFILE1,KEY22;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;DPRDTUNQ,PRDTINVN,DPRDTTRN,PRDTDEBT:
                                  DPRDTSCN,PRDTSDAT,PRDTSTIM,PRDTDESC,PRDTAMNT:
                                  PRDTPRAC,PRDTDOCT,PRDTSERV,PRDTREFD,PRDTREFT:
                                  PRDTCLAM,PRDTCODE,PRDTMESS,PRDTRTYP,PRDTIFLG:
                                  PRDTITMN,PRDTSUBN,PRDTSETC,PRDTRECN,PRDTMETH:
                                  PRDTTTYP,PRDTINVP,PRDTCDAT,PRDTCTIM,PRDTBATC:
                                  PRDTUDF1,PRDTUDF2,PRDTUDF3,PRDTUDF4,PRDTADMN:
                                  PRDTMISG,PRDTRDTE,PRDTIAMT,PRDTOPS,PRDTMFLG:
                                  PRDTGLT,PRDTMEDA,PRDTGSTA,PRDTGSTC,PRDTGSTM:
                                  DPRDTAPA,PRDTRFPD,PRDTREFN,PRDTENCN,PRDTCNST:
                                  PRDTPFLG,PRDTS4B3,PRDTEFFD,PRDTPCOD,PRDTACOI:
                                  PRDTTDUR,OLDSPARE
          GOTO      OVERCOND IF OVER
          MOVE      DPRDTUNQ,PRDTUNIQ
          MOVE      DPRDTTRN,PRDTTRAN
          MOVE      DPRDTSCN,PRDTSCAN
          MOVE      DPRDTAPA,PRDTAPAY
          RETURN
.
.         New File I/O Routines
.
RSPRDT1   RESET     KEY22
          READ      PRIDTRA1,KEY22;;
          RETURN
.
RKPRDT1   MOVE      ZERO,OVRCD
          READKS    PRIDTRA1;DPRDTUNQ,PRDTINVN,DPRDTTRN,PRDTDEBT:
                                  DPRDTSCN,PRDTSDAT,PRDTSTIM,PRDTDESC,PRDTAMNT:
                                  PRDTPRAC,PRDTDOCT,PRDTSERV,PRDTREFD,PRDTREFT:
                                  PRDTCLAM,PRDTCODE,PRDTMESS,PRDTRTYP,PRDTIFLG:
                                  PRDTITMN,PRDTSUBN,PRDTSETC,PRDTRECN,PRDTMETH:
                                  PRDTTTYP,PRDTINVP,PRDTCDAT,PRDTCTIM,PRDTBATC:
                                  PRDTUDF1,PRDTUDF2,PRDTUDF3,PRDTUDF4,PRDTADMN:
                                  PRDTMISG,PRDTRDTE,PRDTIAMT,PRDTOPS,PRDTMFLG:
                                  PRDTGLT,PRDTMEDA,PRDTGSTA,PRDTGSTC,PRDTGSTM:
                                  DPRDTAPA,PRDTRFPD,PRDTREFN,PRDTENCN,PRDTCNST:
                                  PRDTPFLG,PRDTS4B3,PRDTEFFD,PRDTPCOD,PRDTACOI:
                                  PRDTTDUR,PRDTRPER,PRDTROVR,PRDTMPOV,PRDTDSOV:
                                  PRDTSTXT,PRDTASPF,PRDTASPC,PRDTAICF,PRDTAICV:
                                  PRDTNMPT,PRDTLSPN,PRDTSDCD,PRDTLVIS,PRDTSPAR
          GOTO      OVERCOND IF OVER
          MOVE      DPRDTUNQ,PRDTUNIQ
          MOVE      DPRDTTRN,PRDTTRAN
          MOVE      DPRDTSCN,PRDTSCAN
          MOVE      DPRDTAPA,PRDTAPAY
          RETURN
.
UPPRDT1   MOVE      PRDTUNIQ,DPRDTUNQ
          MOVE      PRDTTRAN,DPRDTTRN
          MOVE      PRDTSCAN,DPRDTSCN
          MOVE      PRDTAPAY,DPRDTAPA
          UPDATE    PRIDTRA1;DPRDTUNQ,PRDTINVN,DPRDTTRN,PRDTDEBT:
                                  DPRDTSCN,PRDTSDAT,PRDTSTIM,PRDTDESC,PRDTAMNT:
                                  PRDTPRAC,PRDTDOCT,PRDTSERV,PRDTREFD,PRDTREFT:
                                  PRDTCLAM,PRDTCODE,PRDTMESS,PRDTRTYP,PRDTIFLG:
                                  PRDTITMN,PRDTSUBN,PRDTSETC,PRDTRECN,PRDTMETH:
                                  PRDTTTYP,PRDTINVP,PRDTCDAT,PRDTCTIM,PRDTBATC:
                                  PRDTUDF1,PRDTUDF2,PRDTUDF3,PRDTUDF4,PRDTADMN:
                                  PRDTMISG,PRDTRDTE,PRDTIAMT,PRDTOPS,PRDTMFLG:
                                  PRDTGLT,PRDTMEDA,PRDTGSTA,PRDTGSTC,PRDTGSTM:
                                  DPRDTAPA,PRDTRFPD,PRDTREFN,PRDTENCN,PRDTCNST:
                                  PRDTPFLG,PRDTS4B3,PRDTEFFD,PRDTPCOD,PRDTACOI:
                                  PRDTTDUR,PRDTRPER,PRDTROVR,PRDTMPOV,PRDTDSOV:
                                  PRDTSTXT,PRDTASPF,PRDTASPC,PRDTAICF,PRDTAICV:
                                  PRDTNMPT,PRDTLSPN,PRDTSDCD,PRDTLVIS,PRDTSPAR
          RETURN
.
WRPRDT1   RESET     KEY22
          MOVE      PRDTUNIQ,DPRDTUNQ
          MOVE      PRDTTRAN,DPRDTTRN
          MOVE      PRDTSCAN,DPRDTSCN
          MOVE      PRDTAPAY,DPRDTAPA
          WRITE     PRIDTRA1,KEY22;KEY22,PRDTDEBT:
                                  DPRDTSCN,PRDTSDAT,PRDTSTIM,PRDTDESC,PRDTAMNT:
                                  PRDTPRAC,PRDTDOCT,PRDTSERV,PRDTREFD,PRDTREFT:
                                  PRDTCLAM,PRDTCODE,PRDTMESS,PRDTRTYP,PRDTIFLG:
                                  PRDTITMN,PRDTSUBN,PRDTSETC,PRDTRECN,PRDTMETH:
                                  PRDTTTYP,PRDTINVP,PRDTCDAT,PRDTCTIM,PRDTBATC:
                                  PRDTUDF1,PRDTUDF2,PRDTUDF3,PRDTUDF4,PRDTADMN:
                                  PRDTMISG,PRDTRDTE,PRDTIAMT,PRDTOPS,PRDTMFLG:
                                  PRDTGLT,PRDTMEDA,PRDTGSTA,PRDTGSTC,PRDTGSTM:
                                  DPRDTAPA,PRDTRFPD,PRDTREFN,PRDTENCN,PRDTCNST:
                                  PRDTPFLG,PRDTS4B3,PRDTEFFD,PRDTPCOD,PRDTACOI:
                                  PRDTTDUR,PRDTRPER,PRDTROVR,PRDTMPOV,PRDTDSOV:
                                  PRDTSTXT,PRDTASPF,PRDTASPC,PRDTAICF,PRDTAICV:
                                  PRDTNMPT,PRDTLSPN,PRDTSDCD,PRDTLVIS,PRDTSPAR
          RETURN
.
          INC       STD001IO
