.***************************************************************************
.* System    :   Waiting List                                              *
.* Program   :   F90WTTR1                                                  *
.***************************************************************************
.* Date      :   01/05/2008                                                *
.* Author    :   Jill Habasque                                             *
.* Function  :   This program will populate wattr1af.wtwmassr              *
.***************************************************************************
.
          INC       STD001FD
          INC       WATTR1FD/INC
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
STATUS    FORM      1
STATDESC  DIM       12                      * waiting list status description
STAT1     INIT      "Unscheduled "
STAT2     INIT      "Scheduled   "
STAT3     INIT      "Pre-Admitted"
STAT4     INIT      "Admitted    "
STAT5     INIT      "Discharged  "
STAT6     INIT      "Removed     "
.
PRGID     INIT      "F90WTTR1"
PRGNAM    INIT      "Populate WL Assessor Code    "
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      WATTR1A2,"wattr1af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  CALL      KNPS0000              * keyin status to be processed
          CALL      CONTQST
          BRANCH    CEXIT,PROC1000:          * yes
                          PROC9999:          * no
                          PROC9999           * cancel
.
PROC1000  PACK      KEY20,STATUS,SP70
          CALL      RDSTREA2
PROC1500  CALL      RDKTREA2
          BRANCH    OVRCD,PROC9000
.
          COMPARE   STATUS,WMSTAT
          GOTO      PROC9000 IF NOT EQUAL
.
          MATCH     SP70,WTWMASSR
          GOTO      PROC1500 IF NOT EQUAL
.
          PACK      WTWMASSR,WMDOCTOR
          CALL      UPTREA2
          GOTO      PROC15000
.
PROC9000  DISPLAY   *P1:24,"Finished ";
          CALL      HITENTER
.
PROC9999  RETURN
+
.*********************************************************************
.*                  KNPS0000                    Called by : UPST0000 *
.*        Enter the new procedure status                             *
.*        Returns : ENTSTAT       the procedure status               *
.*                  ENTDATE       entered date (if new status = 1,2) *
.*                  LASTDATE      the last review date               *
.*                                                                   *
.*********************************************************************
KNPS0000  DISPLAY   *P1:8,*EL,*P1:13,*EL,*P1:24,*EL:
                    *V2LON,"1",*HOFF,"=Unscheduled, ":
                    *V2LON,"2",*HOFF,"=Scheduled, ":
                    *V2LON,"3",*HOFF,"=Pre-Admitted, ":
                    *V2LON,"4",*HOFF,"=Admitted, ":
                    *V2LON,"5",*HOFF,"=Discharged, ":
                    *V2LON,"6",*HOFF,"=Removed"
.
          KEYIN     *P28:8,"Status : ",STATUS
.
          COMPARE   ZERO,STATUS            * was anything entered ??
          GOTO      KNPS9999 IF EQUAL       * no, exit
.
          LOAD      STATDESC,STATUS,STAT1,STAT2,STAT3,STAT4,STAT5,STAT6 * new
          DISPLAY   *P36:8,STATDESC         * display new status description
.
          BRANCH    STATUS,KNPS9999,KNPS9999,KNPS9999,KNPS9999:
                            KNPS9999,KNPS9999
.
          BEEP
          GOTO      KNPS0000
.
KNPS9999  RETURN
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       WATTR1IO/INC
