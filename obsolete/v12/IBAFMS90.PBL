. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS90                                             *
. * Desc      :   Actuals Total Account Update                         *
. **********************************************************************
. * Date      :   10.10.1990                                           *
. * Author    :   B.G.Ackland (IBA)                                    *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
. * VF.00.01  11.01.2000 Charles Handaya                               *
. *           Recompiled for Audit File date                           *
. **********************************************************************
.$$$$$$
.Accounting Version 6
.--------------------
.Actuals
.-------
.6. Total Account Update
.-----------------------
.The total account update program for budgets will consist of one passes
.through the budget file to be processed.
.
.The program is written using a procedure for the update section of the
.code. This procedure will be used in the automatic daily update program.
.
.Screen Layout
.-------------------------------------------------------------------------------
.IBAFMS90            Information Builders Australia              Date : 12/12/89
.V6.00               Budget Total Account Update
.
. 1. Budget Type          : ____
.
.Select Field, (P)ost, (C)ancel :
.-------------------------------------------------------------------------------
.Budget Type      : Use Std Keyin Routine.
.
.(C)ancel         : Exit Program
.
.(P)ost           : PROC   Update procedure
.
.NOTE - Add new index on the account master file FMSAMAFD
.FMSAMAA4  IFILE  SQL, FIXED=309, KEY="U53-53,1-2,3-14" 
.
.Update Procedure - DEFPROC
.----------------
.INCLUDE NAME - PRCFMSBU.PBL
.
.For the budget type passed to this procedure perform the following.
.
.1. OPEN Budget File           - fmsbtttt   where tttt = budget type
.   OPEN Account Master File   - fmsamaaf
.   OPEN Report Sequence File  - fmsarsaf
.
.2. Pass 1 Clear Totals
.   if program id = IBAFMS90 Display "Clearing : " on line 24
.   Loop through the account master file by index 4 U53-53,1-2,3-14
.   for all account of Type 6. ( ie TOTAL ACCOUNTS )
.   if program id = IBAFMS90 Display Each Account on line 24
.   Read budget file
.        If no record exists
.             Continue Loop
.        if record exists
.             Move Zero to Budget Period Values and Update Record
.             Continue Loop
.        
.
.3. Pass 2 Accumulate Total Accounts
.   if program id = IBAFMS90 Display "Accumulating : " on line 24
.   Loop through the account reporting sequence file by index 3
.   FMSARSA3  IFILE  SQL, FIXED=71, KEY="U3-4,27-29,15-26,1-2"  * Total Calc 
.   if program id = IBAFMS90 Display Each Account on line 24
.   Read Budget Record
.       If no record exists
.            Continue Loop
.       If record exists
.            Move period values to save values
.            Read budget record for Addto Ledger/Account
.                 If no record exists
.                    Write new record with saved period values
.                    Continue loop
.                 If record exists
.                    Add period values 
.                    Update Total Record
.                    Continue Loop
.
.
.
.$$$$$$
. Standard FMS Accounting System Include
.---------------------------------------
          INC       FMSSTDDF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
          INC       FMSCONFD/INC
          INC       FMSAMAFD/INC
          INC       FMSCSAFD/INC
          INC       FMSLMAFD/INC
          INC       FMSLMCFD/INC
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
FINYEAR   DIM       4          * Passed to procedure the budget type for update
BANKNAME  DIM       35
CREDNAME  DIM       35
DEBTNAME  DIM       35
DISCNAME  DIM       35
PAYMNAME  DIM       35
AGSTNAME  DIM       35
CGSTNAME  DIM       35
.
PRGID     INIT      "IBAFMS90"
PRGNAM    INIT      "Actuals Total Account Update"
VERSION   INIT      "V12.00.00"
.
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML0100    CALL      SCRA0000           * Display Screen Layout
          CALL      ACTA0000           * Accept Financial Year
          BRANCH    EXIT,ML9999        * exit program Null Entry
.
          CALL      SELA0000           * Allow modification
          BRANCH    EXIT,ML9999        * exit program
.
          PROC      FMSTU000           * Perform Actuals Update
          DISPLAY   *P1:24,*EL,"Update Complete - ";
          CALL      HITENTER
          GOTO      ML0100             * get next entry
.
ML9999    CHAIN     PGM                * chain back to menu
.
.******************************************************************************
.* INIT - Open Files                             Called by ML0000             *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening fmslmaaf";  * ledger master file
          OPEN      FMSLMAA1,"fmslmaaf"
.
          DISPLAY   *P64:24,"fmsamaaf";  * account master file
          OPEN      FMSAMAA1,"fmsamaaf"
.
          DISPLAY   *P64:24,"fmscsaaf";  * control account master file
          OPEN      FMSCSAA1,"fmscsaaf"
.
          DISPLAY   *P64:24,"fmslmcaf";  * ledger calendar file
          OPEN      FMSLMCA1,"fmslmcaf"
.
          DISPLAY   *P64:24,"controlf";  * control file
          OPEN      CONTROLF,"controlf"
          CALL      RDFMCO52
          MOVE      SP70,KEY2
          CALL      RSFMLA1
          CALL      RKFMLA1
.
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.******************************************************************************
.* SCRA - Display Screen                         Called by ML0000             *
.******************************************************************************
RLMCFM00
SCRA0000  DISPLAY   *P1:4,*EF,*P2:4,*V2LON,"1",*HOFF:
                    ". Financial Year",*P26:4,": "
          RETURN
.******************************************************************************
.* ACTA - Accept initial value                   Called by ML0000             *
.******************************************************************************
ACTA0000  MOVE      "28",CCOL
          MOVE      "4",CVERT
          CALL      KLMCFM00           * keyin Financial Year code
          BRANCH    EXIT,ACTA9000,ACTA9000
          DISPLAY   *P28:4,*V2LON,FMLCYEAR
          MOVE      FMLCYEAR,FINYEAR
.
          MOVE      ZERO,EXIT
          GOTO      ACTA9999
.
ACTA9000  MOVE      ONE,EXIT
.
ACTA9999  RETURN
.*************************************************************************
.* SELA - Select field to change Post or Cancel                          *
.*************************************************************************
SELA0000  CALL      MAINQST                * select item post cancel?
          COMPARE   "0",CCITEM             * post ?
          GOTO      SELA9500 IF EQUAL 
          COMPARE   "-1",CCITEM            * cancel ?
          GOTO      SELA9000 IF EQUAL 
.
          BRANCH    CCITEM,SELA1000
          BEEP
          GOTO      SELA0000
.
SELA1000  MOVE      "28",CCOL
          MOVE      "4",CVERT
          CALL      KLMCFM00           * keyin budget type code
          BRANCH    EXIT,SELA1000,SELA9000
          DISPLAY   *P28:4,*V2LON,FMLCYEAR
          MOVE      FMLCYEAR,FINYEAR
          GOTO      SELA0000
.
SELA9000  MOVE      ONE,EXIT
          GOTO      SELA9999
.
SELA9500  MOVE      ZERO,EXIT
SELA9999  RETURN
.
.******************************************************************************
.* INCLUDES                                                                   *
.******************************************************************************
.
          INC       PRCFMSTU           * Budget Total Account Update Procedure
          INC       FMSSTDCD           * FMS Acc. standard routines
          INC       FMSLMCKY
          INC       FMSCONIO/INC
          INC       FMSLMCIO/INC
          INC       FMSLMAIO/INC
          INC       FMSAMAIO/INC
          INC       FMSCSAIO/INC
.
