. **********************************************************************
. * Program   :   IBAFMS62                                             *
. * Function  :   Audit File Reporting                                 *
. **********************************************************************
. * Date      :   04/07/1990                                           *
. * Author    :   Neeriem Dye                                          *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
. * VF.00.04  11.01.2000 Charles Handaya                               *
. *           Recompiled for Audit File date                           *
. * VF.00.03  29.12.1999 Charles Handaya                               *
. *           Fix bugs found while testing Print Audit Files           *
. * VF.00.02  09.11.1999 Charles Handaya                               *
. *           Fix Y2K to print Audit Files Date out                    *
. * VF.00.01  20.10.1999 Charles Handaya                               *
. *           Added a new Status field for Cost Center file (FMSCCAFD) *
. **********************************************************************
.$$$$$$
.        PROGRAM IBAFMS62
.             open files and initialise variables
.             WHILE user wants to print reports DO
.                  get file to print - one of :
.                          Account Master File  (fmsamaaf)
.                          Subjective File      (fmssbaaf)
.                          Cost Centre File     (fmsccaaf)
.                          Ledger Master File   (fmslmaaf)
.                          Creditor Master File (fmslmaaf)
.                          Budgets - Normal     (fmsbudaf)
.                          Budgets - Profiles   (fmsbpfaf)
.                          Budgets - Statistical(fmssbuaf)
.                  print report heading
.                  FOR each audit record DO
.                       get data
.                       IF at end of page
.                           THEN start new page and print heading
.                       END IF
.                       print record
.                       mark record as "printed"
.                  END FOR
.                  IF user wants to purge printed data
.                       THEN purge data
.                  END IF
.             END WHILE
.        END
.$$$$$$
. Standard Accounting Include
.---------------------------
          INC       FMSSTDDF
          INC       FMSUPDDF
          INC       FMSUDADF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
.
          INC       APSMASFD/INC
          INC       FMSSBAFD/INC
          INC       FMSBPFFD/INC
          INC       FMSCCAFD/INC
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
.
BLAB      INIT      "                               " * labels for screen B
BLAB1     INIT      "Account Master File  (fmsamaaf)"
BLAB2     INIT      "Subjective File      (fmssbaaf)"
BLAB3     INIT      "Cost Centre File     (fmsccaaf)"
BLAB4     INIT      "Ledger Master File   (fmslmaaf)"
BLAB5     INIT      "Creditor Master File (apsmasaf)"
BLAB6     INIT      "Budgets - Normal     (fmsbudaf)"
BLAB7     INIT      "Budgets - Profiles   (fmsbpfaf)"
BLAB8     INIT      "Budgets - Statistical(fmssbuaf)"
BLAB9     INIT      " "
BLAB10    INIT      " "
BLAB11    INIT      " "
BLAB12    INIT      " "
.
ADDED     FORM      3        * no. of added records
CHANGED   FORM      3        * no. of changed records
DELETED   FORM      3        * no. of deleted records
TEMPNUM   FORM      3        * temporary form 3 variable
OPTIONB   FORM      2        * option on screen B
.
PRGID     INIT      "IBAFMS62"
PRGNAM    INIT      "Audit File Reporting"
VERSION   INIT      "V12.00.00"
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      SCRB0000      * display screen B and get option
          BRANCH    EXIT,ML9999   * user wants to exit screen
          CALL      PRNT0000      * print audits
.
ML2000    CALL      SCRC0000      * delete audits ?
          GOTO      ML1000        * get another option
.
ML9999    CHAIN     PGM
.******************************************************************************
.* INIT - Open Files                                                          *
.******************************************************************************
INIT0000  CALL      DISPHEAD
          CALL      OUDA0000                 * OPen Budget Audit Files
.
          DISPLAY   *P56:24,"Opening apsaudma";
          OPEN      APSAUDMA,"apsaudma"
.
          DISPLAY   *P64:24,"fmsbpfaf";
          OPEN      FMSBPFA1,"fmsbpfaf"
.
          DISPLAY   *P64:24,"fmsaudam";
          OPEN      FMSAUDAM,"fmsaudam"
.
          DISPLAY   *P64:24,"fmsaudsa";
          OPEN      FMSAUDSA,"fmsaudsa"
.
          DISPLAY   *P64:24,"fmsaudcc";
          OPEN      FMSAUDCC,"fmsaudcc"
.
          DISPLAY   *P64:24,"fmsaudla";
          OPEN      FMSAUDLA,"fmsaudla"
.
          DISPLAY   *P1:24,*EL;
.
INIT9999  RETURN
.**********************************************************************
. SCRB -  display screen B and get print option      Called By   ML0000
.         Returns : EXIT       (0=continue, 1=quit)
.**********************************************************************
SCRB0000  DISPLAY  *P1:03,*EF,*+:
                   *P1:04,*V2LON," 0",*HOFF," = Exit":
                   *P1:05,*V2LON," 1",*HOFF," = ",BLAB1:
                   *P1:06,*V2LON," 2",*HOFF," = ",BLAB2:
                   *P1:07,*V2LON," 3",*HOFF," = ",BLAB3:
                   *P1:08,*V2LON," 4",*HOFF," = ",BLAB4:
                   *P1:09,*V2LON," 5",*HOFF," = ",BLAB5:
                   *P1:10,*V2LON," 6",*HOFF," = ",BLAB6:
                   *P1:11,*V2LON," 7",*HOFF," = ",BLAB7:
                   *P1:12,*V2LON," 8",*HOFF," = ",BLAB8:
....               *P1:13,*V2LON," 9",*HOFF," = (Not Used)":
....               *P1:14,*V2LON,"10",*HOFF," = (Not Used)":
....               *P1:15,*V2LON,"11",*HOFF," = (Not Used)":
....               *P1:16,*V2LON,"12",*HOFF," = (Not Used)":
                   *P6:14,"Select Option :";
.
SCRB1000  KEYIN     *P22:14,"__",*P22:14,*V2LON,OPTIONB,*EL; * read option
          COMPARE   OPTIONB,ZERO
          GOTO      SCRB9500 IF EQUAL            * user wants to exit ?
.
          PACK      BLAB,SP70                    * clear screen label
          LOAD      BLAB,OPTIONB,BLAB1,BLAB2,BLAB3: * get screen label
                                 BLAB4,BLAB5,BLAB6,BLAB7,BLAB8:
                                 BLAB9,BLAB10,BLAB11,BLAB12
.
          IF        OPTIONB>=6 & OPTIONB<=8 & FMSAUDG<>1
            DISPLAY   *P1:24,*EL,*B,"Budget Audit Flag is not turned on.  ";
            CALL      HITENTER
            GOTO      SCRB1000
          ENDIF
.
          MATCH     SP70,BLAB
          GOTO      SCRB9000 IF NOT EQUAL        * legal option ?
.
          BEEP                                   * illegal option
          GOTO      SCRB1000                     * get another option
.
SCRB9000  MOVE      ZERO,EXIT                    * continue
          GOTO      SCRB9999
.
SCRB9500  MOVE      ONE,EXIT                     * quit
.
SCRB9999  RETURN
.**********************************************************************
. SCRB -  delete audits if required                  Called By   ML0000
.**********************************************************************
SCRC0000  DISPLAY   *P1:24,"Delete Audit File (Y/N) : ";
.
SCRC1000  KEYIN     *P27:24,"_",*EL,*V2LON,*P27:24,ANS;    * get answer
          PACK      ANS,ANS,SP1
          REP       "1x2xY1y1N2n2",ANS          * convert answer
          MOVE      "9",FORM1
          MOVE      ANS,FORM1
          BRANCH    FORM1,SCRC2000,SCRC9999     * "yes" or "no" entered ?
          BEEP                                  * illegal option
          GOTO      SCRC1000                    * get another option
.
.---- delete audit file ----
.
SCRC2000  CALL      FILE0000           * go to start of file
.
SCRC3000  CALL      NREC0000           * get next record
          BRANCH    OVRCD,SCRC9999     * no more records ?
          CALL      DREC0000           * delete record
          GOTO      SCRC3000           * get next record
.
SCRC9999  RETURN
.********************************************************************
. PRNT - print Report                               Called by ML0000 
.         Requires : OPTIONB (option to print)
.********************************************************************
PRNT0000  DISPLAY   *P50:24,"Printing :   ";
          MOVE      ZERO,CPAGENO                 * reset page no.
          CLOCK     TIME,CTIMEIS                 * get current time
          MOVE      ZERO,ADDED     * no. of added records
          MOVE      ZERO,CHANGED   * no. of changed records
          MOVE      ZERO,DELETED   * no. of deleted records
          CALL      HEAD0000                     * print header
.
          CALL      FILE0000                    * go to correct Position in file
.
PRNT1000  CALL      NREC0000                     * get next code record
          BRANCH    OVRCD,PRNT9000               * end of file ?
.
          COMPARE   "50",CLNO                    * end of page ?
          CALL      HEAD0000 IF NOT LESS         * start new page
.
          CALL      WRIT0000                     * print details
          GOTO      PRNT1000                     * get next record
.
PRNT9000  CALL      LINE0000                     * print line
          PRINT     *N,"Total Additions Printed : ",ADDED,*N:
                    "Total Changes Printed   : ",CHANGED,*N:
                    "Total Deletions Printed : ",DELETED,*N:
                    "****   End of Report   ****"
.
PRNT9999  RETURN
.********************************************************************
. FILE - got to start of desired file               Called by PRNT,SCRC 
.********************************************************************
FILE0000  MOVE      SP20,KEY19
          BRANCH    OPTIONB,FILE0100,FILE0200,FILE0300,FILE0400:
                            FILE0500,FILE0600,FILE0700,FILE0800:
                            FILE9999,FILE9999,FILE9999,FILE9999
.
.---- audit file 1 ----
.
FILE0100  CALL      ASFMAM         * go to correct Position in file
          GOTO      FILE9999
.
.---- audit file 2 ----
.
FILE0200  CALL      ASFMSA         * go to correct Position in file
          GOTO      FILE9999
.
.---- audit file 3 ----
.
FILE0300  CALL      ASFMCC         * go to correct Position in file
          GOTO      FILE9999
.
.---- audit file 4 ----
.
FILE0400  CALL      ASFMLA         * go to correct Position in file
          GOTO      FILE9999
.
.---- audit file 5 ----
.
FILE0500  CALL      ASAPMA         * go to correct Position in file
          GOTO      FILE9999
.
.---- audit file 6 ----
.
FILE0600  CALL      ASFMBU         * go to correct Position in file
          GOTO      FILE9999
.
.---- audit file 7 ----
.
FILE0700  CALL      ASFMBP         * go to correct Position in file
          GOTO      FILE9999
.
.---- audit file 8 ----
.
FILE0800  CALL      ASFMST         * go to correct Position in file
          GOTO      FILE9999
.
FILE9999  RETURN
.********************************************************************
. NREC - get next record of current file            Called by PRNT,SCRC 
.********************************************************************
NREC0000  BRANCH    OPTIONB,NREC0100,NREC0200,NREC0300,NREC0400:
                            NREC0500,NREC0600,NREC0700,NREC0800:
                            NREC9999,NREC9999,NREC9999,NREC9999
.
.---- audit file 1 ----
.
NREC0100  CALL      AKFMAM             * get next record
          GOTO      NREC9999
.
.---- audit file 2 ----
.
NREC0200  CALL      AKFMSA             * get next record
          GOTO      NREC9999
.
.---- audit file 3 ----
.
NREC0300  CALL      AKFMCC             * get next record
          GOTO      NREC9999
.
.---- audit file 4 ----
.
NREC0400  CALL      AKFMLA             * get next record
          GOTO      NREC9999
.
.---- audit file 5 ----
.
NREC0500  CALL      AKAPMA             * get next record
          GOTO      NREC9999
.
.---- audit file 6 ----
.
NREC0600  CALL      AKFMBU         * go to correct Position in file
          GOTO      NREC9999
.
.---- audit file 7 ----
.
NREC0700  CALL      AKFMBP         * go to correct Position in file
          GOTO      NREC9999
.
.---- audit file 8 ----
.
NREC0800  CALL      AKFMST         * go to correct Position in file
          GOTO      NREC9999
.
NREC9999  RETURN
.********************************************************************
. DREC - delete current record of desired file     Called by SCRC0000 
.********************************************************************
DREC0000  BRANCH    OPTIONB,DREC0100,DREC0200,DREC0300,DREC0400:
                            DREC0500,DREC0600,DREC0700,DREC0800:
                            DREC9999,DREC9999,DREC9999,DREC9999
.
.---- audit file 1 ----
.
DREC0100  BRANCH    FMAMAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,FMAMAUDD,FMAMAUDT,FMAMAUDP,FMAMAUDR,FMAMAUDS,FMAMAUDO
          CALL      ADFMAM            * delete record
          CALL      ASFMAM            * reposition file pointer
          GOTO      DREC9999
.
.---- audit file 2 ----
.
DREC0200  BRANCH    FMSAAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,FMSAAUDD,FMSAAUDT,FMSAAUDP,FMSAAUDR,FMSAAUDS,FMSAAUDO
          CALL      ADFMSA            * delete record
          CALL      ASFMSA            * reposition file pointer
          GOTO      DREC9999
.
.---- audit file 3 ----
.
DREC0300  BRANCH    FMCCAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,FMCCAUDD,FMCCAUDT,FMCCAUDP,FMCCAUDR,FMCCAUDS,FMCCAUDO
          CALL      ADFMCC            * delete record
          CALL      ASFMCC            * reposition file pointer
          GOTO      DREC9999
.
.---- audit file 4 ----
.
DREC0400  BRANCH    FMLAAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,FMLAAUDD,FMLAAUDT,FMLAAUDP,FMLAAUDR,FMLAAUDS,FMLAAUDO
          CALL      ADFMLA            * delete record
          CALL      ASFMLA            * reposition file pointer
          GOTO      DREC9999
.
.---- audit file 5 ----
.
DREC0500  BRANCH    APMAAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,APMAAUDD,APMAAUDT,APMAAUDP,APMAAUDR,APMAAUDS,APMAAUDO
          CALL      ADAPMA            * delete record
          CALL      ASAPMA            * reposition file pointer
          GOTO      DREC9999
.
.---- audit file 6 ----
.
DREC0600  BRANCH    FMBUAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,FMBUAUDD,FMBUAUDT,FMBUAUDP,FMBUAUDR,FMBUAUDS,FMBUAUDO
          CALL      ADFMBU            * delete record
          CALL      ASFMBU            * reposition file pointer
          GOTO      DREC9999
.
.---- audit file 7 ----
.
DREC0700  BRANCH    FMBPAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,FMBPAUDD,FMBPAUDT,FMBPAUDP,FMBPAUDR,FMBPAUDS,FMBPAUDO
          CALL      ADFMBP            * delete record
          CALL      ASFMBP            * reposition file pointer
          GOTO      DREC9999
.
.---- audit file 8 ----
.
DREC0800  BRANCH    FMSTAUDS,DREC9999 * has record been printed ?
          PACK      KEY19,FMSTAUDD,FMSTAUDT,FMSTAUDP,FMSTAUDR,FMSTAUDS,FMSTAUDO
          CALL      ADFMST            * delete record
          CALL      ASFMST            * reposition file pointer
          GOTO      DREC9999
.
DREC9999  RETURN
.********************************************************************
. HEAD - print report                             Called by PRNT0000 
.********************************************************************
HEAD0000  CALL      HEAD132                      * print heading
          MOVE      BLAB1,BLAB                   * set up default file type
          LOAD      BLAB,OPTIONB,BLAB1,BLAB2,BLAB3: * get type
                    BLAB4,BLAB5,BLAB6,BLAB7,BLAB8,BLAB9,BLAB10,BLAB11,BLAB12
.
          PRINT     "Audit Report For ",BLAB     * print description
          ADD       ONE,CLNO
          CALL      LINE0000                     * print line
          PRINT     "|   Date   |   Time   |Port|Type| User |":
                    " Audit Data",*132,"|"
          ADD       ONE,CLNO
          CALL      LINE0000                     * print line
.                                                * re-read current record
HEAD9999  RETURN
.********************************************************************
. LINE - print line                               Called by PRNT0000 
.********************************************************************
LINE0000  PRINT     "*-------------------------------------------":
                    "--------------------------------------------":
                    "-------------------------------------------*"
          ADD       ONE,CLNO           * add one to line number
.
LINE9999  RETURN
.********************************************************************
. WRIT - print data line                          Called by PRNT0000 
.********************************************************************
WRIT0000  
.
.----- total additions, changes, and deletions ----
.
          MOVE      SP1,DIM1
          LOAD      DIM1,OPTIONB,FMAMAUDR,FMSAAUDR,FMCCAUDR,FMLAAUDR,APMAAUDR:
                                 FMBUAUDR,FMBPAUDR,FMSTAUDR
          REP       "A1B2D3",DIM1                          * convert type 
          MOVE      ZERO,FORM1
          MOVE      DIM1,FORM1
.
          LOAD      TEMPNUM,FORM1,ADDED,CHANGED,DELETED    * get type
          ADD       ONE,TEMPNUM                            * increment type
          STORE     TEMPNUM,FORM1,ADDED,CHANGED,DELETED    * store type
.
.---- print data ----
.
          BRANCH    OPTIONB,WRIT0100,WRIT0200,WRIT0300,WRIT0400:
                            WRIT0500,WRIT0600,WRIT0700,WRIT0800:
                            WRIT9999,WRIT9999,WRIT9999,WRIT9999
.
.---- print data for file 1 ----
.
WRIT0100  UNPACK    FMAMAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",FMAMAUDT," | ",FMAMAUDP," |  ":
                    FMAMAUDR," | ",FMAMAUDO," | ":
                    FMAMLEDG,"~",FMAMACCT,"~",FMAMDESC,"~":
                    FMAMSEC,"~",FMAMTYPE,"~",FMAMREF1:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMAMREF2,"~",FMAMREF3,"~",FMAMREF4,"~",FMAMDAT1:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMAMDAT2,"~",FMAMDAT3,"~",FMAMDAT4,"~",FMAMCOD1,"~":
                    FMAMCOD2,"~",FMAMCOD3,"~",FMAMCOD4,"~",FMAMAMT1,"~":
                    FMAMAMT2:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMAMAMT3,"~",FMAMAMT4,"~",FMAMSTAT,"~",FMAMCTRL,"~":
                    FMAMASST,"~",FMAMPPT,"~",FMAMONC,"~",FMAMONCC,"~":
                    FMAMCHEQ,"~",FMAMOBAL:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMAMBDAT,"~",FMAMDPLA,"~":
                    FMAMUNIT,"~",FMAMAYTD,"~",FMAMMULT,"~",FMAMDIV,"~":
                    FMAMCLEV,"~",FMAMDPRO,"~":
                    FMAMSPAR,"~",FMAMLOCK:
                    *132,"|"
          ADD       "5",CLNO                     * add one to line number
          DISPLAY   *P61:24,*V2LON,FMAMAUDT,*EL; * display data on screen
.
          MOVE      "2",FMAMAUDS
          CALL      AUFMAM                       * set status flag to "printed"
          GOTO      WRIT9999
.
.---- print data for file 2 ----
.
WRIT0200  UNPACK    FMSAAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",FMSAAUDT," | ",FMSAAUDP," |  ":
                    FMSAAUDR," | ",FMSAAUDO," | ":
                    FMSASUBJ,"~",FMSADESC,"~",FMSACGRP:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMSAADES,"~",FMSAPPOS,"~",FMSATYPE,"~",FMSASTYP,"~":
                    FMSADTYP,"~",FMSASEC,"~",FMSAREF1:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMSAREF2,"~",FMSAREF3,"~",FMSAREF4,"~",FMSADAT1:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMSADAT2,"~",FMSADAT3,"~",FMSADAT4,"~",FMSACOD1,"~":
                    FMSACOD2,"~",FMSACOD3,"~",FMSACOD4,"~",FMSAAMT1,"~":
                    FMSAAMT2:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMSAAMT3,"~",FMSAAMT4,"~",FMSASTAT,"~":
                    FMSACTRL,"~",FMSAASST,"~":
                    FMSAPPT,"~",FMSAONC,"~",FMSAONCC:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMSACHEQ,"~",FMSAADDT,"~",FMSADPLA,"~",FMSAUNIT,"~":
                    FMSAAYTD,"~",FMSAMULT,"~",FMSADIV,"~",FMSABLN:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMSADLEV,"~",FMSASPAR,"~",FMSALOCK:
                    *132,"|"
.
          ADD       "7",CLNO                     * add one to line number
          DISPLAY   *P61:24,*V2LON,FMSAAUDT,*EL; * display data on screen
.
          MOVE      "2",FMSAAUDS
          CALL      AUFMSA                       * set status flag to "printed"
          GOTO      WRIT9999
.
.---- print data for file 3 ----
.
WRIT0300  UNPACK    FMCCAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",FMCCAUDT," | ",FMCCAUDP," |  ":
                    FMCCAUDR," | ",FMCCAUDO," | ":
                    FMCCLEDG,"~",FMCCCOCE,"~",FMCCDESC,"~",FMCCADES:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMCCPPOS,"~",FMCCSECC,"~",FMCCTELE,"~":
                    FMCCCONT,"~",FMCCIDAY,"~",FMCCSTAT,"~",FMCCSPAR:
                    *132,"|"
          ADD       "2",CLNO                     * add one to line number
          DISPLAY   *P61:24,*V2LON,FMCCAUDT,*EL; * display data on screen
.
          MOVE      "2",FMCCAUDS
          CALL      AUFMCC                       * set status flag to "printed"
          GOTO      WRIT9999
.
.---- print data for file 4 ----
.
WRIT0400  UNPACK    FMLAAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",FMLAAUDT:
                    " | ",FMLAAUDP," |  ",FMLAAUDR," | ",FMLAAUDO," | ":
                    FMLALEDG,"~",FMLADESC,"~",FMLACYRR,"~",FMLAPERS,"~":
                    FMLABNK,"~",FMLACRD:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMLADIS,"~",FMLADEB,"~",FMLAPAY,"~",FMLAIFT,"~":
                    FMLAAGST,"~",FMLAPER,"~",FMLAPTY,"~",FMLAABP,"~":
                    FMLABNK2,"~",FMLAPAYG,"~":
                    FMLASPAR:
                    *132,"|"
.
          ADD       "2",CLNO                     * add 2 to line number
          DISPLAY   *P61:24,*V2LON,FMLAAUDT,*EL; * display data on screen
.
          MOVE      "2",FMLAAUDS
          CALL      AUFMLA                       * set status flag to "printed"
          GOTO      WRIT9999
.
.---- print data for file 5 ----
.
WRIT0500  UNPACK    APMAAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",APMAAUDT:
                    " | ",APMAAUDP," |  ",APMAAUDR," | ",APMAAUDO," | ":
                    APMACRD,"~",APMATYP,"~",APMASKY,"~",APMACN1,"~",APMACN2:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    APMACA1,"~",APMACA2,"~",APMACA3,"~",APMACPC:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    APMACTEL,"~",APMACFAX,"~",APMACMOD,"~",APMACEDI:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    APMACON,"~",APMASN1:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    APMASN2,"~",APMASA1,"~",APMASA2:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    APMASA3,"~",APMASPC,"~",APMASTEL,"~",APMASFAX,"~",APMASMOD:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    APMASEDI,"~",APMASCON,"~",APMABSB,"~",APMABAC:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    APMAPNM,"~",APMAPPT,"~",APMAPOT,"~",APMAHOR,"~":
                    APMAHIN,"~",APMAHCR,"~",APMAHPY,"~",APMAPMT,"~":
                    APMAGMT,"~",APMAPPD,"~",APMADTM:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    APMAUR1,"~",APMAUR2,"~",APMAUD1,"~",APMAUD2,"~":
                    APMAUC1,"~",APMAUC2:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    APMAGREG,"~",APMAUY2,"~",APMADIS,"~",APMAABN:
                    *132,"|"
.
          ADD       "10",CLNO                    * add 10 to line number
          DISPLAY   *P61:24,*V2LON,APMAAUDT,*EL; * display data on screen
.
          MOVE      "2",APMAAUDS
          CALL      AUAPMA                       * set status flag to "printed"
          GOTO      WRIT9999
.
.---- print data for file 6 ----
.
WRIT0600  UNPACK    FMBUAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",FMBUAUDT:
                    " | ",FMBUAUDP," |  ",FMBUAUDR," | ",FMBUAUDO," | ":
                    FMBUCULD,"~",FMBUCUAC,"~",FMBUCUPR,"~",FMBUCA01,"~":
                    FMBUCA02,"~",FMBUCA03,"~",FMBUCA04,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMBUCA05,"~",FMBUCA06,"~",FMBUCA07,"~",FMBUCA08,"~":
                    FMBUCA09:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMBUCA10,"~",FMBUCA11,"~",FMBUCA12,"~",FMBUCA13,"~":
                    FMBUCB01,"~",FMBUCB02:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMBUCB03,"~",FMBUCB04,"~",FMBUCB05,"~",FMBUCB06,"~":
                    FMBUCB07,"~",FMBUCB08,"~",FMBUCB09:
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMBUCB10,"~",FMBUCB11,"~",FMBUCB12,"~",FMBUCB13,"~":
                    FMBUCC01,"~",FMBUCC02,"~",FMBUCC03,"~",FMBUCC04,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMBUCC05,"~",FMBUCC06,"~",FMBUCC07,"~",FMBUCC08,"~":
                    FMBUCC09,"~",FMBUCC10,"~",FMBUCC11,"~",FMBUCC12,"~":
                    FMBUCC13,"~":
                    *132,"|"
.
          ADD       "6",CLNO                     * add 10 to line number
          DISPLAY   *P61:24,*V2LON,FMBUAUDT,*EL; * display data on screen
.
          MOVE      "2",FMBUAUDS
          CALL      AUFMBU                       * set status flag to "printed"
          GOTO      WRIT9999
.
.---- print data for file 7 ----
.
WRIT0700  UNPACK    FMBPAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",FMBPAUDT:
                    " | ",FMBPAUDP," |  ",FMBPAUDR," | ",FMBPAUDO," | ":
                    FMBPLEDG,"~",FMBPYEAR,"~",FMBPCODE,"~",FMBPDESC,"~":
                    FMBPPC1,"~",FMBPPC2,"~",FMBPPC3,"~",FMBPPC4,"~",FMBPPC5:
                    "~",FMBPPC6,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMBPPC7,"~",FMBPPC8,"~",FMBPPC9,"~",FMBPPC10,"~":
                    FMBPPC11,"~",FMBPPC12,"~",FMBPPC13,"~":
                    *132,"|"
.
          ADD       "2",CLNO                     * add 10 to line number
          DISPLAY   *P61:24,*V2LON,FMBPAUDT,*EL; * display data on screen
.
          MOVE      "2",FMBPAUDS
          CALL      AUFMBP                       * set status flag to "printed"
          GOTO      WRIT9999
.
.---- print data for file 8 ----
.
WRIT0800  UNPACK    FMSTAUDD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "|",CPCDATE,"| ",FMSTAUDT:
                    " | ",FMSTAUDP," |  ",FMSTAUDR," | ",FMSTAUDO," | ":
                    FMSTCULD,"~",FMSTCUAC,"~",FMSTCUPR,"~",FMSTCA01,"~":
                    FMSTCA02,"~",FMSTCA03,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMSTCA04,"~",FMSTCA05,"~",FMSTCA06,"~",FMSTCA07,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMSTCA08,"~",FMSTCA09,"~",FMSTCA10,"~",FMSTCA11,"~":
                    *132,"|",*N,"|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|":
                    FMSTCA12,"~",FMSTCA13,"~":
                    *132,"|"
.
          ADD       "4",CLNO                     * add 10 to line number
          DISPLAY   *P61:24,*V2LON,FMSTAUDT,*EL; * display data on screen
.
          MOVE      "2",FMSTAUDS
          CALL      AUFMST                       * set status flag to "printed"
          GOTO      WRIT9999
.
WRIT9999  PRINT     "|",*12,"|",*23,"|",*28,"|",*33,"|",*40,"|",*132,"|"
          ADD       ONE,CLNO                     * add one to line number
          RETURN
.******************************************************************************
.* INCLUDES FOR I/O'S                                                         *
.******************************************************************************
.
.--------------------------------
. Standard Accounting Code include
.--------------------------------
          INC       FMSSTDCD
          INC       FMSUPDCD
          INC       FMSUDACD
.
          INC       APSMASIO/INC
          INC       FMSSBAIO/INC
          INC       FMSBPFIO/INC
          INC       FMSCCAIO/INC
