.
. V9.10.01  05.09.2007 Sandra Barcham  177105
.           Added option to fix apsord for P/O
. V9.04.01  08.04.2005 Peter Vela CAR 59696
.           Changed commodity group from DIM 5 to DIM 6
. V9.02.00  13.05.2002  Glenn Saunders 
.           Export to new release i.e. v9.02 from vf.09
. VF.09.B01 02.11.2000 Charles Handaya
.           Modification for sincchaf and FIXUSAGE changes
. VF.00.01  05.01.2000 Charles Handaya
.           Recompiled for APSMASIO change
.           21.12.1999 Charles Handaya
.           Recompiled for Fixing Audit Date (CCYYMMDD)
. VF.00.00  06.11.1998 Sandra Barcham
.           Year 2000 compliance
. V6.00.23  27.08.1998 Sandra Barcham
.           Clear SIPCEDD if required       
. V6.00.22  03.03.1998 B.G.Ackland
.           Conversion to numberic catalogs
. V6.00.21  08.08.1997 Sandra Barcham 
.           Add update of period in sinitmaf
.           srf 621232
. V6.00.20  02.04.1997 B.G.Ackland
.           New Keyword Catalog Search & Update
. V6.00.19  18.02.1997 Sandra Barcham
.           Stop I * U happening
. V6.00.18  16.12.1996 B.G.Ackland
.           New Fix Imprest Seq Option for SAB
. V6.00.17  29.11.1996 Sandra Barcham
.           Remove sincieaf record for non-stock catalogs
. V6.00.16  13.11.1996 Sandra Barcham
.           Write spaces in warehouse of sinitmaf for non-stock catalogs
. V6.00.15  27.10.1995 B.G.Ackland
.           Fix Counter on SOO Recalc
. V6.00.14  16.06.1995 Robert Sumsion
.           Added Complete all Not Outstanding purchase orders option
. V6.00.13  20.04.1995 Robert Sumsion
.           Modified to recalc SOO Recalc        
. V6.00.12  23.12.1994 Robert Sumsion
.           Modified to recalc SOO Recalc        
. V6.00.11  24.03.1994 Neeriem Dye
.           Modified usage update to include non - stock items
. V6.00.10  23.12.1993 Robert Sumsion  
.           Added option 14 - remove spaces in date variables on
.           in date variables on sinperaf file
.           srf 127059
. V6.00.08  21.07.1993 Neeriem Dye
.           Fixed gl interface to use HACCDATA
. V6.00.07  02.07.1993 Neeriem Dye
.           Added create apscioaf option
.
          INC      STDGENDF
.
.  Usage File Update
.  -----------------
          INC      FMSCONFD/INC
          INC      APSCIOFD/INC
          INC      APSORDFD/INC
          INC      SINWARFD/INC
          INC      SINLTTFD/INC
          INC      FMSAMAFD/INC  
          INC      APSMASFD/INC  
          INC      APSSUBFD/INC   
          INC      SINCCAFD/INC 
          INC      SINCCBFD/INC 
          INC      SINCCEFD/INC
          INC      SINCCFFD/INC   
          INC      SINCCHFD/INC  
          INC      SINCGAFD/INC 
          INC      SINCGBFD/INC   
          INC      SINCIAFD/INC  
          INC      SINCIBFD/INC  
          INC      SINCICFD/INC 
          INC      SINCIDFD/INC  
          INC      SINCIEFD/INC 
          INC      SINCIFFD/INC 
          INC      SINCIGFD/INC  
          INC      SINCIHFD/INC
          INC      SINCONFD/INC
          INC      SINFACFD/INC   
          INC      SINIMPFD/INC   
          INC      SINITMFD/INC   
          INC      SINKCTFD/INC   
          INC      SINPERFD/INC  
          INC      SINPOAFD/INC 
          INC      SINPOCFD/INC 
          INC      SINPOFFD/INC 
          INC      SINREAFD/INC   
          INC      SINREBFD/INC  
          INC      SINSUAFD/INC 
          INC      SINSUBFD/INC
          INC      SINTRAFD/INC
          INC      SINTRBFD/INC
.
SAVKEY67  DIM      67
NUMCAT7   FORM     7
LASTDATE  DIM      8
FRCOMM    DIM      5
TOCOMM    DIM      5
FROMDT    DIM      6
TODT      DIM      6
NULL      EQU      000
COSTCENT  DIM      5
F12P2     FORM     12.2
OPTTYP    FORM     1
FDATE     DIM      8
TDATE     DIM      8
SKEY10    DIM      10
SKEY28    DIM      28
COUNTER   FORM     10
UPDATED   FORM     10
COMPLETE  FORM     1
RECORDS   FORM     10
RESTCNT   FORM     10
PURCH     DIM       7
.
Z2        INIT     "ZZ"
.
PRGID     INIT     "FIXSIN"
PRGNAM    INIT     "Various Fix Functions for Supply and Inventory"
VERSION   INIT      "V12.00.00"
.
          CALL     DISPHEAD
          OPEN     CONTROLF,"controlf"
          READ      CONTROLF,TWENTY3;*95,HACCNT,*120,HBACK,*124,HORDDES:
                                    *143,HDEFWH,*248,HAVGCOST
          READ      CONTROLF,TWENTY4;*135,HACCDATA
.
MAIN1000  CALL     MENU0000          * Display Options
          BRANCH   EXIT,MAIN9999     * Exit Program 
.  
          CALL     OPTN0000          * Perform Options 
          GOTO     MAIN1000          * Restart Program 
.
MAIN9999  CHAIN    PGM
.------------------------------------------------------------
. Display Menu and Enter Option
.------------------------------------------------------------
MENU0000  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON," 0. ",*HOFF,"Exit":
                    *P1:5,*V2LON," 1. ",*HOFF,"Re-Calc Req. and ":
                                              "Receival Summary":
                    *P1:6,*V2LON," 2. ",*HOFF,"Re-Calc Purchasing Summary":
                    *P1:7,*V2LON," 3. ",*HOFF,"Not Available":
                    *P1:8,*V2LON," 4. ",*HOFF,"Re-Calc Stock on Order":
                    *P1:9,*V2LON," 5. ",*HOFF,"Clear All Stocktake Indicators":
                   *P1:10,*V2LON," 6. ",*HOFF,"General Fix":
                   *P1:11,*V2LON," 7. ",*HOFF,"HRH Cost Centre Fix":
                   *P1:12,*V2LON," 8. ",*HOFF,"Clear SINREBAF Batch Transfer":
                   *P1:13,*V2LON," 9. ",*HOFF,"Del. Inv. Catalogs in imprest":
                   *P1:14,*V2LON,"10. ",*HOFF,"Update Alias File from Master":
                   *P1:15,*V2LON,"11. ",*HOFF,"Reset oustanding order items":
                   *P1:16,*V2LON,"12. ",*HOFF,"Modify SIIESID":
                   *P1:17,*V2LON,"13. ",*HOFF,"Create APSCIOAF":
                   *P1:18,*V2LON,"14. ",*HOFF,"Fix Spaces in Period File Dates":
                   *P1:19,*V2LON,"15. ",*HOFF,"Complete Orders not Outstanding":
                   *P1:20,*V2LON,"16. ",*HOFF,"Clear Warehouse for ":
                                              "N/S in sinitmaf":
                *P1:21,*V2LON,"17. ",*HOFF,"Remove non-stock sincieaf records ":
                *P1:22,*V2LON,"18. ",*HOFF,"Zero Fill Imprest Sequence No.":
                *P40:5,*V2LON,"19. ",*HOFF,"Recalc Usage Period in sinitmaf":
                *P40:6,*V2LON,"20. ",*HOFF,"Convert Catalogs to Numerics":
                *P40:7,*V2LON,"21. ",*HOFF,"Clear SIPCEDD if required":
                *P40:8,*V2LON,"22. ",*HOFF,"Fix apsordaf for P/O No."
.
MENU1000  KEYIN     *P1:24,*EL,"Enter Option : ",*V2LON,OPTION
          IF        OPTION=0
            MOVE      ONE,EXIT
            GOTO      MENU9999
          ENDIF
          MOVE      ZERO,EXIT
          BRANCH    OPTION,MENU9999,MENU9999,MENU9999,MENU9999,MENU9999:
                           MENU9999,MENU9999,MENU9999,MENU9999,MENU9999:
                           MENU9999,MENU9999,MENU9999,MENU9999,MENU9999:
                           MENU9999,MENU9999,MENU9999,MENU9999,MENU9999:
                           MENU9999,MENU9999
          BEEP
          GOTO      MENU1000
.
MENU9999  RETURN
.------------------------------------------------------------
. Perform Option Selected
.------------------------------------------------------------
OPTN0000  BRANCH    OPTION,OPTN0100,OPTN0200,OPTN0300,OPTN0400,OPTN0500:
                           OPTN0600,OPTN0700,OPTN0800,OPTN0900,OPTN1000:
                           OPTN1100,OPTN1200,OPTN1300,OPTN1400,OPTN1500:
                           OPTN1600,OPTN1700,OPTN1800,OPTN1900,OPTN2000:
                           OPTN2100,OPTN2200
          BEEP
          GOTO      OPTN9999
.
OPTN0100  CALL      UREQ0000    * Requisition Summary
          GOTO      OPTN9999
.
OPTN0200  CALL      UORD0000    * Ordered Summary
          GOTO      OPTN9999
.
OPTN0300  
          GOTO      OPTN9999
.
OPTN0400  CALL      USOO0000    * Re-Calc Stock On Order
          GOTO      OPTN9999
.
OPTN0500  CALL      CLSTI000    * Clear all Stock Indicators
          GOTO      OPTN9999
.
OPTN0600  CALL      FIXCA000    * General Fix Function
          GOTO      OPTN9999
.
OPTN0700  CALL      HRCST000    * Convert Cost Centres for HRH
          GOTO      OPTN9999
.
OPTN0800  CALL      CSIRB000    * Clear batch data for sinrebaf
          GOTO      OPTN9999
.
OPTN0900  CALL      CLRIMP00    * Clear invalid Catalogs in Imprest File
          GOTO      OPTN9999
.
OPTN1000  CALL      UPDCIF00    * Update Alias File
          GOTO      OPTN9999
.
OPTN1100  CALL      CKORD000    * Check outstanding orders
          GOTO      OPTN9999
.
OPTN1200  CALL      MESTI000    * modify SIIESID
          GOTO      OPTN9999
.
OPTN1300  CALL      CAPCI000    * create apscioaf
          GOTO      OPTN9999
.
OPTN1400  CALL      UPPER000    * update sinperaf file
          GOTO      OPTN9999
.
OPTN1500  CALL      COMPL000    * update sinperaf file
          GOTO      OPTN9999
.
OPTN1600  CALL      SPACW000    * Clear warehouse in sinitmaf for non-stock
          GOTO      OPTN9999
.
OPTN1700  CALL      CLCIE000    * Remove sincieaf records for non-stock
          GOTO      OPTN9999
.
OPTN1800  CALL      ZFIMP000    * Zero Fill Imprest Seq No
          GOTO      OPTN9999
.
OPTN1900  CALL      CAITM000    * Recalculate usage period in sinitmaf
          GOTO      OPTN9999
.
OPTN2000  CALL      NUMCAT00    * Recalculate usage period in sinitmaf
          GOTO      OPTN9999
.
OPTN2100  CALL      CLCEDD00    * Clear Expected delivery day if required
          GOTO      OPTN9999
.
OPTN2200  CALL      FAPORD00    * Fix apsordaf for a purchase order
          GOTO      OPTN9999
.
OPTN9999  RETURN
.-----------------------------------
. Clear SIRB Batch Data
.-----------------------------------
CSIRB000  
          KEYIN    *P1:4,"Enter From Batch Number : ":
                   *EF,*V2LON,*ZF,*JR,FRCOMM
          PACK     FRCOMM,FRCOMM,SP70
          REP      " 0",FRCOMM
          MATCH    "00000",FRCOMM
          GOTO     CSIRB999 IF EQUAL
.
          KEYIN    *P1:5,"Enter To   Batch Number : ",*V2LON,*ZF,*JR,TOCOMM
          PACK     TOCOMM,TOCOMM,SP70
          REP      " 0",TOCOMM
          MATCH    "00000",TOCOMM
          GOTO     CSIRB000 IF EQUAL
.
          MATCH    FRCOMM,TOCOMM
          IF       @LESS
            DISPLAY   *P1:24,"Illegal Range - ";
            CALL      HITENTER
            GOTO      CSIRB000
          ENDIF
.
          CALL     CONTQST
          SUB      ONE,CEXIT
          BRANCH   EXIT,CSIRB000,CSIRB999
.
          OPEN     SINREBA1,"sinrebaf"
          OPEN     SINREBA3,"sinrebaf"
          OPEN     SINTRBA1,"sintrbaf"
          PACK     KEY39,FRCOMM,SP70
          MOVE     SP70,KEY5
          CALL     RSSIRB3
.
CSIRB100  CALL     RKSIRB3
          BRANCH   OVRCD,CSIRB500
          MATCH    SIRBBCH,TOCOMM
          GOTO     CSIRB500 IF LESS
.
          MATCH    SIRBBCH,KEY5
          IF       !@EQUAL
            DISPLAY  *P1:24,"Processing 1 : ",*V2LON,SIRBBCH,*EL;
            MOVE     SIRBBCH,KEY5
          ENDIF
.
          PACK     KEY20,SIRBTYP,SIRBREQ,SIRBWAR,SIRBCAT,SP70
          CALL     RDSIRB1
          BRANCH   OVRCD,CSIRB100
.
          UNPACK   SP70,SIRBBCH,SIRBLED,SIRBACC
          CALL     UPSIRB1
          GOTO     CSIRB100
.
CSIRB500  MOVE     SP70,KEY5
          MOVE     SP70,KEY11
.
CSIRB600  CALL     RSSITB1
          CALL     RKSITB1
          BRANCH   OVRCD,CSIRB999
          PACK     KEY11,SITBNUM,SITBUNI,SP70
.
          MATCH    FRCOMM,SITBBCH      * check if in range
          GOTO     CSIRB600 IF LESS
          MATCH    SITBBCH,TOCOMM
          GOTO     CSIRB600 IF LESS
.
          MATCH    SITBBCH,KEY5
          IF       !@EQUAL
            DISPLAY  *P1:24,"Processing 2 : ",*V2LON,SITBBCH,*EL;
            MOVE     SITBBCH,KEY5
          ENDIF
.
          UNPACK   SP70,SITBBCH,SITBLED,SITBACC
          CALL     UPSITB1
          GOTO     CSIRB600
.
CSIRB999  RETURN
.-----------------------------------
. Fix GL Interface
.-----------------------------------
FIXCA000  OPEN     SINCCAA1,"sinccaaf"
          MOVE     SP70,KEY5 
          CALL     RSSICA1
FIXCA100  CALL     RKSICA1
          BRANCH   OVRCD,FIXCA999
          CMATCH    NULL,SICADAD1
          IF       @EQUAL
            MOVE     SP70,SICADAD1
          ENDIF
          CMATCH    NULL,SICADAD2
          IF       @EQUAL
            MOVE     SP70,SICADAD2
          ENDIF
          CMATCH    NULL,SICADAD3
          IF       @EQUAL
            MOVE     SP70,SICADAD3
          ENDIF
          CMATCH    NULL,SICAMES
          IF       @EQUAL
            MOVE     SP70,SICAMES
          ENDIF
          CALL     UPSICA1
          GOTO     FIXCA100
FIXCA999  CLOSE    SINCCAA1
          RETURN
.-----------------------------------
. Fix GL Interface
.-----------------------------------
FIXGL000  OPEN     SINCCAA1,"sinccaaf"
          MOVE      "fmsamaaf",KEY8
          PACK      KEY80,HACCDATA,KEY8
          SQUEEZE   KEY80
          OPEN      FMSAMAA1,KEY80
.
          MOVE     SP70,KEY5 
          CALL     RSSICA1
FIXGL100  CALL     RKSICA1
          BRANCH   OVRCD,FIXGL999
          MOVE     SICAGL,KEY14
          CALL     RDFMAM1
          IF       OVRCD=1
            PRINT   "Error on Cost Centre : ",SICACODE," Code ":
                    SICAGL
          ENDIF
          GOTO     FIXGL100
FIXGL999  CLOSE    SINCCAA1
          RETURN
.-----------------------------------
. Clear Stock Take Indicator
.-----------------------------------
CLSTI000  OPEN     SINCIEA1,"sincieaf"
          MOVE     SP70,KEY12
          CALL     RSSIIE1
CLSTI100  CALL     RKSIIE1
          BRANCH   OVRCD,CLSTI999
          MOVE     ZERO,SIIESTI
          CALL     UPSIIE1
          GOTO     CLSTI100
CLSTI999  CLOSE    SINCIEA1
          RETURN
.-----------------------------------
. Fix HRH Cost Centres 1 Off
.-----------------------------------
HRCST000  OPEN     SINPOCA1,"sinpocaf"
          OPEN     SINREAA1,"sinreaaf"
          DISPLAY  *P1:24,*EL,"Fixing Cost Centres : "
          MOVE     ZERO,SECTOR
          MOVE     SP70,KEY10
          CALL     RSSIPC1
HRCST100  CALL     RKSIPC1
          BRANCH   OVRCD,HRCST190
          ADD      ONE,SECTOR
          IF       (SECTOR%100=0)
             DISPLAY   *P40:24,SECTOR
          ENDIF
          MOVE     SIPCCST,COSTCENT
          CALL     HRCON000
          BRANCH   EXIT,HRCST100
          MOVE     COSTCENT,SIPCCST
          CALL     UPSIPC1
          GOTO     HRCST100
.
HRCST190  MOVE     SP70,KEY8
          CALL     RSSIRA1
HRCST200  CALL     RKSIRA1
          BRANCH   OVRCD,HRCST999
          ADD      ONE,SECTOR
          IF       (SECTOR%100=0)
             DISPLAY   *P60:24,SECTOR
          ENDIF
          MOVE     SIRACST,COSTCENT
          CALL     HRCON000
          BRANCH   EXIT,HRCST200
          MOVE     COSTCENT,SIRACST
          CALL     UPSIRA1
          GOTO     HRCST200
.
HRCST999  CLOSE    SINPOCA1
          CLOSE    SINREAA1
          RETURN
.
HRCON000  MOVE     ONE,EXIT
          MATCH    "317  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "320  ",COSTCENT
          ENDIF
.
          MATCH    "321  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "320  ",COSTCENT
          ENDIF
.
          MATCH    "322  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "320  ",COSTCENT
          ENDIF
.
          MATCH    "339  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "320  ",COSTCENT
          ENDIF
.
          MATCH    "363  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "320  ",COSTCENT
          ENDIF
.
          MATCH    "364  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "320  ",COSTCENT
          ENDIF
.
          MATCH    "370  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "320  ",COSTCENT
          ENDIF
.
          MATCH    "374  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "320  ",COSTCENT
          ENDIF
.
          MATCH    "377  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "320  ",COSTCENT
          ENDIF
.
          MATCH    "383  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "320  ",COSTCENT
          ENDIF
.
          MATCH    "386  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "320  ",COSTCENT
          ENDIF
.
          MATCH    "410  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "320  ",COSTCENT
          ENDIF
.
          MATCH    "110  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "53   ",COSTCENT
          ENDIF
.
          MATCH    "114  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "117  ",COSTCENT
          ENDIF
.
          MATCH    "115  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "109  ",COSTCENT
          ENDIF
.
          MATCH    "118  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "122  ",COSTCENT
          ENDIF
.
          MATCH    "121  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "104  ",COSTCENT
          ENDIF
.
          MATCH    "124  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "109  ",COSTCENT
          ENDIF
.
          MATCH    "126  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "109  ",COSTCENT
          ENDIF
.
          MATCH    "171  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "108  ",COSTCENT
          ENDIF
.
          MATCH    "178  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "84   ",COSTCENT
          ENDIF
.
          MATCH    "186  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "117  ",COSTCENT
          ENDIF
.
          MATCH    "195  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "122  ",COSTCENT
          ENDIF
.
          MATCH    "234  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "233  ",COSTCENT
          ENDIF
.
          MATCH    "401  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "230  ",COSTCENT
          ENDIF
.
          MATCH    "402  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "203  ",COSTCENT
          ENDIF
.
          MATCH    "403  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "229  ",COSTCENT
          ENDIF
.
          MATCH    "405  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "205  ",COSTCENT
          ENDIF
.
          MATCH    "406  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "201  ",COSTCENT
          ENDIF
.
          MATCH    "407  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "207  ",COSTCENT
          ENDIF
.
          MATCH    "408  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "202  ",COSTCENT
          ENDIF
.
          MATCH    "409  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "212  ",COSTCENT
          ENDIF
.
          MATCH    "411  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "221  ",COSTCENT
          ENDIF
.
          MATCH    "412  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "231  ",COSTCENT
          ENDIF
.
          MATCH    "413  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "214  ",COSTCENT
          ENDIF
.
          MATCH    "414  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "211  ",COSTCENT
          ENDIF
.
          MATCH    "424  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "33   ",COSTCENT
          ENDIF
.
          MATCH    "435  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "219  ",COSTCENT
          ENDIF
.
          MATCH    "447  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "86   ",COSTCENT
          ENDIF
.
          MATCH    "449  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "20   ",COSTCENT
          ENDIF
.
          MATCH    "450  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "17   ",COSTCENT
          ENDIF
.
          MATCH    "453  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "16   ",COSTCENT
          ENDIF
.
          MATCH    "91   ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "88   ",COSTCENT
          ENDIF
.
          MATCH    "245  ",COSTCENT
          IF       @EQUAL
            MOVE     ZERO,EXIT
            MOVE     "117  ",COSTCENT
          ENDIF
.
          RETURN
.------------------------------------------------------------
. Re-Calculate Requisition Usage Analysis
.------------------------------------------------------------
UREQ0000  CALL     GETDAT00  
          BRANCH   EXIT,GETDAT99
.
          OPEN     APSMASA1,"apsmasaf"
          OPEN     APSSUBA1,"apssubaf"
          OPEN     SINCCAA1,"sinccaaf"
          OPEN     SINCCBA1,"sinccbaf"
          OPEN     SINCCEA1,"sincceaf"
          OPEN     SINCCFA1,"sinccfaf"
          OPEN     SINCCHA1,"sincchaf"
          OPEN     SINCCHA2,"sincchaf"
          OPEN     SINCGAA1,"sincgaaf"
          OPEN     SINCGBA1,"sincgbaf"
          OPEN     SINCIAA1,"sinciaaf"
          OPEN     SINCIEA1,"sincieaf"
          OPEN     SINCIHA1,"sincihaf"
          OPEN     SINREAA3,"sinreaaf"
          OPEN     SINPOAA1,"sinpoaaf"
          OPEN     SINPOCA1,"sinpocaf"
          OPEN     SINPOFA1,"sinpofaf"
          OPEN     SINREBA1,"sinrebaf"
          OPEN     SINSUAA1,"sinsuaaf"
          OPEN     SINSUBA1,"sinsubaf"
          OPEN     SINTRAA1,"sintraaf"
          OPEN     SINTRBA1,"sintrbaf"
.
          CALL     CLRUS000            * clear usage
.
          PACK     KEY6,FROMDT,SP70
          CALL     RSSIPE1
          CALL     RPSIPE1
.
UREQ1000  CALL     RKSIPE1
          BRANCH   OVRCD,UREQ5000
          PACK     KEY6,SIPEYEAR,SIPEPER,SP70
          MATCH    KEY6,TODT
          GOTO     UREQ5000 IF LESS
.
          UNPACK   SIRADAT,CCENT,CYEAR,CMON,CDAY
          CALL     PACDATE
          DISPLAY  *P1:24,"Scanning Requisitions : ",*V2LON,CPCDATE;
.
          PACK     KEY16,SIPESDT,SP70
          CALL     RSSIRA3
.
UREQ1500  CALL     RKSIRA3
          BRANCH   OVRCD,UREQ1000
          MATCH    SIRADAT,SIPEEDT
          GOTO     UREQ1000 IF LESS
.
          PACK     SICACODE,SIRACST,SP70
          PACK     KEY5,SIRACST,SP70
          CALL     RDSICA1
.
          PACK     KEY20,SIRATYP,SIRAREQ,SP70
          CALL     RSSIRB1
.
UREQ2000  CALL     RKSIRB1
          BRANCH   OVRCD,UREQ1500
          MATCH    SIRBTYP,SIRATYP
          GOTO     UREQ1500 IF NOT EQUAL
          MATCH    SIRBREQ,SIRAREQ
          GOTO     UREQ1500 IF NOT EQUAL
.
          MOVE     SIRBCAT,KEY7
          CALL     RDSIIA1
          IF       OVRCD=1
            DISPLAY    *P50:24,*EL,"Catalog Missing : ",KEY7;
            GOTO       UREQ2000
          ENDIF
          BRANCH   SIIANON,UREQ2000         * skip for non-stock
.
          MOVE     SIIASUB,KEY5
          CALL     RDSISA1
          IF       OVRCD=1
            DISPLAY    *P50:24,*EL,"Product Group Missing : ",KEY5;
            GOTO       UREQ2000
          ENDIF
.
          MOVE      SIRBWAR,SIIEWAR
          MOVE      SIRBAMT,SIUSCST
          MOVE      SIRBIQT,SIUSQTY
.
          CALL      SINUSAGE
.
          GOTO      UREQ2000
.
UREQ5000  PACK      KEY12,SP70
          CALL      RSSIPF1
.
UREQ6000  CALL      RKSIPF1
          BRANCH    OVRCD,UREQ9000
          ADD       ONE,SECTOR
          IF        (SECTOR%100=0)
             DISPLAY   *P1:24,"Processing Receivals ",SECTOR," - ",SIPFPON,*EL;
          ENDIF
.
          MATCH     FROMDT,SIPFUDT     * only process desired date range
          GOTO      UREQ6000 IF LESS
          MATCH     SIPFUDT,TODT
          GOTO      UREQ6000 IF LESS
.
          PACK      KEY7,SIPFPON,SP70
          CALL      RDSIPA1
          BRANCH    OVRCD,UREQ6000     * get header data (should be there)
.
          PACK      KEY10,SIPFPON,SIPFITM,SP70
          CALL      RDSIPC1
          BRANCH    OVRCD,UREQ6000     * get item data (should be there)
.
          MOVE      SIPCPRD,KEY5
          CALL      RDSISA1
          IF        OVRCD=1
            DISPLAY   *P50:24,*EL,"Product Group Missing : ",KEY5;
            GOTO      UREQ6000
          ENDIF
.
          MOVE      ONE,SIIANON
          PACK      KEY7,SIPCCAT,SP70
          CALL      RDSIIA1
.
          PACK      SIIACAT,SIPCCAT,SP70
          PACK      SIIASUB,SIPCPRD,SP70
          PACK      SICACODE,SIPCCST,SP70
          PACK      SIIEWAR,SIPCWAR,SP70
          ASSIGN    SIPFCST*SIPFQTY,SIUSCST
          MOVE      SIPFQTY,SIUSQTY
          UNPACK    SIPFUDT,SIPEYEAR,SIPEPER
.
          CALL      SINRECUS
          IF        SIIANON=1
            CALL      SINUSAGE
          ENDIF
          GOTO      UREQ6000
.
.
UREQ9000  CLOSE    SINCCAA1
          CLOSE    SINCCBA1
          CLOSE    SINCCEA1
          CLOSE    SINCCFA1
          CLOSE    SINCCHA1
          CLOSE    SINCCHA2
          CLOSE    SINCGAA1
          CLOSE    SINCGBA1
          CLOSE    SINCIAA1
          CLOSE    SINCIEA1
          CLOSE    SINCIHA1
          CLOSE    SINREAA3
          CLOSE    SINREBA1
          CLOSE    SINSUAA1
          CLOSE    SINSUBA1
          CLOSE    APSMASA1
          CLOSE    APSSUBA1
.
UREQ9999  DISPLAY  *P1:24,*EL,"Complete - ";
          CALL     HITENTER
          CLOSE    SINPERA1
          RETURN
.------------------------------------------------------------
. Re-Calculate Purchasing Analysis
.------------------------------------------------------------
UORD0000  OPEN     APSMASA1,"apsmasaf"
          OPEN     APSSUBA1,"apssubaf"
          OPEN     SINCGAA1,"sincgaaf"
          OPEN     SINCGBA1,"sincgbaf"
          OPEN     SINCIAA1,"sinciaaf"
          OPEN     SINCIEA1,"sincieaf"
          OPEN     SINCIHA1,"sincihaf"
          OPEN     SINFACA1,"sinfacaf"
          OPEN     SINPERA1,"sinperaf"
          OPEN     SINPERA2,"sinperaf"
          OPEN     SINPOAA1,"sinpoaaf"
          OPEN     SINPOCA1,"sinpocaf"
          OPEN     SINPOCA4,"sinpocaf"
          OPEN     SINPOFA1,"sinpofaf"
          OPEN     SINSUAA1,"sinsuaaf"
          OPEN     SINSUAA3,"sinsuaaf"
          OPEN     SINSUBA1,"sinsubaf"
.
UORD0010  KEYIN    *P1:24,*EL,"Re Calculate Purchase Analysis. ":
                   "Ok (Y/N) ? ",*V2LON,ANS
          REP      UPPLOW,ANS
          MATCH    ANSN,ANS
          GOTO     UORD9000 IF EQUAL
          MATCH    ANSY,ANS
          GOTO     UORD0010 IF NOT EQUAL
.
          CALL     CLROD000
.
          DISPLAY  *P1:24,"Scanning Purchases by Commodity: "
.
          PACK     KEY6,SP70                                           *C-59696
          CALL     RSSIGA1
UORD1000  CALL     RKSIGA1
          BRANCH   OVRCD,UORD9000
.
          DISPLAY  *P34:24,SIGACOD,*EL;
.
          PACK     KEY11,SIGACOD,SP70                                  *C-59696
          CALL     RSSISA3
.
UORD2000  CALL     RKSISA3
          BRANCH   OVRCD,UORD1000
          MATCH    SIGACOD,SISACOMM
          GOTO     UORD1000 IF NOT EQUAL
.
          PACK     KEY19,SISACODE,SP70
          CALL     RSSIPC4
.
UORD3000  CALL     RKSIPC4
          BRANCH   OVRCD,UORD2000
          MATCH    SIPCPRD,SISACODE
          GOTO     UORD2000 IF NOT EQUAL
.
          COMPARE  ZERO,SIPCOQT
          GOTO     UORD3000 IF EQUAL
.
          MOVE     SIPCPON,KEY7
          CALL     RDSIPA1
          BRANCH   OVRCD,UORD3000
.
          MATCH    SP70,SIPADCN             * Check If Order Cancelled.
          GOTO     UORD3000 IF NOT EQUAL
.
          MOVE     ONE,SIFACTOR
          MATCH    SP70,SIPCCAT
          IF       !@EQUAL
            MOVE     SIPCCAT,KEY7
            CALL     RDSIIA1
            IF       OVRCD=0
              PACK     KEY30,SIIAISS,SIPCSUT
              CALL     RDSIFA1
            ENDIF
          ENDIF
.
          PACK     KEY12,SIPADIN,SP70
          CALL     RSSIPE2
          CALL     RKSIPE2
.
          MOVE     SIPADIN,SIPEDATE
          CALL     RSIPE000
          IF       OVRCD=1
            DISPLAY  *P1:24,*B,*EF,"Invalid Date ",SIPAPON;
          ELSE
            PACK     KEY6,SIPEYEAR,SIPEPER,SP70
            MATCH    KEY6,SIPCDAT
            IF       !@EQUAL
              REPEAT
                PACK     KEY10,SIPCPON,SIPCITM,SP70
                CALL     RLSIPC1
                BRANCH   OVRCD,UORD3000
                IF       OVRCD=2
                  DISPLAY   *P40:24,*V2LON," P/O ",KEY10," Locked",*EL;
                ENDIF
              UNTIL    OVRCD=0
              PACK     SIPCDAT,KEY6,SP70
              CALL     UPSIPC1
              CALL     UUSIPC1
            ENDIF
          ENDIF
.
          ASSIGN   SIPCOQT*SIFACTOR,SIUSQTY
          ASSIGN   SIPCECT*SIPCOQT,SIUSCST
.
          CALL     SINORDUS                * Update Initial Purchase Analysis
.
          COMPARE  ZERO,SIPCRQT            * Zero Received 
          GOTO     UORD3000 IF EQUAL       * So get Next
.
          COMPARE  SIUSQTY,SIPCRQT
          GOTO     UORD3700 IF EQUAL
.
.         MATCH    SP70,SIPCEDD            * All Items Received
.         GOTO     UORD3700 IF EQUAL       
.
. Partially Received therefore NO Qty Adjustment Still Outstanding
.----------------------------------------------------------------
.
UORD3100  IF       SIUSQTY<>0
            ASSIGN   SIPCTCR-((SIUSCST/SIUSQTY)*SIPCRQT),SIUSCST
            MOVE     ZERO,SIUSQTY
          ELSE
            ASSIGN   SIPCTCR-SIUSCST,SIUSCST
          ENDIF
.
          PACK     KEY12,SIPCPON,SIPCITM,Z70
          CALL     RSSIPF1
          CALL     RPSIPF1
          BRANCH   OVRCD,UORD3000
          MATCH    SIPCPON,SIPFPON
          GOTO     UORD3000 IF NOT EQUAL
          MATCH    SIPCITM,SIPFITM
          GOTO     UORD3000 IF NOT EQUAL
          UNPACK   SIPFUDT,SIPEYEAR,SIPEPER
.
          CALL     SINORDUS       
          GOTO     UORD3000 
. 
. All Received
.---------------
UORD3700  ASSIGN   SIPCRQT-SIUSQTY,SIUSQTY
          ASSIGN   SIPCTCR-SIUSCST,SIUSCST
          COMPARE  ZERO,SIUSQTY            * Variation Only
          GOTO     UORD3750 IF NOT EQUAL
          COMPARE  ZERO,SIUSCST            * Variation Only
          GOTO     UORD3800 IF EQUAL
.
          PACK     KEY12,SIPCPON,SIPCITM,Z70
          CALL     RSSIPF1
          CALL     RPSIPF1
          BRANCH   OVRCD,UORD3000
          MATCH    SIPCPON,SIPFPON
          GOTO     UORD3000 IF NOT EQUAL
          MATCH    SIPCITM,SIPFITM
          GOTO     UORD3000 IF NOT EQUAL
          UNPACK   SIPFUDT,SIPEYEAR,SIPEPER
.
UORD3750  CALL     SINORDUS
.
UORD3800  COMPARE  ZERO,SIPCIQT            * Nothing Invoiced so get next
          GOTO     UORD3000 IF EQUAL      
.
          ASSIGN   SIPCTIN-SIPCTCR,SIUSCST
          MOVE     ZERO,SIUSQTY            * Variation Only
.
          COMPARE  ZERO,SIUSCST            * No Variation 
          GOTO     UORD3000 IF EQUAL
.
          CALL     SINORDUS                * Update Purchase Analysis
          GOTO     UORD3000
.
UORD9000  CLOSE    SINCCAA1
          CLOSE    SINCCBA1
          CLOSE    SINCCEA1
          CLOSE    SINCCFA1
          CLOSE    SINCCHA1
          CLOSE    SINCCHA2
          CLOSE    SINCGAA1
          CLOSE    SINCGBA1
          CLOSE    SINCIAA1
          CLOSE    SINCIEA1
          CLOSE    SINCIHA1
          CLOSE    SINREAA3
          CLOSE    SINREBA1
          CLOSE    SINSUAA1
          CLOSE    SINSUBA1
.
UORD9999  DISPLAY  *P1:24,*EL,"Complete - ";
          CALL     HITENTER
          CLOSE    SINPERA1
          RETURN
.------------------------------------------------------------
. Determine Period to Process
.------------------------------------------------------------
GETDAT00  OPEN     SINPERA1,"sinperaf"
.
GETDAT10  KEYIN    *P1:23,*EF,"Enter From Date CCYYMM : ",KEY6:
                   *P1:23,*EL,"Enter From Date CCYYMM : ",*DV,KEY6
          ENDSET   KEY6
          APPEND   SP70,KEY6
          RESET    KEY6
          MATCH    SP70,KEY6
          GOTO     GETDAT95 IF EQUAL
          CALL     RDSIPE1
          IF       OVRCD=1
            BEEP
            GOTO     GETDAT10
          ENDIF
          PACK     FROMDT,SIPEYEAR,SIPEPER,SP70
.
GETDAT20  KEYIN    *P40:23,*EL,"Enter To Date CCYYMM : ",KEY6:
                   *P40:23,*EL,"Enter To Date CCYYMM : ",*DV,KEY6
          ENDSET   KEY6
          APPEND   SP70,KEY6
          RESET    KEY6
          MATCH    SP70,KEY6
          GOTO     GETDAT10 IF EQUAL
          CALL     RDSIPE1
          IF       OVRCD=1
            BEEP
            GOTO     GETDAT20
          ENDIF
          PACK     TODT,SIPEYEAR,SIPEPER,SP70
.
          MATCH    FROMDT,TODT
          IF       @LESS
            MOVE     "Illegal Range - ",DISPMSG
            CALL     MESSAGE1
            GOTO     GETDAT10
          ENDIF
.
GETDAT90  CALL     CONTQST
          SUB      ONE,CEXIT
          BRANCH   CEXIT,GETDAT10,GETDAT95
.
          MOVE     ZERO,EXIT
          GOTO     GETDAT99
.
GETDAT95  MOVE     ONE,EXIT
.
GETDAT99  RETURN
.------------------------------------------------------------
. Clear Usage Amounts
.------------------------------------------------------------
. Clear Commodity Usage
.---------------------------
CLRUS000  DISPLAY  *P1:24,*EL,"Clearing Commodity Summary Details"
          MOVE     ZERO,SECTOR
          PACK     KEY12,SP70                                          *C-59696
CLRUS100  CALL     RSSIGB1
          CALL     RKSIGB1
          BRANCH   OVRCD,CLRUS190
          PACK     KEY12,SIGBCOM,SIGBDAT,SP70                          *C-59696
          MATCH    FROMDT,SIGBDAT
          GOTO     CLRUS100 IF LESS
          MATCH    SIGBDAT,TODT
          GOTO     CLRUS100 IF LESS
.
          REPEAT
            CALL     RLSIGB1
            BRANCH   OVRCD,CLRUS100
            IF       OVRCD=2
              DISPLAY  *P50:24,*EF,*V2LON,KEY12," Locked !!";          *C-59696
            ELSE
              IF       (SECTOR%500.0=0)
                DISPLAY   *P50:24,*V2LON,*EF,KEY12;                    *C-59696
              ENDIF
              ADD      ONE,SECTOR      * Display Record 
            ENDIF
          UNTIL    OVRCD=0
.
          MOVE     ZERO,SIGBUAM    * Req Usage
          MOVE     ZERO,SIGBRAM    * Rec Usage
          CALL     UPSIGB1
          CALL     UUSIGB1
          GOTO     CLRUS100
CLRUS190
.
. Clear Product Group Usage
          DISPLAY  *P1:24,*EL,"Clearing Product Group Summary Details"
          MOVE     ZERO,SECTOR
          PACK     KEY11,SP70
CLRUS200  CALL     RSSISB1
          CALL     RKSISB1
          BRANCH   OVRCD,CLRUS290
          PACK     KEY11,SISBSUB,SISBDAT,SP70
          MATCH    FROMDT,SISBDAT
          GOTO     CLRUS200 IF LESS
          MATCH    SISBDAT,TODT
          GOTO     CLRUS200 IF LESS
.
          REPEAT
            CALL     RLSISB1
            BRANCH   OVRCD,CLRUS200
            IF       OVRCD=2
              DISPLAY  *P50:24,*EF,*V2LON,KEY11," Locked !!";
            ELSE
              IF       (SECTOR%500.0=0)
                DISPLAY   *P50:24,*V2LON,*EF,KEY11;
              ENDIF
              ADD      ONE,SECTOR      * Display Record 
            ENDIF
          UNTIL    OVRCD=0
.
          MOVE     ZERO,SISBUAM    * Req Usage
          MOVE     ZERO,SISBRAM    * Rec Usage
          CALL     UPSISB1
          CALL     UUSISB1
          GOTO     CLRUS200
CLRUS290
.
. Clear Cost Centre Usage
.-------------------------
          DISPLAY  *P1:24,*EL,"Clearing Cost Centre Summary Details"
          MOVE     ZERO,SECTOR
          PACK     KEY11,SP70
CLRUS300  CALL     RSSICB1
          CALL     RKSICB1
          BRANCH   OVRCD,CLRUS310
          PACK     KEY11,SICBCST,SICBDAT,SP70
          MATCH    FROMDT,SICBDAT
          GOTO     CLRUS300 IF LESS
          MATCH    SICBDAT,TODT
          GOTO     CLRUS300 IF LESS
.
          REPEAT
            CALL     RLSICB1
            BRANCH   OVRCD,CLRUS300
            IF       OVRCD=2
              DISPLAY  *P50:24,*EF,*V2LON,KEY11," Locked !!";
            ELSE
              IF       (SECTOR%500.0=0)
                DISPLAY   *P50:24,*V2LON,*EF,KEY11;
              ENDIF
              ADD      ONE,SECTOR      * Display Record 
            ENDIF
          UNTIL    OVRCD=0
.
          MOVE     ZERO,SICBAMT    * Req Usage
          CALL     UPSICB1
          CALL     UUSICB1
          GOTO     CLRUS300
CLRUS310
.
. Cost Centre / Commodity Usage
.------------------------------
          DISPLAY  *P1:24,*EL,"Clearing Cost Centre/Commodity Summary Details"
          MOVE     ZERO,SECTOR
          PACK     KEY17,SP70                                          *C-59696
CLRUS320  CALL     RSSICE1
          CALL     RKSICE1
          BRANCH   OVRCD,CLRUS330
          PACK     KEY17,SICECST,SICEDAT,SICECOM,SP70                  *C-59696
          MATCH    FROMDT,SICEDAT
          GOTO     CLRUS320 IF LESS
          MATCH    SICEDAT,TODT
          GOTO     CLRUS320 IF LESS
.
          REPEAT
            CALL     RLSICE1
            BRANCH   OVRCD,CLRUS320
            IF       OVRCD=2
              DISPLAY  *P50:24,*EF,*V2LON,KEY17," Locked !!";          *C-59696
            ELSE
              IF       (SECTOR%500.0=0)
                DISPLAY   *P50:24,*V2LON,*EF,KEY17;                    *C-59696
              ENDIF
              ADD      ONE,SECTOR      * Display Record 
            ENDIF
          UNTIL    OVRCD=0
.
          MOVE     ZERO,SICEAMT    * Req Usage
          CALL     UPSICE1
          CALL     UUSICE1
          GOTO     CLRUS320
CLRUS330
.
. Cost Centre / Product Group Usage
.--------------------------------------
          DISPLAY  *P1:24,*EL,"Clearing Cost Centre/Product Group Details"
          MOVE     ZERO,SECTOR
          PACK     KEY16,SP70
CLRUS340  CALL     RSSICF1
          CALL     RKSICF1
          BRANCH   OVRCD,CLRUS350
          PACK     KEY16,SICFCST,SICFDAT,SICFSUB,SP70
          MATCH    FROMDT,SICFDAT
          GOTO     CLRUS340 IF LESS
          MATCH    SICFDAT,TODT
          GOTO     CLRUS340 IF LESS
.
          REPEAT
            CALL     RLSICF1
            BRANCH   OVRCD,CLRUS340
            IF       OVRCD=2
              DISPLAY  *P50:24,*EF,*V2LON,KEY16," Locked !!";
            ELSE
              IF       (SECTOR%500.0=0)
                DISPLAY   *P50:24,*V2LON,*EF,KEY16;
              ENDIF
              ADD      ONE,SECTOR      * Display Record 
            ENDIF
          UNTIL    OVRCD=0
.
          MOVE     ZERO,SICFAMT    * Req Usage
          CALL     UPSICF1
          CALL     UUSICF1
          GOTO     CLRUS340
CLRUS350
.
. Cost Centre / Catalog Usage
.-----------------------------
          DISPLAY  *P1:24,*EL,"Clearing Cost Centre/Catalog Summary Details"
          MOVE     ZERO,SECTOR
          PACK     KEY28,SP70
CLRUS400  CALL     RSSICH1
          CALL     RKSICH1
          BRANCH   OVRCD,CLRUS490
          PACK     KEY28,SICHCST,SICHCAT,SICHWAR,SICHDAT,SICHSUB,SP70
          MATCH    FROMDT,SICHDAT
          GOTO     CLRUS400 IF LESS
          MATCH    SICHDAT,TODT
          GOTO     CLRUS400 IF LESS
.
          REPEAT
            CALL     RLSICH1
            BRANCH   OVRCD,CLRUS400
            IF       OVRCD=2
              DISPLAY  *P50:24,*EF,*V2LON,KEY28," Locked !!";
            ELSE
              IF       (SECTOR%500.0=0)
                DISPLAY   *P50:24,*V2LON,*EF,KEY28;
              ENDIF
              ADD      ONE,SECTOR      * Display Record 
            ENDIF
          UNTIL    OVRCD=0
.
          MOVE     ZERO,SICHUQT    * Req Usage
          MOVE     ZERO,SICHUAM    * Req Usage
          CALL     UPSICH1
          CALL     UUSICH1
          GOTO     CLRUS400
CLRUS490
.
. Clear Catalog Usage
.-------------------------
          DISPLAY  *P1:24,*EL,"Clearing Catalog Summary Details"
          MOVE     ZERO,SECTOR
          PACK     KEY18,SP70
CLRUS500  CALL     RSSIIH1
          CALL     RKSIIH1
          BRANCH   OVRCD,CLRUS590
          PACK     KEY18,SIIHCAT,SIIHWAR,SIIHDAT,SP70
          MATCH    FROMDT,SIIHDAT
          GOTO     CLRUS500 IF LESS
          MATCH    SIIHDAT,TODT
          GOTO     CLRUS500 IF LESS
.
          REPEAT
            CALL     RLSIIH1
            BRANCH   OVRCD,CLRUS500
            IF       OVRCD=2
              DISPLAY  *P50:24,*EF,*V2LON,KEY18," Locked !!";
            ELSE
              IF       (SECTOR%500.0=0)
                DISPLAY   *P50:24,*V2LON,*EF,KEY18;
              ENDIF
              ADD      ONE,SECTOR      * Display Record 
            ENDIF
          UNTIL    OVRCD=0
.
          MOVE     ZERO,SIIHUQT    * Req Usage
          MOVE     ZERO,SIIHUAM    * Req Usage
          MOVE     ZERO,SIIHRQT    * Rec Usage
          MOVE     ZERO,SIIHRAM    * Rec Usage
          CALL     UPSIIH1
          CALL     UUSIIH1
          GOTO     CLRUS500
CLRUS590
.
. Clear Supplier Usage
.-------------------------
          DISPLAY  *P1:24,*EL,"Clearing Supplier Summary Details"
          MOVE     ZERO,SECTOR
          PACK     KEY11,SP70
CLRUS600  CALL     RSAPSB1
          CALL     RKAPSB1
          BRANCH   OVRCD,CLRUS690
          PACK     KEY11,APSBSUP,APSBDAT,SP70
          MATCH    FROMDT,APSBDAT
          GOTO     CLRUS600 IF LESS
          MATCH    APSBDAT,TODT
          GOTO     CLRUS600 IF LESS
.
          REPEAT
            CALL     RLAPSB1
            BRANCH   OVRCD,CLRUS600
            IF       OVRCD=2
              DISPLAY  *P50:24,*EF,*V2LON,KEY11," Locked !!";
            ELSE
              IF       (SECTOR%500.0=0)
                DISPLAY   *P50:24,*V2LON,*EF,KEY11;
              ENDIF
              ADD      ONE,SECTOR      * Display Record 
            ENDIF
          UNTIL    OVRCD=0
.
          MOVE     ZERO,APSBRAM    * Rec Usage
          CALL     UPAPSB1
          CALL     UUAPSB1
          GOTO     CLRUS600
CLRUS690
.
.
CLRUS999  RETURN
.
.------------------------------------------------------------
. Clear Purchase Analysis Amounts
.------------------------------------------------------------
. Clear Commodity Usage
.---------------------------
CLROD000  DISPLAY  *P1:24,*EL,"Clearing Summary Details "
          PACK     KEY9,SP70
          CALL     RSSIGB1
CLROD100  CALL     RKSIGB1         * Commodity
          BRANCH   OVRCD,CLROD190
          MOVE     ZERO,SIGBPAM    
          CALL     UPSIGB1
          GOTO     CLROD100
.
CLROD190  PACK     KEY9,SP70
          CALL     RSSISB1
CLROD200  CALL     RKSISB1         * Product Group
          BRANCH   OVRCD,CLROD290
          MOVE     ZERO,SISBPAM
          CALL     UPSISB1
          GOTO     CLROD200
.
CLROD290  PACK     KEY16,SP70
          CALL     RSSIIH1
CLROD300  CALL     RKSIIH1          * Catalog
          BRANCH   OVRCD,CLROD390
          MOVE     ZERO,SIIHPQT 
          MOVE     ZERO,SIIHPAM 
          CALL     UPSIIH1
          GOTO     CLROD300  
.
CLROD390  PACK     KEY9,SP70
          CALL     RSAPSB1
CLROD400  CALL     RKAPSB1          * Supplier
          BRANCH   OVRCD,CLROD490
          MOVE     ZERO,APSBPAM    
          CALL     UPAPSB1
          GOTO     CLROD400
CLROD490
CLROD999  RETURN
.------------------------------------------------------------
. Re-calculate Stock on Order Quantities
.------------------------------------------------------------
USOO0000  KEYIN    *P1:24,"Enter Restart Count 0=all : ",RESTCNT
          KEYIN    *P40:24,"Ok Y/N ? ",ANS
          REP      UPPLOW,ANS
          MATCH    ANSY,ANS
          GOTO     USOO0000 IF NOT EQUAL
.
          DISPLAY  *P1:24,*EL,"Re-Calculating Stock on Order. Count : ";
          OPEN     SINCIAA1,"sinciaaf"
          OPEN     SINCIEA1,"sincieaf"
          OPEN     SINFACA1,"sinfacaf"
          OPEN     SINPOAA1,"sinpoaaf"
          OPEN     SINPOCA1,"sinpocaf"
          OPEN     SINPOCA6,"sinpocaf"
.
          MOVE     ZERO,RECORDS
          PACK     KEY12,SP70
          CALL     RSSIIE1
USOO1000  CALL     RKSIIE1           * Read Through Stock/Warehouse Records
          BRANCH   OVRCD,USOO9000
.
          ADD      ONE,RECORDS      * Display Record Counter Every 500 Records
          IF       RECORDS<RESTCNT
            GOTO     USOO1000
          ENDIF
          IF       (RECORDS%100.0=0)
            DISPLAY   *P33:24,*V2LON,RECORDS,*EL;
          ENDIF
.
          CALL     UUSIIA1
          CALL     UUSIIE1
.
USOO1010  MOVE     ZERO,SECTOR
          PACK     KEY12,SIIECAT,SIIEWAR
          CALL     RLSIIE1
          BRANCH   OVRCD,USOO1000
          IF       OVRCD=2
            ADD      ONE,SECTOR
            IF       SECTOR%100=0
              DISPLAY   *P1:23,"W/H Catalog Lock: ",SIIECAT,"/",SIIEWAR,*W
            ENDIF
            GOTO     USOO1010
          ENDIF
.
USOO1020  MOVE     ZERO,SECTOR
          PACK     KEY7,SIIECAT,SP70
          CALL     RLSIIA1
          BRANCH   OVRCD,USOO1000
          IF       OVRCD=2
            ADD      ONE,SECTOR
            IF       SECTOR%100=0
              DISPLAY   *P1:23,"Catalog Locked : ",SIIECAT,"/",SIIEWAR,*W
            ENDIF
            GOTO     USOO1020
          ENDIF
.
          MOVE     ZERO,F12P2
          PACK     KEY25,SIIECAT,SP70
          CALL     RSSIPC6
USOO1100  CALL     RKSIPC6
          BRANCH   OVRCD,USOO1900
          MATCH    SIIECAT,SIPCCAT
          GOTO     USOO1900 IF NOT EQUAL
          MATCH    SIIEWAR,SIPCWAR
          GOTO     USOO1100 IF NOT EQUAL
.
          MOVE     ONE,SIFACTOR
          PACK     KEY30,SIIAISS,SIPCSUT
          CALL     RDSIFA1
.
          MOVE     SIPCPON,KEY7
          CALL     RDSIPA1
          BRANCH   OVRCD,USOO1100
.
          MATCH    SP70,SIPADCM
          GOTO     USOO1200 IF NOT EQUAL  * Completed Order No more Outstanding
          MATCH    SP70,SIPADCN
          GOTO     USOO1200 IF NOT EQUAL  * Cancelled Order No more Outstanding
.
          IF       (SIFACTOR*SIPCOQT)-SIPCRQT > 0
            ASSIGN   F12P2+(SIFACTOR*SIPCOQT)-SIPCRQT,F12P2
          ENDIF
          GOTO     USOO1100
.
USOO1200  PACK     KEY10,SIPCPON,SIPCITM * Should be blank if No Outstanding
          CALL     RDSIPC1
          IF       OVRCD=0
            MOVE     SP70,SIPCEDD
            CALL     UPSIPC1
          ENDIF
          GOTO     USOO1100
.
USOO1900  IF       !(F12P2=SIIESOO)
            PRINT    "Catalog/Warehouse : ",SIIECAT,"/",SIIEWAR:
                     "  Unbalanced Stock On Order. On File = ",SIIESOO:
                     "  Calculated = ",F12P2
            MOVE     F12P2,SIIESOO
            CALL     UPSIIE1
          ENDIF
          GOTO     USOO1000
.
USOO9000  CLOSE    SINFACA1
          CLOSE    SINCIAA1
          CLOSE    SINCIEA1
          CLOSE    SINPOAA1
          CLOSE    SINPOCA1
          CLOSE    SINPOCA6
          RETURN
.
CLRIMP00  OPEN     SINCIAA1,"sinciaaf"
          OPEN     SINIMPA1,"sinimpaf"
.
          DISPLAY  *P1:24,"Delete Invalid Imprest Catalogs. ";
          CALL     CONTQST
          SUB      ONE,CEXIT
          BRANCH   EXIT,CLRIMP90,CLRIMP90
.
          DISPLAY  *P1:24,"Scanning: "
          PACK     KEY12,SP70
          MOVE     ZERO,SECTOR
          CALL     RSSIIM1
.
CLRIMP10  CALL     RKSIIM1
          BRANCH   OVRCD,CLRIMP90
          ADD      ONE,SECTOR
          IF       (SECTOR%100=0)
            DISPLAY  *P20:24,SECTOR
          ENDIF
.
          PACK     KEY7,SIIMCAT
          CALL     RDSIIA1
          IF       OVRCD=1
            PACK     KEY12,SIIMCST,SIIMCAT
            DISPLAY  *P40:24,"Delete : ",KEY12
            CALL     RDSIIM1
            IF       OVRCD=0
              CALL     DESIIM1
            ENDIF
          ENDIF
          GOTO     CLRIMP10
CLRIMP90  CLOSE    SINCIAA1
          CLOSE    SINIMPA1
          DISPLAY  *P1:24,*EL,"Complete - ";
          CALL     HITENTER
          RETURN 
.------------------------------
. Update Alias
.------------------------------
UPDCIF00  OPEN     SINCIAA1,"sinciaaf"
          OPEN     SINCIFA1,"sincifaf"
          OPEN     SINCIFA2,"sincifaf"
          OPEN     SINCICA1,"sincicaf"
.
          DISPLAY  *P1:24,"Update Alias File. ";
          CALL     CONTQST
          SUB      ONE,CEXIT
          BRANCH   EXIT,UPDCIF90,UPDCIF90
.
          DISPLAY  *P1:24,*EL,"Scanning: "
          PACK     KEY12,SP70
          MOVE     ZERO,SECTOR
          CALL     RSSIIA1
.
UPDCIF10  CALL     RKSIIA1
          BRANCH   OVRCD,UPDCIF90
          ADD      ONE,SECTOR
          IF       (SECTOR%100=0)
            DISPLAY  *P20:24,SECTOR
          ENDIF
.
          CALL     PALIA000
.
          GOTO     UPDCIF10
.
UPDCIF90  CLOSE    SINCIAA1
          CLOSE    SINCIFA1
          CLOSE    SINCIFA2
          CLOSE    SINCICA1
          DISPLAY  *P1:24,*EL,"Complete - ";
          CALL     HITENTER
          RETURN 
.------------------------------------------------------------
. Post Alias
.------------------------------------------------------------
PALIA000  PACK      KEY67,SIIACAT,SP70
          CALL      RSSIIF2
PALIA100  CALL      RKSIIF2
          BRANCH    OVRCD,PALIA200
          MATCH     SIIFCAT,SIIACAT
          GOTO      PALIA200 IF NOT EQUAL
          COMPARE   ZERO,SIIFATY
          GOTO      PALIA100 IF EQUAL
          PACK      KEY67,SIIFCAT,SIIFALI
          CALL      DESIIF2
          GOTO      PALIA100
.
PALIA200  MOVE      SIIACAT,SIIFCAT
          MOVE      SIIADES,SIIFALI
          MOVE      ONE,SIIFATY
          CALL      POSTALIA
.
          PACK      KEY27,SIIACAT,SP70
          CALL      RSSIIC1
PALIA300  CALL      RKSIIC1
          BRANCH    OVRCD,PALIA400
          MATCH     SIICCAT,SIIACAT
          GOTO      PALIA400 IF NOT EQUAL
.
          PACK      SIIFALI,SIICPD1,SP70
          MOVE      SIIACAT,SIIFCAT
          MOVE      TWO,SIIFATY
          CALL      POSTALIA
.
          PACK      SIIFALI,SIICPD2,SP70
          MOVE      SIIACAT,SIIFCAT
          MOVE      TWO,SIIFATY
          CALL      POSTALIA
.
          PACK      SIIFALI,SIICPNO,SP70
          MOVE      SIIACAT,SIIFCAT
          MOVE      THREE,SIIFATY
          CALL      POSTALIA
          GOTO      PALIA300
.
PALIA400  PROC      WRKCT000              * Write Keyword Index
          RETURN
.
POSTALIA  MATCH     SP70,SIIFALI
          RETURN    IF EQUAL
          PACK      KEY67,SIIFCAT,SIIFALI
          CALL      RASIIF2
          IF        OVRCD=1
            CALL      WRSIIF2
          ENDIF
          RETURN
.----------------------------------------------------------------------
. check for outstanding items on orders
.----------------------------------------------------------------------
CKORD000
          CALL      CONTQST
          SUB       ONE,CEXIT
          BRANCH    CEXIT,CKORD999,CKORD999
.
          OPEN      SINPOAA1,"sinpoaaf"
          OPEN      SINPOCA1,"sinpocaf"
          OPEN      SINCIAA1,"sinciaaf"
          OPEN      SINFACA1,"sinfacaf"
.
          PACK      KEY7,SP70
          CALL      RSSIPA1
.
CKORD100  
          CALL      RKSIPA1
          BRANCH    OVRCD,CKORD999
          MATCH     SP70,SIPADCM
          GOTO      CKORD100 IF NOT EQUAL
.
          PACK      KEY10,SIPAPON,SP70
          CALL      RSSIPC1
.
CKORD200  
          CALL      RKSIPC1
          BRANCH    OVRCD,CKORD100
          MATCH     SIPCPON,SIPAPON
          GOTO      CKORD100 IF NOT EQUAL
.
          MATCH     SP70,SIPCEDD
          GOTO      CKORD200 IF NOT EQUAL
.
          PACK      KEY7,SIPCCAT
          MOVE      ONE,SIFACTOR
          CALL      RDSIIA1
          IF        OVRCD=0
            PACK      KEY30,SIIAISS,SIPCSUT
            CALL      RDSIFA1
          ENDIF
.
          IF        SIPCOQT-SIPCRQT/SIFACTOR>0
            PACK      SIPCEDD,CCC,CYY,CMM,CDD,SP70
            REP       " 0",SIPCEDD
            CALL      UPSIPC1
            DISPLAY   *P1:24,"P/O ",*V2LON,SIPCPON,SLASH,SIPCITM:
                      *HOFF," Updated",*EF;
            PRINT     "P/O ",SIPCPON,SLASH,SIPCITM," Updated"
          ENDIF
.
          GOTO      CKORD200
.
CKORD999  RETURN
.----------------------------------------------------------------------
. Modify SIIESID
.----------------------------------------------------------------------
MESTI000
          KEYIN    *P1:4,*EF,"0=Exit, 1=Clear SIIESID, 2=Set to SIIASTI : ":
                   *V2LON,OPTTYP
          IF       OPTTYP <> 1 & OPTTYP <> 2
            GOTO     MESTI999
          ENDIF
.
          OPEN     SINCIEA1,"sincieaf"
          OPEN     SINCIAA1,"sinciaaf"
          MOVE      ZERO,SECTOR
          PACK      KEY12,SP70
          CALL      RSSIIE1
.
MESTI100  
          CALL      RKSIIE1
          BRANCH    OVRCD,MESTI999
          ADD       ONE,SECTOR
          IF        (SECTOR%100=0)
            DISPLAY   *P40:24,SECTOR;
          ENDIF
.
          PACK      KEY12,SIIECAT,SIIEWAR,SP70
          REPEAT    
            CALL      RLSIIE1
            BRANCH    OVRCD,MESTI100
            IF        OVRCD=2
              DISPLAY   *P40:24,*V2LON," Cat/War ",KEY12," Locked",*EL;
            ENDIF
          UNTIL     OVRCD<>2
.
          MOVE      SP70,SIIASTI
          PACK      KEY7,SIIECAT,SP70
          IF        OPTTYP=2
            CALL      RDSIIA1
          ENDIF
.
          PACK     SIIESID,SIIASTI,SP70
          CALL     UPSIIE1
          CALL     UUSIIE1
          GOTO     MESTI100
.
MESTI999  RETURN
.----------------------------------------------------------------------
. Create APSCIOAF
.----------------------------------------------------------------------
CAPCI000
          DISPLAY   *P1:4,*EF:
                    *P1:10,"From Date : ":
                    *P1:11,"To   Date : ";
.
          PACK     FDATE,CCC,CYY,CMM,CDD,SP70
          UNPACK   FDATE,CCENT,CYEAR,CMON,CDAY
          REP      " 0",FDATE
          MOVE     ONE,CCENTRY
          MOVE     ONE,CCANLDTE
          MOVE     ONE,CDEFDTE
          MOVE     ZERO,CHIGHLT
.
          MOVE     "10",CVERT
          MOVE     "13",CCOL
          CALL     KEYDATE
          PACK     FDATE,CCENT,CYEAR,CMON,CDAY,SP70
.
          MOVE     "11",CVERT
          MOVE     "13",CCOL
          CALL     KEYDATE
          PACK     TDATE,CCENT,CYEAR,CMON,CDAY,SP70
.
          MATCH    FDATE,TDATE
          GOTO     CAPCI000 IF LESS
.
          CALL     CONTQST
          SUB      ONE,CEXIT
          BRANCH   CEXIT,CAPCI000,CAPCI999
.
          OPEN     APSCIOA1,"apscioaf"
          OPEN     APSORDA1,"apsordaf"
          OPEN     SINCGAA1,"sincgaaf"
          OPEN     SINCIAA1,"sinciaaf"
          OPEN     SINFACA1,"sinfacaf"
          OPEN     SINPOAA1,"sinpoaaf"
          OPEN     SINPOCA1,"sinpocaf"
          OPEN     SINPOFA1,"sinpofaf"
          OPEN     SINSUAA1,"sinsuaaf"
          OPEN     SINWARA1,"sinwaraf"
          OPEN     SINCCAA1,"sinccaaf"
          OPEN     SINLTTA1,"sinlttaf"
.
          MOVE      "fmsamaaf",KEY8
          PACK      KEY80,HACCDATA,KEY8
          SQUEEZE   KEY80
          OPEN      FMSAMAA1,KEY80
.
          CALL      PROC00000
.
CAPCI999  RETURN
.**********************************************************************
.UPPER000  Fix Period File - Spaced Dates
.**********************************************************************
UPPER000  
          OPEN     SINPERA1,"sinperaf"
          MOVE     SP70,KEY6
          CALL     RSSIPE1
UPPER100  CALL     RKSIPE1
          BRANCH   OVRCD,UPPER900
.
          DISPLAY  *P20:24,*EL,SIPEEDT
.
          REP      " 0",SIPEEDT
          REP      " 0",SIPESDT
          CALL     UPSIPE1
          GOTO     UPPER100
UPPER900
          DISPLAY  *P1:24,*EL,"Complete - ";
          CALL     HITENTER
.
UPPER9999 RETURN
.**********************************************************************
. Compelete Order Requiring Completion
.**********************************************************************
COMPL000  
          OPEN     SINPOAA1,"sinpoaaf"
          OPEN     SINPOCA1,"sinpocaf"
          OPEN     SINFACA1,"sinfacaf"
          OPEN     SINCIAA1,"sinciaaf"
.
          MOVE      ZERO,COUNTER
          MOVE      ZERO,UPDATED
          DISPLAY   *P1:3,*EF:
                    *P1:4,"Records Processed: ",*P20:4,COUNTER:
                    *P1:5,"Records Updated  : ",*P20:5,UPDATED
          PACK      KEY7,SP70
          CALL      RSSIPA1
COMPL100  CALL      RKSIPA1
          BRANCH    OVRCD,COMPL999
.
          ADD       ONE,COUNTER
          IF        COUNTER%100 = 1
            DISPLAY   *P20:4,COUNTER
          ENDIF
.
          MATCH     SIPADCM,SP70
          GOTO      COMPL100 IF NOT EQUAL
.
          CALL      CHPOC000
          IF        COMPLETE=TRUE
            PACK      SIPADCM,CCC,CYY,CMM,CDD
            REP       " 0",SIPADCM
            CALL      UPSIPA1
            ADD       ONE,UPDATED
            DISPLAY   *P20:5,UPDATED
          ENDIF
          GOTO      COMPL100
.
COMPL999  MOVE      "Process Complete.  ",DISPMSG
          CALL      MESSAGE1
          RETURN
.---------------------------------------------------------------------- 
. Check for Outstanding Items on Purchase Order Details File
.---------------------------------------------------------------------- 
CHPOC000  MOVE      TRUE,COMPLETE
          PACK      KEY10,SIPAPON,SP70
          CALL      RSSIPC1
CHPOC100  CALL      RKSIPC1
          BRANCH    OVRCD,CHPOC999
.
          MATCH     SIPCPON,SIPAPON
          GOTO      CHPOC999 IF NOT EQUAL
.
          MOVE      ONE,SIFACTOR
          MATCH     SIPCCAT,SP70
          IF        !@EQUAL
            PACK      KEY7,SIPCCAT,SP70
            CALL      RDSIIA1
            IF        OVRCD=0
              PACK      KEY30,SIIAISS,SIPCSUT,SP70
              CALL      RDSIFA1
            ENDIF
          ENDIF
.
          IF        SIPCOQT > (SIPCRQT/SIFACTOR)
            MOVE      FALSE,COMPLETE
            GOTO      CHPOC999
          ENDIF
          GOTO      CHPOC100
.
CHPOC999  RETURN
.**********************************************************************
. read through receivals
.**********************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Scanning : "
          CALL      RCON0000
          MOVE      ZERO,SECTOR
          PACK      SKEY10,SP70
          PACK      KEY12,SP70
          CALL      RSSIPF1
.
PROC1000  CALL      RKSIPF1            * Loop Through Each Item Received
          BRANCH    OVRCD,PROC9999     * End of Receival
.
          ADD      ONE,SECTOR
          IF       (SECTOR%100=0)
             DISPLAY   *P40:24,SECTOR," - ",SIPFPON,*EL;
          ENDIF
.
          MATCH     FDATE,SIPFDAT
          GOTO      PROC1000 IF LESS
          MATCH     SIPFDAT,TDATE
          GOTO      PROC1000 IF LESS
.
          PACK      KEY7,SIPFPON,SP70
          CALL      RDSIPA1
          BRANCH    OVRCD,PROC1000
.
          PACK      KEY10,SIPFPON,SIPFITM,SP70
          CALL      RDSIPC1
          BRANCH    OVRCD,PROC1000
          COMPARE   SIPCRQT,SIPCIQT    * fully invoiced
          GOTO      PROC1000 IF NOT LESS
.
          MATCH     KEY10,SKEY10
          IF        !@EQUAL
            MOVE      KEY10,SKEY10
            CALL      DEOD0000
          ENDIF
.
          MATCH     SP70,SIPCCAT
          GOTO      PROC5000 IF EQUAL  * Item Not Cataloged
.
          MOVE      SIPCCAT,KEY7
          CALL      RDSIIA1            * Get Catalog Details
          BRANCH    OVRCD,PROC5000
.
          MOVE      ONE,SIFACTOR
          PACK      KEY30,SIIAISS,SIPFSUT
          CALL      RDSIFA1            * Determine Total Received Issue Units
.
          BRANCH    SIIANON,PROC5100   * Item Not Stocked
.
          IF        HACCNT=1
            CALL      USAPS000         * Update Accounts Payable Details
          ENDIF
          GOTO      PROC6000           * Next
.
. Non-Catalog Item
.------------------
PROC5000  
.
. Non-Stock
.----------
PROC5100  
          IF        HACCNT=1
            CALL      UNAPS000         * Update Accounts Payable Details
          ENDIF
          GOTO      PROC6000
.
PROC6000  
          GOTO      PROC1000           * Next Item
.
PROC9999  
          RETURN
.----------------------------------------------------------------------
. delete existing orders
.----------------------------------------------------------------------
DEOD0000  MOVE      HORDDES,APCIISC  * System Prefix
          MOVE      SIPFPON,APCIORD
          MOVE      SIPASUP,APCICRD
          PACK      KEY8,APCIISC,APCIORD,SP70
          CALL      RDAPCI1         * Order Header Record
.
          MOVE      HORDDES,APODISC  * System Prefix
          MOVE      SIPFPON,APODORD
          PACK      APODCAT,SIPFITM,SIPFREC,SP70
          PACK      KEY14,APODISC,APODORD,APODCAT,SP70
          CALL      DEAPOD1
.
DEOD9999  RETURN
.----------------------------------------------------------------------
. Update Non-Stock Item to Accounts Payable Files
.----------------------------------------------------------------------
UNAPS000  MOVE      HORDDES,APCIISC  * System Prefix
          MOVE      SIPFPON,APCIORD
          MOVE      SIPASUP,APCICRD
          PACK      KEY8,APCIISC,APCIORD
          CALL      RDAPCI1         * Order Header Record
          IF        OVRCD=1
            MOVE      SIPFDAT,APCIDAT
            MOVE      SP70,APCIDAT
            CALL      WRAPCI1
          ELSE
            MOVE      SIPFDAT,APCIDAT
            CALL      UPAPCI1
          ENDIF
.
          MOVE      HORDDES,APODISC  * System Prefix
          MOVE      SIPFPON,APODORD
          PACK      APODCAT,SIPFITM,SIPFREC,SP70
          PACK      KEY14,APODISC,APODORD,APODCAT
          CALL      RDAPOD1
          IF        OVRCD=0
            MOVE      SIPFQTY,APODQTY
            MOVE      SIPFCST,APODUCT
            MOVE      SIPFGST,APODUGS
            ASSIGN    APODQTY*APODUCT,APODAMT
            ASSIGN    APODQTY*APODUGS,APODGSTA
            MOVE      SIPCGST,APODGST
            CALL      UPAPOD1
            GOTO      UNAPS999
          ENDIF
.
. No Existing Record
.--------------------
          MOVE      HDEFWH,KEY5
          CALL      RDSIWA1
          IF        OVRCD=1
            MOVE      "WARNING: Default Warehouse Missing.First Used.- ",DISPMSG
            BEEP
            CALL      MESSAGE1
            MOVE      SP70,KEY5
            CALL      RSSIWA1
            CALL      RKSIWA1
          ENDIF
.
          MOVE      SIPCCST,KEY5
          CALL      RDSICA1
          IF        OVRCD=1
            BEEP
            CLEAR     DISPMSG
            APPEND    "Invalid Cost Centre : ",DISPMSG
            APPEND    KEY5,DISPMSG
            APPEND    " - ",DISPMSG
            RESET     DISPMSG
            CALL      MESSAGE1
          ENDIF
.
          UNPACK    SICAGL,APODLED,APODACC
          MOVE      SIWADISS,APODDIS
          MOVE      SIWARESP,APODRES
          MOVE      SP70,APODSPA
          MOVE      SIPFQTY,APODQTY
          MOVE      SIPFCST,APODUCT
          ASSIGN    APODQTY*APODUCT,APODAMT
          MOVE      SIPFGST,APODUGS
          ASSIGN    APODQTY*APODUGS,APODGSTA
          MOVE      SIPCGST,APODGST
          MOVE      SIPCPD,APODDES
.
UNAPS100  STORE     APODACC,FMSUUD4A,KEY1,KEY2,KEY3,KEY4,KEY5,KEY6:
                                     KEY7,KEY8,KEY9,KEY10,KEY11,KEY12
          LOAD      APODACC,FMSUUD4A,KEY1,KEY2,KEY3,KEY4,KEY5,KEY6:
                                     KEY7,KEY8,KEY9,KEY10,KEY11,KEY12
          IF        FMSUUD2A=1
            PACK      APODACC,APODACC,FMSUUD2B
          ENDIF
.
          MATCH     SP70,SIPCCAT
          IF        @EQUAL
            MOVE      SIPCPRD,SIIASUB
          ENDIF
          MOVE      SIIASUB,KEY5
          CALL      RDSISA1
          BRANCH    SIWATYP,UNAPS120,UNAPS115
.
UNAPS110
          PACK      SILTSUB,SIIASUB,SP70
          CALL      GTRA0000                     * check for translation table
          PACK      APODACC,APODACC,SILTSUB,SP70 * Add subj code
          GOTO      UNAPS200
.
UNAPS115
          PACK      APODACC,SICAPACC,SP70        * Use CC Posting Account
          GOTO      UNAPS200
.
UNAPS120  MOVE      SISACOMM,KEY6                                      *C-59696
          CALL      RDSIGA1
          PACK      SILTSUB,SISACOMM,SP70
          CALL      GTRA0000                     * check for translation table
          PACK      APODACC,APODACC,SILTSUB,SP70 * Add commodity code
.
UNAPS200  CALL      WRAPOD1
          CALL      CHKFMS00
.
UNAPS999  RETURN
.------------------------------------------------------------
. Check FMS Account
.------------------------------------------------------------
CHKFMS00  PACK      KEY14,APODLED,APODACC
          CALL      RDFMAM1
          IF        OVRCD=1
            BEEP
            CLEAR     DISPMSG
            APPEND    "Ledger/Account : ",DISPMSG
            APPEND    APODLED,DISPMSG
            APPEND    "/",DISPMSG
            APPEND    APODACC,DISPMSG
            APPEND    " on  ",DISPMSG
            APPEND    APODISC,DISPMSG
            APPEND    APODORD,DISPMSG
            APPEND    APODCAT,DISPMSG
            APPEND    " Invalid - ",DISPMSG
            RESET     DISPMSG
            CALL      MESSAGE1
          ENDIF
          RETURN
.----------------------------------------------------------------------
. Update Stock Item to Accounts Payable Files
.----------------------------------------------------------------------
USAPS000  MOVE      HORDDES,APCIISC  * System Prefix
          MOVE      SIPFPON,APCIORD
          MOVE      SIPASUP,APCICRD
          PACK      KEY8,APCIISC,APCIORD
          CALL      RDAPCI1         * Order Header Record
          IF        OVRCD=1
            MOVE      SIPFDAT,APCIDAT
            MOVE      SP70,APCIDAT
            CALL      WRAPCI1
          ENDIF
.
          MOVE      HORDDES,APODISC  * System Prefix
          MOVE      SIPFPON,APODORD
          PACK      APODCAT,SIPFITM,SIPFREC,SP70
          PACK      KEY14,APODISC,APODORD,APODCAT
          CALL      RDAPOD1
          IF        OVRCD=0
            MOVE      SIPFQTY,APODQTY
            MOVE      SIPFCST,APODUCT
            MOVE      SIPFGST,APODUGS
            ASSIGN    APODQTY*APODUCT,APODAMT
            ASSIGN    APODQTY*APODUGS,APODGSTA
            MOVE      SIPCGST,APODGST
            CALL      UPAPOD1
            GOTO      USAPS999
          ENDIF
.
. No Existing Record
.--------------------
          MOVE      SIPCWAR,KEY5
          CALL      RDSIWA1
          IF        OVRCD=1
            MOVE      "Warehouse Code Missing - ",DISPMSG
            BEEP
            CALL      MESSAGE1
            MOVE      SP70,KEY5
            CALL      RSSIWA1
            CALL      RKSIWA1
          ENDIF
.
          MOVE      SIWALED,APODLED
          MOVE      SIWADISS,APODDIS
          MOVE      SIWARESP,APODRES
          MOVE      SP70,APODSPA
          MOVE      SIPFQTY,APODQTY
          MOVE      SIPFCST,APODUCT
          ASSIGN    APODQTY*APODUCT,APODAMT
          MOVE      SIPFGST,APODUGS
          ASSIGN    APODQTY*APODUGS,APODGSTA
          MOVE      SIPCGST,APODGST
          MOVE      SIPCPD,APODDES
.
          BRANCH    SIWAPTY,USAPS100,USAPS100
          MOVE      SIWAPACC,APODACC
          GOTO      USAPS200
.
USAPS100  STORE     SIWAPACC,FMSUUD4A,KEY1,KEY2,KEY3,KEY4,KEY5,KEY6:
                                      KEY7,KEY8,KEY9,KEY10,KEY11,KEY12
          LOAD      APODACC,FMSUUD4A,KEY1,KEY2,KEY3,KEY4,KEY5,KEY6:
                                     KEY7,KEY8,KEY9,KEY10,KEY11,KEY12
          IF        FMSUUD2A=1
            PACK      APODACC,APODACC,FMSUUD2B
          ENDIF
          MOVE      SIIASUB,KEY5
          CALL      RDSISA1
          BRANCH    SIWAPTY,USAPS110,USAPS120
.
USAPS110
          PACK      SILTSUB,SIIASUB,SP70
          CALL      GTRA0000                     * check for translation table
          PACK      APODACC,APODACC,SILTSUB,SP70 * Add subj code
          GOTO      USAPS200
.
USAPS120  MOVE      SISACOMM,KEY6                                      *C-59696
          CALL      RDSIGA1
          PACK      SILTSUB,SISACOMM,SP70
          CALL      GTRA0000                     * check for translation table
          PACK      APODACC,APODACC,SILTSUB,SP70 * Add commodity code
.
USAPS200  CALL      WRAPOD1
          CALL      CHKFMS00
.
USAPS999  RETURN
.----------------------------------------------------------------------
. get subjective/commodity translation
.----------------------------------------------------------------------
GTRA0000
          PACK      KEY8,APODLED,SILTSUB,SP70                          *C-59696
          CALL      RDSILT1
.
GTRA9999  RETURN
.----------------------------------------------------------------------
. Process Receival Parameters
.-----------------------------
. HAVGCOST  FORM 1  When to Calculate Average Cost         1=Receivals
.                                                          2=Invoice
. HBACK     FORM 1  Running Backordering                   0=No
. HACCNT    FORM 1  Running the Accounting System          0=No
. HACCDATA  DIM  20 General Ledger Data Directory
. HDEFWH    DIM  5  Default Warehouse for Non Stock Items
. HORDDES   DIM  1  Order Number Alhpa Prefix
.----------------------------------------------------------------------
RCON0000  READ      CONTROLF,TWENTY3;*95,HACCNT,*120,HBACK,*124,HORDDES:
                                    *143,HDEFWH,*248,HAVGCOST
          READ      CONTROLF,TWENTY4;*135,HACCDATA
          COMPARE   ONE,HACCNT
          GOTO      RCON9999 IF NOT EQUAL
.
          CLOSE     CONTROLF
          MOVE      "controlf.txt",KEY12
          PACK      KEY80,HACCDATA,KEY12
          SQUEEZE   KEY80
          OPEN      CONTROLF,KEY80
          CALL      RDFMCO51           * Read Account Code Parameters
.
          CLOSE     CONTROLF
          OPEN      CONTROLF,"controlf"
.
RCON9999  RETURN
.
.**********************************************************************
. Clear warehouse in sinitmaf if for a non-stock catalog
.**********************************************************************
SPACW000  
          OPEN     SINITMA1,"sinitmaf"
          OPEN     SINCIAA1,"sinciaaf"
.
          MOVE      ZERO,COUNTER
          MOVE      ZERO,UPDATED
          DISPLAY   *P1:3,*EF:
                    *P1:4,"Records Processed: ",*P20:4,COUNTER:
                    *P1:5,"Records Updated  : ",*P20:5,UPDATED
.
          MOVE      "    0",DIM5
          PACK      KEY28,DIM5,Z70
SPACW100  CALL      RSSIIT1
SPACW200  CALL      RKSIIT1
          BRANCH    OVRCD,SPACW999
.
          ADD       ONE,COUNTER
          IF        COUNTER%1000 = 1
            DISPLAY   *P20:4,COUNTER
          ENDIF
.
          PACK      KEY7,SIITCAT,SP70
          CALL      RDSIIA1
          BRANCH    OVRCD,SPACW200
.
          COMPARE   ZERO,SIIANON
          GOTO      SPACW200 IF EQUAL
.
          PACK      SKEY28,SIITWAR,SIITCAT,SIITDAT,SIITTIM,SP70
          MOVE      SP5,SIITWAR
          ADD       ONE,UPDATED
          DISPLAY   *P20:5,UPDATED
SPACW250  PACK      KEY28,SIITWAR,SIITCAT,SIITDAT,SIITTIM,SP70
          CALL      RASIIT1
          BRANCH    OVRCD,SPACW300
.
          UNPACK    SIITTIM,DIM6,DIM2
          MOVE      DIM2,FORM2
          ADD       ONE,FORM2
          PACK      SIITTIM,DIM6,FORM2
          REP       " 0",SIITTIM
          GOTO      SPACW250
.         
SPACW300  MOVE      SKEY28,KEY28
          CALL      RASIIT1
.
          CALL      UPSIIT1
          MOVE      SKEY28,KEY28
          GOTO      SPACW100
.
SPACW999  MOVE      "Process Complete.  ",DISPMSG
          CALL      MESSAGE1
          RETURN
.
.**********************************************************************
. Remove warehouse records for non-stock catalogs
.**********************************************************************
CLCIE000  
          OPEN     SINCIAA1,"sinciaaf"
          OPEN     SINCIEA1,"sincieaf"
.
          MOVE      ZERO,COUNTER
          MOVE      ZERO,UPDATED
          DISPLAY   *P1:3,*EF:
                    *P1:4,"Records Processed: ",*P20:4,COUNTER:
                    *P1:5,"Records Updated  : ",*P20:5,UPDATED
.
          PACK      KEY7,SP70
          CALL      RSSIIA1
.
CLCIE200  CALL      RKSIIA1
          BRANCH    OVRCD,CLCIE999
.
          ADD       ONE,COUNTER
          IF        COUNTER%1000 = 1
            DISPLAY   *P20:4,COUNTER
          ENDIF
.
          COMPARE   ZERO,SIIANON
          GOTO      CLCIE200 IF EQUAL
.
CLCIE300  PACK      KEY12,SIIACAT,SP70
          CALL      RSSIIE1
          CALL      RKSIIE1
          BRANCH    OVRCD,CLCIE200
.
          MATCH     SIIACAT,SIIECAT
          GOTO      CLCIE200 IF NOT EQUAL
.
          PACK      KEY12,SIIECAT,SIIEWAR
          CALL      DESIIE1
          ADD       ONE,UPDATED
          GOTO      CLCIE300
.
CLCIE999  MOVE      "Process Complete.  ",DISPMSG
          CALL      MESSAGE1
          RETURN
.------------------------------------------------------------
. Zero Fill Imprest Sequence Numbers
.------------------------------------------------------------
ZFIMP000  OPEN     SINIMPA1,"sinimpaf"
.
          DISPLAY  *P1:24,"Zero Fill Imprest Seq No. ";
          CALL     CONTQST
          SUB      ONE,CEXIT
          BRANCH   EXIT,ZFIMP900,ZFIMP900
.
          DISPLAY  *P1:24,"Scanning: "
          PACK     KEY12,SP70
          MOVE     ZERO,SECTOR
          CALL     RSSIIM1
ZFIMP100  CALL     RKSIIM1
          BRANCH   OVRCD,ZFIMP900
          ADD      ONE,SECTOR
          IF       (SECTOR%100=0)
            DISPLAY  *P20:24,SECTOR
          ENDIF
.
          REP      " 0",SIIMSEQ
          CALL     UPSIIM1
          GOTO     ZFIMP100
.
ZFIMP900  CLOSE    SINIMPA1
          DISPLAY  *P1:24,*EL,"Complete - ";
          CALL     HITENTER
          RETURN 
.
CAITM000  OPEN      SINITMA1,"sinitmaf"
          OPEN      SINPERA1,"sinperaf"
          OPEN      SINPERA2,"sinperaf"
          DISPLAY   *P1:24,*EL,"Fixing period in sinitmaf";
          MOVE      ZERO,SECTOR
          PACK      KEY28,SP70
          CALL      RSSIIT1
.
CAITM100  CALL      RKSIIT1
          BRANCH    OVRCD,CAITM900
.
          ADD       ONE,SECTOR
          IF        SECTOR%100=1
            DISPLAY   *P40:24,SECTOR
          ENDIF
.
          MOVE      SIITDAT,SIPEDATE
          CALL      RSIPE000
          PACK      DIM6,SIPEYEAR,SIPEPER
.
          MATCH     SIITPER,DIM6
          GOTO      CAITM100 IF EQUAL
.
          MOVE      DIM6,SIITPER
          GOTO      CAITM100
.
CAITM900  CLOSE     SINITMA1
          CLOSE     SINPERA1
          CLOSE     SINPERA2
          DISPLAY   *P1:24,*EL,"Complete - ";
          CALL      HITENTER
CAITM999  RETURN
.-----------------------------------------------------------
. Make Catalog Codes Numeric
.-----------------------------------------------------------
NUMCAT00  DISPLAY  *P1:24,"Convert to Numeric Catalog Numbers. ";
          CALL     CONTQST
          BRANCH   CEXIT,NUMCAT05,NUMCAT99,NUMCAT99
NUMCAT05  OPEN     SINCIAA1,"sinciaaf"
          OPEN     SINCIAA3,"sinciaaf"
          OPEN     SINCIBA1,"sincibaf"
          OPEN     SINCICA1,"sincicaf"
          OPEN     SINCIDA1,"sincidaf"
          OPEN     SINCIEA1,"sincieaf"
          OPEN     SINCIFA2,"sincifaf"
          OPEN     SINCIGA1,"sincigaf"
          OPEN     SINKCTA2,"sinkctaf"
.
          MOVE     ZERO,SECTOR
          PACK     KEY67,SP70
          CALL     RSSIIA3
NUMCAT10  CALL     RKSIIA3
          BRANCH   OVRCD,NUMCAT99
          PACK     SAVKEY67,SIIADES,SIIACAT,SP70
          MOVE     SIIACAT,KEY7
          SQUEEZE  KEY7
          MOVE     KEY7,NUMCAT7
          MOVE     NUMCAT7,KEY7
          MATCH    KEY7,SIIACAT
          GOTO     NUMCAT10 IF EQUAL
          CALL     RASIIA1
          COMPARE  ZERO,OVRCD
          GOTO     NUMCAT95 IF EQUAL
          ADD      ONE,SECTOR
          IF       SECTOR%100=0
            DISPLAY   *P1:24,"Catalog/Record    : ",KEY7,"/",SECTOR
          ENDIF
.           
NUMCAT20  PACK     KEY10,SIIACAT,SP70
          CALL     RSSIIB1
          CALL     RKSIIB1
          BRANCH   OVRCD,NUMCAT30
          MATCH    SIIBCAT,SIIACAT
          GOTO     NUMCAT30 IF NOT EQUAL
          MOVE     NUMCAT7,SIIBCAT
          CALL     UPSIIB1
          GOTO     NUMCAT20
.           
NUMCAT30  PACK     KEY27,SIIACAT,SP70
          CALL     RSSIIC1
          CALL     RKSIIC1
          BRANCH   OVRCD,NUMCAT40
          MATCH    SIICCAT,SIIACAT
          GOTO     NUMCAT40 IF NOT EQUAL
          MOVE     NUMCAT7,SIICCAT
          CALL     UPSIIC1
          GOTO     NUMCAT30
.           
NUMCAT40  PACK     KEY15,SIIACAT,SP70
          CALL     RSSIID1
          CALL     RKSIID1
          BRANCH   OVRCD,NUMCAT50
          MATCH    SIIDCAT,SIIACAT
          GOTO     NUMCAT50 IF NOT EQUAL
          MOVE     NUMCAT7,SIIDCAT
          CALL     UPSIID1
          GOTO     NUMCAT40
.           
NUMCAT50  PACK     KEY12,SIIACAT,SP70
          CALL     RSSIIE1
          CALL     RKSIIE1
          BRANCH   OVRCD,NUMCAT60
          MATCH    SIIECAT,SIIACAT
          GOTO     NUMCAT60 IF NOT EQUAL
          MOVE     NUMCAT7,SIIECAT
          CALL     UPSIIE1
          GOTO     NUMCAT50
.           
NUMCAT60  PACK     KEY67,SIIACAT,SP70
          CALL     RSSIIF2
          CALL     RKSIIF2
          BRANCH   OVRCD,NUMCAT70
          MATCH    SIIFCAT,SIIACAT
          GOTO     NUMCAT70 IF NOT EQUAL
          MOVE     NUMCAT7,SIIFCAT
          CALL     UPSIIF2
          GOTO     NUMCAT60
.           
NUMCAT70  PACK     KEY15,SIIACAT,SP70
          CALL     RSSIIG1
          CALL     RKSIIG1
          BRANCH   OVRCD,NUMCAT80
          MATCH    SIIGCAT,SIIACAT
          GOTO     NUMCAT80 IF NOT EQUAL
          MOVE     NUMCAT7,SIIGCAT
          CALL     UPSIIG1
          GOTO     NUMCAT70
.           
NUMCAT80  PACK     KEY22,SIIACAT,SP70
          CALL     RSSIKC2
          CALL     RKSIKC2
          BRANCH   OVRCD,NUMCAT90
          MATCH    SIKCCAT,SIIACAT
          GOTO     NUMCAT90 IF NOT EQUAL
          MOVE     NUMCAT7,SIKCCAT
          CALL     UPSIKC2
          GOTO     NUMCAT80
.
NUMCAT90  MOVE     SIIACAT,KEY7
          CALL     RDSIIA1
          IF       OVRCD=0
            MOVE     NUMCAT7,SIIACAT
            CALL     UPSIIA1
          ENDIF
          MOVE     SAVKEY67,KEY67
          CALL     RDSIIA3
          GOTO     NUMCAT10
.
NUMCAT95  CLEAR    DISPMSG
          APPEND   "Duplicate Found : ",DISPMSG
          APPEND   KEY7,DISPMSG
          APPEND   " - ",DISPMSG
          RESET    DISPMSG
          CALL     MESSAGE1
          MOVE     SAVKEY67,KEY67
          CALL     RDSIIA3
          GOTO     NUMCAT10
.
NUMCAT99  MOVE     "Complete - ",DISPMSG
          CALL     MESSAGE1
          RETURN
.------------------------------------------------------------
. Clear SIPCEDD if Required
.------------------------------------------------------------
.
CLCEDD00  DISPLAY  *P1:24,*EL,"Clear Expected Delivery Date. Count : ";
          OPEN     SINCIAA1,"sinciaaf"
          OPEN     SINFACA1,"sinfacaf"
          OPEN     SINPOAA1,"sinpoaaf"
          OPEN     SINPOCA1,"sinpocaf"
          OPEN     SINPOFA1,"sinpofaf"
.
          MOVE     ZERO,CPAGENO
          MOVE     "99",CLNO
          CLOCK    TIME,CTIMEIS
          MOVE     ZERO,RECORDS
          PACK     KEY7,SP70
          CALL     RSSIPA1
.
CLCEDD01  CALL     RKSIPA1
          BRANCH   OVRCD,CLCEDD99
.
          ADD      ONE,RECORDS      * Display Record Counter Every 500 Records
          IF       (RECORDS%100.0=0)
            DISPLAY   *P40:24,*V2LON,RECORDS,*EL;
          ENDIF
.
          PACK     KEY10,SIPAPON,SP70
          CALL     RSSIPC1
.
CLCEDD02  CALL     RKSIPC1
          BRANCH   OVRCD,CLCEDD01
          MATCH    SIPAPON,SIPCPON
          GOTO     CLCEDD01 IF NOT EQUAL
.
          MOVE     ONE,SIFACTOR
          MATCH    SP7,SIPCCAT
          IF       !@EQUAL  
            PACK     KEY7,SIPCCAT,SP70
            CALL     RDSIIA1
            PACK     KEY30,SIIAISS,SIPCSUT
            CALL     RDSIFA1
          ENDIF
.
          IF       (SIFACTOR*SIPCOQT)-SIPCRQT < 0 
            GOTO     CLCEDD03
          ENDIF
          IF       (SIFACTOR*SIPCOQT)-SIPCRQT = 0 
            GOTO     CLCEDD03
          ENDIF
          GOTO     CLCEDD02
.
CLCEDD03  MATCH    SIPCEDD,SP70
          GOTO     CLCEDD02 IF EQUAL
. 
          MOVE     SP70,SIPCEDD
          CALL     UPSIPC1
.
          PACK     KEY12,SIPCPON,SIPCITM,Z2
          CALL     RSSIPF1
          CALL     RPSIPF1
.
          COMPARE  "56",CLNO
          CALL     CLCEDD08 IF NOT LESS
.
          UNPACK   SIPFDAT,CCENT,CYEAR,CMON,CDAY
          PRINT    SIPAPON,*11,SIPCITM,*15,SIPCCAT,*31,SIPFREC,*36,SIPFDEL:
                   *59,CDAY,"/",CMON,"/",CCENT,CYEAR
          ADD      ONE,CLNO
          GOTO     CLCEDD02
.
CLCEDD08  CALL     HEAD80
          PRINT    *N,*1,"Purchase",*10,"Item",*16,"Catalog",*25,"Receival":
                   *36,"Delivery Docket",*56,"Receival Date":
                   *N,*1,"Order",*10,"No.",*17,"Code",*25,"Number"
          ADD      TWO,CLNO
          RETURN
.
CLCEDD99  CLOSE    SINFACA1
          CLOSE    SINCIAA1
          CLOSE    SINPOAA1
          CLOSE    SINPOCA1
          CLOSE    SINPOFA1
          RETURN
.
.----------------------------------------------------------------------
. Fix apsordaf for a purchase order
.----------------------------------------------------------------------
.
FAPORD00  MOVE     SP7,PURCH
          DISPLAY   *P1:4,*EF:
                    *P1:10,"P/O Number : ";
.
          KEYIN    *EL,*P14:10,*JR,*DE,PURCH
          RESET    PURCH
          GOTO     FAPORD99 IF EOS
.
          MATCH    SP7,PURCH
          GOTO     FAPORD99 IF EQUAL
.
          CALL     CONTQST
          SUB      ONE,CEXIT
          BRANCH   CEXIT,FAPORD00,FAPORD99
.
          OPEN     APSCIOA1,"apscioaf"
          OPEN     APSORDA1,"apsordaf"
          OPEN     SINCGAA1,"sincgaaf"
          OPEN     SINCIAA1,"sinciaaf"
          OPEN     SINFACA1,"sinfacaf"
          OPEN     SINPOAA1,"sinpoaaf"
          OPEN     SINPOCA1,"sinpocaf"
          OPEN     SINPOFA1,"sinpofaf"
          OPEN     SINSUAA1,"sinsuaaf"
          OPEN     SINWARA1,"sinwaraf"
          OPEN     SINCCAA1,"sinccaaf"
          OPEN     SINLTTA1,"sinlttaf"
.
          MOVE      "fmsamaaf",KEY8
          PACK      KEY80,HACCDATA,KEY8
          SQUEEZE   KEY80
          OPEN      FMSAMAA1,KEY80
.
          CALL      ORDP0000
.
FAPORD99  RETURN
.
.**********************************************************************
. read through receivals
.**********************************************************************
.
ORDP0000  DISPLAY   *P1:24,*EL,"Scanning : "
          CALL      RCON0000
          PACK      KEY12,PURCH,SP70
          CALL      RSSIPF1
.
ORDP1000  CALL      RKSIPF1            * Loop Through Each Item Received
          BRANCH    OVRCD,ORDP9999     * End of Receival
.
          ADD       ONE,SECTOR
          DISPLAY   *P40:24,SIPFPON,"-",SIPFITM,*EL;
.
          MATCH     PURCH,SIPFPON
          GOTO      ORDP9999 IF NOT EQUAL
.
          PACK      KEY7,SIPFPON,SP70
          CALL      RDSIPA1
          BRANCH    OVRCD,ORDP1000
.
          PACK      KEY10,SIPFPON,SIPFITM,SP70
          CALL      RDSIPC1
          BRANCH    OVRCD,ORDP1000
          COMPARE   SIPCRQT,SIPCIQT    * fully invoiced
          GOTO      ORDP1000 IF NOT LESS
.
          MOVE      HORDDES,APODISC  * System Prefix
          MOVE      SIPFPON,APODORD
          PACK      APODCAT,SIPFITM,SIPFREC,SP70
          PACK      KEY14,APODISC,APODORD,APODCAT,SP70
          CALL      DEAPOD1
.
ORDP2000  MATCH     SP70,SIPCCAT
          GOTO      ORDP5000 IF EQUAL  * Item Not Cataloged
.
          MOVE      SIPCCAT,KEY7
          CALL      RDSIIA1            * Get Catalog Details
          BRANCH    OVRCD,ORDP5000
.
          MOVE      ONE,SIFACTOR
          PACK      KEY30,SIIAISS,SIPFSUT
          CALL      RDSIFA1            * Determine Total Received Issue Units
.
          BRANCH    SIIANON,ORDP5000   * Item Not Stocked
.
          IF        HACCNT=1
            CALL      USAPS000         * Update Accounts Payable Details
          ENDIF
          GOTO      ORDP1000           * Next
.
. Non-Stock
.----------
ORDP5000  IF        HACCNT=1
            CALL      UNAPS000         * Update Accounts Payable Details
          ENDIF
          GOTO      ORDP1000
.
ORDP9999 RETURN
.
          INC      STDGENCD
.
          INC      FMSCONIO/INC
          INC      APSCIOIO/INC
          INC      APSORDIO/INC
          INC      SINWARIO/INC
          INC      SINLTTIO/INC
          INC      APSMASIO/INC
          INC      APSSUBIO/INC
          INC      SINCCAIO/INC
          INC      SINCCBIO/INC
          INC      SINCCEIO/INC   
          INC      SINCCFIO/INC  
          INC      SINCCHIO/INC 
          INC      SINCGAIO/INC
          INC      SINCGBIO/INC   
          INC      SINCIAIO/INC  
          INC      SINCIBIO/INC  
          INC      SINCICIO/INC
          INC      SINCIDIO/INC  
          INC      SINCIEIO/INC
          INC      SINCIFIO/INC
          INC      SINCIGIO/INC  
          INC      SINCIHIO/INC
          INC      SINFACIO/INC 
          INC      SINIMPIO/INC 
          INC      SINITMIO/INC 
          INC      SINKCTIO/INC   
          INC      SINKCTPR     
          INC      SINPERIO/INC  
          INC      SINPERCD
          INC      SINPOAIO/INC   
          INC      SINPOCIO/INC   
          INC      SINPOFIO/INC   
.
          INC      SINREAIO/INC
          INC      SINREBIO/INC 
          INC      SINSUAIO/INC  
          INC      SINSUBIO/INC   
          INC      SINTRAIO/INC   
          INC      SINTRBIO/INC   
          INC      FMSAMAIO/INC   
.
          INC      FIXUSAGE
          INC      SINRECUS
          INC      SINORDUS
