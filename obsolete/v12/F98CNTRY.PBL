.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   F98CNTRY                                                  *
.* Desc      :   Load country national HDP equivalents from country.dat    *
.***************************************************************************
.* Date      :   03/05/2007                                                *
.* Author    :   Jill Habasque                                             *
.* Function  :   This program will loop through Category C codes and use   *
.*               the existing value in THCSCOD to read country.dat to find *
.*               the new national HCP equivalent value                     *
.* Modifications :                                                         *
.* V10.06.01     04/05/2015 Jill Parkinson CAR 315241                      *
.*               Fixd length of country file definition                    *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATCODFD/INC
.
COUNTRY1  IFILE     SQL, FIXED=9, KEY="U5-8"
.
NEWCCODE  DIM       4
OLDCCODE  DIM       4
.
LANGUAG1  IFILE     SQL, FIXED=8, KEY="U1-2"
.
OLDLCODE  DIM       2
NEWLCODE  DIM       4
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
PRGID     INIT      "F98CNTRY"
PRGNAM    INIT      "Load Country of Birth HCP    "
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML1000  * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    IF        COPTION=1
            CALL      PROC0000               * process country fix
          ENDIF
          IF        COPTION=2
            CALL      LANG0000               * process language fix
          ENDIF
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PATCODE1,"patcodes"
          OPEN      COUNTRY1,"country"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Country fix":
                    *P1:6,*V2LON,TWO,*HOFF,". Run Language fix"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  PACK      KEY5,ANSC,SP70
          CALL      RDSCODE1
PROC1000  CALL      RDKCODE1
          BRANCH    OVRCD,PROC9999
.
          MATCH     "C ",TCODE
          GOTO      PROC9999 IF NOT EQUAL
.
          MATCH     SP70,THCSCOD
          GOTO      PROC1000 IF EQUAL
.
          PACK      PTCDNHCP,THCSCOD
          PACK      KEY4,THCSCOD,SP70
          RESET     KEY4
          READ      COUNTRY1,KEY4;NEWCCODE,OLDCCODE
          IF        !@OVER
            PACK      PTCDNHCP,NEWCCODE
          ENDIF
.
          CALL      UPCODE1
          GOTO      PROC1000
.
PROC9999  RETURN
+
.**************************************************************************
.*                               LANG0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
LANG0000  OPEN      LANGUAG1,"language"
          PACK      KEY5,ANSL,ANSA,SP70
          CALL      RDSCODE1
LANG1000  CALL      RDKCODE1
          BRANCH    OVRCD,LANG9999
.
          MATCH     "LA",TCODE
          GOTO      LANG9999 IF NOT EQUAL
.
          MATCH     SP70,THCSCOD
          GOTO      LANG1000 IF EQUAL
.
          PACK      PTCDNHCP,THCSCOD
          PACK      KEY2,THCSCOD,SP70
          RESET     KEY2
          READ      LANGUAG1,KEY2;OLDLCODE,NEWLCODE
          IF        !@OVER
            PACK      PTCDNHCP,NEWLCODE
          ENDIF
.
          CALL      UPCODE1
          GOTO      LANG1000
.
LANG9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATCODIO/INC
