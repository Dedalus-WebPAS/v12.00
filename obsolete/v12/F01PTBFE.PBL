.*****************************************************************************
.*    Program      : F01PTBFE.PBL                                            *
.*    Description  : Conversion patbfeef to new File layout                  *
.*                                                                           *
.*    Author       : Peter Vela                                              *
.*    Date         : 07/12/2010                                              *
.*    Modifications:                                                         *
.*        V10.03.01 25/02/2013 Ania  CAR 281021                              *
.*                  Removed unnecessary modification of constants.           *
.*        V10.02.01 Peter Vela       CAR 233034                              *
.*                  Added Duplicate Record header display line.              *
.*        V10.01.05 J.Tan            CAR 233034                              *
.*                  Added option 3 for Oracle                                *
.*        V10.01.04 J.Tan            CAR 233034                              *
.*                  Mods to copy bed fees file to v10bfeef                   *
.*        V10.01.03 J.Tan            CAR 233034                              *
.*                  Mods to check for blank admission type                   *
.*        V10.01.02 Peter Vela       CAR 233034                              *
.*                  Fixed REPTLNNO count                                     *
.*        V10.01.01 Peter Vela       CAR 233034                              *
.*                  Removed save old file option from load contract ids      *
.*        V10.01.00 Peter Vela       CAR 233034                              *
.*                  Added report for no mapping file record                  *
.*                  Peter Vela       CAR 233034                              *
.*                  Reread Bed Fee file after update                         *
.*                  Peter Vela       CAR 233034                              *
.*                  Added Second Option to load contract ids back to patbfee *
.*                  Peter Vela       CAR 233034                              *
.*                  Created program                                          *
.*****************************************************************************
.
.  Global change F10PTELG.PBL  eg F57PTDOC
.  Global change PATELGFD  eg PATDOCFD (new layout)
.  Global change patelgaf  eg patdoctf
.  Global change PATELGA1  eg PATDOCT1
.  Global change KEY8      eg KEY8
.  Global change WRPTELG1  eg WRDOCT1
.  Global change sptelgaf  eg sptdoctf
.
.  vi Global change command  :%s/FromString/ToString/g
.
.
          INC       STD001FD
.
          INC       PATFN1FD/INC
          INC       PATMBFFD/INC
          INC       PATCONFD/INC
.
FINDFILE  FILE      ASCII, FIXED=256
.
. Enter the first or the shortest index from the original FD and rename
. the index OLDPTBFE
.
.                                     ******** copy the old FD here *******
.                                     * comment out all continuing fields and
.                                     * prefix changing fields with "OLD" -
.                                     * remove old Indexes and Redefines     
.   
OLDPTBFE   IFILE    SQL, FIXED=97, KEY="U1-3,4-9,10-17,18-20,21-23,24-26"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.BFCLM      DIM      3            3        1       CLM CODE (CODES FILE CAT. CL)
.BFHFUND    DIM      6            6        4       HEALTH FUND (PATFNDH FILE)
BFHFTAB    DIM      8            8        10      HEALTH FUND TABLE (PATFNDH)
.BFATYPE    DIM      3            3        18      ADMISSION TYPE (CODES CAT. A)
.BFBTYPE    DIM      3            3        21      BED TYPE (CODES CAT. BT)
.DBFENDDY   DIM      3            3        24      HIGHEST DAY IN PERIOD
.BFPAT      FORM  12.2            8        27      PATIENT PORTION OF RATE
.BFHF       FORM  12.2            8        35      REBATE PORTION OF RATE
.BFNINV     FORM     1            2        43      No OF INVOICES (1 OR 2 ONLY)
.BFDESC     DIM     30           30        45      DESCRIPTION
BFHDP      DIM      4            4        75      **** NOT USED ANYMORE ****
.BFSPARE    DIM     18           18        79      SPARE VARIABLES
.
.End of Record                            97
.
.  redefine form fields from key
.  -----------------------------
. NAME     TYPE      LENGTH     START LOC.   DESCRIPTION
. ----     ----      ------     ----------   -----------
BFENDDY    FORM        3                     HIGHEST DAY IN PERIOD

.
.                                     ******** copy the new FD here *******
.
PATBFEE1   IFILE    SQL, FIXED=97, KEY="U1-3,4-9,10-12,13-15,16-18,70-75,19-21"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
BFCLM      DIM      3            3        1       CLM CODE (CODES FILE CAT. CL)
BFHFUND    DIM      6            6        4       HEALTH FUND (PATFNDH FILE)
PTBFHTYP   DIM      3            3        10      Health Fund Table Type(Cat.HT)
BFATYPE    DIM      3            3        13      ADMISSION TYPE (CODES CAT. A)
BFBTYPE    DIM      3            3        16      BED TYPE (CODES CAT. BT)
DBFENDDY   DIM      3            3        19      HIGHEST DAY IN PERIOD
BFPAT      FORM  12.2            8        22      PATIENT PORTION OF RATE
BFHF       FORM  12.2            8        30      REBATE PORTION OF RATE
BFNINV     FORM     1            2        38      No OF INVOICES (1 OR 2 ONLY)
BFDESC     DIM     30           30        40      DESCRIPTION
PTBFCNID   DIM      6            6        70      Contract ID
BFSPARE    DIM     21           21        76      SPARE VARIABLES
.
.End of Record                            97
.
TEMPFILE   IFILE    SQL, FIXED=69, KEY="U1-3,4-9,10-12"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
TMPBCLAM  DIM       3      3        1     Claim code
TMPBHFND  DIM       6      6        4     Health Fund
TMPBATYP  DIM       3      3        10    Admission Type
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       100
.
DEFPATH   DIM       60
DIM60     DIM       60
.
FNAME1    INIT      "f01ptb"
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
REPTLNNO  FORM      2
.
TMPFILNM  DIM       8
.
. 
. PROGRAM CONSTANTS
. -----------------
SP60      INIT    "                                                            "
.
.
.
PRGNAM    INIT      "CONVERSION PATBFEFD"
PRGID     INIT      "F01PTBFE"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                         MAIN0000                                          *
.*                      Mainline code                                        *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      CHKMF000                * Check Mapping File parameter
          BRANCH    EXIT,MAIN9999
.
          CALL      OPTN0000                * Choose main option
          BRANCH    EXIT,MAIN9999           * EXIT
          BRANCH    OPTION,MAIN0500:
                           MAIN4000:
                           MAIN4000
.
MAIN0500  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN1000,MAIN0500,MAIN9999 * Yes, no, cancel
.
MAIN1000  CALL      CPFL0000                      * Copy patbfeef to v10bfeef
          BRANCH    EXIT,MAIN9999
.
          CALL      CBFE0000                      * Create new bed fees file
          GOTO      MAIN9999
.
MAIN4000  OPEN      PATMBFA1,"patmbfaf"
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000,MAIN4000,MAIN9999 * Yes, no, cancel
.
MAIN5000  CALL      CTMP0000           * create tempfile
          CALL      CLER0000           * empty it if Oracle
          CALL      READ0000           * read old records and write
          BRANCH    EXIT,MAIN9999
.
          CALL      REAC0000           * read new records and update contract id
          CALL      PRLR0000           * print error file
          CALL      ENDP0000           * end of processing
          CALL      KILL0000
.
          GOTO      MAIN9999
.
MAIN9999  CHAIN     PGM
+
.*****************************************************************************
.*                          INIT0000               Called by: MAIN0000       *
.*  Display heading and check that the files needed exist & open them        *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
.
          OPEN      PATFN1A1,"patfn1af"           * Open Health Fund File
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND18;*116,PTCNMFFL
.          
INIT9999  RETURN
+
.*****************************************************************************
.*                          KDIR0000               Called by: MAIN0000       *
.*                       Keyin directories                                   *
.*****************************************************************************
KDIR0000  EXECUTE   "ldat patbfeef.dat > temp.txt",TASKID
          CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
.
KDIR1000  MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
.
          KEYIN     *P1:12,*EL,"Keyin path for saving v10bfeef file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
          ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.*****************************************************************************
.*                          CPFL0000               Called by: MAIN0000       *
.*                       Copy patbfeef to v10bfeef                           *
.*****************************************************************************
CPFL0000  CALL      CHEK0000             * old file found ok ?
          BRANCH    EXIT,CPFL9000        * no - finish
.
          CLOSE     OLDPTBFE
          DISPLAY   *P1:24,*EL,"Copying file ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat patbfeef.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "v10bfeef.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      CPFL8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat patbfeef.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "v10bfeef.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      CPFL8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "patbfeef",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      CPFL9000
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      CPFL9999
.
CPFL8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
CPFL9000  MOVE      ONE,EXIT
.
CPFL9999  RETURN
+
.*****************************************************************************
.         Create the new file
.*****************************************************************************
CBFE0000
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "patbfeef",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.      eg    APPEND    " 195 UC9-16",CMDLINE
.
          APPEND    " 97 UC1-3,4-9,10-12,13-15,16-18,70-75,19-21",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has 
.  to be repeated with for each individual index
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC1-3,4-9,13-15,16-18,10-12,70-75,19-21",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC70-75,1-3,4-9,10-12,13-15,16-18,19-21",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      PATBFEE1,NEWFILE
.
CBFE9999  RETURN
+
.*****************************************************************************
.         Opening v10bfeef file after being copied
.*****************************************************************************
OBFE0000  CLEAR     DIM60
          APPEND    "v10bfeef",DIM60
          APPEND    SP70,DIM60
          RESET     DIM60
.
          COMPARE   THREE,OPTION
          GOTO      OBFE2000 IF EQUAL       * Oracle
.
          EXECUTE   "ldat v10bfeef.dat > temp.txt",TASKID
          CALL      DEFT0000                * Get the default path
.
          BRANCH    EXIT,OBFE9000
          MOVE      DEFPATH,OLDPATH
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "v10bfeef",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
OBFE2000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDPTBFE,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open v10bfeef file.  ";
            CALL      HITENTER
            GOTO      OBFE9999
          ENDIF
          GOTO      OBFE9999
.
OBFE9000  MOVE      ONE,OVRCD
OBFE9999  RETURN
+
.*****************************************************************************
.*                               READ0000          Called by: MAIN0000       *
.*        Loop thru old file (v10bfeef) and re-format to new file layout     *
.*****************************************************************************
READ0000  DISPLAY   *P1:24,*EL,"Opening V10bfeef";
.
          CALL      OBFE0000                      * Open v10bfeef file
          BRANCH    OVRCD,READ9000
.
          DISPLAY   *P1:24,*EL;
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATBFEE1,"patbfeef"
          TRAPCLR   IO
          BRANCH    OVRCD,READ8000       * new file does not exist
.
          MOVE      ZERO,CLNO
          CALL      PRHD0000
.
          DISPLAY   *P1:20,*EL,"Record No. : ";
          PACK      KEY26,SP70
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
.  All necessary changes to the files field lengths, date changes
.  or any other sort of modification must be made here
.
.    eg    PACK      DSPARE,SP50
.
          MOVE      SP70,PTBFHTYP
          MOVE      SP70,PTBFCNID
          MOVE      SP70,BFSPARE
          MOVE      SP70,KEY27
.
          PACK      KEY14,BFHFUND,BFHFTAB,SP70
          MATCH     SP70,KEY14
          GOTO      READ2000 IF EQUAL
.
          CALL      RDFUND1
          BRANCH    OVRCD,READ3500
.
          MOVE      PTHFBCAT,PTBFHTYP
.
READ2000  PACK      KEY27,BFCLM,BFHFUND,PTBFHTYP,BFATYPE,BFBTYPE,PTBFCNID,DBFENDDY,SP70
          CALL      RDABFEE1
          BRANCH    OVRCD,READ4000
.
READ3000  CALL      PRLN0000                   * Print duplicate record
          GOTO      READ1000
.
READ3500  CALL      PRLD0000                   * Print missing record
          GOTO      READ1000
.                                               * Pack key here
READ4000  CALL      WRBFEE1                     * write to new file
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     OLDPTBFE                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
          MOVE      ZERO,EXIT
          GOTO      READ9999
.
READ8000  DISPLAY   *P1:23,*EL,"New bed fees file does not exist"
          CALL      HITENTER
.
READ9000  DISPLAY   *P1:24,*EL,"Can not find v10bfeef ";
          CALL      HITENTER
          MOVE      ONE,EXIT
READ9999  RETURN
+
.**********************************************************************
.         Print duplicate record
.**********************************************************************
PRLN0000  COMPARE    "50",CLNO
          CALL       PRHD0000 IF NOT LESS
.
          PRINT      *1,BFCLM,*5,BFHFUND,"/",BFHFTAB,*20,PTBFHTYP,*24,BFATYPE:
                     *28,BFBTYPE,*32,DBFENDDY,*36,BFPAT,*52,BFHF:
                     *67,BFNINV,*70,BFDESC
          ADD        ONE,CLNO
PRLN9999  RETURN
+
.**********************************************************************
.         Print missing record
.**********************************************************************
PRLD0000  COMPARE    "50",CLNO
          CALL       PRHD0000 IF NOT LESS
.
          PRINT      *1,"Health Fund/Table not found in patfn1af: ":
                     *50,BFHFUND,"/",BFHFTAB
          ADD        ONE,CLNO
PRLD9999  RETURN
+
.**********************************************************************
.         Print Heading
.**********************************************************************
PRHD0000  MOVE       ZERO,CLNO
.
          CALL       HEAD132
          PRINT      *1,"DUPLICATE RECORDS (SAME TABLE TYPE) DISPLAYED ON ":
                        "THIS REPORT WILL NOT BE WRITTEN TO THE BED FEE FILE.":
                     *N
          PRINT      *1,"Clm",*5,"Hlth Fnd/Tab",*20,"Typ",*24,"Adm":
                     *28,"Bed",*32,"HDay",*44,"PatP",*62,"Rebt":
                     *67,"In",*70,"Description"
          CALL       UND132
          ADD        THREE,CLNO
PRHD9999  RETURN
+
.*****************************************************************************
.*                               VALD0000          Called by: KDIR0000       *
.*                   Check if it a valid directory                           *
.*****************************************************************************
.
VALD0000  SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9100
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9100
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      VALD9999
.
VALD9100  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.*****************************************************************************
.*                               ENDP0000          Called by: MAIN0000       *
.*                    End of processing                                      *
.*****************************************************************************
ENDP0000  COMPARE   THREE,OPTION
          GOTO      ENDP8000 IF EQUAL    * Oracle
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "v10bfeef*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "compress -f ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          DISPLAY   *P1:24,*EL,"Compressing v10bfeef file.. ":
                    "Please wait !!  ";
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"v10bfeef file not compressed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
ENDP8000  DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.*****************************************************************************
.*                               DEFT0000          Called by: KDIR0000       *
.*                      Get the default directory                            *
.*****************************************************************************
.
DEFT0000  PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding patbfeef.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDPTBFE,"patbfeef"
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDPTBFE
          GOTO      CHEK9000
.
CHEK2000  DISPLAY   *P1:23,*EF,"File 'patbfeef' ";
          SCAN      "I * I",DIM60
          IF        @EQUAL
            DISPLAY   "does not exist - ";
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
            ELSE
              DISPLAY   "has caused an IO error - ";
            ENDIF
          ENDIF
          DISPLAY   "File will be bypassed":
                    *P1:24;
          CALL      HITENTER
.
          MOVE      ONE,EXIT
          GOTO      CHEK9999
.
CHEK9000  MOVE      ZERO,EXIT
.
CHEK9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: MAIN0000 *
.*                                Select Option                               *
.******************************************************************************
.
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          HLINE     *G37,2,57,80
          DISPLAY   *P40:4,*EF:
                    *P1:3,*V2LON,"0",*HOFF," = Exit":
                    *P1:4,*V2LON,"1",*HOFF:
                    " = Copy Current Bed fees to v10bfeef (CISAM only)":
                    *P1:5,*V2LON,"2",*HOFF:
                    " = Load the New Bed Fees file with Contract ID ":
                    "from Mapping file (CISAM)":
                    *P1:6,*V2LON,"3",*HOFF:
                    " = Load the New Bed Fees file with Contract ID ":
                    "from Mapping file (ORACLE)":
                    *P1:8,"Select Option : ";
.
OPTN1000  KEYIN     *P17:8,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000:
                           OPTN9000:
                           OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               REAC0000          Called by: MAIN0000       *
.*             Loop thru new file and load Contract ID from Mapping file     *
.*****************************************************************************
REAC0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          PACK      KEY27,SP70
          CALL      RDSBFEE1                      * position at start
REAC1000  CALL      RDKBFEE1                      * read next record
          BRANCH    OVRCD,REAC6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
          PACK      KEY27,BFCLM,BFHFUND,PTBFHTYP,BFATYPE,BFBTYPE:
                          PTBFCNID,DBFENDDY,SP70
.
          PACK      BFCLM,BFCLM,SP70
          PACK      BFHFUND,BFHFUND,SP70
          PACK      BFATYPE,BFATYPE,SP70
.
          PACK      KEY12,BFCLM,BFHFUND,BFATYPE,SP70
          CALL      RDPTMBF1
          BRANCH    OVRCD,REAC2000
.
          MATCH     SP70,PTMBCNTR
          GOTO      REAC5000 IF NOT EQUAL
.
.         try with blank admission type
.
REAC2000  PACK      KEY12,BFCLM,BFHFUND,SP70
          CALL      RDPTMBF1
          BRANCH    OVRCD,REAC4000
.
          MATCH     SP70,PTMBCNTR
          GOTO      REAC5000 IF NOT EQUAL
.
REAC4000  UNPACK    KEY12,BFCLM,BFHFUND,BFATYPE
          CALL      RATEMP1             
          IF        OVRCD=1
            MOVE      BFCLM,TMPBCLAM
            MOVE      BFHFUND,TMPBHFND
            MOVE      BFATYPE,TMPBATYP
            CALL      WRTEMP1
          ENDIF
          GOTO      REAC1000
.
REAC5000  MOVE      PTMBCNTR,PTBFCNID
.
          CALL      UPBFEE1
          CALL      RDSBFEE1                      * reposition
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      REAC1000                      * get next record
.
REAC6000  CLOSE     PATBFEE1                      * close new file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
REAC9999  RETURN
+
.******************************************************************************
.*                             CTMP0000                 Called by:            *
.*                        create temporary file                               *
.******************************************************************************
CTMP0000  PACK      TMPFILNM,FNAME1,PORT
          REP       " 0",TMPFILNM
.
          CALL      KILL0000
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TMPFILNM,CMDLINE
          APPEND    " 69 U1-3,4-9,10-12",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,TMPFILNM
.
CTMP9999  RETURN
+
.******************************************************************************
.*                             KILL0000                  Called by:           *
.*                   erase temporary file                                     *
.******************************************************************************
KILL0000  CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TMPFILNM,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.******************************************************************************
.*                             CLER0000                 Called by:            *
.*               close and erase the temporary file                           *
.******************************************************************************
.
CLER0000  PACK      KEY12,SP70
          CALL      RSTEMP1
          CALL      RKTEMP1
          BRANCH    OVRCD,CLER9999
.
          PACK      KEY12,TMPBCLAM,TMPBHFND,TMPBATYP,SP70
          CALL      DETEMP1
.
          GOTO      CLER0000
.
CLER9999  RETURN
+
.******************************************************************************
.*                             PRLR0000                 Called by:            *
.******************************************************************************
PRLR0000  MOVE      ZERO,REPTLNNO
          CALL      PRH20000
.
          PACK      KEY12,SP70
          CALL      RSTEMP1
PRLR1000  CALL      RKTEMP1
          BRANCH    OVRCD,PRLR9999
.
          CALL      PRLT0000
.
          GOTO      PRLR1000
.
PRLR9999  RETURN
.**********************************************************************
.         Print duplicate record
.**********************************************************************
PRLT0000  COMPARE    "50",REPTLNNO
          CALL       PRH20000 IF NOT LESS
.
          PRINT      *1,TMPBCLAM,*15,TMPBHFND,*30,TMPBATYP
          ADD        ONE,REPTLNNO
.
PRLT9999  RETURN
+
.**********************************************************************
.         Print Heading
.**********************************************************************
PRH20000  MOVE       ZERO,REPTLNNO
.
          CALL       HEAD132
          PRINT      *1,"BED FEE MAPPING RECORDS NOT FOUND:",*N
          PRINT      *1,"Claim Code",*15,"Health Fund",*30,"Admission Type"
.
          CALL       UND132
          ADD        THREE,REPTLNNO
PRH29999  RETURN
+
.**********************************************************************
.         Check Mapping File Parameter before proceeding
.**********************************************************************
CHKMF000  MATCH      "1",PTCNMFFL
          GOTO       CHKMF500 IF NOT EQUAL
          GOTO       CHKMF900      
.
CHKMF500  DISPLAY   *P1:24,*EL,"Error: Records not found or missing contract ids in bed fee mapping file.";
          CALL      HITENTER
.
          MOVE       ONE,EXIT
          GOTO       CHKMF999
.
CHKMF900  MOVE       ZERO,EXIT
CHKMF999  RETURN
+
.==============================================================================
.                Tempfile I/O
.==============================================================================
RSTEMP1   RESET     KEY12
          READ      TEMPFILE,KEY12;;
          RETURN
.
RATEMP1   RESET     KEY12
          MOVE      ZERO,OVRCD
          READ      TEMPFILE,KEY12;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;TMPBCLAM,TMPBHFND,TMPBATYP
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTEMP1   MOVE      ZERO,OVRCD
          READKP    TEMPFILE;TMPBCLAM,TMPBHFND,TMPBATYP
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY12
          READ      TEMPFILE,KEY12;TMPBCLAM,TMPBHFND,TMPBATYP
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY12
          WRITE     TEMPFILE,KEY12;TMPBCLAM,TMPBHFND,TMPBATYP
          RETURN
.
DETEMP1   RESET     KEY12
          DELETE    TEMPFILE,KEY12
          RETURN
.
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
.                  ******************************    OLD IO ROUTINES
.                  Copy the Positional Read (RS or RDS) from original I/O file,
.                  - change Label to READSOLD and the index name to OLDPTBFE
.
READSOLD  RESET     KEY26
          READ      OLDPTBFE,KEY26;;
          RETURN    
.  
.                  Copy the Read Next (RK or RDK) from the original I/O file
.                  - change Label to READKOLD and the index name to OLDPTBFE
.                  - change any fields that are changing to a prefix of OLD,
.                    eg. PTEDSPAR becomes OLDDSPAR
.
READKOLD
         MOVE      ZERO,OVRCD
         READKS    OLDPTBFE;BFCLM,BFHFUND,BFHFTAB,BFATYPE,BFBTYPE:
                                  DBFENDDY,BFPAT,BFHF,BFNINV,BFDESC:
                                  BFHDP,BFSPARE
         GOTO      OVERCOND IF OVER
         MOVE      DBFENDDY,BFENDDY
         RETURN
.
.                  ******************************    NEW IO ROUTINES
.                  Copy the Write statement from the new I/O file           
.                  (no changes are needed to this routine)               
.
RDBFEE1  RESET     KEY27
         MOVE      ZERO,OVRCD
         READ      PATBFEE1,KEY27;BFCLM,BFHFUND,PTBFHTYP,BFATYPE,BFBTYPE:
                                  DBFENDDY,BFPAT,BFHF,BFNINV,BFDESC:
                                  PTBFCNID,BFSPARE
         GOTO      OVERCOND IF OVER
         MOVE      DBFENDDY,BFENDDY
         RETURN
.
RDABFEE1 RESET     KEY27
         MOVE      ZERO,OVRCD
         READ      PATBFEE1,KEY27;ANS
         GOTO      OVERCOND IF OVER
         RETURN
.
WRBFEE1  RESET     KEY27
         WRITE     PATBFEE1,KEY27;BFCLM,BFHFUND,PTBFHTYP,BFATYPE,BFBTYPE:
                                  DBFENDDY,BFPAT,BFHF,BFNINV,BFDESC:
                                  PTBFCNID,BFSPARE
         GOTO      OVERCOND IF OVER
         RETURN
.
UPBFEE1  MOVE      BFENDDY,DBFENDDY
         UPDATE    PATBFEE1;BFCLM,BFHFUND,PTBFHTYP,BFATYPE,BFBTYPE:
                                  DBFENDDY,BFPAT,BFHF,BFNINV,BFDESC:
                                  PTBFCNID,BFSPARE
         RETURN
.
RDSBFEE1 RESET     KEY27
         READ      PATBFEE1,KEY27;;
         RETURN
.
RDKBFEE1 MOVE      ZERO,OVRCD
         READKS    PATBFEE1;BFCLM,BFHFUND,PTBFHTYP,BFATYPE,BFBTYPE:
                                  DBFENDDY,BFPAT,BFHF,BFNINV,BFDESC:
                                  PTBFCNID,BFSPARE
         GOTO      OVERCOND IF OVER
         MOVE      DBFENDDY,BFENDDY
         RETURN
.
         INC       PATFN1IO/INC
         INC       PATMBFIO/INC
         INC       STD001IO
