.*****************************************************************************
.*    Program      : F02PTCHC                                                *
.*    Description  : Conversion patchcof to new File layout                  *
.*                                                                           *
.*    Author       : Mike Laming                                             *
.*    Date         : 26/07/2011                                              *
.*    Modifications:                                                         *
.*        V10.03.01 08/06/2012  Steve Armstrong  CAR 266826                  *
.*                  Changed fixit to only populate CHEPISNO for both c-isam  *
.*                  & Oracle.                                                *
.*****************************************************************************
.*        V10.02.01 08/08/2011  Mike Laming   CAR 241939                     *
.*                  Change Key 2 from KEY10 to KEY26 (added CHDATE & CHTIME) *
.*****************************************************************************
.
          INC       STD001FD
.
.         New File Definition
.
PATCHCO1  IFILE    SQL, FIXED=100, KEY="U6-13,14-21,22-29"
PATCHCO2  IFILE    SQL, FIXED=100, KEY="U6-13,70-71,14-21,22-29"
.
. NAME     TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION
. ----     ----   ------   --------  ----------   -----------
CHCATG     DIM     2          2          1        Category     (Cat CC)
CHCODE     DIM     3          3          3        Code (Cat CC = Care Type)
DCHADMN    DIM     8          8          6        Admission Number
CHDATE     DIM     8          8          14       Date (Care Type End Date)
CHTIME     DIM     8          8          22       Time (Care Type End Time)
CHCRDATE   DIM     8          8          30       Date Created
CHCRTIME   DIM     8          8          38       Time Created
CHCRUSID   DIM     4          4          46       User ID Created
CHUPDATE   DIM     8          8          50       Date Last Updated
CHUPTIME   DIM     8          8          58       Time Last Updated
CHUPUSID   DIM     4          4          66       User ID Updated
CHEPISNO   DIM     2          2          70       Episode No.
CHCODCMP   DIM     8          8          72       Date Episode Coding Complete
CHSPARE    DIM     20         20         80       Spare
.
.End of Record                           100
.
. Redefined FORM fields from key
. ------------------------------
. NAME     TYPE    LENGTH  START     DESCRIPTION
. ----     ----    ------  -----     -----------
CHADMN     FORM         8      1     Admission Number
.
.
. LOCAL VARIABLES
. ---------------
RECNUM    FORM      8
PREVADMN  DIM       8
.
.
PRGNAM    INIT      "CONVERSION PATCHCFD"
PRGID     INIT      "F02PTCHC"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
.
MAIN1000  CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN2000              * run fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN2000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes             
                          MAIN1000:               * no
                          MAIN9999                * cancel
.
.         Running c-isam/oracle fix               
.
MAIN5000  CALL      FFIX0000                      * run field fix
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening ":
                    *P64:24,"patchcof"
          OPEN      PATCHCO1,"patchcof"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                            FFIX0000                                       *
.*            Oracle fix to populate CHEPISNO                                *
.*****************************************************************************
.
FFIX0000  DISPLAY   *P1:20,*EL,"Record No. :";
          MOVE      ZERO,RECNUM                   * initialise record count
.
          PACK      KEY24,SP70
          CALL      RDSCHCO1                      * position at start of file
FFIX0500  CALL      RDKCHCO1                      * read next record
          BRANCH    OVRCD,FFIX9000                * eof - finished
.
          ADD       ONE,RECNUM                    * increment record counter
.         
          IF        (RECNUM%1000) = 0 | RECNUM = 1
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          UNPACK    SP70,CHCODCMP,CHSPARE,CHEPISNO
.                                           * routine to generate Episode No.
          MATCH     PREVADMN,DCHADMN
          IF        @EQUAL
            ADD       ONE,F2
          ELSE
            MOVE      ONE,F2
            MOVE      DCHADMN,PREVADMN
          ENDIF
          MOVE      F2,CHEPISNO
.
          CALL      UPCHCO1
.
          GOTO      FFIX0500                     * get next record
.         
FFIX9000  CLOSE     PATCHCO1
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:24,*EL,"Update completed.  ";
          CALL      HITENTER
.
FFIX9999  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
.         New I/O Routines
.
RDSCHCO1  MOVE      ZERO,OVRCD
          RESET     KEY24
          READ      PATCHCO1,KEY24;;
          GOTO      OVERCOND IF OVER
          RETURN
.
RDKCHCO1  MOVE      ZERO,OVRCD
          READKS    PATCHCO1;CHCATG,CHCODE,DCHADMN,CHDATE,CHTIME:
                             CHCRDATE,CHCRTIME,CHCRUSID,CHUPDATE:
                             CHUPTIME,CHUPUSID,CHEPISNO,CHCODCMP,CHSPARE
          GOTO      OVERCOND IF OVER
          MOVE      DCHADMN,CHADMN
          RETURN
.
UPCHCO1   MOVE      CHADMN,DCHADMN
          UPDATE    PATCHCO1;CHCATG,CHCODE,DCHADMN,CHDATE,CHTIME:
                             CHCRDATE,CHCRTIME,CHCRUSID,CHUPDATE:
                             CHUPTIME,CHUPUSID,CHEPISNO,CHCODCMP,CHSPARE
          RETURN
.
          INC       STD001IO
