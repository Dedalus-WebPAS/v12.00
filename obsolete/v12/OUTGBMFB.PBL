.******************************************************************************
.* System         : Outpatients                                               *
.*                                                                            *
.* Include name   : OUTGMBFB.PBL                                              *
.*                                                                            *
.* Date           : 11/05/1995                                                *
.*                                                                            *
.* Author         : Justin Coates                                             *
.*                                                                            *
.* Function       : To get the Booking master details from the bokb details.  *
.*                                                                            *
.* Parameters     : KEY6               outpatient site                        *
.*                  KEY8               booking number                         *
.*                                                                            *
.* Returns        : KEY3               hospital code                          *
.*                  KEY18              key to outmasfd                        *
.*                                                                            *
.* Modifications  :                                                           *
.*                                                                            *
.******************************************************************************
.
          DEFPROC   OUTGBMFB
.
          INC       OUTMA1FD/INC
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
.
SITE      DIM       6                  * the site
BOOKING   FORM      8                  * the booking number
DATE      DIM       8                  * the booking date
.
GETBM000  MOVE      KEY6,SITE
          MOVE      KEY8,BOOKING
          CALL      RDSITA1
          BRANCH    OVRCD,GETBM9100         * invalid site
.
          PACK      CFNAME,OSTFILE,FILBB1A1
          CALL      OPOTBB11
          PACK      CFNAME,OSTFILE,FILBOKA1
          OPEN      OUTBOKA2,CFNAME
          PACK      CFNAME,OSTFILE,FILMA1A1
          CALL      OPOTMA11
.
.         get the bokb details
.
          CALL      RDBOKB1
          BRANCH    OVRCD,GETBM9100         * invalid booking number
.
.         get the boka details
.
          MOVE      OBADATE,DATE
          PACK      KEY35,SITE,OBBCTYP,FOUR,DATE
          CALL      RDSBOKA2
.
GETBM100  CALL      RDKBOKA2
          BRANCH    OVRCD,GETBM910          * no boka details
          MATCH     OBASITE,SITE
          GOTO      GETBM910 IF NOT EQUAL   * no boka details
          MATCH     OBACTYP,OBBCTYP
          GOTO      GETBM910 IF NOT EQUAL   * no boka details
          MATCH     DATE,OBADATE
          GOTO      GETBM910 IF NOT EQUAL   * no boka details
.
          COMPARE   BOOKING,OBAOUTNO
          GOTO      GETBM100 IF NOT EQUAL   * different booking number
.
.         get the masa details
.
          PACK      KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT
          CALL      RDMASA1
          BRANCH    OVRCD,GETBM9100         * invalid clinic master
.
          MOVE      OTMAHOSP,KEY3           * return the hospital code
          MOVE      ZERO,EXIT
          GOTO      GETBM999
.
GETBM910  MOVE      ONE,EXIT
          GOTO      GETBM999
.
          INC       IBAOVRIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTMA1IO/INC
.
GETBM999  ENDPROC
.........................................................................
