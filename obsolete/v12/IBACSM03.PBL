.-------------------------------------------------------
          INC       STDGENDF     * Required Include
.
VERSION   INIT      "V12.00.00"
.-------------------------------------------------------
. V9.02.00  03.05.2002  Glenn Saunders
.           Export to new release i.e. v9.02 from vf.09
.-------------------------------------------------------
. VF.00.02  05.01.2000 Charles Handaya
.           Recompiled for APSMASIO change
. VF.00.01  02.11.1999 Sandra Barcham
.           Change locked message to use X for exit
.-------------------------------------------------------
CATDES60  DIM       60
SAVKEY22  DIM       22
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
LOCKCNT   FORM     2
OPENSIN   FORM     1     * Files Open Indicator
.
STRDBS    DIM       3
ENDDBS    DIM       3
.-------------------------------------------------------
.@@@@@ Start of Generated Code @@@@@
. NB - Code & Comments Included Before the above line 
.      will not be overwritten by the generation of a 
.      program. The STDGENDF file and VERSION must appear
.      before the line. 
.-----------------------------------------
.   Program       :   IBACSM03
.   Function      :   Corporate Database Update               
.   Generated  On :   13/12/2005
.-----------------------------------------
.=========================
.File Description Includes
.-------------------------
          INC       APSCNTFD/INC
          INC       APSIMSFD/INC
          INC       APSMASFD/INC
          INC       APSMXRFD/INC
          INC       APSSNTFD/INC
          INC       CSMCIAFD/INC
          INC       CSMCICFD/INC
          INC       CSMCIFFD/INC
          INC       CSMCREFD/INC
          INC       CSMCUCFD/INC
          INC       CSMUIAFD/INC
          INC       CSMUICFD/INC
          INC       IBADBSFD/INC
          INC       SINCIAFD/INC
          INC       SINCICFD/INC
          INC       SINCIFFD/INC
          INC       SINCUAFD/INC
          INC       SINKCTFD/INC
          INC       SINKEXFD/INC
.=========================
.Local Variable Definition
.-------------------------
FROMDBS   DIM      40 * Scr 00 Fld     4
TODBS     DIM      40 * Scr 00 Fld     5
UPDTSUP   FORM      1 * Scr 00 Fld     6
UPDTCAT   FORM      1 * Scr 00 Fld     7
.
ACCEPT    FORM      1
SACCEPT   FORM      1
.
PRGID     INIT      "IBACSM03"
PRGNAM    INIT      "Corporate Database Update               "
.
.--------------------------------
. MAINLINE - Controlling Logic
.--------------------------------
          TOPIC     PRGID
          MOVE      PORT,PARENTID
          CLOCK     PORT,DIM2
          MOVE      DIM2,PORT
ML0000
          CALL      INIT0000
          CALL      SC00L000
          CHAIN     PGM
.---------------------------------------
. INIT - Open Files & Display Heading
.---------------------------------------
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CCENTRY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          DISPLAY   *P56:24,"Opening scrsbga1";
          OPEN      SCRSBGA1,"scrsbgaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA1,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA2,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrslta1";
          OPEN      SCRSLTA1,"scrsltaf"
          MOVE      "   63",AUDTSECT
          MOVE      "158",AUDTPOST
          READ      CONTROLF,AUDTSECT;*AUDTPOST,AUDTFLAG
          IF        AUDTFLAG=1
            DISPLAY   *P56:24,"Opening csmaudcr";
            OPEN      CSMAUDCR,"csmaudcr"
          ENDIF
          MOVE      "   63",AUDTSECT
          MOVE      "159",AUDTPOST
          READ      CONTROLF,AUDTSECT;*AUDTPOST,AUDTFLAG
          IF        AUDTFLAG=1
            DISPLAY   *P56:24,"Opening csmaudia";
            OPEN      CSMAUDIA,"csmaudia"
          ENDIF
          MOVE      "   63",AUDTSECT
          MOVE      "160",AUDTPOST
          READ      CONTROLF,AUDTSECT;*AUDTPOST,AUDTFLAG
          IF        AUDTFLAG=1
            DISPLAY   *P56:24,"Opening csmaudic";
            OPEN      CSMAUDIC,"csmaudic"
          ENDIF
          DISPLAY   *P56:24,"Opening csmciaaf";
          OPEN      CSMCIAA1,"csmciaaf"
          DISPLAY   *P56:24,"Opening csmcicaf";
          OPEN      CSMCICA1,"csmcicaf"
          DISPLAY   *P56:24,"Opening csmcicaf";
          OPEN      CSMCICA2,"csmcicaf"
          DISPLAY   *P56:24,"Opening csmcicaf";
          OPEN      CSMCICA3,"csmcicaf"
          DISPLAY   *P56:24,"Opening csmcifaf";
          OPEN      CSMCIFA1,"csmcifaf"
          DISPLAY   *P56:24,"Opening csmcreaf";
          OPEN      CSMCREA1,"csmcreaf"
          DISPLAY   *P56:24,"Opening csmcucaf";
          OPEN      CSMCUCA1,"csmcucaf"
          DISPLAY   *P56:24,"Opening csmuiaaf";
          OPEN      CSMUIAA1,"csmuiaaf"
          DISPLAY   *P56:24,"Opening csmuicaf";
          OPEN      CSMUICA1,"csmuicaf"
          DISPLAY   *P56:24,"Opening ibadbsaf";
          OPEN      IBADBSA1,"ibadbsaf"
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.
.------------------------------------
. Called for Print Screen Routine 
.------------------------------------
PRTSVARS
          MATCH     "00",SCRID
          IF         @EQUAL
            CALL       PS00L000   * Options                            
          ENDIF
          RETURN
.--------------------------------
. Screen - Options                            
.--------------------------------
SC00L000
SC00L100
          MOVE      "00",SCRID
          MOVE      ZERO,PRCOL
          MOVE      ZERO,PRVERT
          MOVE      "    1",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SCPSCOL,PRCOL
            MOVE     SCPSROW,PRVERT
          ENDIF
          CALL      SBACK000 * Display Background
.
SC00L200
          CALL      CF00L000 * Clear Fields
          CALL      SETSCR00
SC00L300
          CALL      KY00L000 * Enter Key Fields
          BRANCH    EXIT,SC00L999,SC00L999
SC00L400
          CALL      WP00L000 * Load any WP Details
SC00L500
          CALL      LM00L000 * Load any Batch Details
SC00L600
          CALL      DF00L000 * Display Fields
SC00L700
          CALL      SF00L000 * Select Fields
          BRANCH    EXIT,SC00L100,SC00L100,SC00L200
          IF        EXIT=4
            MOVE      ONE,EXIT
          ENDIF
SC00L800
SC00L900
SC00L999  RETURN
.--------------------------------------------------------
. Clear Fields for Screen : Options                            
.-------------------------------------------------------
CF00L000
          MOVE      SP70,FROMDBS 
          MOVE      ZERO,UPDTSUP 
          MOVE      ZERO,UPDTCAT 
CF00L999  RETURN
.--------------------------------------------------------
. Enter Key Fields for Options                            
.--------------------------------------------------------
KY00L000
          MOVE      ZERO,EXIT
          GOTO      KY00L999
.
KY00L900  MOVE      ONE,EXIT
KY00L999  RETURN
.---------------------------------------------------------
. Load WP files for : Options                            
.---------------------------------------------------------
WP00L000
          RETURN
.---------------------------------------------------------
. Load Batch Screen for : Options                            
.---------------------------------------------------------
LM00L000
          RETURN
.---------------------------------------------------------
. Display Fields for : Options                            
.---------------------------------------------------------
DF00L000
          BRANCH   ACCEPT,DF00L999
          PACK     KEY15,PRGID,SCRID,SP70
          CALL     RSSCPS1
DF00L010
          CALL     RKSCPS1
          BRANCH   OVRCD,DF00L900
          MATCH    PRGID,SCPSPID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SCRID,SCPSSID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SP70,SCPSMTY
          GOTO     DF00L010 IF NOT EQUAL
.
          CALL     PS00L000 * Display Field
          GOTO     DF00L010
DF00L900
DF00L999
          RETURN
.---------------------------------------------------------
. Display/Print Fields for : Options                            
.---------------------------------------------------------
PS00L000   PACK      VAR,SP70,SP70
          PACK     PRINTVAR,SP70,SP70
          MOVE     SCPSITM,FIELDNO
          BRANCH    FIELDNO,PS00L999,PS00L999,PS00L999,PS00L101:
                            PS00L102,PS00L103,PS00L104,PS00L999
          GOTO     PS00L999
.
PS00L101
          MOVE      FROMDBS,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L102
          MOVE      TODBS,VAR        
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L103
          MOVE      NO,YESNO
          MOVE      ZERO,FORM2
          MOVE      UPDTSUP,FORM2    
          LOAD      YESNO,FORM2,YES
          MOVE      YESNO,VAR
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L104
          MOVE      NO,YESNO
          MOVE      ZERO,FORM2
          MOVE      UPDTCAT,FORM2    
          LOAD      YESNO,FORM2,YES
          MOVE      YESNO,VAR
          CALL      DISPITEM
          GOTO     PS00L999
PS00L999
          RETURN
.---------------------------------------------------------
. Selections for : Options                            
.---------------------------------------------------------
SF00L000  PACK     KEY13,PRGID,SCRID,SP70
          CALL     RSSCSL1
SF00L001  BRANCH   ACCEPT,SF00L020
.
SF00L005  CALL     MTSTQ000
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L006,SF00L101,SF00L102,SF00L103:
                            SF00L006,SF00L104,SF00L105,SF00L106
SF00L006  BEEP
          GOTO     SF00L005
SF00L020  CALL     RKSCSL1
          BRANCH   OVRCD,SF00L090
          MATCH    PRGID,SCSLPID
          GOTO     SF00L090 IF NOT EQUAL
          MATCH    SCRID,SCSLSID
          GOTO     SF00L090 IF NOT EQUAL
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L020,SF00L020,SF00L020,SF00L103:
                            SF00L020,SF00L104,SF00L105,SF00L020
          GOTO     SF00L020
SF00L090  MOVE     ZERO,ACCEPT
          GOTO     SF00L001
.
SF00L101
          MOVE      ZERO,EXIT
          GOTO      SF00L999
.
SF00L102
          CALL      PROC0000
          MOVE      ZERO,EXIT
          GOTO      SF00L999
.
SF00L103
.
.
          MOVE      "    5",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,TODBS   
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      KRDBS000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L103
            ENDIF
            BRANCH    EXIT,SF00L103,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      FROMDBS,VAR      
          CALL      DISPITEM
.
.
          MOVE      "    5",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      TODBS,VAR        
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L104
          CALL     SELECTED
          CALL      YORN0000
          MOVE       FYESNO,UPDTSUP 
          MOVE      NO,YESNO
          MOVE      ZERO,FORM2
          MOVE      UPDTSUP,FORM2    
          LOAD      YESNO,FORM2,YES
          MOVE      YESNO,VAR
          CALL      DISPITEM
          GOTO     SF00L001
.
SF00L105
          CALL     SELECTED
          CALL      YORN0000
          MOVE       FYESNO,UPDTCAT 
          MOVE      NO,YESNO
          MOVE      ZERO,FORM2
          MOVE      UPDTCAT,FORM2    
          LOAD      YESNO,FORM2,YES
          MOVE      YESNO,VAR
          CALL      DISPITEM
          GOTO     SF00L001
.
SF00L106
          CALL      UPALL000
          MOVE      ZERO,EXIT
          GOTO      SF00L999
SF00L990  MOVE      FOUR,EXIT * Exit Char Exit
SF00L999  RETURN
.@@@@@ Function Restart Point @@@@@
.------------------------------------------------------------
. Setup Screen Options
.------------------------------------------------------------
SETSCR00  MOVE     ONE,UPDTCAT
          MOVE     ONE,UPDTSUP
          MOVE     SP70,STRDBS
          MOVE     Z70,ENDDBS
          MOVE     "Start",FROMDBS
          MOVE     "Finish",TODBS
          RETURN
.--------------------------------------------------
. Process Database Update
.--------------------------------------------------
PROC0000  PACK     KEY3,SP70
          CALL     RSIBDB1
PROC1000  CALL     RKIBDB1                * Update Each Database
          BRANCH   OVRCD,PROC9000
          DISPLAY  *P1:23,*EL,"Database ",IBDBDBS,SP1,IBDBDES
.
          CALL     OPNFIL00               * Open Database Files
.
          IF       UPDTSUP=1
            CALL     UPDSUP00
          ENDIF
          IF       UPDTCAT=1
            CALL     UPDCAT00
          ENDIF
.
          CALL     CLSFIL00
.
          GOTO     PROC1000
.
PROC9000  MOVE     "Update Complete - ",DISPMSG
          CALL     MESSAGE1
.
PROC9999  RETURN
.------------------------------------------------------------
. Update Creditor Supplier Details
.------------------------------------------------------------
UPDSUP00  DISPLAY  *P1:24,*EL,"Processing Suppliers ... "
          MOVE     ZERO,SECTOR
          PACK     KEY8,IBDBDBS,SP70
          CALL     RSCSCU1
UPDSUP10  CALL     RKCSCU1                * Check Creditor Requires Update
          BRANCH   OVRCD,UPDSUP99
          ADD      ONE,SECTOR
          IF       SECTOR%100=0
            DISPLAY   *P30:24,*V2LON,SECTOR
          ENDIF
.
          MOVE     CSCUCRD,KEY5
          CALL     RDCSCR1
          BRANCH   OVRCD,UPDSUP30
.
          MOVE     CSCRCRD,KEY5
          CALL     RDAPMA1
          BRANCH   OVRCD,UPDSUP20
.
          CALL     LOCSUP00
          BRANCH   EXIT,UPDSUP10
.
          CALL     APSUPD00
          GOTO     UPDSUP90
.
UPDSUP20  CALL     APSWRT00               * Write New Masterfile Record
          GOTO     UPDSUP90
.
UPDSUP30  COMPARE  TWO,CSCUTYP
          GOTO     UPDSUP95 IF NOT EQUAL
.
          CALL     APSDEL00
          BRANCH   EXIT,UPDSUP10
.
          GOTO     UPDSUP95
.
UPDSUP90  CALL     UPMXR000               * Update Cross Reference Files
.
UPDSUP95  PACK     KEY8,CSCUDBS,CSCUCRD
          CALL     DECSCU1                * Delete Update Record
          GOTO     UPDSUP10               * Next
.
UPDSUP99  RETURN
.------------------------------------------------------------
. Lock Supplier for Update
.------------------------------------------------------------
LOCSUP00  MOVE     ZERO,LOCKCNT
          MOVE     ONE,EXIT
LOCSUP10  CALL     RLAPMA1
          BRANCH   OVRCD,LOCSUP99,LOCSUP20
          MOVE     ZERO,EXIT
          GOTO     LOCSUP99
.
LOCSUP20  ADD      ONE,LOCKCNT
          COMPARE  TEN,LOCKCNT
          GOTO     LOCSUP10 IF NOT EQUAL
          CALL     RECLOC00
          BRANCH   EXIT,LOCSUP00
          MOVE     ONE,EXIT
          GOTO     LOCSUP99
LOCSUP99  RETURN
.------------------------------------------------------------
. Record Locked in Database
.------------------------------------------------------------
RECLOC00  MOVE      SP70,DISPMSG            * Clear Line 24 Message Display
          CALL      MESSAGE2                * Clear Line 24 and Save
          KEYIN     *P1:24,*EL,"Record Locked in Database ",IBDBDBS," - (":
                    *V2LON,"R",*HOFF,")etry or e(":
                    *V2LON,"X",*HOFF,")it ? ",ANS
          CALL      MESSAGE3                * Restore Line 24
          REP       UPPLOW,ANS              * Convert to Upper Case
          MATCH     ANSR,ANS                * Re-try
          GOTO      RECLOC90 IF EQUAL       * Return to locking
          MATCH     ANSX,ANS                * Exit
          GOTO      RECLOC10 IF EQUAL       * Must be a valid response
          MATCH     ANSE,ANS                * Exit
          GOTO      RECLOC00 IF NOT EQUAL   * Must be a valid response
.
RECLOC10  MOVE      TWO,EXIT
          GOTO      RECLOC99
RECLOC90  MOVE      ONE,EXIT
RECLOC99  RETURN
.------------------------------------------------------------
. Open Files for Update
.------------------------------------------------------------
OPNFIL00  MOVE     "apsmasaf",KEY8
          STRIP    IBDBDB01
          PACK     KEY50,IBDBDB01,KEY8,SP70
          STRIP    KEY50
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     APSMASA1,KEY50
          TRAPCLR  IO
          BRANCH   OVRCD,OPNFIL90
.
          MOVE     "apsmxraf",KEY8
          STRIP    IBDBDB01
          PACK     KEY50,IBDBDB01,KEY8,SP70
          STRIP    KEY50
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     APSMXRA2,KEY50
          TRAPCLR  IO
          BRANCH   OVRCD,OPNFIL90
.
          MOVE     "sincuaaf",KEY8
          STRIP    IBDBDB01
          PACK     KEY50,IBDBDB01,KEY8,SP70
          STRIP    KEY50
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     SINCUAA1,KEY50
          TRAPCLR  IO
          BRANCH   OVRCD,OPNFIL90
.
          MOVE     "sinciaaf",KEY8
          STRIP    IBDBDB01
          PACK     KEY50,IBDBDB01,KEY8,SP70
          STRIP    KEY50
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     SINCIAA1,KEY50
          TRAPCLR  IO
          BRANCH   OVRCD,OPNFIL90
.
          MOVE     "sincicaf",KEY8
          STRIP    IBDBDB01
          PACK     KEY50,IBDBDB01,KEY8,SP70
          STRIP    KEY50
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     SINCICA1,KEY50
          TRAPCLR  IO
          BRANCH   OVRCD,OPNFIL90
.
          MOVE     "sincifaf",KEY8
          STRIP    IBDBDB01
          PACK     KEY50,IBDBDB01,KEY8,SP70
          STRIP    KEY50
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     SINCIFA2,KEY50
          TRAPCLR  IO
          BRANCH   OVRCD,OPNFIL90
.
          MOVE     "sinkexaf",KEY8
          STRIP    IBDBDB01
          PACK     KEY50,IBDBDB01,KEY8,SP70
          STRIP    KEY50
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     SINKEXA1,KEY50
          TRAPCLR  IO
          BRANCH   OVRCD,OPNFIL90
.
          MOVE     "sinkctaf",KEY8
          STRIP    IBDBDB01
          PACK     KEY50,IBDBDB01,KEY8,SP70
          STRIP    KEY50
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     SINKCTA2,KEY50
          TRAPCLR  IO
          BRANCH   OVRCD,OPNFIL90
          OPEN     SINKCTA1,KEY50
.
          MOVE     ZERO,EXIT
          GOTO     OPNFIL99
.
OPNFIL90  MOVE     ONE,EXIT
.
OPNFIL99  RETURN
.
CLSFIL00  CLOSE    APSMASA1
          CLOSE    APSMXRA2
          CLOSE    APSIMSA2
          CLOSE    SINCIAA1
          CLOSE    SINCICA1
          CLOSE    SINCIFA2
          CLOSE    SINCUAA1
          CLOSE    SINKEXA1
          CLOSE    SINKCTA1
          CLOSE    SINKCTA2
CLSFIL99  RETURN
.------------------------------------------------------------
. Move in Fields ans Write New Creditor Record
.------------------------------------------------------------
APSWRT00  MOVE      CSCRCRD,APMACRD
          MOVE      CSCRTYP,APMATYP
          MOVE      CSCRSKY,APMASKY
          MOVE      CSCRCN1,APMACN1
          MOVE      CSCRCN2,APMACN2
          MOVE      CSCRCA1,APMACA1
          MOVE      CSCRCA2,APMACA2
          MOVE      CSCRCA3,APMACA3
          MOVE      CSCRCPC,APMACPC
          MOVE      CSCRCTEL,APMACTEL
          MOVE      CSCRCFAX,APMACFAX
          MOVE      CSCRCON,APMACON
.
          MOVE      CSCRCN1,APMASN1
          MOVE      CSCRCN2,APMASN2
          MOVE      CSCRCA1,APMASA1
          MOVE      CSCRCA2,APMASA2
          MOVE      CSCRCA3,APMASA3
          MOVE      CSCRCPC,APMASPC
          MOVE      CSCRCTEL,APMASTEL
          MOVE      CSCRCFAX,APMASFAX
          MOVE      CSCRCON,APMASCON
.
          MOVE      CSCRHOR,APMAHOR
          MOVE      CSCRHIN,APMAHIN
          MOVE      CSCRHCR,APMAHCR
          MOVE      CSCRHPY,APMAHPY
.
          MOVE      SP70,APMACMOD
          MOVE      SP70,APMACEDI
          MOVE      SP70,APMASMOD
          MOVE      SP70,APMASEDI
          MOVE      SP70,APMABSB
          MOVE      SP70,APMABAC
          MOVE      SP70,APMAPNM
          MOVE      ZERO,APMAPPT
          MOVE      ZERO,APMAPOT
          MOVE      ZERO,APMAPMT
          MOVE      ZERO,APMAGMT
          MOVE      SP70,APMAPPD
          MOVE      SP70,APMADTM
          MOVE      SP70,APMAUR1
          MOVE      SP70,APMAUR2
          MOVE      SP70,APMAUD1
          MOVE      SP70,APMAUD2
          MOVE      SP70,APMAUC1
          MOVE      SP70,APMAUC2
          MOVE      ZERO,APMAGREG
          MOVE      ZERO,APMAUY2
          MOVE      ZERO,APMADIS
          MOVE      SP70,APMATSE
          MOVE      SP70,APMASPA
.
          CALL      WRAPMA1             * Update Masterfile
          CALL      AUDT0100            * Update Audit
          RETURN
.------------------------------------------------------------
. Update Masterfile
.------------------------------------------------------------
APSUPD00  ASSIGN    CSCRHOR+CSCRHIN+CSCRHCR+CSCRHPY,F1
          IF        F1<>0
            CALL      AUDT0800
          ENDIF
          MATCH     CSCRCN1,APMACN1
          GOTO      APSUPD10 IF NOT EQUAL
          MATCH     CSCRCN2,APMACN2
          GOTO      APSUPD20 IF EQUAL
APSUPD10  CALL      AUDT1600
.
APSUPD20  MATCH     CSCRCN1,APMASN1
          GOTO      APSUPD30 IF NOT EQUAL
          MATCH     CSCRCN2,APMASN2
          GOTO      APSUPD40 IF EQUAL
APSUPD30  CALL      AUDT1700
.
APSUPD40  MOVE      CSCRCN1,APMACN1
          MOVE      CSCRCN2,APMACN2
          MOVE      CSCRCN1,APMASN1
          MOVE      CSCRCN2,APMASN2
          MOVE      CSCRHOR,APMAHOR
          MOVE      CSCRHIN,APMAHIN
          MOVE      CSCRHCR,APMAHCR
          MOVE      CSCRHPY,APMAHPY
          CALL      UPAPMA1             * Update Masterfile
          RETURN
.------------------------------------------------------------
. Update Cross Reference Table
.------------------------------------------------------------
UPMXR000  PACK     KEY35,APMACRD,SP70
          CALL     RSAPMX2
UPMXR100  CALL     RKAPMX2
          BRANCH   OVRCD,UPMXR200
          MATCH    APMXCRD,APMACRD
          GOTO     UPMXR200 IF NOT EQUAL
          MATCH    "1",APMXTYP
          IF       @EQUAL
            PACK     KEY35,APMXCRD,APMXREF
            CALL     DEAPMX2
          ENDIF
          MATCH    "2",APMXTYP
          IF       @EQUAL
            PACK     KEY35,APMXCRD,APMXREF
            CALL     DEAPMX2
          ENDIF
          GOTO     UPMXR100
.
UPMXR200  MOVE     APMACRD,APMXCRD
          MOVE     APMACN1,APMXREF
          MOVE     ONE,APMXTYP
          MOVE     SP70,APMXSPA
          PACK     KEY35,APMXCRD,APMXREF
          MATCH    SP70,APMXREF
          GOTO     UPMXR300 IF EQUAL
          CALL     RAAPMX2
          IF       OVRCD=1
            CALL     WRAPMX2
          ENDIF
.
UPMXR300  MOVE     APMACRD,APMXCRD
          MOVE     APMASN1,APMXREF
          MOVE     TWO,APMXTYP
          MOVE     SP70,APMXSPA
          PACK     KEY35,APMXCRD,APMXREF
          MATCH    SP70,APMXREF
          GOTO     UPMXR999 IF EQUAL
          CALL     RAAPMX2
          IF       OVRCD=1
            CALL     WRAPMX2
          ENDIF
.
UPMXR999  RETURN
.
.------------------------------------------------------------
. Update Catalogs in Database
.------------------------------------------------------------
UPDCAT00  DISPLAY  *P1:24,*EL,"Processing Catalogs ... "
          MOVE     ZERO,SECTOR
          PACK     KEY10,IBDBDBS,SP70
          CALL     RSCSUA1
UPDCAT10  CALL     RKCSUA1                * Check Catalog for Update
          BRANCH   OVRCD,UPDCAT20
          MATCH    IBDBDBS,CSUADBS
          GOTO     UPDCAT20 IF NOT EQUAL
          ADD      ONE,SECTOR
          IF       SECTOR%100=0
            DISPLAY   *P30:24,*V2LON,SECTOR
          ENDIF
.
          CALL     UPCIA000               * Update Master Record
          BRANCH   EXIT,UPDCAT10
.
          PACK     KEY10,CSUADBS,CSUACAT
          CALL     DECSUA1                * Delete Update Record
          CALL     PALIA000               * Update Alias and Keyword Index
          GOTO     UPDCAT10
.
UPDCAT20  DISPLAY  *P1:24,*EL,"Processing Catalogs/Supplier ... "
          MOVE     ZERO,SECTOR
          PACK     KEY30,IBDBDBS,SP70
          CALL     RSCSUC1
UPDCAT30  CALL     RKCSUC1                * Check Catalog/Suppliers
          BRANCH   OVRCD,UPDCAT99
          MATCH    IBDBDBS,CSUCDBS
          GOTO     UPDCAT99 IF NOT EQUAL
          ADD      ONE,SECTOR
          IF       SECTOR%100=0
            DISPLAY   *P40:24,*V2LON,SECTOR
          ENDIF
.
          CALL     UPCIC000               * Update Catalog/Supplier Record
          BRANCH   EXIT,UPDCAT30
.
          PACK     KEY30,CSUCDBS,CSUCCAT,CSUCSUP,CSUCSUT,SP70
          CALL     DECSUC1                * Delete Update Record
          CALL     PALIA000               * Update Alias and Keyword Index
          GOTO     UPDCAT30
.
UPDCAT99  RETURN
.------------------------------------------------------------
. Update Catalog Master Record
.------------------------------------------------------------
UPCIA000  MOVE     CSUACAT,KEY7
          CALL     RDCSIA1
          BRANCH   OVRCD,UPCIA910
.
          MOVE     CSIACAT,KEY7
          CALL     RDSIIA1                * Read Master Record
          BRANCH   OVRCD,UPCIA100
.
          CALL     LOCCIA00               * Lock Master Record
          BRANCH   EXIT,UPCIA990
.
          CALL     CIAUP000               * Update Master Record
          GOTO     UPCIA900
.
UPCIA100  CALL     CIAWR000               * Write New Masterfile Record
          GOTO     UPCIA900
.
UPCIA900  CALL     UUSIIA1
UPCIA910  MOVE     ZERO,EXIT
          GOTO     UPCIA999
.
UPCIA990  MOVE     ONE,EXIT
UPCIA999  RETURN
.------------------------------------------------------------
. Update Catalog/Supplier Record
.------------------------------------------------------------
UPCIC000  PACK     KEY27,CSUCCAT,CSUCSUP,CSUCSUT
          CALL     RDCSIC1
          BRANCH   OVRCD,UPCIC800
.
          MOVE     CSICCAT,KEY7
          CALL     RDCSIA1                * Read Master Record
          BRANCH   OVRCD,UPCIC920
.
          MOVE     CSICCAT,KEY7
          CALL     RDSIIA1                * Read Master Record
          BRANCH   OVRCD,UPCIC920
.
          CALL     LOCCIA00               * Lock Master Record
          BRANCH   EXIT,UPCIC990
.
          PACK     KEY27,CSUCCAT,CSUCSUP,CSUCSUT
          CALL     RDSIIC1
          IF       OVRCD=0
            CALL     CICUP000             * Update Record
          ELSE
            CALL     CICWR000             * Write Record
          ENDIF
.
          GOTO     UPCIC900
.
UPCIC800  COMPARE  TWO,CSUCTYP
          GOTO     UPCIC900 IF NOT EQUAL
.
          MOVE     CSUCCAT,KEY7
          CALL     RDSIIA1                * Read Master Record
          BRANCH   OVRCD,UPCIC920
.
          CALL     LOCCIA00               * Lock Master Record
          BRANCH   EXIT,UPCIC990
.
          CALL     CICDE000
.
UPCIC900  CALL     UUSIIA1
          MOVE     ZERO,EXIT
          GOTO     UPCIC999
.
UPCIC920  CLEAR    DISPMSG
          APPEND   "ERROR Catalog ",DISPMSG
          APPEND   CSICCAT,DISPMSG
          APPEND   " Not Found in Cat/Sup Update - ",DISPMSG
          RESET    DISPMSG
          BEEP
          CALL     MESSAGE1
.
UPCIC990  MOVE     ONE,EXIT
UPCIC999  RETURN
.------------------------------------------------------------
. Lock Master Record for Update
.------------------------------------------------------------
LOCCIA00  MOVE     ZERO,LOCKCNT
          MOVE     ONE,EXIT
LOCCIA10  CALL     RLSIIA1                * Read Master Record
          BRANCH   OVRCD,LOCCIA99,LOCCIA20
          MOVE     ZERO,EXIT
          GOTO     LOCCIA99
LOCCIA20  ADD      ONE,LOCKCNT            * Try Ten Times
          COMPARE  TEN,LOCKCNT
          GOTO     LOCCIA10 IF NOT EQUAL
          CALL     RECLOC00
          BRANCH   EXIT,LOCCIA00
          MOVE     ONE,EXIT
LOCCIA99  RETURN
.------------------------------------------------------------
. Move in Fields ans Write New Record
.------------------------------------------------------------
CIAWR000  MOVE      CSIACAT,SIIACAT
          MOVE      CSIADES,SIIADES
          MOVE      CSIASUB,SIIASUB
          MOVE      CSIAISS,SIIAISS
          MOVE      CSIASTI,SIIASTI
          MOVE      CSIAAVC,SIIAAVC
          MOVE      CSIANAT,SIIANAT
          MOVE      CSIAABC,SIIAABC
          MOVE      CSIAPSC,SIIAPSC
          MOVE      CSIAREX,SIIAREX
          MOVE      ONE,SIIANON
          MOVE      CSIASAB,SIIASAB
          MOVE      CSIASUS,SIIASUS
          MOVE      CSIADTC,SIIADTC
          MOVE      CSIATMC,SIIATMC
          MOVE      CSIAUSC,SIIAUSC
          MOVE      CSIAUR1,SIIAUR1
          MOVE      CSIAUR2,SIIAUR2
          MOVE      CSIAUD1,SIIAUD1
          MOVE      CSIAUD2,SIIAUD2
          MOVE      SP70,SIIAUC1
          MOVE      SP70,SIIAUC2
          MOVE      CSIAUY1,SIIAUY1
          MOVE      CSIAUY2,SIIAUY2
          MOVE      SP70,SIIAQIN
          MOVE      SP70,SIIADUN
          MOVE      SP70,SIIASPA
.
          CALL      WRSIIA1       * Write to Hospital Database
          CALL      AUDT0200      * Write to Hospital Corporate Update Audit
          RETURN
.------------------------------------------------------------
. Update Masterfile
.------------------------------------------------------------
CIAUP000  IF        CSIASUS<>0
            CALL      AUDT0900
          ENDIF
.
          MATCH     CSIADES,SIIADES
          IF        !@EQUAL
            CALL      AUDT0700
          ENDIF
.
          MATCH     CSIAPSC,SIIAPSC
          IF        !@EQUAL
            CALL      AUDT1400
          ENDIF
.
          MATCH     CSIASUB,SIIASUB
          IF        !@EQUAL
            CALL      AUDT1500
          ENDIF
.
          MOVE      CSIADES,SIIADES
          MOVE      CSIASUB,SIIASUB
          MOVE      CSIASTI,SIIASTI
          MOVE      CSIANAT,SIIANAT
          MOVE      CSIAPSC,SIIAPSC
          MOVE      CSIASUS,SIIASUS
          MOVE      CSIADTC,SIIADTC
          MOVE      CSIATMC,SIIATMC
          MOVE      CSIAUSC,SIIAUSC
          MOVE      CSIAUR1,SIIAUR1
          MOVE      CSIAUR2,SIIAUR2
          MOVE      CSIAUD1,SIIAUD1
          MOVE      CSIAUD2,SIIAUD2
          MOVE      SP70,SIIAUC1
          MOVE      SP70,SIIAUC2
          MOVE      CSIAUY1,SIIAUY1
          MOVE      CSIAUY2,SIIAUY2
.
          CALL      UPSIIA1             * Update Masterfile
          RETURN
.------------------------------------------------------------
. Move in Fields ans Write New Record
.------------------------------------------------------------
CICWR000  MOVE      CSICCAT,SIICCAT
          MOVE      CSICSUP,SIICSUP
          MOVE      CSICSUT,SIICSUT
          MOVE      CSICCCT,SIICCCT
          MOVE      CSICLCT,SIICLCT
          MOVE      CSICLTM,SIICLTM
          MOVE      CSICPNO,SIICPNO
          MOVE      CSICPD1,SIICPD1
          MOVE      CSICPD2,SIICPD2
          MOVE      CSICMAN,SIICMAN
          MOVE      CSICAPO,SIICAPO
          MOVE      CSICUR1,SIICUR1
          MOVE      CSICUR2,SIICUR2
          MOVE      CSICUD1,SIICUD1
          MOVE      CSICUD2,SIICUD2
          MOVE      SP70,SIICUC1
          MOVE      SP70,SIICUC2
          MOVE      CSICUY1,SIICUY1
          MOVE      CSICUY2,SIICUY2
          MOVE      SP70,SIICSPA
          CALL      WRSIIC1              * Write Hospital Record
          CALL      AUDT0300             * Write Audit File
.
          RETURN
.------------------------------------------------------------
. Update Masterfile
.------------------------------------------------------------
CICUP000  IF        CSICAPO=0
            CALL      AUDT1000
          ENDIF
.
          IF        CSICCCT<>SIICCCT
            CALL      AUDT0400
          ENDIF
.
          MATCH     CSICPNO,SIICPNO
          IF        !@EQUAL
            CALL      AUDT0500
          ENDIF
.
          MATCH     CSICPD1,SIICPD1
          IF        !@EQUAL
            CALL      AUDT0600
          ENDIF
.
          MOVE      CSICCCT,SIICCCT
          MOVE      CSICPNO,SIICPNO
          MOVE      CSICPD1,SIICPD1
          MOVE      CSICPD2,SIICPD2
          MOVE      CSICMAN,SIICMAN
          MOVE      CSICAPO,SIICAPO
          MOVE      CSICUR1,SIICUR1
          MOVE      CSICUR2,SIICUR2
          MOVE      CSICUD1,SIICUD1
          MOVE      CSICUD2,SIICUD2
          MOVE      SP70,SIICUC1
          MOVE      SP70,SIICUC2
          MOVE      CSICUY1,SIICUY1
          MOVE      CSICUY2,SIICUY2
.
          CALL      UPSIIC1             * Update Masterfile
.
          RETURN
.------------------------------------------------------------
. Delete Masterfile
.------------------------------------------------------------
CICDE000  PACK     KEY27,CSUCCAT,CSUCSUP,CSUCSUT
          CALL     RDSIIC1
          IF       OVRCD=0
            CALL     AUDT1300
            CALL     DESIIC1
          ENDIF
          RETURN
.------------------------------------------------------------
. Post Alias
.------------------------------------------------------------
PALIA000  PACK      KEY67,SIIACAT,SP70
          CALL      RSSIIF2
.
PALIA100  CALL      RKSIIF2
          BRANCH    OVRCD,PALIA200
          MATCH     SIIFCAT,SIIACAT
          GOTO      PALIA200 IF NOT EQUAL
          COMPARE   ZERO,SIIFATY
          GOTO      PALIA100 IF EQUAL
          PACK      KEY67,SIIFCAT,SIIFALI
          CALL      DESIIF2
          GOTO      PALIA100
.
PALIA200  MOVE      SIIACAT,SIIFCAT
          MOVE      SIIADES,SIIFALI
          MOVE      ONE,SIIFATY
          CALL      POSTALIA
.
          CALL      CSMALI00    * Update Corporate Aliases
.
          PACK      KEY27,SIIACAT,SP70
          CALL      RSSIIC1
PALIA300  CALL      RKSIIC1
          BRANCH    OVRCD,PALIA400
          MATCH     SIICCAT,SIIACAT
          GOTO      PALIA400 IF NOT EQUAL
.
          PACK      SIIFALI,SIICPD1,SP70
          MOVE      SIIACAT,SIIFCAT
          MOVE      TWO,SIIFATY
          CALL      POSTALIA
.
          PACK      SIIFALI,SIICPD2,SP70
          MOVE      SIIACAT,SIIFCAT
          MOVE      TWO,SIIFATY
          CALL      POSTALIA
.
          PACK      SIIFALI,SIICPNO,SP70
          MOVE      SIIACAT,SIIFCAT
          MOVE      THREE,SIIFATY
          CALL      POSTALIA
          GOTO      PALIA300
.
PALIA400  CALL      WRKCT000            * Update Keyword Index
.
          RETURN
.
POSTALIA  MATCH     SP70,SIIFALI
          RETURN    IF EQUAL
          PACK      KEY67,SIIFCAT,SIIFALI
          CALL      RASIIF2
          IF        OVRCD=1
            CALL      WRSIIF2
          ENDIF
          RETURN
.------------------------------------------------------------
. Post Corporate Alias Detials
.------------------------------------------------------------
CSMALI00  MOVE      SIIACAT,KEY6
          CALL      RDCSIF1
          BRANCH    OVRCD,CSMALI99
.
          MATCH     SP70,CSIFDE01
          IF        !@EQUAL
            MOVE      SIIACAT,SIIFCAT
            MOVE      CSIFDE01,SIIFALI
            MOVE      ZERO,SIIFATY
            CALL      POSTALIA
          ENDIF
.
          MATCH     SP70,CSIFDE02
          IF        !@EQUAL
            MOVE      SIIACAT,SIIFCAT
            MOVE      CSIFDE02,SIIFALI
            MOVE      ZERO,SIIFATY
            CALL      POSTALIA
          ENDIF
.
          MATCH     SP70,CSIFDE03
          IF        !@EQUAL
            MOVE      SIIACAT,SIIFCAT
            MOVE      CSIFDE03,SIIFALI
            MOVE      ZERO,SIIFATY
            CALL      POSTALIA
          ENDIF
.
          MATCH     SP70,CSIFDE04
          IF        !@EQUAL
            MOVE      SIIACAT,SIIFCAT
            MOVE      CSIFDE04,SIIFALI
            MOVE      ZERO,SIIFATY
            CALL      POSTALIA
          ENDIF
.
          MATCH     SP70,CSIFDE05
          IF        !@EQUAL
            MOVE      SIIACAT,SIIFCAT
            MOVE      CSIFDE05,SIIFALI
            MOVE      ZERO,SIIFATY
            CALL      POSTALIA
          ENDIF
.
          MATCH     SP70,CSIFDE06
          IF        !@EQUAL
            MOVE      SIIACAT,SIIFCAT
            MOVE      CSIFDE06,SIIFALI
            MOVE      ZERO,SIIFATY
            CALL      POSTALIA
          ENDIF
.
          MATCH     SP70,CSIFDE07
          IF        !@EQUAL
            MOVE      SIIACAT,SIIFCAT
            MOVE      CSIFDE07,SIIFALI
            MOVE      ZERO,SIIFATY
            CALL      POSTALIA
          ENDIF
.
          MATCH     SP70,CSIFDE08
          IF        !@EQUAL
            MOVE      SIIACAT,SIIFCAT
            MOVE      CSIFDE08,SIIFALI
            MOVE      ZERO,SIIFATY
            CALL      POSTALIA
          ENDIF
.
CSMALI99  RETURN
.------------------------------------------------------------
. Generate Catalog Word Index
.------------------------------------------------------------
WRKCT000  CALL     CLEAR000
          PACK     KEY67,SIIACAT,SP70
          CALL     RSSIIF2
WRKCT100  CALL     RKSIIF2
          BRANCH   OVRCD,WRKCT999
          MATCH    SIIFCAT,SIIACAT
          GOTO     WRKCT999 IF NOT EQUAL
.
          MOVE     SIIFALI,CATDES60
          CALL     BWORD000
          GOTO     WRKCT100
.
WRKCT999  RETURN
.------------------------------------------------------------
. Recreate File
.------------------------------------------------------------
CLEAR000  PACK     KEY22,SIIACAT,SP70
          CALL     RSSIKC2
          CALL     RKSIKC2
          BRANCH   OVRCD,CLEAR999
          MATCH    SIIACAT,SIKCCAT
          GOTO     CLEAR999 IF NOT EQUAL
.
          PACK     KEY22,SIKCCAT,SIKCKWD,SP70
          CALL     DESIKC2
          GOTO     CLEAR000
.
CLEAR999  RETURN
.-------------------------------------------------
. Format Given Names
.-------------------------------------------------
BWORD000  CMATCH    SP1,CATDES60             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      CATDES60
            GOTO      BWORD000 IF NOT EOS
            GOTO      BWORD999               * entire name if blank
          ENDIF
          PACK      KEY60,CATDES60,SP30        * reset form pointer
          MOVE      KEY60,CATDES60
.
          SCAN      SP1,CATDES60               * Locate the first blank
          GOTO      BWORD100 IF NOT EQUAL
          BUMP      CATDES60,-1              * go back 1 to end of name
          MOVEFPTR  CATDES60,CCITEM          * another handly form field
          RESET     CATDES60                 * reset the form pointer
          SETLPTR   CATDES60,CCITEM          * set logical length to end of name
.
.         Save this Word
.
BWORD100  MOVE      CATDES60,KEY15
          PACK      KEY15,KEY15,SP70
          REP       UPPLOW,KEY15
          CALL      RDSIKE1                  * Check Exclusion
          COMPARE   ZERO,OVRCD
          GOTO      BWORD190 IF EQUAL
.
          PACK      KEY22,KEY15,SIIFCAT
          UNPACK    KEY22,SIKCKWD,SIKCCAT
          CALL      RDSIKC1
          IF        OVRCD=1
            CALL      WRSIKC1
          ENDIF
          GOTO      BWORD190
.
.         Check for any more words
.
BWORD190  SETLPTR   CATDES60,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     CATDES60,CCITEM        * reset to this point
          PACK      KEY60,CATDES60,SP30    * reset form pointer
          MOVE      KEY60,CATDES60
.
          GOTO      BWORD000
.
BWORD999  RETURN
.------------------------------------------------------------
. Delete Creditor/Supplier
.------------------------------------------------------------
APSDEL00  MOVE     CSCUCRD,KEY5
          CALL     RDAPMA1
          BRANCH   OVRCD,APSDEL90           * Not On File So Delete Not Req.
.
          CALL     LOCSUP00                 * Lock Before Delete
          BRANCH   EXIT,APSDEL95
.
          CALL     DCROP000
          BRANCH   EXIT,APSDEL95
          CALL     DCRED000
          CLOSE    APSIMSA2
          CLOSE    APSCNTA1
          CLOSE    APSSNTA1
          BRANCH   EXIT,APSDEL95
          CALL     AUDT1200
.
APSDEL90  MOVE     ZERO,EXIT
          GOTO     APSDEL99
APSDEL95  MOVE     ONE,EXIT
APSDEL99  RETURN
.------------------------------------------------------------
. Open Files for Deletion
.------------------------------------------------------------
DCROP000  MOVE     "apsimsaf",KEY8
          STRIP    IBDBDB01
          PACK     KEY50,IBDBDB01,KEY8,SP70
          STRIP    KEY50
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     APSIMSA2,KEY50
          TRAPCLR  IO
          BRANCH   OVRCD,DCROP900
.
          MOVE     "apscntaf",KEY8
          STRIP    IBDBDB01
          PACK     KEY50,IBDBDB01,KEY8,SP70
          STRIP    KEY50
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     APSCNTA1,KEY50
          TRAPCLR  IO
          BRANCH   OVRCD,DCROP900
.
          MOVE     "apssntaf",KEY8
          STRIP    IBDBDB01
          PACK     KEY50,IBDBDB01,KEY8,SP70
          STRIP    KEY50
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     APSSNTA1,KEY50
          TRAPCLR  IO
          BRANCH   OVRCD,DCROP900
          MOVE     ZERO,EXIT
          GOTO     DCROP999
DCROP900  MOVE     ONE,EXIT
DCROP999  RETURN
.----------------------------------------------------------------------
. Delete Creditor Details
.----------------------------------------------------------------------
DCRED000  PACK      KEY48,APMACRD,SP70
          CALL      RSAPIM2
          CALL      RKAPIM2
          BRANCH    OVRCD,DCRED100
          MATCH     APMACRD,APIMCRD
          GOTO      DCRED920 IF EQUAL
.
          COMPARE   ZERO,APMAPOT
          GOTO      DCRED925 IF NOT EQUAL
.
DCRED100  MOVE      APMACRD,KEY5
          CALL      RAAPMA1
          IF        OVRCD=0
            CALL      DEAPMA1
          ENDIF
.
          PACK      KEY35,APMACRD,SP70
          CALL      RSAPMX2
DCRED200  CALL      RKAPMX2
          BRANCH    OVRCD,DCRED290
          MATCH     APMXCRD,APMACRD
          GOTO      DCRED290 IF NOT EQUAL
          PACK      KEY35,APMXCRD,APMXREF
          CALL      DEAPMX2
          GOTO      DCRED200
.
DCRED290  PACK      KEY8,APMACRD,SP70
          CALL      RSAPCN1
DCRED300  CALL      RKAPCN1
          BRANCH    OVRCD,DCRED390
          MATCH     APMACRD,APCNCRD
          GOTO      DCRED390 IF NOT EQUAL
          PACK      KEY8,APCNCRD,APCNLIN
          CALL      DEAPCN1
          GOTO      DCRED300
.
DCRED390  PACK      KEY8,APMACRD,SP70
          CALL      RSAPSN1
DCRED400  CALL      RKAPSN1
          BRANCH    OVRCD,DCRED900
          MATCH     APMACRD,APSNCRD
          GOTO      DCRED900 IF NOT EQUAL
          PACK      KEY8,APSNCRD,APSNLIN
          CALL      DEAPSN1
          GOTO      DCRED400
.
DCRED900  MOVE     ZERO,EXIT            * Return to Prompt on "No"
          GOTO     DCRED999
.
DCRED920  BEEP
          CLEAR    DISPMSG
          APPEND   "Transactions Exist for Creditor ",DISPMSG
          APPEND   APMACRD,DISPMSG
          APPEND   " - ",DISPMSG
          RESET    DISPMSG
          CALL     MESSAGE1
          MOVE     ONE,EXIT
          GOTO     DCRED999
.
DCRED925  BEEP
          CLEAR    DISPMSG
          APPEND   "Balance at Last Purge Exists for Creditor - ",DISPMSG
          APPEND   APMACRD,DISPMSG
          APPEND   " - ",DISPMSG
          RESET    DISPMSG
          CALL     MESSAGE1
          MOVE     ONE,EXIT
          GOTO     DCRED999
.
DCRED999  RETURN
.
.--------------------------------------------------
. Enter Range
.  where IBDB    = System & File for Keyin Routines
.        DBS     = Field ID
.        KRDBS   = Call Routine Label
.        "    1" = Screen Item Number for "from" Variable
.        "    2" = Screen Item Number for "to" Variable
.        yyyyy.. = Description of Variable
.
.  Return Values
.        FROMDBS  Code & Description for Start of Range
.        TODBS    Code & Description for End of Range
.        STRDBS   Code for Start of Range
.        ENDDBS   Code for End of Range
.--------------------------------------------------
KRDBS000   MOVE     "    4",FLDID
           CALL     GETPOSIT
           DISPLAY  *PCCOL:CVERT,SP6
           CALL     KIBDBA00
           BRANCH   EXIT,KRDBS100,KRDBS999
           MOVE     IBDBDBS,STRDBS
           PACK     FROMDBS,IBDBDBS,SP1,IBDBDES
           GOTO     KRDBS140
KRDBS100   MOVE     "Start             ",FROMDBS
           MOVE     SP70,STRDBS
.
KRDBS140   MOVE     FROMDBS,VAR
           CALL     DISPITEM
.
KRDBS150   MOVE     "    5",FLDID
           CALL     GETPOSIT
           DISPLAY  *PCCOL:CVERT,SP6
           CALL     KIBDBA00
           BRANCH   EXIT,KRDBS200,KRDBS999
           MOVE     IBDBDBS,ENDDBS
           PACK     TODBS,IBDBDBS,SP1,IBDBDES
           MATCH    STRDBS,ENDDBS
           GOTO     KRDBS999 IF EQUAL
           GOTO     KRDBS999 IF NOT LESS
           BEEP
           MOVE     "To Database Must Be > From Database - ",DISPMSG
           CALL     MESSAGE1
           GOTO     KRDBS000
.
KRDBS200   MOVE     "Finish             ",TODBS
           MOVE     Z70,ENDDBS
.
KRDBS999   MOVE     "    4",FLDID
           CALL     GETPOSIT
           RETURN
.
.------------------------------------------------------------
. Update All records for Update to Database 1
.------------------------------------------------------------
UPALL000  DISPLAY  *P1:24,*EL,"Processing Suppliers ... "
          MOVE     ZERO,SECTOR
          PACK     KEY5,SP70
          CALL     RSCSCR1
UPALL100  CALL     RKCSCR1
          BRANCH   OVRCD,UPALL199
          ADD      ONE,SECTOR
          IF       SECTOR%100=0
            DISPLAY   *P30:24,*V2LON,SECTOR
          ENDIF
          MOVE     "1  ",CSCUDBS
          MOVE     CSCRCRD,CSCUCRD
          MOVE     "1",CSCUTYP
          PACK     KEY8,CSCUDBS,CSCUCRD,SP70
          CALL     RACSCU1
          IF       OVRCD=1
            CALL     WRCSCU1
          ENDIF
          GOTO     UPALL100
.
UPALL199  DISPLAY  *P1:24,*EL,"Processing Catalog ... "
          MOVE     ZERO,SECTOR
          PACK     KEY7,SP70
          CALL     RSCSIA1
UPALL200  CALL     RKCSIA1
          BRANCH   OVRCD,UPALL299
          ADD      ONE,SECTOR
          IF       SECTOR%100=0
            DISPLAY   *P30:24,*V2LON,SECTOR
          ENDIF
          MOVE     "1  ",CSUADBS
          MOVE     CSIACAT,CSUACAT
          MOVE     "1",CSUATYP
          PACK     KEY10,CSUADBS,CSUACAT,SP70
          CALL     RACSUA1
          IF       OVRCD=1
            CALL     WRCSUA1
          ENDIF
          GOTO     UPALL200
.
UPALL299  DISPLAY  *P1:24,*EL,"Processing Catalog/Supplier ... "
          MOVE     ZERO,SECTOR
          PACK     KEY27,SP70
          CALL     RSCSIC1
UPALL300  CALL     RKCSIC1
          BRANCH   OVRCD,UPALL399
          ADD      ONE,SECTOR
          IF       SECTOR%100=0
            DISPLAY   *P30:24,*V2LON,SECTOR
          ENDIF
          MOVE     "1  ",CSUCDBS
          MOVE     CSICCAT,CSUCCAT
          MOVE     CSICSUP,CSUCSUP
          MOVE     CSICSUT,CSUCSUT
          MOVE     "1",CSUCTYP
          PACK     KEY30,CSUCDBS,CSUCCAT,CSUCSUP,CSUCSUT,SP70
          CALL     RACSUC1
          IF       OVRCD=1
            CALL     WRCSUC1
          ENDIF
          GOTO     UPALL300
UPALL399  RETURN
.
.-------------------------------
. Includes for I/O and Keyins
.-------------------------------
          INC       APSCNTIO/INC
          INC       APSIMSIO/INC
          INC       APSMASIO/INC
          INC       APSMXRIO/INC
          INC       APSSNTIO/INC
          INC       CSMAUD01    
          INC       CSMCIAIO/INC
          INC       CSMCICIO/INC
          INC       CSMCIFIO/INC
          INC       CSMCREIO/INC
          INC       CSMCUCIO/INC
          INC       CSMUIAIO/INC
          INC       CSMUICIO/INC
          INC       IBADBSIO/INC
          INC       IBADBSKY    
          INC       SINCIAIO/INC
          INC       SINCICIO/INC
          INC       SINCIFIO/INC
          INC       SINCUAIO/INC
          INC       SINKCTIO/INC
          INC       SINKEXIO/INC
.
          INC       STDGENCD
.
