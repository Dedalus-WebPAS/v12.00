.------------------------------------------------------------
. Add Field
.------------------------------------------------------------
FMSB0000  BRANCH    FLDITMNO,FMSB0100,FMSB0200,FMSB0300,FMSB0400,FMSB0500:
                             FMSB0600,FMSB0700,FMSB0800,FMSB0900,FMSB1000:
                             FMSB1100,FMSB1200,FMSB1300,FMSB1400,FMSB1500:
                             FMSB1600,FMSB1700,FMSB1800,FMSB1900,FMSB2000:
                             FMSB2100
.
FMSB0100  MOVE      ACCTKEYS,KEY14
          CALL      SETACC00
          REP       " +",FMTACCT
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      PERIODNO,KEY2
          REP       " 0",KEY2
          WRITE     HTMLFILE;"fmsweb04.pbl?reportno=1":
                             "&template=",FLDPARA:
                             "&fincyear=",CURYEAR:
                             "&fincperd=",KEY2:
                             "&acctkeys=",FMTACCT;
          GOTO      FMSB9999
.
FMSB0200  MOVE      ACCTKEYS,KEY14
          CALL      SETACC00
          REP       " +",FMTACCT
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      PERIODNO,KEY2
          REP       " 0",KEY2
          WRITE     HTMLFILE;"fmsweb04.pbl?reportno=2":
                             "&template=",FLDPARA:
                             "&fincyear=",CURYEAR:
                             "&fincperd=",KEY2:
                             "&acctkeys=",FMTACCT;
          GOTO      FMSB9999
.
FMSB0300  WRITE     HTMLFILE;TOTLLEDG;
          GOTO      FMSB9999
.
FMSB0400  WRITE     HTMLFILE;TOTLACCT;
          GOTO      FMSB9999
.
FMSB0500  CLEAR     DISPNAME
          MOVE      TOTLDESC,NAME35
          CALL      NAMSTR00
          RESET     DISPNAME
          WRITE     HTMLFILE;DISPNAME;
          GOTO      FMSB9999
.
FMSB0600  MOVE      TOTLTYPE,F1
          BRANCH    F1,FMSB0610,FMSB0620,FMSB0630,FMSB0640,FMSB0650:
                       FMSB0660,FMSB0670,FMSB0680,FMSB0690
FMSB0610  WRITE     HTMLFILE;"Income";
          GOTO      FMSB9999
FMSB0620  WRITE     HTMLFILE;"Expense";
          GOTO      FMSB9999
FMSB0630  WRITE     HTMLFILE;"Asset";
          GOTO      FMSB9999
FMSB0640  WRITE     HTMLFILE;"Liability";
          GOTO      FMSB9999
FMSB0650  WRITE     HTMLFILE;"Equity";
          GOTO      FMSB9999
FMSB0660  WRITE     HTMLFILE;"Total";
          GOTO      FMSB9999
FMSB0670  WRITE     HTMLFILE;"Heading";
          GOTO      FMSB9999
FMSB0680  WRITE     HTMLFILE;"Statistic";
          GOTO      FMSB9999
FMSB0690  WRITE     HTMLFILE;"Calculated Statistic";
          GOTO      FMSB9999
.
FMSB0700  WRITE     HTMLFILE;CURYEAR;
          GOTO      FMSB9999
.
FMSB0800  MOVE      PERIODNO,F2
          LOAD      PERNAME,F2,FMLC01DE,FMLC02DE,FMLC03DE,FMLC04DE,FMLC05DE:
                               FMLC06DE,FMLC07DE,FMLC08DE,FMLC09DE,FMLC10DE:
                               FMLC11DE,FMLC12DE
          WRITE     HTMLFILE;PERNAME;
          GOTO      FMSB9999
.
FMSB0900  UNPACK    DIM127,D1,ACCTKEYS,DIM127
          PACK      KEY14,ACCTKEYS,SP70
          CALL      RDFMAM1
          BRANCH    OVRCD,FMSB9999
.
          CALL      FINDET00   * Get Financial Data
          CALL      BLDARR00   * Get Financial Data
.
          GOTO      FMSB9999
.
FMSB1000  CALL      SELPER00
          GOTO      FMSB9999
.
FMSB1100  UNPACK    DIM127,D1,DSETNUM,DIM127
          CALL      ADDNAM00
          GOTO      FMSB9999
.
FMSB1200  UNPACK    DIM127,D1,DSETNUM,D1,LINKPRG,D1,LINKREP,D1,LINKTEM,DIM127
          MATCH     SP70,LINKPRG
          IF        @EQUAL
            MOVE      "fmsweb04",LINKPRG
          ENDIF
          MATCH     SP70,LINKREP
          IF        @EQUAL
            MOVE      "01",LINKREP
          ENDIF
          MATCH     SP70,LINKTEM
          IF        @EQUAL
            MOVE      "001",LINKTEM
          ENDIF
          CALL      ADDLNK00
          GOTO      FMSB9999
.
FMSB1300  UNPACK    DIM127,D1,DSETNUM,DIM127
          CALL      ADDCOL00
          GOTO      FMSB9999
.
FMSB1400  UNPACK    DIM127,D1,DSETNUM,DSETAXIS,KEY1,D1,FLDPARA,DIM127
          MOVE      KEY1,DSETVALU
          CALL      ADDVAL00
          GOTO      FMSB9999
.
FMSB1500  MOVE      ZERO,LASTCOL
          CALL      SMTABA00
          GOTO      FMSB9999
.
FMSB1600  WRITE     HTMLFILE;LASTPERD;
          GOTO      FMSB9999
.
FMSB1700  WRITE     HTMLFILE;LASTYEAR;
          GOTO      FMSB9999
.
FMSB1800  WRITE     HTMLFILE;NEXTPERD;
          GOTO      FMSB9999
.
FMSB1900  WRITE     HTMLFILE;NEXTYEAR;
          GOTO      FMSB9999
.
FMSB2000  IF        LOCKFLAG=0
            WRITE     HTMLFILE;"Interim Results";
          ENDIF
          GOTO      FMSB9999 
.
FMSB2100  MOVE      ONE,LASTCOL
          CALL      SMTABA00
          GOTO      FMSB9999
.
FMSB9999  RETURN
.------------------------------------------------------------
. Build Array of Sub Accounts
.------------------------------------------------------------
BLDARR00  MOVE      ZERO,LASTARRY
          CALL      HEDCOD00
          PACK      KEY33,ACCTKEYS,SP70
          CALL      RSFMTC1              * go to start of records
BLDARR10  CALL      RKFMTC1              * get next record
          BRANCH    OVRCD,BLDARR99               * no more records ?
          PACK      KEY14,FMTCTLED,FMTCTACC,SP70
          MATCH     KEY14,KEY33
          GOTO      BLDARR99 IF NOT EQUAL
.
          PACK      KEY14,FMTCSLED,FMTCSACC,SP70
          CALL      RDFMAM1
.
          CALL      FINDET00        * Get Financial Details
          CALL      CHKZER00
          BRANCH    EXIT,BLDARR10
.
          ADD       ONE,LASTARRY
          MOVE      KEY14,ACCARR[LASTARRY]
          CALL      GETNAM00
          CLEAR     DISPNAME
          MOVE      ACCOUNTN,NAME35
          CALL      NAMSTR00
          RESET     DISPNAME
          MOVE      DISPNAME,NAMARR[LASTARRY]
.
          CALL      MAKLNK00
          COMPARE   "150",LASTARRY
          GOTO      BLDARR10 IF NOT EQUAL
.
          WRITE     HTMLFILE;"<H1>ERROR TOO MANY SUB ACCOUNTS<H1>"
.        
BLDARR99  RETURN
.------------------------------------------------------------
. Add to Link Array
.------------------------------------------------------------
MAKLNK00  CLEAR     HTMLLINK
          APPEND    "&fincyear=",HTMLLINK
          APPEND    CURYEAR,HTMLLINK
          APPEND    "&fincperd=",HTMLLINK
          MOVE      PERIODNO,KEY2
          REP       " 0",KEY2
          APPEND    KEY2,HTMLLINK
          APPEND    "&acctkeys=",HTMLLINK
          PACK      KEY14,FMTCSLED,FMTCSACC,SP70
          CALL      SETACC00
          REP       " +",FMTACCT
          APPEND    FMTACCT,HTMLLINK
          REP       "+ ",FMTACCT
          RESET     HTMLLINK
          SQUEEZE   HTMLLINK
          MOVE      HTMLLINK,LNKARR[LASTARRY]
          RETURN
.------------------------------------------------------------
. Colors Dataset
.------------------------------------------------------------
ADDCOL00  MOVE      LASTARRY,ARRYCNT
          COMPARE   ZERO,LASTARRY
          GOTO      ADDCOL99 IF EQUAL
          WRITE     HTMLFILE;" <param name=dataset",DSETNUM,"Colors ":
                                    "value=#"",COLARR[ARRYCNT];
ADDCOL10  SUB       ONE,ARRYCNT
          COMPARE   ZERO,ARRYCNT
          GOTO      ADDCOL90 IF EQUAL
          WRITE     HTMLFILE;",",COLARR[ARRYCNT];
          GOTO      ADDCOL10
ADDCOL90  WRITE     HTMLFILE;"#">"
ADDCOL99  RETURN
.------------------------------------------------------------
. Name Dataset
.------------------------------------------------------------
ADDNAM00  MOVE      LASTARRY,ARRYCNT
          COMPARE   ZERO,LASTARRY
          GOTO      ADDNAM99 IF EQUAL
          STRIP     NAMARR[ARRYCNT]
          MOVELPTR  NAMARR[ARRYCNT],LENGTH
          SFORMAT   VAR,LENGTH
          MOVE      NAMARR[ARRYCNT],VAR
          WRITE     HTMLFILE;"  <param name=dataset",DSETNUM,"Labels ":
                                    "value=#"",VAR;
          SFORMAT   VAR,127
ADDNAM10  SUB       ONE,ARRYCNT
          COMPARE   ZERO,ARRYCNT
          GOTO      ADDNAM90 IF EQUAL
          STRIP     NAMARR[ARRYCNT]
          MOVELPTR  NAMARR[ARRYCNT],LENGTH
          SFORMAT   VAR,LENGTH
          MOVE      NAMARR[ARRYCNT],VAR
          WRITE     HTMLFILE;",",VAR;
          SFORMAT   VAR,127
          GOTO      ADDNAM10
ADDNAM90  WRITE     HTMLFILE;"#">"
ADDNAM99  RETURN
.------------------------------------------------------------
. Name Dataset
.------------------------------------------------------------
ADDLNK00  MOVE      LASTARRY,ARRYCNT
          COMPARE   ZERO,LASTARRY
          GOTO      ADDLNK99 IF EQUAL
          STRIP     LNKARR[ARRYCNT]
          MOVELPTR  LNKARR[ARRYCNT],LENGTH
          SFORMAT   VAR,LENGTH
          MOVE      LNKARR[ARRYCNT],VAR
          WRITE     HTMLFILE;"  <param name=dataset",DSETNUM,"Links ":
                             "value=#"",LINKPRG,".pbl?reportno=",LINKREP:
                             "&template=",LINKTEM,VAR;
          APPEND    KEY3,HTMLLINK
          SFORMAT   VAR,127
ADDLNK10  SUB       ONE,ARRYCNT
          COMPARE   ZERO,ARRYCNT
          GOTO      ADDLNK90 IF EQUAL
          STRIP     LNKARR[ARRYCNT]
          MOVELPTR  LNKARR[ARRYCNT],LENGTH
          SFORMAT   VAR,LENGTH
          MOVE      LNKARR[ARRYCNT],VAR
          WRITE     HTMLFILE;",",LINKPRG,".pbl?reportno=",LINKREP:
                             "&template=",LINKTEM,VAR;
          SFORMAT   VAR,127
          GOTO      ADDLNK10
ADDLNK90  WRITE     HTMLFILE;"#">"
ADDLNK99  RETURN
.------------------------------------------------------------
. Name Dataset
.------------------------------------------------------------
ADDVAL00  MOVE      LASTARRY,ARRYCNT
          COMPARE   ZERO,LASTARRY
          GOTO      ADDVAL99 IF EQUAL
          MOVE      ACCARR[ARRYCNT],KEY14
          CALL      RDFMAM1
          CALL      FINDET00
.
          PERFORM   DSETVALU,ACTAMT00,LYRAMT00,BUDAMT00,ALTAMT00
          MULT      INCOMEFL,F12P5
          WRITE     HTMLFILE;"  <param name=dataset",DSETNUM,DSETAXIS,"Values ":
                                    "value=#"",F12P5;
ADDVAL10  SUB       ONE,ARRYCNT
          COMPARE   ZERO,ARRYCNT
          GOTO      ADDVAL90 IF EQUAL
          MOVE      ACCARR[ARRYCNT],KEY14
          CALL      RDFMAM1
          CALL      FINDET00
          PERFORM   DSETVALU,ACTAMT00,LYRAMT00,BUDAMT00,ALTAMT00
          MULT      INCOMEFL,F12P5
          WRITE     HTMLFILE;",",F12P5;
          GOTO      ADDVAL10
ADDVAL90  WRITE     HTMLFILE;"#">"
ADDVAL99  RETURN
.------------------------------------------------------------
. Table A
.------------------------------------------------------------
SMTABA00  MOVE      ZERO,FORMATTY
          UNPACK    SP70,LINKPRG,LINKREP,LINKTEM
          UNPACK    DIM127,KEY1,ANS,D1,LINKPRG,KEY1,LINKREP,KEY1,LINKTEM,DIM127
          MOVE      ANS,FORMATTY
          MATCH     SP70,LINKPRG
          IF        @EQUAL
            MOVE      "fmsweb04",LINKPRG
          ENDIF
          MATCH     SP70,LINKREP
          IF        @EQUAL
            MOVE      "01",LINKREP
          ENDIF
          MATCH     SP70,LINKTEM
          IF        @EQUAL
            MOVE      "001",LINKTEM
          ENDIF
          WRITE     HTMLFILE;"<table border=1 width=#"100%#" >"
          MOVE      PERIODNO,F2
          LOAD      PERNAME,F2,FMLC01DE,FMLC02DE,FMLC03DE,FMLC04DE,FMLC05DE:
                               FMLC06DE,FMLC07DE,FMLC08DE,FMLC09DE,FMLC10DE:
                               FMLC11DE,FMLC12DE
          LOAD      KEY8,PERIODNO,FMLC01TO,FMLC02TO,FMLC03TO,FMLC04TO:
                                  FMLC05TO,FMLC06TO,FMLC07TO,FMLC08TO:
                                  FMLC09TO,FMLC10TO,FMLC11TO,FMLC12TO,FMLC13TO
          LOAD      LOCKFLAG,F2,FMLC01IN,FMLC02IN,FMLC03IN,FMLC04IN,FMLC05IN:
                                FMLC06IN,FMLC07IN,FMLC08IN,FMLC09IN,FMLC10IN:
                                FMLC11IN,FMLC12IN
.
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          WRITE     HTMLFILE;"<tr><td colspan=8 align=center":
                             " bgcolor=#"##FFFF00#">";
          IF        LOCKFLAG=0
            WRITE     HTMLFILE;"Interim Results for ",PERNAME,SP1,CCENT,CYEAR;
          ELSE
            WRITE     HTMLFILE;PERNAME,SP1,CCENT,CYEAR;
          ENDIF
          WRITE     HTMLFILE;"</td></tr>";
          WRITE     HTMLFILE;"<TR><TD ALIGN=RIGHT><B>Actual</B></TD>":
                             "<TD ALIGN=RIGHT><B>Budget</B></TD>":
                             "<TD ALIGN=RIGHT><B>Variance</B></TD>":
                             "<TD><B>Account</B></TD>":
                             "<TD ALIGN=RIGHT><B>YTD Actual</B></TD>":
                             "<TD ALIGN=RIGHT><B>YTD Budget</B></TD>":
                             "<TD ALIGN=RIGHT><B>Variance</B></TD>";
          IF        LASTCOL=0
            WRITE     HTMLFILE;"<TD ALIGN=RIGHT><B>Last Year</B></TD></TR>"
          ELSE
            WRITE     HTMLFILE;"<TD ALIGN=RIGHT><B>Annual Budget</B></TD></TR>"
          ENDIF
.
          CALL      HEDCOD00
          PACK      KEY33,ACCTKEYS,SP70
          CALL      RSFMTC1              * go to start of records
SMTABA10  CALL      RKFMTC1              * get next record
          BRANCH    OVRCD,SMTABA90               * no more records ?
          PACK      KEY14,FMTCTLED,FMTCTACC,SP70
          MATCH     KEY14,KEY33
          GOTO      SMTABA90 IF NOT EQUAL
.
          ADD       ONE,LASTARRY
          PACK      KEY14,FMTCSLED,FMTCSACC,SP70
          CALL      RDFMAM1
          CALL      FINDET00        * Get Financial Details
          CALL      CHKZER00
          BRANCH    EXIT,SMTABA10
.
          CALL      VCTABA00        * Determine Variance & Color Color
.
          CALL      LKTABA00        * Determine HTML Link
.
          CALL      LNTABA00        * Output Table Line Details
.
          GOTO      SMTABA10
.
SMTABA90  PACK      KEY14,ACCTKEYS  * Do it for the Total of the Accounts
          CALL      RDFMAM1
          CALL      FINDET00        * Get Financial Details
          CALL      CHKZER00
          BRANCH    EXIT,SMTABA95
.
          CALL      VCTABA00        * Determine Variance & Color Color
.
          CALL      LKTABA00        * Determine HTML Link
.
          CALL      LNTABA00        * Output Table Line Details
.
SMTABA95  WRITE     HTMLFILE;"</table>"
.
SMTABA99  RETURN
.------------------------------------------------------------
. Check for Zero Output Line to Ignore
.------------------------------------------------------------
CHKZER00  MOVE      ZERO,EXIT
          COMPARE   ZERO,CURAPER
          GOTO      CHKZER99 IF NOT EQUAL
          COMPARE   ZERO,CURBPER
          GOTO      CHKZER99 IF NOT EQUAL
          COMPARE   ZERO,CURAYTD
          GOTO      CHKZER99 IF NOT EQUAL
          COMPARE   ZERO,CURBYTD
          GOTO      CHKZER99 IF NOT EQUAL
          COMPARE   ZERO,LYRAYTD
          GOTO      CHKZER99 IF NOT EQUAL
.
          MOVE      ONE,EXIT
.
CHKZER99  RETURN
.------------------------------------------------------------
. Output Line in Table
.------------------------------------------------------------
LNTABA00  MOVE      CURAPER,AMTCOL01
          MOVE      CURBPER,AMTCOL02
          MOVE      CURAPERV,AMTCOL03
          MOVE      CURAYTD,AMTCOL04
          MOVE      CURBYTD,AMTCOL05
          MOVE      CURAYTDV,AMTCOL06
          IF        LASTCOL=0
            MOVE      LYRAYTD,AMTCOL07
          ELSE
            MOVE      CURB01,AMTCOL07   * Annual Budget
            ADD       CURB02,AMTCOL07
            ADD       CURB03,AMTCOL07
            ADD       CURB04,AMTCOL07
            ADD       CURB05,AMTCOL07
            ADD       CURB06,AMTCOL07
            ADD       CURB07,AMTCOL07
            ADD       CURB08,AMTCOL07
            ADD       CURB09,AMTCOL07
            ADD       CURB10,AMTCOL07
            ADD       CURB11,AMTCOL07
            ADD       CURB12,AMTCOL07
            ADD       CURB13,AMTCOL07
          ENDIF
.
          MULT      INCOMEFL,AMTCOL01
          MULT      INCOMEFL,AMTCOL02
          MULT      INCOMEFL,AMTCOL03
          MULT      INCOMEFL,AMTCOL04
          MULT      INCOMEFL,AMTCOL05
          MULT      INCOMEFL,AMTCOL06
          MULT      INCOMEFL,AMTCOL07
          MOVE      "2",EDITTYPE        * Standard 2 Decimal Places Financial
          MOVE      FMAMTYPE,F1
          IF        F1>7
            MOVE      FMAMDPLA,EDITTYPE * Set up Number for Stats
          ENDIF
.
          CALL      FORMAT00
.
          CALL      GETNAM00
          CLEAR     DISPNAME
          MOVE      ACCOUNTN,NAME35
          CALL      NAMSTR00
          RESET     DISPNAME
          WRITE     HTMLFILE;"<tr>":
                             "<td align=right>",FMTCOL01,"</td>":
                             "<td align=right>",FMTCOL02,"</td>":
                             "<td align=right><font color=",CURBGC,">":
                             FMTCOL03,"</font></td>":
                             "<td><a href=",HTMLLINK,">":
                              DISPNAME,"</a></td>":
                             "<td align=right>",FMTCOL04,"</td>":
                             "<td align=right>",FMTCOL05,"</td>":
                             "<td align=right><font color=",YTDBGC,">":
                              FMTCOL06,"</font></td>":
                             "<td align=right>",FMTCOL07,"</td></tr>"
          RETURN
.------------------------------------------------------------
. Variance Color Determination
.------------------------------------------------------------
VCTABA00  MATCH     "1",FMCOBVAR
          IF        @EQUAL
            MOVE      BGGREEN,BGBAD
            MOVE      BGRED,BGGOOD
            ASSIGN    CURBYTD-CURAYTD,CURAYTDV
            ASSIGN    CURBPER-CURAPER,CURAPERV
          ELSE
            MOVE      BGGREEN,BGGOOD
            MOVE      BGRED,BGBAD
            ASSIGN    CURAYTD-CURBYTD,CURAYTDV
            ASSIGN    CURAPER-CURBPER,CURAPERV
          ENDIF
.
          PACK      KEY14,FMAMLEDG,FMAMACCT
          CALL      RDFMWC1                   * Variance Amount
          BRANCH    OVRCD,VCTABA10
.
          MOVE      BGGOOD,YTDBGC
          IF        CURAYTDV<=0
            MOVE      BGBAD,YTDBGC
          ENDIF
          MOVE      BGGOOD,CURBGC
          IF        CURAPERV<=0
            MOVE      BGBAD,CURBGC
          ENDIF
.
          GOTO      VCTABA99
.
VCTABA10  MOVE      BGGOOD,YTDBGC
          IF        CURAYTDV>=0
            MOVE      BGBAD,YTDBGC
          ENDIF
          MOVE      BGGOOD,CURBGC
          IF        CURAPERV>=0
            MOVE      BGBAD,CURBGC
          ENDIF
.
VCTABA99  RETURN
.------------------------------------------------------------
. Get Link for Line in Table
.------------------------------------------------------------
LKTABA00  CLEAR     HTMLLINK
          APPEND    LINKPRG,HTMLLINK
          APPEND    ".pbl?reportno=",HTMLLINK
          APPEND    LINKREP,HTMLLINK
          APPEND    "&template=",HTMLLINK
          APPEND    LINKTEM,HTMLLINK
          APPEND    "&fincyear=",HTMLLINK
          APPEND    CURYEAR,HTMLLINK
          APPEND    "&fincperd=",HTMLLINK
          MOVE      PERIODNO,KEY2
          REP       " 0",KEY2
          APPEND    KEY2,HTMLLINK
          APPEND    "&acctkeys=",HTMLLINK
          CALL      SETACC00
          REP       " +",FMTACCT
          APPEND    FMTACCT,HTMLLINK
          REP       "+ ",FMTACCT
          APPEND    SP70,HTMLLINK
          RESET     HTMLLINK
          SQUEEZE   HTMLLINK
          RETURN
.------------------------------------------------------------
. Set up Color Array
.------------------------------------------------------------
SETCOL00  MOVE      "CC0000",COLARR[1]
          MOVE      "CC00CC",COLARR[2]
          MOVE      "0000CC",COLARR[3]
          MOVE      "00CCCC",COLARR[4]
          MOVE      "00CC00",COLARR[5]
          MOVE      "CCCC00",COLARR[6]
          MOVE      "000000",COLARR[7]
          MOVE      "CCCCCC",COLARR[8]
.
          MOVE      "FF0000",COLARR[9]
          MOVE      "FF00FF",COLARR[10]
          MOVE      "0000FF",COLARR[11]
          MOVE      "00FFFF",COLARR[12]
          MOVE      "00FF00",COLARR[13]
          MOVE      "FFFF00",COLARR[14]
          MOVE      "999999",COLARR[15]
          MOVE      "FFFFFF",COLARR[16]
.
          MOVE      "330000",COLARR[17]
          MOVE      "330033",COLARR[18]
          MOVE      "000033",COLARR[19]
          MOVE      "003333",COLARR[20]
          MOVE      "003300",COLARR[21]
          MOVE      "333300",COLARR[22]
          MOVE      "666666",COLARR[23]
          MOVE      "333333",COLARR[24]
.
SETCOL99  RETURN
.------------------------------------------------------------
. Create Select Period Options List
.------------------------------------------------------------
SELPER00  MOVE      "12",YEAREND
          ADD       FMLAPERS,YEAREND
          PACK      KEY6,FMLALEDG,CURYEAR
          CALL      RDFMLC1
          CALL      RPFMLC1
          BRANCH    OVRCD,SELPER10
.
          MOVE      ZERO,SELPERD
          CALL      ADDPER00      * Last Year
.
SELPER10  CALL      RKFMLC1
          BRANCH    OVRCD,SELPER99
.
          MOVE      PERIODNO,SELPERD
          CALL      ADDPER00     * Current Year
          
          CALL      RKFMLC1
          BRANCH    OVRCD,SELPER99
.
          MOVE      ZERO,SELPERD
          CALL      ADDPER00     * Next Year
.
          CALL      RPFMLC1
.
SELPER99  RETURN
.------------------------------------------------------------
. Add all Periods for a Year
.------------------------------------------------------------
ADDPER00  MOVE      ONE,F2
ADDPER10  LOAD      SELNAME,F2,FMLC01DE,FMLC02DE,FMLC03DE,FMLC04DE,FMLC05DE:
                               FMLC06DE,FMLC07DE,FMLC08DE,FMLC09DE,FMLC10DE:
                               FMLC11DE,FMLC12DE
          LOAD      KEY8,F2,FMLC01TO,FMLC02TO,FMLC03TO,FMLC04TO:
                                  FMLC05TO,FMLC06TO,FMLC07TO,FMLC08TO:
                                  FMLC09TO,FMLC10TO,FMLC11TO,FMLC12TO,FMLC13TO
          MOVE      KEY8,SELYEAR 
          PACK      KEY6,F2,FMLCYEAR
          REP       " 0",KEY6
          IF        F2=SELPERD
            WRITE     HTMLFILE;"<option value=#"",KEY6,"#" selected>":
                               SELNAME,SP1,SELYEAR,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=#"",KEY6,"#">":
                               SELNAME,SP1,SELYEAR,"</option>"
          ENDIF
          ADD       ONE,F2
          IF        F2<=YEAREND
            GOTO      ADDPER10
          ENDIF
ADDPER99  RETURN
.------------------------------------------------------------
. See if its a Total Account Subjective or Cost Centre
.------------------------------------------------------------
HEDCOD00  MOVE      SP70,SUBJHEAD
          MOVE      SP70,COSTHEAD
          UNPACK    ACCTKEYS,KEY2,KEY12
          PACK      KEY14,KEY2,KEY12
          CALL      RDFMSA1
          IF        OVRCD=0
            MOVE      KEY12,SUBJHEAD
            MOVE      SP70,COSTHEAD
            GOTO      HEDCOD99
          ENDIF
          PACK      KEY14,KEY2,KEY12
          CALL      RDFMCC1
          IF        OVRCD=0
            MOVE      SP70,SUBJHEAD
            MOVE      KEY12,COSTHEAD
            GOTO      HEDCOD99
          ENDIF
.
          MOVE      FMSUUD4A,F2
          ADD       FMSUUD2A,F2
          ADD       ONE,F2
          RESET     KEY12,F2
          PACK      SUBJHEAD,KEY12,SP70
.
          UNPACK    ACCTKEYS,KEY2,KEY12
          STORE     KEY12,FMSUUD4A,KEY1,KEY2,KEY3,KEY4,KEY5,KEY6:
                                     KEY7,KEY8,KEY9,KEY10,KEY11,KEY12
          LOAD      KEY11,FMSUUD4A,KEY1,KEY2,KEY3,KEY4,KEY5,KEY6:
                                     KEY7,KEY8,KEY9,KEY10,KEY11,KEY12
          PACK      COSTHEAD,KEY11,SP70
.
HEDCOD99  RETURN
.----------------------------------------------------------------------
. Determine Name for Account Code
.   If Subjective  = Total Subjective Show Cost Centre Name
.   If Cost Center = Total Total Cost Center Show Subjective Name
.----------------------------------------------------------------------
GETNAM00  MOVE      FMAMDESC,ACCOUNTN
.
          MOVE      FMSUUD4A,F2
          ADD       FMSUUD2A,F2
          ADD       ONE,F2
          MOVE      FMAMACCT,KEY12
          RESET     KEY12,F2
          PACK      SUBJCODE,KEY12,SP70
.
          RESET     FMAMACCT
          STORE     FMAMACCT,FMSUUD4A,KEY1,KEY2,KEY3,KEY4,KEY5,KEY6:
                                     KEY7,KEY8,KEY9,KEY10,KEY11,KEY12
          LOAD      KEY12,FMSUUD4A,KEY1,KEY2,KEY3,KEY4,KEY5,KEY6:
                                     KEY7,KEY8,KEY9,KEY10,KEY11,KEY12
          PACK      COSTCODE,KEY12,SP70
.
          MATCH     SUBJCODE,SUBJHEAD
          IF        @EQUAL
            PACK      KEY14,FMAMLEDG,COSTCODE
            CALL      RDFMCC1
            IF        OVRCD=ZERO
              MOVE      FMCCDESC,ACCOUNTN
            ENDIF
            GOTO      GETNAM99
          ENDIF
.
          MATCH     COSTCODE,COSTHEAD
          IF        @EQUAL
            PACK      KEY14,FMAMLEDG,SUBJCODE,SP70
            CALL      RDFMSA1
            IF        OVRCD=0
              MOVE      FMSADESC,ACCOUNTN
            ENDIF
          ENDIF
.
GETNAM99  RETURN
.
