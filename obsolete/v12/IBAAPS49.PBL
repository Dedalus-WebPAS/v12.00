. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAAPS49                                             *
. * Desc      :   Unpresent Cheques                                    *
. **********************************************************************
. * Date      :   18.12.96                                             *
. * Mods      :                                                        *
. **********************************************************************
. *V10.03.01  27/02/2013  Ania P              CAR 281021               *
. *           RECOMPILED for FMSBNKKY                                  *
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
. * VF.09.01  28.02.2001 Sandra Barcham                                *
. *           Fixed 2665 I * C                                         *
. *           srf 8897                                                 *
. **********************************************************************
. * VF.00.03  28.04.2000 Sandra Barcham                                *
. *           Recompiled for FMSBNKKY change                           *
. * VF.00.02  13.01.2000 Sandra Barcham                                *
. *           Recompiled for FMSBNKKY change                           *
. * VF.00.01  05.01.2000 Charles Handaya                               *
. *           Recompiled for APSMASIO change                           *
. **********************************************************************
.$$$$$$
.        PROGRAM IBAAPS49
.             open files and initialise variables
.             WHILE user wants to view data DO
.                  get desired cheque account, cheque number
.                  allow user to view data
.             END WHILE
.        END
.$$$$$$
. Standard FMS Accounting System Include
.---------------------------------------
          INC       FMSSTDDF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
.
          INC       APSMASFD/INC
          INC       FMSBNKFD/INC       * bank rec
          INC       FMSCHQFD/INC       * cheque
          INC       FMSTRNFD/INC       * transaction
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
FORMATP2  INIT      "(99,999,999,999.99 "   * format variables
FORMATPA  INIT      "(9,999,999.99 "
F11P2     FORM      11.2
.
STATUS    INIT      "           "
STATUS0   INIT      "Unpresented"
STATUS1   INIT      "Presented  "
STATUS2   INIT      "Cancelled  "
STATUS3   INIT      "Not Used   "
.
FMST      INIT      "fmst"   * file prefix for transaction file
STARTB    FORM      "7"      * constant line to print heading for screen B
MAXITEMB  FORM      "15"     * maximum no. of items for screen B
Z20       INIT      "ZZZZZZZZZZZZZZZZZZZZ"
.
ACCEPT    FORM      1        * accept mode (1=on)
CANDATE   DIM       10       * transaction date
CORREF    DIM       15
CREDITOR  DIM       5        * creditor
...DIM14     DIM       14       * general dim 14
FILENAM   DIM       8        * filename
FILENAMB  INIT      "        "         * file open for screen B
PAGENO    FORM      3        * current page number
SCRNO     FORM      1        * current screen number
TRNDATE   DIM       10       * transaction date
.
PRGID     INIT      "IBAAPS49"
PRGNAM    INIT      "Unpresent Cheques"
VERSION   INIT      "V12.00.00"
.
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML1000    CALL      PROC0000           * get key
          BRANCH    EXIT,ML9999        * exit program
.
ML2000    CALL      SELA0000           
          GOTO      ML1000
.
ML9999    CHAIN     PGM                * chain back to menu
.
.******************************************************************************
.* INIT - Open Files                             Called by ML0000             *
.******************************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CCENTRY
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
.
          DISPLAY   *P56:24,"Opening apsmasaf";
          OPEN      APSMASA1,"apsmasaf"
.
          DISPLAY   *P64:24,"fmsbnkaf";  * bank rec
          OPEN      FMSBNKA2,"fmsbnkaf"
.
          DISPLAY   *P64:24,"fmsbnkaf";  * bank rec
          OPEN      FMSBNKA3,"fmsbnkaf"
.
          DISPLAY   *P64:24,"fmschqaf";  * cheque
          OPEN      FMSCHQA1,"fmschqaf"
.
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.**********************************************************************
.  PROC - Keyin cheque account and number              Called By ML
.        Returns  - FMCHCHQ  (bank account)
.                   CHEQNO   (cheque number)
.                   CRECNO   (cheque number)
.**********************************************************************
PROC0000  CALL      RCHQFM00                * display screen
          MOVE      "28",CCOL
          MOVE      "4",CVERT
          CALL      KCHQFM00                * get cheque account
          BRANCH    EXIT,PROC9500,PROC9500  * exit program ?
          DISPLAY   *P28:4,*V2LON,FMCHCHQ,*HOFF,SP2,FMCHDES;
.
PROC9000  MOVE      ZERO,EXIT          * continue
          GOTO      PROC9999
.
PROC9500  MOVE      ONE,EXIT           * quit
.
PROC9999  RETURN
.******************************************************************************
.* SCRA - Display Screen A                       Called by SELA, redisps      *
.******************************************************************************
SCRA0000  MOVE      ZERO,KAMOUNT            * clear amount
          PACK      KEY36,FMCHCHQ,ZERO,CRECNO,SP70
          PACK      KEY31,FMCHCHQ,ZERO,CRECNO,SP70
          CALL      RSFMBN2                 * calc amount
.
SCRA1100  CALL      RKFMBN2                 * get next record
          BRANCH    OVRCD,SCRA1200          * no more records ?
          PACK      KEY36,FMBNBNK,FMBNTYP,FMBNREF,SP70     * build current key
          MATCH     KEY36,KEY31
          GOTO      SCRA1200 IF NOT EQUAL   * out of range of desired records ?
.
          MATCH     "1",FMBNPRE
          IF        !@EQUAL
            MOVE      "Cheque must already be presented - ",DISPMSG
            CALL      MESSAGE1
            GOTO      SCRA9500
          ENDIF
.
          ADD       FMBNAMT,KAMOUNT         * total amounts
          UNPACK    FMBNDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * get dates
          MOVE      CPCDATE,TRNDATE
.
          UNPACK    FMBNPDT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,CANDATE
.
          MOVE      FMBNNAM,APMACN1         * get creditor name
          PACK      KEY5,FMBNCRE,SP70
          CALL      RDAPMA1
          LOAD      FMBNCRE,OVRCD,SP70
          MOVE      FMBNCRE,CREDITOR
.
          MOVE      FMBNPRE,F1              * get status
          MOVE      STATUS0,STATUS
          LOAD      STATUS,F1,STATUS1,STATUS2
          COMPARE   TWO,F1
          GOTO      SCRA1100 IF NOT EQUAL   * cancelled cheque ?
          COMPARE   ZERO,KAMOUNT
          GOTO      SCRA1100 IF NOT EQUAL   * zero amount ?
          MOVE      STATUS3,STATUS          * this is an unused cheque
          GOTO      SCRA1100
.
SCRA1200  CALL      RPFMBN2
          CALL      FORM0000                * format number
.
          DISPLAY   *P28:5,*EF,*V2LON,CHEQNO,*HOFF:
                    *P5:7,"Transaction Date     : ",*V2LON,TRNDATE,*HOFF:
                    *P5:8,"Status               : ",*V2LON,STATUS,*HOFF:
                    *P5:9,"Presented/Cancel Date: ",*V2LON,CANDATE,*HOFF:
                   *P5:10,"Amount               : ",*V2LON,DIM19,*HOFF:
                   *P5:11,"Creditor             : ":
                          *V2LON,CREDITOR,*HOFF,SP2,APMACN1;
.
SCRA9000  MOVE      ZERO,EXIT
          GOTO      SCRA9999
.
SCRA9500  MOVE      ONE,EXIT
.
SCRA9999  RETURN
.******************************************************************************
.* SELA - Perform Screen A                       Called by ML                 *
.*        Returns : EXIT     (1=exit program)                                 *
.******************************************************************************
.
SELA0000  DISPLAY   *P1:6,*EF
          MOVE      "28",CCOL
          MOVE      "5",CVERT
          MOVE      ZERO,CCHEQREC           * set for cheques
          CALL      KBNKFM00                * get cheque number
          BRANCH    EXIT,SELA9500,SELA9500  * nothing entered ?
.
          CALL      SCRA0000           * display screen A
.
SELA0100  KEYIN     *P1:24,*EL,"(",*V2LON,"U",*HOFF,")npresent Cheque , e(":
                    *V2LON,"X",*HOFF,")it ? ",*EL,*V2LON,ANS;
          PACK      ANS,ANS,SP70
          REP       UPPLOW,ANS
.
          MATCH     " ",ANS
          GOTO      SELA0000 IF EQUAL
          MATCH     "X",ANS            * exit screen ?
          GOTO      SELA9500 IF EQUAL
          MATCH     "U",ANS            * unpresent details ?
          GOTO      SELA7000 IF EQUAL
.
SELA5000  BEEP                         * illegal option
          GOTO      SELA0100
.
SELA7000  PACK      KEY36,FMCHCHQ,ZERO,CRECNO,SP70
          PACK      KEY31,FMCHCHQ,ZERO,CRECNO,SP70
          CALL      RSFMBN2                 * calc amount
.
SELA7500  CALL      RKFMBN2                 * get next record
          BRANCH    OVRCD,SELA0000          * no more records ?
          PACK      KEY36,FMBNBNK,FMBNTYP,FMBNREF,SP70     * build current key
          MATCH     KEY36,KEY31
          GOTO      SELA0000 IF NOT EQUAL   * out of range of desired records ?
.
          MOVE      ZERO,FMBNPRE
          MOVE      SP8,FMBNPDT
          CALL      UPFMBN2
          GOTO      SELA7500
.
SELA9500  MOVE      ZERO,EXIT
.
SELA9999  RETURN
.******************************************************************************
.* FORM - Format  amount                         Called by lots               *
.*       Requires : KAMOUNT       (amount)                                    *
.*       Returns  : DIM19         (formatted 19 char)                         *
.*                  DIM14         (formatted 14 char)                         *
.******************************************************************************
FORM0000  MOVE      KAMOUNT,F11P2
.
          MOVE      FORMATP2,DIM19
          FEDIT     F11P2,DIM19
.
          MOVE      FORMATPA,DIM14
          FEDIT     F11P2,DIM14
.
FORM9999  RETURN
.******************************************************************************
.* Redisplays                                                                 *
.******************************************************************************
RCHQFM00  DISPLAY   *P1:3,*EF:
                    *P5:4,"Cheque Account       : ":
                    *P5:5,"Cheque Number        : ";
          RETURN
.
RBNKFM00  DISPLAY   *P1:3,*EF:
                    *P5:4,"Cheque Account       : ":
                    *P28:4,*V2LON,FMCHCHQ,*HOFF,SP2,FMCHDES:
                    *P5:5,"Cheque Number        : ";
          RETURN
.
.******************************************************************************
.* INCLUDES FOR I/O'S                                                         *
.******************************************************************************
.
          INC       APSMASIO/INC
          INC       FMSBNKIO/INC       * bank rec
          INC       FMSCHQIO/INC       * cheque
          INC       FMSTRNIO/INC       * transaction
          INC       FMSCHQKY           * cheque
          INC       FMSBNKKY           * cheque no
          INC       FMSSTDCD           * FMS Acc. standard routines
