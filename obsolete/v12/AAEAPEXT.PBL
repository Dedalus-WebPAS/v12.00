.******************************************************************************
.* System         : Accident and Emergency                                    *
.*                                                                            *
.* Include Name   : AAEAPEXT.PBL                                              *
.*                                                                            *
.* Function       : Keyin extra details for Category AP if required           *
.*                                                                            *
.* Author         : Justin Coates      14/09/1993                             *
.*                                                                            *
.* Parameters     : AEDAPLIN      place of incident (Cat AP)                  *
.*                  RTAOVRCD      0=RTA details exist                         *
.*                  AAERTAFD, AAESAWFD, PATCODFD files open                   *
.*                                                                            *
.* Modifications  :                                                           *
.*        V5.06.01  31/07/1998  Greg Horvat      98/99 PRS2 Mods              *
.*                  Added AUTOPCOD - Automatic generation of the postcode     *
.*                                                                            *
.*                  13/12/93  ROD                                             *
.*                  Added Driver/Passenger to screen  SRF 440505              *
.*                  18/01/94  ROD                                             *
.*                  Remove traces of the EMPLWORK.txt file    SRF 440522      *
.******************************************************************************
AAEAPEXT  MATCH     SP3,AEDAPLIN
          GOTO      AEXAP900 IF EQUAL       * nothing entered
.
          PACK      KEY5,ANSA,ANSP,AEDAPLIN
          CALL      RDCODE1
          BRANCH    OVRCD,AEXAP900          * invalid code
.
.         see if any indicators set
.
          MATCH     ANSR,TCINDC1
          GOTO      AEXAP100 IF EQUAL       * RTA accident
.
          MOVE      ANSW,AESWQTYP           * display work
          MOVE      "WORK ACCIDENT",KEY17
          MATCH     ANSW,TCINDC1
          GOTO      AEXAP500 IF EQUAL       * Workcare accident
.
          MOVE      ANSS,AESWQTYP           * display schools
          MOVE      "SCHOOL ACCIDENT",KEY17
          MATCH     ANSS,TCINDC1
          GOTO      AEXAP500 IF EQUAL       * School accident
          GOTO      AEXAP900
.
. ******* RTA details *******
.
AEXAP100  CALL      XAPRTD00                * display RTA screen/details
          COMPARE   ZERO,RTAOVRCD
          GOTO      AEXAP200 IF EQUAL       * go to change mode
.
          CALL      XAPRTI00                * insert details
          BRANCH    OVRCD,AEXAP995          * cancel 
.
AEXAP200  CALL      XAPRTC00                * change details
          GOTO      AEXAP995
.
. ******* Workcare/School accident *******
.
AEXAP500  CALL      XAPSWD00                * display Work/School details
          BRANCH    FORM1,AEXAP600
.
          CALL      XAPSWI00                * insert details
          BRANCH    EXIT,AEXAP995           * cancel 
.
AEXAP600  CALL      XAPSWC00                * change details
          GOTO      AEXAP995
.
. ******* shouldnt have any details *******
.
AEXAP900  CALL      XAPCSW00                * clear temp variables
          CALL      XAPFSW00                * copy temp -> file variables
          MOVE      SP6,AEDAEMPL
          MOVE      SP30,ADAEMPL
          PACK      ADAADD1,SP20,SP20
          PACK      ADAADD2,SP20,SP20
          PACK      ADASUBR,SP20,SP20
          PACK      ADAADD4,SP20,SP20
          MOVE      SP20,ADAPOST
          MOVE      SP20,ADATELE
          MOVE      SP30,ADACONT
          GOTO      AEXAP999
.
AEXAP995  CALL      PUTSCR00
AEXAP999  RETURN
+
.*********************************************************************
.         display the RTA screen and current details if any
.*********************************************************************
XAPRTD00  MOVE      "25",HLEF
          MOVE      FOUR,HTOP
          MOVE      "79",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          MOVE      "RTA ACCIDENT DETAILS",BOXHDR
          CALL      COMDRBOX
          DISPLAY   *P26:05,*V2LON," 1",*HOFF,". Date      :             ":
                            *V2LON," 2",*HOFF,". Time : ":
                    *P26:06,*V2LON," 3",*HOFF,". Place     : ":
                    *P26:07,*V2LON," 4",*HOFF,". Driver/Passenger : ":
                    *P48:08,*V2LON,*ULON,"Vehicle 1",*HOFF:
                    *P26:09,*V2LON," 5",*HOFF,". Driver    : ":
                    *P26:10,*V2LON," 6",*HOFF,". Address   : ":
                    *P26:11,*V2LON," 7",*HOFF,".           : ":
                    *P26:12,*V2LON," 8",*HOFF,".           : ":
                    *P26:13,*V2LON," 9",*HOFF,".           : ":
                    *P26:14,*V2LON,"10",*HOFF,". Post Code :          ":
                            *V2LON,"11",*HOFF,". Vehicle Reg. : ":
                    *P48:15,*V2LON,*ULON,"Vehicle 2",*HOFF:
                    *P26:16,*V2LON,"12",*HOFF,". Driver    : ":
                    *P26:17,*V2LON,"13",*HOFF,". Address   : ":
                    *P26:18,*V2LON,"14",*HOFF,".           : ":
                    *P26:19,*V2LON,"15",*HOFF,".           : ":
                    *P26:20,*V2LON,"16",*HOFF,".           : ":
                    *P26:21,*V2LON,"17",*HOFF,". Post Code :          ":
                            *V2LON,"18",*HOFF,". Vehicle Reg. : "
.
.         display the details if any
.
          PACK      AERTDATE,AERTDATE,SP8
          PACK      AERTTIME,AERTTIME,SP5
          BRANCH    RTAOVRCD,XAPRTD99       * no RTA details
.
          UNPACK    AERTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      SP20,TDESC
          MATCH     SP3,AEDARTAS
          IF        !@EQUAL
            MOVE      "<Missing>",TDESC
            PACK      KEY5,ANSA,ANSR,AEDARTAS
            CALL      RDCODE1
          ENDIF
.
          DISPLAY   *V2LON,*P42:5,CPCDATE,*P65:5,AERTTIME,*P42:6,AERTLOCN:
                    *P49:7,AEDARTAS,*HOFF,*P54:7,TDESC,*V2LON:
                    *P42:9,AERTADRV,*P42:10,AERTAAD1,*P42:11,AERTAAD2:
                    *P42:12,AERTAAD3,*P42:13,AERTAAD4,*P42:14,AERTAPST:
                    *P70:14,AERTAREG:
                    *P42:16,AERTBDRV,*P42:17,AERTBAD1,*P42:18,AERTBAD2:
                    *P42:19,AERTBAD3,*P42:20,AERTBAD4,*P42:21,AERTBPST:
                    *P70:21,AERTBREG
.
XAPRTD99  RETURN
+
.*********************************************************************
.         insert mode for RTA
.*********************************************************************
XAPRTI00  MOVE      ONE,CCITEM
          MOVE      ZERO,OVRCD
          WHILE     CCITEM <= 18
            MOVE      ZERO,EXIT
            PERFORM   CCITEM,KAPRTA00,KAPRTB00,KAPRTC00:
                             KDRPAS00,KAPRTD00,KAPRTE00:
                             KAPRTF00,KAPRTG00,KAPRTH00,KAPRTI00,KAPRTJ00:
                             KAPRTK00,KAPRTL00,KAPRTM00,KAPRTN00,KAPRTO00:
                             KAPRTP00,KAPRTQ00
            BRANCH    OVRCD,XAPRTI99        * no date entered
            BRANCH    EXIT,XAPRTI99         * no driver 2 entered
            ADD       ONE,CCITEM
          DO
.
XAPRTI99  RETURN
+
.*********************************************************************
.         change mode for RTA
.*********************************************************************
XAPRTC00  MOVE      "23",CVERT
          MOVE      "26",CCOL
          CALL      MAEAP000
          COMPARE   ZERO,CCITEM
          GOTO      XAPRTC90 IF EQUAL       * Post
          GOTO      XAPRTC95 IF LESS        * Cancel
.
XAPRTC30  MOVE      ZERO,OVRCD
          MOVE      ZERO,EXIT
          PERFORM   CCITEM,KAPRTA00,KAPRTB00,KAPRTC00:
                           KDRPAS00,KAPRTD00,KAPRTE00:
                           KAPRTF00,KAPRTG00,KAPRTH00,KAPRTI00,KAPRTJ00:
                           KAPRTK00,KAPRTL00,KAPRTM00,KAPRTN00,KAPRTO00:
                           KAPRTP00,KAPRTQ00
          BRANCH    OVRCD,XAPRTC30          * force date keyin
          PERFORM   CCITEM,KAPRTB00         * enter time also if change date
          GOTO      XAPRTC00
.
.         update details if they exist and clear school/work details
.
XAPRTC90  IF        RTAOVRCD = ZERO
            CALL      UPAERTA1
          ENDIF
          CALL      XAPCSW00                * clear school/work temp vars
          CALL      XAPFSW00                * copy temp -> file vars
          GOTO      XAPRTC99
.
.         cancel selected, if no details originally, clear date
.
XAPRTC95  LOAD      AERTDATE,RTAOVRCD,SP8
.
XAPRTC99  RETURN
+
.*********************************************************************
.         RTA keyins
.*********************************************************************
KAPRTA00  MOVE      "42",CCOL
          MOVE      "05",CVERT
          MOVE      ONE,CDEFDTE
          UNPACK    AERTDATE,CCENT,CYEAR,CMON,CDAY
          MATCH     SP2,CCENT
          IF        @EQUAL
            MOVE      CCC,CCENT
          ENDIF
.
          CALL      KEYDATE
          BRANCH    CQUEST,KAPRTA00
          BRANCH    OVRCD,KAPRTA95
.
          PACK      AERTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",AERTDATE
          MATCH     AERTDATE,TODAY
          GOTO      KAPRTA99 IF NOT LESS    * date OK
.
          KEYIN     *P26:23,*B,"Date can't be into the future.  Hit <":
                    *V2LON,"ENTER",*HOFF,">    ",*EOFF,ANS;
          DISPLAY   *P26:23,SP20,SP20,SP10,SP3
          GOTO      KAPRTA00
.
KAPRTA95  MOVE      SP8,AERTDATE            * clear the date
.
KAPRTA99  RETURN
+
KAPRTB00  MOVE      "65",CCOL
          MOVE      "05",CVERT
          UNPACK    AERTTIME,CHOUR,ANS,CMIN
          MOVE      SP5,AERTTIME
.
          CALL      KEYTIME
          BRANCH    CQUEST,KAPRTA00
          BRANCH    OVRCD,KAPRTA90
.
          PACK      AERTTIME,CHOUR,COLON,CMIN
          REP       " 0",AERTTIME
          MATCH     AERTDATE,TODAY
          GOTO      KAPRTB99 IF NOT EQUAL   * not current date
.
          CLOCK     TIME,KEY5
          MATCH     AERTTIME,KEY5
          GOTO      KAPRTB99 IF NOT LESS    * time OK
.
          KEYIN     *P26:23,*B,"Time can't be into the future.  Hit <":
                    *V2LON,"ENTER",*HOFF,">    ",*EOFF,ANS;
          DISPLAY   *P26:23,SP20,SP20,SP10,SP3
          GOTO      KAPRTB00
.
KAPRTA90  MOVE      ZERO,OVRCD
KAPRTB99  RETURN
.
KAPRTC00  KEYIN     *V2LON,*P42:6,*RV,AERTLOCN
          PACK      AERTLOCN,AERTLOCN,SP100
          RETURN
+
.*********************************************************************
.*  KDRPAS00  :  Keyin Driver/Passenger                              *
.*********************************************************************
KDRPAS00  PACK      CODE,ANSA,ANSR
          MOVE      SP3,CKYIDEF3
          LOAD      CKYIDEF3,YESDFLTS,AEDARTAS
          MOVE      "49",ECOL
          MOVE      "7",EVERT
          MOVE      ZERO,CKYIMAND
.
. save screen in case of EXITCHAR
.
          MOVE      ZERO,HLEF
          CALL      GETSCR00
.
          CALL      PATCODKY
          COMPARE   TWO,EXIT                      * EXITCHAR ?
          GOTO      KDRPAS90 IF EQUAL
          MOVE      ACODE,AEDARTAS
          DISPLAY   *P54:7,TDESC
.
          CALL      FRESCR00                      * no EXITCHAR input
          MOVE      ZERO,EXIT
          GOTO      KDRPAS99
.
KDRPAS90  MOVE      SP3,AEDARTAS
          MOVE      SP20,TDESC
          CALL      PUTSCR00
          DISPLAY   *P49:7,SP5,SP20
          MOVE      ONE,EXIT                      * EXITCHAR
.
KDRPAS99  RETURN
+
.*********************************************************************
.         enter the driver 1 name
.         if <ENTER> hit, default to PMI details
.*********************************************************************
KAPRTD00  KEYIN     *V2LON,*P42:9,*RV,AERTADRV 
          RESET     AERTADRV
          GOTO      KAPRTD70 IF NOT EOS     * return not entered
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,AERTADRV
          MOVE      PADD1,AERTAAD1
          MOVE      PADD2,AERTAAD2
          MOVE      PSUBRB,AERTAAD3
          MOVE      PADD4,AERTAAD4
          MOVE      PPOST,AERTAPST
          DISPLAY   *V2LON,*P42:09,AERTADRV,*P42:10,AERTAAD1,*P42:11,AERTAAD2:
                           *P42:12,AERTAAD3,*P42:13,AERTAAD4,*P42:14,AERTAPST
          MOVE      NINE,CCITEM             * set so keyins in reg.
KAPRTD70  PACK      AERTADRV,AERTADRV,SP100
          RETURN
+
.*********************************************************************
.         enter the address line 1
.         if <ENTER> hit, default to PMI address
.*********************************************************************
KAPRTE00  KEYIN     *V2LON,*P42:10,*RV,AERTAAD1
          RESET     AERTAAD1
          GOTO      KAPRTE40 IF NOT EOS     * something entered
.
          MOVE      PADD1,AERTAAD1
          MOVE      PADD2,AERTAAD2
          MOVE      PSUBRB,AERTAAD3
          MOVE      PADD4,AERTAAD4
          MOVE      PPOST,AERTAPST
          DISPLAY   *V2LON,*P42:09,AERTADRV,*P42:10,AERTAAD1,*P42:11,AERTAAD2:
                           *P42:12,AERTAAD3,*P42:13,AERTAAD4,*P42:14,AERTAPST
          GOTO      KAPRTE70
.
KAPRTE40  PACK      AERTAAD1,AERTAAD1,SP100
          MATCH     SP30,AERTAAD1
          GOTO      KAPRTE99 IF NOT EQUAL     * something entered
.
KAPRTE70  MOVE      NINE,CCITEM             * set so keyins in reg.
KAPRTE99  RETURN
.
KAPRTF00  KEYIN     *V2LON,*P42:11,*RV,AERTAAD2
          PACK      AERTAAD2,AERTAAD2,SP100
.
          MOVE      TWO,SUBKYLIN            * 2nd address line keyin
          PACK      IBAADLN2,AERTAAD2,SP30,SP30
          PACK      IBAADLN3,AERTAAD3,SP30,SP30
          PROC      AUTOPCOD                * Auto generation of the postcode
          BRANCH    EXIT,KAPRTF00,KAPRTF99
.
          MOVE      IBAPCOD,AERTAPST
          DISPLAY   *P42:14,*V2LON,AERTAPST;
KAPRTF99  RETURN
.
KAPRTG00  KEYIN     *V2LON,*P42:12,*RV,AERTAAD3
          PACK      AERTAAD3,AERTAAD3,SP100
.
          MOVE      THREE,SUBKYLIN          * 3rd address line keyin
          PACK      IBAADLN2,AERTAAD2,SP30,SP30
          PACK      IBAADLN3,AERTAAD3,SP30,SP30
          PROC      AUTOPCOD                * Auto generation of the postcode
          BRANCH    EXIT,KAPRTG00,KAPRTG99
.
          MOVE      IBAPCOD,AERTAPST
          DISPLAY   *P42:14,*V2LON,AERTAPST;
KAPRTG99  RETURN
.
KAPRTH00  KEYIN     *V2LON,*P42:13,*RV,AERTAAD4
          PACK      AERTAAD4,AERTAAD4,SP100
          RETURN
KAPRTI00  KEYIN     *V2LON,*P42:14,*RV,AERTAPST
          PACK      AERTAPST,AERTAPST,SP100
          RETURN
KAPRTJ00  KEYIN     *V2LON,*P70:14,*RV,AERTAREG
          PACK      AERTAREG,AERTAREG,SP100
          RETURN
KAPRTK00  KEYIN     *V2LON,*P42:16,*RV,AERTBDRV
          PACK      AERTBDRV,AERTBDRV,SP100
          MATCH     SP30,AERTBDRV
          IF        @EQUAL
            MOVE      ONE,EXIT                * set to go to change mode
          ENDIF
          RETURN
KAPRTL00  KEYIN     *V2LON,*P42:17,*RV,AERTBAD1
          PACK      AERTBAD1,AERTBAD1,SP100
          RETURN
.
KAPRTM00  KEYIN     *V2LON,*P42:18,*RV,AERTBAD2
          PACK      AERTBAD2,AERTBAD2,SP100
.
          MOVE      TWO,SUBKYLIN            * 2nd address line keyin
          PACK      IBAADLN2,AERTBAD2,SP30,SP30
          PACK      IBAADLN3,AERTBAD3,SP30,SP30
          PROC      AUTOPCOD                * Auto generation of the postcode
          BRANCH    EXIT,KAPRTM00,KAPRTM99
.
          MOVE      IBAPCOD,AERTBPST
          DISPLAY   *P42:21,*V2LON,AERTBPST;
KAPRTM99  RETURN
.
KAPRTN00  KEYIN     *V2LON,*P42:19,*RV,AERTBAD3
          PACK      AERTBAD3,AERTBAD3,SP100
.
          MOVE      THREE,SUBKYLIN          * 3rd address line keyin
          PACK      IBAADLN2,AERTBAD2,SP30,SP30
          PACK      IBAADLN3,AERTBAD3,SP30,SP30
          PROC      AUTOPCOD                * Auto generation of the postcode
          BRANCH    EXIT,KAPRTN00,KAPRTN99
.
          MOVE      IBAPCOD,AERTBPST
          DISPLAY   *P42:21,*V2LON,AERTBPST;
KAPRTN99  RETURN
.
KAPRTO00  KEYIN     *V2LON,*P42:20,*RV,AERTBAD4
          PACK      AERTBAD4,AERTBAD4,SP100
          RETURN
KAPRTP00  KEYIN     *V2LON,*P42:21,*RV,AERTBPST
          PACK      AERTBPST,AERTBPST,SP100
          RETURN
KAPRTQ00  KEYIN     *V2LON,*P70:21,*RV,AERTBREG
          PACK      AERTBREG,AERTBREG,SP100
          RETURN
+
.*********************************************************************
.         display the screen for School/Work details
.*********************************************************************
XAPSWD00  MOVE      "27",HLEF
          MOVE      "12",HTOP
          MOVE      "79",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          MOVE      KEY17,BOXHDR
          CALL      COMDRBOX
          DISPLAY   *P28:13,*V2LON,"1",*HOFF,". Code      : ":
                    *P28:14,*V2LON,"2",*HOFF,". Name      : ":
                    *P28:15,*V2LON,"3",*HOFF,". Address   : ":
                    *P28:16,*V2LON,"4",*HOFF,".           : ":
                    *P28:17,*V2LON,"5",*HOFF,".           : ":
                    *P28:18,*V2LON,"6",*HOFF,".           : ":
                    *P28:19,*V2LON,"7",*HOFF,". Post Code : ":
                    *P28:20,*V2LON,"8",*HOFF,". Telephone : ":
                    *P28:21,*V2LON,"9",*HOFF,". Contact   : ":
                    *P43:13,*V2LON,AEDAEMPL,*P43:14,ADAEMPL,*P43:15,ADAADD1:
                    *P43:16,ADAADD2,*P43:17,ADASUBR,*P43:18,ADAADD4:
                    *P43:19,ADAPOST,*P43:20,ADATELE,*P43:21,ADACONT
.
          CALL      XAPCSW00                * clear the temp variables
          MOVE      ZERO,FORM1              * default to having no details
          MATCH     SP30,ADAEMPL
          GOTO      XAPSWD99 IF EQUAL
.
          MOVE      ONE,FORM1               * have current details
          MOVE      AEDAEMPL,CDSWCODE       * Set up temp variables
          MOVE      ADAEMPL,CDSWNAME
          MOVE      ADAADD1,CDSWADD1
          MOVE      ADAADD2,CDSWADD2
          MOVE      ADASUBR,CDSWADD3
          MOVE      ADAADD4,CDSWADD4
          MOVE      ADAPOST,CDSWPOST
          MOVE      ADATELE,CDSWTELE
          MOVE      ADACONT,CDSWCONT
XAPSWD99  RETURN
+
.*********************************************************************
.         enter details for school/work file
.*********************************************************************
XAPSWI00  CALL      KAPSWA00                * enter works code
          BRANCH    EXIT,XAPSWI99           * nothing entered
          BRANCH    CEXIT,XAPSWI99          * code entered
.
.         enter the details
.
          MOVE      TWO,CCITEM
          WHILE     CCITEM <= NINE
            PERFORM   CCITEM,KAPSWA00,KAPSWB00,KAPSWC00,KAPSWD00,KAPSWE00:
                             KAPSWF00,KAPSWG00,KAPSWH00,KAPSWI00
            ADD       ONE,CCITEM
          DO
.
XAPSWI99  RETURN
+
.*********************************************************************
.         change the work/school details
.*********************************************************************
XAPSWC00  MOVE      "23",CVERT
          MOVE      "28",CCOL
          CALL      MAEAP000
          COMPARE   ZERO,CCITEM
          GOTO      XAPSWC70 IF EQUAL       * Post
          GOTO      XAPSWC99 IF LESS        * Cancel
.
XAPSWC30  MOVE      ZERO,EXIT 
          PERFORM   CCITEM,KAPSWA00,KAPSWB00,KAPSWC00,KAPSWD00,KAPSWE00:
                           KAPSWF00,KAPSWG00,KAPSWH00,KAPSWI00
          GOTO      XAPSWC00
.
.         check that have a valid code for school/work
.
XAPSWC70  MATCH     SP6,CDSWCODE
          GOTO      XAPSWC80 IF EQUAL       * no code entered
.  
          PACK      KEY7,AESWQTYP,CDSWCODE
          CALL      RAAESAW1
          COMPARE   ZERO,OVRCD
          GOTO      XAPSWC80 IF EQUAL       * valid code
.
          KEYIN     *P28:23,*B,"Code ",*V2LON,*DV,CDSWCODE,*HOFF," is ":
                    "invalid.  Hit <",*V2LON,"ENTER",*HOFF,"> to continue":
                    *EOFF,ANS;
          DISPLAY   *P28:23,SP20,SP20,SP10
          GOTO      XAPSWC00
.
XAPSWC80  CALL      XAPFSW00                * copy temp -> file variables
.
XAPSWC99  RETURN
+
.*********************************************************************
.         work/school keyins
.*********************************************************************
KAPSWA00  MOVE      "43",ECOL
          MOVE      "13",EVERT
          MOVE      AEDAEMPL,CKYIDEF6       * set default code
          CALL      KYAAESAW                * enter work/school details
.
          MOVE      ZERO,CEXIT              * set to keyin details
          IF        EXIT = ZERO
            MOVE      AESWCODE,CDSWCODE
            MOVE      AESWNAME,CDSWNAME
            MOVE      AESWADD1,CDSWADD1
            MOVE      AESWADD2,CDSWADD2
            MOVE      AESWADD3,CDSWADD3
            MOVE      AESWADD4,CDSWADD4
            MOVE      AESWPOST,CDSWPOST
            MOVE      AESWTELE,CDSWTELE
            MOVE      AESWCONT,CDSWCONT
            MOVE      ONE,CEXIT               * set to avoid keyins
          ELSE
            CALL      XAPCSW00                * clear the temp variables
          ENDIF
          DISPLAY   *P43:13,*V2LON,CDSWCODE,*P43:14,CDSWNAME,*P43:15,CDSWADD1:
                    *P43:16,CDSWADD2,*P43:17,CDSWADD3,*P43:18,CDSWADD4:
                    *P43:19,CDSWPOST,*P43:20,CDSWTELE,*P43:21,CDSWCONT
.
KAPSWA99  RETURN
.
KAPSWB00  KEYIN     *V2LON,*P43:14,*RV,CDSWNAME
          PACK      ADAEMPL,ADAEMPL,SP100
          RETURN
KAPSWC00  KEYIN     *V2LON,*P43:15,*RV,CDSWADD1
          PACK      CDSWADD1,CDSWADD1,SP100
          RETURN
KAPSWD00  KEYIN     *V2LON,*P43:16,*RV,CDSWADD2
          PACK      CDSWADD2,CDSWADD2,SP100
          RETURN
KAPSWE00  KEYIN     *V2LON,*P43:17,*RV,CDSWADD3
          PACK      CDSWADD3,CDSWADD3,SP100
          RETURN
KAPSWF00  KEYIN     *V2LON,*P43:18,*RV,CDSWADD4
          PACK      CDSWADD4,CDSWADD4,SP100
          RETURN
KAPSWG00  KEYIN     *V2LON,*P43:19,*RV,CDSWPOST
          PACK      CDSWPOST,CDSWPOST,SP100
          RETURN
KAPSWH00  KEYIN     *V2LON,*P43:20,*RV,CDSWTELE
          PACK      CDSWTELE,CDSWTELE,SP100
          RETURN
KAPSWI00  KEYIN     *V2LON,*P43:21,*RV,CDSWCONT
          PACK      CDSWCONT,CDSWCONT,SP100
          RETURN
+
.*********************************************************************
.         clear the temp variables for school/work
.*********************************************************************
XAPCSW00  MOVE      SP30,CDSWCODE
          MOVE      SP30,CDSWNAME
          PACK      CDSWADD1,SP30,SP30
          PACK      CDSWADD2,SP30,SP30
          PACK      CDSWADD3,SP30,SP30
          PACK      CDSWADD4,SP30,SP30
          MOVE      SP20,CDSWPOST
          MOVE      SP20,CDSWTELE
          MOVE      SP30,CDSWCONT
          RETURN
+
.*********************************************************************
.          copy temp variables to file variables
.*********************************************************************
XAPFSW00  MOVE      CDSWCODE,AEDAEMPL
          MOVE      CDSWNAME,ADAEMPL
          MOVE      CDSWADD1,ADAADD1
          MOVE      CDSWADD2,ADAADD2
          MOVE      CDSWADD3,ADASUBR
          MOVE      CDSWADD4,ADAADD4
          MOVE      CDSWPOST,ADAPOST
          MOVE      CDSWTELE,ADATELE
          MOVE      CDSWCONT,ADACONT
          RETURN
+
.*********************************************************************
.         select item, (P)ost, (C)ancel ?
.*********************************************************************
MAEAP000  KEYIN     *PCCOL:CVERT,"Select item, (":
                    *V2LON,*DV,ANSP,*HOFF,")ost, (":
                    *V2LON,*DV,ANSC,*HOFF,")ancel ? ",*V2LON,*JR,CCITEMD
          PACK      CCITEMD,CCITEMD,SP3
          REP       UPPLOW,CCITEMD
.
          MOVE      ZERO,CCITEM
          MATCH     "  P",CCITEMD
          GOTO      MAEAP999 IF EQUAL
          MOVE      "-1",CCITEM
          MATCH     "  C",CCITEMD
          GOTO      MAEAP999 IF EQUAL
.
          TYPE      CCITEMD
          GOTO      MAEAP700 IF NOT EQUAL   * not a number entered
.
          MOVE      CCITEMD,CCITEM
          COMPARE   ONE,CCITEM
          GOTO      MAEAP999 IF NOT LESS
.
MAEAP700  BEEP
          GOTO      MAEAP000
.
MAEAP999  RETURN
+
