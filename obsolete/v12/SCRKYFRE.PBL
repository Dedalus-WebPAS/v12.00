.******************************************************************************
.* System         : ALL with screen painting                                  *
.*                                                                            *
.* Include Name   : SCRKYFRE.PBL                                              *
.*                                                                            *
.* Function       : Keyin a free format field allowing for defaults.          *
.*                                                                            *
.* Author         : Justin Coates                                             *
.*                                                                            *
.* Date Written   : 04/02/1992                                                *
.*                                                                            *
.* Parameters     : SCRPSTFD record                                           *
.*                  CCOL & CVERT  keyin positions                             *
.*                  KEYFIELD      default value (blank for no default)        *
.*                                                                            *
.* Modifications  : 10/02/1993    Justin Coates                               *
.*                  set up the default properly                               *
.*                  made it keyin to KEYFIELD                                 *
.******************************************************************************
.
SCRKYFRE  MOVE      ZERO,EXIT
          MOVE      KEYFIELD,VAR            * move to temp var
          SFORMAT   KEYFIELD,SCPSLEN        * format to desired length
          MOVE      VAR,KEYFIELD            * move temp var to work var
.
          MATCH     SP30,KEYFIELD
          GOTO      KEYFREE1 IF EQUAL       * no default code
.
.         keyin with default
.
          KEYIN     *PCCOL:CVERT,*DV,KEYFIELD:
                    *PCCOL:CVERT,*V2LON,*RV,KEYFIELD:
                    *PCCOL:CVERT,*DV,KEYFIELD
          GOTO      KEYFREE5
.
KEYFREE1  PACK      KEYFIELD,UNDLN20,UNDLN20,UNDLN20,UNDLN20
          DISPLAY   *PCCOL:CVERT,KEYFIELD
          PACK      KEYFIELD,SP70,SP70
          KEYIN     *PCCOL:CVERT,*DV,KEYFIELD:
                    *PCCOL:CVERT,*V2LON,KEYFIELD:
                    *PCCOL:CVERT,*DV,KEYFIELD
.
KEYFREE5  ENDSET    KEYFIELD
          APPEND    SP70,KEYFIELD
          APPEND    SP70,KEYFIELD
          RESET     KEYFIELD
.
          COMPARE   ONE,SCPSMAN
          GOTO      KEYFREE9 IF NOT EQUAL   * not a mandatory field
.
          MATCH     SP30,KEYFIELD
          GOTO      KEYFREE9 IF NOT EQUAL   * field has been entered
.
          DISPLAY   *P1:24,*B,"Field is mandatory.  ",*EL;
          CALL      HITENTER 
          GOTO      SCRKYFRE
.
KEYFREE9  RETURN
...............................................................................
