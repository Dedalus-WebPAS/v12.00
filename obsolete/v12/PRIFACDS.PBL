.  ****************************************************************************
.  *                                                                          *
.  *  Program Name     :  PRIFACDS.PBL                                        *
.  *                                                                          *
.  *  Function         :  Displays the future actions on file for a debtor    *
.  *                      using PRIFACT2                                      *
.  *                                                                          *
.  *  Author           :  Stephen Armstrong      15/10/91                     *
.  *                                                                          *
.  *  Keys             :  KEY18                                               *
.  *                                                                          *
.  *  Requirements     :  VDEBTOR - DIM8 (debtor number)                      *
.  *                      VSCANPMI - DIM 2 (scan flag)                        *
.  *                                                                          *
.  *  Returns          :  OVRCD = 1 if nothing on file                        *
.  *                                                                          *
.  *  Modifications    :  07/10/92  Steve Armstrong                           *
.  *                      Modified for alphanumeric debtor number             *
.  *                                                                          *
.  ****************************************************************************
.
PRIFACDS  DISPLAY    *P1:6,*EF,*P34:7,*V2LON,*ULON,"Future Actions":
                     *P1:8,SP3,"Date",SP3,*P12:8,"Actn Code",*P22:8,"Comments":
                     SP30,SP20
          MOVE       NINE,CVERT
.
          MOVE       "zzzzzzzz",PRFADATE
          PACK       KEY18,VDEBTOR,VSCANPMI,PRFADATE
          CALL       RSPRFA2                * see if anything is on file
          CALL       RPPRFA2  
          BRANCH     OVRCD,FACDS950
.
          MATCH      VDEBTOR,PRFADEBT            * same debtor ?
          GOTO       FACDS950 IF NOT EQUAL       * no
.
          COMPARE    VSCANPMI,PRFASCAN           * same scan flag ?
          GOTO       FACDS950 IF NOT EQUAL       * no
.
          GOTO       FACDS150
.        
.         loop through the future action file
.
FACDS100  CALL       RPPRFA2  
          BRANCH     OVRCD,FACDS900
.
          MATCH      VDEBTOR,PRFADEBT            * same debtor ?
          GOTO       FACDS900 IF NOT EQUAL       * no
.
          COMPARE    VSCANPMI,PRFASCAN           * same scan flag ?
          GOTO       FACDS900 IF NOT EQUAL       * no
.
          ADD        ONE,CVERT
.
          COMPARE    TWENTY3,CVERT          * Page full ?
          GOTO       FACDS150 IF LESS       * no
          GOTO       FACDS200               * yes
.
.         display the current record
.
FACDS150  UNPACK     PRFADATE,CCENT,CYEAR,CMON,CDAY
          CALL       PACDATE
          DISPLAY    *P1:CVERT,CPCDATE,*P12:CVERT,PRFAACTN,*P22:CVERT,PRFACOMM;
.
          GOTO       FACDS100
.
.         Page full
.
FACDS200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, ":
                    "(",*V2LON,"F",*HOFF,")irst Screen, ":
                    "(",*V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSC,ANS                * continue ?
          GOTO      FACDS900 IF EQUAL
.
          CMATCH    ANSF,ANS                * first screen ?
          GOTO      PRIFACDS IF EQUAL
.
          CMATCH    ANSN,ANS                * next screen ?
          GOTO      FACDS400 IF EQUAL
.
          BEEP                              * invalid option
          GOTO      FACDS200
.
.         Clear the screen
.
FACDS400  MOVE      NINE,CVERT
          DISPLAY   *P1:CVERT,*EF;
.
          GOTO      FACDS150
.
.         end of file reached or Continue selected so reset OVRCD flag
.
FACDS900  MOVE      ZERO,OVRCD
.
          GOTO      FACDS999
.
.         future action file is empty
.
FACDS950  DISPLAY   *P1:24,*EL,*B,"No Future Actions on file.  "; 
          CALL      HITENTER
.         
FACDS999  RETURN
+
