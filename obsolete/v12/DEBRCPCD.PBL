.---------------------------------------------------------------------------
. Include  : DEBRCPCD.PBL
.
. Function : This include contains all the routines required by the
.            Cash Receipt Input Programs for interfacing to the new
.            Sundry Accounts Rec. System.
. 
. Required FDs : DEBDBTFD/INC   Debtors Masterfile
.                DEBFTHFD/INC   Transaction Header File
.                DEBFDTFD/INC   Transaction Details File
.
.---------------------------------------------------------------------------
. DBINV000    : Invoice Validation.   
.
. Input       : INVOICE DIM 8  Contains Entered Invoice *JR
.
. Output      : EXIT = 0 Invoice Entry Ok
.                      1 Null Entry or Exit Char
.
.---------------------------------------------------------------------------
DBINV000  ENDSET    INVOICE
          APPEND    SP70,INVOICE
          RESET     INVOICE
.
          MATCH     SP70,INVOICE
          GOTO      DBINV990 IF EQUAL
.
          ENDSET    INVOICE
          CMATCH    EXITCHAR,INVOICE
          GOTO      DBINV990 IF EQUAL
.
          CMATCH    QUEST,INVOICE
          GOTO      DBINV100 IF NOT EQUAL
.
          CALL      HDBFHC00
.
          ENDSET    DBFHDOC
          APPEND    SP70,DBFHDOC
          RESET     DBFHDOC
          MATCH     SP70,DBFHDOC
          GOTO      DBINV990 IF EQUAL
.
          PACK      KEY21,DBFHDTY,DBFHDOC,DBFHDEB
          CALL      RDDBFH6
          BRANCH    OVRCD,DBINV800
          UNPACK    DBFHDOC,KEY4,INVOICE
          MOVE      INVOICE,F8
          MOVE      F8,INVOICE
          GOTO      DBINV110
.
DBINV100  RESET     INVOICE
          CALL      GETINV00                * Get Invoice Header
          BRANCH    OVRCD,DBINV800
.
DBINV110  CALL      AROUT000                * Calculate Outstanding Balance
          COMPARE   ZERO,TOTALOUT
          GOTO      DBINV900 IF NOT EQUAL
          BEEP      
          MOVE      "No Outstanding Amounts for Invoice - ",DISPMSG
          CALL      MESSAGE1
          GOTO      DBINV990
.
DBINV800  BEEP      
          MOVE      "Invalid Invoice Reference in Sundry A/R - ",DISPMSG
          CALL      MESSAGE1
          GOTO      DBINV990                * Set Exit to 1
.
DBINV900  MOVE      ZERO,EXIT
          GOTO      DBINV999
.
DBINV990  MOVE      ONE,EXIT
DBINV999  RETURN
.---------------------------------------------------------------------------
. Routine   :   HDBINV00 Invoice Help        
.-------------------------------------------------------------------------------
HDBINV00  MOVE      ZERO,CSCRSIZ
          MOVE      "15",HTOP
          MOVE      "24",HBOT
          MOVE      "01",HLEF
          MOVE      "80",HRIG
.
HDBINV04  CALL      GSCRN000
          BRANCH    EXIT,HDBINV99
          MOVE      STDCOL00,STDCOL01
          ADD       " 5",STDCOL01
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON," Invoice  "            
          MOVE      STDCOL01,STDCOL02
          ADD       "13",STDCOL02
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"Debtor                  ":
                                                   "          "
          MOVE      STDCOL02,STDCOL03
          ADD       "36",STDCOL03
          DISPLAY   *PSTDCOL03:STDROW,*V2LON,*ULON,"   Date   "
          MOVE      STDCOL03,STDCOL04
          ADD       "11",STDCOL04
          DISPLAY   *PSTDCOL04:STDROW,*V2LON,*ULON,"        Amount"   
          MOVE      STDCOL04,STDCOL05
          ADD       "12",STDCOL05
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
          GOTO      HDBINV50
.
HDBINV08  MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          MOVE      SCHFLD01,DBFHDOC 
          PACK      KEY21,ONE,DBFHDOC,SP70                          
.                                                                               
          CALL      RSDBFH6 
HDBINV10  CALL      RKDBFH6 
          BRANCH    OVRCD,HDBINV15
          MATCH     "1",DBFHDTY
          GOTO      HDBINV15 IF NOT EQUAL
          PACK      KEY21,DBFHDTY,DBFHDOC,DBFHDEB,SP70                          
          MOVE      DBFHDEB,KEY8
          CALL      RDDBDB1
          IF        OVRCD=1
            MOVE      SP70,DBDBNA1
          ENDIF
          MOVE      "999,999,999.99",KEY14
          ADD       DBFHTAX,DBFHTOT
          FEDIT     DBFHTOT,KEY14
          UNPACK    DBFHDDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MATCH     SP70,SCHFLD02
          IF        !@EQUAL
            MATCH     SCHFLD02,DBDBNA1 
            GOTO      HDBINV10 IF NOT EQUAL
          ENDIF
          MATCH     SP70,SCHFLD03
          IF        !@EQUAL
            MATCH     SCHFLD03,DBFHDDAT
            GOTO      HDBINV10 IF NOT EQUAL
          ENDIF
.
HDBINV12  ADD       ONE,XITEMNO
          ADD       ONE,STDROW
          STORE     KEY21,XITEMNO,ST1,ST2,ST3,ST4,ST5:
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
.
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,XITEMNO,DOT
          DISPLAY   *PSTDCOL01:STDROW,DBFHDOC 
          DISPLAY   *PSTDCOL02:STDROW,DBDBNA1
          DISPLAY   *PSTDCOL03:STDROW,CPCDATE 
          DISPLAY   *PSTDCOL04:STDROW,KEY14 
          COMPARE   MAXITEM,XITEMNO
          GOTO      HDBINV10 IF NOT EQUAL
.
HDBINV15  MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
HDBINV20  CALL      HSCRI000
          BRANCH    EXIT,HDBINV30,HDBINV35,HDBINV40,HDBINV45,HDBINV04:
                         HDBINV91,HDBINV50
          GOTO      HDBINV92
HDBINV30  CALL      RKDBFH6 
          BRANCH    OVRCD,HDBINV85
          PACK      KEY21,DBFHDTY,DBFHDOC,DBFHDEB,SP70                          
          CALL      RPDBFH6 
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HDBINV10
.
HDBINV35  MOVE      ST1,KEY21
          CALL      RDDBFH6 
          CALL      RPDBFH6 
          BRANCH    OVRCD,HDBINV85
          PACK      KEY21,DBFHDTY,DBFHDOC,DBFHDEB,SP70                          
.                                                                               
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
HDBINV36
          CALL      RPDBFH6 
          BRANCH    OVRCD,HDBINV37
          PACK      KEY21,DBFHDTY,DBFHDOC,DBFHDEB,SP70                          
.                                                                               
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HDBINV36 IF NOT EQUAL
HDBINV37
          MOVE      ZERO,XITEMNO
          GOTO      HDBINV10
HDBINV40
          MOVE      ST1,KEY21
          CALL      RDDBFH6 
          CALL      RPDBFH6 
          BRANCH    OVRCD,HDBINV85
          PACK      KEY21,DBFHDTY,DBFHDOC,DBFHDEB,SP70                          
.                                                                               
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HDBINV08
HDBINV45
          CALL      RKDBFH6 
          BRANCH    OVRCD,HDBINV85
          PACK      KEY21,DBFHDTY,DBFHDOC,DBFHDEB,SP70                          
.                                                                               
          SUSPEND
          CLIP      1,1,CRIG,24
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,XITEMNO
          PACK      KEY21,ONE,Z70
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
          CALL      RSDBFH6 
HDBINV46  CALL      RPDBFH6 
          BRANCH    OVRCD,HDBINV47
          PACK      KEY21,DBFHDTY,DBFHDOC,DBFHDEB,SP70                          
.                                                                               
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HDBINV46 IF NOT EQUAL
HDBINV47  MOVE      ZERO,XITEMNO
          GOTO      HDBINV10
.
HDBINV50  MOVE      ZERO,CSEARCH
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          UNPACK    SP70,SCHFLD01,SCHFLD02,SCHFLD03,SCHFLD04,SCHFLD05
          UNPACK    SP70,SCHFLD06,SCHFLD07,SCHFLD08,SCHFLD09,SCHFLD10
          BOXCLR    CLEF,CTOP,CRIG,CBOT
.
          CALL      CHKSQL00
          BRANCH    EXIT,HDBINV92
.
          DISPLAY   *PSTDCOL00:STDROW,*V2LON,*HON,"Sch"
          KEYIN     *PSTDCOL01:STDROW,*V2LON,*JR,*ZF,SCHFLD01
          RESET     SCHFLD01
          MATCH     "000000000000",SCHFLD01
          IF        @EQUAL
            MOVE      SP70,SCHFLD01
          ENDIF
.
          KEYIN     *PSTDCOL02:STDROW,*V2LON,SCHFLD02
          RESET     SCHFLD02
.
          MOVE      STDCOL03,CCOL 
          MOVE      STDROW,CVERT
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      SP2,CMON
          MOVE      SP2,CDAY
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          MOVE      ONE,CCANLDTE     *  1 Default dates cancelled by a space 
          CALL      KEYDATE
          IF        OVRCD=0
            PACK      SCHFLD03,CCENT,CYEAR,CMON,CDAY
            REP       " 0",SCHFLD03
          ENDIF
          RESET     SCHFLD03
.
          PACK      KEY8,SP70
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HDBINV08
.
HDBINV85
          LOAD      KEY21,XITEMNO,ST1,ST2,ST3,ST4,ST5:
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY21,DBFHDTY,DBFHDOC,DBFHDEB                               
          CALL      RDDBFH6 
HDBINV90
.                                                                               
          BEEP
          GOTO      HDBINV20
HDBINV91
          MOVE      SP70,DBFHDDAT
          MOVE      SP70,DBFHTER 
          MOVE      ZERO,DBFHTOT 
          MOVE      ZERO,DBFHTAX 
          MOVE      SP70,DBFHDPRT
          MOVE      SP70,DBFHDSTA
          MOVE      SP70,DBFHCDAT
          MOVE      SP70,DBFHCTIM
          MOVE      SP70,DBFHCUSR
          MOVE      SP70,DBFHUR1 
          MOVE      SP70,DBFHUR2 
          MOVE      SP70,DBFHUD1 
          MOVE      SP70,DBFHUD2 
          MOVE      SP70,DBFHUY1 
          MOVE      SP70,DBFHUY2 
          MOVE      SP70,DBFHUC1 
          MOVE      SP70,DBFHUC2 
          MOVE      SP70,DBFHSPA 
          MOVE      SP70,KEY21
          MOVE      SP70,DBFHDOC 
          GOTO      HDBINV96
HDBINV92  MOVE      SP70,KEY21
          MOVE      SP70,DBFHDOC 
          LOAD      KEY21,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY21,DBFHDTY,DBFHDOC,DBFHDEB                               
HDBINV96  PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
HDBINV99  RETURN
.------------------------------------------------------------
. Calculate Outstanding Balance for Invoice Selected
.------------------------------------------------------------
AROUT000  PACK     KEY44,DBFHDEB,DBFHDDAT,DBFHDOC,SP70
          CALL     RDDBFD3
          IF       OVRCD=0
            CALL     RPDBFD3
          ENDIF
          MOVE     ZERO,TOTALOUT
AROUT100  CALL     RKDBFD3
          BRANCH   OVRCD,AROUT999
          MATCH    DBFHDEB,DBFDDEB           * Same Debtor
          GOTO     AROUT999 IF NOT EQUAL
          MATCH    DBFHDDAT,DBFDIDAT         * Same Invoice Date
          GOTO     AROUT999 IF NOT EQUAL
          MATCH    DBFHDOC,DBFDINV           * Same Invoice Reference
          GOTO     AROUT999 IF NOT EQUAL
.
          MOVE      DBFDDTY,F1
          BRANCH    F1,AROUT110,AROUT120,AROUT120,AROUT110,AROUT120
AROUT110  ASSIGN    TOTALOUT+DBFDTOT+DBFDTAX,TOTALOUT   
          GOTO      AROUT100
.
AROUT120  ASSIGN    TOTALOUT-DBFDTOT-DBFDTAX,TOTALOUT   
          GOTO      AROUT100
.
AROUT999  RETURN
.-------------------------------------------------------------------------
. Get Invoice Header Given INVOICE Reference : Returns OVRCD=0 Ok
.                                                           =1 Not Found
.-------------------------------------------------------------------------
GETINV00  PACK      KEY12,SP4,INVOICE
          REP       " 0",KEY12
          PACK      KEY21,ONE,KEY12,SP70
          CALL      RSDBFH6
          CALL      RKDBFH6
          BRANCH    OVRCD,GETINV90
          MATCH     "1",DBFHDTY             * Check Document Type 1=Invoice
          GOTO      GETINV90 IF NOT EQUAL
          MATCH     KEY12,DBFHDOC           * Check Document Ref.
          GOTO      GETINV90 IF NOT EQUAL
          MOVE      ZERO,OVRCD
          GOTO      GETINV99
GETINV90  MOVE      ONE,OVRCD
GETINV99  RETURN
.---------------------------------------------------------------------------
.   System    :   Accounts Receiveable               
.   Include   :   DEBFTHKY
.   Routine   :   HDBFHC00 Invoice             
.   Return    :           
.-------------------------------------------------------------------------------
HDBFHC00  MOVE      ZERO,CSCRSIZ
          MOVE      "10",HTOP
          MOVE      "24",HBOT
          MOVE      "01",HLEF
          MOVE      "80",HRIG
HDBFHC04  CALL      GSCRN000
          BRANCH    EXIT,HDBFHC99
          MOVE      STDCOL00,STDCOL01
          ADD       " 5",STDCOL01
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON,"Invoice No  "
.
          MOVE      STDCOL01,STDCOL02
          ADD       "13",STDCOL02
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"  Date  "               
.
          MOVE      STDCOL02,STDCOL03
          ADD       "9",STDCOL03
          DISPLAY   *PSTDCOL03:STDROW,*V2LON,*ULON,"Debtor             "
.
          MOVE      STDCOL03,STDCOL04
          ADD       "20",STDCOL04
          DISPLAY   *PSTDCOL04:STDROW,*V2LON,*ULON,"Reference          "
.
          MOVE      STDCOL04,STDCOL05
          ADD       "19",STDCOL05
          DISPLAY   *PSTDCOL05:STDROW,*V2LON,*ULON,"      Amount"          
.
          MOVE      STDCOL05,STDCOL06
          ADD       "14",STDCOL06
.
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
          GOTO      HDBFHC50
.
HDBFHC08  MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          CALL      RSDBFH00
.
HDBFHC10  CALL      RKDBFH00
          BRANCH    OVRCD,HDBFHC15
          BRANCH    CSEARCH,HDBFHC12
          CALL      RDBFHC00          * Check Search Ranges
          BRANCH    OVRCD,HDBFHC10
HDBFHC12
          ADD       ONE,XITEMNO
          ADD       ONE,STDROW
          PACK      KEY21,DBFHDEB,DBFHDTY,DBFHDOC
          PACK      KEY22,DBFHDTY,DBFHDOC,DBFHDEB
          PACK      KEY46,DBFHDTY,DBFHUR1,DBFHDOC,DBFHDEB
          LOAD      KEY46,CKEYTYPE,KEY21,KEY22,KEY46
          STORE     KEY46,XITEMNO,ST1,ST2,ST3,ST4,ST5:
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    DBFHDDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      DBFHDEB,KEY8
          CALL      RDDBDB1
.
          ASSIGN    DBFHTOT+DBFHTAX,F12P2
          MOVE      "9,999,999.99",KEY12
          FEDIT     F12P2,KEY12
          MOVE      DBDBNA1,DEBNAM
          MOVE      DBFHUR1,DEBREF
.
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,XITEMNO,DOT
          DISPLAY   *PSTDCOL01:STDROW,DBFHDOC 
          DISPLAY   *PSTDCOL02:STDROW,CPDATE 
          DISPLAY   *PSTDCOL03:STDROW,DEBNAM 
          DISPLAY   *PSTDCOL04:STDROW,DEBREF 
          DISPLAY   *PSTDCOL05:STDROW,KEY12   
          COMPARE   MAXITEM,XITEMNO
          GOTO      HDBFHC10 IF NOT EQUAL
.
HDBFHC15  IF        CKEYTYPE=3
            UNPACK    SAVKEY80,DBFHDTY,DBFHUR1,DBFHDOC,DBFHDEB
          ELSE
            IF        CKEYTYPE=2
              UNPACK    SAVKEY80,DBFHDTY,DBFHDOC,DBFHDEB
            ELSE
              UNPACK    SAVKEY80,DBFHDEB,DBFHDTY,DBFHDOC
            ENDIF
          ENDIF
          MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
HDBFHC20  CALL      HSCRI000
          BRANCH    EXIT,HDBFHC30,HDBFHC35,HDBFHC40,HDBFHC45,HDBFHC04:
                              HDBFHC91,HDBFHC50
          GOTO      HDBFHC92
.
HDBFHC30  CALL      RKDBFH00
          BRANCH    OVRCD,HDBFHC85
          CALL      RPDBFH00
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HDBFHC10
.
HDBFHC35  CALL      RDDBFH00
          CALL      RPDBFH00
          BRANCH    OVRCD,HDBFHC85
          MATCH     SP70,DBFHDOC                                                
          GOTO      HDBFHC85 IF EQUAL                                           
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
HDBFHC36
          CALL      RPDBFH00
          BRANCH    OVRCD,HDBFHC37
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HDBFHC36 IF NOT EQUAL
HDBFHC37
          MOVE      ZERO,XITEMNO
          GOTO      HDBFHC10
.
HDBFHC40  CALL      RDDBFH00
          CALL      RPDBFH00
          BRANCH    OVRCD,HDBFHC85
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HDBFHC08
.
HDBFHC45  CALL      RKDBFH00
          BRANCH    OVRCD,HDBFHC85
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
          CALL      RLDBFH00    * Position at Last
.
HDBFHC46  CALL      RPDBFH00
          BRANCH    OVRCD,HDBFHC47
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HDBFHC46 IF NOT EQUAL
HDBFHC47
          MOVE      ZERO,XITEMNO
          GOTO      HDBFHC10
.
HDBFHC50  CALL      SDBFHC00           * Enter Search Details
.
.
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HDBFHC08
.
HDBFHC85  CALL      LKDBFH00
          BRANCH    CKEYTYPE,HDBFHC86,HDBFHC87,HDBFHC88
HDBFHC86  CALL      RDDBFH1 
          GOTO      HDBFHC90
HDBFHC87  CALL      RDDBFH6 
          GOTO      HDBFHC90
HDBFHC88  CALL      RDDBFH7 
          GOTO      HDBFHC90
.
HDBFHC90  IF        CKEYTYPE=3
            UNPACK    SAVKEY80,DBFHDTY,DBFHUR1,DBFHDOC,DBFHDEB
          ELSE
            IF        CKEYTYPE=2
              UNPACK    SAVKEY80,DBFHDTY,DBFHDOC,DBFHDEB
            ELSE
              UNPACK    SAVKEY80,DBFHDEB,DBFHDTY,DBFHDOC
            ENDIF
          ENDIF
          BEEP
          GOTO      HDBFHC20
HDBFHC91
          MOVE      SP70,DBFHDDAT
          MOVE      SP70,DBFHTER 
          MOVE      ZERO,DBFHTOT 
          MOVE      ZERO,DBFHTAX 
          MOVE      SP70,DBFHDPRT
          MOVE      SP70,DBFHDSTA
          MOVE      SP70,DBFHCDAT
          MOVE      SP70,DBFHCTIM
          MOVE      SP70,DBFHCUSR
          MOVE      SP70,DBFHUR2 
          MOVE      SP70,DBFHUD1 
          MOVE      SP70,DBFHUD2 
          MOVE      SP70,DBFHUY1 
          MOVE      SP70,DBFHUY2 
          MOVE      SP70,DBFHUC1 
          MOVE      SP70,DBFHUC2 
          MOVE      SP70,DBFHSPA 
          MOVE      SP70,KEY46
          MOVE      SP70,DBFHDOC 
          GOTO      HDBFHC96
.
HDBFHC92  MOVE      SP70,KEY46
          MOVE      SP70,KEY21
          MOVE      SP70,DBFHDOC 
          MOVE      CCITEM,XITEMNO
          CALL      LKDBFH00
.
HDBFHC96  PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
HDBFHC99  RETURN
.
LKDBFH00  BRANCH    CKEYTYPE,LKDBFH01,LKDBFH02,LKDBFH03
LKDBFH01  LOAD      KEY21,XITEMNO,ST1,ST2,ST3,ST4,ST5:  
                                 ST6,ST7,ST8,ST9,ST10:
                                 ST11,ST12,ST13,ST14,ST15:
                                 ST16,ST17
          UNPACK    KEY21,DBFHDEB,DBFHDTY,DBFHDOC
          GOTO      LKDBFH99
LKDBFH02  LOAD      KEY21,XITEMNO,ST1,ST2,ST3,ST4,ST5:  
                                 ST6,ST7,ST8,ST9,ST10:
                                 ST11,ST12,ST13,ST14,ST15:
                                 ST16,ST17
          UNPACK    KEY21,DBFHDTY,DBFHDOC,DBFHDEB                       
          GOTO      LKDBFH99
LKDBFH03  LOAD      KEY46,XITEMNO,ST1,ST2,ST3,ST4,ST5:  
                                 ST6,ST7,ST8,ST9,ST10:
                                 ST11,ST12,ST13,ST14,ST15:
                                 ST16,ST17
          UNPACK    KEY46,DBFHDTY,DBFHUR1,DBFHDOC,DBFHDEB                       
          GOTO      LKDBFH99
LKDBFH99  RETURN
.------------------------------------------------------------
. Position at First Record for Search
.------------------------------------------------------------
RSDBFH00  BRANCH    CKEYTYPE,RSDBFH01,RSDBFH02,RSDBFH03
.
RSDBFH01  PACK      KEY21,SRCHDEB,ONE,SP70                  
          PACK      SAVKEY80,SRCHDEB,ONE,SP70                  
          STRIP     SAVKEY80
          CALL      RSDBFH1      * by Debtor Code, Type, Document
          GOTO      RSDBFH99
.
RSDBFH02  PACK      KEY21,ONE,SRCHDOC,SP70                  
          PACK      SAVKEY80,ONE,SP70                  
          STRIP     SAVKEY80
          CALL      RSDBFH6      * by Type, Docment, Debtor Code
          GOTO      RSDBFH99
.
RSDBFH03  PACK      KEY46,ONE,SRCHUR1,SP70                  
          PACK      SAVKEY80,ONE,SRCHUR1,SP70                  
          STRIP     SAVKEY80
          CALL      RSDBFH7      * By Type, Reference, Document, Debtor
          GOTO      RSDBFH99
RSDBFH99  RETURN
.------------------------------------------------------------
. Position at First Record for Search
.------------------------------------------------------------
RLDBFH00  BRANCH    CKEYTYPE,RLDBFH01,RLDBFH02,RLDBFH03
.
RLDBFH01  PACK      KEY21,SAVKEY80,Z70
          CALL      RSDBFH1      * by Debtor Code, Type, Document
          GOTO      RSDBFH99
.
RLDBFH02  PACK      KEY21,SAVKEY80,Z70
          CALL      RSDBFH6      * by Type, Docment, Debtor Code
          GOTO      RSDBFH99
.
RLDBFH03  PACK      KEY46,SAVKEY80,Z70
          CALL      RSDBFH7      * By Type, Reference, Document, Debtor
          GOTO      RLDBFH99
RLDBFH99  RETURN
.------------------------------------------------------------
. Read Next Search
.------------------------------------------------------------
RKDBFH00  BRANCH    CKEYTYPE,RKDBFH01,RKDBFH02,RKDBFH03
.
RKDBFH01  CALL      RKDBFH1      * by Debtor Code, Type, Document
          PACK      KEY21,DBFHDEB,DBFHDTY,DBFHDOC,SP70                  
          MATCH     SAVKEY80,KEY21
          GOTO      OVERCOND IF NOT EQUAL
          GOTO      RKDBFH99
.
RKDBFH02  CALL      RKDBFH6      * by Type, Docment, Debtor Code
          PACK      KEY21,DBFHDTY,DBFHDOC,DBFHDEB,SP70                  
          MATCH     SAVKEY80,KEY21
          GOTO      OVERCOND IF NOT EQUAL
          GOTO      RKDBFH99
.
RKDBFH03  CALL      RKDBFH7      * By Type, Reference, Document, Debtor
          PACK      KEY46,DBFHDTY,DBFHUR1,DBFHDOC,DBFHDEB,SP70                  
          MATCH     SAVKEY80,KEY46
          GOTO      OVERCOND IF NOT EQUAL
          GOTO      RKDBFH99
.
RKDBFH99  RETURN
.------------------------------------------------------------
. Read Previous Search
.------------------------------------------------------------
RPDBFH00  BRANCH    CKEYTYPE,RPDBFH01,RPDBFH02,RPDBFH03
.
RPDBFH01  CALL      RPDBFH1      * by Debtor Code, Type, Document
          PACK      KEY21,DBFHDEB,DBFHDTY,DBFHDOC,SP70                  
          MATCH     SAVKEY80,KEY21
          GOTO      OVERCOND IF NOT EQUAL
          GOTO      RPDBFH99
.
RPDBFH02  CALL      RPDBFH6      * by Type, Docment, Debtor Code
          PACK      KEY21,DBFHDTY,DBFHDOC,DBFHDEB,SP70                  
          MATCH     SAVKEY80,KEY21
          GOTO      OVERCOND IF NOT EQUAL
          GOTO      RPDBFH99
.
RPDBFH03  CALL      RPDBFH7      * By Type, Reference, Document, Debtor
          PACK      KEY46,DBFHDTY,DBFHUR1,DBFHDOC,DBFHDEB,SP70                  
          MATCH     SAVKEY80,KEY46
          GOTO      OVERCOND IF NOT EQUAL
          GOTO      RPDBFH99
.
RPDBFH99  RETURN
.------------------------------------------------------------
. Read Previous Search
.------------------------------------------------------------
RDDBFH00  BRANCH    CKEYTYPE,RDDBFH01,RDDBFH02,RDDBFH03
.
RDDBFH01  MOVE      ST1,KEY21
          CALL      RDDBFH1      * by Debtor Code, Type, Document
          GOTO      RKDBFH99
.
RDDBFH02  MOVE      ST1,KEY21
          CALL      RDDBFH6      * by Type, Docment, Debtor Code
          GOTO      RKDBFH99
.
RDDBFH03  MOVE      ST1,KEY46
          CALL      RDDBFH7      * By Type, Reference, Document, Debtor
          GOTO      RKDBFH99
.
RDDBFH99  RETURN
.
          DEFPROC   KEYDEB00
.
          INC       STDHLPDF
.
          CALL      KDBDBA00
          GOTO      ENDDBDBA
.
          INC       DEBDBTKY
          INC       STDHLPCD
.
ENDDBDBA  ENDPROC      
.------------------------------------------------------------
. Check Search Ranges Returns OVRCD=1 if Out of Range
.------------------------------------------------------------
RDBFHC00  MOVE      ZERO,OVRCD
.          MATCH     SP70,SRCHDOC
.          IF        !@EQUAL
.            MATCH     SRCHDOC,DBFHDOC 
.            GOTO      RDBFHC90 IF NOT EQUAL
.          ENDIF
          MATCH     SP70,SRCHDAT
          IF        !@EQUAL
            MATCH     SRCHDAT,DBFHDDAT 
            GOTO      RDBFHC90 IF NOT EQUAL
          ENDIF
          MATCH     SP70,SRCHUR1
          IF        !@EQUAL
            MOVE      SRCHUR1,KEY25 
            STRIP     KEY25 
            MATCH     KEY25,DBFHUR1 
            GOTO      RDBFHC90 IF NOT EQUAL
          ENDIF
          MATCH     SP70,SRCHDEB
          IF        !@EQUAL
            MATCH     SRCHDEB,DBFHDEB 
            GOTO      RDBFHC90 IF NOT EQUAL
          ENDIF
          IF        !(SRCHAMT=0)
            ASSIGN    DBFHTOT+DBFHTAX,F12P2
            COMPARE   SRCHAMT,F12P2   
            GOTO      RDBFHC90 IF NOT EQUAL
          ENDIF
          GOTO      HDBFHC99
.
RDBFHC90  MOVE      ONE,OVRCD
.
RDBFHC99  RETURN
.------------------------------------------------------------
. Enter Search Parameters
.------------------------------------------------------------
SDBFHC00  MOVE      CCOL,SAVCOL
          MOVE      CVERT,SAVVERT
          MOVE      ZERO,CSEARCH
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          MOVE      TWO,CKEYTYPE   
.
          UNPACK    SP70,SRCHDOC,SRCHDAT,SRCHUR1,SRCHDEB
          MOVE      ZERO,SRCHAMT
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          DISPLAY   *PSTDCOL00:STDROW,*V2LON,*HON,"Sch"
.
. Document Reference
.--------------------
          KEYIN     *PSTDCOL01:STDROW,*V2LON,*JR,*ZF,SRCHDOC
          RESET     SRCHDOC
          MATCH     "0000000000",SRCHDOC
          IF        @EQUAL
            MOVE      SP70,SRCHDOC
          ENDIF
.
. Date
.-----
          MOVE      STDCOL02,CCOL 
          MOVE      STDROW,CVERT
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      SP2,CMON
          MOVE      SP2,CDAY
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
          MOVE      ONE,CCANLDTE     *  1 Default dates cancelled by a space 
          PACK      SRCHDAT,SP70
          CALL      KEYDATE
          IF        OVRCD=0
            PACK      SRCHDAT,CCENT,CYEAR,CMON,CDAY
            REP       " 0",SRCHDAT
          ENDIF
.
. Debtor
.-------
          MOVE      STDCOL03,CCOL
          MOVE      STDROW,CVERT
          PROC      KEYDEB00
          IF        EXIT=0
            MOVE      DBDBNA1,DEBNAM
            DISPLAY   *PSTDCOL03:STDROW,*V2LON,DEBNAM  
            MOVE      DBDBDEB,SRCHDEB
            MOVE      ONE,CKEYTYPE
          ELSE
            MOVE      SP70,SRCHDEB
          ENDIF
.
. Statement Reference
.---------------------
          KEYIN     *PSTDCOL04:STDROW,*V2LON,SRCHUR1  
          ENDSET    SRCHUR1 
          APPEND    SP70,SRCHUR1 
          RESET     SRCHUR1 
          MATCH     SP70,SRCHUR1 
          IF        !@EQUAL
            MOVE      "3",CKEYTYPE
          ENDIF
.
. Amount
.--------
          KEYIN     *PSTDCOL05:STDROW,*V2LON,SRCHAMT  
.
          MOVE      SAVCOL,CCOL
          MOVE      SAVVERT,CVERT
          RETURN
.
