. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS14                                             *
. * Desc      :   Print Code Maintenance                               *
. **********************************************************************
. * Date      :   23.10.1990                                           *
. * Author    :   B.G.Ackland (IBA)                                    *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
. * VF.00.01  15.02.2000 Charles Handaya                               *
. *           Clear line 24 after finish with selection No.2           *
. *           srf 637627                                               *
. **********************************************************************
.$$$$$$
.        PROGRAM IBAFMS14
.             open files and initialise variables
.             WHILE user wants to add/modify/delete DO
.                  get desired option (add, modify or delete)
.                  CASE option OF
.                       ADD     : WHILE user wants to add records DO
.                                      accept initial values
.                                      WHILE user wants modify screen data DO
.                                           get number of item to be modified
.                                           allow user to modify option
.                                      END WHILE
.                                      IF user wants to post data
.                                           THEN write new data
.                                      END IF
.                                 END WHILE
.                       MODIFY  : WHILE user wants to modify records DO
.                                      get and display desired record
.                                      WHILE user wants modify screen data DO
.                                           get number of item to be modified
.                                           allow user to modify option
.                                      END WHILE
.                                      IF user wants to post data
.                                           THEN update data
.                                      END IF
.                                 END WHILE
.                       DELETE  : WHILE user wants to delete records DO
.                                      get and display desired record
.                                      IF user wants to delete data
.                                           THEN delete data
.                                      END IF
.                                 END WHILE
.                  END CASE
.             END WHILE
.        END
.$$$$$$
. Standard FMS Accounting System Include
.---------------------------------------
          INC       FMSSTDDF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
          INC       FMSPRCFD/INC
          INC       IBAPTYFD/INC
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
.
ACCEPT    FORM      1                  * accept mode (1=on)
BLYESNO   DIM       3
DESCB1    DIM       35
DESCB2    DIM       35
DESCB3    DIM       35
DESCA1    DIM       35
DESCA2    DIM       35
DESCA3    DIM       35
LINEBEF   DIM       20
LINEAFT   DIM       20
SELECT    FORM      2
SPYESNO   DIM       3
ULYESNO   DIM       3
UNDLDES   DIM       15
UNDLBEF   INIT      "Before        "
UNDLAFT   INIT      "After         "
UNDLBTH   INIT      "Before & After"
.
PRGID     INIT      "IBAFMS14"
PRGNAM    INIT      "Print Code Maintenance"
VERSION   INIT      "V12.00.00"
.
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML0100    CALL      SELOPTN            * insert/modify/delete/exit ?
          COMPARE   ZERO,COPTION
          GOTO      ML9999 IF EQUAL    * exit program
.
ML0200    CALL      PROC0000           * get entry
          BRANCH    EXIT,ML0100,ML0100 * exit option
.
          LOAD      ACCEPT,COPTION,ONE,ZERO,ZERO * set up mode
          CALL      SCRA0000           * display screen A
.
          BRANCH    COPTION,ML1000,ML1000,ML2000
.
.---- add/modify data ----
.
ML1000    CALL      SELA0000           * modify screen A
          BRANCH    EXIT,ML0200        * don't save data
.
          CALL      POST0000           * post screen A data
          GOTO      ML0200             * get next entry
.
.---- delete data ----
.
ML2000    CALL      DELQST             * ok to delete (y/n/c) ?
          BRANCH    CEXIT,ML2100,ML0200,ML0100
.
ML2100    CALL      DELT0000           * delete screen A data
          GOTO      ML0200             * get next entry
.
.
ML9999    CHAIN     PGM                * chain back to menu
.
.******************************************************************************
.* INIT - Open Files                             Called by ML0000             *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening fmsprcaf";  * Print Code file
          OPEN      FMSPRCA1,"fmsprcaf"
.
          DISPLAY   *P56:24,"Opening ibaptyaf";  * 
          OPEN      IBAPTYA1,"ibaptyaf"
.
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.******************************************************************************
.* SCRA - Display Screen A                       Called by SELA0000, redisps  *
.*       Requires : ACCEPT   (1=accept mode)                                  *
.******************************************************************************
SCRA0000  DISPLAY   *P1:4,"    Print Code           : ",*V2LON,FMPRCODE,*EF:
                    *P1:6," 1",*HOFF,". Description          : ":
                    *P1:7,*V2LON," 2",*HOFF,". Draft UnderLine      : ":
                    *P45:7,"UnderLine Character  : ":
                    *P1:8,*V2LON," 3",*HOFF,". Draft Bold      (Y/N): ":
                    *P1:9,*V2LON," 4",*HOFF,". Skip Lines Before    : ":
                    *P1:10,*V2LON," 5",*HOFF,". Skip Lines After     : ":
                    *P1:11,*V2LON," 6",*HOFF,". Before Print Type 1  : ":
                    *P1:12,*V2LON," 7",*HOFF,". Before Print Type 2  : ":
                    *P1:13,*V2LON," 8",*HOFF,". Before Print Type 3  : ":
                    *P1:14,*V2LON," 9",*HOFF,". After  Print Type 1  : ":
                    *P1:15,*V2LON,"10",*HOFF,". After  Print Type 2  : ":
                    *P1:16,*V2LON,"11",*HOFF,". After  Print Type 3  : ":
                    *P1:17,*V2LON,"12",*HOFF,". Suppress Post Ac Prt : "
          BRANCH    ACCEPT,SCRA9999    * accept mode
.
          MOVE      NO,UNDLDES
          LOAD      UNDLDES,FMPRUNDL,UNDLAFT,UNDLBEF,UNDLBTH
.
          MOVE      NO,BLYESNO
          LOAD      BLYESNO,FMPRBOLD,YES
          MOVE      ZERO,F1
          MOVE      FMPRSUPP,F1
          MOVE      NO,SPYESNO
          LOAD      SPYESNO,F1,YES
.
          CLEAR     LINEBEF
          APPEND    FMPRBLIN,LINEBEF
          APPEND    SP4,LINEBEF
          COMPARE   "9",FMPRBLIN
          GOTO      SCRA2100 IF NOT EQUAL
          APPEND    "New Page",LINEBEF
.
SCRA2100  RESET     LINEBEF
.
          CLEAR     LINEAFT
          APPEND    FMPRALIN,LINEAFT
          APPEND    SP4,LINEAFT
          COMPARE   "9",FMPRALIN
          GOTO      SCRA2200 IF NOT EQUAL
          APPEND    "New Page",LINEAFT
SCRA2200  RESET     LINEAFT
.
          MOVE      FMPRPT1B,KEY3
          MOVE      SP70,IBPTDESC
          CALL      RDIBPT1
          MOVE      IBPTDESC,DESCB1
.
          MOVE      FMPRPT2B,KEY3
          MOVE      SP70,IBPTDESC
          CALL      RDIBPT1
          MOVE      IBPTDESC,DESCB2
.
          MOVE      FMPRPT3B,KEY3
          MOVE      SP70,IBPTDESC
          CALL      RDIBPT1
          MOVE      IBPTDESC,DESCB3
.
          MOVE      FMPRPT1A,KEY3
          MOVE      SP70,IBPTDESC
          CALL      RDIBPT1
          MOVE      IBPTDESC,DESCA1
.
          MOVE      FMPRPT2A,KEY3
          MOVE      SP70,IBPTDESC
          CALL      RDIBPT1
          MOVE      IBPTDESC,DESCA2
.
          MOVE      FMPRPT3A,KEY3
          MOVE      SP70,IBPTDESC
          CALL      RDIBPT1
          MOVE      IBPTDESC,DESCA3
.
          DISPLAY   *P28:6,*V2LON,FMPRDESC:
                    *P28:7,UNDLDES,*P68:7,FMPRUNDC:
                    *P28:8,BLYESNO:
                    *P28:9,LINEBEF:
                    *P28:10,LINEAFT:
                    *P28:11,*V2LON,FMPRPT1B,SP1,*HOFF,DESCB1:
                    *P28:12,*V2LON,FMPRPT2B,SP1,*HOFF,DESCB2:
                    *P28:13,*V2LON,FMPRPT3B,SP1,*HOFF,DESCB3:
                    *P28:14,*V2LON,FMPRPT1A,SP1,*HOFF,DESCA1:
                    *P28:15,*V2LON,FMPRPT2A,SP1,*HOFF,DESCA2:
                    *P28:16,*V2LON,FMPRPT3A,SP1,*HOFF,DESCA3:
                    *P28:17,*V2LON,SPYESNO
.
SCRA9999  RETURN
.******************************************************************************
.* DEFV - set up default variables               Called by SELA0000           *
.******************************************************************************
DEFV0000  MOVE      UNDLN70,FMPRDESC   * set description
          MOVE      SP70,FMPRUNDC
          MOVE      SP70,FMPRPT1B   
          MOVE      SP70,FMPRPT2B   
          MOVE      SP70,FMPRPT3B   
          MOVE      SP70,FMPRPT1A   
          MOVE      SP70,FMPRPT2A   
          MOVE      SP70,FMPRPT3A   
          MOVE      SP70,DESCB1
          MOVE      SP70,DESCB2
          MOVE      SP70,DESCB3
          MOVE      SP70,DESCA1
          MOVE      SP70,DESCA2
          MOVE      SP70,DESCA3
          MOVE      SP70,UNDLDES
          MOVE      SP70,BLYESNO
          MOVE      SP70,SPYESNO
          MOVE      SP70,LINEBEF 
          MOVE      SP70,LINEAFT 
.
DEFV9999  RETURN
.******************************************************************************
.* PROC - Enter Print Code Code                  Called by ML0000             *
.*        Returns : EXIT     (2=EXITCHAR entered, 1=nothing entered, 0=cont.) *
.*                  FMPRCODE (variable entered)                               *
.******************************************************************************
PROC0000  DISPLAY   *P1:4,"    Print Code           : ",*EF;
.
          MOVE      "28",CCOL          * set up screen address
          MOVE      "4",CVERT
.
          COMPARE   ONE,COPTION        * insert mode ?
          GOTO      PROC1000 IF NOT EQUAL
          CALL      APRCFM00 
          GOTO      PROC9999
.
PROC1000  CALL      KPRCFM00
.
PROC9999  RETURN
.******************************************************************************
.* SELA - Modify screen A data                   Called by ML0000             *
.*        Returns : EXIT     (2=EXITCHAR entered, 1=nothing entered, 0=cont.) *
.*                  FMPRDISS (variable entered)                               *
.******************************************************************************
SELA0000  MOVE      ZERO,SELECT
.
          COMPARE   ONE,ACCEPT
          CALL      DEFV0000 IF EQUAL            * set up default values
.
.---- determine option to execute ----
.
SELA0100  ADD       ONE,SELECT                   * get next option
          BRANCH    ACCEPT,SELA0500              * in accept mode ?
          CALL      MAINQST                      * select/post/cancel ?
          MOVE      CCITEM,SELECT
.
          COMPARE   ZERO,SELECT
          GOTO      SELA9000 IF EQUAL            * post ?
.
          COMPARE   SEQ,SELECT
          GOTO      SELA9500 IF EQUAL            * cancel ?
.
SELA0500  MOVE      "28",CCOL                    * set up screen address
          MOVE      "5",CVERT
          ADD       SELECT,CVERT
          BRANCH    SELECT,SELA1100,SELA1200,SELA1300,SELA1400,SELA1500:
                           SELA1600,SELA1700,SELA1800,SELA1900,SELA2000:
                           SELA2100,SELA2200
.
          BRANCH    ACCEPT,SELA0900              * finished accept mode ?
          BEEP                                   * illegal option
.
SELA0900  MOVE      ZERO,ACCEPT                  * clear accept mode
          GOTO      SELA0100                     * get next option
.
.---- option 1 ----
.
SELA1100  MATCH     FMPRDESC,SP70
          GOTO      SELA1150 IF NOT EQUAL        * description spaces ?
.
          PACK      FMPRDESC,UNDLN70
.
SELA1150  KEYIN     *PCCOL:CVERT,*DV,FMPRDESC:   * read in description
                    *PCCOL:CVERT,*V2LON,*RV,FMPRDESC;
          PACK      FMPRDESC,FMPRDESC,SP70
.
          MATCH     FMPRDESC,SP70
          GOTO      SELA1100 IF EQUAL            * description spaces ?
          MATCH     FMPRDESC,UNDLN70
          GOTO      SELA9500 IF EQUAL            * nothing entered ?
          MATCH     FMPRDESC,EXITCHAR
          GOTO      SELA9500 IF EQUAL            * EXITCHAR entered ?
.
          DISPLAY   *PCCOL:CVERT,*V2LON,FMPRDESC;
          GOTO      SELA0100
.
.---- option 2 ----
.
SELA1200  MOVE      ZERO,FMPRUNDL
          DISPLAY   *P1:24,*EL,"Underline : ":
                    *V2LON,"0. ",*HOFF,"No, ":
                    *V2LON,"1. ",*HOFF,"After, ":
                    *V2LON,"2. ",*HOFF,"Before, ":
                    *V2LON,"3. ",*HOFF,"Before & After"
          KEYIN     *PCCOL:CVERT,*DV,SP10,*DV,SP5:
                    *PCCOL:CVERT,*V2LON,FMPRUNDL
          MOVE      NO,UNDLDES
          COMPARE   ZERO,FMPRUNDL
          GOTO      SELA1250 IF EQUAL
          BRANCH    FMPRUNDL,SELA1250,SELA1250,SELA1250
          BEEP
          GOTO      SELA1200
SELA1250  DISPLAY   *P1:24,*EL
          LOAD      UNDLDES,FMPRUNDL,UNDLAFT,UNDLBEF,UNDLBTH
          DISPLAY   *PCCOL:CVERT,*V2LON,UNDLDES
.
          COMPARE   ZERO,FMPRUNDL
          GOTO      SELA1290 IF EQUAL
.
          REP       " _",FMPRUNDC
          KEYIN     *P68:CVERT,*DV,FMPRUNDC:
                    *P68:CVERT,*RV,*V2LON,FMPRUNDC:
                    *P68:CVERT,*DV,FMPRUNDC
          GOTO      SELA0100
.
SELA1290  MOVE      SP70,FMPRUNDC      * clear underline char if no entered
          DISPLAY   *P68:CVERT,*EL;
          GOTO      SELA0100
.
.---- option 3 ----
.
SELA1300  MOVE      "N",DEFYESNO
          CALL      YORN0000
          MOVE      YESNO,BLYESNO
          DISPLAY   *PCCOL:CVERT,*V2LON,YESNO
          MOVE      FYESNO,FMPRBOLD
          GOTO      SELA0100
.
.---- option 4 ----
.
SELA1400  KEYIN     *PCCOL:CVERT,"_",*EL:
                    *P40:CVERT,"(9 = New Page)":
                    *PCCOL:CVERT,*V2LON,FMPRBLIN:
                    *PCCOL:CVERT,*DV,FMPRBLIN,*EL;
.
          CLEAR     LINEBEF
          APPEND    FMPRBLIN,LINEBEF
          APPEND    SP4,LINEBEF
          COMPARE   "9",FMPRBLIN
          GOTO      SELA1450 IF NOT EQUAL
          APPEND    "New Page",LINEBEF
SELA1450  RESET     LINEBEF
          DISPLAY   *PCCOL:CVERT,*V2LON,LINEBEF
          GOTO      SELA0100
.
.---- option 5 ----
.
SELA1500  KEYIN     *PCCOL:CVERT,"_",*EL:
                    *P40:CVERT,"(9 = New Page)":
                    *PCCOL:CVERT,*V2LON,FMPRALIN:
                    *PCCOL:CVERT,*DV,FMPRALIN,*EL;
.
          CLEAR     LINEAFT
          APPEND    FMPRALIN,LINEAFT
          APPEND    SP4,LINEAFT
          COMPARE   "9",FMPRALIN
          GOTO      SELA1550 IF NOT EQUAL
          APPEND    "New Page",LINEAFT
SELA1550  RESET     LINEAFT
          DISPLAY   *PCCOL:CVERT,*V2LON,LINEAFT
          GOTO      SELA0100
.
.---- option 6 ----
.
SELA1600  MOVE      ZERO,CKEYTYP
          CALL      KIBPTA00
          BRANCH    EXIT,SELA1650,SELA9500
          MOVE      IBPTCODE,FMPRPT1B
          MOVE      IBPTDESC,DESCB1
          DISPLAY   *PCCOL:CVERT,*V2LON,FMPRPT1B,SP1,*HOFF,DESCB1
          GOTO      SELA0100
SELA1650  UNPACK    SP70,FMPRPT1B,DESCB1
          GOTO      SELA0100
.
.---- option 7 ----
.
SELA1700  MOVE      ZERO,CKEYTYP
          CALL      KIBPTA00
          BRANCH    EXIT,SELA1750,SELA9500
          MOVE      IBPTCODE,FMPRPT2B
          MOVE      IBPTDESC,DESCB2
          DISPLAY   *PCCOL:CVERT,*V2LON,FMPRPT2B,SP1,*HOFF,DESCB2
          GOTO      SELA0100
SELA1750  UNPACK    SP70,FMPRPT2B,DESCB2
          GOTO      SELA0100
.
.---- option 8 ----
.
SELA1800  MOVE      ZERO,CKEYTYP
          CALL      KIBPTA00
          BRANCH    EXIT,SELA1850,SELA9500
          MOVE      IBPTCODE,FMPRPT3B
          MOVE      IBPTDESC,DESCB3
          DISPLAY   *PCCOL:CVERT,*V2LON,FMPRPT3B,SP1,*HOFF,DESCB3
          GOTO      SELA0100
SELA1850  UNPACK    SP70,FMPRPT3B,DESCB3
          GOTO      SELA0100
.
.---- option 9 ----
.
SELA1900  MOVE      ZERO,CKEYTYP
          CALL      KIBPTA00
          BRANCH    EXIT,SELA1950,SELA9500
          MOVE      IBPTCODE,FMPRPT1A
          MOVE      IBPTDESC,DESCA1
          DISPLAY   *PCCOL:CVERT,*V2LON,FMPRPT1A,SP1,*HOFF,DESCA1
          GOTO      SELA0100
SELA1950  UNPACK    SP70,FMPRPT1A,DESCA1
          GOTO      SELA0100
.
.---- option 10 ----
.
SELA2000  MOVE      ZERO,CKEYTYP
          CALL      KIBPTA00
          BRANCH    EXIT,SELA2050,SELA9500
          MOVE      IBPTCODE,FMPRPT2A
          MOVE      IBPTDESC,DESCA2
          DISPLAY   *PCCOL:CVERT,*V2LON,FMPRPT2A,SP1,*HOFF,DESCA2
          GOTO      SELA0100
SELA2050  UNPACK    SP70,FMPRPT2A,DESCA2
          GOTO      SELA0100
.
.---- option 11 ----
.
SELA2100  MOVE      ZERO,CKEYTYP
          CALL      KIBPTA00
          BRANCH    EXIT,SELA2150,SELA9500
          MOVE      IBPTCODE,FMPRPT3A
          MOVE      IBPTDESC,DESCA3
          DISPLAY   *PCCOL:CVERT,*V2LON,FMPRPT3A,SP1,*HOFF,DESCA3
          GOTO      SELA0100
SELA2150  UNPACK    SP70,FMPRPT3A,DESCA3
          GOTO      SELA0100
.
.---- option 12 ----
.
SELA2200  MOVE      "N",DEFYESNO
          CALL      YORN0000
          MOVE      YESNO,SPYESNO
          DISPLAY   *PCCOL:CVERT,*V2LON,YESNO
          MOVE      FYESNO,FMPRSUPP
          GOTO      SELA0100
.
.---- exit points ----
.
SELA9000  MOVE      ZERO,EXIT                    * post
          GOTO      SELA9999
.
SELA9500  MOVE      ONE,EXIT                     * cancel
.
SELA9999  RETURN
.******************************************************************************
.* POST - Post data                              Called by ML0000             *
.******************************************************************************
POST0000  PACK      KEY3,FMPRCODE,SP70
          CALL      DEFMPR1                 * delete old record (if possible)
          CALL      WRFMPR1                 * write new record
.
POST9999  RETURN
.******************************************************************************
.* DELT - Delete data                            Called by ML0000             *
.******************************************************************************
DELT0000  PACK      KEY3,FMPRCODE,SP70
          CALL      DEFMPR1                 * delete old record (if possible)
.
DELT9999  RETURN
.******************************************************************************
.* INCLUDES FOR I/O'S                                                         *
.******************************************************************************
RPRCFM00  DISPLAY   *P1:4,"    Print Code           : ",*EF;
          RETURN
.
.
RPTYIB00  DISPLAY   *P1:4,"    Print Code           : ",*V2LON,FMPRCODE,*EF:
                    *P1:6," 1",*HOFF,". Description          : ":
                    *P1:7,*V2LON," 2",*HOFF,". Draft UnderLine (Y/N): ":
                    *P45:7,"UnderLine Character  : ":
                    *P1:8,*V2LON," 3",*HOFF,". Draft Bold      (Y/N): ":
                    *P1:9,*V2LON," 4",*HOFF,". Skip Lines Before    : ":
                    *P1:10,*V2LON," 5",*HOFF,". Skip Lines After     : ":
                    *P1:11,*V2LON," 6",*HOFF,". Before Print Type 1  : ":
                    *P1:12,*V2LON," 7",*HOFF,". Before Print Type 2  : ":
                    *P1:13,*V2LON," 8",*HOFF,". Before Print Type 3  : ":
                    *P1:14,*V2LON," 9",*HOFF,". After  Print Type 1  : ":
                    *P1:15,*V2LON,"10",*HOFF,". After  Print Type 2  : ":
                    *P1:16,*V2LON,"11",*HOFF,". After  Print Type 3  : "
.
          DISPLAY   *P28:6,*V2LON,FMPRDESC:
                    *P28:7,ULYESNO,*P68:7,FMPRUNDC:
                    *P28:8,BLYESNO:
                    *P28:9,LINEBEF:
                    *P28:10,LINEAFT:
                    *P28:11,*V2LON,FMPRPT1B,SP1,*HOFF,DESCB1:
                    *P28:12,*V2LON,FMPRPT2B,SP1,*HOFF,DESCB2:
                    *P28:13,*V2LON,FMPRPT3B,SP1,*HOFF,DESCB3:
                    *P28:14,*V2LON,FMPRPT1A,SP1,*HOFF,DESCA1:
                    *P28:15,*V2LON,FMPRPT2A,SP1,*HOFF,DESCA2:
                    *P28:16,*V2LON,FMPRPT3A,SP1,*HOFF,DESCA3
          RETURN
.
          INC       FMSPRCIO/INC       * Print Code
          INC       IBAPTYIO/INC       * Print Type
.
          INC       FMSPRCKY           * Print Code
          INC       IBAPTYKY           * Print Type
          INC       FMSSTDCD           * FMS Acc. standard routines
