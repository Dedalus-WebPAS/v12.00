.******************************************************************************
.* System         : Accident and Emergency                                    *
.*                                                                            *
.* Include Name   : AAEGICDS.PBL                                              *
.*                                                                            *
.* Function       : Display the codes from AAEGICFD and AAEGILFD.             *
.*                                                                            *
.* Author         : Justin Coates      03/08/1993                             *
.*                                                                            *
.* Returns        : KEY6               (AEICCODE+AEILCODE)                    *
.*                                                                            *
.* Modifications  :                                                           *
.*        V5.03.01  23/06/1995    Justin Coates  SRF 133250                   *
.*                  if a code is selected that has links, but the main code   *
.*                  is selected, set KEY6 to AEICCODE and spaces              *
.******************************************************************************
.
AAEGICDS  MOVE      FIVE,CVERT              * line counter
          MOVE      ONE,CCOL                * start on LHS of screen
          MOVE      ZERO,AEICQCNT           * clear the count
          MOVE      ONE,CPAGENO             * set page
          MOVE      SP4,KEY4                * key of blanks
          CALL      RSAEGIC1                * positional read
.
          DISPLAY   *P1:3,*EF,*V2LON,*P25:4,"INVESTIGATION CODES":
                    *P1:5,*ULON,"Item",*P6:5,"Code   ":
                    *P14:5,"Description         ":
                    *P41:5,"Item",*P46:5,"Code   ":
                    *P54:5,"Description         "
.
. ---------------------------------------------------------------
. ------- next read on diagnosis file and position on link ------
. ---------------------------------------------------------------
.
HAEIC100  CALL      RKAEGIC1
          BRANCH    OVRCD,HAEIC500          * end of file
.
          PACK      KEY6,AEICCODE,SP2
          CALL      RSAEGIL1
.
          PACK      CKYIDEF6,AEICCODE,SP6
          PACK      KEY22,AEICDESC,SP2
.
. ----------------------------------
. ------- increment counters -------
. ----------------------------------
.
HAEIC200  ADD       ONE,CVERT               * inc line counter
.
          COMPARE   "23",CVERT
          GOTO      HAEIC300 IF LESS        * will fit on the page
.
          ADD       "40",CCOL               * inc column counter
          MOVE      SIX,CVERT               * reset row position
.
          COMPARE   "80",CCOL  
          GOTO      HAEIC400 IF NOT LESS    * need a new page
.
. -------------------------------------------
. ------- display all the information -------
. -------------------------------------------
.
HAEIC300  ADD       ONE,AEICQCNT
          DISPLAY   *PCCOL:CVERT,*V2LON,SP1,AEICQCNT,*HOFF,DOT:
                                        SP1,CKYIDEF6,SP2,KEY22
          MOVE      CKYIDEF6,AEICQARR[AEICQCNT]
.
.         now loop over link file
.
          CALL      RKAEGIL1
          BRANCH    OVRCD,HAEIC100          * no more codes
          MATCH     AEICCODE,AEILMAIN
          GOTO      HAEIC100 IF NOT EQUAL   * different code
.
          PACK      CKYIDEF6,AEICCODE,AEILCODE,SP6
          PACK      KEY22,SP2,AEILDESC
          GOTO      HAEIC200
.
. ------- new page needed -------
.
HAEIC400  BRANCH    CPAGENO,HAEIC450        * first page
.
HAEIC410  MOVE      ONE,AEICQLIN            * (N)ext, (F)irst
          CALL      HAEICK00
          BRANCH    EXIT,HAEIC999,AAEGICDS,HAEIC700
.                        exit     first    next
          GOTO      HAEIC600
.
HAEIC450  MOVE      TWO,AEICQLIN            * (N)ext only
          CALL      HAEICK00
          BRANCH    EXIT,HAEIC999,HAEIC590,HAEIC700
.                        exit     first    next
          GOTO      HAEIC600
.
. ------- end of data -------
.
HAEIC500  BRANCH    CPAGENO,HAEIC550        * first page
.
HAEIC510  MOVE      THREE,AEICQLIN          * (F)irst
          CALL      HAEICK00
          BRANCH    EXIT,HAEIC999,AAEGICDS,HAEIC590
.                        exit     first    next
          GOTO      HAEIC600
.
HAEIC550  MOVE      FOUR,AEICQLIN           * no paging
          CALL      HAEICK00
          BRANCH    EXIT,HAEIC999,HAEIC590,HAEIC590
.                        exit     first    next
          GOTO      HAEIC600
.
HAEIC590  BEEP
          BRANCH    AEICQLIN,HAEIC410,HAEIC450,HAEIC510,HAEIC550
.
. ------- item selected -------
.
HAEIC600  MOVE      AEICQARR[FORM2],KEY6
          UNPACK    KEY6,KEY4,KEY2
          CALL      RDAEGIC1
          BRANCH    OVRCD,HAEIC590
.
          MATCH     ANSY,AEICSUBA           * using sub-analysis
          IF        @EQUAL
            MATCH     SP2,KEY2                * has a code been entered
            IF        !@EQUAL
              PACK      KEY6,AEICCODE,KEY2
              CALL      RDAEGIL1
              BRANCH    OVRCD,HAEIC590          * invalid link
            ELSE 
              PACK      KEY6,AEICCODE,SP2         * clear link code
            ENDIF
          ELSE
            PACK      KEY6,AEICCODE,SP2         * clear link code
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      HAEIC999
.
. ------- (N)ext screen selected -------
.
HAEIC700  DISPLAY   *P1:6,*EF;
          MOVE      SIX,CVERT               * first display row 
          MOVE      ONE,CCOL                * get back to LHS of screen
          MOVE      ZERO,AEICQCNT           * reset count
          ADD       ONE,CPAGENO             * add to page count
          GOTO      HAEIC300                * display the data
.
HAEIC999  RETURN
+
.*********************************************************************
.         keyin 
.*********************************************************************
HAEICK00  DISPLAY   *P1:24,*EL,"Select item, (":
                    *V2LON,ANSE,*HOFF,")xit";
          MOVE      "19",CCOL
.
          IF        AEICQLIN = ONE | AEICQLIN = TWO
            DISPLAY   ", (",*V2LON,ANSN,*HOFF,")ext";
            ADD       EIGHT,CCOL
          ENDIF
          IF        AEICQLIN = ONE | AEICQLIN = THREE
            DISPLAY   ", (",*V2LON,ANSF,*HOFF,")irst";
            ADD       NINE,CCOL
          ENDIF
.
          DISPLAY   " : ";
          ADD       FOUR,CCOL
.
HAEICK10  KEYIN     *PCCOL:24,*V2LON,*JR,DIM2
          PACK      DIM2,DIM2,SP2
          TYPE      DIM2
          GOTO      HAEICK50 IF EQUAL       * number entered
.
          UNPACK    DIM2,ANS,ANS
          REP       UPPLOW,ANS
          MOVE      ZERO,EXIT
          REP       "E1F2N3",ANS
          MOVE      ANS,EXIT
          TYPE      ANS
          GOTO      HAEICK99 IF EQUAL       * valid number
.
HAEICK20  BEEP
          GOTO      HAEICK10
.
HAEICK50  MOVE      DIM2,FORM2
          MOVE      ZERO,EXIT
          COMPARE   FORM2,ZERO
          GOTO      HAEICK20 IF NOT LESS    * negative invalid
          COMPARE   FORM2,AEICQCNT
          GOTO      HAEICK20 IF LESS        * too high
.
HAEICK99  RETURN
+
