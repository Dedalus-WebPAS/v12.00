.*****************************************************************************
.*    Program      : FXPATDGW                                                *
.*    Description  : Conversion to new File layout                           *
.*                                                                           *
.*    Author       : Mike Laming   CAR 137125 July 2007 changes              *
.*    Date         : 11/04/2007                                              *
.*    Modifications:                                                         *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001FD
          INC       PATDGWFD/INC                        * new format
.
OLDFILE   IFILE     SQL, FIXED=109, KEY="U1-4"             
.
CMDLINE   DIM       100
NEWLINNO  DIM       2
RECNUM    FORM      8
OLDSPARE  DIM       24                            * decreasing to 21
.
PRGNAM    INIT      "CONVERSION PATDGWFD"
PRGID     INIT      "FXPATDGW"
VERSION   INIT      "V12.00.00"
.*****************************************************************************
.
. Start of Program
.
          CALL      INIT0000                      * init and open files
.
MAIN1000  CALL      OPTN0000
          BRANCH    EXIT,MAIN9999
.
          CALL      OPEN0000
          BRANCH    EXIT,MAIN1000
.
. Loop thru old file and write records to new file
.
MAIN5000  CALL      READ0000                      * read old records and write
          GOTO      MAIN1000
.
MAIN9999  CHAIN     PGM
+
.******************************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.******************************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
.
INIT9999  RETURN
.
.******************************************************************************
.*                          OPEN0000                                *
.*          Check that the files needed exist & open them           *
.******************************************************************************
OPEN0000  MOVE      ZERO,OVRCD
          DISPLAY   *P1:3,*EF;
.
. open old file
.
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE,"patdgwaf"
          TRAPCLR   IO
.
          BRANCH    OVRCD,OPEN5000                * check if Old file exists
.
. check if new file already exists
.
          MOVE      ZERO,OVRCD
.
          TRAP      OVERCOND IF IO
          OPEN      PATDGWA1,"newdgwaf"
          TRAPCLR   IO
.
          BRANCH    OVRCD,OPEN2000
          CLOSE     PATDGWA1
.
. File found. Tell users and check if they want to proceed
.
OPEN1000  KEYIN     *P1:10,*B,*EF,*V2LON,"New file already exists.":
                    *HOFF," Do you want to Proceed (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*V2LON,ANS
.
          REP       UPPLOW,ANS
.
          CMATCH    ANSY,ANS
          GOTO      OPEN2000 IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      OPEN9000 IF EQUAL
.
          GOTO      OPEN1000
.
. Create the new file
.
OPEN2000  EXECUTE   "iserase newdgwaf",TASKID
          CLEAR     CMDLINE
          APPEND    "isbuild newdgwaf 109 ",CMDLINE
          APPEND    "UC1-4,85-87 UC5-8,1-4,85-87 UC9-68,1-4,85-87",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      PATDGWA1,"newdgwaf"
.
. set the flag to say we want to continue
.
          MOVE      ZERO,EXIT
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:12,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:14,"Record : "
.
          GOTO      OPEN9999
.
. old file not found
.
OPEN5000  KEYIN     *P1:10,*B,*V2LON,"Old file not found.",*HOFF:
                    "  Hit ",*V2LON,"<ENTER>",*HOFF," to exit  ":
                    *EOFF,ANS
.
. we don't want to continue
.
OPEN9000  MOVE      ONE,EXIT
.
OPEN9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Run Conversion":
                    *P1:7,"Select Option :";
.
OPTN1000  KEYIN     *P17:7,*DV,UNDLN2,*P17:7,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.******************************************************************************
READ0000  PACK      KEY4,SP5
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
          UNPACK    OLDSPARE,PTDWDRGV             * new field 
          PACK      PTDWSPAR,SP30
          PACK      KEY7,PTDWDRGC,PTDWDRGV,SP10
          CALL      WRPTDGW1                      * write to new file
.
          IF        (RECNUM%50) = 0
            DISPLAY   *P10:14,*V2LON,RECNUM,*P20:14,KEY6
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     PATDGWA1                      * close new file
          CLOSE     OLDFILE                       * close old file
          DISPLAY   *P10:14,*V2LON,RECNUM,*P20:14,KEY6
.
          EXECUTE   "cp `ldat patdgwaf.dat` savdgwaf.dat",TASKID
          EXECUTE   "cp `ldat patdgwaf.idx` savdgwaf.idx",TASKID
.
          CALL      IBACLOCK
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:18,"Ended   : ",CDATE,SP1,CTIMEIS
.
READ7000  KEYIN     *P1:23,*B,*EF,*V2LON,"Conversion is complete.":
                    *P1:24,*HOFF,"Copy back the new file (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*V2LON,ANS
          REP       UPPLOW,ANS
.
          CMATCH    ANSY,ANS
          GOTO      READ8000 IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      READ9000 IF EQUAL
.
READ8000  EXECUTE   "mv newdgwaf.dat `ldat patdgwaf.dat`",TASKID
          EXECUTE   "mv newdgwaf.idx `ldat patdgwaf.idx`",TASKID
.
READ9000  DISPLAY   *P1:23,*EF
.
READ9999  RETURN

.
. ---- OLD IO ROUTINES ----
.
READSOLD  RESET     KEY4
          READ      OLDFILE,KEY4;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE;PTDWDRGC,PTDWMDCC,PTDWDESC,PTDWLOWT:
                            PTDWHIGH,PTDWRWGT,PTDWCLSP,OLDSPARE
          GOTO      OVERCOND IF OVER
          RETURN
.
          INC       PATDGWIO/INC
          INC       STD001IO
