. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS44                                             *
. * Desc      :   Cash Trial Balance                                   *
. **********************************************************************
. * Date      :   10.12.1990                                           *
. * Author    :   Peter Eddey                                          *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
.
          INC       FMSSTDDF
          INC       FMSAMAFD/INC
          INC       FMSONCFD/INC
          INC       FMSLMAFD/INC
          INC       FMSLMCFD/INC
          INC       FMSFPSFD/INC
          INC       FMSDSFFD/INC
          INC       FMSCTRFD/INC
          INC       FMSCSAFD/INC
.
.**********************************************************************
.*                   Data variables required                          *
.**********************************************************************
BANKNAME  DIM       35
CREDNAME  DIM       35
DEBTNAME  DIM       35
DISCNAME  DIM       35
PAYMNAME  DIM       35
AGSTNAME  DIM       35
CGSTNAME  DIM       35
.
ACCTTYPE  DIM       15
FMSP      INIT      "fmsp"
FMSR      INIT      "fmsr"
FORMAT    INIT      "(999,999,999.99 "
EFINDATE  DIM       10
ENDDATE    DIM       8
EPERDATE  DIM       10
FPSFILE   DIM       8
ITEM      FORM      1
MODIFLAG  FORM      1
NOTES     DIM       30
NEGONE    FORM      "-1"
OUTPERI   DIM       16 
OUTTRAN   DIM       16
PERIAMT   FORM      9.2
PERIDESC  DIM       15
PERITYPE  FORM      9.2 
FPERIOD   DIM       2
RECCOUNT  FORM      5
SAVETYPE  DIM       1
STARTDTE  DIM       8
TOTLPERI  FORM      9.2
TOTLTRAN  FORM      9.2
TRANAMT   FORM      9.2
TRANTYPE  FORM      9.2
CTRFILE   DIM       8
VALIDFLG  FORM      1
YEAREND   DIM       4
Z12       INIT      "zzzzzzzzzzzz"
.
PRGID     INIT      "IBAFMS44"
PRGNAM    INIT      "Cash Trial Balance"
VERSION   INIT      "V12.00.00"
.
.**********************************************************************
.*                           ML0000                                   *
.*                    Mainline  Code                                  *
.**********************************************************************
.
ML0000
          CALL      INIT0000             * initialisation
.
ML1000
          CALL      DSCR0000             * display the screen
.
          CALL      KRED0000             * keyin the report details
          BRANCH    EXIT,ML9999,ML9999

.
ML2000
          CALL      SIPC0000             * select item run report exit
          BRANCH    EXIT,ML9999,ML9999   * cancel or exitchar selected
.
.----  Post selected -----
.
          CALL      OPEN0000             * open the relative files     
          BRANCH    EXIT,ML2000
          CALL      PRIN0000             * Print the report
          GOTO      ML2000               * return to the prompt
.
ML9999    CHAIN     PGM
.
.*********************************************************************
.*                          INIT0000                                 *
.*                     initialisation                                *
.*********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P64:24,"fmsamaaf"
          OPEN      FMSAMAA1,"fmsamaaf"
          OPEN      FMSAMAA2,"fmsamaaf"
          OPEN      FMSAMAA3,"fmsamaaf"
.
          DISPLAY   *P64:24,"fmslmaaf"
          OPEN      FMSLMAA1,"fmslmaaf"
.
          DISPLAY   *P64:24,"fmslmcaf"
          OPEN      FMSLMCA1,"fmslmcaf"
         
          MOVE      ZERO,MODIFLAG
.
INIT9999  RETURN

.**********************************************************************
.*                         DSCR0000                                   *
.*                     Display the screen                             *
.**********************************************************************
.
DSCR0000
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,"1.",*HOFF,"  Ledger           : ":
                    *P2:5,*V2LON,"2.",*HOFF,"  Financial Year   : ":
                    *P2:6,*V2LON,"3.",*HOFF,"  Period           : "
.
DSCR9999  RETURN
.
.**********************************************************************
.*                         KRED0000                                   *
.*               Keyin the report details                             *
.**********************************************************************
.
KRED0000
          MOVE      ZERO,ITEM
.
.-----  This routine keyins in the fields by incrementing a  -----
.-----  counter and callling the modify routine.             -----
.
KRED1000
          ADD       ONE,ITEM
.
          COMPARE   FOUR,ITEM            * test if all details have been
          GOTO      KRED9999 IF EQUAL      entered
.
          MOVE      ITEM,CCITEM
          CALL      MRED0000             * call keyin routine
          BRANCH    EXIT,KRED2000,KRED9999      exit if exitchar
          GOTO      KRED1000
.
.----- spaces entered so keyin previous field ----
.
KRED2000
          SUB       TWO,ITEM
.
          GOTO      KRED1000
.
KRED9999  RETURN
.
.**********************************************************************
.*                         MRED0000                                   *
.*                  Modify the report details                         *
.**********************************************************************
.
MRED0000
          BRANCH    CCITEM,MRED1000,MRED2000,MRED3000
          BEEP
          GOTO      MRED9999
.
MRED1000
          CALL      KLED0000             * keyin ledger           
          GOTO      MRED9999
.
MRED2000 
          CALL      KFIN0000             * keyin financial year
          BRANCH    MODIFLAG,MRED3000    * if in modify mode enforce
          GOTO      MRED9999               keyin of period
.
MRED3000
          BRANCH    EXIT,MRED9999,MRED9999
          CALL      KPER0000             * keyin period        
          GOTO      MRED9999
.
MRED9999  RETURN
.
.**********************************************************************
.*                         KLED0000                                   *
.*                      keyin the ledger                              *
.**********************************************************************
.
KLED0000
          MOVE      ZERO,EXIT
          MOVE      FOUR,CVERT
          MOVE      TWENTY5,CCOL
.
          CALL      KLMAFM00
.
          BRANCH    EXIT,KLED9000,KLED9000  * if spaces or exitchar in
.                                                exit program
          DISPLAY   *P42:4,FMLADESC
          GOTO      KLED9999
.
KLED9000
          MOVE      TWO,EXIT
    
KLED9999  RETURN
.
RLMAFM00
          CALL      DSCR0000
          CALL      RDIS0000
                    
          RETURN
.
.********************************************************************
.*                          KFIN0000                                *
.*                     keyin the finacial year                      *
.********************************************************************
.
KFIN0000
          MOVE      ZERO,EXIT
.
KFIN0500
.
          DISPLAY   *P25:5,*EL
          MOVE      TWENTY5,CCOL
          MOVE      FIVE,CVERT
.
          CALL      KLMCFM00             * keyin financial year
. 
          BRANCH    EXIT,KFIN9000,KFIN9999
.
          CLEAR     EFINDATE
          CLEAR     YEAREND
          BRANCH    FMLAPERS,KFIN3000    * branch if thirteen periods
.
. 12 periods
.
          UNPACK    FMLC12TO,CCENT,CYEAR,CMON,CDAY
          PACK      EFINDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          PACK      YEAREND,CCENT,CYEAR  
          DISPLAY   *P32:5,"Ending ",*P42:5,*V2LON,EFINDATE
          GOTO      KFIN9999
.
. 13 periods
.
KFIN3000
          UNPACK    FMLC13TO,CCENT,CYEAR,CMON,CDAY
          PACK      EFINDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          PACK      YEAREND,CCENT,CYEAR
          DISPLAY   *P32:5,"Ending ",*P42:5,*V2LON,EFINDATE
          GOTO      KFIN9999
.
KFIN9000  BRANCH    MODIFLAG,KFIN0000
          MOVE      ONE,EXIT
.
KFIN9500  DISPLAY   *P25:5,*EL
.
KFIN9999  RETURN
.
RLMCFM00
          CALL      DSCR0000
          CALL      RDIS0000
          RETURN
.
.
.********************************************************************
.*                          KPER0000                                *
.*                     keyin the finacial period                    *
.********************************************************************
.
KPER0000
          CLEAR     STARTDTE
          CLEAR     ENDDATE
          CLEAR     PERIDESC
.
          DISPLAY   *P25:6,*EL
          MOVE      ZERO,EXIT
          KEYIN     *P25:6,*DV,UNDLN2:
                    *P25:6,*V2LON,FORM2:
                    *P25:6,*DV,FORM2
.
          COMPARE   ONE,FORM2           * exit if equal
          GOTO      KPER9000 IF LESS 
.
          BRANCH    FMLAPERS,KPER2000    * branch on number of periods
.
          COMPARE   TEN3,FORM2           * test for <= 12 periods
          GOTO      KPER5000 IF LESS
          BEEP 
          GOTO      KPER0000
KPER2000
.
          COMPARE   TEN4,FORM2           * test for <= 13 periods
          GOTO      KPER5000 IF LESS
          BEEP 
          GOTO      KPER0000
.
KPER5000
          MOVE      FORM2,FPERIOD        * set period variable
.
          BRANCH    FORM2,KPER5100,KPER5200,KPER5300,KPER5400,KPER5500:
                          KPER5600,KPER5700,KPER5800,KPER5900,KPER6000:
                          KPER6100,KPER6200,KPER6300
.
KPER5100
          UNPACK    FMLC01TO,CCENT,CYEAR,CMON,CDAY
          PACK      EPERDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          DISPLAY   *P32:6,"Ending ",*P42:6,*V2LON,EPERDATE
          MOVE      FMLC01TO,ENDDATE     * set end date
          MOVE      FMLC01FR,STARTDTE    * set period start date
          MOVE      FMLC01DE,PERIDESC    * set period description
          GOTO      KPER9999
KPER5200
          UNPACK    FMLC02TO,CCENT,CYEAR,CMON,CDAY
          PACK      EPERDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          DISPLAY   *P32:6,"Ending ",*P42:6,*V2LON,EPERDATE
          MOVE      FMLC02TO,ENDDATE     * set end date
          MOVE      FMLC02FR,STARTDTE    * set period start date
          MOVE      FMLC02DE,PERIDESC    * set period description
          GOTO      KPER9999
KPER5300
          UNPACK    FMLC03TO,CCENT,CYEAR,CMON,CDAY
          PACK      EPERDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          DISPLAY   *P32:6,"Ending ",*P42:6,*V2LON,EPERDATE
          MOVE      FMLC03TO,ENDDATE     * set end date
          MOVE      FMLC03FR,STARTDTE    * set period start date
          MOVE      FMLC03DE,PERIDESC    * set period description
          GOTO      KPER9999
KPER5400
          UNPACK    FMLC04TO,CCENT,CYEAR,CMON,CDAY
          PACK      EPERDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          DISPLAY   *P32:6,"Ending ",*P42:6,*V2LON,EPERDATE
          MOVE      FMLC04TO,ENDDATE     * set end date
          MOVE      FMLC04FR,STARTDTE    * set period start date
          MOVE      FMLC04DE,PERIDESC    * set period description
          GOTO      KPER9999
KPER5500
          UNPACK    FMLC05TO,CCENT,CYEAR,CMON,CDAY
          PACK      EPERDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          DISPLAY   *P32:6,"Ending ",*P42:6,*V2LON,EPERDATE
          MOVE      FMLC05TO,ENDDATE     * set end date
          MOVE      FMLC05FR,STARTDTE    * set period start date
          MOVE      FMLC05DE,PERIDESC    * set period description
          GOTO      KPER9999
KPER5600
          UNPACK    FMLC06TO,CCENT,CYEAR,CMON,CDAY
          PACK      EPERDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          DISPLAY   *P32:6,"Ending ",*P42:6,*V2LON,EPERDATE
          MOVE      FMLC06TO,ENDDATE     * set end date
          MOVE      FMLC06FR,STARTDTE    * set period start date
          MOVE      FMLC06DE,PERIDESC    * set period description
          GOTO      KPER9999
KPER5700
          UNPACK    FMLC07TO,CCENT,CYEAR,CMON,CDAY
          PACK      EPERDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          DISPLAY   *P32:6,"Ending ",*P42:6,*V2LON,EPERDATE
          MOVE      FMLC07TO,ENDDATE     * set end date
          MOVE      FMLC07FR,STARTDTE    * set period start date
          MOVE      FMLC07DE,PERIDESC    * set period description
          GOTO      KPER9999
KPER5800
          UNPACK    FMLC08TO,CCENT,CYEAR,CMON,CDAY
          PACK      EPERDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          DISPLAY   *P32:6,"Ending ",*P42:6,*V2LON,EPERDATE
          MOVE      FMLC08TO,ENDDATE     * set end date
          MOVE      FMLC08DE,PERIDESC    * set period description
          MOVE      FMLC08FR,STARTDTE    * set period start date
          GOTO      KPER9999
KPER5900
          UNPACK    FMLC09TO,CCENT,CYEAR,CMON,CDAY
          PACK      EPERDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          DISPLAY   *P32:6,"Ending ",*P42:6,*V2LON,EPERDATE
          MOVE      FMLC09TO,ENDDATE     * set end date
          MOVE      FMLC09FR,STARTDTE    * set period start date
          MOVE      FMLC09DE,PERIDESC    * set period description
          GOTO      KPER9999
KPER6000
          UNPACK    FMLC10TO,CCENT,CYEAR,CMON,CDAY
          PACK      EPERDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          DISPLAY   *P32:6,"Ending ",*P42:6,*V2LON,EPERDATE
          MOVE      FMLC10TO,ENDDATE     * set end date
          MOVE      FMLC10FR,STARTDTE    * set period start date
          MOVE      FMLC10DE,PERIDESC    * set period description
          GOTO      KPER9999
KPER6100
          UNPACK    FMLC11TO,CCENT,CYEAR,CMON,CDAY
          PACK      EPERDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          DISPLAY   *P32:6,"Ending ",*P42:6,*V2LON,EPERDATE
          MOVE      FMLC11TO,ENDDATE     * set end date
          MOVE      FMLC11FR,STARTDTE    * set period start date
          MOVE      FMLC11DE,PERIDESC    * set period description
          GOTO      KPER9999
KPER6200
          UNPACK    FMLC12TO,CCENT,CYEAR,CMON,CDAY
          PACK      EPERDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          DISPLAY   *P32:6,"Ending ",*P42:6,*V2LON,EPERDATE
          MOVE      FMLC12TO,ENDDATE     * set end date
          MOVE      FMLC12FR,STARTDTE    * set period start date
          MOVE      FMLC12DE,PERIDESC    * set period description
          GOTO      KPER9999
KPER6300
          UNPACK    FMLC13TO,CCENT,CYEAR,CMON,CDAY
          PACK      EPERDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          DISPLAY   *P32:6,"Ending ",*P42:6,*V2LON,EPERDATE
          MOVE      FMLC13TO,ENDDATE     * set end date
          MOVE      FMLC13FR,STARTDTE    * set period start date
          MOVE      FMLC13DE,PERIDESC    * set period description
          GOTO      KPER9999
.
KPER9000  
          BRANCH    MODIFLAG,KPER0000
          MOVE      ONE,EXIT
          DISPLAY   *P25:6,*EL
.
KPER9999
          RETURN
.**********************************************************************
.*                         SIPC0000                                   *
.*               Select Item Post Cancel                              *
.**********************************************************************
.
SIPC0000
          MOVE      ZERO,EXIT
          MOVE      ONE,MODIFLAG
.
          CALL      MQST0000             * select item run report exit
.
          COMPARE   ZERO,CCITEM
          GOTO      SIPC9999 IF EQUAL
          GOTO      SIPC9500 IF LESS
.
          CALL      MRED0000            * modify the field
          BRANCH    EXIT,SIPC0000,SIPC9999
.
          GOTO      SIPC0000
.
SIPC9500  MOVE      ONE,EXIT             * cancel selected
.
SIPC9999  RETURN
.
.**********************************************************************
.*                           PRIN0000                                 *
.*                        Print the report                            *
.**********************************************************************
.
PRIN0000
          ADD       FOUR,CLNO      
          CLOCK     TIME,CTIMEIS

.----- clear the ledger totals ----
.
          MOVE      ZERO,TOTLPERI
          MOVE      ZERO,TOTLTRAN
.
.----- clear the account type totals ----
.
          MOVE      ZERO,PERITYPE
          MOVE      ZERO,TRANTYPE
          MOVE      ZERO,RECCOUNT
.
          DISPLAY   *P1:24,*EL,"Printing   : "
.
.----- get starting account type ----
.
          PACK      KEY14,FMLALEDG,SP20      
          CALL      RSFMAM2
          CALL      RKFMAM2
          BRANCH    OVRCD,PRIN9999
          MOVE      FMAMTYPE,SAVETYPE
          CALL      HEAD0000             * print the heading
.
          PACK      KEY14,FMLALEDG,SP20      
          CALL      RSFMAM2              * position at start of ledger
PRIN1000
          CALL      RKFMAM2              * read next
          BRANCH    OVRCD,PRIN9500       * end of report if over
.
          MATCH     FMLALEDG,FMAMLEDG    * test if same ledger
          GOTO      PRIN9500 IF NOT EQUAL
.
          MATCH     "6",FMAMTYPE
          GOTO       PRIN9500 IF NOT LESS
PRIN3000       
          MATCH     SAVETYPE,FMAMTYPE    * if the account type has 
.                                            changed then throw the page
          GOTO      PRIN6000 IF NOT EQUAL
.
          COMPARE   SIXTY,CLNO           * if the page is full
.                                             then throw the page
          GOTO      PRIN7000 IF EQUAL
.
PRIN4000
.
          ADD       ONE,RECCOUNT
          ADD       ONE,CLNO             * 
.
.---- display the account being processed ---
.
          DISPLAY   *P14:24,*V2LON,FMAMLEDG,SLASH,FMAMACCT
.
          CALL      SETP0000             * set print display
          CALL      PLIN0000             * print the report line
.
          GOTO      PRIN1000
.
PRIN6000
          CALL      PTYT0000             * end of type so print type
.                                          totals
          MOVE      ZERO,PERITYPE          then clear totals
          MOVE      ZERO,TRANTYPE
          
PRIN7000
          MOVE      FMAMTYPE,SAVETYPE
          CALL      HEAD0000             * print heading
          GOTO      PRIN4000
.
PRIN9500
          CALL      PTYT0000             * end of type so print type
.                                          totals
          MOVE      ZERO,CPAGENO
          CALL      PRPT0000             * print report totals
          
.
PRIN9999  
          DISPLAY   *P1:24,*EL
          RETURN
.
.**********************************************************************
.*                           PTYT0000                                 *
.*                  Print the account type totals                     *
.**********************************************************************
.
PTYT0000
.
.----- format the numeric totals ----
.
          MOVE      FORMAT,OUTPERI
          FEDIT     PERITYPE,OUTPERI
          MOVE      FORMAT,OUTTRAN
          FEDIT     TRANTYPE,OUTTRAN
.
          CALL      UND132
       	  PRINT     *2,"Total ",ACCTTYPE,*60,OUTPERI:
                    *84,OUTTRAN,*101,"|"
.
PTYT9999  RETURN
.
.**********************************************************************
.*                           PRPT0000                                 *
.*                  Print the report ledger totals                    *
.**********************************************************************
.
PRPT0000
.
.----- format the numeric totals ----
.
          MOVE      FORMAT,OUTPERI
          FEDIT     TOTLPERI,OUTPERI
          MOVE      FORMAT,OUTTRAN
          FEDIT     TOTLTRAN,OUTTRAN
.
          CALL      UND132
          PRINT     *2,"Ledger Total",*60,OUTPERI:
                    *84,OUTTRAN,*101,"|"
          CALL      UND132
.
PRPT9999  RETURN
.
.
.**********************************************************************
.*                           SETP0000                                 *
.*                  set up the print variables                        *
.**********************************************************************
.
SETP0000
          CLEAR     NOTES
          MOVE      ZERO,TRANAMT
          MOVE      ZERO,PERIAMT
.
.---- get the accrual total for the period from the summary file ---
.
          PACK      KEY14,FMLALEDG,FMAMACCT
          CALL      RDFMFP1
          BRANCH    OVRCD,SETP7000
.
          MOVE      FPERIOD,FORM2
          BRANCH    FORM2,SETP5100,SETP5200,SETP5300,SETP5400,SETP5500:
                           SETP5600,SETP5700,SETP5800,SETP5900,SETP6000:
                           SETP6100,SETP6200,SETP6300
.
SETP5100
          MOVE      FMFPC01,PERIAMT
          GOTO      SETP7000
SETP5200
          MOVE      FMFPC02,PERIAMT
          GOTO      SETP7000
SETP5300
          MOVE      FMFPC03,PERIAMT
          GOTO      SETP7000
SETP5400
          MOVE      FMFPC04,PERIAMT
          GOTO      SETP7000
SETP5500
          MOVE      FMFPC05,PERIAMT
          GOTO      SETP7000
SETP5600
          MOVE      FMFPC06,PERIAMT
          GOTO      SETP7000
SETP5700
          MOVE      FMFPC07,PERIAMT
          GOTO      SETP7000
SETP5800
          MOVE      FMFPC08,PERIAMT
          GOTO      SETP7000
SETP5900
          MOVE      FMFPC09,PERIAMT
          GOTO      SETP7000
SETP6000
          MOVE      FMFPC10,PERIAMT
          GOTO      SETP7000
SETP6100
          MOVE      FMFPC11,PERIAMT
          GOTO      SETP7000
SETP6200
          MOVE      FMFPC12,PERIAMT
          GOTO      SETP7000
SETP6300
          MOVE      FMFPC13,PERIAMT
          GOTO      SETP7000
.
SETP7000
          ADD       PERIAMT,PERITYPE
          ADD       PERIAMT,TOTLPERI
.
.-----  total the transactions for the period for this account ---
.
          PACK      KEY32,FMLALEDG,FMAMACCT,STARTDTE,SP20
.
          CALL      RSFMCT2
SETP8000
          CALL      RKFMCT2
          BRANCH    OVRCD,SETP9000
.
          MATCH     FMLALEDG,FMCTLEDG    * test same ledger
          GOTO      SETP9000 IF NOT EQUAL
.
          MATCH     FMAMACCT,FMCTACCT    * test same account
          GOTO      SETP9000 IF NOT EQUAL
.
          MATCH     FMCTPDAT,ENDDATE     * test not past end of period
          GOTO      SETP9000 IF LESS
.
          CALL      FAMT0000             * Fix Negative Amounts
.
          ADD       FMCTAMT,TRANTYPE
          ADD       FMCTAMT,TRANAMT
.
          GOTO      SETP8000
.
.----- Test if the thre totals are out of balance ----
.
SETP9000
          ADD       TRANAMT,TOTLTRAN
          COMPARE   PERIAMT,TRANAMT
          GOTO      SETP9999 IF EQUAL
.
          MOVE      "Summary Files Out of Balance",NOTES
          GOTO      SETP9999
.
SETP9999  RETURN
.
.**********************************************************************
.*                         PLIN0000                                   *
.*                   Print a line on the report                       *
.**********************************************************************
.
PLIN0000
.
.----- format the numeric totals ---
.
          MOVE      FORMAT,OUTPERI
          FEDIT     PERIAMT,OUTPERI
          MOVE      FORMAT,OUTTRAN
          FEDIT     TRANAMT,OUTTRAN
.
          PRINT     *1,FMAMACCT,*14,FMAMDESC,*60,OUTPERI:
                    *84,OUTTRAN,*101,"| ",NOTES
PLIN9999  RETURN
.
.**********************************************************************
.*                         HEAD0000                                   *
.*                    print the page heading                          *
.**********************************************************************
.
HEAD0000
.
.----- format the account type description for the heading ----
.
          MOVE      FMAMTYPE,FORM1
          BRANCH    FORM1,HEAD1100,HEAD1200,HEAD1300,HEAD1400,HEAD1500
.
HEAD1100
          MOVE      "Income",ACCTTYPE
          GOTO      HEAD2000
HEAD1200
          MOVE      "Expense",ACCTTYPE
          GOTO      HEAD2000
HEAD1300
          MOVE      "Asset",ACCTTYPE
          GOTO      HEAD2000
HEAD1400
          MOVE      "Liability",ACCTTYPE
          GOTO      HEAD2000
HEAD1500
          MOVE      "Equity",ACCTTYPE
          GOTO      HEAD2000
.
HEAD2000
          CALL      HEAD132
          PRINT     *1,"Ledger   :  ",FMLALEDG,SP1,FMLADESC:
                    *54,"Account Type : ",ACCTTYPE:
                    *79,"Accounting Period : ",FPERIOD,SP1,PERIDESC:
                    *115,"Year Ending : ",YEAREND
.
          CALL      UND132
.
          PRINT     *1,"Account",*14,"Description",*69,"Period":
                    *88,"Transaction",*101,"|":
                    *104,"Notes"
.
          CALL      UND132
.
HEAD9999  RETURN
         
+
.***********************************************************************
.         Subroutine to SELECT ITEM, RUN REPORT, EXIT
.-----------------------------------------------------------------------
.
MQST0000  MOVE      ZERO,CCITEM
          MOVE      SP10,CCITEMD
          KEYIN     *P1:24,*EL,"Select Item, (",*V2LON,"R",*HOFF,")un ":
                    "Report, e(":
                    *V2LON,"X",*HOFF,")it ? ",*V2LON,*JR,CCITEMD;
.
          RESET     CCITEMD
          GOTO      MQST0000 IF EOS
.
          REP       UPPLOW,CCITEMD
.
          MATCH     "  X",CCITEMD
          GOTO      MQST1000 IF EQUAL
.
          MATCH     "  R",CCITEMD
          GOTO      MQST2000 IF EQUAL
.
          TYPE      CCITEMD
          GOTO      MQST0500 IF NOT EQUAL
.
          MOVE      CCITEMD,CCITEM
          COMPARE   ONE,CCITEM
          GOTO      MQST0500 IF LESS
.
          RETURN
.
MQST0500  BEEP
          GOTO      MQST0000
.
MQST1000  MOVE      "-1",CCITEM
          RETURN
.
MQST2000  MOVE      "0",CCITEM
          RETURN
+
.**********************************************************************
.                          OPEN0000
.                   Open the fps trn and dsf files
.**********************************************************************
.
OPEN0000
.
          CLEAR     FPSFILE
          CLEAR     CTRFILE
.
.----  set up the filenames ----
.
          PACK      FPSFILE,FMSP,FMLCYEAR    
          PACK      CTRFILE,FMSR,FMLCYEAR
.
OPEN1000
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FMSFPSA1,FPSFILE    * open FPS file
          TRAPCLR   IO
          BRANCH    OVRCD,OPEN6000       * if no file then no records
          OPEN      FMSFPSA2,FPSFILE    * open FPS file
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FMSCTRA2,CTRFILE    * open CTR file
          TRAPCLR   IO
          BRANCH    OVRCD,OPEN7000       * if no file then no records
          GOTO      OPEN9999
.
OPEN6000
          MOVE      ONE,EXIT
          BEEP
          DISPLAY   *P1:24,*EL,"No financial period summaries exist for this year. ";
          CALL      HITENTER
          GOTO      OPEN9999
.
OPEN7000
          MOVE      ONE,EXIT
          BEEP
          DISPLAY   *P1:24,*EL,"No transactions exist for this year. ";
          CALL      HITENTER
          GOTO      OPEN9999
.
OPEN9999  RETURN
.
.**********************************************************************
. FAMT0000 - Fix BCAMT as Debit/Credit
.**********************************************************************
FAMT0000  MATCH     "JC",FMCTTYPE
          GOTO      FAMT1000 IF EQUAL
.
          MATCH     "CC",FMCTTYPE
          GOTO      FAMT1000 IF EQUAL
.
          MATCH     "CS",FMCTTYPE
          GOTO      FAMT1000 IF EQUAL
.
          MATCH     "CR",FMCTTYPE
          GOTO      FAMT1000 IF EQUAL
.
          MATCH     "RF",FMCTTYPE
          GOTO      FAMT1000 IF EQUAL
.
          MATCH     "RI",FMCTTYPE
          GOTO      FAMT1000 IF EQUAL
.
          GOTO      FAMT9999
.
FAMT1000  MULT      SEQ,FMCTAMT
.
FAMT9999  RETURN
.
RDIS0000
          DISPLAY   *P25:4,*V2LON,FMLALEDG,*HOFF,*P42:4,FMLADESC
          DISPLAY   *P25:5,*V2LON,FMLCYEAR,*HOFF:
                    *P32:5,"Ending ",*P42:5,*V2LON,EFINDATE
          BRANCH    MODIFLAG,RDIS5000
          GOTO      RDIS9999
RDIS5000
          DISPLAY   *P25:6,*V2LON,FPERIOD,*HOFF:
                    *P32:6,"Ending ",*P42:6,*V2LON,EPERDATE
RDIS9999  RETURN
.
          INC       FMSSTDCD
          INC       FMSAMAIO/INC
          INC       FMSONCIO/INC
          INC       FMSLMAIO/INC
          INC       FMSFPSIO/INC
          INC       FMSDSFIO/INC
          INC       FMSLMCIO/INC
          INC       FMSCTRIO/INC
          INC       FMSLMCKY
          INC       FMSLMAKY
