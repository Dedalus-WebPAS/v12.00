.******************************************************************************
.* Include Name   : KYAAEGIC.PBL                                              *
.*                                                                            *
.* Description    : Keyin of a Investigation code from AAEGICFD               *
.*                                                                            *
.* Author         : LOFTY      03/08/1993                                     *
.*                                                                            *
.* Parameters     : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF3 - default value                                  *
.*                  CKYIMAND - 1=mandatory                                    *
.*                                                                            *
.* Returns        : AEICCODE - Keyed in investigation code                    *
.*                  AEICDESC - Description                                    *
.*                  AEILCODE - Keyed in investigation link code               *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.* On return      : You must display the description yourself.                *
.*                                                                            *
.* Modifications  :                                                           *
.*        V5.03.01  23/06/1995    Justin Coates  SRF 133250                   *
.*                  if a code is selected that has links, but the main code   *
.*                     is selected, set AEILCODE to spaces                    *
.*                  fix the unpack of key6 after ? routine                    *
.******************************************************************************
KYAAEGIC  MOVE      ECOL,CCOL
          MOVE      EVERT,CVERT
          MOVE      CKYIDEF3,KEY3
          KEYIN     *PCCOL:CVERT,*V2LON,*RV,KEY3:
                    *PCCOL:CVERT,*DV,KEY3
          PACK      KEY3,KEY3,SP3
          MATCH     SP3,KEY3
          GOTO      KAEIC790 IF EQUAL       * nothing entered
          MATCH     EXITCHAR,KEY3
          GOTO      KAEIC820 IF EQUAL       * exitchar entered
          MATCH     QUEST,KEY3
          GOTO      KAEIC200 IF NOT EQUAL   * ? not entered
.
.         ? entered
.
          MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
          CALL      AAEGICDS                * display valid entries
          CALL      PUTSCR00                * restore screen
          BRANCH    EXIT,KAEIC790           * nothing entered
          UNPACK    KEY6,KEY2,ANS,ANS,KEY1,ANS
          PACK      KEY3,KEY2,KEY1
.
.         validate what was entered
.
KAEIC200  UNPACK    KEY3,KEY2,KEY1
          PACK      KEY4,KEY2,SP4 
          CALL      RDAEGIC1
          BRANCH    OVRCD,KAEIC400          * invalid code
.
          MATCH     ANSY,AEICSUBA           * using sub-analysis
          IF        @EQUAL
            MATCH     SP1,KEY1                * has a code been entered
            IF        !@EQUAL
              PACK      KEY6,AEICCODE,KEY1,SP1
              CALL      RDAEGIL1
              BRANCH    OVRCD,KAEIC450          * invalid link
            ELSE 
              MOVE      SP2,AEILCODE            * clear link code
            ENDIF
          ELSE
            MOVE      SP2,AEILCODE            * clear link code
          ENDIF
          MOVE      ZERO,EXIT               * set as valid code
          GOTO      KAEIC800
.
.         invalid code
.
KAEIC400  DISPLAY   *P1:24,*B,"Invalid Investigation Code.  ",*EL;
          CALL      HITENTER
          GOTO      KYAAEGIC
.
KAEIC450  DISPLAY   *P1:24,*B,"Invalid Sub-Analysis Code.  ",*EL;
          CALL      HITENTER
          GOTO      KYAAEGIC
.
.         nothing entered
.
KAEIC790  MOVE      ONE,EXIT                * set exit flag
          MOVE      SP4,AEICCODE
          MOVE      SP2,AEILCODE
          COMPARE   ZERO,CKYIMAND
          GOTO      KAEIC800 IF EQUAL       * not mandatory
.
          DISPLAY   *P1:24,*B,"Code must be entered.  ",*EL;
          CALL      HITENTER
          GOTO      KYAAEGIC
.
.         valid entry
.
KAEIC800  DISPLAY   *PECOL:EVERT,*V2LON,AEICCODE
          GOTO      KAEIC999 
.
.         exitchar entered
.
KAEIC820  MOVE      TWO,EXIT                * EXITCHAR entered
.
KAEIC999  RETURN
+
