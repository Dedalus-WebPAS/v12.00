.******************************************************************************
.* System         : Mental Health                                             *
.*                                                                            *
.* Include Name   : DXSETVAL.PBL                                              *
.*                                                                            *
.* Function       : This moves the required length of the data variable to the*
.*                  DATA string and sets the ATTRIBUTES string to the att. val*
.*                                                                            *
.* Author         : Justin Coates                                             *
.*                                                                            *
.* Date Written   : 14/08/1991                                                *
.*                                                                            *
.* Parameters     : SCPSLEN       the length of string to add                 *
.*                  DIM80         the string to add/insert                    *
.*                  CCOL          the keyin/display column of var.            *
.*                  SCPSATT       the attribute value                         *
.*                  SCSBDAT       the DATA string                             *
.*                  SCSBATT       the ATTRIBUTE string                        *
.*                  IMON          = 99 dont display variable                  *
.*                                                                            *
.* Returns        : SCSBDAT       the DATA string                             *
.*                  SCSBATT       the ATTRIBUTE string                        *
.*                  For SCSBATT and SCSBDAT :                                 *
.*                  The FormPointer   will be at position CCOL.               *
.*                  The LogicalLength will be at position CCOL+SCPSLEN-1      *
.*                                                                            *
.* Modifications  :                                                           *
.******************************************************************************
DXSETVAL  RESET     DIM80                   * reset data variable to start
          SETLPTR   DIM80                   * set LL to end of string
          SETLPTR   SCSBDAT                 * set LL to end of string
          SETLPTR   SCSBATT                 * set LL to end of string
          RESET     SCSBDAT,CCOL            * reset FP to desired column
          RESET     SCSBATT,CCOL            * reset FP to desired column
          MOVE      SCPSLEN,CCOL1           * get copy of length
.
.         while [we haven't added all the length of the string] &
.               [there is still data left in each string]
.
          WHILE     CCOL1 & !@EOS
            CMOVE     DIM80,SCSBDAT         * move in the letter
            CMOVE     SCPSATT,SCSBATT       * move in the attribute value
            BUMP      DIM80                 * get to next character
            BUMP      SCSBDAT               * get to next character
            BUMP      SCSBATT               * get to next character
            SUB       ONE,CCOL1             * reduce counter of length added
          DO
.
.         now set FP and LL of the DATA and ATTRIBUTES strings
.
          MOVE      CCOL,CCOL1              * starting position 
          ADD       SCPSLEN,CCOL1           * add length of string
          SUB       ONE,CCOL1               * get the correct end position - LL
.
          SETLPTR   SCSBDAT,CCOL1           * set LL for DATA
          RESET     SCSBDAT,CCOL            * set FP for DATA
.
          RESET     SCSBATT,CCOL            * set FP for ATTRIBUTES
          SETLPTR   SCSBATT,CCOL1           * set LL for ATTRIBUTES
.
          MOVE      SCPSROW,CVERT
.
          IF        !(IMON=99)
            PUTVAR    SCSBDAT,SCSBATT,CCOL,CVERT * not in print option
          ENDIF
.
          RETURN
+
