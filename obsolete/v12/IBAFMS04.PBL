. ********************************************************************** 
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS04                                             *
. * Desc      :   On-Cost Maintenance                                  *
. **********************************************************************
. * Date      :   12.09.1990                                           *
. * Author    :   Neeriem Dye                                          *
. * Mods      :                                                        *
. *                                                                    *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
.$$$$$$
.        PROGRAM IBAFMS04
.             open files and initialise variables
.             WHILE user wants to add/modify/delete DO
.                  get desired option (add, modify or delete)
.                  CASE option OF
.                       ADD     : WHILE user wants to add records DO
.                                      accept initial values
.                                      WHILE user wants modify screen data DO
.                                           get number of item to be modified
.                                           allow user to modify option
.                                      END WHILE
.                                      IF user wants to post data
.                                           THEN write new data
.                                      END IF
.                                 END WHILE
.                       MODIFY  : WHILE user wants to modify records DO
.                                      get and display desired record
.                                      WHILE user wants modify screen data DO
.                                           get number of item to be modified
.                                           allow user to modify option
.                                      END WHILE
.                                      IF user wants to post data
.                                           THEN update data
.                                      END IF
.                                 END WHILE
.                       DELETE  : WHILE user wants to delete records DO
.                                      get and display desired record
.                                      IF user wants to delete data
.                                           THEN delete data
.                                      END IF
.                                 END WHILE
.                  END CASE
.             END WHILE
.        END
.$$$$$$
. Standard FMS Accounting System Include
.---------------------------------------
          INC       FMSSTDDF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
          INC       FMSONCFD/INC
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
.
ACCEPT    FORM      1                  * accept mode (1=on)
DISPPERC  DIM       7
.
PRGID     INIT      "IBAFMS04"
PRGNAM    INIT      "On-Cost Maintenance"
VERSION   INIT      "V12.00.00"
.
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML0100    CALL      SELOPTN            * insert/modify/delete/exit ?
          COMPARE   ZERO,COPTION
          GOTO      ML9999 IF EQUAL    * exit program
.
ML0200    CALL      PROC0000           * get entry
          BRANCH    EXIT,ML0100,ML0100 * exit option
.
          LOAD      ACCEPT,COPTION,ONE,ZERO,ZERO * set up mode
          CALL      SCRA0000           * display screen A
.
          BRANCH    COPTION,ML1000,ML1000,ML2000
.
.---- add/modify data ----
.
ML1000    CALL      SELA0000           * modify screen A
          BRANCH    EXIT,ML0200        * don't save data
.
          CALL      POST0000           * post screen A data
          GOTO      ML0200             * get next entry
.
.---- delete data ----
.
ML2000    CALL      DELQST             * ok to delete (y/n/c) ?
          BRANCH    CEXIT,ML2100,ML0200,ML0100
.
ML2100    CALL      DELT0000           * delete screen A data
          GOTO      ML0200             * get next entry
.
.
ML9999    CHAIN     PGM                * chain back to menu
.
.******************************************************************************
.* INIT - Open Files                             Called by ML0000             *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening fmsoncaf";  * On-Cost file
          OPEN      FMSONCA1,"fmsoncaf"
.
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.******************************************************************************
.* SCRA - Display Screen A                       Called by SELA0000, redisps  *
.*       Requires : ACCEPT   (1=accept mode)                                  *
.******************************************************************************
SCRA0000  DISPLAY   *P1:4,"    On-Cost Code         : ",*V2LON,FMONCODE,*EF:
                    *P1:6," 1",*HOFF,". Description          : ":
                    *P1:7,*V2LON," 2",*HOFF,". Percentage           : ";
          BRANCH    ACCEPT,SCRA9999    * accept mode
.
          DISPLAY   *P28:6,*V2LON,FMONDESC:
                    *P28:7,DISPPERC

SCRA9999  RETURN
.******************************************************************************
.* DEFV - set up default variables               Called by SELA0000           *
.******************************************************************************
DEFV0000  MOVE      UNDLN70,FMONDESC   * set description
          MOVE      UNDLN70,DISPPERC   * set percentage display
.
DEFV9999  RETURN
.******************************************************************************
.* PROC - Enter On-Cost Code                  Called by ML0000             *
.*        Returns : EXIT     (2=EXITCHAR entered, 1=nothing entered, 0=cont.) *
.*                  FMONCODE (variable entered)                               *
.******************************************************************************
PROC0000  DISPLAY   *P1:4,"    On-Cost Code         : ",*EF;
.
          MOVE      "28",CCOL          * set up screen address
          MOVE      "4",CVERT
.
          COMPARE   ONE,COPTION        * insert mode ?
          GOTO      PROC1000 IF NOT EQUAL
          CALL      AONCFM00 
          GOTO      PROC9999
.
PROC1000  CALL      KONCFM00
          MOVE      FMONPERC,DISPPERC
.
PROC9999  RETURN
.******************************************************************************
.* SELA - Modify screen A data                   Called by ML0000             *
.*        Returns : EXIT     (2=EXITCHAR entered, 1=nothing entered, 0=cont.) *
.*                  FMONDISS (variable entered)                               *
.******************************************************************************
SELA0000  MOVE      ZERO,CCITEM
.
          COMPARE   ONE,ACCEPT
          CALL      DEFV0000 IF EQUAL            * set up default values
.
.---- determine option to execute ----
.
SELA0100  ADD       ONE,CCITEM                   * get next option
          BRANCH    ACCEPT,SELA0500              * in accept mode ?
          CALL      MAINQST                      * select/post/cancel ?
.
          COMPARE   ZERO,CCITEM
          GOTO      SELA9000 IF EQUAL            * post ?
.
          COMPARE   SEQ,CCITEM
          GOTO      SELA9500 IF EQUAL            * cancel ?
.
SELA0500  MOVE      "28",CCOL                    * set up screen address
          MOVE      "5",CVERT
          ADD       CCITEM,CVERT
          BRANCH    CCITEM,SELA1100,SELA1200     * execute option
.
          BRANCH    ACCEPT,SELA0900              * finished accept mode ?
          BEEP                                   * illegal option
.
SELA0900  MOVE      ZERO,ACCEPT                  * clear accept mode
          GOTO      SELA0100                     * get next option
.
.---- option 1 ----
.
SELA1100  MATCH     FMONDESC,SP70
          GOTO      SELA1150 IF NOT EQUAL        * description spaces ?
.
          PACK      FMONDESC,UNDLN70
.
SELA1150  KEYIN     *PCCOL:CVERT,*DV,FMONDESC:   * read in description
                    *PCCOL:CVERT,*V2LON,*RV,FMONDESC;
          PACK      FMONDESC,FMONDESC,SP70
.
          MATCH     FMONDESC,SP70
          GOTO      SELA1100 IF EQUAL            * description spaces ?
          MATCH     FMONDESC,UNDLN70
          GOTO      SELA9500 IF EQUAL            * nothing entered ?
          MATCH     FMONDESC,EXITCHAR
          GOTO      SELA9500 IF EQUAL            * EXITCHAR entered ?
.
          DISPLAY   *PCCOL:CVERT,*V2LON,FMONDESC;
          GOTO      SELA0100
.
.---- option 2 ----
.
SELA1200  KEYIN     *PCCOL:CVERT,*DV,DISPPERC:   * read in percentage
                    *PCCOL:CVERT,*V2LON,FMONPERC:
                    *PCCOL:CVERT,*DV,FMONPERC;
          GOTO      SELA0100
.
.---- exit points ----
.
SELA9000  MOVE      ZERO,EXIT                    * post
          GOTO      SELA9999
.
SELA9500  MOVE      ONE,EXIT                     * cancel
.
SELA9999  RETURN
.******************************************************************************
.* POST - Post data                              Called by ML0000             *
.******************************************************************************
POST0000  PACK      KEY4,FMONCODE,SP70
          COMPARE   ONE,COPTION
          GOTO      POST1000 IF NOT EQUAL
          CALL      RAFMON1                 * record exists ?
          BRANCH    OVRCD,POST1000
          DISPLAY   *P1:24,*B,*EF,"On Cost Code Has Just Been Entered - ";
          CALL      HITENTER
          GOTO      POST9999
.
POST1000  CALL      DEFMON1                 * delete old record (if possible)
          CALL      WRFMON1                 * write new record
.
POST9999  RETURN
.******************************************************************************
.* DELT - Delete data                            Called by ML0000             *
.******************************************************************************
DELT0000  PACK      KEY4,FMONCODE,SP70
          CALL      DEFMON1                 * delete old record (if possible)
.
DELT9999  RETURN
.******************************************************************************
.* INCLUDES FOR I/O'S                                                         *
.******************************************************************************
RONCFM00  
          DISPLAY   *P1:4,"    On-Cost Code         : ",*EF;
          RETURN
.
          INC       FMSONCIO/INC       * On-Cost
.
          INC       FMSONCKY           * On-Cost
          INC       FMSSTDCD           * FMS Acc. standard routines
