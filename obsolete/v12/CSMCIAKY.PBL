.---------------------------------------------------------------------------
.   System    :   Supply and Inventory               
.   Include   :   CSMCIAKY
.   Routine   :   KCSIAA00 Keyin Catalog Code        
.   Parameter :   CKEYTYP 0 = Keyin , 1 = Accept, 2 = Either
.   Return    :   EXIT = 1 null entry, 2 EXITCHAR entered
.-------------------------------------------------------------------------------
KCSIAA00  TOPIC     "CSIACAT  "
          MOVE      "63",SECTOR
          READ      CONTROLF,SECTOR;*67,INCOCATE,*141,INCNNATO
          MOVE      SP70,CSIACAT 
          COMPARE   ZERO,INCOCATE
          GOTO      KCSIAA01 IF NOT EQUAL
.
          KEYIN     *PCCOL:CVERT,*V2LON,CSIACAT
.
          ENDSET    CSIACAT 
          APPEND    SP70,CSIACAT 
          RESET     CSIACAT 
          REP       UPPLOW,CSIACAT
.
          MATCH     SP70,CSIACAT 
          IF        @EQUAL
            MOVE      ONE,EXIT
            GOTO      KCSIAA99
          ENDIF
.
          CMATCH    EXITCHAR,CSIACAT 
          IF        @EQUAL
            MOVE      TWO,EXIT
            GOTO      KCSIAA99
          ENDIF
.
          CMATCH    QUEST,CSIACAT 
          GOTO      KCSIAA10 IF NOT EQUAL
          GOTO      KCSIAA02
.
KCSIAA01  MOVE      SP70,CSIACAT 
          KEYIN     *PCCOL:CVERT,*V2LON,*JR,CSIACAT
.
          ENDSET    CSIACAT 
          APPEND    SP70,CSIACAT 
          RESET     CSIACAT 
          REP       UPPLOW,CSIACAT
.
          MATCH     SP70,CSIACAT 
          IF        @EQUAL
            MOVE      ONE,EXIT
            GOTO      KCSIAA99
          ENDIF
.
          ENDSET    CSIACAT 
          CMATCH    EXITCHAR,CSIACAT 
          IF        @EQUAL
            MOVE      TWO,EXIT
            GOTO      KCSIAA99
          ENDIF
.
          CMATCH    QUEST,CSIACAT 
          GOTO      KCSIAA02 IF EQUAL
          RESET     CSIACAT 
.
          TYPE      CSIACAT
          IF        !@EQUAL & CKEYTYP=1
            MOVE      "Entry is not numeric.  ",DISPMSG
            CALL      MESSAGE1
            GOTO      KCSIAA00
          ENDIF
          GOTO      KCSIAA10
.
KCSIAA02  MOVE      CCITEM,SCCITEM
          PROC      HCSCKA00               * Keyword Search (New)
          MOVE      SCCITEM,CCITEM
          BRANCH    EXIT,KCSIAA00,KCSIAA00
.
          MATCH     SP70,CSIACAT 
          GOTO      KCSIAA00 IF EQUAL
.
KCSIAA10  PACK      KEY7,CSIACAT,SP70
          CALL      RACSIA1 
          IF        OVRCD=1
            BRANCH    CKEYTYP,KCSIAA99,KCSIAA99
            MOVE      "Catalog Does Not Exist - ",DISPMSG
            CALL      MESSAGE1
            GOTO      KCSIAA00
          ENDIF
          IF        CKEYTYP=1
            MOVE      "Catalog Code Already Exists - ",DISPMSG
            CALL      MESSAGE1
            GOTO      KCSIAA00
          ENDIF
.
. ------- CSMciaaf record must exist as in modify mode
.
KCSIAA80  PACK      KEY7,CSIACAT
          CALL      RDCSIA1 
          MOVE      ZERO,EXIT
          GOTO      KCSIAA99
.
KCSIAA99  DISPLAY   *PCCOL:CVERT,*V2LON,CSIACAT,SP3
          TOPIC     PRGID
          RETURN
.------------------------------------------------------------
.
. Function : Procedure for Keyword Seach of Catalog 
.
. Created  : 02.04.97 B.G.Ackland
.
. Parameters : CKEYSUSP    1=Show Suspended Items
.              CKEYSTCK    S=Show Stock Only      
.                          N=Show Non-Stock Only      
.
.------------------------------------------------------------
           DEFPROC   HCSCKA00
.
           INC       CSMCKYFD/INC
           INC       CSMCICFD/INC
           INC       CSMCREFD/INC
.
DISPSOH   DIM       15
DISPSOO   DIM       15
F12       FORM      12
F2        FORM      2
FIRSTSEC  FORM      5
CATDES60  DIM       60
SAVKEY22  DIM       22
SIXTY     FORM      "60"
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
SAVSTCK   DIM       1
SAVSUSP   FORM      1
.---------------------------------------------------------------------------
.   System    :   Supply and Inventory               
.   Include   :   CSMKCTKY
.   Routine   :   HCSCKA00 Catalog             
.   Return    :           
.-------------------------------------------------------------------------------
HCSCKA00  MOVE      CKEYSTCK,SAVSTCK
          MOVE      CKEYSUSP,SAVSUSP
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      CSMCKYA1,"csmckyaf"
          TRAPCLR   IO
          IF        OVRCD=1
            BEEP
            MOVE      "ERROR: Keyword Search Table Missing (csmckyaf)- ",DISPMSG
            CALL      MESSAGE1
            GOTO      HCSCKAZZ
          ENDIF
.
          OPEN      CSMCKYA2,"csmckyaf"
          MOVE      ZERO,CSCRSIZ
.
          MOVE      "08",HTOP
          MOVE      "24",HBOT
          MOVE      "01",HLEF
          MOVE      "80",HRIG
.
HCSCKA04  CALL      GCSCK000
          BRANCH    EXIT,HCSCKA99
          MOVE      STDCOL00,STDCOL01
          ADD       " 5",STDCOL01
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON,"Catalog"            
.
          MOVE      "All Catalogs",KEY12
          MATCH     "N",CKEYSTCK
          IF        @EQUAL
            MOVE      "Non-Stock   ",KEY12
          ENDIF
          MATCH     "S",CKEYSTCK
          IF        @EQUAL
            MOVE      "Stock       ",KEY12
          ENDIF
          MOVE      "Hide Suspended",KEY14
          IF        CKEYSUSP=1
            MOVE      "Show Suspended",KEY14
          ENDIF
          MOVE      STDCOL01,STDCOL02
          ADD       "8",STDCOL02
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"Stk",*HOFF,SP1:
                    *V2LON,*ULON,"Description",*HOFF:
                    SP2,*V2LON,*ULON,"Flags: ",KEY12,SP1,KEY14
          MOVE      STDCOL02,STDCOL03
          ADD       "60",STDCOL03
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
HCSCKA08  MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    STDROW+1,F2
          DISPLAY   *P21:F2,*V2LON,*ULON,"Enter Key Words for Catalog Search"
          ASSIGN    STDROW+3,F2
          KEYIN     *P10:F2,*V2LON,KEYWRD00
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ENDSET    KEYWRD00
          APPEND    SP70,KEYWRD00
          RESET     KEYWRD00
          CALL      ECSCKA00     * Break Words into Seprate Fields
.
HCSCKA09  MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          MOVE      SP70,CSCKCAT 
.
          STRIP     KEYWRD01
          PACK      KEY22,KEYWRD01,SP70
          CALL      RSCSCK1 
HCSCKA10  CALL      RKCSCK1 
          BRANCH    OVRCD,HCSCKA15
          CALL      CHREC000
          BRANCH    OVRCD,HCSCKA10,HCSCKA15
.
          ADD       ONE,XITEMNO
          ADD       ONE,STDROW
          PACK      KEY22,CSCKKWD,CSCKCAT
          STORE     KEY22,XITEMNO,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,XITEMNO,DOT;
          IF        CSIASUS<>0
            DISPLAY   *V2LON,*BLINKON,"*"
          ENDIF
          DISPLAY   *PSTDCOL01:STDROW,CSCKCAT 
          IF        CSIANON=0
            MOVE      "Yes",KEY3
          ELSE
            MOVE      "No ",KEY3
          ENDIF
          DISPLAY   *PSTDCOL02:STDROW,KEY3,SP1,CSIADES 
          COMPARE   MAXITEM,XITEMNO
          GOTO      HCSCKA10 IF NOT EQUAL
.
HCSCKA15  MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
.
HCSCKA20  CALL      SCSCK000
          BRANCH    EXIT,HCSCKA30,HCSCKA35,HCSCKA40,HCSCKA45,HCSCKA04:
                              HCSCKA91,HCSCKA08,HCSCKA50,HCSCKA70
          GOTO      HCSCKA92
.
HCSCKA30  CALL      RKCSCK1 
          BRANCH    OVRCD,HCSCKA85
          CALL      CHREC000
          BRANCH    OVRCD,HCSCKA30,HCSCKA85
.
          PACK      KEY22,CSCKKWD,CSCKCAT,SP70                                  
          CALL      RPCSCK1 
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HCSCKA10
.
HCSCKA35  MOVE      ST1,KEY22
          CALL      RDCSCK1 
.
HCSCKA36  CALL      RPCSCK1 
          BRANCH    OVRCD,HCSCKA85
          CALL      CHREC000
          BRANCH    OVRCD,HCSCKA36,HCSCKA85
          PACK      KEY22,CSCKKWD,CSCKCAT,SP70                                  
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
.
HCSCKA37  CALL      RPCSCK1 
          BRANCH    OVRCD,HCSCKA38
          CALL      CHREC000
          BRANCH    OVRCD,HCSCKA37,HCSCKA38
.
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HCSCKA37 IF NOT EQUAL
.
HCSCKA38  MOVE      ZERO,XITEMNO
          GOTO      HCSCKA10
.
HCSCKA40  MOVE      ST1,KEY22
          CALL      RDCSCK1 
HCSCKA41  CALL      RPCSCK1 
          BRANCH    OVRCD,HCSCKA85
          CALL      CHREC000
          BRANCH    OVRCD,HCSCKA41,HCSCKA85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HCSCKA09
.
HCSCKA45  CALL      RKCSCK1 
          BRANCH    OVRCD,HCSCKA85
          CALL      CHREC000
          BRANCH    OVRCD,HCSCKA45,HCSCKA85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,XITEMNO
          PACK      KEY22,KEYWRD01,Z70                                          
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
          CALL      RSCSCK1 
.
HCSCKA46  CALL      RPCSCK1 
          BRANCH    OVRCD,HCSCKA47
          CALL      CHREC000
          BRANCH    OVRCD,HCSCKA46,HCSCKA47
.
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HCSCKA46 IF NOT EQUAL
.
HCSCKA47  MOVE      ZERO,XITEMNO
          GOTO      HCSCKA10
.
HCSCKA50  MOVE      SP70,KEY67
          MOVE      SP70,CSCKCAT 
          LOAD      KEY22,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY22,CSCKKWD,CSCKCAT                                       
          CALL      VCSCK000                * view catalog
          GOTO      HCSCKA20
.
HCSCKA70  CALL      FLAGS000
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HCSCKA09
.
HCSCKA85  LOAD      KEY22,XITEMNO,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY22,CSCKKWD,CSCKCAT                                       
          CALL      RDCSCK1 
.
HCSCKA90  BEEP
          GOTO      HCSCKA20
.
HCSCKA91  MOVE      SP70,CSCKSPA 
          MOVE      SP70,KEY22
          MOVE      SP70,CSCKCAT 
          MOVE      SP70,CSIACAT 
          GOTO      HCSCKA96
.
HCSCKA92  MOVE      SP70,KEY22
          MOVE      SP70,CSCKCAT 
          LOAD      KEY22,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY22,CSCKKWD,CSCKCAT                                       
          MOVE      CSCKCAT,CSIACAT 
.
HCSCKA96  PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
.
HCSCKA99  CLOSE     CSMCKYA1
          CLOSE     CSMCKYA2
          MOVE      SAVSTCK,CKEYSTCK
          MOVE      SAVSUSP,CKEYSUSP
          GOTO      HCSCKAZZ     * End Procedure
.------------------------------------------------------------
. Alter Flags
.------------------------------------------------------------
FLAGS000  BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    MINITEM+2,F2
FLAGS100  MOVE      "A",CKEYSTCK
          KEYIN     *PSTDCOL02:F2,*V2LON,"A",*HOFF,"ll, ":
                                  *V2LON,"S",*HOFF,"tock or ":
                                  *V2LON,"N",*HOFF,"on Stock : ":
                    *V2LON,*RV,CKEYSTCK
          REP       UPPLOW,CKEYSTCK
          MATCH     "A",CKEYSTCK
          GOTO      FLAGS200 IF EQUAL
          MATCH     "S",CKEYSTCK
          GOTO      FLAGS200 IF EQUAL
          MATCH     "N",CKEYSTCK
          GOTO      FLAGS200 IF EQUAL
          BEEP 
          GOTO      FLAGS100
FLAGS200  ASSIGN    MINITEM+4,F2
FLAGS210  MOVE      "N",ANS
          KEYIN     *PSTDCOL02:F2,"Show Suspended (",*V2LON,"Y",*HOFF,"/":
                                  *V2LON,"N",*HOFF,")    : ",*V2LON,*RV,ANS
          REP       UPPLOW,ANS
          MATCH     "Y",ANS
          GOTO      FLAGS220 IF EQUAL
          MATCH     "N",ANS
          GOTO      FLAGS210 IF NOT EQUAL
          MOVE      ZERO,CKEYSUSP
          GOTO      FLAGS290
.
FLAGS220  MOVE      ONE,CKEYSUSP
.
FLAGS290  MOVE      "All Catalogs",KEY12
          MATCH     "N",CKEYSTCK
          IF        @EQUAL
            MOVE      "Non-Stock   ",KEY12
          ENDIF
          MATCH     "S",CKEYSTCK
          IF        @EQUAL
            MOVE      "Stock       ",KEY12
          ENDIF
          MOVE      "Hide Suspended",KEY14
          IF        CKEYSUSP=1
            MOVE      "Show Suspended",KEY14
          ENDIF
          DISPLAY   *PSTDCOL02:MINITEM,*V2LON,*ULON,"Stk",*HOFF,SP1:
                    *V2LON,*ULON,"Description",*HOFF:
                    SP2,*V2LON,*ULON,"Flags: ",KEY12,SP1,KEY14
          RETURN 
.------------------------------------------------------------
. Check Record
.------------------------------------------------------------
CHREC000  MATCH     KEYWRD01,CSCKKWD
          GOTO      CHREC920 IF NOT EQUAL                                       
          IF        KEYWRDNO>1
            PACK      SAVKEY22,CSCKKWD,CSCKCAT
            CALL      ECSCKB00
            BRANCH    EXIT,CHREC910
            MOVE      SAVKEY22,KEY22
            CALL      RDCSCK1
          ENDIF
          MOVE      CSCKCAT,KEY7
          CALL      RDCSIA1
          BRANCH    OVRCD,CHREC910
          COMPARE   ZERO,CSIASUS
          GOTO      CHREC100 IF EQUAL
          COMPARE   ONE,CKEYSUSP
          GOTO      CHREC910 IF NOT EQUAL
.
CHREC100  MATCH     "S",CKEYSTCK
          GOTO      CHREC200 IF NOT EQUAL
          COMPARE   ZERO,CSIANON
          GOTO      CHREC910 IF NOT EQUAL
          GOTO      CHREC900
.
CHREC200  MATCH     "N",CKEYSTCK
          GOTO      CHREC900 IF NOT EQUAL
          COMPARE   ONE,CSIANON
          GOTO      CHREC910 IF NOT EQUAL
          GOTO      CHREC900
.
CHREC900  MOVE      ZERO,OVRCD
          GOTO      CHREC999
CHREC910  MOVE      ONE,OVRCD
          GOTO      CHREC999
CHREC920  MOVE      TWO,OVRCD
CHREC999  RETURN
.------------------------------------------------------------
. Break Keywords into Words
.------------------------------------------------------------
ECSCKA00  MOVE      ZERO,KEYWRDNO
          MOVE      SP70,KEYWRD01
          MOVE      SP70,KEYWRD02
          MOVE      SP70,KEYWRD03
          MOVE      SP70,KEYWRD04
          MOVE      SP70,KEYWRD05
          MOVE      SP70,KEYWRD06
          MOVE      SP70,KEYWRD07
          MOVE      SP70,KEYWRD08
          MOVE      SP70,KEYWRD09
          MOVE      SP70,KEYWRD10
ECSCKA10  CMATCH    SP1,KEYWRD00             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWRD00
            GOTO      ECSCKA10 IF NOT EOS
            GOTO      ECSCKA99               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWRD00,SP30        * reset form pointer
          MOVE      KEY60,KEYWRD00
.
          SCAN      SP1,KEYWRD00               * Locate the first blank
          GOTO      ECSCKA20 IF NOT EQUAL
          BUMP      KEYWRD00,-1              * go back 1 to end of name
          MOVEFPTR  KEYWRD00,CCITEM          * another handly form field
          RESET     KEYWRD00                 * reset the form pointer
          SETLPTR   KEYWRD00,CCITEM          * set logical length to end of name
.
.         Save this Word
.
ECSCKA20  MOVE      KEYWRD00,KEY15           
          ADD       ONE,KEYWRDNO
          REP       UPPLOW,KEY15
          STORE     KEY15,KEYWRDNO,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                                   KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10 
          GOTO      ECSCKA19
.
.         Check for any more words
.
ECSCKA19  SETLPTR   KEYWRD00,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWRD00,CCITEM        * reset to this point
          PACK      KEY60,KEYWRD00,SP30    * reset form pointer
          MOVE      KEY60,KEYWRD00
.
          GOTO      ECSCKA10
.
ECSCKA99  RETURN
.------------------------------------------------------------
. Check for Matching Words
.------------------------------------------------------------
ECSCKB00  MOVE      CSCKCAT,KEY7
          MOVE      ONE,F2
          MOVE      ZERO,EXIT
.
ECSCKB10  COMPARE   KEYWRDNO,F2
          GOTO      ECSCKB99 IF EQUAL
          ADD       ONE,F2
          LOAD      KEY15,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY22,KEY7,KEY15
          CALL      RDCSCK2
          COMPARE   ZERO,OVRCD
          GOTO      ECSCKB10 IF EQUAL
          CALL      RKCSCK2
          BRANCH    OVRCD,ECSCKB90
          MATCH     CSCKCAT,KEY7
          GOTO      ECSCKB90 IF NOT EQUAL
          STRIP     KEY15
          MATCH     CSCKKWD,KEY15
          GOTO      ECSCKB90 IF NOT EQUAL
          GOTO      ECSCKB10
.
ECSCKB90  MOVE      ONE,EXIT
ECSCKB99  RETURN
.
.------------------------------------------------------------
.
.----- Routines required for "V"iew Screen -----
.
.------------------------------------------------------------
.
. Routine   : VCSCK000
.
. Function  : View Catalog Details
.
.------------------------------------------------------------
VCSCK000  MOVE      HTOP,SHTOP         * restore screen vars
          MOVE      HLEF,SHLEF
          MOVE      HRIG,SHRIG
          MOVE      HBOT,SHBOT
          OPEN      CSMCREA1,"csmcreaf"
          OPEN      CSMCICA1,"csmcicaf"
.
.---- display sub screen ----
.
          CALL      GETSCR00
          BRANCH    EXIT,VCSCK999
.
          MOVE      HTOP,XTOP
          MOVE      HLEF,XLEF
          MOVE      HBOT,XBOT
          MOVE      HRIG,XRIG
.
          ADD       ONE,XTOP       * Cxxx Clip Box Size
          ADD       ONE,XLEF
          SUB       THREE,XBOT
          SUB       ONE,XRIG
.
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          ADD       ONE,XBOT
          DISPLAY   *PHRIG:XBOT,*G34,*PHLEF:XBOT,*G33;
          HLINE     *G37,XBOT,XLEF,XRIG
          SUB       ONE,XBOT
.
          PACK      KEY7,CSCKCAT,SP70
          CALL      RDCSIA1                 * read catalog
          BRANCH    OVRCD,VCSCK800
.
          MOVE      XLEF,F2
          ADD       "35",F2
          MOVE      XTOP,SECTOR
.
          IF        CSIANON=0
            MOVE      "Stock     ",KEY10
          ELSE
            MOVE      "Non-Stock ",KEY10
          ENDIF
          DISPLAY   *PXLEF:SECTOR," Catalog      : ",*V2LON,CSIACAT,*HOFF:
                    *PF2:SECTOR," Catalog Type   : ",*V2LON,KEY10,*HOFF
.
          ADD       ONE,SECTOR
          DISPLAY   *PXLEF:SECTOR," Description  : ",*V2LON,CSIADES
.
          ADD       ONE,SECTOR
          IF        CSIANON=0
            MOVE      "Stock     ",KEY10
          ELSE
            MOVE      "Non-Stock ",KEY10
          ENDIF
          DISPLAY   *PXLEF:SECTOR," Issue Unit   : ",*V2LON,CSIAISS,*HOFF:
                    *PF2:SECTOR," Product Group  : ",*V2LON,CSIASUB
.
          ADD       ONE,SECTOR
          MOVE      CSIAPSC,KEY5
          CALL      RDCSCR1
          IF        OVRCD=1
            CLEAR     CSCRCN1
            APPEND    "Code Invalid ",CSCRCN1
            APPEND    KEY5,CSCRCN1
            RESET     CSCRCN1
          ENDIF
          MOVE      CSCRCN1,KEY24
          DISPLAY   *PXLEF:SECTOR," NATO Number  : ",*V2LON,CSIANAT,*HOFF:
                    *PF2:SECTOR," Pref Supplier  : ",*V2LON,KEY24
.
          ADD       ONE,SECTOR
          DISPLAY   *PXLEF:SECTOR," Average Cost : ",*V2LON,CSIAAVC,*HOFF:
                    *PF2:SECTOR," Stock Catagory : ",*V2LON,CSIASTI
          MOVE      SECTOR,FIRSTSEC
.
VCSCK090  ADD       ONE,SECTOR
          DISPLAY   *PXLEF:SECTOR,SP1:
                    *V2LON,*ULON,"Supplier                ",*HOFF,SP1:
                    *V2LON,*ULON,"Unit      ",*HOFF,SP1:
                    *V2LON,*ULON,"Part Number     ",*HOFF,SP1:
                    *V2LON,*ULON,"Lead Time",*HOFF,SP1:
                    *V2LON,*ULON,"   Curr Cost",*HOFF,SP1
.         
          PACK      KEY27,CSIACAT,SP70
          CALL      RSCSIC1
VCSCK100  CALL      RKCSIC1
          BRANCH    OVRCD,VCSCK800
          MATCH     CSICCAT,CSIACAT
          GOTO      VCSCK800 IF NOT EQUAL
.
          ADD       ONE,SECTOR
          IF        SECTOR>XBOT
            GOTO      VCSCK800 
          ENDIF
          MOVE      CSICSUP,KEY5
          CALL      RDCSCR1
          IF        OVRCD=1
            CLEAR     CSCRCN1
            APPEND    "Code Invalid ",CSCRCN1
            APPEND    KEY5,CSCRCN1
            RESET     CSCRCN1
          ENDIF
.
          MOVE      CSCRCN1,KEY24
          MOVE      CSICSUT,KEY10
          MOVE      CSICPNO,KEY19
          MOVE      CSICPD1,KEY30
          MOVE      "9999999.9999",KEY12
          FEDIT     CSICCCT,KEY12
          MATCH     CSIAPSC,CSICSUP
          IF        @EQUAL
            DISPLAY   *PXLEF:SECTOR,*V2LON,SP1,KEY24,SP1,KEY10,SP1,KEY19,SP1:
                                    CSICLTM,SP1,KEY12
          ELSE
            DISPLAY   *PXLEF:SECTOR,SP1,KEY24,SP1,KEY10,SP1,KEY19,SP1:
                                    CSICLTM,SP1,KEY12
          ENDIF
          GOTO      VCSCK100
.
VCSCK800  ADD       TWO,XBOT
          DISPLAY   *PXLEF:XBOT,SP1,*V2LON,"D",*HOFF,"own, ":
                                  *V2LON,"H",*HOFF,"ome, e":
                                  *V2LON,"X",*HOFF,"it      "
          BRANCH    CSIASUS,VCSCK801,VCSCK802,VCSCK803
          GOTO      VCSCK804
VCSCK801  DISPLAY   *P50:XBOT,*V2LON,*BLINKON,"Suspended for Requisitions"
          GOTO      VCSCK804
VCSCK802  DISPLAY   *P50:XBOT,*V2LON,*BLINKON,"Suspended for Purchase Orders"
          GOTO      VCSCK804
VCSCK803  DISPLAY   *P50:XBOT,*V2LON,*BLINKON,"Suspended"
.
VCSCK804  SUB       TWO,XBOT
VCSCK809  KEYIN     *NOEDIT,*+,*EOFF,ANS
          GOTO      VCSCK810 IF PAGEDOWN
          GOTO      VCSCK820 IF HOME
          REP       UPPLOW,ANS
          MATCH     "X",ANS
          GOTO      VCSCK900 IF EQUAL
          MATCH     "D",ANS
          GOTO      VCSCK810 IF EQUAL
          MATCH     "H",ANS
          GOTO      VCSCK820 IF EQUAL
          BEEP
          GOTO      VCSCK800
.
VCSCK810  CALL      CLRLIN00
          MOVE      FIRSTSEC,SECTOR
          ADD       ONE,SECTOR
          GOTO      VCSCK100
.
VCSCK820  CALL      CLRLIN00
          MOVE      FIRSTSEC,SECTOR
          GOTO      VCSCK090
.
.---- restore help screen ----
.
VCSCK900  PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
.
          MOVE      SHTOP,HTOP         * restore screen vars
          MOVE      SHLEF,HLEF
          MOVE      SHRIG,HRIG
          MOVE      SHBOT,HBOT
.
          CLOSE     CSMCREA1
          CLOSE     CSMCICA1
.
VCSCK999  RETURN
.------------------------------------------------------------
. Clear Supplier Details 
.------------------------------------------------------------
CLRLIN00  MOVE      FIRSTSEC,SECTOR
          ADD       ONE,SECTOR
CLRLIN10  ADD       ONE,SECTOR
          IF        SECTOR>XBOT
            GOTO      CLRLIN99 
          ENDIF
          DISPLAY   *PXLEF:SECTOR,SP70,SP8
          GOTO      CLRLIN10
CLRLIN99  RETURN
.------------------------------------------------------------
. Routine   : GCSCK000
.
. Function  : Initialisation for Help Screen Display.
.              - Save Screen
.              - Display Box Details
.
. Called by : All Computer Generated Help Screens
.------------------------------------------------------------
GCSCK000  
          MOVE      ZERO,EXIT
          MOVE      ONE,CSEARCH
.
          CALL      GETSCR00
          BRANCH    EXIT,GCSCK999
.
          MOVE      HTOP,CTOP
          MOVE      HLEF,CLEF
          MOVE      HBOT,CBOT
          MOVE      HRIG,CRIG
.
          ADD       TWO,CTOP       * Cxxx Clip Box Size
          ADD       ONE,CLEF
          SUB       THREE,CBOT
          SUB       ONE,CRIG
          MOVE      HBOT,MAXITEM
          SUB       HTOP,MAXITEM
          SUB       "4",MAXITEM
.
          SUSPEND
.
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          ADD       ONE,CBOT
          DISPLAY   *PHRIG:CBOT,*G34,*PHLEF:CBOT,*G33;
          HLINE     *G37,CBOT,CLEF,CRIG
          ADD       ONE,CBOT
.
          CLIP      1,1,CRIG,24    * Start Inside of Box Displays
.
          DISPLAY   *PCLEF:CBOT,"e",*V2LON,"X",*HOFF,"it, ex":
                                    *V2LON,"P",*HOFF,"and, ":
                                    *V2LON,"U",*HOFF,"p, ":
                                    *V2LON,"D",*HOFF,"own, ":
                                    *V2LON,"S",*HOFF,"earch, ":
                                    *V2LON,"H",*HOFF,"ome, ":
                                    *V2LON,"E",*HOFF,"nd, ":
                                    *V2LON,"R",*HOFF,"educe, ":
                                    *V2LON,"V",*HOFF,"iew, ":
                                    *V2LON,"F",*HOFF,"lags ";
.exit, expand, up, down, search, home, end, reduce, view, Flags
          SUB       TWO,CBOT
.
          MOVE      HTOP,MINITEM
          ADD       ONE,MINITEM
          MOVE      MINITEM,STDROW
.
          MOVE      HLEF,STDCOL00
          ADD       ONE,STDCOL00
          DISPLAY   *PSTDCOL00:STDROW,*V2LON,*ULON,"Item"
.
GCSCK999  RETURN
.
.------------------------------------------------------------
. Function    : Select Option Function for Help Screens
.
. Called by   : All Computer Generated Help Screens
.
. Exit Values : 0 = Item Selected      
.               1 = Page Up
.               2 = Page Down            
.               3 = Home
.               4 = End      
.               5 = Display Screen from start
.               6 = Exit
.               7 = Search
.               8 = View
.------------------------------------------------------------
SCSCK000  MOVE      ZERO,CCITEM
          MOVE      SP2,CCITEMD
.
          KEYIN     *PCLEF:CBOT,*NOEDIT,*+,*EOFF,ANS
          GOTO      SCSCK300 IF PAGEDOWN
          GOTO      SCSCK350 IF PAGEUP
          GOTO      SCSCK400 IF HOME
          GOTO      SCSCK450 IF END
          GOTO      SCSCK500 IF RIGHT
          GOTO      SCSCK550 IF LEFT 
          GOTO      SCSCK600 IF UP
          GOTO      SCSCK650 IF DOWN
          RESET     ANS
          GOTO      SCSCK250 IF EOS
          TYPE      ANS
          GOTO      SCSCK220 IF NOT EQUAL
          MOVE      ANS,CCITEM
.
          MOVE      XITEMNO,F3
          DIV       TEN,F3
          COMPARE   CCITEM,F3          * Less than ten items on screen 
          GOTO      SCSCK210 IF LESS   * so no need to enter second character
.
          KEYIN     *PCLEF:CBOT,*NOEDIT,*+,*EOFF,ANS
          RESET     ANS
          GOTO      SCSCK210 IF EOS
          MULT      TEN,CCITEM
          MOVE      ANS,F1
          ADD       F1,CCITEM
.
SCSCK210  
          COMPARE   ONE,CCITEM
          GOTO      SCSCK900 IF LESS       * Invalid Entry
          COMPARE   CCITEM,XITEMNO
          GOTO      SCSCK900 IF LESS       * Invalid Entry
          MOVE      ZERO,EXIT
          GOTO      SCSCK999               * Item Selected
.
SCSCK220 
          REP       UPPLOW,ANS
          MATCH     SP70,ANS           * Next Item
          GOTO      SCSCK650 IF EQUAL  
          MATCH     "X",ANS             * Exit
          GOTO      SCSCK910 IF EQUAL
          MATCH     "D",ANS             * Page Down
          GOTO      SCSCK300 IF EQUAL
          MATCH     "U",ANS             * Page Up
          GOTO      SCSCK350 IF EQUAL
          MATCH     "H",ANS             * Home
          GOTO      SCSCK400 IF EQUAL
          MATCH     "E",ANS             * End
          GOTO      SCSCK450 IF EQUAL
          MATCH     "S",ANS             * Search
          GOTO      SCSCK480 IF EQUAL
          MATCH     "P",ANS             * Left  (Expand Display)
          GOTO      SCSCK500 IF EQUAL
          MATCH     "R",ANS             * Right (Reduce Display)
          GOTO      SCSCK550 IF EQUAL
          MATCH     "V",ANS             * View Catalog
          GOTO      SCSCK700 IF EQUAL
          MATCH     "F",ANS             * View Catalog
          GOTO      SCSCK750 IF EQUAL
          GOTO      SCSCK900               * Invalid Entry
.
SCSCK250  
          MOVE      DITEM,CCITEM
          COMPARE   CCITEM,XITEMNO
          GOTO      SCSCK900 IF LESS       * Invalid Entry
          MOVE      ZERO,EXIT
          GOTO      SCSCK999               * Item Selected
.         
. Page Down
.
SCSCK300  MOVE      ONE,EXIT
          GOTO      SCSCK999
.
. Page Up
.
SCSCK350  MOVE      TWO,EXIT
          GOTO      SCSCK999
.
. Home
.
SCSCK400  MOVE      THREE,EXIT
          GOTO      SCSCK999
.
. End
.
SCSCK450  MOVE      FOUR,EXIT
          GOTO      SCSCK999
.
. Search
.
SCSCK480  MOVE      SEVEN,EXIT
          GOTO      SCSCK999
.
. Expand Display
.
SCSCK500  BRANCH    CSCRSIZ,SCSCK900
          MOVE      ONE,CSCRSIZ
          SUSPEND
          CALL      PUTSCR00
          MOVE      HTOP,STOP     * Save Screen Size
          MOVE      HLEF,SLEF
          MOVE      HBOT,SBOT
          MOVE      HRIG,SRIG
          MOVE      "3",HTOP      * Full Screen Display Size
          MOVE      "1",HLEF
          MOVE      "24",HBOT
          MOVE      "80",HRIG
          MOVE      FIVE,EXIT     * Display Help Screen from Start
          GOTO      SCSCK999
.
.
. Reduce Display
.
SCSCK550  COMPARE   ZERO,CSCRSIZ
          GOTO      SCSCK900 IF EQUAL
          MOVE      ZERO,CSCRSIZ
          CALL      PUTSCR00
          MOVE      STOP,HTOP     * Restore Saved Screen Size
          MOVE      SLEF,HLEF
          MOVE      SBOT,HBOT
          MOVE      SRIG,HRIG
          MOVE      FIVE,EXIT     * Display Help Screen from Start
          GOTO      SCSCK999
.
. Up Arrow. Highlight Last Item
.
SCSCK600  DISPLAY   *PSTDCOL00:DROW,*V2LON,SP1,DITEM,DOT
          COMPARE   DITEM,ONE
          GOTO      SCSCK610 IF NOT EQUAL
          COMPARE   XITEMNO,ZERO
          GOTO      SCSCK620 IF EQUAL
          MOVE      XITEMNO,DITEM
          MOVE      CTOP,DROW
          ADD       XITEMNO,DROW  
          ADD       ONE,DITEM
.
SCSCK610  SUB       ONE,DITEM
          SUB       ONE,DROW
SCSCK620  DISPLAY   *PSTDCOL00:DROW,*HON,*V2LON,SP1,DITEM,DOT
          GOTO      SCSCK000
.
. Down Arrow. Highlight Next Item
.
SCSCK650  DISPLAY   *PSTDCOL00:DROW,*V2LON,SP1,DITEM,DOT
          COMPARE   DITEM,XITEMNO
          GOTO      SCSCK660 IF NOT EQUAL
          MOVE      ZERO,DITEM
          MOVE      CTOP,DROW
          SUB       ONE,DROW
.
SCSCK660  COMPARE   XITEMNO,ZERO
          GOTO      SCSCK670 IF EQUAL
          ADD       ONE,DITEM
          ADD       ONE,DROW
SCSCK670  DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
          GOTO      SCSCK000
.
SCSCK700  MOVE      DITEM,CCITEM
          COMPARE   CCITEM,XITEMNO
          GOTO      SCSCK900 IF LESS       * Invalid Entry
          MOVE      EIGHT,EXIT
          GOTO      SCSCK999               * Item Selected
.
SCSCK750  MOVE      NINE,EXIT
          GOTO      SCSCK999               * Item Selected
.
SCSCK900  BEEP
          GOTO      SCSCK000
.
SCSCK910  MOVE      SIX,EXIT
.
SCSCK999  RETURN
.

          INC       CSMCKYIO/INC
          INC       CSMCICIO/INC
          INC       CSMCREIO/INC
          INC       IBAOVRIO/INC
.
HCSCKAZZ  ENDPROC   
.
