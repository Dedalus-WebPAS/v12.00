. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS77                                             *
. * Desc      :   Batch Update                                         *
. **********************************************************************
. * Date      :   27.02.1991                                           *
. * Author    :   B.G.Ackland                                          *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
. * VF.09.B01 14.11.2000 Sandra Barcham                                *
. *           Added BAS Code                                           *
. **********************************************************************
. * VF.00.03  05.01.2000 Charles Handaya                               *
. *           Recompiled for APSMASIO change                           *
. *           12.11.1999 Sandra Barcham                                *
. *           Delete direct debits from apscilaf as well as cheques    *
. *           srf 635048                                               *
. * VF.00.02  19.08.1999 Sandra Barcham                                *
. *           Update port to say batch update in progress              *
. *           srf 645667                                               *
. * VF.00.01  01.07.1999 Sandra Barcham                                *
. *           Fix overprinting of total debit entries                  *
. *           srf 632424                                               *
. **********************************************************************
.
          INC       FMSSTDDF
          INC       FMSUPDDF           * standard update include
          INC       FMSUDADF           * FMS audit update variables
.
          INC       APSCILFD/INC
          INC       APSMASFD/INC
          INC       APSNSCFD/INC
          INC       FMSBASFD/INC
          INC       FMSBCFFD/INC
          INC       FMSBNKFD/INC
          INC       FMSBPFFD/INC
          INC       FMSCAFFD/INC
          INC       FMSCSAFD/INC
          INC       FMSCTRFD/INC
          INC       FMSSCZFD/INC
          INC       FMSSIBFD/INC
          INC       FMSSTRFD/INC
          INC       FMSTRNFD/INC
          INC       FMSDSFFD/INC
.
          INC       IBABATFD/INC
          INC       IBACONFD/INC
          INC       IBAPASFD/INC
.
.$$$$$$
.**********************************************************************
.*                   Data variables required                          *
.**********************************************************************
.
ACCPER    FORM      2     * Accrual Period
ACCYEAR   DIM       4     * Accrual Financial Year
CASPER    FORM      2     * Cash Period
CASYEAR   DIM       4     * Cash Financial Year
D15       DIM       15
D30       DIM       30
LLPOS     FORM      2
LASTDRCR  DIM       2
LASTNAME  DIM       40
.
BASFLAG   FORM      1
BATNAME   DIM       8
BATDATE   DIM       8
.
STATBAT   DIM       5
STATDESC  DIM       30
MENUOPT11  FORM      1
PRTDESC   DIM       7
POSTDATE  DIM       10
BATDESC   DIM       18
BSTATUS   FORM      1
BATCHNO   FORM      5
DIM5A     DIM       5
ENTRDATE  DIM       10
ENDPDATE  DIM       10
.
BCH       INIT      "bch"
FMSA      INIT      "fmsa"
FMSD      INIT      "fmsd"
FMSG      INIT      "fmsg"
FMSR      INIT      "fmsr"
FMST      INIT      "fmst"
FMSZ      INIT      "fmsz"
SIB       INIT      "sib"
SIBPRE    DIM       1
FILENAME  DIM       8
SIBFILE   DIM       8
SIBTOTAL  FORM      12.6
F15       FORM      15
REPUNIQ   INIT      "yzxywxvwuvtustrsqrpqopnomnlmkljkijhighfgefdecdbcabZa":
                    "YZXYWXVWUVTUSTRSQRPQOPNOMNLMKLJKIJHIGHFGEFDECDBCAB9A":
                    "8978675645342312 1"
.
RESTART   FORM     1
PAMOUNT   FORM     12.2  * Period Amount
DAMOUNT   FORM     12.2  * Dissection Amount
TRUNIQ    FORM     5
.
. Working Variables for Print Routine
.-------------------------------------
DEBCRED   DIM       2
FORMAT2   INIT      "(999,999,999,999.99 "
FORMAT3   INIT      "(99,999,999,999,999.99 "
OUTFORM   DIM       20
OUTFORM1  DIM       20
OUTFORM2  DIM       20
OUTFORM3  DIM       23
RECCOUNT  FORM      2
TOTDEBS   FORM      14.2
TOTCREDS  FORM      14.2 
Z5        INIT      "~~~~~"
.
BANKNAME  DIM       35
CREDNAME  DIM       35
DEBTNAME  DIM       35
DISCNAME  DIM       35
PAYMNAME  DIM       35
AGSTNAME  DIM       35
CGSTNAME  DIM       35
.
PRGID     INIT      "IBAFMS77"
PRGNAM    INIT      "Batch Update"           
VERSION   INIT      "V12.00.00"
.
.**********************************************************************
.*                           ML0000                                   *
.*                    Mainline  Code                                  *
.**********************************************************************
ML0000    CALL      INIT0000             * initialisation
          BRANCH    EXIT,ML9999
.
          CALL      CRES0000
.
ML0500    CALL      MENU0000             * perform the menu
.
          BRANCH    MENUOPT1,ML1000,ML2000
          GOTO      ML9000
.
.-----  Selected batch -----
.
ML1000    CALL      DSBS0000             * display the screen
          MOVE      "4",CVERT
          MOVE      "25",CCOL
          CALL      KBCFFM00             * keyin the batch number
          BRANCH    EXIT,ML0500,ML0500
.
          CALL      FORM0000
          CALL      DSBD0000
          BRANCH    EXIT,ML1000          * can batch be updated ?
.
          CALL      DSEX0000
          BRANCH    EXIT,ML1000          * do we want to update batch ?
.
          CALL      IBAT0000     * Initialise Batch File and Summary Files
          BRANCH    EXIT,ML0500
.
          MATCH     "2",FMBCSTA
          GOTO      ML1100 IF NOT EQUAL
.         
          CALL      PBAT0000     * Print Batch
          BRANCH    EXIT,ML1000
.
ML1100    CALL      UBAT0000     * Update Batch
          GOTO      ML0500
.
.-----   All Batches   ------
.
ML2000    PACK      KEY6,TWO,SP5
          CALL      RSFMBC2
.
ML2100    CALL      RKFMBC2
          BRANCH    OVRCD,ML2900
          MATCH     "4",FMBCSTA
          GOTO      ML2900 IF EQUAL
.
          CALL      FORM0000     * format batch
          CALL      IBAT0000     * Initialise Batch File and Summary Files
          BRANCH    EXIT,ML0500
.
          MATCH     "2",FMBCSTA
          GOTO      ML2200 IF NOT EQUAL
.         
          CALL      PBAT0000     * Print Batch
          BRANCH    EXIT,ML2100
.
ML2200    CALL      UBAT0000     * Update Batch
          GOTO      ML2100
.
ML2900    DISPLAY   *P1:24,*EL,"Update Completed - ";
          CALL      HITENTER
.
ML9000    MOVE      PORT,PORTCODE
          CALL      CTUP0000           * Update all changed accounts
.
          OPEN      CONTROLF,"controlf"
          WRITAB    CONTROLF,FIFTY;*248,SP2
          CLOSE     CONTROLF
.
ML9999    CHAIN     PGM
.*********************************************************************
. Check for any restarted batches
.*********************************************************************
CRES0000  PACK      KEY6,FIVE,SP5
          CALL      RSFMBC2
.
CRES0100  CALL      RKFMBC2
          BRANCH    OVRCD,CRES9999
.
          MATCH     "5",FMBCSTA
          GOTO      CRES9999 IF NOT EQUAL
.
          DISPLAY   *P1:3,*EF,*P20:5,*V2LON,"**** Batch Update Restarting ****"
.
          CALL      IBAT0000     * Initialise Batch File and Summary Files
          BRANCH    EXIT,CRES9999
          CALL      UBAT0000     * Update Batch
          GOTO      CRES0100
.
CRES9999  RETURN
.
.*********************************************************************
.*                          INIT0000                                 *
.*                     initialisation                                *
.*********************************************************************
.
INIT0000  CALL      DISPHEAD                     * display screen
          CALL      OUPD0000                     * open files for update procs
.
          DISPLAY   *P64:24,"fmsbpfaf"
          OPEN      FMSBPFA1,"fmsbpfaf"
.
          DISPLAY   *P64:24,"fmsbnkaf"
          OPEN      FMSBNKA1,"fmsbnkaf"
          OPEN      FMSBNKA2,"fmsbnkaf"
.
          DISPLAY   *P64:24,"fmscafaf"
          OPEN      FMSCAFA1,"fmscafaf"
.
          DISPLAY   *P64:24,"apsnscaf"
          OPEN      APSNSCA1,"apsnscaf"
.
          DISPLAY   *P64:24,"apsmasaf"
          OPEN      APSMASA1,"apsmasaf"
.
          DISPLAY   *P64:24,"apscilaf"
          OPEN      APSCILA1,"apscilaf"
.
          DISPLAY   *P64:24,"fmsbcfaf"
          OPEN      FMSBCFA1,"fmsbcfaf"
          OPEN      FMSBCFA2,"fmsbcfaf"
.
          DISPLAY   *P64:24,"ihapassf"
          OPEN      IHAPASS1,"ihapassf"
          OPEN      IHAPASS2,"ihapassf"
.
          MOVE      SP70,UTYPE
          MOVE      ZERO,CPAGENO         * reset page no.           
          CLOCK     TIME,CTIMEIS         * get current time        
          MOVE      ZERO,EXIT
.
          READ      CONTROLF,ZERO;*107,IBCNUBAS,IBCNBSFY
          CALL      RDFMCO50
.
          MATCH     PORT,FMCOBUPD
          GOTO      INIT9999 IF EQUAL
.
          MATCH     SP2,FMCOBUPD
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Batch Update Already Running on Port ":
                      *V2LON,FMCOBUPD,*HOFF," Try Again Later - ";
            CALL      HITENTER
            MOVE      ONE,EXIT
            GOTO      INIT9999
          ENDIF
.
          WRITAB     CONTROLF,FIFTY;*248,PORT
.
INIT9999  RETURN
.
.**********************************************************************
.*                         MENU0000                                   *
.*                     Display the screen                             *
.**********************************************************************
.
MENU0000  DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,"0",*HOFF," = Exit":
                    *P2:5,*V2LON,"1",*HOFF," = Selected Batch":
                    *P2:6,*V2LON,"2",*HOFF," = All Batches":
                    *P1:8,"Selection : ";
.
MENU1000
          MOVE      ZERO,MENUOPT1
          KEYIN     *P13:8,*DV,UNDLN1:
                    *P13:8,*V2LON,MENUOPT1:
                    *P13:8,*DV,MENUOPT1
.
          COMPARE   ZERO,MENUOPT1         * test entry not less than 0
          GOTO      MENU1200 IF LESS
.
          COMPARE   THREE,MENUOPT1         * test entry less than 3
          GOTO      MENU1500 IF LESS
.
MENU1200
          BEEP
          GOTO      MENU1000
.
MENU1500
          BRANCH    MENUOPT1,MENU2000,MENU3000
          GOTO      MENU9999
.
.-----   Display the option heading -----
.
MENU2000  DISPLAY   *P60:3,*V2LON,"Selected Batch"                  
          GOTO      MENU9999
.
MENU3000  DISPLAY   *P60:3,*V2LON,"All Batches"
          GOTO      MENU9999
.
MENU9999  RETURN
.*********************************************************************
.*                         FORM0000                                  *
.*                  format the batch data                            *
.*********************************************************************
.
FORM0000  MOVE      FMBCSTA,FORM1
          BRANCH    FORM1,FORM0100,FORM0200,FORM0300,FORM0400
.
          MOVE      "Batch in Use",STATDESC
          GOTO      FORM10000
.
FORM0100  MOVE      "Batch Awaiting Mods",STATDESC
          GOTO      FORM10000
.
FORM0200  MOVE      "Batch Awaiting Printing",STATDESC
          GOTO      FORM10000
.
FORM0300  MOVE      "Batch Awaiting Update",STATDESC
          GOTO      FORM10000
.
FORM0400  MOVE      "Batch Processed",STATDESC
.
FORM1000  MATCH      "CE",FMBCTRT
          GOTO       FORM1100 IF NOT EQUAL
          MOVE       "Committed Expend. ",BATDESC
          GOTO        FORM2000
.
FORM1100  MATCH      "CR",FMBCTRT
          GOTO       FORM1200 IF NOT EQUAL
          MOVE       "Credit Notes      ",BATDESC
          GOTO        FORM2000
.
FORM1200  MATCH      "CS",FMBCTRT
          GOTO       FORM1300 IF NOT EQUAL
          MOVE       "Cash Receipts     ",BATDESC
          GOTO        FORM2000
.
FORM1300  MATCH      "IN",FMBCTRT
          GOTO       FORM1400 IF NOT EQUAL
          MOVE       "Invoices          ",BATDESC
          GOTO        FORM2000
.
FORM1400  MATCH      "JA",FMBCTRT
          GOTO       FORM1450 IF NOT EQUAL
          MOVE       "Journals          ",BATDESC
          GOTO        FORM2000
.
FORM1450  MATCH      "JC",FMBCTRT
          GOTO       FORM1500 IF NOT EQUAL
          MOVE       "Journals          ",BATDESC
          GOTO        FORM2000
.
FORM1500  MATCH      "PY",FMBCTRT
          GOTO       FORM1600 IF NOT EQUAL
          MOVE       "Payments          ",BATDESC
          GOTO        FORM2000
.
FORM1600  MATCH      "RF",FMBCTRT
          GOTO       FORM1700 IF NOT EQUAL
          MOVE       "Refunds           ",BATDESC
          GOTO        FORM2000
.
FORM1700  MATCH      "SC",FMBCTRT
          GOTO       FORM1800 IF NOT EQUAL
          MOVE       "Supply Credits    ",BATDESC
          GOTO        FORM2000
.
FORM1800  MATCH      "SI",FMBCTRT
          GOTO       FORM1900 IF NOT EQUAL
          MOVE       "Supply Invoices   ",BATDESC
          GOTO        FORM2000
.
FORM1900  MATCH      "CJ",FMBCTRT
          GOTO       FORM1940 IF NOT EQUAL
          MOVE       "Cash Journals     ",BATDESC
          GOTO        FORM2000
.
FORM1940  MATCH      "RI",FMBCTRT
          GOTO       FORM1950 IF NOT EQUAL
          MOVE       "Reinstate Invoices",BATDESC
          GOTO       FORM2000
.
FORM1950  MATCH      "CC",FMBCTRT
          GOTO       FORM1960 IF NOT EQUAL
          MOVE       "Cancelled Payments",BATDESC
          GOTO       FORM2000
.
FORM1960  MATCH      "PP",FMBCTRT
          GOTO       FORM2000 IF NOT EQUAL
          MOVE       "Prompt Payments   ",BATDESC
          GOTO       FORM2000
.
FORM2000  CLEAR      FMLALEDG
          PACK       KEY2,FMBCLED
          CALL       RDFMLA1
.
          REP        " 0",FMBCPDAT
          UNPACK     FMBCPDAT,CCENT,CYEAR,CMON,CDAY
          PACK       POSTDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          UNPACK     SP70,CCENT,CYEAR,CMON,CDAY
          REP        " 0",FMBCDAT
          UNPACK     FMBCDAT,CCENT,CYEAR,CMON,CDAY
          PACK       ENTRDATE,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          CLEAR      SECUSER
          PACK       KEY4,FMBCUID
          CALL       RDPASS1
.
FORM9999  RETURN
.**********************************************************************
.*                        DSBS0000                                    *
.*                 display single batch screen                        *
.**********************************************************************
.
DSBS0000  DISPLAY   *P1:4,*EF:
                    *P5:4,"Batch No          : ":
                    *P5:6,"Status            : ":
                    *P5:7,"Ledger Number     : ":
                    *P5:8,"Transaction Type  : ":
                    *P5:9,"Number of Entries : ":
                    *P5:10,"Batch Total       : ":
                    *P5:11,"Operator ID       : ":
                    *P5:12,"Date Entered      : ": 
                    *P5:13,"Post to Date      : "
DSBS9999  RETURN
.
.-----  Display the batch data -----
.
DSBD0000  MOVE      FMBCSEC,F5
          SUB       "2",F5
          MOVE      FMBCFMT,FMBCFTOT
          FEDIT     FMBCTOT,FMBCFTOT
          DISPLAY   *P25:4,*V2LON,FMBCBAT:
                    *P25:6,FMBCSTA,*HOFF,*P32:6,STATDESC:
                    *P25:7,*V2LON,FMBCLED,*HOFF,*P32:7,FMLADESC:
                    *P25:8,*V2LON,FMBCTRT,*HOFF,*P32:8,BATDESC:
                    *P25:9,*V2LON,F5:
                    *P25:10,FMBCFTOT:
                    *P25:11,SECUSER:
                    *P25:12,*V2LON,ENTRDATE:
                    *P25:13,POSTDATE
.
          MOVE      ZERO,EXIT
          MATCH     "4",FMBCSTA
          GOTO      DSBD9999 IF NOT EQUAL
          MOVE      ONE,EXIT
.
          DISPLAY   *P1:24,*B,*EF,"Batch Already Processed - ";
          CALL      HITENTER
.
DSBD9999  RETURN
.
.*********************************************************************
.*                         DSEX0000                                  *
.*                     display or exit                               *
.*********************************************************************
.
DSEX0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL:
                    "(",*V2LON,"U",*HOFF,")pdate Batch, ":
                    "e(",*V2LON,"X",*HOFF,")it : "                  
.
DSEX1000  KEYIN     *P26:24,*DV,UNDLN1:
                    *P26:24,*V2LON,ANS:
                    *P26:24,*DV,ANS
          REP       UPPLOW,ANS
.
          MATCH     ANSX,ANS             * test for exit
          GOTO      DSEX9000 IF EQUAL
.
          MATCH     ANSU,ANS             * test for display
          GOTO      DSEX9999 IF EQUAL
.
          BEEP 
          GOTO      DSEX1000
.
DSEX9000  MOVE     ONE,EXIT
DSEX9999  RETURN
.
RLMCFM00  RETURN
.
RBCFFM00  CALL      DSBS0000             * display the screen
          RETURN
.
.**********************************************************************
. Includes
.**********************************************************************
          INC       APSCILIO/INC
          INC       APSMASIO/INC
          INC       APSNSCIO/INC
          INC       FMSBASIO/INC
          INC       FMSBCFIO/INC
          INC       FMSBNKIO/INC
          INC       FMSBPFIO/INC
          INC       FMSCAFIO/INC
          INC       FMSCSAIO/INC
          INC       FMSCTRIO/INC
          INC       FMSSCZIO/INC
          INC       FMSSIBIO/INC
          INC       FMSSTRIO/INC
          INC       FMSTRNIO/INC
          INC       FMSDSFIO/INC
.
          INC       IBABATIO/INC 
          INC       IBAPASIO/INC
.
          INC       FMSLMCKY
          INC       FMSBCFKY
.
          INC       PRCFMSUB           * Initialise, Print, Update Batch 
          INC       FMSUPDCD           * standard update routines
          INC       FMSSTDCD
          INC       FMSUDACD           * FMS audit update routines 
