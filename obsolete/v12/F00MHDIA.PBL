.*****************************************************************************
.*    Program      : F00MHDIA                                                *
.*    Description  : Conversion mehdiaaf to new File layout                  *
.*                                                                           *
.*    Author       : Mike Laming                                             *
.*    Date         : 29/03/2010                                              *
.*    Modifications:                                                         *
.*      V10.00.01  26/03/2010  Mike Laming    CAR 217575                     *
.*                 Program created.                                          *
.*****************************************************************************
.
.  Global change F00MHDIA  eg F57PTDOC
.  Global change MEHDIAFD  eg PATDOCFD (new layout)
.  Global change mehdiaaf  eg patdoctf
.  Global change MEHDIAA1  eg PATDOCT1
.  Global change KEY16   eg KEY8
.  Global change KEY16  eg KEY9
.  Global change WRMHDIA1  eg WRDOCT1
.  Global change smhdiaaf  eg sptdoctf
.
.  vi Global change command  :%s/FromString/ToString/g
.
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
.
. Enter the first or the shortest index from the original FD and rename
. the index OLDFILE1
..OLDFILE1  IFILE     SQL, FIXED=xxx, KEY="Uxx-xx"
.
.                                     ******** copy the old FD here *******
.                                     * comment out all continuing fields and
.                                     * prefix changing fields with "OLD" -
.                                     * remove old Indexes and Redefines     
.Data File Definition
.--------------------
OLDFILE1  IFILE     SQL, FIXED=1047, KEY="U1-8,9-16"
.
. Name    Type      Length  Phys.  Start  Description
. ----    ----      ------  -----  -----  -----------
.DMHDIADM  DIM       8           8      1  Admission Number
.MHDIDATE  DIM       8           8      9  Date of diagnosis change ccyymmdd
.MHDIICD1  DIM       9           9     17  1st ICD9CM code
.MHDIDES1  DIM       30         30     26  1st ICD9CM description
.MHDISUP1  FORM      1           2     56  Suppress 1st ICD9 code ?
.                                           1 = Yes ; 2 = No
.MHDIDAT1  DIM       8           8     58  Date of Last change to 1st ICD9 code
.MHDIICD2  DIM       9           9     66  2nd ICD9CM code
.MHDIDES2  DIM       30         30     75  2nd ICD9CM description
.MHDISUP2  FORM      1           2    105  Suppress 2nd ICD9 code ?
.                                           1 = Yes ; 2 = No
.MHDIDAT2  DIM       8           8    107  Date of Last change to 2nd ICD9 code
.MHDIX1CA  DIM       9           9    115  Axis I Code A
.MHDIX1CB  DIM       9           9    124  Axis I Code B
.MHDIX1CC  DIM       9           9    133  Axis I Code C
.MHDIX1DA  DIM       70         70    142  Axis I description A
.MHDIX1DB  DIM       70         70    212  Axis I description B
.MHDIX1DC  DIM       70         70    282  Axis I description C
.MHDIX2CA  DIM       9           9    352  Axis II Code A
.MHDIX2CB  DIM       9           9    361  Axis II Code B
.MHDIX2CC  DIM       9           9    370  Axis II Code C
.MHDIX2DA  DIM       70         70    379  Axis II description A
.MHDIX2DB  DIM       70         70    449  Axis II description B
.MHDIX2DC  DIM       70         70    519  Axis II description C
.MHDIX3DS  DIM       50         50    589  Axis III description
.MHDIX4CD  DIM       3           3    639  Axis IV code  (Cat XA)
.MHDIX5CD  DIM       3           3    642  Axis V code   (Cat XB)
.MHDIXDAT  DIM       8           8    645  Date of last change to DSMIIIR diag.
.MHDIOPER  DIM       4           4    653  Operator id who made change
.MHDIAXIV  DIM       20         20    657  Axis IV Description
.MHDIMOHR  DIM       1           1    677  MOH Indicator Record sent to MHINC
.                                         Blank=Not for MHINC
.                                         0=Unsent
.                                         1=Sent
.                                         2=Send Delete required
.                                         3=Send Discharge required
.                                         4=Send RF and Discharge required
.                                         5=Send Delete Discharge required
.MHDIX3CA  DIM       9           9    678  Axis III Code A
.MHDIX3CB  DIM       9           9    687  Axis III Code B
.MHDIX3CC  DIM       9           9    696  Axis III Code C
.MHDIX3DA  DIM       70         70    705  Axis III description A
.MHDIX3DB  DIM       70         70    775  Axis III description B
.MHDIX3DC  DIM       70         70    845  Axis III description C
.MHDICDAT  DIM       8           8    915  Date Record Created (ccyymmdd)
.MHDICTIM  DIM       8           8    923  Time Record Created (hh:mm:ss)
.MHDICUID  DIM       10         10    931  WEB User ID Created (websecaf)
.MHDIUDAT  DIM       8           8    941  Date Record Updated (ccyymmdd)
.MHDIUTIM  DIM       8           8    949  Time Record Updated (hh:mm:ss)
.MHDIUUID  DIM       10         10    957  WEB User ID Updated (websecaf)
OLDISPAR  DIM       80         80    967  Spare variable
..End of Record                      1047
.
.
.                                     ******** copy the new FD here *******
.Data File Definition
.--------------------
MEHDIAA1  IFILE     SQL, FIXED=1249, KEY="U1-8,9-16"
.
. Name    Type      Length  Phys.  Start  Description
. ----    ----      ------  -----  -----  -----------
DMHDIADM  DIM       8           8      1  Admission Number
MHDIDATE  DIM       8           8      9  Date of diagnosis change ccyymmdd
MHDIICD1  DIM       9           9     17  1st ICD10  code
MHDIDES1  DIM       30         30     26  1st ICD10  description
MHDISUP1  FORM      1           2     56  Suppress 1st ICD9 code ?
.                                           1 = Yes ; 2 = No
MHDIDAT1  DIM       8           8     58  Date of Last change to 1st ICD9 code
MHDIICD2  DIM       9           9     66  2nd ICD10  code
MHDIDES2  DIM       30         30     75  2nd ICD10  description
MHDISUP2  FORM      1           2    105  Suppress 2nd ICD9 code ?
.                                           1 = Yes ; 2 = No
MHDIDAT2  DIM       8           8    107  Date of Last change to 2nd ICD9 code
MHDIX1CA  DIM       9           9    115  Axis I Code A
MHDIX1CB  DIM       9           9    124  Axis I Code B
MHDIX1CC  DIM       9           9    133  Axis I Code C
MHDIX1DA  DIM       70         70    142  Axis I description A
MHDIX1DB  DIM       70         70    212  Axis I description B
MHDIX1DC  DIM       70         70    282  Axis I description C
MHDIX2CA  DIM       9           9    352  Axis II Code A
MHDIX2CB  DIM       9           9    361  Axis II Code B
MHDIX2CC  DIM       9           9    370  Axis II Code C
MHDIX2DA  DIM       70         70    379  Axis II description A
MHDIX2DB  DIM       70         70    449  Axis II description B
MHDIX2DC  DIM       70         70    519  Axis II description C
MHDIX3DS  DIM       50         50    589  Axis III description
MHDIX4CD  DIM       3           3    639  Axis IV code  (Cat XA)
MHDIX5CD  DIM       3           3    642  Axis V code   (Cat XB)
MHDIXDAT  DIM       8           8    645  Date of last change to DSMIIIR diag.
MHDIOPER  DIM       4           4    653  Operator id who made change
MHDIAXIV  DIM       20         20    657  Axis IV Description
MHDIMOHR  DIM       1           1    677  MOH Indicator Record sent to MHINC
.                                         Blank=Not for MHINC
.                                         0=Unsent
.                                         1=Sent
.                                         2=Send Delete required
.                                         3=Send Discharge required
.                                         4=Send RF and Discharge required
.                                         5=Send Delete Discharge required
MHDIX3CA  DIM       9           9    678  Axis III Code A
MHDIX3CB  DIM       9           9    687  Axis III Code B
MHDIX3CC  DIM       9           9    696  Axis III Code C
MHDIX3DA  DIM       70         70    705  Axis III description A
MHDIX3DB  DIM       70         70    775  Axis III description B
MHDIX3DC  DIM       70         70    845  Axis III description C
MHDICDAT  DIM       8           8    915  Date Record Created (ccyymmdd)
MHDICTIM  DIM       8           8    923  Time Record Created (hh:mm:ss)
MHDICUID  DIM       10         10    931  WEB User ID Created (websecaf)
MHDIUDAT  DIM       8           8    941  Date Record Updated (ccyymmdd)
MHDIUTIM  DIM       8           8    949  Time Record Updated (hh:mm:ss)
MHDIUUID  DIM       10         10    957  WEB User ID Updated (websecaf)
MHDIICD3  DIM       9           9    967  3rd ICD10 Code
MHDIDES3  DIM       30         30    976  3rd ICD10 Description
MHDISUP3  FORM      1           2   1006  Suppress 3rd ICD10 Code? - 1=Yes 2=No
MHDIDAT3  DIM       8           8   1008  Date of last change to 3rd ICD10 Code
MHDIICD4  DIM       9           9   1016  4th ICD10 Code
MHDIDES4  DIM       30         30   1025  4th ICD10 Description
MHDISUP4  FORM      1           2   1055  Suppress 4th ICD10 Code? - 1=Yes 2=No
MHDIDAT4  DIM       8           8   1057  Date of last change to 4th ICD10 Code
MHDIICD5  DIM       9           9   1065  5th ICD10 Code
MHDIDES5  DIM       30         30   1074  5th ICD10 Description
MHDISUP5  FORM      1           2   1104  Suppress 5th ICD10 Code? - 1=Yes 2=No
MHDIDAT5  DIM       8           8   1106  Date of last change to 5th ICD10 Code
MHDIICD6  DIM       9           9   1114  6th ICD10 Code
MHDIDES6  DIM       30         30   1123  6th ICD10 Description
MHDISUP6  FORM      1           2   1153  Suppress 6th ICD10 Code? - 1=Yes 2=No
MHDIDAT6  DIM       8           8   1155  Date of last change to 6th ICD10 Code
MHDIP1CA  DIM       1           1   1163  Axis I  Code A is Primary - 0=no 1=yes
MHDIP1CB  DIM       1           1   1164  Axis I  Code B is Primary - 0=no 1=yes
MHDIP1CC  DIM       1           1   1165  Axis I  Code C is Primary - 0=no 1=yes
MHDIP2CA  DIM       1           1   1166  Axis II Code A is Primary - 0=no 1=yes
MHDIP2CB  DIM       1           1   1167  Axis II Code B is Primary - 0=no 1=yes
MHDIP2CC  DIM       1           1   1168  Axis II Code C is Primary - 0=no 1=yes
MHDIHCPC  DIM       10         10   1169  HCP Code
MHDICAST  DIM       10         10   1179  Case Team Code
MHDISPAR  DIM       60         60   1189  Spare variable
.End of Record                      1249
.
.Redefine form fields from key
.-----------------------------
. Name    Type      Length  Start  Description
. ----    ----      ------  -----  -----------
MHDIADMN  FORM      8           1  Admission Number
.
.
CMDLINE   DIM       100
DIM4      DIM       4
DIM4A     DIM       4
DIM6      DIM       6
DIM6A     DIM       6
DIM60     DIM       60
DDCENT    DIM       2
.
RECNUM    FORM      8
NEWFILE   DIM       60
NEWPATH   DIM       60
OLDPATH   DIM       60
DEFPATH   DIM       60
SAVEFLG   FORM      1
SP50      INIT    "                                                  "
SP60      INIT    "                                                            "
.
PRGNAM    INIT      "CONVERSION MEHDIAFD"
PRGID     INIT      "F00MHDIA"
VERSION   INIT      "V12.00.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN1000,MAIN0000,MAIN9999 * Yes, no, cancel
.
MAIN1000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
. Loop thru old file and write records to new file
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/compress saved file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          
INIT9999  RETURN
.
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
PREP0000 
. open old file
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,"mehdiaaf"
          TRAPCLR   IO
          BRANCH    OVRCD,PREP5000       * Original file does not exist
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
. Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat mehdiaaf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "smhdiaaf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat mehdiaaf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "smhdiaaf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
. remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "mehdiaaf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "smhdiaaf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "mehdiaaf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.      eg    APPEND    " 195 UC9-16",CMDLINE
.
          APPEND    " 1249 UC1-8,9-16",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has 
.  to be repeated with for each individual index
.
..         PACK      CMDLINE,SP60,SP60
..         CLEAR     CMDLINE
..         APPEND    "isadd ",CMDLINE
..         APPEND    NEWFILE,CMDLINE
..         APPEND    " UCxx-xx,x-xx",CMDLINE
..         APPEND    SP60,CMDLINE
..         RESET     CMDLINE
..         EXECUTE   CMDLINE,TASKID
.
          OPEN      MEHDIAA1,NEWFILE
.
. set the flag to say we want to continue
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
PREP9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          MOVE      ZERO,MHDISUP3
          MOVE      ZERO,MHDISUP4
          MOVE      ZERO,MHDISUP5
          MOVE      ZERO,MHDISUP6
          UNPACK    SP70,MHDIICD3,MHDIDES3,MHDIDAT3
          UNPACK    SP70,MHDIICD4,MHDIDES4,MHDIDAT4
          UNPACK    SP70,MHDIICD5,MHDIDES5,MHDIDAT5
          UNPACK    SP70,MHDIICD6,MHDIDES6,MHDIDAT6
          UNPACK    SP70,MHDIP1CA,MHDIP1CB,MHDIP1CC,MHDIP2CA,MHDIP2CB,MHDIP2CC
          UNPACK    SP70,MHDIHCPC,MHDICAST
          UNPACK    SP70,MHDISPAR
.
          PACK      KEY16,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
.  All necessary changes to the files field lengths, date changes
.  or any other sort of modification must be made here
.
.    eg    PACK      DSPARE,SP50
.          PACK      PTDSDMDC,SP10
.          PACK      PTDSDDRG,SP10
.                                               * Pack key here
          PACK      KEY16,MHDIADMN,MHDIDATE,SP70
          CALL      WRMHDIA1                    * write to new file
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     MEHDIAA1                      * close new file
          CLOSE     OLDFILE1                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
READ9999  RETURN
.
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
VALD0000  MOVE      ZERO,EXIT
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or compress save file                                *
.**********************************************************************
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "smhdiaaf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "compress -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not compressed or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.         Get the default directory
.**********************************************************************
DEFT0000  EXECUTE   "ldat mehdiaaf.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding 'mehdiaaf'.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
DEFT9999  RETURN
+
.
.                  ******************************    OLD IO ROUTINES
.                  Copy the Positional Read (RS or RDS) from original I/O file,
.                  - change Label to READSOLD and the index name to OLDFILE1
.
.READSOLD  MOVE      ZERO,OVRCD
.          RESET     KEY6
.          PACK      KEY8,SP2,KEY6
.          READ      OLDFILE1,KEY8;;
.          RETURN
READSOLD  RESET     KEY16
          READ      OLDFILE1,KEY16;;
          RETURN
.
.                  Copy the Read Next (RK or RDK) from the original I/O file
.                  - change Label to READKOLD and the index name to OLDFILE1
.                  - change any fields that are changing to a prefix of OLD,
.                    eg. PTEDSPAR becomes OLDDSPAR
.
.READKOLD  MOVE      ZERO,OVRCD
.          READKS    OLDFILE1;DDURNO,DDADMNO:
.                             DIM6,DTIME,DSTAT,DDEST:
.                             PTDSVOGU,DSPARE
.          GOTO      OVERCOND IF OVER
.          MOVE      QDURNO,DURNO
.          RETURN
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;DMHDIADM,MHDIDATE,MHDIICD1,MHDIDES1:
                                  MHDISUP1,MHDIDAT1,MHDIICD2,MHDIDES2,MHDISUP2:
                                  MHDIDAT2,MHDIX1CA,MHDIX1CB,MHDIX1CC,MHDIX1DA:
                                  MHDIX1DB,MHDIX1DC,MHDIX2CA,MHDIX2CB,MHDIX2CC:
                                  MHDIX2DA,MHDIX2DB,MHDIX2DC,MHDIX3DS,MHDIX4CD:
                                  MHDIX5CD,MHDIXDAT,MHDIOPER,MHDIAXIV,MHDIMOHR:
                                  MHDIX3CA,MHDIX3CB,MHDIX3CC,MHDIX3DA,MHDIX3DB:
                                  MHDIX3DC,MHDICDAT,MHDICTIM,MHDICUID,MHDIUDAT:
                                  MHDIUTIM,MHDIUUID,OLDISPAR
          GOTO      OVERCOND IF OVER
          MOVE      DMHDIADM,MHDIADMN
          RETURN
.
.                  ******************************    NEW IO ROUTINES
.                  Copy the Write statement from the new I/O file           
.                  (no changes are needed to this routine)               
WRMHDIA1  RESET     KEY16
          MOVE      MHDIADMN,DMHDIADM
          WRITE     MEHDIAA1,KEY16;DMHDIADM,MHDIDATE,MHDIICD1,MHDIDES1:
                                  MHDISUP1,MHDIDAT1,MHDIICD2,MHDIDES2,MHDISUP2:
                                  MHDIDAT2,MHDIX1CA,MHDIX1CB,MHDIX1CC,MHDIX1DA:
                                  MHDIX1DB,MHDIX1DC,MHDIX2CA,MHDIX2CB,MHDIX2CC:
                                  MHDIX2DA,MHDIX2DB,MHDIX2DC,MHDIX3DS,MHDIX4CD:
                                  MHDIX5CD,MHDIXDAT,MHDIOPER,MHDIAXIV,MHDIMOHR:
                                  MHDIX3CA,MHDIX3CB,MHDIX3CC,MHDIX3DA,MHDIX3DB:
                                  MHDIX3DC,MHDICDAT,MHDICTIM,MHDICUID,MHDIUDAT:
                                  MHDIUTIM,MHDIUUID,MHDIICD3,MHDIDES3,MHDISUP3:
                                  MHDIDAT3,MHDIICD4,MHDIDES4,MHDISUP4,MHDIDAT4:
                                  MHDIICD5,MHDIDES5,MHDISUP5,MHDIDAT5,MHDIICD6:
                                  MHDIDES6,MHDISUP6,MHDIDAT6,MHDIP1CA,MHDIP1CB:
                                  MHDIP1CC,MHDIP2CA,MHDIP2CB,MHDIP2CC,MHDIHCPC:
                                  MHDICAST,MHDISPAR
          RETURN
.
          INC       STD001IO
