. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS32                                             *
. * Desc      :   Default Control Accounts                             *
. **********************************************************************
. * Date      :   23.01.1991                                           *
. * Author    :   B.G.Ackland (IBA)                                    *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
.$$$$$$
.        PROGRAM IBAFMS32
.             open files and initialise variables
.             WHILE user wants to add/modify/delete DO
.                  get desired option (add, modify or delete)
.                  CASE option OF
.                       ADD     : WHILE user wants to add records DO
.                                      accept initial values
.                                      WHILE user wants modify screen data DO
.                                           get number of item to be modified
.                                           allow user to modify option
.                                      END WHILE
.                                      IF user wants to post data
.                                           THEN write new data
.                                      END IF
.                                 END WHILE
.                       MODIFY  : WHILE user wants to modify records DO
.                                      get and display desired record
.                                      WHILE user wants modify screen data DO
.                                           get number of item to be modified
.                                           allow user to modify option
.                                      END WHILE
.                                      IF user wants to post data
.                                           THEN update data
.                                      END IF
.                                 END WHILE
.                       DELETE  : WHILE user wants to delete records DO
.                                      get and display desired record
.                                      IF user wants to delete data
.                                           THEN delete data
.                                      END IF
.                                 END WHILE
.                  END CASE
.             END WHILE
.        END
.$$$$$$
. Standard FMS Accounting System Include
.---------------------------------------
          INC       FMSSTDDF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
.
          INC       FMSLMAFD/INC           * Ledger
          INC       FMSAMAFD/INC           * Account
          INC       FMSCAFFD/INC           * Control Account Masterfile
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
.
ACCEPT    FORM      1                  * accept mode (1=on)
BNK2DESC  DIM       35
BNKDESC   DIM       35
DISDESC   DIM       35
CRDDESC   DIM       35
DEBDESC   DIM       35
PAYDESC   DIM       35
PAYGDESC  DIM       35
AGSDESC   DIM       35
IFTDESC   DIM       35
.
LCCITEM   FORM      2
.
PRGID     INIT      "IBAFMS32"
PRGNAM    INIT      "Default Control Accounts"
VERSION   INIT      "V12.00.00"
.
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML0100    CALL      PROC0000           * get entry
          BRANCH    EXIT,ML9999,ML9999 * exit option
          CALL      SCRA0000           * display screen A
          CALL      SELA0000           * modify screen A
          BRANCH    EXIT,ML0100        * don't save data
          CALL      POST0000           * post screen A data
          GOTO      ML0100             * get next entry
.
ML9999    
          CHAIN     PGM
.
.******************************************************************************
.* INIT - Open Files                             Called by ML0000             *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P64:24,"fmsamaaf";
          OPEN      FMSAMAA1,"fmsamaaf"
.
          DISPLAY   *P64:24,"fmsamaaf";
          OPEN      FMSAMAA2,"fmsamaaf"
.
          DISPLAY   *P64:24,"fmsamaaf";
          OPEN      FMSAMAA3,"fmsamaaf"
.
          DISPLAY   *P64:24,"fmsamaaf";
          OPEN      FMSAMAA4,"fmsamaaf"
.
          DISPLAY   *P64:24,"fmslmaaf";
          OPEN      FMSLMAA1,"fmslmaaf"
.
          DISPLAY   *P64:24,"fmscafaf";
          OPEN      FMSCAFA1,"fmscafaf"
.
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.******************************************************************************
.* SCRA - Display Screen A                       Called by SELA0000, redisps  *
.*       Requires : ACCEPT   (1=accept mode)                                  *
.******************************************************************************
SCRA0000  DISPLAY   *P1:4,*EF,"    Ledger               : ",*V2LON,FMLALEDG:
                    *HOFF,SP10,SP1,FMLADESC:
                    *P1:6,*V2LON," 1",*HOFF,". Bank Account         : ":
                    *P1:7,*V2LON," 2",*HOFF,". Bank Account 2       : ":
                    *P1:8,*V2LON," 3",*HOFF,". Creditors Control    : ":
                    *P1:9,*V2LON," 4",*HOFF,". Discount Control     : ":
                   *P1:10,*V2LON," 5",*HOFF,". Debtors Control      : ":
                   *P1:11,*V2LON," 6",*HOFF,". Payments in Suspense : ":
                   *P1:12,*V2LON," 7",*HOFF,". G.S.T. Control       : ":
                   *P1:13,*V2LON," 8",*HOFF,". P.A.Y.G. Account     : ":
                   *P1:14,*V2LON," 9",*HOFF,". Inter Fund Transfer  : "
          MOVE      SP70,BNK2DESC
          MOVE      SP70,BNKDESC
          MOVE      SP70,CRDDESC
          MOVE      SP70,DISDESC
          MOVE      SP70,DEBDESC
          MOVE      SP70,PAYDESC
          MOVE      SP70,AGSDESC
          MOVE      SP70,PAYGDESC
          MOVE      SP70,IFTDESC
.
          PACK      KEY14,FMLALEDG,FMLABNK2
          CALL      RDFMAM1
          BRANCH    OVRCD,SCRA0900
          MOVE      FMAMDESC,BNK2DESC
.
SCRA0900  PACK      KEY14,FMLALEDG,FMLABNK
          CALL      RDFMAM1
          BRANCH    OVRCD,SCRA1000
          MOVE      FMAMDESC,BNKDESC
.
SCRA1000  PACK      KEY14,FMLALEDG,FMLACRD
          CALL      RDFMAM1
          BRANCH    OVRCD,SCRA2000
          MOVE      FMAMDESC,CRDDESC
.
SCRA2000  PACK      KEY14,FMLALEDG,FMLADIS
          CALL      RDFMAM1
          BRANCH    OVRCD,SCRA3000
          MOVE      FMAMDESC,DISDESC
.
SCRA3000  PACK      KEY14,FMLALEDG,FMLADEB
          CALL      RDFMAM1
          BRANCH    OVRCD,SCRA4000
          MOVE      FMAMDESC,DEBDESC
.
SCRA4000  PACK      KEY14,FMLALEDG,FMLAPAY
          CALL      RDFMAM1
          BRANCH    OVRCD,SCRA5000
          MOVE      FMAMDESC,PAYDESC
.
SCRA5000  PACK      KEY14,FMLALEDG,FMLAAGST
          CALL      RDFMAM1
          BRANCH    OVRCD,SCRA6000
          MOVE      FMAMDESC,AGSDESC
.
SCRA6000  PACK      KEY14,FMLALEDG,FMLAIFT
          CALL      RDFMAM1
          BRANCH    OVRCD,SCRA7000
          MOVE      FMAMDESC,IFTDESC
.
SCRA7000  PACK      KEY14,FMLALEDG,FMLAPAYG
          CALL      RDFMAM1
          BRANCH    OVRCD,SCRA8000
          MOVE      FMAMDESC,PAYGDESC
.
SCRA8000  DISPLAY   *P28:6,*V2LON,FMLABNK,*HOFF,SP1,BNKDESC:
                    *P28:7,*V2LON,FMLABNK2,*HOFF,SP1,BNK2DESC:
                    *P28:8,*V2LON,FMLACRD,*HOFF,SP1,CRDDESC:
                    *P28:9,*V2LON,FMLADIS,*HOFF,SP1,DISDESC:
                    *P28:10,*V2LON,FMLADEB,*HOFF,SP1,DEBDESC:
                    *P28:11,*V2LON,FMLAPAY,*HOFF,SP1,PAYDESC:
                    *P28:12,*V2LON,FMLAAGST,*HOFF,SP1,AGSDESC:
                    *P28:13,*V2LON,FMLAPAYG,*HOFF,SP1,PAYGDESC:
                    *P28:14,*V2LON,FMLAIFT,*HOFF,SP1,IFTDESC
.
SCRA9999  RETURN
.******************************************************************************
.* PROC - Enter Control Account                  Called by ML0000             *
.*        Returns : EXIT     (2=EXITCHAR entered, 1=nothing entered, 0=cont.) *
.*                  FMCALED,FMCAACC (variable entered)                        *
.******************************************************************************
PROC0000  DISPLAY   *P1:4,*EF,"    Ledger               : "
.
          MOVE      "28",CCOL          * set up screen address
          MOVE      "4",CVERT
          CALL      KLMAFM00
          BRANCH    EXIT,PROC9999,PROC9999
          DISPLAY   *P28:4,*EL,*V2LON,FMLALEDG,*HOFF,SP10,SP1,FMLADESC
.
PROC9999  RETURN
.******************************************************************************
.* SELA - Modify screen A data                   Called by ML0000             *
.*        Returns : EXIT     (2=EXITCHAR entered, 1=nothing entered, 0=cont.) *
.******************************************************************************
SELA0000  MOVE      ZERO,LCCITEM
.
.---- determine option to execute ----
.
SELA0100  ADD       ONE,LCCITEM                   * get next option
          BRANCH    ACCEPT,SELA0500              * in accept mode ?
          CALL      MAINQST                      * select/post/cancel ?
          MOVE      CCITEM,LCCITEM
.
          COMPARE   ZERO,LCCITEM
          GOTO      SELA9000 IF EQUAL            * post ?
.
          COMPARE   SEQ,LCCITEM
          GOTO      SELA9500 IF EQUAL            * cancel ?
.
SELA0500  MOVE      "28",CCOL                    * set up screen address
          MOVE      "5",CVERT
          ADD       LCCITEM,CVERT
          BRANCH    LCCITEM,SELA1100,SELA1150,SELA1200,SELA1300:
                            SELA1400,SELA1500,SELA1600,SELA1700:
                            SELA1800
.
          BRANCH    ACCEPT,SELA0900              * finished accept mode ?
          BEEP                                   * illegal option
.
SELA0900  MOVE      ZERO,ACCEPT                  * clear accept mode
          GOTO      SELA0100                     * get next option
.
.---- option 1 ---- Bank
.
SELA1100  MOVE      "1",CCONTIND
          CALL      KCAFFM00
          BRANCH    EXIT,SELA1110,SELA9500
          MOVE      FMAMACCT,FMLABNK
          MOVE      FMAMDESC,BNKDESC
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMACCT,*HOFF,SP1,FMAMDESC
          GOTO      SELA0100
.
SELA1110  MOVE      SP70,FMLABNK
          MOVE      SP70,BNKDESC
          DISPLAY   *PCCOL:CVERT,*EL
          GOTO      SELA0100
.
.---- option 1 ---- Bank 2
.
SELA1150  MOVE      "1",CCONTIND
          CALL      KCAFFM00
          BRANCH    EXIT,SELA1160,SELA9500
          MOVE      FMAMACCT,FMLABNK2
          MOVE      FMAMDESC,BNK2DESC
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMACCT,*HOFF,SP1,FMAMDESC
          GOTO      SELA0100
.
SELA1160  MOVE      SP70,FMLABNK2
          MOVE      SP70,BNK2DESC
          DISPLAY   *PCCOL:CVERT,*EL
          GOTO      SELA0100
.
.---- option 2 ---- Creditors
.
SELA1200  MOVE      "2",CCONTIND
          CALL      KCAFFM00
          BRANCH    EXIT,SELA1210,SELA9500
          MOVE      FMAMACCT,FMLACRD
          MOVE      FMAMDESC,CRDDESC
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMACCT,*HOFF,SP1,FMAMDESC
          GOTO      SELA0100
.
SELA1210  MOVE      SP70,FMLACRD
          MOVE      SP70,CRDDESC
          DISPLAY   *PCCOL:CVERT,*EL
          GOTO      SELA0100
.
.---- option 3 ---- Discount
.
SELA1300  MOVE      "3",CCONTIND
          CALL      KCAFFM00
          BRANCH    EXIT,SELA1310,SELA9500
          MOVE      FMAMACCT,FMLADIS
          MOVE      FMAMDESC,DISDESC
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMACCT,*HOFF,SP1,FMAMDESC
          GOTO      SELA0100
.
SELA1310  MOVE      SP70,FMLADIS
          MOVE      SP70,DISDESC
          DISPLAY   *PCCOL:CVERT,*EL
          GOTO      SELA0100
.
.---- option 4 ---- Debtors
.
SELA1400  MOVE      "4",CCONTIND
          CALL      KCAFFM00
          BRANCH    EXIT,SELA1410,SELA9500
          MOVE      FMAMACCT,FMLADEB
          MOVE      FMAMDESC,DEBDESC
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMACCT,*HOFF,SP1,FMAMDESC
          GOTO      SELA0100
.
SELA1410  MOVE      SP70,FMLADEB
          MOVE      SP70,DEBDESC
          DISPLAY   *PCCOL:CVERT,*EL
.
.---- option 5 ---- Payments in Suspense
.
SELA1500  MOVE      "6",CCONTIND
          CALL      KCAFFM00
          BRANCH    EXIT,SELA1510,SELA9500
          MOVE      FMAMACCT,FMLAPAY
          MOVE      FMAMDESC,PAYDESC
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMACCT,*HOFF,SP1,FMAMDESC
          GOTO      SELA0100
.
SELA1510  MOVE      SP70,FMLAPAY
          MOVE      SP70,PAYDESC
          DISPLAY   *PCCOL:CVERT,*EL
          GOTO      SELA0100
.
.---- option 6 ---- Cash GST
.
SELA1600  MOVE      "7",CCONTIND
          CALL      KCAFFM00
          BRANCH    EXIT,SELA1610,SELA9500
          MOVE      FMAMACCT,FMLAAGST
          MOVE      FMAMDESC,AGSDESC
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMACCT,*HOFF,SP1,FMAMDESC
          GOTO      SELA0100
.
SELA1610  MOVE      SP70,FMLAAGST
          MOVE      SP70,AGSDESC
          DISPLAY   *PCCOL:CVERT,*EL
          GOTO      SELA0100
.
.---- option 7 ---- Pay as You Go Control
.
SELA1700  MOVE      "1",CACCTIND
          CALL      KAMAFM00
          BRANCH    EXIT,SELA1710,SELA9500
          MOVE      FMAMACCT,FMLAPAYG
          MOVE      FMAMDESC,PAYGDESC
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMACCT,*HOFF,SP1,FMAMDESC
          GOTO      SELA0100

SELA1710  MOVE      SP70,FMLAPAYG
          MOVE      SP70,PAYGDESC
          DISPLAY   *PCCOL:CVERT,*EL
          GOTO      SELA0100
.
.---- option 7 ---- Inter fund trans
.
SELA1800  MOVE      "1",CACCTIND
          CALL      KAMAFM00
          BRANCH    EXIT,SELA1810,SELA9500
          MOVE      FMAMACCT,FMLAIFT
          MOVE      FMAMDESC,IFTDESC
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMACCT,*HOFF,SP1,FMAMDESC
          GOTO      SELA0100

SELA1810  MOVE      SP70,FMLAIFT
          MOVE      SP70,IFTDESC
          DISPLAY   *PCCOL:CVERT,*EL
          GOTO      SELA0100
.
.---- exit points ----
.
SELA9000  MOVE      ZERO,EXIT                    * post
          GOTO      SELA9999
.
SELA9500  MOVE      ONE,EXIT                     * cancel
.
SELA9999  RETURN
.******************************************************************************
.* POST - Post data                              Called by ML0000             *
.******************************************************************************
POST0000  PACK      KEY2,FMLALEDG
          CALL      RAFMLA1      
          BRANCH    OVRCD,POST9999
          CALL      UPFMLA1                 * write new record
.
POST9999  RETURN
.
          INC       FMSLMAIO/INC           * Ledger
          INC       FMSAMAIO/INC           * Account
          INC       FMSCAFIO/INC           * Account
.
          INC       FMSAMAKY           * Account
          INC       FMSCAFKY           * Account
          INC       FMSLMAKY           * Ledger
.
          INC       FMSSTDCD           * FMS Acc. standard routines
