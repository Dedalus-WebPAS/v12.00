. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS81                                             *
. * Desc      :   Budget Type Maintenance                              *
. **********************************************************************
. * Date      :   17.09.1990                                           *
. * Author    :   B.G.Ackland (IBA)                                    *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
. * VF.00.01  11.01.2000 Charles Handaya                               *
. *           Recompiled for Audit File date                           *
. **********************************************************************
.$$$$$$
.        PROGRAM IBAFMS81
.             open files and initialise variables
.             WHILE user wants to add/modify/delete DO
.                  get desired option (add, modify or delete)
.                  CASE option OF
.                       ADD     : WHILE user wants to add records DO
.                                      accept initial values
.                                      WHILE user wants modify screen data DO
.                                           get number of item to be modified
.                                           allow user to modify option
.                                      END WHILE
.                                      IF user wants to post data
.                                           THEN write new data
.                                      END IF
.                                 END WHILE
.                       MODIFY  : WHILE user wants to modify records DO
.                                      get and display desired record
.                                      WHILE user wants modify screen data DO
.                                           get number of item to be modified
.                                           allow user to modify option
.                                      END WHILE
.                                      IF user wants to post data
.                                           THEN update data
.                                      END IF
.                                 END WHILE
.                       DELETE  : WHILE user wants to delete records DO
.                                      get and display desired record
.                                      IF user wants to delete data
.                                           THEN delete data
.                                      END IF
.                                 END WHILE
.                  END CASE
.             END WHILE
.        END
.$$$$$$
. Standard FMS Accounting System Include
.---------------------------------------
          INC       FMSSTDDF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
          INC       FMSBUDFD/INC
          INC       FMSBTYFD/INC
          INC       FMSAMAFD/INC   * Ledger Master
          INC       FMSLMAFD/INC
          INC       FMSLMCFD/INC
          INC       FMSLBUFD/INC
          INC       FMSSBUFD/INC
          INC       FMSCSAFD/INC
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
BANKNAME  DIM       35
CREDNAME  DIM       35
DEBTNAME  DIM       35
DISCNAME  DIM       35
PAYMNAME  DIM       35
AGSTNAME  DIM       35
CGSTNAME  DIM       35
.
ACCEPT    FORM      1                  * accept mode (1=on)
FILENAME  DIM       8
.
FMSB      INIT      "fmsb"
MAKE      INIT      "isbuild "
KEYBUD    INIT      " 311 UC1-2,3-14 UC15-17,1-2,3-14"
.
FMSC      INIT      "fmsc"
KEYSBU    INIT      " 168 UC1-2,3-14 UC15-17,1-2,3-14"
.
FMSL      INIT      "fmsl"
KEYLBU    INIT      " 50 UC1-2,3-14 UC15-16,17-28,1-2,3-14"
.
KILL      INIT      "iserase "
STATDESC  DIM       25
NORMAL    INIT      "Normal               "
UPDATE    INIT      "Total Update Required"
LOCKED    INIT      "Budget File Locked   "
.                    
PRGID     INIT      "IBAFMS81"
PRGNAM    INIT      "Budget Type Maintenance"
VERSION   INIT      "V12.00.00"
.
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML0100    CALL      SELOPTN            * insert/modify/delete/exit ?
          COMPARE   ZERO,COPTION
          GOTO      ML9999 IF EQUAL    * exit program
.
ML0200    CALL      PROC0000           * get entry
          BRANCH    EXIT,ML0100,ML0100 * exit option
.
          LOAD      ACCEPT,COPTION,ONE,ZERO,ZERO * set up mode
          CALL      SCRA0000           * display screen A
.
          BRANCH    COPTION,ML1000,ML1000,ML2000
.
.---- add/modify data ----
.
ML1000    CALL      SELA0000           * modify screen A
          BRANCH    EXIT,ML0200        * don't save data
.
          CALL      POST0000           * post screen A data
          GOTO      ML0200             * get next entry
.
.---- delete data ----
.
ML2000    CALL      DELQST             * ok to delete (y/n/c) ?
          BRANCH    CEXIT,ML2100,ML0200,ML0100
.
ML2100    CALL      DELT0000           * delete screen A data
          GOTO      ML0200             * get next entry
.
.
ML9999    CHAIN     PGM                * chain back to menu
.
.******************************************************************************
.* INIT - Open Files                             Called by ML0000             *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening fmsbtyaf";  * Budget file
          OPEN      FMSBTYA1,"fmsbtyaf"
          DISPLAY   *P64:24,"fmslmcaf";          * Calendar File
          OPEN      FMSLMCA1,"fmslmcaf"
.
          DISPLAY   *P64:24,"fmslmaaf";          * Ledger file
          OPEN      FMSLMAA1,"fmslmaaf"
          PACK      KEY2,SP70
          CALL      RSFMLA1
          CALL      RKFMLA1
.
          DISPLAY   *P64:24,"fmsamaaf";          * Ledger file
          OPEN      FMSAMAA1,"fmsamaaf"
.
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.******************************************************************************
.* SCRA - Display Screen A                       Called by SELA0000, redisps  *
.*       Requires : ACCEPT   (1=accept mode)                                  *
.******************************************************************************
SCRA0000  DISPLAY   *P1:4,"    Budget Type          : ",*V2LON,FMBTCODE,*EF:
                    *P1:6," 1",*HOFF,". Description          : ",*V2LON:
                    *P1:7," 2",*HOFF,". Financial Year       : ",*V2LON:
                    *P1:8," 3",*HOFF,". Status               : ";
          BRANCH    ACCEPT,SCRA9999    * accept mode
.
          MOVE      FMBTSTAT,F1
          ADD       ONE,F1
          LOAD      STATDESC,F1,NORMAL,UPDATE,LOCKED
          DISPLAY   *P28:6,*V2LON,FMBTDESC:
                    *P28:7,*V2LON,FMBTYEAR:
                    *P28:8,*V2LON,STATDESC 

SCRA9999  RETURN
.******************************************************************************
.* DEFV - set up default variables               Called by SELA0000           *
.******************************************************************************
DEFV0000  MOVE      UNDLN70,FMBTDESC   * set description
          MOVE      UNDLN70,FMBTYEAR   * set description
          MOVE      UNDLN70,STATDESC   * set description
          MOVE      UNDLN70,FMBTSTAT   * set description
.
DEFV9999  RETURN
.******************************************************************************
.* PROC - Enter Budget Type              Called by ML0000             *
.*        Returns : EXIT     (2=EXITCHAR entered, 1=nothing entered, 0=cont.) *
.*                  FMBTCODE (variable entered)                               *
.******************************************************************************
PROC0000  DISPLAY   *P1:4,"    Budget Type          : ",*EF;
.
          MOVE      "28",CCOL          * set up screen address
          MOVE      "4",CVERT
.
          COMPARE   ONE,COPTION        * insert mode ?
          GOTO      PROC1000 IF EQUAL
.
          CALL      KBTYFM00
          GOTO      PROC9999
.
.--- Dont allow /.*[]\${}'`|<> in insert ---
.
PROC1000  CALL      ABTYFM00
          BRANCH    EXIT,PROC9999,PROC9999
.
          MOVE      "/",ANS
          SCAN      ANS,FMBTCODE
          GOTO      PROC8000 IF EQUAL
          MOVE      ".",ANS
          SCAN      ANS,FMBTCODE
          GOTO      PROC8000 IF EQUAL
          MOVE      "*",ANS
          SCAN      ANS,FMBTCODE
          GOTO      PROC8000 IF EQUAL
          MOVE      "[",ANS
          SCAN      ANS,FMBTCODE
          GOTO      PROC8000 IF EQUAL
          MOVE      "]",ANS
          SCAN      ANS,FMBTCODE
          GOTO      PROC8000 IF EQUAL
          MOVE      "\",ANS
          SCAN      ANS,FMBTCODE
          GOTO      PROC8000 IF EQUAL
          MOVE      "$",ANS
          SCAN      ANS,FMBTCODE
          GOTO      PROC8000 IF EQUAL
          MOVE      "{",ANS
          SCAN      ANS,FMBTCODE
          GOTO      PROC8000 IF EQUAL
          MOVE      "}",ANS
          SCAN      ANS,FMBTCODE
          GOTO      PROC8000 IF EQUAL
          MOVE      "'",ANS
          SCAN      ANS,FMBTCODE
          GOTO      PROC8000 IF EQUAL
          MOVE      "`",ANS
          SCAN      ANS,FMBTCODE
          GOTO      PROC8000 IF EQUAL
          MOVE      "|",ANS
          SCAN      ANS,FMBTCODE
          GOTO      PROC8000 IF EQUAL
          MOVE      "<",ANS
          SCAN      ANS,FMBTCODE
          GOTO      PROC8000 IF EQUAL
          MOVE      ">",ANS
          SCAN      ANS,FMBTCODE
          GOTO      PROC8000 IF EQUAL
.
          GOTO      PROC9999
.
PROC8000  
          DISPLAY   *P1:24,*B,*EF,ANS," is Not Alowed in a Budget Code - ";
          CALL      HITENTER
          GOTO      PROC0000
.
PROC9999  RETURN
.******************************************************************************
.* SELA - Modify screen A data                   Called by ML0000             *
.*        Returns : EXIT     (2=EXITCHAR entered, 1=nothing entered, 0=cont.) *
.*                  FMBTCODE (variable entered)                               *
.******************************************************************************
SELA0000  MOVE      ZERO,CCITEM
.
          COMPARE   ONE,ACCEPT
          CALL      DEFV0000 IF EQUAL            * set up default values
.
.---- determine option to execute ----
.
SELA0100  ADD       ONE,CCITEM                   * get next option
          BRANCH    ACCEPT,SELA0500              * in accept mode ?
          CALL      MAINQST                      * select/post/cancel ?
.
          COMPARE   ZERO,CCITEM
          GOTO      SELA9000 IF EQUAL            * post ?
.
          COMPARE   SEQ,CCITEM
          GOTO      SELA9500 IF EQUAL            * cancel ?
.
SELA0500  MOVE      "28",CCOL                    * set up screen address
          MOVE      "5",CVERT
          ADD       CCITEM,CVERT
          BRANCH    CCITEM,SELA1100,SELA1200,SELA1300
.                                                * execute option
.
          BRANCH    ACCEPT,SELA0900              * finished accept mode ?
          BEEP                                   * illegal option
.
SELA0900  MOVE      ZERO,ACCEPT                  * clear accept mode
          GOTO      SELA0100                     * get next option
.
.---- option 1 ----
.
SELA1100  MATCH     FMBTDESC,SP70
          GOTO      SELA1150 IF NOT EQUAL        * description spaces ?
.
          PACK      FMBTDESC,UNDLN70
.
SELA1150  KEYIN     *PCCOL:CVERT,*DV,FMBTDESC:   * read in description
                    *PCCOL:CVERT,*V2LON,*RV,FMBTDESC;
          PACK      FMBTDESC,FMBTDESC,SP70
.
          MATCH     FMBTDESC,SP70
          GOTO      SELA1100 IF EQUAL            * description spaces ?
          MATCH     FMBTDESC,UNDLN70
          GOTO      SELA9500 IF EQUAL            * nothing entered ?
          MATCH     FMBTDESC,EXITCHAR
          GOTO      SELA9500 IF EQUAL            * EXITCHAR entered ?
.
          DISPLAY   *PCCOL:CVERT,*V2LON,FMBTDESC;
          GOTO      SELA0100
.
.---- option 2 ----
.
SELA1200  CALL      ALMCFM00
          BRANCH    EXIT,SELA1210,SELA9500
          MOVE      FMLCYEAR,FMBTYEAR
          DISPLAY   *PCCOL:CVERT,*V2LON,FMBTYEAR
          GOTO      SELA0100
.
SELA1210  DISPLAY   *P1:24,*EL,*B:               * nothing entered
                    "A Financial Year Must Be Entered - ";
          CALL      HITENTER
          GOTO      SELA1200

.
.---- option 3 ----
.
SELA1300  KEYIN     *P1:24,*EL,*V2LON,"0",*HOFF:
                    " = Normal, ",*V2LON,"1",*HOFF:
                    " = Update Required, ",*V2LON,"2",*HOFF:
                    " = Locked":
                    *PCCOL:CVERT,*EL,*DV,UNDLN1:
                    *PCCOL:CVERT,*V2LON,FMBTSTAT
          TYPE      FMBTSTAT
          GOTO      SELA1310 IF NOT EQUAL
          MOVE      FMBTSTAT,F1
          ADD       ONE,F1
          LOAD      STATDESC,F1,NORMAL,UPDATE,LOCKED
          BRANCH    F1,SELA1320,SELA1320,SELA1320
SELA1310  DISPLAY   *P1:24,*EL,*B,"Invalid Status - ";
          CALL      HITENTER
          GOTO      SELA1300
.
SELA1320  DISPLAY   *PCCOL:CVERT,*V2LON,STATDESC
          GOTO      SELA0100
.
.---- exit points ----
.
SELA9000  MOVE      ZERO,EXIT                    * post
          GOTO      SELA9999
.
SELA9500  MOVE      ONE,EXIT                     * cancel
.
SELA9999  RETURN
.******************************************************************************
.* POST - Post data                              Called by ML0000             *
.******************************************************************************
POST0000  PACK      KEY4,FMBTCODE,SP70
          CALL      DEFMBT1                 * delete old record (if possible)
          CALL      WRFMBT1                 * write new record
.
          PACK      FILENAME,FMSB,FMBTCODE
          REP       " 0",FILENAME
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FMSBUDA1,FILENAME
          TRAPCLR   IO
          COMPARE   ZERO,OVRCD
          GOTO      POST1000 IF EQUAL
.
          DISPLAY   *P1:24,*EL,"Creating New Budget File - ",FILENAME,*P1:10;
          PACK      CMDLINE,MAKE,FILENAME,KEYBUD
          EXECUTE   CMDLINE,TASKID
.
POST1000  PACK      FILENAME,FMSL,FMBTCODE
          REP       " 0",FILENAME
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FMSLBUA1,FILENAME
          TRAPCLR   IO
          COMPARE   ZERO,OVRCD
          GOTO      POST2000 IF EQUAL
.
          DISPLAY   *P1:24,*EL,"Creating New Budget File - ",FILENAME,*P1:10;
          PACK      CMDLINE,MAKE,FILENAME,KEYLBU
          EXECUTE   CMDLINE,TASKID
.
POST2000  PACK      FILENAME,FMSC,FMBTCODE
          REP       " 0",FILENAME
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FMSSBUA1,FILENAME
          TRAPCLR   IO
          COMPARE   ZERO,OVRCD
          GOTO      POST9999 IF EQUAL
.
          DISPLAY   *P1:24,*EL,"Creating New Budget File - ",FILENAME,*P1:10;
          PACK      CMDLINE,MAKE,FILENAME,KEYSBU
          EXECUTE   CMDLINE,TASKID
.
POST9999  RETURN
.******************************************************************************
.* DELT - Delete data                            Called by ML0000             *
.******************************************************************************
DELT0000  PACK      FILENAME,FMSB,FMBTCODE
          REP       " 0",FILENAME
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FMSBUDA1,FILENAME
          TRAPCLR   IO
          BRANCH    OVRCD,DELT9000
.
          PACK      KEY14,SP70
          CALL      RSFMBU1
          CALL      RKFMBU1
          BRANCH    OVRCD,DELT1000
DELT0500  KEYIN     *P1:24,*B,*EL,"WARNING - Budget File Contains Data Do You ":
                    "Wish to Continue ? ",*V2LON,ANS
          REP       UPPLOW,ANS
          MATCH     ANSN,ANS
          GOTO      DELT9999 IF EQUAL
          MATCH     ANSY,ANS
          GOTO      DELT0500 IF NOT EQUAL
.         
DELT1000  DISPLAY   *P1:24,*EL,"Deleting Budget File - ",FILENAME,*P1:10;
          PACK      CMDLINE,KILL,FILENAME
          EXECUTE   CMDLINE,TASKID
.
          PACK      FILENAME,FMSC,FMBTCODE
          REP       " 0",FILENAME
          DISPLAY   *P1:24,*EL,"Deleting Budget File - ",FILENAME,*P1:10;
          PACK      CMDLINE,KILL,FILENAME
          EXECUTE   CMDLINE,TASKID
.
          PACK      FILENAME,FMSL,FMBTCODE
          REP       " 0",FILENAME
          DISPLAY   *P1:24,*EL,"Deleting Budget File - ",FILENAME,*P1:10;
          PACK      CMDLINE,KILL,FILENAME
          EXECUTE   CMDLINE,TASKID
.
DELT9000  PACK      KEY4,FMBTCODE,SP70
          CALL      DEFMBT1                 * delete old record (if possible)
.
DELT9999  RETURN
.******************************************************************************
.* INCLUDES FOR I/O'S                                                         *
.******************************************************************************
RBTYFM00  DISPLAY   *P1:4,"    Budget Type          : ",*EF;
          RETURN              
.
RLMCFM00  CALL      SCRA0000
          RETURN              
.
          INC       FMSBUDIO/INC
          INC       FMSBTYIO/INC
          INC       FMSAMAIO/INC       * Ledger
          INC       FMSLMAIO/INC
          INC       FMSLMCIO/INC
          INC       FMSLBUIO/INC
          INC       FMSSBUIO/INC
.
          INC       FMSLMCKY           * Budget Type Keyin
          INC       FMSBTYKY           * Budget Type Keyin
          INC       FMSSTDCD           * FMS Acc. standard routines
