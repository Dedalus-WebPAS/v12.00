.*****************************************************************************
.*    Program      : F01WTTR1                                                *
.*    Description  : Conversion wattr1af to new File layout                  *
.*                                                                           *
.*    Author       : Jill Parkinson CAR 234014                               *
.*    Date         : 12/01/2011                                              *
.*    Modifications:                                                         *
.*****************************************************************************
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
.
OLDFILE1  IFILE    SQL, FIXED=450, KEY="U1-8,9-17,18-19"
.
.NAME     TYPE      LENGTH  PHYS   START  DESCRIPTION
.----     ----      ------  ----   -----  -----------
.WMURNO    DIM       8       8         1   U/R Number
.WMCODE    DIM       9       9         9   Procedure Code
.DWMCNT    DIM       2       2        18   Procedure Count
.
.WMTIME    FORM      5       4        20   Procedure Time (Minutes)
OLDMDESC    DIM       50     50        24   Procedure Description
.WMREASON  DIM       40     40        74   Reason/Diagnosis for Procedure
.DWMSTAT   DIM       1       1       114   Procedure Status
..                                           1 - Unscheduled
..                                           2 - Scheduled
..                                           3 - Pre-Admitted
..                                           4 - Admitted
..                                           5 - Discharged
..                                           6 - Removed from Waiting List
..                                           7 - Performed
..                                           8 - Not Performed
..                                           9 - Cancelled Admission
.WMDATE1   DIM       8       8       115   Date on List (ccyymmdd)
.WMDATE2   DIM       8       8       123   Date of Last Review (ccyymmdd) /
..                                         Final Outcome of Offer
.WMDATE3   DIM       8       8       131   Scheduled Admit. Date (ccyymmdd)
.WMDATE4   DIM       8       8       139   Removal Date (ccyymmdd)
.WMREMOVE  DIM       3       3       147   Removal Reason (Category WR/BC)
.WMUNIT    DIM       3       3       150   Unit/Clinic (Category WU)
.WMDOCTOR  DIM       6       6       153   Attending Doctor
.WMSTAY    FORM      5       4       159   Estimated Stay (Days)
.WMPTY     DIM       3       3       163   Priority (Category TP)
.WMNOTICE  DIM       3       3       166   Short Notice (Category WS)
.WMPTYPE   DIM       3       3       169   Resident Status (Category T)
.WMPCAT    DIM       3       3       172   Cat "A"  - dunno what desc is ????
.WMUDEF1   DIM       3       3       175   User Defined 1 (Category W1)
.WMUDEF2   DIM       3       3       178   User Defined 2 (Category W2)
.WMUDEF3   DIM       3       3       181   User Defined 3 (Category W3)
.WMUDEF4   DIM       3       3       184   User Defined 4 (Category W4)
..
.WMBOOK    FORM      8       5       187   Booking Number
.WMKEYDT   DIM       8       8       192   Keyin Date For Procedure
..                                                 (ccyymmdd)
.WTWMPORT  DIM       2       2       200   Locking Indicator
.WTWMREAD  DIM       8       8       202   Re-Assessment Date (ccyymmdd)
.WTWMDEDA  DIM       8       8       210   Deferred or Staged Adm Date
..                                               (ccyymmdd)
.. The following two fields are used for Transmission to the SAHC
..
.WTWMUNIQ  FORM      5       4       218   Unique Identifier
.WTWMSENT  FORM      1       2       222   Procedure Status 4 & 5 records
.                                         already sent = 1, otherwise = 0
.WTWMCLSS  DIM       3       3       224   Patient Cat/Adm Class (Cat P)
..
.WTWMDTAD  DIM       8       8       227   Decision to Admit Date
.WTWMACAT  DIM       3       3       235   Administrative Category  "CL"
.WTWMGADD  DIM       8       8       238   Guaranteed Admission Date
.WTWMDABD  DIM       8       8       246   Do Not Admit Before Date
.WTWMEATY  DIM       3       3       254   Elective Admission Type  "CC"
.WTWMADMC  DIM       3       3       257   Admission Category   "WA"
.WTWMECRA  DIM       20      20      260   ECR Approval Number
.WTWMGPFA  DIM       20      20      280   GPFH Approval Number
..WTWMRFGP  DIM       8       8       300   Referring GP
.WTWMGPPC  DIM       6       6       308   GP Practice code
.WTWMNAFR  DIM       8       8       314   Not available from date
.WTWMNATO  DIM       8       8       322   Not available to date
.WTWMGPPT  DIM       2       2       330   GP Practice count
.WTWMDOFR  DIM       3       3       332   District of Residence (Cat DA)
.WTWMOPER  DIM       4       4       335   Operator who added proc to W/L
.WTWMIHSP  DIM       3       3       339   Intended Hospital (pathspaf)
.WTWMVLSF  DIM       1       1       342   Validation Letter Sent Flag (Y/N)
.WTWMPRO2  DIM       9       9       343   Procedure Code 2
.WTWMPRO3  DIM       9       9       352   Procedure Code 3
.WTWMTRPA  DIM       8       8       361   Pre-Anaesthetic Date (CCYYMMDD)
.WMUDEF5   DIM       3       3       369   User Defined 5 (Category X5)
.WMUDEF6   DIM       3       3       372   User Defined 6 (Category X6)
.WMUDEF7   DIM       3       3       375   User Defined 7 (Category X7)
.WMUDEF8   DIM       3       3       378   User Defined 8 (Category X8)
.WTWMRANK  DIM       6       6       381   Patient Rank
.WTWMSRCR  DIM       3       3       387   Source of Referral (S)
.WTWMDRSA  DIM       8       8       390   Date of ref for 1st spec. assessment
.WTWMDOSA  DIM       8       8       398   Date of 1st specialised assessment
.WTWMPHSP  DIM       3       3       406   Principal Health serv Purchaser Cat HP
.WTWMBSCD  DIM       2       2       409   Booking Status Code
.WTWMRSFL  DIM       1       1       411   Record sent flag (Y or N)
.WTWMECNT  DIM       9       9       412   ESIS unique count eg. CCYY?????
.WTWMCSST  DIM       1       1       421   Consent Obtained (N=NO,Y=YES)
.WTWMCSDT  DIM       8       8       422   Consent Changed Date (ccyymmdd)
.WTWMASSR  DIM       10      10      430   Assessor Code (pmshcpaf)
OLDMSPAR  DIM       10      10      440   Spare
..End of Record                      450
.
.    redefine form fields from key
.    -----------------------------
.NAME     TYPE   LENGTH  PHYSICAL   START     DESCRIPTION
.----     ----   ------  --------   -----     -----------
WMCNT     FORM      2                18       Procedure Count
WMSTAT    FORM      1               115       Procedure Status
.
.
.
.
.                                     ******** copy the new FD here *******
WATTR1A1  IFILE    SQL, FIXED=570, KEY="U1-8,9-17,18-19"
WATTR1A2  IFILE    SQL, FIXED=570, KEY="U144-144,1-8,9-17,18-19"
WATTR1A3  IFILE    SQL, FIXED=570, KEY="U145-152,1-8,9-17,18-19"
WATTR1A4  IFILE    SQL, FIXED=570, KEY="U1-8,145-152,9-17,18-19"
WATTR1A5  IFILE    SQL, FIXED=570, KEY="U144-144,183-188,180-182,193-195,1-8,9-17,18-19"
WATTR1A6  IFILE    SQL, FIXED=570, KEY="U257-264,144-144,1-8,9-17,18-19"
WATTR1A7  IFILE    SQL, FIXED=570, KEY="U144-144,180-182,145-152,1-8,9-17,18-19"
WATTR1A8  IFILE    SQL, FIXED=570, KEY="U441-441,1-8,9-17,18-19"
.
.NAME     TYPE      LENGTH  PHYS   START  DESCRIPTION
.----     ----      ------  ----   -----  -----------
WMURNO    DIM       8       8         1   U/R Number
WMCODE    DIM       9       9         9   Procedure Code
DWMCNT    DIM       2       2        18   Procedure Count
.
WMTIME    FORM      5       4        20   Procedure Time (Minutes)
WMDESC    DIM       80     80        24   Procedure Description
WMREASON  DIM       40     40       104   Reason/Diagnosis for Procedure
DWMSTAT   DIM       1       1       144   Procedure Status
.                                           1 - Unscheduled
.                                           2 - Scheduled
.                                           3 - Pre-Admitted
.                                           4 - Admitted
.                                           5 - Discharged
.                                           6 - Removed from Waiting List
.                                           7 - Performed
.                                           8 - Not Performed
.                                           9 - Cancelled Admission
WMDATE1   DIM       8       8       145   Date on List (ccyymmdd)
WMDATE2   DIM       8       8       153   Date of Last Review (ccyymmdd) /
.                                         Final Outcome of Offer
WMDATE3   DIM       8       8       161   Scheduled Admit. Date (ccyymmdd)
WMDATE4   DIM       8       8       169   Removal Date (ccyymmdd)
WMREMOVE  DIM       3       3       177   Removal Reason (Category WR/BC)
WMUNIT    DIM       3       3       180   Unit/Clinic (Category WU)
WMDOCTOR  DIM       6       6       183   Attending Doctor
WMSTAY    FORM      5       4       189   Estimated Stay (Days)
WMPTY     DIM       3       3       193   Priority (Category TP)
WMNOTICE  DIM       3       3       196   Short Notice (Category WS)
WMPTYPE   DIM       3       3       199   Resident Status (Category T)
WMPCAT    DIM       3       3       202   Cat "A"  - dunno what desc is ????
WMUDEF1   DIM       3       3       205   User Defined 1 (Category W1)
WMUDEF2   DIM       3       3       208   User Defined 2 (Category W2)
WMUDEF3   DIM       3       3       211   User Defined 3 (Category W3)
WMUDEF4   DIM       3       3       214   User Defined 4 (Category W4)
.
WMBOOK    FORM      8       5       217   Booking Number
WMKEYDT   DIM       8       8       222   Keyin Date For Procedure
.                                                 (ccyymmdd)
WTWMPORT  DIM       2       2       230   Locking Indicator
WTWMREAD  DIM       8       8       232   Re-Assessment Date (ccyymmdd)
WTWMDEDA  DIM       8       8       240   Deferred or Staged Adm Date
.                                               (ccyymmdd)
. The following two fields are used for Transmission to the SAHC
.
WTWMUNIQ  FORM      5       4       248   Unique Identifier
WTWMSENT  FORM      1       2       252   Procedure Status 4 & 5 records
.                                         already sent = 1, otherwise = 0
WTWMCLSS  DIM       3       3       254   Patient Cat/Adm Class (Cat P)
.
WTWMDTAD  DIM       8       8       257   Decision to Admit Date
WTWMACAT  DIM       3       3       265   Administrative Category  "CL"
WTWMGADD  DIM       8       8       268   Guaranteed Admission Date
WTWMDABD  DIM       8       8       276   Do Not Admit Before Date
WTWMEATY  DIM       3       3       284   Elective Admission Type  "CC"
WTWMADMC  DIM       3       3       287   Admission Category   "WA"
WTWMECRA  DIM       20      20      290   ECR Approval Number
WTWMGPFA  DIM       20      20      310   GPFH Approval Number
WTWMRFGP  DIM       8       8       330   Referring GP
WTWMGPPC  DIM       6       6       338   GP Practice code
WTWMNAFR  DIM       8       8       344   Not available from date
WTWMNATO  DIM       8       8       352   Not available to date
WTWMGPPT  DIM       2       2       360   GP Practice count
WTWMDOFR  DIM       3       3       362   District of Residence (Cat DA)
WTWMOPER  DIM       4       4       365   Operator who added proc to W/L
WTWMIHSP  DIM       3       3       369   Intended Hospital (pathspaf)
WTWMVLSF  DIM       1       1       372   Validation Letter Sent Flag (Y/N)
WTWMPRO2  DIM       9       9       373   Procedure Code 2
WTWMPRO3  DIM       9       9       382   Procedure Code 3
WTWMTRPA  DIM       8       8       391   Pre-Anaesthetic Date (CCYYMMDD)
WMUDEF5   DIM       3       3       399   User Defined 5 (Category X5)
WMUDEF6   DIM       3       3       402   User Defined 6 (Category X6)
WMUDEF7   DIM       3       3       405   User Defined 7 (Category X7)
WMUDEF8   DIM       3       3       408   User Defined 8 (Category X8)
WTWMRANK  DIM       6       6       411   Patient Rank
WTWMSRCR  DIM       3       3       417   Source of Referral (S)
WTWMDRSA  DIM       8       8       420   Date of ref for 1st spec. assessment
WTWMDOSA  DIM       8       8       428   Date of 1st specialised assessment
WTWMPHSP  DIM       3       3       436   Principal Health serv Purchaser Cat HP
WTWMBSCD  DIM       2       2       439   Booking Status Code
WTWMRSFL  DIM       1       1       441   Record sent flag (Y or N)
WTWMECNT  DIM       9       9       442   ESIS unique count eg. CCYY?????
WTWMCSST  DIM       1       1       451   Consent Obtained (N=NO,Y=YES)
WTWMCSDT  DIM       8       8       452   Consent Changed Date (ccyymmdd)
WTWMASSR  DIM       10      10      460   Assessor Code (pmshcpaf)
WTWMSPAR  DIM      100     100      470   Spare
.End of Record                      570
.
.
.
.
CMDLINE   DIM       100
DIM4      DIM       4
DIM4A     DIM       4
DIM6      DIM       6
DIM6A     DIM       6
DIM60     DIM       60
DDCENT    DIM       2
.
INPFILE   DIM       8
RECNUM    FORM      8
NEWFILE   DIM       60
NEWPATH   DIM       60
OLDPATH   DIM       60
DEFPATH   DIM       60
SAVEFLG   FORM      1
SP50      INIT    "                                                  "
SP60      INIT    "                                                            "
.
PRGNAM    INIT      "CONVERSION WATTR1FD"
PRGID     INIT      "F01WTTR1"
VERSION   INIT      "V12.00.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN1000,MAIN0000,MAIN9999 * Yes, no, cancel
.
MAIN1000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
. Loop thru old file and write records to new file
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/compress saved file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          
INIT9999  RETURN
.
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
PREP0000 
. open old file
          MOVE      "wattr1af",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
. Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat wattr1af.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "swttr1af.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat wattr1af.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "swttr1af.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
. remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "wattr1af",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "swttr1af",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "wattr1af",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.      eg    APPEND    " 195 UC9-16",CMDLINE
.
          APPEND    " 570 UC1-8,9-17,18-19",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has 
.  to be repeated with for each individual index
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC144-144,1-8,9-17,18-19",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC145-152,1-8,9-17,18-19",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC1-8,145-152,9-17,18-19",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC144-144,183-188,180-182,193-195,1-8,9-17,18-19",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC257-264,144-144,1-8,9-17,18-19",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC144-144,180-182,145-152,1-8,9-17,18-19",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC441-441,1-8,9-17,18-19",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
          OPEN      WATTR1A1,NEWFILE
.
. set the flag to say we want to continue
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
          ELSE
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
            ELSE
              DISPLAY   "has caused an IO error - ";
            ENDIF
          ENDIF
          DISPLAY   "File will be bypassed"
.
          IF        FORM2 < 17
            CALL      HITENTER
          ENDIF
          DISPLAY   *P1:23,*EF
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
.
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          PACK      KEY19,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
.  All necessary changes to the files field lengths, date changes
.  or any other sort of modification must be made here
.
          PACK      WMDESC,OLDMDESC,SP70
          PACK      WTWMSPAR,SP70,SP70
.                                               * Pack key here
          PACK      KEY19,WMURNO,WMCODE,DWMCNT,SP70
          CALL      WRTREA1                    * write to new file
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     WATTR1A1                      * close new file
          CLOSE     OLDFILE1                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
READ9999  RETURN
.
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
VALD0000  MOVE      ZERO,EXIT
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or compress save file                                *
.**********************************************************************
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "swttr1af*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "compress -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not compressed or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.         Get the default directory
.**********************************************************************
DEFT0000  EXECUTE   "ldat wattr1af.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding 'wattr1af'.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
DEFT9999  RETURN
+
.
.                  ******************************    OLD IO ROUTINES
READSOLD  RESET     KEY19
          READ      OLDFILE1,KEY19;;
          RETURN
.

.                  Copy the Positional Read (RS or RDS) from original I/O file,
.                  - change Label to READSOLD and the index name to OLDFILE1
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;WMURNO,WMCODE,DWMCNT,WMTIME,OLDMDESC:
                             WMREASON,DWMSTAT,WMDATE1,WMDATE2,WMDATE3:
                             WMDATE4,WMREMOVE,WMUNIT,WMDOCTOR,WMSTAY:
                             WMPTY,WMNOTICE,WMPTYPE,WMPCAT:
                             WMUDEF1,WMUDEF2,WMUDEF3,WMUDEF4:
                             WMBOOK,WMKEYDT,WTWMPORT,WTWMREAD:
                             WTWMDEDA,WTWMUNIQ,WTWMSENT,WTWMCLSS:
                             WTWMDTAD,WTWMACAT,WTWMGADD,WTWMDABD:
                             WTWMEATY,WTWMADMC,WTWMECRA,WTWMGPFA:
                             WTWMRFGP,WTWMGPPC,WTWMNAFR,WTWMNATO:
                             WTWMGPPT,WTWMDOFR,WTWMOPER,WTWMIHSP:
                             WTWMVLSF,WTWMPRO2,WTWMPRO3,WTWMTRPA:
                             WMUDEF5,WMUDEF6,WMUDEF7,WMUDEF8:
                             WTWMRANK,WTWMSRCR,WTWMDRSA,WTWMDOSA:
                             WTWMPHSP,WTWMBSCD,WTWMRSFL,WTWMECNT:
                             WTWMCSST,WTWMCSDT,WTWMASSR,OLDMSPAR
         GOTO      OVERCOND IF OVER
         MOVE      DWMCNT,WMCNT
         MOVE      DWMSTAT,WMSTAT
         RETURN
.

.                  ******************************    NEW IO ROUTINES
WRTREA1  RESET     KEY19
         MOVE      WMCNT,DWMCNT
         MOVE      WMSTAT,DWMSTAT
          MOVE      PASSCODE,WTWMOPER
          WRITE     WATTR1A1,KEY19;WMURNO,WMCODE,DWMCNT,WMTIME,WMDESC:
                                   WMREASON,DWMSTAT,WMDATE1,WMDATE2,WMDATE3:
                                   WMDATE4,WMREMOVE,WMUNIT,WMDOCTOR,WMSTAY:
                                   WMPTY,WMNOTICE,WMPTYPE,WMPCAT:
                                   WMUDEF1,WMUDEF2,WMUDEF3,WMUDEF4:
                                   WMBOOK,WMKEYDT,WTWMPORT,WTWMREAD:
                                   WTWMDEDA,WTWMUNIQ,WTWMSENT,WTWMCLSS:
                                   WTWMDTAD,WTWMACAT,WTWMGADD,WTWMDABD:
                                   WTWMEATY,WTWMADMC,WTWMECRA,WTWMGPFA:
                                   WTWMRFGP,WTWMGPPC,WTWMNAFR,WTWMNATO:
                                   WTWMGPPT,WTWMDOFR,WTWMOPER,WTWMIHSP:
                                   WTWMVLSF,WTWMPRO2,WTWMPRO3,WTWMTRPA:
                                   WMUDEF5,WMUDEF6,WMUDEF7,WMUDEF8:
                                   WTWMRANK,WTWMSRCR,WTWMDRSA,WTWMDOSA:
                                   WTWMPHSP,WTWMBSCD,WTWMRSFL,WTWMECNT:
                                   WTWMCSST,WTWMCSDT,WTWMASSR,WTWMSPAR
         RETURN
.
          INC       STD001IO
