.******************************************************************************
.* System         : Accident and Emergency                                    *
.*                                                                            *
.* Include Name   : AAEGDCDS.PBL                                              *
.*                                                                            *
.* Function       : Display the codes from AAEGDCFD and AAEGDLFD.             *
.*                                                                            *
.* Author         : Justin Coates      03/08/1993                             *
.*                                                                            *
.* Returns        : KEY3               (AEDCCODE+AEDLCODE)                    *
.*                                                                            *
.* Modifications  : 22/11/93 ROD Change heading to Diagnosis                  *
.******************************************************************************
.
AAEGDCDS  MOVE      FIVE,CVERT              * line counter
          MOVE      ONE,CCOL                * start on LHS of screen
          MOVE      ZERO,AEDCQCNT           * clear the count
          MOVE      ONE,CPAGENO             * set page number
          MOVE      SP2,KEY2                * key of blanks
          CALL      RSAEGDC1                * positional read
.
          DISPLAY   *P1:3,*EF,*V2LON,*P25:4,"DIAGNOSIS CODES":
                    *P1:5,*ULON,"Item",*P6:5,"Code":
                    *P11:5,"Description         ":
                    *P41:5,"Item",*P46:5,"Code":
                    *P51:5,"Description         "
.
. ---------------------------------------------------------------
. ------- next read on diagnosis file and position on link ------
. ---------------------------------------------------------------
.
HAEDC100  CALL      RKAEGDC1
          BRANCH    OVRCD,HAEDC500          * end of file
.
          PACK      KEY3,AEDCCODE,SP1
          CALL      RSAEGDL1
.
          PACK      CKYIDEF3,AEDCCODE,SP1
          PACK      KEY22,AEDCDESC,SP2
.
. ----------------------------------
. ------- increment counters -------
. ----------------------------------
.
HAEDC200  ADD       ONE,CVERT               * inc line counter
.
          COMPARE   "23",CVERT
          GOTO      HAEDC300 IF LESS        * will fit on the page
.
          ADD       "40",CCOL               * inc column counter
          MOVE      SIX,CVERT               * reset row position
.
          COMPARE   "80",CCOL  
          GOTO      HAEDC400 IF NOT LESS    * need a new page
.
. -------------------------------------------
. ------- display all the information -------
. -------------------------------------------
.
HAEDC300  ADD       ONE,AEDCQCNT
          DISPLAY   *PCCOL:CVERT,*V2LON,SP1,AEDCQCNT,*HOFF,DOT:
                                        SP1,CKYIDEF3,SP2,KEY22
          MOVE      CKYIDEF3,AEDCQARR[AEDCQCNT]
.
.         now loop over link file
.
          CALL      RKAEGDL1
          BRANCH    OVRCD,HAEDC100          * no more codes
          MATCH     AEDCCODE,AEDLMAIN
          GOTO      HAEDC100 IF NOT EQUAL   * different code
.
          PACK      CKYIDEF3,AEDCCODE,AEDLCODE
          PACK      KEY22,SP2,AEDLDESC
          GOTO      HAEDC200
.
. ------- new page needed -------
.
HAEDC400  BRANCH    CPAGENO,HAEDC450        * first page
.
HAEDC410  MOVE      ONE,AEDCQLIN            * (N)ext, (F)irst
          CALL      HAEDCK00
          BRANCH    EXIT,HAEDC999,AAEGDCDS,HAEDC700
.                        exit     first    next
          GOTO      HAEDC600
.
HAEDC450  MOVE      TWO,AEDCQLIN            * (N)ext only
          CALL      HAEDCK00
          BRANCH    EXIT,HAEDC999,HAEDC590,HAEDC700
.                        exit     first    next
          GOTO      HAEDC600
.
. ------- end of data -------
.
HAEDC500  BRANCH    CPAGENO,HAEDC550        * first page
.
HAEDC510  MOVE      THREE,AEDCQLIN          * (F)irst
          CALL      HAEDCK00
          BRANCH    EXIT,HAEDC999,AAEGDCDS,HAEDC590
.                        exit     first    next
          GOTO      HAEDC600
.
HAEDC550  MOVE      FOUR,AEDCQLIN           * no paging
          CALL      HAEDCK00
          BRANCH    EXIT,HAEDC999,HAEDC590,HAEDC590
.                        exit     first    next
          GOTO      HAEDC600
.
HAEDC590  BEEP
          BRANCH    AEDCQLIN,HAEDC410,HAEDC450,HAEDC510,HAEDC550
.
. ------- item selected -------
.
HAEDC600  MOVE      AEDCQARR[FORM2],KEY3
          UNPACK    KEY3,KEY2,KEY1
          CALL      RDAEGDC1
          BRANCH    OVRCD,HAEDC590
.
          MATCH     ANSY,AEDCSUBA           * using sub-analysis
          IF        @EQUAL
            MATCH     SP1,KEY1                * has a code been entered
            IF        !@EQUAL
              PACK      KEY3,AEDCCODE,KEY1
              CALL      RDAEGDL1
              BRANCH    OVRCD,HAEDC590          * invalid link
            ENDIF
          ELSE
            PACK      KEY3,AEDCCODE,SP1         * clear link code
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      HAEDC999
.
. ------- (N)ext screen selected -------
.
HAEDC700  DISPLAY   *P1:6,*EF;
          MOVE      SIX,CVERT               * first display row 
          MOVE      ONE,CCOL                * get back to LHS of screen
          MOVE      ZERO,AEDCQCNT           * reset count
          ADD       ONE,CPAGENO             * add to page count
          GOTO      HAEDC300                * display the data
.
HAEDC999  RETURN
+
.*********************************************************************
.         keyin 
.*********************************************************************
HAEDCK00  DISPLAY   *P1:24,*EL,"Select item, (":
                    *V2LON,ANSE,*HOFF,")xit";
          MOVE      "19",CCOL
.
          IF        AEDCQLIN = ONE | AEDCQLIN = TWO
            DISPLAY   ", (",*V2LON,ANSN,*HOFF,")ext";
            ADD       EIGHT,CCOL
          ENDIF
          IF        AEDCQLIN = ONE | AEDCQLIN = THREE
            DISPLAY   ", (",*V2LON,ANSF,*HOFF,")irst";
            ADD       NINE,CCOL
          ENDIF
.
          DISPLAY   " : ";
          ADD       FOUR,CCOL
.
HAEDCK10  KEYIN     *PCCOL:24,*V2LON,*JR,DIM2
          PACK      DIM2,DIM2,SP2
          TYPE      DIM2
          GOTO      HAEDCK50 IF EQUAL       * number entered
.
          UNPACK    DIM2,ANS,ANS
          REP       UPPLOW,ANS
          MOVE      ZERO,EXIT
          REP       "E1F2N3",ANS
          MOVE      ANS,EXIT
          TYPE      ANS
          GOTO      HAEDCK99 IF EQUAL       * valid number
.
HAEDCK20  BEEP
          GOTO      HAEDCK10
.
HAEDCK50  MOVE      DIM2,FORM2
          MOVE      ZERO,EXIT
          COMPARE   FORM2,ZERO
          GOTO      HAEDCK20 IF NOT LESS    * negative invalid
          COMPARE   FORM2,AEDCQCNT
          GOTO      HAEDCK20 IF LESS        * too high
.
HAEDCK99  RETURN
+
