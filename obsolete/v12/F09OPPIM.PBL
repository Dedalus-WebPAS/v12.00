.*****************************************************************************
.*    Program      : F09OPPIM                                                *
.*    Description  : Conversion oprpimaf to new File layout                  *
.*                                                                           *
.*    Author       : Steve Armstrong                                         *
.*    Date         : 18/01/2017                                              *
.*    Modifications:                                                         *
.*        V10.09.02 28/02/2017  Davin            TSK 0829089                 *
.*                  Added OPPIUDAT,OPPIUTIM,OPPIUUID to New file             *
.*        V10.09.01 14/02/2017  J.Tan            TSK 0829089                 *
.*                  Added index 3 to New file                                *
.*        V10.09.00 18/01/2017  Steve Armstrong  TSK 0829089                 *
.*                  Created program.                                         *
.*****************************************************************************
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
LOGFILE   FILE      ASCII, FIXED=256
.
.         Old File Definition
.
OLDFILE1   IFILE    SQL, FIXED=362, KEY="U1-10,11-11,12-13"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.OPPIUNID  DIM       10     10       1     Unique ID.
.OPPITMNO  DIM       1      1        11    Team Number
.DOPPICNT  DIM       2      2        12    Counter
.OPPIICDE  DIM       15     15       14    EAN Number/Implant Code
.OPPIIQTY  FORM      3      3        29    Implant Quantity (from oprdecaf)
.OPPILTSN  DIM       20     20       32    Lot/Serial Number
.OPPICOST  DIM       10     10       52    Cost Code
.OPPILOCA  DIM       3      3        62    Location  (category Ow)
.OPPIUC01  DIM       3      3        65    User Defined Code 1  (category oE)
.OPPIUC02  DIM       3      3        68    User Defined Code 2  (category oF)
.OPPIUC03  DIM       3      3        71    User Defined Code 3  (category oG)
.OPPIUC04  DIM       3      3        74    User Defined Code 4  (category oH)
.OPPIUC05  DIM       3      3        77    User Defined Code 5  (category oI)
.OPPIUC06  DIM       3      3        80    User Defined Code 6  (category oJ)
.OPPIUC07  DIM       3      3        83    User Defined Code 7  (category oK)
.OPPIUC08  DIM       3      3        86    User Defined Code 8  (category oL)
.OPPIUC09  DIM       3      3        89    User Defined Code 9  (category oM)
.OPPIUC10  DIM       3      3        92    User Defined Code 10 (category oN)
.OPPIUC11  DIM       3      3        95    User Defined Code 11 (category oO)
.OPPIUC12  DIM       3      3        98    User Defined Code 12 (category oP)
.OPPIUC13  DIM       3      3        101   User Defined Code 13 (category oQ)
.OPPIUC14  DIM       3      3        104   User Defined Code 14 (category oR)
.OPPIUC15  DIM       3      3        107   User Defined Code 15 (category oS)
.OPPIUC16  DIM       3      3        110   User Defined Code 16 (category oT)
.OPPIUC17  DIM       3      3        113   User Defined Code 17 (category oU)
.OPPIUC18  DIM       3      3        116   User Defined Code 18 (category oV)
.OPPIUC19  DIM       3      3        119   User Defined Code 19 (category oW)
.OPPIUC20  DIM       3      3        122   User Defined Code 20 (category oX)
.OPPIUY01  DIM       1      1        125   User Defined Y/N 1
.OPPIUY02  DIM       1      1        126   User Defined Y/N 2
.OPPIUY03  DIM       1      1        127   User Defined Y/N 3
.OPPIUY04  DIM       1      1        128   User Defined Y/N 4
.OPPIUY05  DIM       1      1        129   User Defined Y/N 5
.OPPIUY06  DIM       1      1        130   User Defined Y/N 6
.OPPIUY07  DIM       1      1        131   User Defined Y/N 7
.OPPIUY08  DIM       1      1        132   User Defined Y/N 8
.OPPIUY09  DIM       1      1        133   User Defined Y/N 9
.OPPIUY10  DIM       1      1        134   User Defined Y/N 10
.OPPIUT01  DIM       8      8        135   User Defined Time Field 1
.OPPIUT02  DIM       8      8        143   User Defined Time Field 2
.OPPIUT03  DIM       8      8        151   User Defined Time Field 3
.OPPIUT04  DIM       8      8        159   User Defined Time Field 4
.OPPIUT05  DIM       8      8        167   User Defined Time Field 5
.OPPIUT06  DIM       8      8        175   User Defined Time Field 6
.OPPIUT07  DIM       8      8        183   User Defined Time Field 7
.OPPIUT08  DIM       8      8        191   User Defined Time Field 8
.OPPIUT09  DIM       8      8        199   User Defined Time Field 9
.OPPIUT10  DIM       8      8        207   User Defined Time Field 10
.OPPIUF01  FORM      6      4        215   User Defined Form Field 1
.OPPIUF02  FORM      6      4        219   User Defined Form Field 2
.OPPIUF03  FORM      6      4        223   User Defined Form Field 3
.OPPIUF04  FORM      6      4        227   User Defined Form Field 4
.OPPIUF05  FORM      6      4        231   User Defined Form Field 5
.OPPIUF06  FORM      6      4        235   User Defined Form Field 6
.OPPIUF07  FORM      6      4        239   User Defined Form Field 7
.OPPIUF08  FORM      6      4        243   User Defined Form Field 8
.OPPIUF09  FORM      6      4        247   User Defined Form Field 9
.OPPIUF10  FORM      6      4        251   User Defined Form Field 10
.OPPIDTRT  DIM       6      6        255   DTR Transaction Number (PMMITRAN)
.OPPIDUMM  DIM       1      1        261   No longer used
.OPPIONUM  DIM       7      7        262   Order Number
.OPPIICOS  DIM       15     15       269   Item Cost
.OPPIRCNT  DIM       2      2        284   Replacement Registry Form Counter
.OPPICDAT  DIM       8      8        286   Date Record Created (CCYYMMDD)
.OPPICTIM  DIM       8      8        294   Time Record Created (HH:MM:SS)
.OPPICUID  DIM       10     10       302   WEB User ID Created (websecaf)
.OPPISPAR  DIM       50     50       312   Spare
.
.End of Record                      362
.
.
.         New File Definition
.
OPRPIMA1   IFILE    SQL, FIXED=423, KEY="U1-10,11-11,12-13"
OPRPIMA2   IFILE    SQL, FIXED=423, KEY="U14-28,1-10,11-11,12-13"
OPRPIMA3   IFILE    SQL, FIXED=423, KEY="U312-371,1-10,11-11,12-13"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
OPPIUNID  DIM       10     10       1     Unique ID.
OPPITMNO  DIM       1      1        11    Team Number
DOPPICNT  DIM       2      2        12    Counter
OPPIICDE  DIM       15     15       14    EAN Number/Implant Code
OPPIIQTY  FORM      3      3        29    Implant Quantity (from oprdecaf)
OPPILTSN  DIM       20     20       32    Lot/Serial Number
OPPICOST  DIM       10     10       52    Cost Code
OPPILOCA  DIM       3      3        62    Location  (category Ow)
OPPIUC01  DIM       3      3        65    User Defined Code 1  (category oE)
OPPIUC02  DIM       3      3        68    User Defined Code 2  (category oF)
OPPIUC03  DIM       3      3        71    User Defined Code 3  (category oG)
OPPIUC04  DIM       3      3        74    User Defined Code 4  (category oH)
OPPIUC05  DIM       3      3        77    User Defined Code 5  (category oI)
OPPIUC06  DIM       3      3        80    User Defined Code 6  (category oJ)
OPPIUC07  DIM       3      3        83    User Defined Code 7  (category oK)
OPPIUC08  DIM       3      3        86    User Defined Code 8  (category oL)
OPPIUC09  DIM       3      3        89    User Defined Code 9  (category oM)
OPPIUC10  DIM       3      3        92    User Defined Code 10 (category oN)
OPPIUC11  DIM       3      3        95    User Defined Code 11 (category oO)
OPPIUC12  DIM       3      3        98    User Defined Code 12 (category oP)
OPPIUC13  DIM       3      3        101   User Defined Code 13 (category oQ)
OPPIUC14  DIM       3      3        104   User Defined Code 14 (category oR)
OPPIUC15  DIM       3      3        107   User Defined Code 15 (category oS)
OPPIUC16  DIM       3      3        110   User Defined Code 16 (category oT)
OPPIUC17  DIM       3      3        113   User Defined Code 17 (category oU)
OPPIUC18  DIM       3      3        116   User Defined Code 18 (category oV)
OPPIUC19  DIM       3      3        119   User Defined Code 19 (category oW)
OPPIUC20  DIM       3      3        122   User Defined Code 20 (category oX)
OPPIUY01  DIM       1      1        125   User Defined Y/N 1
OPPIUY02  DIM       1      1        126   User Defined Y/N 2
OPPIUY03  DIM       1      1        127   User Defined Y/N 3
OPPIUY04  DIM       1      1        128   User Defined Y/N 4
OPPIUY05  DIM       1      1        129   User Defined Y/N 5
OPPIUY06  DIM       1      1        130   User Defined Y/N 6
OPPIUY07  DIM       1      1        131   User Defined Y/N 7
OPPIUY08  DIM       1      1        132   User Defined Y/N 8
OPPIUY09  DIM       1      1        133   User Defined Y/N 9
OPPIUY10  DIM       1      1        134   User Defined Y/N 10
OPPIUT01  DIM       8      8        135   User Defined Time Field 1
OPPIUT02  DIM       8      8        143   User Defined Time Field 2
OPPIUT03  DIM       8      8        151   User Defined Time Field 3
OPPIUT04  DIM       8      8        159   User Defined Time Field 4
OPPIUT05  DIM       8      8        167   User Defined Time Field 5
OPPIUT06  DIM       8      8        175   User Defined Time Field 6
OPPIUT07  DIM       8      8        183   User Defined Time Field 7
OPPIUT08  DIM       8      8        191   User Defined Time Field 8
OPPIUT09  DIM       8      8        199   User Defined Time Field 9
OPPIUT10  DIM       8      8        207   User Defined Time Field 10
OPPIUF01  FORM      6      4        215   User Defined Form Field 1
OPPIUF02  FORM      6      4        219   User Defined Form Field 2
OPPIUF03  FORM      6      4        223   User Defined Form Field 3
OPPIUF04  FORM      6      4        227   User Defined Form Field 4
OPPIUF05  FORM      6      4        231   User Defined Form Field 5
OPPIUF06  FORM      6      4        235   User Defined Form Field 6
OPPIUF07  FORM      6      4        239   User Defined Form Field 7
OPPIUF08  FORM      6      4        243   User Defined Form Field 8
OPPIUF09  FORM      6      4        247   User Defined Form Field 9
OPPIUF10  FORM      6      4        251   User Defined Form Field 10
OPPIDTRT  DIM       6      6        255   DTR Transaction Number (PMMITRAN)
OPPIDUMM  DIM       1      1        261   No longer used
OPPIONUM  DIM       7      7        262   Order Number
OPPIICOS  DIM       15     15       269   Item Cost
OPPIRCNT  DIM       2      2        284   Replacement Registry Form Counter
OPPICDAT  DIM       8      8        286   Date Record Created (CCYYMMDD)
OPPICTIM  DIM       8      8        294   Time Record Created (HH:MM:SS)
OPPICUID  DIM       10     10       302   WEB User ID Created (websecaf)
OPPIBCOD  DIM       60     60       312   Barcode
OPPIAIMP  DIM       1      1        372   Ad-hoc Implant
.                                           Blank or 0 = No
.                                           1 = Yes
OPPIUDAT  DIM       8      8        373   Date Record Updated (CCYYMMDD)
OPPIUTIM  DIM       8      8        381   Time Record Updated (HH:MM:SS)
OPPIUUID  DIM       10     10       389   WEB User ID Updated (websecaf)
OPPISPAR  DIM       24     24       399   Spare
.
.End of Record                      423
.
.Redefine Key Variables
.----------------------
OPPICNTR  FORM      2
.
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       200
.
DEFPATH   DIM       60
DIM60     DIM       60
DISNUM    FORM      4
.
ERORTEXT  DIM       80
.
INPFILE   DIM       8
.
MINUS3    FORM      "-3"
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
SAVEFLG   FORM      1
.
USINGORC  DIM       1
.
.
. PROGRAM CONSTANTS
. -----------------
CREATEXT  INIT    "Log file created "
DASHLINE  INIT      "--------------------------------------------------":
                    "------------------------------"
ENDGTEXT  INIT    "Ending   fixit (Option "
OPENTEXT  INIT    "Log file opened "
SP60      INIT    "                                                            "
STARTTXT  INIT    "Starting fixit (Option "
.
.
.
PRGNAM    INIT      "CONVERSION OPRPIMFD"
PRGID     INIT      "F09OPPIM"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
.
          CALL      OLOG0000                      * create/open log file
MAIN0500  CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000              * run c-isam fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9000
.
MAIN2000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN6000:               * yes
                          MAIN0500:               * no
                          MAIN9000                * cancel
.
.         Running c-isam fix
.
MAIN6000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9000
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/zip saved file
.
MAIN9000  CALL      CLOG0000                      * close log file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
          
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run c-isam fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
.
PREP0000  CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
.
          CALL      SLOG0000                      * write start to log file
.
          MOVE      "oprpimaf",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat oprpimaf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "soppimaf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat oprpimaf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "soppimaf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "oprpimaf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Unable to remove the original file.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "soppimaf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            MOVE      "Unable to open saved original file.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "oprpimaf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " 423 UC1-10,11-11,12-13",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC14-28,1-10,11-11,12-13",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC312-371,1-10,11-11,12-13",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      OPRPIMA1,NEWFILE
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          GOTO      PREP9999
.
PREP5000  MOVE      "Old file not found.  ",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  MOVE      "Unable to copy original file.  (Error: ",ERORTEXT
          ENDSET    ERORTEXT
          APPEND    TASKID,ERORTEXT
          APPEND    ")  ",ERORTEXT
          RESET     ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
            MOVE      "File does not exist",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
              MOVE      "File has already been converted",ERORTEXT
              WRITE     LOGFILE,SEQ;ERORTEXT
            ELSE
              DISPLAY   "has caused an IO error - ";
              MOVE      "File has caused an IO Error",ERORTEXT
              WRITE     LOGFILE,SEQ;ERORTEXT
            ENDIF
          ENDIF
          MOVE      "File will be bypassed",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *+,ERORTEXT
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          MOVE      SP20,KEY13
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MOVE      SP70,OPPIBCOD                * load new fields
          MOVE      "0",OPPIAIMP
          MOVE      SP70,OPPIUDAT
          MOVE      SP70,OPPIUTIM
          MOVE      SP70,OPPIUUID
          CALL      WROPPIM1                     * write to new file
.
          GOTO      READ1000                     * get next record
.
READ6000  CLOSE     OPRPIMA1                     * close new file
          CLOSE     OLDFILE1                     * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS:
                    *P1:24;
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
.
VALD0000  MOVE      ZERO,EXIT
.
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            MOVE      "Directory path must end with a slash (/) ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Directory does not exist ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or zip save file                                     *
.**********************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "soppimaf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "gzip -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Saved file not zipped or removed.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:24,*EL,*B,"Finished processing  ",CDATE,SP1,CTIMEIS:
                    ".  ";
          CALL      HITENTER
.
          CALL      ELOG0000                     * write an end log entry
.
ENDP9999  RETURN
+
.**********************************************************************
.*        DEFT0000 -  Get the default directory                       *
.**********************************************************************
.
DEFT0000  EXECUTE   "ldat oprpimaf.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  MOVE      "Error finding 'oprpimaf'.  ",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.*                            OLOG0000                                       *
.*                    Open the log file                                      *
.*****************************************************************************
.
OLOG0000  CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      LOGFILE,"f09oppim.log"       * open log file
          TRAPCLR   IO
          IF        OVRCD = 1
            PREP      LOGFILE,"f09oppim.log"     * create and open log file
            WRITE     LOGFILE,MINUS3;DASHLINE
            WRITE     LOGFILE,SEQ;CREATEXT,CDATE,SP1,CTIMEIS
          ELSE
            WRITE     LOGFILE,MINUS3;DASHLINE
            WRITE     LOGFILE,SEQ;OPENTEXT,CDATE,SP1,CTIMEIS
          ENDIF
.
OLOG9999  RETURN
+
.*****************************************************************************
.*                            CLOG0000                                       *
.*                    Close the log file                                     *
.*****************************************************************************
.
CLOG0000  CLOSE     LOGFILE
.
CLOG9999  RETURN
+
.*****************************************************************************
.*                            SLOG0000                                       *
.*                    Write a start log to the log file                      *
.*****************************************************************************
.
SLOG0000  WRITE     LOGFILE,SEQ;DASHLINE
          WRITE     LOGFILE,SEQ;STARTTXT,COPTION,RBRACK,SP1,CDATE,SP1:
                                   CTIMEIS
.
SLOG9999  RETURN
+
.*****************************************************************************
.*                            ELOG0000                                       *
.*                    Write an end log to the log file                       *
.*****************************************************************************
.
ELOG0000  WRITE     LOGFILE,SEQ;ENDGTEXT,COPTION,RBRACK,SP1,CDATE,SP1:
                                   CTIMEIS
ELOG9999  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
.         Old File I/O Routines
.
READSOLD  RESET     KEY13
          READ      OLDFILE1,KEY13;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;OPPIUNID,OPPITMNO,DOPPICNT,OPPIICDE,OPPIIQTY:
                        OPPILTSN,OPPICOST,OPPILOCA,OPPIUC01,OPPIUC02,OPPIUC03:
                        OPPIUC04,OPPIUC05,OPPIUC06,OPPIUC07,OPPIUC08,OPPIUC09:
                        OPPIUC10,OPPIUC11,OPPIUC12,OPPIUC13,OPPIUC14,OPPIUC15:
                        OPPIUC16,OPPIUC17,OPPIUC18,OPPIUC19,OPPIUC20,OPPIUY01:
                        OPPIUY02,OPPIUY03,OPPIUY04,OPPIUY05,OPPIUY06,OPPIUY07:
                        OPPIUY08,OPPIUY09,OPPIUY10,OPPIUT01,OPPIUT02,OPPIUT03:
                        OPPIUT04,OPPIUT05,OPPIUT06,OPPIUT07,OPPIUT08,OPPIUT09:
                        OPPIUT10,OPPIUF01,OPPIUF02,OPPIUF03,OPPIUF04,OPPIUF05:
                        OPPIUF06,OPPIUF07,OPPIUF08,OPPIUF09,OPPIUF10,OPPIDTRT:
                        OPPIDUMM,OPPIONUM,OPPIICOS,OPPIRCNT,OPPICDAT,OPPICTIM:
                        OPPICUID,OPPISPAR
          GOTO      OVERCOND IF OVER
          MOVE      DOPPICNT,OPPICNTR
          RETURN
.
.         New File I/O Routines
.
WROPPIM1  RESET     KEY13
          MOVE      OPPICNTR,DOPPICNT
          WRITE     OPRPIMA1,KEY13;OPPIUNID,OPPITMNO,DOPPICNT,OPPIICDE,OPPIIQTY:
                        OPPILTSN,OPPICOST,OPPILOCA,OPPIUC01,OPPIUC02,OPPIUC03:
                        OPPIUC04,OPPIUC05,OPPIUC06,OPPIUC07,OPPIUC08,OPPIUC09:
                        OPPIUC10,OPPIUC11,OPPIUC12,OPPIUC13,OPPIUC14,OPPIUC15:
                        OPPIUC16,OPPIUC17,OPPIUC18,OPPIUC19,OPPIUC20,OPPIUY01:
                        OPPIUY02,OPPIUY03,OPPIUY04,OPPIUY05,OPPIUY06,OPPIUY07:
                        OPPIUY08,OPPIUY09,OPPIUY10,OPPIUT01,OPPIUT02,OPPIUT03:
                        OPPIUT04,OPPIUT05,OPPIUT06,OPPIUT07,OPPIUT08,OPPIUT09:
                        OPPIUT10,OPPIUF01,OPPIUF02,OPPIUF03,OPPIUF04,OPPIUF05:
                        OPPIUF06,OPPIUF07,OPPIUF08,OPPIUF09,OPPIUF10,OPPIDTRT:
                        OPPIDUMM,OPPIONUM,OPPIICOS,OPPIRCNT,OPPICDAT,OPPICTIM:
                        OPPICUID,OPPIBCOD,OPPIAIMP,OPPIUDAT,OPPIUTIM,OPPIUUID:
                        OPPISPAR
          RETURN
.
          INC       STD001IO
