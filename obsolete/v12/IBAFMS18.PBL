. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS18                                             *
. * Desc      :   Report Code Maintenance                              *
. **********************************************************************
. * Date      :   12.09.1990                                           *
. * Author    :   Neeriem Dye                                          *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
.$$$$$$
.        PROGRAM IBAFMS18
.             open files and initialise variables
.             WHILE user wants to add/modify/delete DO
.                  get desired option (add, modify or delete)
.                  CASE option OF
.                       ADD     : WHILE user wants to add records DO
.                                      accept initial values
.                                      WHILE user wants modify screen data DO
.                                           get number of item to be modified
.                                           allow user to modify option
.                                      END WHILE
.                                      IF user wants to post data
.                                           THEN write new data
.                                      END IF
.                                 END WHILE
.                       MODIFY  : WHILE user wants to modify records DO
.                                      get and display desired record
.                                      WHILE user wants modify screen data DO
.                                           get number of item to be modified
.                                           allow user to modify option
.                                      END WHILE
.                                      IF user wants to post data
.                                           THEN update data
.                                      END IF
.                                 END WHILE
.                       DELETE  : WHILE user wants to delete records DO
.                                      get and display desired record
.                                      IF user wants to delete data
.                                           THEN delete data
.                                      END IF
.                                 END WHILE
.                  END CASE
.             END WHILE
.        END
.$$$$$$
. Standard FMS Accounting System Include
.---------------------------------------
          INC       FMSSTDDF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
          INC       FMSLMBFD/INC
          INC       FMSRSFFD/INC
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
.
ACCEPT    FORM      1                  * accept mode (1=on)
.
PRGID     INIT      "IBAFMS18"
PRGNAM    INIT      "Report Code Maintenance"
VERSION   INIT      "V12.00.00"
.
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML0100    CALL      SELOPTN            * insert/modify/delete/exit ?
          COMPARE   ZERO,COPTION
          GOTO      ML9999 IF EQUAL    * exit program
.
ML0200    CALL      PROC0000           * get entry
          BRANCH    EXIT,ML0100,ML0100 * exit option
.
          LOAD      ACCEPT,COPTION,ONE,ZERO,ZERO * set up mode
          CALL      SCRA0000           * display screen A
          BRANCH    EXIT,ML0200        * if cannot delete as rep. seq's exist
.
          BRANCH    COPTION,ML1000,ML1000,ML2000
.
.---- add/modify data ----
.
ML1000    CALL      SELA0000           * modify screen A
          BRANCH    EXIT,ML0200        * don't save data
.
          CALL      POST0000           * post screen A data
          GOTO      ML0200             * get next entry
.
.---- delete data ----
.
ML2000    CALL      DELQST             * ok to delete (y/n/c) ?
          BRANCH    CEXIT,ML2100,ML0200,ML0100
.
ML2100    CALL      DELT0000           * delete screen A data
          GOTO      ML0200             * get next entry
.
.
ML9999    CHAIN     PGM                * chain back to menu
.
.******************************************************************************
.* INIT - Open Files                             Called by ML0000             *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening fmslmbaf";  * Report Code file
          OPEN      FMSLMBA1,"fmslmbaf"
          DISPLAY   *P64:24,"fmsrsfaf";  * Report Sequence File
          OPEN      FMSRSFA1,"fmsrsfaf"          
.
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.******************************************************************************
.* SCRA - Display Screen A                       Called by SELA0000, redisps  *
.*       Requires : ACCEPT   (1=accept mode)                                  *
.******************************************************************************
SCRA0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:4,"    Report Code          : ",*V2LON,FMLBRPNO,*EF:
                    *P1:6," 1",*HOFF,". Description          : ";
          BRANCH    ACCEPT,SCRA9999    * accept mode
.
          DISPLAY   *P28:6,*V2LON,FMLBDESC
.
.-------> Start SRF126657
          IF        COPTION=3
            PACK      KEY26,FMLBRPNO,SP70
            CALL      RSFMRF1
            CALL      RKFMRF1
            BRANCH    OVRCD,SCRA9999
            MATCH     FMLBRPNO,FMRFREP
            GOTO      SCRA9999 IF NOT EQUAL
            BEEP
            DISPLAY   *P1:22,*EL,"WARNING - Report Sequences ":
                      "exist for this Report.  ";
            DISPLAY   *P1:24,*EL,"This Will DELETE All Report Details - ";
            CALL      HITENTER
          ENDIF
.-------> End   SRF126657

SCRA9999  RETURN
.******************************************************************************
.* DEFV - set up default variables               Called by SELA0000           *
.******************************************************************************
DEFV0000  MOVE      UNDLN70,FMLBDESC   * set description
.
DEFV9999  RETURN
.******************************************************************************
.* PROC - Enter Report Code                      Called by ML0000             *
.*        Returns : EXIT     (2=EXITCHAR entered, 1=nothing entered, 0=cont.) *
.*                  FMLBDISS (variable entered)                               *
.******************************************************************************
PROC0000  DISPLAY   *P1:4,"    Report Code          : ",*EF;
.
          MOVE      "28",CCOL          * set up screen address
          MOVE      "4",CVERT
.
          COMPARE   ONE,COPTION        * insert mode ?
          CALL      ALMBFM00 IF EQUAL
.
          COMPARE   ONE,COPTION        * not insert mode ?
          CALL      KLMBFM00 IF NOT EQUAL
.
PROC9999  RETURN
.******************************************************************************
.* SELA - Modify screen A data                   Called by ML0000             *
.*        Returns : EXIT     (2=EXITCHAR entered, 1=nothing entered, 0=cont.) *
.*                  FMLBDISS (variable entered)                               *
.******************************************************************************
SELA0000  MOVE      ZERO,CCITEM
.
          COMPARE   ONE,ACCEPT
          CALL      DEFV0000 IF EQUAL            * set up default values
.
.---- determine option to execute ----
.
SELA0100  ADD       ONE,CCITEM                   * get next option
          BRANCH    ACCEPT,SELA0500              * in accept mode ?
          CALL      MAINQST                      * select/post/cancel ?
.
          COMPARE   ZERO,CCITEM
          GOTO      SELA9000 IF EQUAL            * post ?
.
          COMPARE   SEQ,CCITEM
          GOTO      SELA9500 IF EQUAL            * cancel ?
.
SELA0500  MOVE      "28",CCOL                    * set up screen address
          MOVE      "5",CVERT
          ADD       CCITEM,CVERT
          BRANCH    CCITEM,SELA1100              * execute option
.
          BRANCH    ACCEPT,SELA0900              * finished accept mode ?
          BEEP                                   * illegal option
.
SELA0900  MOVE      ZERO,ACCEPT                  * clear accept mode
          GOTO      SELA0100                     * get next option
.
.---- option 1 ----
.
SELA1100  MATCH     FMLBDESC,SP70
          GOTO      SELA1150 IF NOT EQUAL        * description spaces ?
.
          PACK      FMLBDESC,UNDLN70
.
SELA1150  KEYIN     *PCCOL:CVERT,*DV,FMLBDESC:   * read in description
                    *PCCOL:CVERT,*V2LON,*RV,FMLBDESC;
          PACK      FMLBDESC,FMLBDESC,SP70
.
          MATCH     FMLBDESC,SP70
          GOTO      SELA1100 IF EQUAL            * description spaces ?
          MATCH     FMLBDESC,UNDLN70
          GOTO      SELA9500 IF EQUAL            * nothing entered ?
          MATCH     FMLBDESC,EXITCHAR
          GOTO      SELA9500 IF EQUAL            * EXITCHAR entered ?
.
          DISPLAY   *PCCOL:CVERT,*V2LON,FMLBDESC;
          GOTO      SELA0100
.
.---- exit points ----
.
SELA9000  MOVE      ZERO,EXIT                    * post
          GOTO      SELA9999
.
SELA9500  MOVE      ONE,EXIT                     * cancel
.
SELA9999  RETURN
.******************************************************************************
.* POST - Post data                              Called by ML0000             *
.******************************************************************************
POST0000  PACK      KEY2,FMLBRPNO,SP70
          CALL      RAFMLB1
          IF        OVRCD=0
            CALL      UPFMLB1                 * update
          ELSE
            CALL      WRFMLB1                 * write
          ENDIF
.
POST9999  RETURN
.******************************************************************************
.* DELT - Delete data                            Called by ML0000             *
.******************************************************************************
DELT0000  PACK      KEY2,FMLBRPNO,SP70
          CALL      DEFMLB1                 * delete old record (if possible)
.
. Cascade Delete Report Sequence File
.
          DISPLAY   *P1:24,*EL,"Deleting Report Sequence Details ..."
          PACK      KEY26,FMLBRPNO,SP70
          CALL      RSFMRF1
DELT1000  CALL      RKFMRF1
          BRANCH    OVRCD,DELT9999
          MATCH     FMLBRPNO,FMRFREP
          GOTO      DELT9999 IF NOT EQUAL
          PACK      KEY26,FMRFREP,FMRFPOS,FMRFSEQ,FMRFLED,FMRFACC
          CALL      DEFMRF1
          CALL      RDFMRF1
          GOTO      DELT1000
.
DELT9999  RETURN
.******************************************************************************
.* INCLUDES FOR I/O'S                                                         *
.******************************************************************************
RLMBFM00  
          DISPLAY   *P1:4,"    Report Code          : ",*EF;
          RETURN
.
          INC       FMSLMBIO/INC       * Report Code
          INC       FMSRSFIO/INC
.
          INC       FMSLMBKY           * Report Code
          INC       FMSSTDCD           * FMS Acc. standard routines
