. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS38                                             *
. * Desc      :   Bank Account Listing                                 *
. **********************************************************************
. * Date      :   20.02.1991                                           *
. * Author    :   Neeriem Dye                                          *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
.$$$$$$
.        PROGRAM IBAFMS38
.             open files and initialise variables
.             WHILE user wants to print reports DO
.                  set line pointer to end of page
.                  FOR each record on file DO
.                       IF at end of page 
.                            THEN print header and reset line pointer
.                       print record
.                  END FOR
.                  IF nothing printed
.                       THEN print header and reset line pointer
.                  print end of report message
.             END WHILE
.        END
.$$$$$$
. Standard Accounting Include
.----------------------------
          INC       FMSSTDDF
          INC       FMSCHQFD/INC       * Cheque Accounts
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
.
ALAB      INIT      "            "                * labels for screen A
ALAB1     INIT      "Print Report"
DIM3A     DIM       3
DIM3B     DIM       3
.
OPTIONA   FORM      1        * option on screen A
.
PRGID     INIT      "IBAFMS38"
PRGNAM    INIT      "Cheque Account Listing"
VERSION   INIT      "V12.00.00"
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      SELA0000      * display screen A and get "report by" option
          BRANCH    EXIT,ML9999   * user wants to exit screen
.
          CALL      PRNT0000      * print report
          GOTO      ML1000        * get another option
.
ML9999    CHAIN     PGM
.******************************************************************************
.* INIT - Open Files                                                          *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening fmschqaf";  * Report Code
          OPEN      FMSCHQA1,"fmschqaf"
.
          DISPLAY   *P1:24,*EL;
.
INIT9999  RETURN
.**********************************************************************
. SELA -  display screen A and get "print by" option   Called By ML
.         Returns : EXIT       (0=continue, 1=quit)
.**********************************************************************
SELA0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = ",ALAB1:
                    *P6:7,"Select Option :";
.
SELA1000  KEYIN     *P22:7,"_",*EL,*P22:7,*V2LON,OPTIONA; * read option
          COMPARE   OPTIONA,ZERO
          GOTO      SELA9500 IF EQUAL            * user wants to exit ?
.
          PACK      ALAB,SP70                    * clear screen label
          LOAD      ALAB,OPTIONA,ALAB1           * get screen label
.
          MATCH     SP70,ALAB
          GOTO      SELA9000 IF NOT EQUAL        * legal option ?
.
          BEEP                                   * illegal option
          GOTO      SELA1000                     * get another option
.
SELA9000  MOVE      ZERO,EXIT                    * continue
          GOTO      SELA9999
.
SELA9500  MOVE      ONE,EXIT                     * quit
.
SELA9999  RETURN
.********************************************************************
. PRNT - print report                               Called by ML
.********************************************************************
PRNT0000  DISPLAY   *P50:24,"Printing :   ";
          MOVE      ZERO,CPAGENO                 * reset page no.
          CLOCK     TIME,CTIMEIS                 * get current time
          MOVE      "999",CLNO                   * init last line number
.
          CALL      FILE0000                    * go to correct position in file
.
PRNT1000  CALL      NREC0000                     * get next record
          BRANCH    OVRCD,PRNT9000               * end of file ?
.
          DISPLAY   *P61:24,*V2LON,FMCHCHQ,*EL;
          COMPARE   "55",CLNO                    * end of page ?
          CALL      HEAD0000 IF NOT LESS
.
          CALL      WRIT0000                     * print details
          GOTO      PRNT1000                     * get next record
.
PRNT9000  COMPARE   ZERO,CPAGENO
          CALL      HEAD0000 IF EQUAL          * print header if nothing printed
          CALL      UND80                       * print line
          PRINT     "****   End of Report   ****"
.
PRNT9999  RETURN
.**********************************************************************
.  FILE - Set file pointer                             Called By PRNT
.**********************************************************************
FILE0000  
.
.---- print by type 1 ----
.
FILE0100  PACK      KEY2,SP70
          CALL      RSFMCH1
          GOTO      FILE9999
.
FILE9999  RETURN
.**********************************************************************
.  NREC - Get next record                              Called By PRNT
.**********************************************************************
NREC0000  
.
.---- print by type 1 ----
.
NREC0100  CALL      RKFMCH1
          BRANCH    OVRCD,NREC9500
          GOTO      NREC9000
.
NREC9000  MOVE      ZERO,OVRCD         * continue
          GOTO      NREC9999
.
NREC9500  MOVE      ONE,OVRCD          * quit
.
NREC9999  RETURN
.********************************************************************
. HEAD - print report                               Called by PRNT
.********************************************************************
HEAD0000  CALL      HEAD80                      * print heading
.
          CALL      UND80                       * print line
          PRINT     "|  Cheque Account   |  Description     ":
                    "                          | B.S.B. Code |"
          ADD       "1",CLNO
          CALL      UND80                       * print line
.
HEAD9999  RETURN
.********************************************************************
. WRIT - print data line                            Called by PRNT
.********************************************************************
WRIT0000  UNPACK    FMCHBSB,DIM3A,DIM3B
          PRINT     "|  ",FMCHCHQ,"  |  ",FMCHDES,"  |  ",DIM3A,"-",DIM3B:
                    *80,"|"
          ADD       "1",CLNO           * add one to line number
.
WRIT9999  RETURN
.******************************************************************************
.  INCLUDES FOR I/O'S
.******************************************************************************
.---------------------------------
. Standard Accounting Code include
.---------------------------------
          INC       FMSSTDCD
          INC       FMSCHQIO/INC       * Report Code
.
