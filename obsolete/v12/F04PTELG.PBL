.*****************************************************************************
.*    Program      : F04PTELG                                                *
.*    Description  : Conversion patelgaf to new File layout                  *
.*                                                                           *
.*    Author       : Steve Armstrong  CAR 292257                             *
.*    Date         : 26/02/2014                                              *
.*    Modifications:                                                         *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
.
.
. Old File Definition
.
OLDFILE1   IFILE    SQL, FIXED=655, KEY="U1-8"                                  
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.PTELVISN DIM       8      8        1     Visit Number
.PTELFINC DIM       1      1        9     Financial Y/N (1-Yes)
.PTELXCSS FORM      12.2   8        10    Excess for this Admission
.PTELPPRI FORM      12.2   8        18    Co-payments Private room
.PTELPSHA FORM      12.2   8        26    Co-payments Shared room
.PTELPTHE FORM      12.2   8        34    Theatre Co-payments
.PTELXCLS DIM       1      1        42    Exclusions exist Y/N (1-Yes)
.PTELCMPL DIM       1      1        43    Check Complete Y/N (1-Yes)
.PTELCOVR DIM       1      1        44    Covered for this Procedure Y/N
.PTELQEXS DIM       1      1        45    Quote Exists Y/N (1-Yes)
.PTELQACC DIM       1      1        46    Quote Accepted Y/N (1-Yes)
.PTELCDON DIM       3      3        47    Check done via (Catg EG)
.PTELRDAT DIM       8      8        50    Request date (CCYYMMDD)
.PTELSPEA DIM       1      1        58    Subject to Pre-Existing Ailment
.                                         0=No, 1=Yes, 2=Query
.PTELHFRN DIM       20     20       59    Health Fund Reference Number
.PTELPTHF DIM       25     25       79    Person at Health Fund provided Info
.PTELXCMN DIM       100    100      104   Exclusion Comments
.PTELPPUT FORM      5.2    5        204   Private Dat Co-payment Up To
.PTELPSUT FORM      5.2    5        209   Shared Day Co-payment Up To
.PTELPTUT FORM      5.2    5        214   Theatre Day Co-payment Up To
.PTELECMN DIM       200    200      219   Eligibility Comments
.PTELHSTM DIM       25     25       419   Hospital Staff Member
OLDHFPN   DIM       25     25       444   Health Fund Policy Name
.PTELPSDY FORM      12.2   8        469   Co-payment required - Same day
.PTELPSDU FORM      5.2    5        477   Same day Co-payment up to
.PTELPHSP FORM      12.2   8        482   Co-payment required - Hospital
.PTELPHSU FORM      5.2    5        490   Hospital Co-payment up to
.PTELAFRM DIM       1      1        495   Accident Form? Y/N (1=Yes)
.PTELQLPS DIM       1      1        496   Qualifying Period Served? (1=Yes)
.PTELMDES DIM       70     70       497   Miscellaneous Description
.PTELMAMT FORM      12.2   8        567   Miscellaneous $ Value
.PTELEADM DIM       1      1        575   Emergency Admission (1-Yes)
.PTELCPCP DIM       1      1        576   Calculate Private Co-payment
.PTELCSCP DIM       1      1        577   Calculate Shared Co-payment
.PTELCDCP DIM       1      1        578   Calculate Same Day Co-payment
.PTELCHCP DIM       1      1        579   Calculate Hospital Co-payment
.PTELCTCP DIM       1      1        580   Calculate Theatre Co-payment
.PTELTOOP FORM      12.2   8        581   Total Out Of Pockets
OLDSPARE  DIM       66     66       589   Spare Variable
.
.End of Record                      655
.
.
. New File Definition
.
PATELGA1   IFILE    SQL, FIXED=694, KEY="U1-8"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
PTELVISN  DIM       8      8        1     Visit Number
PTELFINC  DIM       1      1        9     Financial Y/N (1-Yes)
PTELXCSS  FORM      12.2   8        10    Excess for this Admission
PTELPPRI  FORM      12.2   8        18    Co-payments Private room
PTELPSHA  FORM      12.2   8        26    Co-payments Shared room
PTELPTHE  FORM      12.2   8        34    Theatre Co-payments
PTELXCLS  DIM       1      1        42    Exclusions exist Y/N (1-Yes)
PTELCMPL  DIM       1      1        43    Check Complete Y/N (1-Yes)
PTELCOVR  DIM       1      1        44    Covered for this Procedure Y/N
PTELQEXS  DIM       1      1        45    Quote Exists Y/N (1-Yes)
PTELQACC  DIM       1      1        46    Quote Accepted Y/N (1-Yes)
PTELCDON  DIM       3      3        47    Check done via (Catg EG)
PTELRDAT  DIM       8      8        50    Request date (CCYYMMDD)
PTELSPEA  DIM       1      1        58    Subject to Pre-Existing Ailment
.                                         0=No, 1=Yes, 2=Query
PTELHFRN  DIM       20     20       59    Health Fund Reference Number
PTELPTHF  DIM       25     25       79    Person at Health Fund provided Info
PTELXCMN  DIM       100    100      104   Exclusion Comments
PTELPPUT  FORM      5.2    5        204   Private Dat Co-payment Up To
PTELPSUT  FORM      5.2    5        209   Shared Day Co-payment Up To
PTELPTUT  FORM      5.2    5        214   Theatre Day Co-payment Up To
PTELECMN  DIM       200    200      219   Eligibility Comments
PTELHSTM  DIM       25     25       419   Hospital Staff Member
PTELHFPN  DIM       80     80       444   Health Fund Policy Name
PTELPSDY  FORM      12.2   8        524   Co-payment required - Same day
PTELPSDU  FORM      5.2    5        532   Same day Co-payment up to
PTELPHSP  FORM      12.2   8        537   Co-payment required - Hospital
PTELPHSU  FORM      5.2    5        545   Hospital Co-payment up to
PTELAFRM  DIM       1      1        550   Accident Form? Y/N (1=Yes)
PTELQLPS  DIM       1      1        551   Qualifying Period Served? (1=Yes)
PTELMDES  DIM       70     70       552   Miscellaneous Description
PTELMAMT  FORM      12.2   8        622   Miscellaneous $ Value
PTELEADM  DIM       1      1        630   Emergency Admission (1-Yes)
PTELCPCP  DIM       1      1        631   Calculate Private Co-payment
PTELCSCP  DIM       1      1        632   Calculate Shared Co-payment
PTELCDCP  DIM       1      1        633   Calculate Same Day Co-payment
PTELCHCP  DIM       1      1        634   Calculate Hospital Co-payment
PTELCTCP  DIM       1      1        635   Calculate Theatre Co-payment
PTELTOOP  FORM      12.2   8        636   Total Out Of Pockets
PTELSPAR  DIM       50     50       644   Spare Variable
.
.End of Record                      694
.
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       200
.
DEFPATH   DIM       60
DIM60     DIM       60
DISNUM    FORM      4
.
INPFILE   DIM       8
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
SAVEFLG   FORM      1
.
.
. PROGRAM CONSTANTS
. -----------------
SP60      INIT    "                                                            "
.
.
.
PRGNAM    INIT      "CONVERSION PATELGFD"
PRGID     INIT      "F04PTELG"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000              * run c-isam fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN0000:               * no
                          MAIN9999                * cancel
.
.         Running c-isam fix
.
MAIN5000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/zip saved file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
          
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run c-isam fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
.
PREP0000  MOVE      "patelgaf",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat patelgaf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sptelgaf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat patelgaf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sptelgaf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "patelgaf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "sptelgaf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "patelgaf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " 694 UC1-8",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      PATELGA1,NEWFILE
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
            ELSE
              DISPLAY   "has caused an IO error - ";
            ENDIF
          ENDIF
          DISPLAY   "File will be bypassed"
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY8,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          PACK      PTELHFPN,OLDHFPN,SP70,SP70
          MOVE      SP70,PTELSPAR
.
          CALL      WRPTELG1                      * write to new file
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     PATELGA1                      * close new file
          CLOSE     OLDFILE1                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS:
                    *P1:24;
          CALL      HITENTER
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
.
VALD0000  MOVE      ZERO,EXIT
.
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or zip save file                                     *
.**********************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "sptelgaf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "gzip ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not zipped or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:24,*EL,*B,"Finished processing  ",CDATE,SP1,CTIMEIS:
                    ".  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.*        DEFT0000 -  Get the default directory                       *
.**********************************************************************
.
DEFT0000  EXECUTE   "ldat patelgaf.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding 'patelgaf'.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
.         Old I/O Routines
.
READSOLD  RESET     KEY8
          READ      OLDFILE1,KEY8;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;PTELVISN,PTELFINC,PTELXCSS,PTELPPRI,PTELPSHA:
                        PTELPTHE,PTELXCLS,PTELCMPL,PTELCOVR,PTELQEXS,PTELQACC:
                        PTELCDON,PTELRDAT,PTELSPEA,PTELHFRN,PTELPTHF,PTELXCMN:
                        PTELPPUT,PTELPSUT,PTELPTUT,PTELECMN,PTELHSTM,OLDHFPN:
                        PTELPSDY,PTELPSDU,PTELPHSP,PTELPHSU,PTELAFRM,PTELQLPS:
                        PTELMDES,PTELMAMT,PTELEADM,PTELCPCP,PTELCSCP,PTELCDCP:
                        PTELCHCP,PTELCTCP,PTELTOOP,OLDSPARE
          GOTO      OVERCOND IF OVER
          RETURN
.
.         New I/O Routines
.
WRPTELG1  RESET     KEY8
          WRITE     PATELGA1,KEY8;PTELVISN,PTELFINC,PTELXCSS,PTELPPRI,PTELPSHA:
                        PTELPTHE,PTELXCLS,PTELCMPL,PTELCOVR,PTELQEXS,PTELQACC:
                        PTELCDON,PTELRDAT,PTELSPEA,PTELHFRN,PTELPTHF,PTELXCMN:
                        PTELPPUT,PTELPSUT,PTELPTUT,PTELECMN,PTELHSTM,PTELHFPN:
                        PTELPSDY,PTELPSDU,PTELPHSP,PTELPHSU,PTELAFRM,PTELQLPS:
                        PTELMDES,PTELMAMT,PTELEADM,PTELCPCP,PTELCSCP,PTELCDCP:
                        PTELCHCP,PTELCTCP,PTELTOOP,PTELSPAR
          RETURN
.
          INC       STD001IO
