.******************************************************************************
.* System         : Clinical Audits                                           *
.* Program        : IBACLA07.PBL                                              *
.* Name           : Codes file Enquiry                                        *
.******************************************************************************
.* Date           : 11/07/1992                                                *
.* Author         : Justin Coates                                             *
.* Function       : To enquire upon codes in the codes file (CLACODFD)        *
.* Modifications  :                                                           *
.******************************************************************************
.
          INC       STD001FD/PBL
          INC       CLACODFD/INC            * codes file
.
.         program variables
.
CALLPOSN  FORM      1             * which line 24 prompt
CNTITEM   FORM      2             * counter
ENCAT     DIM       2             * category to enquire on
ENDESC    DIM       20            * category description
PAGENO    FORM      2             * display page number
.
PRGID     INIT      "IBACLA07"
PRGNAM    INIT      "Codes File Enquiry"
VERSION   INIT      "V12.00.00"
+
.*********************************************************************
.                   ML0000
.         MAINLINE CODE / CONTROLLING LOGIC
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      KCAT0000                * enter category
          BRANCH    EXIT,ML9999             * nothing entered
.
ML2000    CALL      DCOD0000                * display codes
          BRANCH    EXIT,ML1000,ML9999      * re-enter cat/exit
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.                   INIT0000                    Called by : ML0000
.         open files
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"opening"
.
          DISPLAY   *P58:24,"clacodaf"
          OPEN      CLACODA1,"clacodaf"
.
INIT9999  RETURN
+
.*********************************************************************
.                   KCAT0000                    Called by : xxxx0000 
.         enter the category
.*********************************************************************
KCAT0000  DISPLAY   *P1:3,*EF:
                    *P5:4,"Category : "
.
KCAT1000  KEYIN     *P16:4,*DV,UNDLN2:
                    *P16:4,*V2LON,ENCAT
          PACK      ENCAT,ENCAT,SP2
          MATCH     SP2,ENCAT
          GOTO      KCAT9100 IF EQUAL       * nothing entered
.
          PACK      KEY5,ENCAT,SP3
          CALL      RDCACOD1
          BRANCH    OVRCD,KCAT7000          * invalid cat.
.
          DISPLAY   *P25:4,CACDDESC
          MOVE      CACDDESC,ENDESC
          MOVE      ZERO,EXIT
          GOTO      KCAT9999
.
KCAT7000  DISPLAY   *P1:24,*B,"Invalid category.  ",*EL;
          CALL      HITENTER
          GOTO      KCAT0000
.
KCAT9100  MOVE      ONE,EXIT
.
KCAT9999  RETURN
+
.*********************************************************************
.*                  DCOD0000                    Called by : xxxx0000 *
.*        Display items from the CLACODFD file                       *
.*********************************************************************
DCOD0000  MOVE      ONE,PAGENO              * set page
          MOVE      "6",CVERT               * starting line
          MOVE      "1",CCOL                * starting column
          MOVE      ZERO,CNTITEM            * clear count
          DISPLAY   *P1:5,*EF,*V2LON,*ULON:
                    *P1:6,"Code",*P6:6,"Description         "
          PACK      KEY5,ENCAT,SP3
          CALL      RDCACOD1
.
. ******* loop over file *******
.
DCOD1000  CALL      RKCACOD1
          BRANCH    OVRCD,DCOD5000          * end of file
.
          MATCH     CACDCATG,ENCAT
          GOTO      DCOD5000 IF NOT EQUAL   * different category
.
. ******* increment counters *******
.
          ADD       ONE,CVERT
          COMPARE   "23",CVERT
          GOTO      DCOD4000 IF NOT LESS    * need new page
          ADD       ONE,CNTITEM             * add to item counter
.
. ******* display data and store key *******
.
DCOD3000  DISPLAY   *PCCOL:CVERT,CACDCODE,SP2,CACDDESC
          GOTO      DCOD1000
.
. ******* new screen needed *******
.
DCOD4000  BRANCH    PAGENO,DCOD4500
.
.         Middle pages : (M)ore & (F)irst
.
DCOD4100  MOVE      ONE,CALLPOSN            * set position
          CALL      KEYA0000
          BRANCH    EXIT,DCOD6000,DCOD9200,DCOD9100,DCOD7000
.                        Print    Exit     Next     More
          GOTO      DCOD0000                * first
.
.         first page : (M)ore
.
DCOD4500  MOVE      TWO,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,DCOD6000,DCOD9200,DCOD9100,DCOD7000
.                        Print    Exit     Next     More
          GOTO      DCOD5900                * first
.
. ******* no more data to display *******
.
DCOD5000  COMPARE   ZERO,CNTITEM
          GOTO      DCOD8900 IF EQUAL       * nothing on file
.
          BRANCH    PAGENO,DCOD5500
.
.         last page : (F)irst
.
DCOD5100  MOVE      THREE,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,DCOD6000,DCOD9200,DCOD9100,DCOD5900
.                        Print    Exit     Next     More
          GOTO      DCOD0000                * first
.
.         first page :
.
DCOD5500  MOVE      FOUR,CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,DCOD6000,DCOD9200,DCOD9100,DCOD5900
.                        Print    Exit     Next     More
          GOTO      DCOD5900                * first
.
DCOD5900  BEEP   
          BRANCH    CALLPOSN,DCOD4100,DCOD4500,DCOD5100,DCOD5500
.
. ******* print enquiry *******
.
DCOD6000  CALL      PCOD0000                * print the codes
          GOTO      DCOD0000
.
. ******* set up for a new page *******
.
DCOD7000  ADD       ONE,PAGENO              * add to page counter
          MOVE      ONE,CNTITEM             * reset item counter
          MOVE      "7",CVERT               * set row
          DISPLAY   *P1:CVERT,*EF           * clear screen
          GOTO      DCOD3000
.
DCOD8900  DISPLAY   *P1:24,*B,"No codes found.  ",*EL;
          CALL      HITENTER
.
. ******* set exit flags *******
.
DCOD9100  MOVE      ONE,EXIT                * next selected
          GOTO      DCOD9999
.
DCOD9200  MOVE      TWO,EXIT                * exit selected
.
DCOD9999  RETURN
+
.*********************************************************************
.*                  KEYA0000                    Called by :          *
.*        Keyin response for line 24 question                        *
.*        Para's  : CALLPOSN      which line 24 prompt               *
.*        Returns : EXIT = 0      First                              *
.*                  EXIT = 1      Print                              *
.*                  EXIT = 2      Exit                               *
.*                  EXIT = 3      Next                               *
.*                  EXIT = 4      More                               *
.*********************************************************************
KEYA0000  DISPLAY   *P1:24,*EL,"(",*V2LON,"P",*HOFF,")rint, (":
                    *V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"N",*HOFF,")ext";
          MOVE      "23",CCOL
          IF        ((CALLPOSN = ONE) | (CALLPOSN = TWO))
            DISPLAY   ", (",*V2LON,"M",*HOFF,"ore";
            ADD       "8",CCOL
          ENDIF
          IF        ((CALLPOSN = ONE) | (CALLPOSN = THREE))
            DISPLAY   ", (",*V2LON,"F",*HOFF,"irst";
            ADD       "9",CCOL
          ENDIF
          DISPLAY   " ? ",*EL;
          ADD       FOUR,CCOL
.
KEYA1000  KEYIN     *PCCOL:24,*DV,UNDLN2:
                    *PCCOL:24,*V2LON,*JR,DIM2:
                    *PCCOL:24,*DV,DIM2
.
          TYPE      DIM2
          GOTO      KEYA1500 IF EQUAL       * number entered
.
          RESET     DIM2
          GOTO      KEYA1500 IF EOS         * nothing entered
.
.         letter entered
.
          MATCH     SP1,DIM2
          GOTO      KEYA1500 IF NOT EQUAL   * invalid entry
.
          UNPACK    DIM2,ANS,ANS            * get required letter in ANS
          REP       UPPLOW,ANS              * get as uppercase
          REP       "P1E2N3M4",ANS          * turn valid letters into number
          MOVE      ZERO,EXIT               * clear exit flag
          MOVE      ANS,EXIT                * set exit flag
          BRANCH    EXIT,KEYA9999,KEYA9999,KEYA9999,KEYA9999
.
          MATCH     "F",ANS
          GOTO      KEYA4000 IF EQUAL       * first
.
KEYA1500  BEEP
          GOTO      KEYA1000
.
KEYA4000  MOVE      ZERO,EXIT
          GOTO      KEYA9999
.
KEYA9999  RETURN
+
.*********************************************************************
.*                  PCOD0000                    Called by : DCOD6000 *
.*        Print items from the CLACODFD file                         *
.*********************************************************************
PCOD0000  MOVE      ZERO,CPAGENO            * set page
          CALL      IBACLOCK
          MOVE      "70",CLNO
          PACK      KEY5,ENCAT,SP3
          CALL      RDCACOD1
.
. ******* loop over file *******
.
PCOD1000  CALL      RKCACOD1
          BRANCH    OVRCD,PCOD5000          * end of file
.
          MATCH     CACDCATG,ENCAT
          GOTO      PCOD5000 IF NOT EQUAL   * different category
.
. ******* increment counters *******
.
          IF        CLNO > 55
            CALL      HEAD80
            PRINT     *5,"Category : ",ENCAT,SP2,ENDESC
            PRINT     *N,"Code",*6,"Description         "
            PRINT     *1,"----",*6,"--------------------"
            MOVE      "9",CLNO
          ENDIF
          PRINT     *1,CACDCODE,SP2,CACDDESC
          ADD       ONE,CLNO
          GOTO      PCOD1000
.
. ******* no more data to display *******
.
PCOD5000  PRINT     *N,*N,"*** End of Report ***",*N
.
PCOD9999  RETURN
+
.
.         I/O includes needed
.
          INC       STD001IO/PBL
          INC       CLACODIO/INC            * codes file
...............................................................................
