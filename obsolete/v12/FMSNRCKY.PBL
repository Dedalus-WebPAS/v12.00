.---------------------------------------------------------------------------
.   System    :   Financial Management System
.   Include   :   FMSNRCKY
.   Routine   :   KFMNCA00 Keyin Cost Centre
.   Parameter :   CKEYTYP 0 = Keyin , 1 = Accept, 2 = Either
.   Return    :   EXIT = 1 null entry, 2 EXITCHAR entered
.   Note      :   Uses HFMCCA00 and HFMNBA00
.-------------------------------------------------------------------------------
KFMNCA00
          MOVE      FMSUUD4A,CSTRLEN          * set up string length
          CALL      GSTR0000                  * get code
          PACK      FMNCSCOD,CSTRING,SP70
.
          ENDSET    FMNCSCOD
          APPEND    SP70,FMNCSCOD
          RESET     FMNCSCOD
.
          MATCH     SP70,FMNCSCOD
          GOTO      KFMNCA90 IF EQUAL
.
          CMATCH    EXITCHAR,FMNCSCOD
          GOTO      KFMNCA95 IF EQUAL
.
          CMATCH    QUEST,FMNCSCOD
          GOTO      KFMNCA10 IF NOT EQUAL
.
          PACK      DISPMSG,SP70,SP70
          CALL      MESSAGE2
          KEYIN     *P1:24,*EF,*V2LON:
                    "N",*HOFF,"ormal or ",*V2LON:
                    "T",*HOFF,"otal Cost Centre ? ",*V2LON,ANS
          CALL      MESSAGE3
          PACK      ANS,ANS,SP70
          REP       UPPLOW,ANS
.
          MOVE      CCITEM,SCCITEM
          MOVE      SP70,FMNCSCOD
.
          MATCH     "N",ANS
          IF        @EQUAL
            PACK      FMCCLEDG,FMNALEDG,SP70
            CALL      HFMCCA00
            PACK      FMNCSCOD,FMCCCOCE,SP70
            MOVE      SP70,ANS
          ENDIF
.
          MATCH     "T",ANS
          IF        @EQUAL
            PACK      FMNBCODE,FMNCCODE,SP70
            CALL      HFMNBA00
            PACK      FMNCSCOD,FMNBTCOD,SP70
            MOVE      SP70,ANS
          ENDIF
.
          MOVE      SCCITEM,CCITEM
.
          ENDSET    FMNCSCOD
          APPEND    SP70,FMNCSCOD
          RESET     FMNCSCOD
.
          MATCH     SP70,FMNCSCOD
          GOTO      KFMNCA00 IF EQUAL
.
          DISPLAY   *PCCOL:CVERT,*V2LON,FMNCSCOD
KFMNCA10
          MOVE      "000",FMNBTLEV
          PACK      FMCCDESC,SP70
          PACK      KEY14,FMNALEDG,FMNCSCOD,SP70
          CALL      RDFMCC1
          PACK      FMNBDESC,FMCCDESC,SP70
          IF        OVRCD=1
            PACK      KEY15,FMNCCODE,FMNCSCOD,SP70
            CALL      RDFMNB1
          ENDIF
.
          BRANCH    OVRCD,KFMNCA80
          BRANCH    CKEYTYP,KFMNCA75
KFMNCA70
          MOVE      ZERO,EXIT
          GOTO      KFMNCA99
KFMNCA75
          MOVE      "Cost Centre Already Exists - ",DISPMSG
          CALL      MESSAGE1
          GOTO      KFMNCA00
KFMNCA80
          MOVE      ZERO,EXIT
          BRANCH    CKEYTYP,KFMNCA99,KFMNCA99
          MOVE      "Cost Centre Does Not Exist - ",DISPMSG
          CALL      MESSAGE1
          GOTO      KFMNCA00
KFMNCA90
          MOVE      ONE,EXIT
          GOTO      KFMNCA99
KFMNCA95
          MOVE      TWO,EXIT
KFMNCA99
          RETURN
