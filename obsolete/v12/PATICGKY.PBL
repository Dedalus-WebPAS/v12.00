.******************************************************************************
.*  Include Name  : PATICGKY.PBL                                              *
.*  Description   : Keyin of Disease codes                                    *
.*  Author        : Ian Rutt                                                  *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF4 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMODE - is keyin mode                                  *
.*                  IDCODE   - D= Disease, O=peration                         *
.*                                                                            *
.*  Returns       : GGPCODE  - Keyed in code                                   *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing Input                                  *
.*                       = 2   Spaces Input                                   *
.*                       = 3   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Modifications :                                                           *
.*                                                                            *
.******************************************************************************
PATICGKY
PTIGKY00 
          MATCH     SP4,CKYIDEF4                  * using a default ?
          GOTO      PTIGKY10 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*V2LON,*JR,GGPCODE:  * no default used
                    *PECOL:EVERT,*DV,GGPCODE
          GOTO      PTIGKY20
.
PTIGKY10  MOVE      CKYIDEF4,GGPCODE                 * default
          KEYIN     *PECOL:EVERT,*V2LON,*JR,GGPCODE:
                    *PECOL:EVERT,*DV,GGPCODE
.
.         check what was entered
.
PTIGKY20  CALL      ICGCK000
          BRANCH    EXIT,PTIGKY25,PTIGKY31,PTIGKY41,PTIGKY95
.                        "?"      valid    nothing  exitchar
          GOTO      PTIGKY00
.
.         a "?" was input
.
PTIGKY25  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
PTIGKY26  CALL      PATICGDS
          BRANCH    CNEWDS,PTIGKY27 
.
          CALL      PUTSCR00                * redisplay screen
          GOTO      PTIGKY00                * rekey code
.
.         now ask for code while leaving displayed codes on screen
.
PTIGKY27  MATCH     SP4,CKYIDEF4                  * using a default
          GOTO      PTIGKY28 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,"Group Code   : ":
                    *P16:24,*V2LON,*JR,GGPCODE:
                    *P16:24,*DV,GGPCODE
          GOTO      PTIGKY29
.
PTIGKY28  MOVE      CKYIDEF4,GGPCODE
          KEYIN     *P1:24,*EL,"Group Code   : ":
                    *P16:24,*V2LON,*JR,GGPCODE:
                    *P16:24,*DV,GGPCODE
.
.         repeat the validation of the code
.         check what was entered
.
PTIGKY29  CALL      ICGCK000
          BRANCH    EXIT,PTIGKY26,PTIGKY30,PTIGKY40,PTIGKY90
.                        "?"      valid    nothing  exitchar
.
          GOTO      PTIGKY27
.
.         a valid code was entered 
. 
PTIGKY30  CALL      PUTSCR00                      * restore screen
.
PTIGKY31  MOVE      FALSE,EXIT                    * valid input
          DISPLAY   *PECOL:EVERT,*V2LON,GGPCODE
          GOTO      PTIGKY99 
.
.         nothing was input
.
PTIGKY40  CALL      PUTSCR00                * restore screen
.
PTIGKY41  MOVE      ONE,EXIT                * Nothing or Spaces entered
          DISPLAY   *PECOL:EVERT,*V2LON,GGPCODE
          GOTO      PTIGKY99 
.
.         EXITCHAR input
.
PTIGKY90  CALL      PUTSCR00                * free the screen saved
.
PTIGKY95  MOVE      TWO,EXIT                * EXITCHAR
.
PTIGKY99  RETURN
+
.*********************************************************************
.*                  ICGCK000                    Called by : PATICGKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
ICGCK000  ENDSET    GGPCODE
          APPEND    SP4,GGPCODE
          RESET     GGPCODE
.
          MATCH     "   \",GGPCODE
          GOTO      ICGCK600 IF EQUAL       * exit char
.
          MATCH     SP4,GGPCODE
          GOTO      ICGCK700 IF EQUAL       * nothing
.
          MATCH     "   ?",GGPCODE
          GOTO      ICGCK800 IF EQUAL       * ? mark
.
.         a code was entered so validate
.
ICGCK500  
.          PACK      KEY8,CCC,CYY,CMM,CDD
.          REP       " 0",KEY8
.          MATCH     PTCNI10D,KEY8
.          CALL      FTICD000 IF NOT LESS
. 
          PACK      KEY5,IDCODE,GGPCODE,SP9
          CALL      RDICG1
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,ICGCK900          * invalid code
          ELSE
            COMPARE   ZERO,OVRCD   
            GOTO      ICGCK900 IF EQUAL
          ENDIF  
.
          MOVE      TWO,EXIT                * valid code
          GOTO      ICGCK999
.
.         exitchar entered
.
ICGCK600  MOVE      FOUR,EXIT
          MOVE      SP9,GGPCODE
          GOTO      ICGCK999
.
.         nothing entered
.
ICGCK700  BRANCH    CKYIMAND,ICGCK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP9,GGPCODE
          GOTO      ICGCK999
.
.         "?" entered
.
ICGCK800  MOVE      ONE,EXIT
          GOTO      ICGCK999
.
.         invalid code entered
.
ICGCK900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
.
          IF        CKYIMODE = 0
            DISPLAY   *P1:24,*EL,*B,"Invalid Group Code. ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      ICGCK999
.
.         field is mandatory
.
ICGCK950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is mandatory. ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
ICGCK999  RETURN
+
          INCLUDE   PATICGDS
+
