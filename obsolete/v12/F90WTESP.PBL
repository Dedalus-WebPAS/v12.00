.***************************************************************************
.* System    :   Waiting List                                              *
.* Program   :   F90WTESP                                                  *
.***************************************************************************
.* Date      :   30/04/2008                                                *
.* Author    :   Jill Habasque                                             *
.* Function  :   This program will populate WTEPEDOB from pmspx2af.pmpxedob*
.***************************************************************************
.
          INC       STD001FD
          INC       WATESPFD/INC
          INC       PMSPX2FD/INC
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
PRGID     INIT      "F90WTESP"
PRGNAM    INIT      "Populate ESIS Estimated Date of Birth"
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      WATESPA1,"watespaf"
.
          OPEN      PMSPX2A1,"pmspx2af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  PACK      KEY16,SP70
          CALL      RSWTESP1
PROC1000  CALL      RKWTESP1
          BRANCH    OVRCD,PROC9000
.
          PACK      KEY8,WTEPURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,PROC1000
.
          MOVE      PMPXEDOB,WTEPEDOB
          CALL      UPWTESP1
          GOTO      PROC1000
.
PROC9000  DISPLAY   *P1:24,"Finished processing ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       WATESPIO/INC
          INC       PMSPX2IO/INC
