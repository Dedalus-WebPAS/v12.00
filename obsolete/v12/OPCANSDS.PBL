.******************************************************************************
.* System         : Operating Rooms                                           *
.*                                                                            *
.* Include Name   : OPCANSDS.PBL                                              *
.*                                                                            *
.* Function       : Display the all cancelled session for a Surgeon/Clinic    *
.*                                                                            *
.* Author         : Justin Coates                                             *
.*                                                                            *
.* Date Written   : 29/10/1991                                                *
.*                                                                            *
.* Index used     : OPRSESA2,"oprsesaf"                                       *
.*                                                                            *
.* Parameters     : SESNCODE      the surgeon/clinic code                     *
.*                  KEY8          current date ccyymmdd                       *
.*                                                                            *
.* Modifications  :                                                           *
.******************************************************************************
.
.         OPCANDDS : Show all sessions from KEY8 onwards. Highlight the
.                    dates.
.
OPCANDDS  MOVE       ONE,FORM1
          GOTO       OPCANSDS
.
.         OPCANTDS : Show all sessions on the date KEY8. Highlight the times.
.
OPCANTDS  MOVE       ZERO,FORM1
.
.         Display headings
.
OPCANSDS  DISPLAY    *P1:4,*EF,*P32:4,*V2LON,*ULON,"SESSION DETAILS":
                     *P1:6,*ULON,"  Date    ",*P13:6,"Start",*P20:6," End ":
                     *P29:6,"Booked":
                     *P41:6,*ULON,"  Date    ",*P53:6,"Start",*P60:6," End ":
                     *P69:6,"Booked"
          MOVE       SEVEN,CVERT
          MOVE       ONE,CCOL
.
.         KEY8 contains the starting date for the scan.
.
          PACK       KEY19,SESNCODE,KEY8,SP30
          CALL       RSOPSES2
.
CANSQ222  CALL       RKOPSES2
          BRANCH     OVRCD,CANSQ999
.
          MATCH      SESNCODE,OPSECLIN
          GOTO       CANSQ999 IF NOT EQUAL  * different clinic
.
          COMPARE    ZERO,OPSESTAT
          GOTO       CANSQ222 IF NOT EQUAL  * not cancelled session
.
.         For a time scan, we only want the requested date
.
          BRANCH     FORM1,CANSQ444    * branch on ? type date or time
.
          MATCH      OPSEDATE,KEY8
          GOTO       CANSQ999 IF LESS       * different date
.
.          Display this session
.
CANSQ444  UNPACK     OPSEDATE,CCENT,CYEAR,CMON,CDAY
          CALL       PACDATE            * format the date
.
.         Check the whether the date or time is to be highlighted
.
          BRANCH     FORM1,CANSQ666         * highlight date
.
.         display time in highlight
.
          DISPLAY    *PCCOL:CVERT,CPCDATE,SP2:
                     *V2LON,OPSETIME,*HOFF,SP2,OPSEENDT,SP4,OPSETIMB
          GOTO       CANSQ777
.
.         display date in highlight
.
CANSQ666  DISPLAY    *PCCOL:CVERT,*V2LON,CPCDATE,*HOFF,SP2:
                     OPSETIME,SP2,OPSEENDT,SP4,OPSETIMB
.
CANSQ777  ADD        ONE,CVERT
          COMPARE    TWENTY3,CVERT
          GOTO       CANSQ222 IF LESS       * keep displaying data
.
          MOVE       SEVEN,CVERT
          ADD        "40",CCOL              * go to next column
.
          COMPARE    "70",CCOL
          GOTO       CANSQ222 IF LESS       * keep displaying data
.
.         New standards question
.
          KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, ":
                    "(",*V2LON,"F",*HOFF,")irst Screen, ":
                    "(",*V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
          REP       UPPLOW,ANS
          CMATCH    ANSC,ANS
          GOTO      CANSQ999 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      OPCANSDS IF EQUAL
.
.         Clear the screen
.
          MOVE      SEVEN,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:CVERT,*EF
          GOTO      CANSQ222
.
CANSQ999  RETURN
................................................................................
