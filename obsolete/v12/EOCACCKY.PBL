.******************************************************************************
.*  Include Name  : EOCACCKY.PBL                                              *
.*  Description   : Keyin of an ACC Number                                    *
.*  Author        : Howellsy                                                  *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDE20 - default value (blank if no default)           *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMODE - 0=change mode, 1=insert mode                   *
.*                  DCLOSEOC - Display Closed EOC (0=No, 1=Yes)               *
.*                                                                            *
.*  Returns       : ECACACCN - Keyed in code                                  *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.* Modifications  :                                                           *
.******************************************************************************
EOCACCKY  
EACKY010  MATCH     SP20,CKYIDE20                  * using a default ?
          GOTO      EACKY050 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN20:      * no default used
                    *PECOL:EVERT,*V2LON,ECACACCN:
                    *PECOL:EVERT,*DV,ECACACCN
          GOTO      EACKY080
.
EACKY050  MOVE      CKYIDE20,ECACACCN
          KEYIN     *PECOL:EVERT,*DV,ECACACCN:     * default used
                    *PECOL:EVERT,*V2LON,*RV,ECACACCN:
                    *PECOL:EVERT,*DV,ECACACCN
.
.         check what was entered
.
EACKY080  CALL      ACCCK000
          BRANCH    EXIT,EACKY100,EACKY705,EACKY805,EACKY905
.                         "?"      valid    nothing exitchar
          GOTO      EACKY010
.
.         a "?" was input
.
EACKY100  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
EACKY105  CALL      EOCACCDS
          BRANCH    CNEWDS,EACKY150
.
          CALL      PUTSCR00
          GOTO      EACKY010
.
.         now ask for code while leaving displayed codes on screen
.
EACKY150  MATCH     SP20,CKYIDE20                  * using a default
          GOTO      EACKY200 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"ACC Number  : ":
                    *P15:24,*DV,UNDLN20:      * no default used
                    *P15:24,*V2LON,ECACACCN:
                    *P15:24,*DV,ECACACCN
          GOTO      EACKY300
.
EACKY200  MOVE      CKYIDE20,ECACACCN
          KEYIN     *P1:24,*EL,*DV,"ACC NUmber  : ":
                    *P15:24,*DV,ECACACCN:     * default used
                    *P15:24,*V2LON,*RV,ECACACCN:
                    *P15:24,*DV,ECACACCN
.
.         repeat the validation of the code
.         check what was entered
.
EACKY300  CALL      ACCCK000
          BRANCH    EXIT,EACKY105,EACKY700,EACKY800,EACKY900
.                         "?"      valid    nothing exitchar
.
          GOTO      EACKY150
.
.         a valid code was entered 
. 
EACKY700  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,ECACACCN
.
EACKY705  MOVE      FALSE,EXIT                    * valid input
          GOTO      EACKY999 
.
.         nothing was input
.
EACKY800  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,ECACACCN
.
EACKY805  MOVE      TRUE,EXIT                     * Nothing or Spaces entered
          GOTO      EACKY999 
.
.         EXITCHAR input
.
EACKY900  CALL      FRESCR00                      * free the screen saved
.
EACKY905  MOVE      TWO,EXIT                      * EXITCHAR
.
EACKY999  RETURN
+
.***************************************************************************
.*                              ACCCK000             Called by: EOCACCKY   *
.*                        check what was entered                           *
.*          Returns:  EXIT  = 0   invalid                                  *
.*                          = 1   "?"                                      *
.*                          = 2   valid                                    *
.*                          = 3   nothing                                  *
.*                          = 4   exitchar                                 *
.***************************************************************************
ACCCK000  ENDSET    ECACACCN
          APPEND    SP20,ECACACCN
          RESET     ECACACCN
.
          MATCH     EXITCHAR,ECACACCN
          GOTO      ACCCK600 IF EQUAL
.
          MATCH     SP20,ECACACCN
          GOTO      ACCCK700 IF EQUAL
.
.         check for a "?"
.
          MATCH     QUEST,ECACACCN
          GOTO      ACCCK800 IF EQUAL
.
. a code was entered so validate
. 
ACCCK500  PACK      KEY20,ECACACCN,SP20
          CALL      RDECACC1
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,ACCCK900
          ELSE
            COMPARE   ZERO,OVRCD
            GOTO      ACCCK900 IF EQUAL
          ENDIF 
.
          PACK      KEY33,ECACACCN,SP20,SP20
          CALL      RSECREF3
          CALL      RKECREF3
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,ACCCK900
            MATCH     ECACACCN,ECRFACCN
            GOTO      ACCCK900 IF NOT EQUAL
          ELSE
            COMPARE   ZERO,OVRCD
            IF        @EQUAL
              MATCH     ECACACCN,ECRFACCN
              GOTO      ACCCK900 IF EQUAL
            ENDIF
          ENDIF 
.
          BRANCH    DCLOSEOC,ACCCK550
          IF        ECRFSTAT=1
            DISPLAY   *P1:24,*EL,*B,"This Episode is Closed. ";
            CALL      HITENTER
            MOVE      ZERO,EXIT
            GOTO      ACCCK999
          ENDIF
.
ACCCK550  MOVE      TWO,EXIT
          GOTO      ACCCK999
.
. exitchar entered
.
ACCCK600  MOVE      FOUR,EXIT
          MOVE      SP20,ECACACCN                   * clear code
          GOTO      ACCCK999
.
. nothing entered
.
ACCCK700  BRANCH    CKYIMAND,ACCCK950             * mandatory
          MOVE      THREE,EXIT
          GOTO      ACCCK999
.
. "?" entered
.
ACCCK800  MOVE      ONE,EXIT
          GOTO      ACCCK999
.
. invalid code entered
.
ACCCK900  IF        CKYIMODE=0
            DISPLAY   *P1:24,*EL,*B,"Invalid ACC Number. ";
            CALL      HITENTER
          ELSE
            DISPLAY   *P1:24,*EL,*B,"ACC Number Already Exists. ";
            CALL      HITENTER
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      ACCCK999
.
ACCCK950  BEEP
          MOVE      ZERO,EXIT
.
ACCCK999  RETURN
...............................................................................
.
          INC       EOCACCDS
