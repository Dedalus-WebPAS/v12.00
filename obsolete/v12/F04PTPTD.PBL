.*****************************************************************************
.*    Program      : F04PTPTD                                                *
.*    Description  : Conversion patptdaf to new File layout                  *
.*                                                                           *
.*    Author       : Patrick Adair       CAR 294449                          *
.*    Date         : 30/12/2013                                              *
.*    Modifications:                                                         *
.*        V10.04.00 C-ISAM table fix program created                         *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
.
.                                     ******** copy the old FD here *******
.
OLDFILE1  IFILE     SQL, FIXED=250, KEY="U1-8"
.
. NAME    TYPE   LENGTH  PHYSICAL START LOC.  DESCRIPTION
. ----    ----   ------  -------- ----------  -----------
.DPTPDADM  DIM       8        8        1       Admission Number
.
. ----- Totals for the current month for this period -----
.
.PTPDMADM  FORM      4        3        9       PTD Admissions (inc statistics)
.PTPDMSEP  FORM      4        3       12       PTD Separations
.PTPDMSTS  FORM      4        3       15       PTD Statistical Separations
.PTPDMLVE  FORM      3        3       18       On-Leave at End of Month
.PTPDMINH  FORM      4        3       21       Remaining In at End of Month
.PTPDMBED  FORM      5        4       24       PTD Bed Days
.
. ----- PTD bed days & separations for the current period -----
.
.PTPDMHAS  FORM      4        3       28       PTD Public Acute Separations
.PTPDMHAB  FORM      5        4       31       PTD Public Acute Bed Days
.PTPDMPAS  FORM      4        3       35       PTD Private Acute Separations
.PTPDMPAB  FORM      5        4       38       PTD Private Acute Bed Days
.PTPDMHNS  FORM      4        3       42       PTD Public NHT Separations
.PTPDMHNB  FORM      5        4       45       PTD Public NHT Bed Days
.PTPDMPNS  FORM      4        3       49       PTD Private NHT Separations
.PTPDMPNB  FORM      5        4       52       PTD Private NHT Bed Days
.PTPDMH5S  FORM      4        3       56       PTD Public NHT/NH5 Separations
.PTPDMH5B  FORM      5        4       59       PTD Public NHT/NH5 Bed Days
.PTPDMP5S  FORM      4        3       63       PTD Private NHT/NH5 Separations
.PTPDMP5B  FORM      5        4       66       PTD Private NHT/NH5 Bed Days
.PTPDMCOS  FORM      4        3       70       PTD Compensable Separations
.PTPDMCOB  FORM      5        4       73       PTD Compensable Bed Days
.PTPDMCNS  FORM      4        3       77       PTD Compensable Non-Acute Seps
.PTPDMCNB  FORM      5        4       80       PTD Compensable Non-Acute Bed Days
.PTPDMIES  FORM      4        3       84       PTD Ineligible Separations
.PTPDMIEB  FORM      5        4       87       PTD Ineligible Bed Days
.PTPDMINS  FORM      4        3       91       PTD Ineligible Non-Acute Seps
.PTPDMINB  FORM      5        4       94       PTD Ineligible Non-Acute Bed DaysPTPDMHCS  FORM      4        3       98       PTD Public Contract Separations
.PTPDMHCB  FORM      5        4      101       PTD Public Contract Bed Days
.PTPDMPCS  FORM      4        3      105       PTD Private Contract Separations
.PTPDMPCB  FORM      5        4      108       PTD Private Contract Bed Days
.
. ----- YTD separations for the current period -----
.
.PTPDYHAT  FORM      6        4      112       Public Acute Bed Days-YTD Seps
.PTPDYPAT  FORM      6        4      116       Privat Acute Bed Days-YTD Seps
.PTPDYCOT  FORM      6        4      120       Comp. Acute Bed Days-YTD Seps
.PTPDYIET  FORM      6        4      124       Inelig. Acute Bed Days-YTD Seps
.PTPDYHNT  FORM      6        4      128       Public NHT Bed Days-YTD Seps
.PTPDYPNT  FORM      6        4      132       Private NHT Bed Days-YTD Seps
.PTPDYH5T  FORM      6        4      136       Public NHT/NH5 Bed Days-YTD Seps
.PTPDYP5T  FORM      6        4      140       Private NHT/NH5 Bed Days-YTD Seps
.PTPDYHCT  FORM      6        4      144       Public Contract Bed Days-YTD Seps
.PTPDYPCT  FORM      6        4      148       Private Contract Bed Day-YTD Seps
.PTPDYCNT  FORM      6        4      152       Comp. Non-Acute Bed Days-YTD Seps
.PTPDYINT  FORM      6        4      156       Inelig Non-Acute Bed Days-YTD Seps
.
. ----- Same day patients for the current period -----
.
.PTPDMHSD  FORM      4        3      160       PTD Public Same Day
.PTPDMPSD  FORM      4        3      163       PTD Private Same Day
.PTPDMCSD  FORM      4        3      166       PTD Compensable Same Day
.PTPDMISD  FORM      4        3      169       PTD Ineligible Same Day
.PTPDMHCD  FORM      4        3      172       PTD Public Contract Same Day
.PTPDMPCD  FORM      4        3      175       PTD Private Contract Same Day
.
.PTPDFORM  FORM      1        2      178       Unique Key Format for PRS/2
.                                         1 = Base 36
.                                         2 = Episode & Admission number type 1
.                                         3 = Episode & Admission number type 2
.
.PTPDMUEP  FORM      3        3      180       Number of entirely unqualified
.                                             episodes
.PTPDMUBD  FORM      4        3      183       Number of unqualified bed days
.PTPDHOSP  DIM       3        3      186       Hospital Code (pathspaf)
.
OLDDSPAR  DIM      61       61      189       Spare Variable
.
.End of Record                      250
.
.
.                                     ******** copy the new FD here *******
PATPTDA1  IFILE     SQL, FIXED=250, KEY="U1-8"
.
. NAME    TYPE   LENGTH  PHYSICAL START LOC.  DESCRIPTION
. ----    ----   ------  -------- ----------  -----------
DPTPDADM  DIM       8        8        1       Admission Number
.
. ----- Totals for the current month for this period -----
.
PTPDMADM  FORM      4        3        9       PTD Admissions (inc statistics)
PTPDMSEP  FORM      4        3       12       PTD Separations
PTPDMSTS  FORM      4        3       15       PTD Statistical Separations
PTPDMLVE  FORM      3        3       18       On-Leave at End of Month
PTPDMINH  FORM      4        3       21       Remaining In at End of Month
PTPDMBED  FORM      5        4       24       PTD Bed Days
.
. ----- PTD bed days & separations for the current period -----
.
PTPDMHAS  FORM      4        3       28       PTD Public Acute Separations
PTPDMHAB  FORM      5        4       31       PTD Public Acute Bed Days
PTPDMPAS  FORM      4        3       35       PTD Private Acute Separations
PTPDMPAB  FORM      5        4       38       PTD Private Acute Bed Days
PTPDMHNS  FORM      4        3       42       PTD Public NHT Separations
PTPDMHNB  FORM      5        4       45       PTD Public NHT Bed Days
PTPDMPNS  FORM      4        3       49       PTD Private NHT Separations
PTPDMPNB  FORM      5        4       52       PTD Private NHT Bed Days
PTPDMH5S  FORM      4        3       56       PTD Public NHT/NH5 Separations
PTPDMH5B  FORM      5        4       59       PTD Public NHT/NH5 Bed Days
PTPDMP5S  FORM      4        3       63       PTD Private NHT/NH5 Separations
PTPDMP5B  FORM      5        4       66       PTD Private NHT/NH5 Bed Days
PTPDMCOS  FORM      4        3       70       PTD Compensable Separations
PTPDMCOB  FORM      5        4       73       PTD Compensable Bed Days
PTPDMCNS  FORM      4        3       77       PTD Compensable Non-Acute Seps
PTPDMCNB  FORM      5        4       80       PTD Compensable Non-Acute Bed Days
PTPDMIES  FORM      4        3       84       PTD Ineligible Separations
PTPDMIEB  FORM      5        4       87       PTD Ineligible Bed Days
PTPDMINS  FORM      4        3       91       PTD Ineligible Non-Acute Seps
PTPDMINB  FORM      5        4       94       PTD Ineligible Non-Acute Bed Days
PTPDMHCS  FORM      4        3       98       PTD Public Contract Separations
PTPDMHCB  FORM      5        4      101       PTD Public Contract Bed Days
PTPDMPCS  FORM      4        3      105       PTD Private Contract Separations
PTPDMPCB  FORM      5        4      108       PTD Private Contract Bed Days
.
. ----- YTD separations for the current period -----
.
PTPDYHAT  FORM      6        4      112       Public Acute Bed Days-YTD Seps
PTPDYPAT  FORM      6        4      116       Privat Acute Bed Days-YTD Seps
PTPDYCOT  FORM      6        4      120       Comp. Acute Bed Days-YTD Seps
PTPDYIET  FORM      6        4      124       Inelig. Acute Bed Days-YTD Seps
PTPDYHNT  FORM      6        4      128       Public NHT Bed Days-YTD Seps
PTPDYPNT  FORM      6        4      132       Private NHT Bed Days-YTD Seps
PTPDYH5T  FORM      6        4      136       Public NHT/NH5 Bed Days-YTD Seps
PTPDYP5T  FORM      6        4      140       Private NHT/NH5 Bed Days-YTD Seps
PTPDYHCT  FORM      6        4      144       Public Contract Bed Days-YTD Seps
PTPDYPCT  FORM      6        4      148       Private Contract Bed Day-YTD Seps
PTPDYCNT  FORM      6        4      152       Comp. Non-Acute Bed Days-YTD Seps
PTPDYINT  FORM      6        4      156       Inelig Non-Acute Bed Days-YTD Seps
.
. ----- Same day patients for the current period -----
.
PTPDMHSD  FORM      4        3      160       PTD Public Same Day
PTPDMPSD  FORM      4        3      163       PTD Private Same Day
PTPDMCSD  FORM      4        3      166       PTD Compensable Same Day
PTPDMISD  FORM      4        3      169       PTD Ineligible Same Day
PTPDMHCD  FORM      4        3      172       PTD Public Contract Same Day
PTPDMPCD  FORM      4        3      175       PTD Private Contract Same Day
.
PTPDFORM  FORM      1        2      178       Unique Key Format for PRS/2
.                                         1 = Base 36
.                                         2 = Episode & Admission number type 1
.                                         3 = Episode & Admission number type 2
.
PTPDMUEP  FORM      3        3      180       Number of entirely unqualified
.                                             episodes
PTPDMUBD  FORM      4        3      183       Number of unqualified bed days
PTPDHOSP  DIM       3        3      186       Hospital Code (pathspaf)
.
PTPDMOPS FORM       2        2      189       PTD Posthumous Organ Procure sepns
PTPDMOPB FORM       3        3      191       PTD Posthumous Organ Proc bed days
PTPDYOPT FORM       5        4      194       Post Org Procure Bed Days-YTD Seps
PTPDSPAR  DIM      52       52      198       Spare Variable
.
.End of Record                      250
.
.
PTPDADMN  FORM      8
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       200
.
DEFPATH   DIM       60
DIM60     DIM       60
DISNUM    FORM      4
.
INPFILE   DIM       8
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
SAVEFLG   FORM      1
.
USINGORC  DIM       1
.
.
. PROGRAM CONSTANTS
. -----------------
SP60      INIT    "                                                            "
.
.
.
PRGNAM    INIT      "CONVERSION PATPTDFD"
PRGID     INIT      "F04PTPTD"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000:             * run c-isam fixit
                            MAIN2000              * run oracle fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
MAIN2000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN0000:               * no
                          MAIN9999                * cancel
.
MAIN5000  BRANCH    COPTION,MAIN6000:             * c-isam fixit
                            MAIN7000              * oracle fixit
.
.         Running c-isam fix
.
MAIN6000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/zip saved file
          GOTO      MAIN9999
.
.         Running oracle fix
.
MAIN7000  CALL      OFIX0000                      * run oracle fix
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
          
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  MOVE      "No",USINGORC
          REP       "yYnN",USINGORC
          MOVE      USINGORC,KEY1
          REP       "Y-N-",KEY1
          MATCH     "-",KEY1
          GOTO      OPTN9200 IF NOT EQUAL
.
          DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)"
          MATCH     "Y",USINGORC
          IF        @EQUAL
            DISPLAY   *P1:6,*V2LON,TWO,*HOFF,". Run Fixit (Oracle only)"
          ENDIF
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * run c-isam fixit
                            OPTN9000             * run oracle fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9200  DISPLAY   *P1:3,*EF,*P1:4," Oracle Option has not been set up ":
                          "correctly":
                    *P1:6," Program will terminate    - ";
          CALL      HITENTER
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
.
PREP0000  MOVE      "patptdaf",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat patptdaf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sptptdaf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat patptdaf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sptptdaf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "patptdaf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "sptptdaf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "patptdaf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.      eg    APPEND    " 195 UC9-16",CMDLINE
.
          APPEND    " 250 UC1-8",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      PATPTDA1,NEWFILE
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
            ELSE
              DISPLAY   "has caused an IO error - ";
            ENDIF
          ENDIF
          DISPLAY   "File will be bypassed"
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY8,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
.  All necessary changes to the files field lengths, date changes
.  or any other sort of modification must be made here
.
.    eg    PACK      DSPARE,SP70
.          PACK      PTDSDMDC,SP10
.          PACK      PTDSDDRG,SP10
.                                               * Pack key here
          PACK      PTPDSPAR,SP70
          PACK      KEY8,DPTPDADM,SP70
          CALL      WRPTPTD1                    * write to new file
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     PATPTDA1                      * close new file
          CLOSE     OLDFILE1                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS:
                    *P1:24;
          CALL      HITENTER
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
.
VALD0000  MOVE      ZERO,EXIT
.
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or zip save file                                     *
.**********************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "sptptdaf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "gzip ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not zipped or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:24,*EL,*B,"Finished processing  ",CDATE,SP1,CTIMEIS:
                    ".  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.*        DEFT0000 -  Get the default directory                       *
.**********************************************************************
.
DEFT0000  EXECUTE   "ldat patptdaf.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding 'patptdaf'.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.*                            OFIX0000                                       *
.*                    Oracle fix to "PATPTDFD"                               *
.*****************************************************************************
.
OFIX0000  DISPLAY   *P1:24,*EL;
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,*EL,"Record No. :";
          OPEN      PATPTDA1,"patptdaf"
.
          PACK      KEY8,SP70
          CALL      OFDUMYIO                      * position at start of file
OFIX0500  CALL      OFDUMYIO                      * read next record
          BRANCH    OVRCD,OFIX9000                * eof - finished
.
          ADD       ONE,RECNUM                    * increment record counter
          ADD       ONE,DISNUM                    * Display counter
.         
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM 
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
.>>>>>>>  Add code here to populate/update variables
.
          CALL      OFDUMYIO
.
          GOTO      OFIX0500                     * get next record
.         
OFIX9000  CLOSE     PATPTDA1
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Update completed ",CDATE,SP1,CTIMEIS:
                    *P1:24;
          CALL      HITENTER
.
OFIX9999  RETURN
.
OFDUMYIO  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
.                  ******************************    OLD IO ROUTINES
.READSOLD  MOVE      ZERO,OVRCD
.
READSOLD  RESET     KEY8
          READ      OLDFILE1,KEY8;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;DPTPDADM,PTPDMADM,PTPDMSEP,PTPDMSTS,PTPDMLVE:
                             PTPDMINH,PTPDMBED,PTPDMHAS,PTPDMHAB,PTPDMPAS:
                             PTPDMPAB,PTPDMHNS,PTPDMHNB,PTPDMPNS,PTPDMPNB:
                             PTPDMH5S,PTPDMH5B,PTPDMP5S,PTPDMP5B,PTPDMCOS:
                             PTPDMCOB,PTPDMCNS,PTPDMCNB,PTPDMIES,PTPDMIEB:
                             PTPDMINS,PTPDMINB,PTPDMHCS,PTPDMHCB,PTPDMPCS:
                             PTPDMPCB,PTPDYHAT,PTPDYPAT,PTPDYCOT,PTPDYIET:
                             PTPDYHNT,PTPDYPNT,PTPDYH5T,PTPDYP5T,PTPDYHCT:
                             PTPDYPCT,PTPDYCNT,PTPDYINT,PTPDMHSD,PTPDMPSD:
                             PTPDMCSD,PTPDMISD,PTPDMHCD,PTPDMPCD,PTPDFORM:
                             PTPDMUEP,PTPDMUBD,PTPDHOSP,OLDDSPAR
          GOTO      OVERCOND IF OVER
          MOVE      DPTPDADM,PTPDADMN
          UNPACK    OLDDSPAR,D2,D3,D5
          MOVE      D2,PTPDMOPS
          MOVE      D3,PTPDMOPB
          MOVE      D5,PTPDYOPT
          RETURN
.
.                  ******************************    NEW IO ROUTINES
WRPTPTD1  RESET     KEY8
          MOVE      PTPDADMN,DPTPDADM
          WRITE     PATPTDA1,KEY8;DPTPDADM,PTPDMADM,PTPDMSEP,PTPDMSTS,PTPDMLVE:
                                  PTPDMINH,PTPDMBED,PTPDMHAS,PTPDMHAB,PTPDMPAS:
                                  PTPDMPAB,PTPDMHNS,PTPDMHNB,PTPDMPNS,PTPDMPNB:
                                  PTPDMH5S,PTPDMH5B,PTPDMP5S,PTPDMP5B,PTPDMCOS:
                                  PTPDMCOB,PTPDMCNS,PTPDMCNB,PTPDMIES,PTPDMIEB:
                                  PTPDMINS,PTPDMINB,PTPDMHCS,PTPDMHCB,PTPDMPCS:
                                  PTPDMPCB,PTPDYHAT,PTPDYPAT,PTPDYCOT,PTPDYIET:
                                  PTPDYHNT,PTPDYPNT,PTPDYH5T,PTPDYP5T,PTPDYHCT:
                                  PTPDYPCT,PTPDYCNT,PTPDYINT,PTPDMHSD,PTPDMPSD:
                                  PTPDMCSD,PTPDMISD,PTPDMHCD,PTPDMPCD,PTPDFORM:
                                  PTPDMUEP,PTPDMUBD,PTPDHOSP,PTPDMOPS,PTPDMOPB:
                                  PTPDYOPT,PTPDSPAR
          RETURN
.
          INC       STD001IO
