.-------------------------------------------------------
          INC       STDGENDF     * Required Include
.
VERSION   INIT      "V12.00.00"
.-------------------------------------------------------
.V10.03.01  27/02/2013  Ania P              CAR 281021
.           RECOMPILED for FMSBNKKY
. V9.02.00  03.05.2002  Glenn Saunders
.           Export to new release i.e. v9.02 from vf.09
.-------------------------------------------------------
. VF.00.04  28.04.2000 Sandra Barcham
.           Recompiled for FMSBNKKY
. VF.00.03  13.01.2000 Sandra Barcham
.           Recompiled for FMSBNKKY
. VF.00.02  05.01.2000 Charles Handaya
.           Recompiled for APSMASIO
. VF.00.01  22.11.1999 Sandra Barcham
.           Make program case tools and screen painted
.           Allow for cancelling direct debits for a creditor
.           srf 635048
.-------------------------------------------------------
ISBUILD   INIT      "isbuild "
ISERASE   INIT      "iserase "
.
FMSTMP01  IFILE     SQL, FIXED=150, KEY="U21-35"     * temp file vars
FILEDAT1  INIT      " 150 UC21-35"
NAME1     INIT      "fms341"
FILENAM1  DIM       8
KILLTMP1  DIM       80
MAKETMP1  DIM       80
.
AMOUNT    FORM      12.2
.
CHQRANGE  DIM       30
CORREF    DIM       15
.
D11       DIM       11
DATRANGE  DIM       30
.
ENDCHQ    DIM       8
ENDDAT    DIM       8
.
FORM15    FORM      15       * general form 15
FORMAT    INIT      "(99,999,999,999,999.99 "
FORMAT1   INIT      "(999,999,999,999.99 "
.
ISSDATE   DIM       10
.
LASTCHEQ  FORM      15       * last cheque printed
.
PRES      INIT      "   "   * presented vars
PRES0     INIT      "No "
PRES1     INIT      "Yes"
PRES2     INIT      "Can"
PRESDATE  DIM       10
.
SKEY15    DIM       15
STRCHQ    DIM       8
STRDAT    DIM       8
.
TOTAL1    FORM      14.2     * grand total (pres or not pres)
TOTAL2    FORM      14.2     * grand total (cancelled)
.-------------------------------------------------------
.@@@@@ Start of Generated Code @@@@@
. NB - Code & Comments Included Before the above line 
.      will not be overwritten by the generation of a 
.      program. The STDGENDF file and VERSION must appear
.      before the line. 
.-----------------------------------------
.   Program       :   IBAFMS34
.   Function      :   Cheque Range Report                     
.   Generated  On :   13/12/2005
.-----------------------------------------
.=========================
.File Description Includes
.-------------------------
          INC       APSMASFD/INC
          INC       FMSBNKFD/INC
          INC       FMSCHQFD/INC
          INC       FMSCONFD/INC
          INC       FMSGENDF    
.=========================
.Local Variable Definition
.-------------------------
FROMDAT   DIM      10 * Scr 00 Fld     4
TODAT     DIM      10 * Scr 00 Fld     5
FROMCHQ   DIM       8 * Scr 00 Fld     8
TOCHQ     DIM       8 * Scr 00 Fld     9
.
ACCEPT    FORM      1
SACCEPT   FORM      1
.
PRGID     INIT      "IBAFMS34"
PRGNAM    INIT      "Cheque Range Report                     "
.
.--------------------------------
. MAINLINE - Controlling Logic
.--------------------------------
          TOPIC     PRGID
          MOVE      PORT,PARENTID
          CLOCK     PORT,DIM2
          MOVE      DIM2,PORT
ML0000
          CALL      INIT0000
          CALL      REDPAR00
          CALL      SC00L000
          CHAIN     PGM
.---------------------------------------
. INIT - Open Files & Display Heading
.---------------------------------------
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CCENTRY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          DISPLAY   *P56:24,"Opening scrsbga1";
          OPEN      SCRSBGA1,"scrsbgaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA1,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA2,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrslta1";
          OPEN      SCRSLTA1,"scrsltaf"
          DISPLAY   *P56:24,"Opening apsmasaf";
          OPEN      APSMASA1,"apsmasaf"
          DISPLAY   *P56:24,"Opening apsmasaf";
          OPEN      APSMASA2,"apsmasaf"
          DISPLAY   *P56:24,"Opening apsmasaf";
          OPEN      APSMASA3,"apsmasaf"
          DISPLAY   *P56:24,"Opening fmsbnkaf";
          OPEN      FMSBNKA1,"fmsbnkaf"
          DISPLAY   *P56:24,"Opening fmsbnkaf";
          OPEN      FMSBNKA2,"fmsbnkaf"
          DISPLAY   *P56:24,"Opening fmsbnkaf";
          OPEN      FMSBNKA3,"fmsbnkaf"
          DISPLAY   *P56:24,"Opening fmsbnkaf";
          OPEN      FMSBNKA4,"fmsbnkaf"
          DISPLAY   *P56:24,"Opening fmsbnkaf";
          OPEN      FMSBNKA5,"fmsbnkaf"
          DISPLAY   *P56:24,"Opening fmsbnkaf";
          OPEN      FMSBNKA6,"fmsbnkaf"
          DISPLAY   *P56:24,"Opening fmschqaf";
          OPEN      FMSCHQA1,"fmschqaf"
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.
.------------------------------------
. Called for Print Screen Routine 
.------------------------------------
PRTSVARS
          MATCH     "00",SCRID
          IF         @EQUAL
            CALL       PS00L000   * Menu Options                       
          ENDIF
          RETURN
.--------------------------------
. Screen - Menu Options                       
.--------------------------------
SC00L000
SC00L100
          MOVE      "00",SCRID
          MOVE      ZERO,PRCOL
          MOVE      ZERO,PRVERT
          MOVE      "    1",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SCPSCOL,PRCOL
            MOVE     SCPSROW,PRVERT
          ENDIF
          CALL      SBACK000 * Display Background
.
SC00L200
          CALL      CF00L000 * Clear Fields
          CALL      DEFPAR00
SC00L300
          CALL      KY00L000 * Enter Key Fields
          BRANCH    EXIT,SC00L999,SC00L999
SC00L400
          CALL      WP00L000 * Load any WP Details
SC00L500
          CALL      LM00L000 * Load any Batch Details
SC00L600
          CALL      DF00L000 * Display Fields
SC00L700
          CALL      SF00L000 * Select Fields
          BRANCH    EXIT,SC00L100,SC00L100,SC00L200
          IF        EXIT=4
            MOVE      ONE,EXIT
          ENDIF
SC00L800
SC00L900
SC00L999  RETURN
.--------------------------------------------------------
. Clear Fields for Screen : Menu Options                       
.-------------------------------------------------------
CF00L000
          MOVE      SP70,FROMDAT 
          MOVE      SP70,TODAT   
          MOVE      SP70,FMCHCHQ 
          MOVE      SP70,FMCHDES 
          MOVE      SP70,FROMCHQ 
          MOVE      SP70,TOCHQ   
CF00L999  RETURN
.--------------------------------------------------------
. Enter Key Fields for Menu Options                       
.--------------------------------------------------------
KY00L000
          MOVE      ZERO,EXIT
          GOTO      KY00L999
.
KY00L900  MOVE      ONE,EXIT
KY00L999  RETURN
.---------------------------------------------------------
. Load WP files for : Menu Options                       
.---------------------------------------------------------
WP00L000
          RETURN
.---------------------------------------------------------
. Load Batch Screen for : Menu Options                       
.---------------------------------------------------------
LM00L000
          RETURN
.---------------------------------------------------------
. Display Fields for : Menu Options                       
.---------------------------------------------------------
DF00L000
          BRANCH   ACCEPT,DF00L999
          PACK     KEY15,PRGID,SCRID,SP70
          CALL     RSSCPS1
DF00L010
          CALL     RKSCPS1
          BRANCH   OVRCD,DF00L900
          MATCH    PRGID,SCPSPID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SCRID,SCPSSID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SP70,SCPSMTY
          GOTO     DF00L010 IF NOT EQUAL
.
          CALL     PS00L000 * Display Field
          GOTO     DF00L010
DF00L900
DF00L999
          RETURN
.---------------------------------------------------------
. Display/Print Fields for : Menu Options                       
.---------------------------------------------------------
PS00L000   PACK      VAR,SP70,SP70
          PACK     PRINTVAR,SP70,SP70
          MOVE     SCPSITM,FIELDNO
          BRANCH    FIELDNO,PS00L999,PS00L999,PS00L999,PS00L101:
                            PS00L102,PS00L103,PS00L104,PS00L105:
                            PS00L106
          GOTO     PS00L999
.
PS00L101
          MOVE      FROMDAT,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L102
          MOVE      TODAT,VAR        
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L103
          MOVE      FMCHCHQ,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L104
          MOVE      FMCHDES,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L105
          MOVE      FROMCHQ,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L106
          MOVE      TOCHQ,VAR        
          CALL      DISPITEM
          GOTO     PS00L999
PS00L999
          RETURN
.---------------------------------------------------------
. Selections for : Menu Options                       
.---------------------------------------------------------
SF00L000  PACK     KEY13,PRGID,SCRID,SP70
          CALL     RSSCSL1
SF00L001  BRANCH   ACCEPT,SF00L020
.
SF00L005  CALL     MTSTQ000
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L006,SF00L101,SF00L102,SF00L103:
                            SF00L006,SF00L104,SF00L006,SF00L105:
                            SF00L006
SF00L006  BEEP
          GOTO     SF00L005
SF00L020  CALL     RKSCSL1
          BRANCH   OVRCD,SF00L090
          MATCH    PRGID,SCSLPID
          GOTO     SF00L090 IF NOT EQUAL
          MATCH    SCRID,SCSLSID
          GOTO     SF00L090 IF NOT EQUAL
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L020,SF00L020,SF00L020,SF00L103:
                            SF00L020,SF00L104,SF00L020,SF00L105:
                            SF00L020
          GOTO     SF00L020
SF00L090  MOVE     ZERO,ACCEPT
          GOTO     SF00L001
.
SF00L101
          MOVE      ZERO,EXIT
          GOTO      SF00L999
.
SF00L102
          CALL      REPT0000
          MOVE      ZERO,EXIT
          GOTO      SF00L999
.
SF00L103
.
.
          MOVE      "    5",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,TODAT   
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      RDAT0000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L103
            ENDIF
            BRANCH    EXIT,SF00L103,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      FROMDAT,VAR      
          CALL      DISPITEM
.
.
          MOVE      "    5",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      TODAT,VAR        
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L104
.
.
          MOVE      "    7",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,FMCHDES 
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      KCHQ0000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L104
            ENDIF
            BRANCH    EXIT,SF00L104,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      FMCHCHQ,VAR      
          CALL      DISPITEM
.
.
          MOVE      "    7",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      FMCHDES,VAR      
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L105
.
.
          MOVE      "    9",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,TOCHQ   
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      RCHQ0000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L105
            ENDIF
            BRANCH    EXIT,SF00L105,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      FROMCHQ,VAR      
          CALL      DISPITEM
.
.
          MOVE      "    9",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      TOCHQ,VAR        
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
SF00L990  MOVE      FOUR,EXIT * Exit Char Exit
SF00L999  RETURN
.@@@@@ Function Restart Point @@@@@
.---------------------------------------
. Read Parameters in controlf
.---------------------------------------
REDPAR00  OPEN      CONTROLF,"controlf"
          CALL      RDFMCO50
          CALL      RDFMCO51
          CALL      RDFMCO52
          RETURN
.
.---------------------------------------
. Setup Defaults
.---------------------------------------
DEFPAR00  MOVE      ONE,ACCEPT
          PACK      STRDAT,CCC,CYY,CMM,CDD
          REP       " 0",STRDAT
          UNPACK    STRDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,FROMDAT
          MOVE      CPCDATE,TODAT
          PACK      ENDDAT,CCC,CYY,CMM,CDD
          REP       " 0",ENDDAT
.
          MOVE      SP70,STRCHQ
          MOVE      "99999999",ENDCHQ
          RETURN
.
.---------------------------------------
. Print Report
.---------------------------------------
REPT0000  STRIP     FROMDAT
          STRIP     TODAT
          MOVE      " to ",KEY4
          PACK      DATRANGE,FROMDAT,KEY4,TODAT
.
          STRIP     FROMCHQ
          STRIP     TOCHQ
          MOVE      " to ",KEY4
          PACK      CHQRANGE,FROMCHQ,KEY4,TOCHQ
.
          CALL      EXTR0000
          CALL      PRNT0000
.
REPT9000  CLOSE     FMSTMP01
          EXECUTE   KILLTMP1,TASKID         * create file
REPT9999  RETURN
.
.---------------------------------------
.EXTR0000 - Extract Report
.---------------------------------------
EXTR0000  DISPLAY   *P1:24,"Clearing Temp File : ",*EL;
          PACK      FILENAM1,NAME1,PORT
          REP       " 0",FILENAM1
          PACK      KILLTMP1,ISERASE,FILENAM1
          PACK      MAKETMP1,ISBUILD,FILENAM1,FILEDAT1
          CLOSE     FMSTMP01
          EXECUTE   KILLTMP1,TASKID         * create file
          EXECUTE   MAKETMP1,TASKID
          OPEN      FMSTMP01,FILENAM1
.
EXTR1000  DISPLAY   *P1:24,"Extracting Data    :",*EL;
          PACK      KEY44,FMCHCHQ,ZERO,STRDAT,SP70
          CALL      RSFMBN3            * get desired records
.
EXTR1100  CALL      RKFMBN3            * get next record
          BRANCH    OVRCD,EXTR9999     * no more files ?
          MATCH     FMCHCHQ,FMBNBNK   * at end of account
          GOTO      EXTR9999 IF NOT EQUAL
          MATCH     "0",FMBNTYP        * at end cheq records
          GOTO      EXTR9999 IF NOT EQUAL
.
          MATCH     FMBNDAT,ENDDAT   * at end of date range
          GOTO      EXTR9999 IF LESS
.
          DISPLAY   *P22:24,*V2LON,FMBNREF,*EL;
.
          MOVE      ZERO,F1
          MOVE      FMBNPRE,F1
          MATCH     FMBNPDT,ENDDAT * treat as unpres if cancelled after period
          IF        @LESS & F1=2 & FMBNAMT<>0
            MOVE      "0",FMBNPRE
          ENDIF
.
          PACK      KEY15,SP7,STRCHQ
          MATCH     KEY15,FMBNREF
          GOTO      EXTR1100 IF LESS
          PACK      KEY15,SP7,ENDCHQ
          MATCH     FMBNREF,KEY15
          GOTO      EXTR1100 IF LESS
.
          ADD       FMBNAMT,TOTAL1
          PACK      KEY15,FMBNREF,SP70
          CALL      RDTEMP1
          BRANCH    OVRCD,EXTR1200
          ADD       AMOUNT,FMBNAMT
          CALL      UPTEMP1
          SUB       AMOUNT,FMBNAMT
          GOTO      EXTR1100
.
EXTR1200  CALL      WRTEMP1            * delete record
          GOTO      EXTR1100
.
EXTR9999  RETURN
.
.---------------------------------------
. PRNT0000 - Print Report
.---------------------------------------
PRNT0000  DISPLAY   *P1:24,"Printing Report    : ",*EL;
          MOVE      ZERO,CPAGENO                 * reset page no.
          MOVE      ZERO,CNOUNDLN
          MOVE      ZERO,TOTAL1
          MOVE      ZERO,TOTAL2
          CLOCK     TIME,CTIMEIS                 * get current time
          CALL      HEAD0000                     * print header
.
          PACK      KEY15,SP70
          CALL      RSTEMP1
.
PRNT1000  CALL      RKTEMP1                      * get next code record
          BRANCH    OVRCD,PRNT9000               * end of file ?
.
          DISPLAY   *P22:24,*V2LON,FMBNREF,*EL;
.
          COMPARE   "56",CLNO
          CALL      HEAD0000 IF NOT LESS
          CALL      WRIT0000
          GOTO      PRNT1000
.
PRNT9000  CALL      PTOT0000                     * print totals
.
PRNT9999  RETURN
.---------------------------------------
. PTOT0000 - Print totals                                 Called By PRNT
.---------------------------------------
PTOT0000  CALL      UND80                     * print line
          MOVE      FORMAT,KEY23
          FEDIT     TOTAL1,KEY23
          PRINT     "Total Presented/Not-Presented Cheques     ",KEY23
          MOVE      FORMAT,KEY23
          FEDIT     TOTAL2,KEY23
          PRINT     "Total Cancelled Cheques                   ",KEY23
          CALL      UND80                     * print line
          PRINT     "****   End of Report   ****"
.
PTOT9999  RETURN
.---------------------------------------
. HEAD0000 - Print page heading                           Called By PRNT
.---------------------------------------
HEAD0000  CALL      HEAD80            * print header
          PRINT     "Cheque Account : ",FMCHCHQ,SP2,FMCHDES,*N:
                    "Period         : ",DATRANGE,*N:
                    "Cheque Number  : ",CHQRANGE
          CALL      UND80             * print line
          PRINT     *1,"Cheque No.",*16,"Cheque Amount",*30,"Paid to":
                    *56,"Issued",*67,"Presented"
          ADD       FOUR,CLNO
          CALL      UND80
          GOTO      HEAD9999
.
HEAD9999  RETURN
.---------------------------------------
. WRIT0000 - Print data                                   Called By PRNT
.---------------------------------------
WRIT0000  COMPARE   ZERO,LASTCHEQ
          GOTO      WRIT1000 IF EQUAL  * first item ?
.
          MOVE      FMBNREF,FORM15
          SUB       ONE,FORM15
          COMPARE   FORM15,LASTCHEQ
          GOTO      WRIT1000 IF EQUAL  * break in cheque sequence ?
.
          CALL      CHGAP000           * check if there is actually a gap in seq
          BRANCH    EXIT,WRIT1000
.
          PRINT     "*************** Break in Cheque Sequence"
          ADD       ONE,CLNO           * increment line number
.
WRIT1000  MOVE      FMBNREF,LASTCHEQ   * set up data for printing
          UNPACK    FMBNDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,ISSDATE
          MOVE      ZERO,F1
          MOVE      FMBNPRE,F1
          MOVE      PRES0,PRES
          LOAD      PRES,F1,PRES1,PRES2
          MATCH     "00000",FMBNCRE
          GOTO      WRIT1100 IF EQUAL
          MATCH     SP5,FMBNCRE
          GOTO      WRIT1100 IF EQUAL
          MOVE      FMBNCRE,KEY5
          CALL      RDAPMA1
          BRANCH    OVRCD,WRIT1100
          MOVE      APMACN1,KEY25
          GOTO      WRIT1200
.
WRIT1100  MOVE      FMBNNAM,KEY25
.
WRIT1200  UNPACK    FMBNREF,KEY7,KEY8
          MOVE      SP10,PRESDATE
          MATCH     SP8,FMBNPDT
          IF        !@EQUAL
            UNPACK    FMBNPDT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,PRESDATE
          ENDIF
          MOVE      FORMAT1,KEY20
          FEDIT     FMBNAMT,KEY20
          PRINT     KEY8,*10,KEY20,*30,KEY25,*56,ISSDATE,*67,PRES,*71,PRESDATE
          ADD       ONE,CLNO           * increment line number
          MATCH     "2",FMBNPRE
          GOTO      WRIT1300 IF EQUAL
.
          ADD       FMBNAMT,TOTAL1     * add to total
          GOTO      WRIT9999
.
WRIT1300  ADD       FMBNAMT,TOTAL2     * add to total
.
WRIT9999  RETURN
.******************************************************************************
. CHGAP - check if gap exists   (exit=1 means no gap)
.******************************************************************************
CHGAP000  PACK      SKEY15,FMBNREF,SP70          * save current rec
          MOVE      LASTCHEQ,FORM15
.
CHGAP100  ADD       ONE,FORM15
          MOVE      FORM15,KEY15
          MATCH     SKEY15,KEY15
          GOTO      CHGAP950 IF NOT LESS
.
          PACK      KEY36,FMCHCHQ,ZERO,KEY15,SP70
          CALL      RSFMBN2
          CALL      RKFMBN2
          BRANCH    OVRCD,CHGAP900
          PACK      KEY31,FMBNBNK,FMBNTYP,FMBNREF,SP70
          MATCH     KEY36,KEY31
          GOTO      CHGAP900 IF NOT EQUAL
          GOTO      CHGAP100
.
CHGAP900  MOVE      ZERO,EXIT
          GOTO      CHGAP999
.
CHGAP950  MOVE      ONE,EXIT
.
CHGAP999  PACK      KEY15,SKEY15,SP70            * restore current rec
          CALL      RDTEMP1
          MOVE      AMOUNT,FMBNAMT
          RETURN
.---------------------------------------
. Routines for temp file
.---------------------------------------
RSTEMP1   RESET     KEY15
          READ      FMSTMP01,KEY15;;
          RETURN
.
RDTEMP1   RESET     KEY15
          MOVE      ZERO,OVRCD
          READ      FMSTMP01,KEY15;FMBNBNK,FMBNUNQ,FMBNREF,FMBNLED:
                                   FMBNACC,FMBNDAT,FMBNTYP,FMBNPRE,FMBNPDT:
                                   AMOUNT,FMBNFYR,FMBNBCH,FMBNLNO,FMBNCRE:
                                   FMBNNAM,FMBNSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    FMSTMP01;FMBNBNK,FMBNUNQ,FMBNREF,FMBNLED:
                             FMBNACC,FMBNDAT,FMBNTYP,FMBNPRE,FMBNPDT:
                             FMBNAMT,FMBNFYR,FMBNBCH,FMBNLNO,FMBNCRE:
                             FMBNNAM,FMBNSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   UPDATE    FMSTMP01;FMBNBNK,FMBNUNQ,FMBNREF,FMBNLED:
                             FMBNACC,FMBNDAT,FMBNTYP,FMBNPRE,FMBNPDT:
                             FMBNAMT,FMBNFYR,FMBNBCH,FMBNLNO,FMBNCRE:
                             FMBNNAM,FMBNSPAR
          RETURN
.
RPTEMP1   MOVE      ZERO,OVRCD
          READKP    FMSTMP01;FMBNBNK,FMBNUNQ,FMBNREF,FMBNLED:
                             FMBNACC,FMBNDAT,FMBNTYP,FMBNPRE,FMBNPDT:
                             FMBNAMT,FMBNFYR,FMBNBCH,FMBNLNO,FMBNCRE:
                             FMBNNAM,FMBNSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY15
          WRITE     FMSTMP01,KEY15;FMBNBNK,FMBNUNQ,FMBNREF,FMBNLED:
                                   FMBNACC,FMBNDAT,FMBNTYP,FMBNPRE,FMBNPDT:
                                   FMBNAMT,FMBNFYR,FMBNBCH,FMBNLNO,FMBNCRE:
                                   FMBNNAM,FMBNSPAR
          RETURN
.
DETEMP1   RESET     KEY15
          DELETE    FMSTMP01,KEY15
          RETURN
.
.----------------------------------------
. Enter Date Range
.----------------------------------------
RDAT0000   MOVE     "    4",FLDID
           CALL     GETPOSIT
           MOVE     SP70,CDAY
           MOVE     CMM,CMON
           MOVE     CYY,CYEAR
           MOVE     CCC,CCENT
           CALL     KEYDATE
           BRANCH   OVRCD,RDAT0100
           MATCH    SP2,CDAY
           GOTO     RDAT0100 IF EQUAL
           PACK     STRDAT,CCENT,CYEAR,CMON,CDAY
           REP      " 0",STRDAT
           CALL     PACDATE
           MOVE     CPCDATE,FROMDAT
           GOTO     RDAT0140
.
RDAT0100   MOVE     "Start             ",FROMDAT
           MOVE     SP70,STRDAT
.
RDAT0140   MOVE     FROMDAT,VAR
           CALL     DISPITEM
.
RDAT0150   MOVE     "    5",FLDID
           CALL     GETPOSIT
           MOVE     SP70,CDAY
           MOVE     CMM,CMON
           MOVE     CYY,CYEAR
           MOVE     CCC,CCENT
           CALL     KEYDATE
           BRANCH   OVRCD,RDAT0200
           MATCH    SP2,CDAY
           GOTO     RDAT0200 IF EQUAL
           PACK     ENDDAT,CCENT,CYEAR,CMON,CDAY
           REP      " 0",ENDDAT
           CALL     PACDATE
           MOVE     CPCDATE,TODAT
           MATCH    STRDAT,ENDDAT
           GOTO     RDAT0999 IF EQUAL
           GOTO     RDAT0999 IF NOT LESS
           BEEP
           MOVE     "To Date Must Be > From Date - ",DISPMSG
           CALL     MESSAGE1
           GOTO     RDAT0000
.
RDAT0200   MOVE     "Finish             ",TODAT
           MOVE     "99999999",ENDDAT
.
RDAT0999   MOVE     "    4",FLDID
           CALL     GETPOSIT
           RETURN
.----------------------------------------------------------------------
. Enter Cheque Account
.----------------------------------------------------------------------
KCHQ0000  MOVE      ZERO,CKEYTYP
          CALL      KFMCHA00
.
KCHA9999  RETURN
.
.--------------------------------------------------
. Enter Range
.  where FMBN    = System & File for Keyin Routines
.        CHQ     = Field ID
.        RCHQ0   = Call Routine Label
.        "    8" = Screen Item Number for "from" Variable
.        "    9" = Screen Item Number for "to" Variable
.        yyyyy.. = Description of Variable
.
.  Return Values
.        FROMCHQ  Code & Description for Start of Range
.        TOCHQ    Code & Description for End of Range
.        STRCHQ   Code for Start of Range
.        ENDCHQ   Code for End of Range
.--------------------------------------------------
RCHQ0000   MOVE     "    8",FLDID
           CALL     GETPOSIT
.
           MOVE     ZERO,CKEYTYP
           MOVE     FMCHCHQ,FMBNBNK
           MOVE     ZERO,FMBNTYP
           CALL     KFMBNA00
           BRANCH   EXIT,RCHQ1000,RCHQ9500
           UNPACK   FMBNREF,KEY7,STRCHQ
           PACK     FROMCHQ,STRCHQ,SP70
           GOTO     RCHQ1400
RCHQ1000   MOVE     "Start             ",FROMCHQ
           MOVE     SP70,STRCHQ
.
RCHQ1400   MOVE     FROMCHQ,VAR
           CALL     DISPITEM
.
RCHQ1500   MOVE     "    9",FLDID
           CALL     GETPOSIT
.
           MOVE     ZERO,CKEYTYP
           MOVE     FMCHCHQ,FMBNBNK
           MOVE     ZERO,FMBNTYP
           CALL     KFMBNA00
           BRANCH   EXIT,RCHQ2000,RCHQ9500
           UNPACK   FMBNREF,KEY7,ENDCHQ
           PACK     TOCHQ,ENDCHQ,SP70
           MATCH    STRCHQ,ENDCHQ
           GOTO     RCHQ9800 IF NOT LESS
           BEEP
           MOVE     "To Cheque Number Must Be > From Cheque Number - ",DISPMSG
           CALL     MESSAGE1
           GOTO     RCHQ0000
.
RCHQ2000   MOVE     "Finish             ",TOCHQ
           MOVE     "99999999",ENDCHQ
           GOTO     RCHQ9800
.
RCHQ9500   MOVE     TWO,EXIT
           GOTO     RCHQ9999
.
RCHQ9800   MOVE     "    8",FLDID
           CALL     GETPOSIT
RCHQ9999   RETURN
.
.-------------------------------
. Includes for I/O and Keyins
.-------------------------------
          INC       APSMASIO/INC
          INC       FMSBNKIO/INC
          INC       FMSBNKKY    
          INC       FMSCHQIO/INC
          INC       FMSCHQKY    
          INC       FMSCONIO/INC
.
          INC       STDGENCD
.
