.----------------------------------------------------------------------
.    File          : DEBOUTCD.PBL
.    
.    Function      : Include to Calculate Current Outstanding Balances
.                    for a Debtor
.
.    Modifications :                               
.                    V6.00.01 21.06.96 B.G.Ackland
.                             Remove amount paid from calculation
.                    V6.00.00 04.04.95 B.G.Ackland
.                             New System Include  
.
.    Operation     : Before calling the debtor balance calculation routine
.                    the calling program should have set up the as at date
.                    in the variable ASATDATE and call the routine SETOUT00.
.                    This routine will calculate the dates for the aging of
.                    the debtors information. ASATDATE is declared in the
.                    include DEBOUTDF.PBL.
.
.                    The calling program should have read the debtors
.                    that the balances are to be calculated. ie RDDBDB1
.                    If this date is blank then todays date will be used.
.
.                    Return Values (Declared in the include DEBOUTDF.PBL)
.                      TOTALOUT  FORM     12.2 Total Outstanding
.                      CURRENT   FORM     12.2 Current
.                      BALTOT30  FORM     12.2 30 Days
.                      BALTOT60  FORM     12.2 60 Days
.                      BALTOT90  FORM     12.2 90 Days
.                      ASAT30    DIM      8 
.                      ASAT60    DIM      8 
.                      ASAT90    DIM      8 
.                       
.                    The following files need to be open
.                                 debdbtaf
.                                 debfdtaf
.
.----------------------------------------------------------------------
. DEBOUT00 - Calculate Outstanding Balance as at a set date
.----------------------------------------------------------------------
DEBOUT00  ENDSET    ASATDATE
          APPEND    SP70,ASATDATE
          RESET     ASATDATE
          MATCH     SP70,ASATDATE
          CALL      SETOUT00 IF EQUAL
          MOVE      ZERO,TOTALOUT
          MOVE      ZERO,CURRENT
          MOVE      ZERO,BALTOT30
          MOVE      ZERO,BALTOT60
          MOVE      ZERO,BALTOT90
.
          PACK      KEY52,DBDBDEB,ASATDATE,Z40
          CALL      RSDBFD2            * Process all transactions paid after
DEBOUT10  CALL      RKDBFD2            * the as at date.
          BRANCH    OVRCD,DEBOUT95
          MATCH     DBFDDEB,DBDBDEB
          GOTO      DEBOUT95 IF NOT EQUAL
          MATCH     "1",DBFDDTY        * Process Invoices Only
          GOTO      DEBOUT10 IF NOT EQUAL
          MATCH     DBFDIDAT,ASATDATE  * Ignore Invoices Dated After 
          GOTO      DEBOUT10 IF LESS   * as at Date
.
          MATCH     ASAT90,DBFDIDAT    * Over 90 days
          GOTO      DEBOUT90 IF LESS   
          MATCH     ASAT60,DBFDIDAT    * Over 60 days
          GOTO      DEBOUT60 IF LESS   
          MATCH     ASAT30,DBFDIDAT    * Over 30 days
          GOTO      DEBOUT30 IF LESS   
.
          ASSIGN    CURRENT+DBFDTAX+DBFDTOT-DBFDPAID,CURRENT
          GOTO      DEBOUT10
DEBOUT30  ASSIGN    BALTOT30+DBFDTAX+DBFDTOT-DBFDPAID,BALTOT30
          GOTO      DEBOUT10
DEBOUT60  ASSIGN    BALTOT60+DBFDTAX+DBFDTOT-DBFDPAID,BALTOT60
          GOTO      DEBOUT10
DEBOUT90  ASSIGN    BALTOT90+DBFDTAX+DBFDTOT-DBFDPAID,BALTOT90
          GOTO      DEBOUT10
.
DEBOUT95  ASSIGN    CURRENT+BALTOT30+BALTOT60+BALTOT90,TOTALOUT
.
          RETURN
.
.----------------------------------------------------------------------
. SETOUT00 - Set up dates for Outstanding Balance Calculation
.----------------------------------------------------------------------
SETOUT00  ENDSET    ASATDATE
          APPEND    SP70,ASATDATE
          RESET     ASATDATE
          MATCH     SP70,ASATDATE
          IF        @EQUAL
            PACK      ASATDATE,CCC,CYY,CMM,CDD
          ENDIF
          REP       " 0",ASATDATE
          UNPACK    ASATDATE,CCENT,CYEAR,CMON,CDAY
.
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON
.
          SUB       "30",JULDAY
.
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      ASAT30,CC,YY,MM,DD
          REP       " 0",ASAT30
.
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON
.
          SUB       "60",JULDAY
.
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      ASAT60,CC,YY,MM,DD
          REP       " 0",ASAT60
.
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON
.
          SUB       "90",JULDAY
.
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      ASAT90,CC,YY,MM,DD
          REP       " 0",ASAT90
.
          RETURN
.
