. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS60                                             *
. * Function  :   Print Type Code Listing                              *
. **********************************************************************
. * Date      :   07.03.1991                                           *
. * Author    :   Neeriem Dye                                          *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
.$$$$$$
.        PROGRAM IBAFMS60
.             open files and initialise variables
.             WHILE user wants to print reports DO
.                  set line pointer to end of page
.                  FOR each record on file DO
.                       IF at end of page 
.                            THEN print header and reset line pointer
.                       print record
.                  END FOR
.                  IF nothing printed
.                       THEN print header and reset line pointer
.                  print end of report message
.             END WHILE
.        END
.$$$$$$
. Standard Accounting Include
.----------------------------
          INC       FMSSTDDF
          INC       IBAESCFD/INC       * Printer Type Code
          INC       IBAPTYFD/INC       * Print Type Code
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
.
ALAB      INIT      "            "                * labels for screen A
ALAB1     INIT      "Print Report"
Z20       INIT      "ZZZZZZZZZZZZZZZZZZZZ"
.
ASCII     DIM       35       * ASCII decimal Equiv
OPTIONA   FORM      1        * option on screen A
LASTCODE  DIM       3        * last print type
PRTTYP    DIM       3        * print type
START     FORM      1        * 1=start of page
.
PRGID     INIT      "IBAFMS60"
PRGNAM    INIT      "Print Type Code Listing"
VERSION   INIT      "V12.00.00"
.******************************************************************************
. MAINLINE - Controlling Logic
.******************************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      SELA0000      * display screen A and get "report by" option
          BRANCH    EXIT,ML9999   * user wants to exit screen
.
          CALL      PRNT0000      * print report
          GOTO      ML1000        * get another option
.
ML9999    CHAIN     PGM
.**********************************************************************
. INIT - Open Files                                Called by ML
.**********************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening ibaescaf";  * Printer Type Code
          OPEN      IBAESCA1,"ibaescaf"
          OPEN      IBAESCA2,"ibaescaf"
.
          DISPLAY   *P56:24,"Opening ibaptyaf";  * Print Type Code
          OPEN      IBAPTYA1,"ibaptyaf"
.
          DISPLAY   *P1:24,*EL;
.
INIT9999  RETURN
.**********************************************************************
. SELA -  display screen A and get "print by" option   Called By ML
.         Returns : EXIT       (0=continue, 1=quit)
.**********************************************************************
SELA0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = ",ALAB1:
                    *P6:7,"Select Option :";
.
SELA1000  KEYIN     *P22:7,"_",*EL,*P22:7,*V2LON,OPTIONA; * read option
          COMPARE   OPTIONA,ZERO
          GOTO      SELA9500 IF EQUAL            * user wants to exit ?
.
          PACK      ALAB,SP70                    * clear screen label
          LOAD      ALAB,OPTIONA,ALAB1           * get screen label
.
          MATCH     SP70,ALAB
          GOTO      SELA9000 IF NOT EQUAL        * legal option ?
.
          BEEP                                   * illegal option
          GOTO      SELA1000                     * get another option
.
SELA9000  MOVE      ZERO,EXIT                    * continue
          GOTO      SELA9999
.
SELA9500  MOVE      ONE,EXIT                     * quit
.
SELA9999  RETURN
.********************************************************************
. PRNT - print report                               Called by ML
.********************************************************************
PRNT0000  DISPLAY   *P50:24,"Printing :   ";
          MOVE      ONE,START                    * reset start of page indic
          MOVE      ZERO,CPAGENO                 * reset page no.
          CLOCK     TIME,CTIMEIS                 * get current time
          MOVE      "999",CLNO                   * init last line number
.
          CALL      FILE0000                    * go to correct position in file
.
PRNT1000  CALL      NREC0000                     * get next record
          BRANCH    OVRCD,PRNT9000               * end of file ?
.
          DISPLAY   *P61:24,*V2LON,IBESPR,*EL;
          COMPARE   "55",CLNO                    * end of page ?
          CALL      HEAD0000 IF NOT LESS
.
          CALL      WRIT0000                     * print details
          GOTO      PRNT1000                     * get next record
.
PRNT9000  COMPARE   ZERO,CPAGENO
          CALL      HEAD0000 IF EQUAL          * print header if nothing printed
          CALL      UND132                       * print line
          PRINT     "****   End of Report   ****"
.
PRNT9999  RETURN
.**********************************************************************
.  FILE - Set file pointer                             Called By PRNT
.**********************************************************************
FILE0000  
.
.---- print by type 1 ----
.
FILE0100  PACK      KEY5,SP3,Z20,SP70
          CALL      RSIBES2
          MOVE      SP70,LASTCODE
          GOTO      FILE9999
.
FILE9999  RETURN
.**********************************************************************
.  NREC - Get next record                              Called By PRNT
.**********************************************************************
NREC0000  
.
.---- print by type 1 ----
.
NREC0100  CALL      RKIBES2
          BRANCH    OVRCD,NREC9500
          MATCH     SP70,IBESPT
          GOTO      NREC0100 IF EQUAL       * header record ?
.
          PACK      PRTTYP,IBESPT,SP70      * save data
          PACK      ASCII,IBESSEQ,SP70
.
          PACK      KEY5,IBESPR,SP70
          CALL      RDIBES1                 * get printer type
          BRANCH    OVRCD,NREC0100          * printer type found ?
.
          GOTO      NREC9000
.
NREC9000  MOVE      ZERO,OVRCD         * continue
          GOTO      NREC9999
.
NREC9500  MOVE      ONE,OVRCD          * quit
.
NREC9999  RETURN
.********************************************************************
. HEAD - print report                               Called by PRNT
.********************************************************************
HEAD0000  CALL      HEAD132                      * print heading
.
          MOVE      ONE,START                    * reset start of page indic
          CALL      UND132                       * print line
          PRINT     "| Print Type                               |":
                    " Printer Type               ":
                    "             | ASCII Decimal Equivalent":
                    *132,"|"
          ADD       "1",CLNO
          CALL      UND132                       * print line
.
          MOVE      SP70,LASTCODE                * clear last code printed
.
HEAD9999  RETURN
.********************************************************************
. WRIT - print data line                            Called by PRNT
.********************************************************************
WRIT0000  UNPACK    SP70,IBPTDESC,IBPTCODE  * get print type desc
          MATCH     PRTTYP,LASTCODE         * new print type ?
          GOTO      WRIT1000 IF EQUAL
          PACK      KEY3,PRTTYP,SP70
          CALL      RDIBPT1
          MOVE      PRTTYP,LASTCODE
.
WRIT1000  MATCH     SP70,IBPTCODE      * print blank line
          GOTO      WRIT2000 IF EQUAL
          BRANCH    START,WRIT2000
          PRINT     "| ",SP10,SP30," | ":
                    SP9,SP30," | ":
                    *132,"|"
          ADD       "1",CLNO           * add one to line number
.
WRIT2000  MOVE      ZERO,START         * clear start of page indicator
          PRINT     "| ",IBPTCODE,SP2,IBPTDESC," | ":
                    IBESPR,SP2,IBESSEQ," | ",ASCII:
                    *132,"|"
          ADD       "1",CLNO           * add one to line number
.
WRIT9999  RETURN
.*********************************************************************
.  Redisplay for standard keyins
.*********************************************************************
RXXXFM00  RETURN
.*********************************************************************
.  INCLUDES FOR I/O'S
.*********************************************************************
.
.---------------------------------
. Standard Accounting Code include
.---------------------------------
          INC       FMSSTDCD
          INC       IBAESCIO/INC       * Printer Type Code
          INC       IBAPTYIO/INC       * Print Type Code
.
