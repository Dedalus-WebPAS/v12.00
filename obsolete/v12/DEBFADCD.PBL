.----------------------------------------------------------------------
.    File          : DEBFADCD.PBL
.    
.    Function      : Include to Format Debtors Name & Address into 5 Lines    
.                    for Display & Invoice Printing
.
.    Modifications : V6.00.00 04.04.95 B.G.Ackland
.                                      Copied from old Sundry Debtors.
.                    V6.00.01 27.08.96 Sandra Barcham
.                                      Fix line address 3 overwriting add 2
.
.    Operation     : Read Debtors Record before Calling Routine
.                    Include DEBINVDF.PBL required
.----------------------------------------------------------------------
. Format Name & Address Lines
. Returns Debtors Name & Address formatted into 5 Lines
. NADLINE1, NADLINE3, NADLINE3, NADLINE4, NADLINE5
.----------------------------------------------------------------------
FORAD000  MOVE      SP70,NADLINE1
          MOVE      SP70,NADLINE2
          MOVE      SP70,NADLINE3
          MOVE      SP70,NADLINE4
          MOVE      SP70,NADLINE5
.
          MOVE      DBDBNA1,NADLINE1
          MATCH     SP70,DBDBNA2
          GOTO      FORAD100 IF EQUAL 
          MOVE      DBDBNA2,NADLINE2
.
          MATCH     SP70,DBDBAD1
          GOTO      FORAD200 IF EQUAL
          MOVE      DBDBAD1,NADLINE3
.
          MATCH     SP70,DBDBAD2
          GOTO      FORAD300 IF EQUAL
          MOVE      DBDBAD2,NADLINE4
.
          MATCH     SP70,DBDBAD3
          GOTO      FORAD400 IF EQUAL
.
          MOVE      DBDBAD3,NADLINE5
.
          MATCH     SP70,DBDBAD4
          GOTO      FORAD900 IF EQUAL
.
          STRIP     DBDBAD3
          MOVE      "  ",KEY2
          PACK      NADLINE5,DBDBAD3,KEY2,DBDBAD4
          GOTO      FORAD900
.
. Blank Name Line 2
.
FORAD100  MOVE      DBDBAD1,NADLINE2
          MOVE      DBDBAD2,NADLINE3
          MOVE      DBDBAD3,NADLINE4
          MOVE      DBDBAD4,NADLINE5
          GOTO      FORAD900
.
. Blank Address Line 1
.
FORAD200  MOVE      DBDBAD2,NADLINE3
          MOVE      DBDBAD3,NADLINE4
          MOVE      DBDBAD4,NADLINE5
          GOTO      FORAD900
.
. Blank Address Line 2
.
FORAD300  MOVE      DBDBAD3,NADLINE4
          MOVE      DBDBAD4,NADLINE5
          GOTO      FORAD900
.
. Blank Address Line 3
.
FORAD400  MOVE      DBDBAD4,NADLINE5
          GOTO      FORAD900
.
FORAD900  MATCH     SP8,DBDBPC
          GOTO      FORAD999 IF EQUAL
.
          MATCH     SP70,NADLINE5
          IF        @EQUAL
            MATCH     SP70,NADLINE4
            IF        @EQUAL
              STRIP   NADLINE3
              ENDSET  NADLINE3
              APPEND  "  ",NADLINE3
              APPEND  DBDBPC,NADLINE3
              APPEND  SP70,NADLINE3
              RESET   NADLINE3
            ELSE
              STRIP   NADLINE4
              ENDSET  NADLINE4
              APPEND  "  ",NADLINE4
              APPEND  DBDBPC,NADLINE4
              APPEND  SP70,NADLINE4
              RESET   NADLINE4
            ENDIF
          ELSE
            STRIP   NADLINE5
            ENDSET  NADLINE5
            APPEND  "  ",NADLINE5
            APPEND  DBDBPC,NADLINE5
            APPEND  SP70,NADLINE5
            RESET   NADLINE5
          ENDIF
FORAD999  RETURN
.
