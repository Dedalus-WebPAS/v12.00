.---------------------------------------------------------------------------
          INC       STDGENDF     * Required Include
.
VERSION   INIT      "V12.00.00"
.-------------------------------------------------------
. V9.02.00  04.07.2002  Glenn Saunders
.           Export to new release i.e. v9.02 from r5.09
.---------------------------------------------------------------------------
. V5.09.01  08/08/2001  Davin
.           Recompiled for change to CCSEPSFD
.---------------------------------------------------------------------------
. V5.04.03  28.07.1996 B.G.Ackland
.           New Fields for Southern Health
. V5.04.02  14.03.1996 B.G.Ackland
.           Split Cost Models
. V5.04.01  11.02.1996 B.G.Ackland
.           Extend Episode file.
. V6.00.05  13.07.1993 B.G.Ackland
.           Extend Episode file.
.           Fix Quartile Calculation
.---------------------------------------------------------------------------
.@@@@@ Start of Generated Code @@@@@
. NB - Code & Comments Included Before the above line 
.      will not be overwritten by the generation of a 
.      program. The STDGENDF file and VERSION must appear
.      before the line. 
.-----------------------------------------
.   Program       :   IBACCR65
.   Function      :   Trim Casemix Extract                    
.   Generated  On :   04/07/2002
.-----------------------------------------
.=========================
.File Description Includes
.-------------------------
          INC       CCR65TFD    
          INC       CCSCEAFD/INC
          INC       CCSCEBFD/INC
          INC       CCSCECFD/INC
          INC       CCSCEDFD/INC
          INC       CCSCEEFD/INC
          INC       CCSCEFFD/INC
          INC       CCSCEGFD/INC
          INC       CCSCMAFD/INC
          INC       CCSEPSFD/INC
          INC       CCSREPDF    
          INC       CCSSTADF    
          INC       CCSSTYFD/INC
          INC       CCSXHDFD/INC
.=========================
.Local Variable Definition
.-------------------------
TRIMLIM   DIM       1 * Scr 00 Fld     5
TRIMLEVL  FORM      1 * Scr 00 Fld     6
TRIMPERC  FORM      2.2 * Scr 00 Fld     7
TRIMBASE  FORM      1 * Scr 00 Fld     8
TRIMDESC  DIM      20 * Scr 00 Fld     9
CALCPERC  FORM      1 * Scr 00 Fld    11
TLDESC    DIM      25 * Scr 00 Fld    12
.
ACCEPT    FORM      1
SACCEPT   FORM      1
.
PRGID     INIT      "IBACCR65"
PRGNAM    INIT      "Trim Casemix Extract                    "
.
.--------------------------------
. MAINLINE - Controlling Logic
.--------------------------------
          TOPIC     PRGID
          CLOCK     PORT,DIM2
          MOVE      DIM2,PORT
ML0000
          CALL      INIT0000
          CALL      SC00L000
          CHAIN     PGM
.---------------------------------------
. INIT - Open Files & Display Heading
.---------------------------------------
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CCENTRY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          DISPLAY   *P56:24,"Opening scrsbga1";
          OPEN      SCRSBGA1,"scrsbgaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA1,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA2,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrslta1";
          OPEN      SCRSLTA1,"scrsltaf"
          MOVE      "   64",AUDTSECT
          MOVE      " 37",AUDTPOST
          READ      CONTROLF,AUDTSECT;*AUDTPOST,AUDTFLAG
          IF        AUDTFLAG=1
            DISPLAY   *P56:24,"Opening ccsaudep";
            OPEN      CCSAUDEP,"ccsaudep"
          ENDIF
          DISPLAY   *P56:24,"Opening ccsceaaf";
          OPEN      CCSCEAA1,"ccsceaaf"
          DISPLAY   *P56:24,"Opening ccscmaaf";
          OPEN      CCSCMAA1,"ccscmaaf"
          DISPLAY   *P56:24,"Opening ccsepsaf";
          OPEN      CCSEPSA1,"ccsepsaf"
          DISPLAY   *P56:24,"Opening ccsepsaf";
          OPEN      CCSEPSA2,"ccsepsaf"
          DISPLAY   *P56:24,"Opening ccsepsaf";
          OPEN      CCSEPSA3,"ccsepsaf"
          DISPLAY   *P56:24,"Opening ccsepsaf";
          OPEN      CCSEPSA4,"ccsepsaf"
          DISPLAY   *P56:24,"Opening ccsepsaf";
          OPEN      CCSEPSA5,"ccsepsaf"
          DISPLAY   *P56:24,"Opening ccsstyaf";
          OPEN      CCSSTYA1,"ccsstyaf"
          DISPLAY   *P56:24,"Opening ccsxhdaf";
          OPEN      CCSXHDA1,"ccsxhdaf"
          DISPLAY   *P56:24,"Opening ccsxhdaf";
          OPEN      CCSXHDA2,"ccsxhdaf"
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.
.------------------------------------
. Called for Print Screen Routine 
.------------------------------------
PRTSVARS
          MATCH     "00",SCRID
          IF         @EQUAL
            CALL       PS00L000   * Options                            
          ENDIF
          RETURN
.--------------------------------
. Screen - Options                            
.--------------------------------
SC00L000
SC00L100
          MOVE      "00",SCRID
          MOVE      ZERO,PRCOL
          MOVE      ZERO,PRVERT
          MOVE      "    1",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SCPSCOL,PRCOL
            MOVE     SCPSROW,PRVERT
          ENDIF
          CALL      SBACK000 * Display Background
.
SC00L200
          CALL      CF00L000 * Clear Fields
          CALL      MVACCEPT
SC00L300
          CALL      KY00L000 * Enter Key Fields
          BRANCH    EXIT,SC00L999,SC00L999
SC00L400
          CALL      WP00L000 * Load any WP Details
SC00L500
          CALL      LM00L000 * Load any Batch Details
SC00L600
          CALL      DF00L000 * Display Fields
SC00L700
          CALL      SF00L000 * Select Fields
          BRANCH    EXIT,SC00L100,SC00L100,SC00L200
          IF        EXIT=4
            MOVE      ONE,EXIT
          ENDIF
SC00L800
SC00L900
SC00L999  RETURN
.--------------------------------------------------------
. Clear Fields for Screen : Options                            
.-------------------------------------------------------
CF00L000
          MOVE      SP70,CCEAEID 
          MOVE      SP70,TRIMLIM 
          MOVE      ZERO,TRIMLEVL
          MOVE      ZERO,TRIMPERC
          MOVE      ZERO,TRIMBASE
          MOVE      SP70,TRIMDESC
          MOVE      SP70,CCEADES 
          MOVE      ZERO,CALCPERC
          MOVE      SP70,TLDESC  
          MOVE      SP70,CCSTDES 
CF00L999  RETURN
.--------------------------------------------------------
. Enter Key Fields for Options                            
.--------------------------------------------------------
KY00L000
          MOVE      ZERO,EXIT
          GOTO      KY00L999
.
KY00L900  MOVE      ONE,EXIT
KY00L999  RETURN
.---------------------------------------------------------
. Load WP files for : Options                            
.---------------------------------------------------------
WP00L000
          RETURN
.---------------------------------------------------------
. Load Batch Screen for : Options                            
.---------------------------------------------------------
LM00L000
          RETURN
.---------------------------------------------------------
. Display Fields for : Options                            
.---------------------------------------------------------
DF00L000
          BRANCH   ACCEPT,DF00L999
          PACK     KEY15,PRGID,SCRID,SP70
          CALL     RSSCPS1
DF00L010
          CALL     RKSCPS1
          BRANCH   OVRCD,DF00L900
          MATCH    PRGID,SCPSPID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SCRID,SCPSSID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SP70,SCPSMTY
          GOTO     DF00L010 IF NOT EQUAL
.
          CALL     PS00L000 * Display Field
          GOTO     DF00L010
DF00L900
DF00L999
          RETURN
.---------------------------------------------------------
. Display/Print Fields for : Options                            
.---------------------------------------------------------
PS00L000   PACK      VAR,SP70,SP70
          PACK     PRINTVAR,SP70,SP70
          MOVE     SCPSITM,FIELDNO
          BRANCH    FIELDNO,PS00L999,PS00L999,PS00L999,PS00L101:
                            PS00L102,PS00L103,PS00L104,PS00L105:
                            PS00L106,PS00L107,PS00L108,PS00L109:
                            PS00L110
          GOTO     PS00L999
.
PS00L101
          MOVE      CCEAEID,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L102
          MOVE      TRIMLIM,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L103
          MOVE      TRIMLEVL,VAR     
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L104
          MOVE      TRIMPERC,VAR     
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L105
          MOVE      TRIMBASE,VAR     
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L106
          MOVE      TRIMDESC,VAR     
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L107
          MOVE      CCEADES,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L108
          MOVE      NO,YESNO
          MOVE      ZERO,FORM2
          MOVE      CALCPERC,FORM2   
          LOAD      YESNO,FORM2,YES
          MOVE      YESNO,VAR
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L109
          MOVE      TLDESC,VAR       
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L110
          MOVE      CCSTDES,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
PS00L999
          RETURN
.---------------------------------------------------------
. Selections for : Options                            
.---------------------------------------------------------
SF00L000  PACK     KEY13,PRGID,SCRID,SP70
          CALL     RSSCSL1
SF00L001  BRANCH   ACCEPT,SF00L020
.
SF00L005  CALL     MTSTQ000
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L006,SF00L101,SF00L102,SF00L103:
                            SF00L104,SF00L105,SF00L106,SF00L107:
                            SF00L006,SF00L006,SF00L108,SF00L006:
                            SF00L006
SF00L006  BEEP
          GOTO     SF00L005
SF00L020  CALL     RKSCSL1
          BRANCH   OVRCD,SF00L090
          MATCH    PRGID,SCSLPID
          GOTO     SF00L090 IF NOT EQUAL
          MATCH    SCRID,SCSLSID
          GOTO     SF00L090 IF NOT EQUAL
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L020,SF00L020,SF00L020,SF00L103:
                            SF00L104,SF00L105,SF00L106,SF00L107:
                            SF00L020,SF00L020,SF00L108,SF00L020:
                            SF00L020
          GOTO     SF00L020
SF00L090  MOVE     ZERO,ACCEPT
          GOTO     SF00L001
.
SF00L101
          MOVE      ZERO,EXIT
          GOTO      SF00L999
.
SF00L102
          CALL      POST0000
          MOVE      ZERO,EXIT
          GOTO      SF00L999
.
SF00L103
.
.
          MOVE      "   10",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,CCEADES 
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      EEID0000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L103
            ENDIF
            BRANCH    EXIT,SF00L103,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      CCEAEID,VAR      
          CALL      DISPITEM
.
.
          MOVE      "   10",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      CCEADES,VAR      
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L104
.
.
          MOVE      "   12",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,TLDESC  
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      ETLIM000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L104
            ENDIF
            BRANCH    EXIT,SF00L104,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      TRIMLIM,VAR      
          CALL      DISPITEM
.
.
          MOVE      "   12",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      TLDESC,VAR       
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L105
.
.
          MOVE      "   13",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,CCSTDES 
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      ELEVL000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L105
            ENDIF
            BRANCH    EXIT,SF00L105,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      TRIMLEVL,VAR     
          CALL      DISPITEM
.
.
          MOVE      "   13",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      CCSTDES,VAR      
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L106
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      EPERC000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L106
            ENDIF
            BRANCH    EXIT,SF00L106,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      TRIMPERC,VAR     
          CALL      DISPITEM
          GOTO     SF00L001
.
SF00L107
.
.
          MOVE      "    9",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,TRIMDESC
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      EFORM000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L107
            ENDIF
            BRANCH    EXIT,SF00L107,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      TRIMBASE,VAR     
          CALL      DISPITEM
.
.
          MOVE      "    9",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      TRIMDESC,VAR     
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L108
          CALL     SELECTED
          CALL      YORN0000
          MOVE       FYESNO,CALCPERC
          MOVE      NO,YESNO
          MOVE      ZERO,FORM2
          MOVE      CALCPERC,FORM2   
          LOAD      YESNO,FORM2,YES
          MOVE      YESNO,VAR
          CALL      DISPITEM
          GOTO     SF00L001
SF00L990  MOVE      FOUR,EXIT * Exit Char Exit
SF00L999  RETURN
.@@@@@ Function Restart Point @@@@@
.----------------------------------------
. Move one to accept to for insert mode
.----------------------------------------
MVACCEPT  MOVE      ONE,ACCEPT
          RETURN
.------------------------------------------------------------
. Save Trim Details
.------------------------------------------------------------
POST0000  CALL      UNTRIM00      * Untrim Extract Records.
.
          MOVE      "99",CLNO
          MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
.
          CALL      PROC0000      * Process.
.
          CALL      CLEAR000      * Clear Summary Files.
          BRANCH    EXIT,POST9999
.
          CALL      SUMM0000      * Summarize Levels.
.
          CALL      STAT0000      * Update Statistics.
.
          IF        CPAGENO>0
            CALL      UND132
            PRINT     *50,"****  End of Report ****"
          ENDIF
.
POST9999  RETURN
.------------------------------------------------------------
. Un Trim Extract ie Turn off Exclusion.
.------------------------------------------------------------
UNTRIM00  PACK      KEY48,SP70
          DISPLAY   *P1:24,*EL,"Clearing : "
          MOVE      ZERO,TOTALEXT
          CALL      RSCCEC1
.
UNTRIM10  CALL      RKCCEC1
          BRANCH    OVRCD,UNTRIM99
          ADD       ONE,TOTALEXT
          IF        (TOTALEXT%100=0)
            DISPLAY   *P20:24,*V2LON,TOTALEXT
          ENDIF
          MOVE      ZERO,CCECEXC
          PACK      KEY18,CCECHCD,CCECEPS
          CALL      RDCCEP1
          IF        OVRCD=0
            MOVE      CCEPEXC,CCECEXC
          ENDIF
          CALL      UPCCEC1
.
          GOTO      UNTRIM10
.
UNTRIM99  RETURN
.------------------------------------------------------------
. Clear Summary Files
.------------------------------------------------------------
CLEAR000  DISPLAY  *P1:24,*EL,"Clearing Summary : "
          MOVE     "cceb",KEY4
          PACK     FILENAME,KEY4,CCEAEID
          DISPLAY  *P26:24,*EL,FILENAME
          REP      " 0",FILENAME
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     CCSCEBA1,FILENAME
          TRAPCLR  IO
          IF       OVRCD=0
            CLOSE    CCSCEBA1,DELETE
          ENDIF
          PACK     COMMAND,SGENISB,FILENAME,CCEBIDX
          EXECUTE  COMMAND,TASKID
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     CCSCEBA1,FILENAME
          TRAPCLR  IO
          IF       OVRCD=1
            BEEP
            MOVE     "Unable to Create Casemix Extract Files - ",DISPMSG
            CALL     MESSAGE1
            GOTO     CLEAR900
          ENDIF
.
          MOVE     "cced",KEY4
          PACK     FILENAME,KEY4,CCEAEID
          DISPLAY  *P26:24,*EL,FILENAME
          REP      " 0",FILENAME
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     CCSCEDA1,FILENAME
          TRAPCLR  IO
          IF       OVRCD=0
            CLOSE    CCSCEDA1,DELETE
          ENDIF
          PACK     COMMAND,SGENISB,FILENAME,CCEDIDX
          EXECUTE  COMMAND,TASKID
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     CCSCEDA1,FILENAME
          TRAPCLR  IO
          IF       OVRCD=1
            BEEP
            MOVE     "Unable to Create Casemix Extract Files - ",DISPMSG
            CALL     MESSAGE1
            GOTO     CLEAR900
          ENDIF
.
          MOVE     "ccee",KEY4
          PACK     FILENAME,KEY4,CCEAEID
          DISPLAY  *P26:24,*EL,FILENAME
          REP      " 0",FILENAME
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     CCSCEEA1,FILENAME
          TRAPCLR  IO
          IF       OVRCD=0
            CLOSE    CCSCEEA1,DELETE
          ENDIF
          PACK     COMMAND,SGENISB,FILENAME,CCEEIDX
          EXECUTE  COMMAND,TASKID
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     CCSCEEA1,FILENAME
          TRAPCLR  IO
          IF       OVRCD=1
            BEEP
            MOVE     "Unable to Create Casemix Extract Files - ",DISPMSG
            CALL     MESSAGE1
            GOTO     CLEAR900
          ENDIF
          MOVE     ZERO,EXIT
          GOTO     CLEAR999
CLEAR900  MOVE     ONE,EXIT
CLEAR999  RETURN
.------------------------------------------------------------
. Close and Delete Temp Sort File
.------------------------------------------------------------
CLSTMP00  DISPLAY  *P1:10;
          CLOSE    CCSTMPA1,DELETE
          RETURN
.------------------------------------------------------------
. Create Sort File
.------------------------------------------------------------
OPNTMP00  DISPLAY  *P1:10;
          CLEAR    FILENAME
          APPEND   "CCR65A",FILENAME
          APPEND   PORT,FILENAME
          RESET    FILENAME
          REP      " 0",FILENAME
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     CCSTMPA1,FILENAME
          TRAPCLR  IO
          IF       OVRCD=0
            CLOSE    CCSTMPA1,DELETE
          ENDIF
.
          CLEAR    COMMAND
          APPEND   ISBUILD,COMMAND
          APPEND   FILENAME,COMMAND
          APPEND   " 29 U1-10,11-12,13-28",COMMAND
          RESET    COMMAND
          EXECUTE  COMMAND,TASKID
          OPEN     CCSTMPA1,FILENAME
.
          RETURN
.------------------------------------------------------------
. Process at Specified Trim Level
.------------------------------------------------------------
PROC0000  DISPLAY   *P1:24,*EL,"Processing : "
          MOVE      ZERO,TOTALEXT
          BRANCH    TRIMLEVL,PROC1000,PROC2000,PROC3000
.
PROC1000  PACK      KEY10,SP70
          CALL      RSCCEB1
PROC1100  CALL      RKCCEB1
          BRANCH    OVRCD,PROC9999
          ADD       ONE,TOTALEXT
          IF        (TOTALEXT%100=0)
            DISPLAY   *P23:24,*V2LON,TOTALEXT
          ENDIF
          PACK      KEY30,CCEBLV1,SP70
          CALL      ITEM0000
          GOTO      PROC1100
.
PROC2000  PACK      KEY20,SP70
          CALL      RSCCED1
PROC2100  CALL      RKCCED1
          BRANCH    OVRCD,PROC9999
          ADD       ONE,TOTALEXT
          IF        (TOTALEXT%100=0)
            DISPLAY   *P23:24,*V2LON,TOTALEXT
          ENDIF
          PACK      KEY30,CCEDLV1,CCEDLV2,SP70
          CALL      ITEM0000
          GOTO      PROC2100
.
PROC3000  PACK      KEY20,SP70
          CALL      RSCCEE1
PROC3100  CALL      RKCCEE1
          BRANCH    OVRCD,PROC9999
          ADD       ONE,TOTALEXT
          IF        (TOTALEXT%100=0)
            DISPLAY   *P23:24,*V2LON,TOTALEXT
          ENDIF
          PACK      KEY30,CCEELV1,CCEELV2,CCEELV3,SP70
          CALL      ITEM0000
          GOTO      PROC3100
.
PROC9999  RETURN
.------------------------------------------------------------
. Process an Item
.------------------------------------------------------------
ITEM0000  CALL      OPNTMP00
.
          CALL      SORT0000
          BRANCH    EXIT,ITEM1000         * Insufficent Data for Trim
.
          CALL      QUAR0000            * Calculate Quartiles and Trim Points
          BRANCH    EXIT,ITEM1000       * Insufficent Data for Trim
.
          CALL      TRIM0000              * Trim Epsiodes in CEC File
          BRANCH    EXIT,ITEM1000         * NO Episodes Trimmed
.
ITEM1000  CALL      CLSTMP00
.
          RETURN
.------------------------------------------------------------
. Sort Episodes into Temp file by Trim Basis
.------------------------------------------------------------
SORT0000  MOVE      ZERO,TOTALEPS
          PACK      KEY48,KEY30,SP70
          CALL      RSCCEC2
SORT1000  CALL      RKCCEC2
          BRANCH    OVRCD,SORT2000
          BRANCH    TRIMLEVL,SORT1010,SORT1020,SORT1030
.
SORT1010  PACK      KEY48,CCECLV1,SP70
          GOTO      SORT1090
.
SORT1020  PACK      KEY48,CCECLV1,CCECLV2,SP70
          GOTO      SORT1090
.
SORT1030  PACK      KEY48,CCECLV1,CCECLV2,CCECLV3,SP70
.
SORT1090  MATCH     KEY30,KEY48
          GOTO      SORT2000 IF NOT EQUAL
.
          BRANCH    TRIMBASE,SORT1100,SORT1200,SORT1300
.
SORT1100  MOVE      CCECLOS,F7P2
          PACK      KEY10,F7P2
          GOTO      SORT1900
.
SORT1200  ASSIGN    CCECFTC,F7P2
          PACK      KEY10,F7P2
          GOTO      SORT1900
.
SORT1300  ASSIGN    CCECTTC,F7P2
          PACK      KEY10,F7P2
          GOTO      SORT1900
.
SORT1900  PACK      KEY28,KEY10,CCECHCD,CCECEPS
          UNPACK    KEY28,CCTMKEY,CCTMHCD,CCTMEPS
          CALL      RDCCTM1
          IF        OVRCD=1
            CALL      WRCCTM1
            ADD       ONE,TOTALEPS
          ENDIF
          GOTO      SORT1000
.
SORT2000  IF        TOTALEPS<5
            GOTO      SORT9500
          ENDIF
SORT9000  MOVE      ZERO,EXIT
          GOTO      SORT9999
SORT9500  MOVE      ONE,EXIT
SORT9999  RETURN
.------------------------------------------------------------
. Calculate Quartiles and Trim Points
.------------------------------------------------------------
. Calculate Quartile Positions from Top and Bottom of File
.---------------------------------------------------------
QUAR0000  COMPARE   ZERO,CALCPERC
          GOTO      QUAR8000 IF EQUAL
.
          ASSIGN    ((TOTALEPS)/4.0)+0.75,LQCNT
          ASSIGN    TOTALEPS-LQCNT+1.00,UQCNT
.
. Calculate Lower Quartile.
.---------------------------
          PACK      KEY28,SP70
          MOVE      ZERO,SECTOR
          CALL      RSCCTM1
QUAR1000  CALL      RKCCTM1
          BRANCH    OVRCD,QUAR9500   * Not Enough Data
          ADD       ONE,SECTOR
          IF        (SECTOR=LQCNT)
            SQUEEZE   CCTMKEY
            MOVE      CCTMKEY,LQTRAMT  * Quartile in exact position
            GOTO      QUAR1900
          ENDIF
          IF        (SECTOR>LQCNT)
            SQUEEZE   CCTMKEY
            MOVE      CCTMKEY,LQTRMAX  * Interpolated
            ASSIGN    LQTRMIN+((LQTRMAX-LQTRMIN)*(LQCNT-SECTOR+1)),LQTRAMT
            GOTO      QUAR1900
          ENDIF
          SQUEEZE   CCTMKEY
          MOVE      CCTMKEY,LQTRMIN
          GOTO      QUAR1000
QUAR1900
.
. Calculate Upper Quartile.
.---------------------------
          PACK      KEY28,Z70
          MOVE      TOTALEPS,SECTOR
          ADD       ONE,SECTOR
          CALL      RSCCTM1
QUAR2000  CALL      RPCCTM1
          BRANCH    OVRCD,QUAR9500   * Not Enough Data
          SUB       ONE,SECTOR
          IF        (SECTOR=UQCNT)
            SQUEEZE   CCTMKEY
            MOVE      CCTMKEY,UQTRAMT  * Quartile in exact position
            GOTO      QUAR2900
          ENDIF
          IF        (SECTOR<UQCNT)
            SQUEEZE   CCTMKEY
            MOVE      CCTMKEY,UQTRMIN  * Interpolated
            ASSIGN    UQTRMIN+((UQTRMAX-UQTRMIN)*(UQCNT-SECTOR)),UQTRAMT
            GOTO      QUAR2900
          ENDIF
          SQUEEZE   CCTMKEY
          MOVE      CCTMKEY,UQTRMAX
          GOTO      QUAR2000
.-------------------------------------------------------------------------
. Calculate Trim Points
.-----------------------
.
. Refer "DRGs 1991-92" Dept of Health, Vic. Nov 1992. page 60.
.
. Upper Trim Point TRIMUP = 2.5*UQTRAMT - 1.5*LQTRAMT
. Lower Trim Point TRIMLW = 2.5*LQTRAMT - 1.5*UQTRAMT
.
. LQTRAMT = Lower Quartile Amount
. UQTRAMT = Upper Quartile Amount
.-------------------------------------------------------------------------
QUAR2900  ASSIGN    (2.5*UQTRAMT)-(1.5*LQTRAMT),TRIMUP
          ASSIGN    (2.5*LQTRAMT)-(1.5*UQTRAMT),TRIMLW
          MOVE      ZERO,TRIMNUMB
          GOTO      QUAR9000
.
. Percenatge to Trim
.--------------------------------------
QUAR8000  ASSIGN    ((TOTALEPS*TRIMPERC)/100.0)/2.0,TRIMNUMB
          IF        TRIMNUMB<1
            GOTO       QUAR9500
          ENDIF
          MOVE      ZERO,TRIMUP
          MOVE      ZERO,TRIMLW
.
QUAR9000  CALL      PTRIM000
          MOVE      ZERO,EXIT
.
          GOTO      QUAR9999
QUAR9500  MOVE      ONE,EXIT
QUAR9999  RETURN
.------------------------------------------------------------
. Trim Data
.------------------------------------------------------------
TRIM0000  COMPARE   ZERO,CALCPERC      * Trim Based on Percentage at Top
          GOTO      TRIM2000 IF EQUAL  * and Bottom of Sorted List.
.
          PACK      KEY28,SP70
          MOVE      ZERO,SECTOR
          CALL      RSCCTM1
TRIM1000  CALL      RKCCTM1            * Quartile Trimming
          BRANCH    OVRCD,TRIM9999
          SQUEEZE   CCTMKEY
          MOVE      CCTMKEY,F7P2
.
          IF        (F7P2>TRIMUP)
            MATCH     "L",TRIMLIM      * If Lower Trim Only Don't Trim Upper
            IF        !@EQUAL
              PACK      KEY18,CCTMHCD,CCTMEPS
              CALL      RDCCEC1
              IF        OVRCD=0
                MOVE      ONE,CCECEXC * Exclude
                CALL      UPCCEC1
                CALL      PRTLIN00
              ENDIF
            ENDIF
          ENDIF
.
          IF        (F7P2<TRIMLW)
            MATCH     "U",TRIMLIM      * If Upper Trim Only Don't Trim Lower
            IF        !@EQUAL
              PACK      KEY18,CCTMHCD,CCTMEPS
              CALL      RDCCEC1
              IF        OVRCD=0
                MOVE      ONE,CCECEXC * Exclude
                CALL      UPCCEC1
                CALL      PRTLIN00
              ENDIF
            ENDIF
          ENDIF
          GOTO      TRIM1000
.
. Trim Based on Percentage - Lower Trim
.---------------------------------------
TRIM2000  ASSIGN    ((TOTALEPS*TRIMPERC)/100.0)/2.0,TRIMNUMB
          IF        TRIMNUMB<1
            GOTO       TRIM9999
          ENDIF
          MATCH     "U",TRIMLIM      * If Upper Trim Only Don't Trim Lower
          GOTO      TRIM3900 IF EQUAL
          PACK      KEY28,SP70
          MOVE      ZERO,SECTOR
          CALL      RSCCTM1
TRIM3000  CALL      RKCCTM1
          BRANCH    OVRCD,TRIM3900
          PACK      KEY18,CCTMHCD,CCTMEPS
          CALL      RDCCEC1
          IF        OVRCD=0
            MOVE      ONE,CCECEXC * Exclude
            CALL      UPCCEC1
            CALL      PRTLIN00
          ENDIF
          ADD       ONE,SECTOR
          IF        SECTOR<TRIMNUMB
            GOTO      TRIM3000
          ENDIF
.
. Trim Based on Percentage - Upper Trim
.---------------------------------------
TRIM3900  PACK      KEY28,Z70
          MATCH     "L",TRIMLIM      * If Lower Trim Only Don't Trim Upper
          GOTO      TRIM9999 IF EQUAL
          MOVE      ZERO,SECTOR
          CALL      RSCCTM1
TRIM4000  CALL      RPCCTM1
          BRANCH    OVRCD,TRIM9999
          PACK      KEY18,CCTMHCD,CCTMEPS
          CALL      RDCCEC1
          IF        OVRCD=0
            MOVE      ONE,CCECEXC * Exclude
            CALL      UPCCEC1
            CALL      PRTLIN00
          ENDIF
          ADD       ONE,SECTOR
          IF        SECTOR<TRIMNUMB
            GOTO      TRIM4000
          ENDIF
.
TRIM9999  RETURN
.--------------------------------------------------
. Calculate Summary Costs
.--------------------------------------------------
SUMM0000  DISPLAY   *P1:24,*EL,"Casemix Extract Level Summary : "
          PACK      KEY18,SP70
          MOVE      ZERO,TOTALEXT
          CALL      RSCCEC1
SUMM1000  CALL      RKCCEC1
          BRANCH    OVRCD,SUMM9000
          ADD       ONE,TOTALEXT
          IF        (TOTALEXT%100=0)
            DISPLAY   *P33:24,*V2LON,TOTALEXT
          ENDIF
.
          MATCH     "1",CCECEXC         * Excluded from Summary
          GOTO      SUMM1000 IF EQUAL
.
          CALL      UPDEB000
.
          MATCH     SP70,CCEALV2
          GOTO      SUMM1000 IF EQUAL
          CALL      UPDED000
.
          MATCH     SP70,CCEALV3
          GOTO      SUMM1000 IF EQUAL
          CALL      UPDEE000
          GOTO      SUMM1000
SUMM9000
SUMM9999  RETURN
.------------------------------------------------------------
. Update Casemix Level 1
.------------------------------------------------------------
UPDEB000  PACK      KEY10,CCECLV1
          CALL      RDCCEB1
          IF        OVRCD=1
            CALL      CLREB000
            MOVE      CCECLV1,CCEBLV1
            CALL      WRCCEB1
            CALL      RDCCEB1
          ENDIF
          ADD       ONE,CCEBNEP    * Increase Number of Episodes
          IF        CCECFTC>CCEBFMX
            MOVE      CCECFTC,CCEBFMX
          ENDIF
          IF        CCECFTC<CCEBFMN
            MOVE      CCECFTC,CCEBFMN
          ENDIF
.
          IF        CCECTTC>CCEBTMX
            MOVE      CCECTTC,CCEBTMX
          ENDIF
          IF        CCECTTC<CCEBTMN
            MOVE      CCECTTC,CCEBTMN
          ENDIF
.
          IF        CCECLOS>CCEBLMX
            MOVE      CCECLOS,CCEBLMX
          ENDIF
          IF        CCECLOS<CCEBLMN
            MOVE      CCECLOS,CCEBLMN
          ENDIF
.
          ADD       CCECFTC,CCEBFTC
          ADD       CCECTTC,CCEBTTC
          ADD       CCECLOS,CCEBLTO
          CALL      UPCCEB1
          RETURN
.------------------------------------------------------------
. Update Casemix Level 2
.------------------------------------------------------------
UPDED000  PACK      KEY20,CCECLV1,CCECLV2,SP70
          MATCH     SP70,KEY20
          GOTO      UPDED999 IF EQUAL
          CALL      RDCCED1
          IF        OVRCD=1
            CALL      CLRED000
            UNPACK    KEY20,CCEDLV1,CCEDLV2
            CALL      WRCCED1
            CALL      RDCCED1
          ENDIF
          ADD       ONE,CCEDNEP    * Increase Number of Episodes
          IF        CCECFTC>CCEDFMX
            MOVE      CCECFTC,CCEDFMX
          ENDIF
          IF        CCECFTC<CCEDFMN
            MOVE      CCECFTC,CCEDFMN
          ENDIF
.
          IF        CCECTTC>CCEDTMX
            MOVE      CCECTTC,CCEDTMX
          ENDIF
          IF        CCECTTC<CCEDTMN
            MOVE      CCECTTC,CCEDTMN
          ENDIF
.
          IF        CCECLOS>CCEDLMX
            MOVE      CCECLOS,CCEDLMX
          ENDIF
          IF        CCECLOS<CCEDLMN
            MOVE      CCECLOS,CCEDLMN
          ENDIF
.
          ADD       CCECFTC,CCEDFTC
          ADD       CCECTTC,CCEDTTC
          ADD       CCECLOS,CCEDLTO
          CALL      UPCCED1
UPDED999  RETURN
.------------------------------------------------------------
. Update Casemix Level 3
.------------------------------------------------------------
UPDEE000  PACK      KEY30,CCECLV1,CCECLV2,CCECLV3,SP70
          MATCH     SP70,KEY30
          GOTO      UPDEE999 IF EQUAL
          CALL      RDCCEE1
          IF        OVRCD=1
            CALL      CLREE000
            UNPACK    KEY30,CCEELV1,CCEELV2,CCEELV3
            CALL      WRCCEE1
            CALL      RDCCEE1
          ENDIF
          ADD       ONE,CCEENEP    * Increase Number of Episodes
          IF        CCECFTC>CCEEFMX
            MOVE      CCECFTC,CCEEFMX
          ENDIF
          IF        CCECFTC<CCEEFMN
            MOVE      CCECFTC,CCEEFMN
          ENDIF
.
          IF        CCECTTC>CCEETMX
            MOVE      CCECTTC,CCEETMX
          ENDIF
          IF        CCECTTC<CCEETMN
            MOVE      CCECTTC,CCEETMN
          ENDIF
.
          IF        CCECLOS>CCEELMX
            MOVE      CCECLOS,CCEELMX
          ENDIF
          IF        CCECLOS<CCEELMN
            MOVE      CCECLOS,CCEELMN
          ENDIF
.
          ADD       CCECFTC,CCEEFTC
          ADD       CCECTTC,CCEETTC
          ADD       CCECLOS,CCEELTO
          CALL      UPCCEE1
.
UPDEE999  RETURN
.------------------------------------------------------------
. Clear Summary File Details
.------------------------------------------------------------
CLREB000  MOVE      ZERO,CCEBNEP
          MOVE      ZERO,CCEBFTC
          MOVE      "999999999999",CCEBFMN
          MOVE      ZERO,CCEBFMX
          MOVE      ZERO,CCEBFSV
          MOVE      ZERO,CCEBTTC
          MOVE      "999999999999",CCEBTMN
          MOVE      ZERO,CCEBTMX
          MOVE      ZERO,CCEBTSV
          MOVE      "999999999999",CCEBLMN
          MOVE      ZERO,CCEBLMX
          MOVE      ZERO,CCEBLSV
          MOVE      ZERO,CCEBLTO
          RETURN
.
CLRED000  MOVE      ZERO,CCEDNEP
          MOVE      ZERO,CCEDFTC
          MOVE      "999999999999",CCEDFMN
          MOVE      ZERO,CCEDFMX
          MOVE      ZERO,CCEDFSV
          MOVE      ZERO,CCEDTTC
          MOVE      "999999999999",CCEDTMN
          MOVE      ZERO,CCEDTMX
          MOVE      ZERO,CCEDTSV
          MOVE      "999999999999",CCEDLMN
          MOVE      ZERO,CCEDLMX
          MOVE      ZERO,CCEDLSV
          MOVE      ZERO,CCEDLTO
          RETURN
.
CLREE000  MOVE      ZERO,CCEENEP
          MOVE      ZERO,CCEEFTC
          MOVE      "999999999999",CCEEFMN
          MOVE      ZERO,CCEEFMX
          MOVE      ZERO,CCEEFSV
          MOVE      ZERO,CCEETTC
          MOVE      "999999999999",CCEETMN
          MOVE      ZERO,CCEETMX
          MOVE      ZERO,CCEETSV
          MOVE      "999999999999",CCEELMN
          MOVE      ZERO,CCEELMX
          MOVE      ZERO,CCEELSV
          MOVE      ZERO,CCEELTO
          RETURN
.------------------------------------------------------------
. Calculate Statistics
.------------------------------------------------------------
STAT0000  PACK      KEY10,SP70
          DISPLAY   *P1:24,*EL,"Updating Statistics : "
          MOVE      ZERO,TOTALEXT
.
          CALL      STEB0000  * Level 1 Variance Calculation
          CALL      STED0000  * Level 2 Variance Calculation
          CALL      STEE0000  * Level 3 Variance Calculation
.
STAT9999  RETURN
.
.------------------------------------------------------------
. Update Variance Casemix Level 1
.------------------------------------------------------------
STEB0000  PACK      KEY10,SP70
          CALL      RSCCEB1
STEB1000  CALL      RKCCEB1
          BRANCH    OVRCD,STEB9999
.
          ADD       ONE,TOTALEXT
          IF        (TOTALEXT%100=0)
            DISPLAY   *P23:24,*V2LON,TOTALEXT
          ENDIF
.
          MOVE      ZERO,CCEBFSV
          MOVE      ZERO,CCEBTSV
          MOVE      ZERO,CCEBLSV
.
          PACK      KEY48,CCEBLV1,SP70
          CALL      RSCCEC2
STEB1100  CALL      RKCCEC2
          BRANCH    OVRCD,STEB1900
          MATCH     CCEBLV1,CCECLV1
          GOTO      STEB1900 IF NOT EQUAL
.
          MATCH     "1",CCECEXC         * Excluded from Summary
          GOTO      STEB1100 IF EQUAL
.
          ASSIGN    CCECFTC-(CCEBFTC/CCEBNEP),F12P4
          ASSIGN    CCEBFSV+(F12P4*F12P4),CCEBFSV
.
          ASSIGN    CCECTTC-(CCEBTTC/CCEBNEP),F12P4
          ASSIGN    CCEBTSV+(F12P4*F12P4),CCEBTSV
.
          ASSIGN    CCECLOS-(CCEBLTO/CCEBNEP),F12P4
          ASSIGN    CCEBLSV+(F12P4*F12P4),CCEBLSV
.
          GOTO      STEB1100
.
STEB1900  IF        CCEBNEP>1
            ASSIGN    CCEBFSV/(CCEBNEP-1),CCEBFSV
            ASSIGN    CCEBTSV/(CCEBNEP-1),CCEBTSV
            ASSIGN    CCEBLSV/(CCEBNEP-1),CCEBLSV
          ENDIF
          CALL      UPCCEB1
          GOTO      STEB1000
.
STEB9999  RETURN
.
.------------------------------------------------------------
. Update Variance Casemix Level 2
.------------------------------------------------------------
STED0000  PACK      KEY20,SP70
          CALL      RSCCED1
STED1000  CALL      RKCCED1
          BRANCH    OVRCD,STED9999
.
          ADD       ONE,TOTALEXT
          IF        (TOTALEXT%100=0)
            DISPLAY   *P23:24,*V2LON,TOTALEXT
          ENDIF
.
          MOVE      ZERO,CCEDFSV
          MOVE      ZERO,CCEDTSV
          MOVE      ZERO,CCEDLSV
.
          PACK      KEY48,CCEDLV1,CCEDLV2,SP70
          PACK      KEY20,CCEDLV1,CCEDLV2,SP70
          CALL      RSCCEC2
STED1100  CALL      RKCCEC2
          BRANCH    OVRCD,STED1900
          PACK      KEY48,CCECLV1,CCECLV2,SP70
          MATCH     KEY20,KEY48
          GOTO      STED1900 IF NOT EQUAL
.
          MATCH     "1",CCECEXC         * Excluded from Summary
          GOTO      STED1100 IF EQUAL
.
          ASSIGN    CCECFTC-(CCEDFTC/CCEDNEP),F12P4
          ASSIGN    CCEDFSV+(F12P4*F12P4),CCEDFSV
.
          ASSIGN    CCECTTC-(CCEDTTC/CCEDNEP),F12P4
          ASSIGN    CCEDTSV+(F12P4*F12P4),CCEDTSV
.
          ASSIGN    CCECLOS-(CCEDLTO/CCEDNEP),F12P4
          ASSIGN    CCEDLSV+(F12P4*F12P4),CCEDLSV
.
          GOTO      STED1100
.
STED1900  IF        CCEDNEP>1
            ASSIGN    CCEDFSV/(CCEDNEP-1),CCEDFSV
            ASSIGN    CCEDTSV/(CCEDNEP-1),CCEDTSV
            ASSIGN    CCEDLSV/(CCEDNEP-1),CCEDLSV
          ENDIF
          CALL      UPCCED1
          GOTO      STED1000
.
STED9999  RETURN
.
.------------------------------------------------------------
. Update Variance Casemix Level 3
.------------------------------------------------------------
STEE0000  PACK      KEY30,SP70
          CALL      RSCCEE1
STEE1000  CALL      RKCCEE1
          BRANCH    OVRCD,STEE9999
.
          ADD       ONE,TOTALEXT
          IF        (TOTALEXT%100=0)
            DISPLAY   *P23:24,*V2LON,TOTALEXT
          ENDIF
.
          MOVE      ZERO,CCEEFSV
          MOVE      ZERO,CCEETSV
          MOVE      ZERO,CCEELSV
.
          PACK      KEY48,CCEELV1,CCEELV2,CCEELV3,SP70
          PACK      KEY30,CCEELV1,CCEELV2,CCEELV3,SP70
          CALL      RSCCEC2
STEE1100  CALL      RKCCEC2
          BRANCH    OVRCD,STEE1900
          PACK      KEY48,CCECLV1,CCECLV2,CCECLV3,SP70
          MATCH     KEY30,KEY48
          GOTO      STEE1900 IF NOT EQUAL
.
          MATCH     "1",CCECEXC         * Excluded from Summary
          GOTO      STEE1100 IF EQUAL
.
          ASSIGN    CCECFTC-(CCEEFTC/CCEENEP),F12P4
          ASSIGN    CCEEFSV+(F12P4*F12P4),CCEEFSV
.
          ASSIGN    CCECTTC-(CCEETTC/CCEENEP),F12P4
          ASSIGN    CCEETSV+(F12P4*F12P4),CCEETSV
.
          ASSIGN    CCECLOS-(CCEELTO/CCEENEP),F12P4
          ASSIGN    CCEELSV+(F12P4*F12P4),CCEELSV
.
          GOTO      STEE1100
.
STEE1900  IF        CCEENEP>1
            ASSIGN    CCEEFSV/(CCEENEP-1),CCEEFSV
            ASSIGN    CCEETSV/(CCEENEP-1),CCEETSV
            ASSIGN    CCEELSV/(CCEENEP-1),CCEELSV
          ENDIF
          CALL      UPCCEE1
          GOTO      STEE1000
.
STEE9999  RETURN
.------------------------------------------------------------
. Page Heading
.------------------------------------------------------------
HEAD0000  CALL      HEAD132
          PRINT     "Casemix Extract         : ",CCEAEID,SP1,CCEADES:
                    *80,"Trim Limits             : ",TLDESC
          PRINT     "Trim at Extract Level   : ",TRIMLEVL,SP4,CCSTDES:
                    *80,"Trim Based On           : ",TRIMBASE,SP1,TRIMDESC
          MOVE      NO,KEY3
          LOAD      KEY3,CALCPERC,YES
          PRINT     "Percentage to Trim      : ",TRIMPERC:
                    *80,"Use Quartile Calc.      : ",KEY3
          CALL      UND132
          IF        CALCPERC=0
            PRINT     "| Extract Key                   Number to Trim":
                      "                | Episode             L.O.S ":
                      "      Cost (1)       Cost (2) Excluded",*132,"|"
          ELSE
            PRINT     "| Extract Key                 Trim Point Lower":
                      "         Upper  | Episode             L.O.S ":
                      "      Cost (1)       Cost (2) Excluded",*132,"|"
          ENDIF
          CALL      UND132
          RETURN
.------------------------------------------------------------
. Print Excluded Records
.------------------------------------------------------------
PRTLIN00  ADD       ONE,CLNO
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
          MOVE      NO,KEY3
          MATCH     "1",CCECEXC
          IF        @EQUAL
            MOVE      YES,KEY3
          ENDIF
          PRINT     "|",*63,"| ",CCECHCD,"/",CCECEPS,CCECLOS,CCECFTC,CCECTTC:
                    SP3,KEY3,*132,"|"
          RETURN
.------------------------------------------------------------
. Print Header Record for Trimming
.------------------------------------------------------------
PTRIM000  ADD       ONE,CLNO
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
          IF        CALCPERC=0
            PRINT     "| ",KEY30,SP4,TRIMNUMB,*63,"|",*132,"|"
          ELSE
            PRINT     "| ",KEY30,SP4,TRIMLW,SP4,TRIMUP,*63,"|",*132,"|"
          ENDIF
          RETURN
.----------------------------------------------------------------------
. Enter Casemix Extract ID
.----------------------------------------------------------------------
EEID0000  MOVE      ZERO,CKEYTYP
          CALL      CCSCOPCL         * Close Files
          CALL      KCCEAA00
          BRANCH    EXIT,EEID9999,EEID9999
.
          MATCH     SP70,CCEASCM1
          IF        !@EQUAL
            MATCH     "1",CCEAUST
            IF        !@EQUAL
             MOVE      "Trimming Not Enabled for Split Cost Extracts - ",DISPMSG
             BEEP
             CALL      MESSAGE1
             GOTO      EEID0000
            ENDIF
          ENDIF
.
          CALL      CCSCOPOP         * Open Files
EEID9999  RETURN
ETLIM000  KEYIN    *PCCOL:CVERT,*V2LON,TRIMLIM
          REP      UPPLOW,TRIMLIM
          MOVE     "Trim Upper Limit Only",TLDESC
          MATCH    "U",TRIMLIM
          GOTO     ETLIM999 IF EQUAL
          MOVE     "Trim Lower Limit Only",TLDESC
          MATCH    "L",TRIMLIM
          GOTO     ETLIM999 IF EQUAL
          MOVE     "Trim at Both Limits",TLDESC
          MATCH    "B",TRIMLIM
          GOTO     ETLIM999 IF EQUAL
          BEEP
          MOVE     "Valid Entries = U,L,B  - ",DISPMSG
          CALL     MESSAGE1
          GOTO     ETLIM000
ETLIM999  RETURN
.------------------------------------------------------------
. Enter Level
.------------------------------------------------------------
ELEVL000  KEYIN     *PCCOL:CVERT,*V2LON,TRIMLEVL
          BRANCH    TRIMLEVL,ELEVL110,ELEVL120,ELEVL130
          BEEP
          GOTO      ELEVL000
.
ELEVL110
          GOTO      ELEVL999
.
ELEVL120
          MATCH     SP70,CCEALV2
          IF        @EQUAL
            BEEP
            MOVE      "Extract Not Valid for this level - ",DISPMSG
            CALL      MESSAGE1
            GOTO      ELEVL000
          ENDIF
          GOTO      ELEVL999
.
ELEVL130
          MATCH     SP70,CCEALV3
          IF        @EQUAL
            BEEP
            MOVE      "Extract Not Valid for this level - ",DISPMSG
            CALL      MESSAGE1
            GOTO      ELEVL000
          ENDIF
          GOTO      ELEVL999
.
ELEVL999  RETURN
.
.------------------------------------------------------------
. Enter Percentage to Trim
.------------------------------------------------------------
EPERC000  MOVE      ZERO,TRIMPERC
          COMPARE   ONE,CALCPERC
          GOTO      EPERC999 IF EQUAL
.
          MOVE      SP70,VAR
          CALL      DISPITEM
          KEYIN     *PCCOL:CVERT,*V2LON,*NOMASK,*RV,TRIMPERC
.
EPERC999  RETURN
.------------------------------------------------------------
. Enter Format
.------------------------------------------------------------
EFORM000  MOVE      SP70,DISPMSG
          CALL      MESSAGE2
          DISPLAY   *P1:24,*EL,"Trim Based on : ":
                    *V2LON,"1",*HOFF,". Length of Stay, ":
                    *V2LON,"2",*HOFF,". Cost (1), ":
                    *V2LON,"3",*HOFF,". Cost (2)"
EFORM100  KEYIN     *PCCOL:CVERT,*V2LON,TRIMBASE
          BRANCH    TRIMBASE,EFORM110,EFORM120,EFORM130
          BEEP
          GOTO      EFORM100
.
EFORM110  MOVE      "Length of Stay    ",TRIMDESC
          GOTO      EFORM999
.
EFORM120  MOVE      "Cost (1)    ",TRIMDESC
          GOTO      EFORM999
.
EFORM130  MOVE      "Cost (2)     ",TRIMDESC
          GOTO      EFORM999
.
EFORM999  CALL      MESSAGE3
          RETURN
.
.-------------------------------
. Includes for I/O and Keyins
.-------------------------------
          INC       CCR65TIO    
          INC       CCSCEAIO/INC
          INC       CCSCEAKY    
          INC       CCSCEBIO/INC
          INC       CCSCECIO/INC
          INC       CCSCEDIO/INC
          INC       CCSCEEIO/INC
          INC       CCSCMAIO/INC
          INC       CCSCOPCD    
          INC       CCSEPSIO/INC
          INC       CCSSTACD    
          INC       CCSSTYIO/INC
          INC       CCSXHDIO/INC
          INC       CCSXHDKY    
.
          INC       STDGENCD
.
