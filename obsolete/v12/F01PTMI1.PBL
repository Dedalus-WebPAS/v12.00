.*****************************************************************************
.*    Program      : F01PTMI1                                                *
.*    Description  : Conversion patmi1af to new File layout                  *
.*                                                                           *
.*    Author       : Jill Parkinson                                          *
.*    Date         : 24/11/2010                                              *
.*    Modifications:                                                         *
.*        V10.01.01  24/01/2010 Jill Parkinson  CAR 233154                   *
.*                   Increased diagnosis descriptions                        *
.*        V10.01.00  24/11/2010 Jill Parkinson  CAR 233154                   *
.*                   Created                                                 *
.*****************************************************************************
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
.
OLDFILE1  IFILE     SQL, FIXED=620, KEY="U1-8"
.
.DAADMNO   DIM       8          8       1  PATIENT ADMISSION NO.
.AURNO     DIM       8          8       9  PATIENT U.R NO.
.ADATE     DIM       8          8      17  Admission Date (CCYYMMDD)
.ATIME     DIM       8          8      25  PATIENT ADMISSION TIME
.DASTAT    DIM       1          1      33  ADMISSION STATUS
.                                           1 = PRE-ADMISSION
.                                           2 = CURRENT ADMISSION
.                                           3 = DISCHARGED
.                                           4 = ON LEAVE
.                                           5 = CANCELLED
.APORT     DIM       2          2      34  CURRENT PORT USING THIS RECORD
.ATRANNO   FORM      3          3      36  NEXT TRANSACTION NUMBER
.AINVPRT   FORM      2          2      39  NUMBER OF INVOICES PRINTED
.ALACDTE   DIM       8          8      41  last accom date invd (CCYYMMDD)
.ACANCEL   DIM       3          3      49  REASON PRE-ADMMSS CANCELATION
.AWARD     DIM       3          3      52  PATIENT WARD NO.
.ABED      DIM       3          3      55  PATIENT BED NO.
.ADOCTR    DIM       6          6      58  DOCTOR REFERRED BY.
.ADOCTA    DIM       6          6      64  DOCTOR ATTENDING PATIENT.
.ADOCTL    DIM       30        30      70  LOCAL DOCTOR
.ASRCE     DIM       3          3     100  PATIENT ADMISSIONS SOURCE  (CAT S )
.ATYPE     DIM       3          3     103  PATIENT ADMISSIONS TYPE    (CAT A )
.ACLSS     DIM       3          3     106  PATIENT ADMISSIONS CLASS   (CAT P )
.ACARE     DIM       3          3     109  PATIENT CARE CLASS         (CAT CC)
.AFUNDH    DIM       6          6     112  PATIENT HEALTH FUND
.AFNDTB    DIM       8          8     118  PATIENT FUND TABLE
.AFNDMM    DIM       10        10     126  PATIENT FUND MEMBERSHIP NO
.AELIG     FORM      1          2     136  PATIENT ELIGABILITY FOR GOV'T
.                                         REBATE. 1 = YES  2 = NO
.                                         STH - DISCHARGE DIAG.PRINTED
.AVISIT    FORM      1          2     138  PATIENT RELIGIOUS VISIT
.                                                 1 = YES, 2 = NO
.AALERG    FORM      1          2     140  PATIENT ALLERGIES (Y/N)
.                                                 1 = YES, 2 = NO
.AILLN     FORM      1          2     142  PRE-EXISTING ILLNESS
.                                                 1 = YES, 2 = NO
OLDDIAG1    DIM       50        50     144  PATIENTS ADMITTING DIAG 1
OLDDIAG2    DIM       50        50     194   "   "     "   "     "  2
.ADISC     FORM      6.2        5     244  PATIENT DISCOUNT PERCENTAGE
.ADOCTT    DIM       6          6     249  TREATING DOCTOR
.ACLSSFT   DIM       3          3     255  DEPARTMENT CODE (Cat AC)
.AALLOW    DIM       2          2     258  PATIENT ALLOWANCE CODE
.AMBS      DIM       9          9     260  ADMITTING MBS CODE
.ACLAIM    DIM       3          3     269  CLAIM CODE (CAT CL)
.ADIET     DIM       3          3     272  DIET CODE (CAT DC)
.APLOCCR   DIM       3          3     275  PLACE OF OCCR (CATEGORY PO)
.ASTAY     FORM      3          3     278  EXPECTED LENGTH OF STAY (DAYS)
.ADYHOSP   FORM      3          3     281  No OF DAYS OF HOSPITALIZATION
.AMEMB     FORM      1          2     284  DIAMOND VALLEY MEMBERSHIP
.                                                 1 = YES, 2 = NO
.AMEMBNO   FORM      6          4     286  MEMBERSHIP NUMBER
.ACOMM1    DIM       40        40     290  COMMENTS FIELD 1
.ACOMM2    DIM       30        30     330  COMMENTS FIELD 2
.APLUR     FORM      1          2     360  PLURATILY OF BABY (0=NORMAL)
.APURGE    FORM      1          2     362  PURGE INDICATOR
.                                                 OTHER = NO PURGE
.                                                 1     = PARTIAL
.                                                 2     = FULLY
.AUSR1     DIM       3          3     364  USER DEFINED FIELD 1 (CAT U1)
.AUSR2     DIM       3          3     367  USER DEFINED FIELD 2 (CAT U2)
.AUSR3     DIM       3          3     370  USER DEFINED FIELD 3 (CAT U3)
.AUSR4     DIM       3          3     373  USER DEFINED FIELD 4 (CAT U4)
.AUSR5     DIM       3          3     376  USER DEFINED FIELD 5 (CAT U5)
.AUSR6     DIM       3          3     379  USER DEFINED FIELD 6 (CAT U6)
.                                                 - Clinic Unit No. for Publics
.AUSR7     DIM       3          3     382  USER DEFINED FIELD 7 (CAT U7)
.                                                 - Coding status for Publics
.ADSCHI    FORM      1          2     385  DISCHARGE INVOICE INDICATOR
.                                                 0 = Discharge inv not printed
.                                                 1 = Have printed discharge inv
.ARDRNAM   DIM       22        22     387  FREE FORMAT REFERRING DOCTOR
.AFNDYY    DIM       2          2     409  YRS OF FUND MEMBERSHIP (JR,DE)
.AFNDCG    DIM       6          6     411  fund cover change (CCYYMM)
.AOCCDTE   DIM       8          8     417  DATE OF OCCURANCE (CCYYMMDD)
.ACODDTE   DIM       8          8     425  DATE CODING COMPLETED
.ARETDTE   DIM       8          8     433  EXPECTED RETURN DATE FROM LVE
.AMUMADM   FORM      8          5     441  MOTHERS ADMSS NUMBER(IF BABY)
.DPTMIPVI  DIM       3          3     446  No of prev. visits charged
.ABWGHT    DIM       6          6     449  Baby's /Admission weight
.PTMSRFGP  DIM       8          8     455  Referring GP.
.PTMSGPPC  DIM       6          6     463  GP Practice Code
.PTMSFHAN  DIM       20        20     469  GPFH Approval Number
.PTMSECRA  DIM       20        20     489  ECR Approval Number
.PTMSMGIN  DIM       3          3     509  Management Intent   (CAT MI)
.DPTMIDDI  DIM       1          1     512  Discharged diagnosis printed?
.PTMIGPPT  DIM       2          2     513  GP Practice count
.PTMIDOFR  DIM       3          3     515  District of Residence (Cat DA)
.PTMIREFD  DIM       8          8     518  Date of Referral (EDI nz)
.PTMIDFCN  DIM       8          8     526  Date of 1st consultation (EDI nz)
.PTMIPHPU  DIM       3          3     534  Principle Health Service Purchaser
.                                         (EDI nz) (CAT HP)
.PTMIAGNC  DIM       3          3     537  Health Agency (EDI nz) (CAT HA)
.PTMIESSF  DIM       1          1     540  Event Summary supress flag Y/N(EDI nz)
.                                         "Y" = Suppress event summary
.                                         "N" = Don't supress
.PTMIUSR8  DIM       3          3     541  User defined field 8 (CAT U8)
.PTMIUSR9  DIM       3          3     544  User defined field 9 (CAT U9)
.PTMIUSR0  DIM       3          3     547  User defined field 0 (CAT U0)
.PTMIPDRG  DIM       4          4     550  Provisional DRG
.PTMIOPER  DIM       4          4     554  Operator who pre-admitted patient
.PTMIXWRD  DIM       3          3     558  Expected Ward for Pre-Admission
.PTMIADOC  DIM       6          6     561  Associated Doctor
.PTMIREGS  DIM       6          6     567  Registrar
.PTMIUYN1  FORM      1          2     573  User Defined Y/N Field (1=Yes, 0=No)
.PTMIUYN2  FORM      1          2     575  User Defined Y/N Field (1=Yes, 0=No)
.PTMIPANO  DIM       8          8     577  Previous admission number
.PTMIPADT  DIM       8          8     585  Previous admission date (ccyymmdd)
.PTMIPCMX  DIM       9          9     593  Casemix Code (Provisional MBS/DRG)
.PTMIGSTA  DIM       1          1     602  Is GST applicable to this event
.                                         Y=Yes, N/Space=No, U=Unknown
.PTMICMXP  DIM       1          1     603  Casemix Payment (Y/N)
.                                         NZ Billing - FFS has been charged(Y/N)
.PTMIEBED  DIM       3          3     604  Expected Bed
.PTMIDMOV  DIM       1          1     607  Pre-admission movement indicator
.PTMIDFAS  DIM       8          8     608  Date Patient First Aware of Symptoms
.PTMICRCD  DIM       1          1     616  Credit Card Details on File (1=Yes)
.PTMIFINV  DIM       1          1     617  Follow-up for implant invoicing(1=Yes)
OLDSSPAR  DIM        2          2    618  Spare Variable
.End of Record                       620 
.
.                                     ******** copy the new FD here *******
PATMI1A1  IFILE     SQL, FIXED=831, KEY="U1-8"
PATMI1A2  IFILE     SQL, FIXED=831, KEY="U33-33,1-8"
PATMI1A3  IFILE     SQL, FIXED=831, KEY="U17-24,1-8"
PATMI1A4  IFILE     SQL, FIXED=831, KEY="U485-492,1-8"
PATMI1A5  IFILE     SQL, FIXED=831, KEY="U33-33,64-69,52-54,55-57,1-8"
PATMI1A6  IFILE     SQL, FIXED=831, KEY="U33-33,52-54,17-24,1-8"
PATMI1A7  IFILE     SQL, FIXED=831, KEY="U64-69,33-33,485-492,1-8"
PATMI1A8  IFILE     SQL, FIXED=831, KEY="U9-16,33-33,1-8"
.
.Name     Type      Length  Phys.  Start  Description
.-------  ----      ------  -----  -----  ----------------------------------
DAADMNO   DIM       8          8       1  PATIENT ADMISSION NO.
AURNO     DIM       8          8       9  PATIENT U.R NO.
ADATE     DIM       8          8      17  Admission Date (CCYYMMDD)
ATIME     DIM       8          8      25  PATIENT ADMISSION TIME
DASTAT    DIM       1          1      33  ADMISSION STATUS
.                                           1 = PRE-ADMISSION
.                                           2 = CURRENT ADMISSION
.                                           3 = DISCHARGED
.                                           4 = ON LEAVE
.                                           5 = CANCELLED
APORT     DIM       2          2      34  CURRENT PORT USING THIS RECORD
ATRANNO   FORM      3          3      36  NEXT TRANSACTION NUMBER
AINVPRT   FORM      2          2      39  NUMBER OF INVOICES PRINTED
ALACDTE   DIM       8          8      41  last accom date invd (CCYYMMDD)
ACANCEL   DIM       3          3      49  REASON PRE-ADMMSS CANCELATION
AWARD     DIM       3          3      52  PATIENT WARD NO.
ABED      DIM       3          3      55  PATIENT BED NO.
ADOCTR    DIM       6          6      58  DOCTOR REFERRED BY.
ADOCTA    DIM       6          6      64  DOCTOR ATTENDING PATIENT.
ADOCTL    DIM       30        30      70  LOCAL DOCTOR
ASRCE     DIM       3          3     100  PATIENT ADMISSIONS SOURCE  (CAT S )
ATYPE     DIM       3          3     103  PATIENT ADMISSIONS TYPE    (CAT A )
ACLSS     DIM       3          3     106  PATIENT ADMISSIONS CLASS   (CAT P )
ACARE     DIM       3          3     109  PATIENT CARE CLASS         (CAT CC)
AFUNDH    DIM       6          6     112  PATIENT HEALTH FUND
AFNDTB    DIM       8          8     118  PATIENT FUND TABLE
AFNDMM    DIM       10        10     126  PATIENT FUND MEMBERSHIP NO
AELIG     FORM      1          2     136  PATIENT ELIGABILITY FOR GOV'T
.                                         REBATE. 1 = YES  2 = NO
.                                         STH - DISCHARGE DIAG.PRINTED
AVISIT    FORM      1          2     138  PATIENT RELIGIOUS VISIT
.                                                 1 = YES, 2 = NO
AALERG    FORM      1          2     140  PATIENT ALLERGIES (Y/N)
.                                                 1 = YES, 2 = NO
AILLN     FORM      1          2     142  PRE-EXISTING ILLNESS
.                                                 1 = YES, 2 = NO
ADIAG1    DIM       80        80     144  PATIENTS ADMITTING DIAG 1
ADIAG2    DIM       80        80     224   "   "     "   "     "  2
ADISC     FORM      6.2        5     304  PATIENT DISCOUNT PERCENTAGE
ADOCTT    DIM       6          6     309  TREATING DOCTOR
ACLSSFT   DIM       3          3     315  DEPARTMENT CODE (Cat AC)
AALLOW    DIM       2          2     318  PATIENT ALLOWANCE CODE
AMBS      DIM       9          9     320  ADMITTING MBS CODE
ACLAIM    DIM       3          3     329  CLAIM CODE (CAT CL)
ADIET     DIM       3          3     332  DIET CODE (CAT DC)
APLOCCR   DIM       3          3     335  PLACE OF OCCR (CATEGORY PO)
ASTAY     FORM      3          3     338  EXPECTED LENGTH OF STAY (DAYS)
ADYHOSP   FORM      3          3     341  No OF DAYS OF HOSPITALIZATION
AMEMB     FORM      1          2     344  DIAMOND VALLEY MEMBERSHIP
.                                                 1 = YES, 2 = NO
AMEMBNO   FORM      6          4     346  MEMBERSHIP NUMBER
ACOMM1    DIM       40        40     350  COMMENTS FIELD 1
ACOMM2    DIM       30        30     390  COMMENTS FIELD 2
APLUR     FORM      1          2     420  PLURATILY OF BABY (0=NORMAL)
APURGE    FORM      1          2     422  PURGE INDICATOR
.                                                 OTHER = NO PURGE
.                                                 1     = PARTIAL
.                                                 2     = FULLY
AUSR1     DIM       3          3     424  USER DEFINED FIELD 1 (CAT U1)
AUSR2     DIM       3          3     427  USER DEFINED FIELD 2 (CAT U2)
AUSR3     DIM       3          3     430  USER DEFINED FIELD 3 (CAT U3)
AUSR4     DIM       3          3     433  USER DEFINED FIELD 4 (CAT U4)
AUSR5     DIM       3          3     436  USER DEFINED FIELD 5 (CAT U5)
AUSR6     DIM       3          3     439  USER DEFINED FIELD 6 (CAT U6)
.                                                 - Clinic Unit No. for Publics
AUSR7     DIM       3          3     442  USER DEFINED FIELD 7 (CAT U7)
.                                                 - Coding status for Publics
ADSCHI    FORM      1          2     445  DISCHARGE INVOICE INDICATOR
.                                                 0 = Discharge inv not printed
.                                                 1 = Have printed discharge inv
ARDRNAM   DIM       22        22     447  FREE FORMAT REFERRING DOCTOR
AFNDYY    DIM       2          2     469  YRS OF FUND MEMBERSHIP (JR,DE)
AFNDCG    DIM       6          6     471  fund cover change (CCYYMM)
AOCCDTE   DIM       8          8     477  DATE OF OCCURANCE (CCYYMMDD)
ACODDTE   DIM       8          8     485  DATE CODING COMPLETED
ARETDTE   DIM       8          8     493  EXPECTED RETURN DATE FROM LVE
AMUMADM   FORM      8          5     501  MOTHERS ADMSS NUMBER(IF BABY)
DPTMIPVI  DIM       3          3     506  No of prev. visits charged
ABWGHT    DIM       6          6     509  Baby's /Admission weight
PTMSRFGP  DIM       8          8     515  Referring GP.
PTMSGPPC  DIM       6          6     423  GP Practice Code
PTMSFHAN  DIM       20        20     429  GPFH Approval Number
PTMSECRA  DIM       20        20     549  ECR Approval Number
PTMSMGIN  DIM       3          3     569  Management Intent   (CAT MI)
DPTMIDDI  DIM       1          1     572  Discharged diagnosis printed?
PTMIGPPT  DIM       2          2     573  GP Practice count
PTMIDOFR  DIM       3          3     575  District of Residence (Cat DA)
PTMIREFD  DIM       8          8     578  Date of Referral (EDI nz)
PTMIDFCN  DIM       8          8     586  Date of 1st consultation (EDI nz)
PTMIPHPU  DIM       3          3     594  Principle Health Service Purchaser
.                                         (EDI nz) (CAT HP)
PTMIAGNC  DIM       3          3     597  Health Agency (EDI nz) (CAT HA)
PTMIESSF  DIM       1          1     600  Event Summary supress flag Y/N(EDI nz)
.                                         "Y" = Suppress event summary
.                                         "N" = Don't supress
PTMIUSR8  DIM       3          3     601  User defined field 8 (CAT U8)
PTMIUSR9  DIM       3          3     604  User defined field 9 (CAT U9)
PTMIUSR0  DIM       3          3     607  User defined field 0 (CAT U0)
PTMIPDRG  DIM       4          4     610  Provisional DRG
PTMIOPER  DIM       4          4     614  Operator who pre-admitted patient
PTMIXWRD  DIM       3          3     618  Expected Ward for Pre-Admission
PTMIADOC  DIM       6          6     671  Associated Doctor
PTMIREGS  DIM       6          6     627  Registrar
PTMIUYN1  FORM      1          2     633  User Defined Y/N Field (1=Yes, 0=No)
PTMIUYN2  FORM      1          2     635  User Defined Y/N Field (1=Yes, 0=No)
PTMIPANO  DIM       8          8     637  Previous admission number
PTMIPADT  DIM       8          8     645  Previous admission date (ccyymmdd)
PTMIPCMX  DIM       9          9     653  Casemix Code (Provisional MBS/DRG)
PTMIGSTA  DIM       1          1     662  Is GST applicable to this event
.                                         Y=Yes, N/Space=No, U=Unknown
PTMICMXP  DIM       1          1     663  Casemix Payment (Y/N)
.                                         NZ Billing - FFS has been charged(Y/N)
PTMIEBED  DIM       3          3     664  Expected Bed
PTMIDMOV  DIM       1          1     667  Pre-admission movement indicator
PTMIDFAS  DIM       8          8     668  Date Patient First Aware of Symptoms
PTMICRCD  DIM       1          1     676  Credit Card Details on File (1=Yes)
PTMIFINV  DIM       1          1     677  Follow-up for implant invoicing(1=Yes)
PTMICONB  DIM       1          1     678  Continuous Billing Event (1=Yes)
PTMIDATC  DIM       8          8     679  Date Created CCYYMMDD
PTMITIMC  DIM       8          8     687  Time Created hh:mm:ss
PTMIWEBC  DIM      10         10     695  Web user Id Created
PTMIDATU  DIM       8          8     705  Date Updated CCYYMMDD
PTMITIMU  DIM       8          8     713  Time Updated hh:mm:ss
PTMIWEBU  DIM      10         10     721  Web user Id Updated
PTMI3BDY  DIM       3          3     731  3B certificate days
PTMI3BAD  DIM       8          8     734  3B certificate admission date
PTMSSPAR  DIM      89         89     742  Spare Variable
.
.End of Record                       831

.
.         SPARE VARIABLES
.
SSAADMNO  FORM      8                   PATIENT ADMISSION NO.
SSASTAT   FORM      1                   ADMISSION STATUS
SSADATE   DIM       8                   PATIENT ADMISSION DATE (CCYYMMDD)
.
SAADMNO   FORM      8                   PATIENT ADMISSION NO.
SASTAT    FORM      1                   ADMISSION STATUS
SADATE    DIM       8                   PATIENT ADMISSION DATE (CCYYMMDD)
AADAY     DIM       2
AAMON     DIM       2
AAYEAR    DIM       2
AACENT    DIM       2
.
.  redefine form fields for key
.  ----------------------------
.Name     Type      Length  Description
.-------  ----      ------  ---------------------
AADMNO    FORM      8       PATIENT ADMISSION NO.
ASTAT     FORM      1       ADMISSION STATUS
PTMIPVIS  FORM      3       No of prev visits charged
PTMIDDIG  FORM      1       Discharged diagnosis printed?
.
CMDLINE   DIM       100
DIM4      DIM       4
DIM4A     DIM       4
DIM6      DIM       6
DIM6A     DIM       6
DIM60     DIM       60
DDCENT    DIM       2
.
INPFILE   DIM       8
RECNUM    FORM      8
NEWFILE   DIM       60
NEWPATH   DIM       60
OLDPATH   DIM       60
DEFPATH   DIM       60
SAVEFLG   FORM      1
SP50      INIT    "                                                  "
SP60      INIT    "                                                            "
.
PRGNAM    INIT      "CONVERSION PATMI1FD"
PRGID     INIT      "F01PTMI1"
VERSION   INIT      "V12.00.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN1000,MAIN0000,MAIN9999 * Yes, no, cancel
.
MAIN1000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
. Loop thru old file and write records to new file
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/compress saved file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          
INIT9999  RETURN
.
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
PREP0000 
. open old file
          MOVE      "patmi1af",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
. Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat patmi1af.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sptmi1af.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat patmi1af.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sptmi1af.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
. remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "patmi1af",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "sptmi1af",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "patmi1af",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
          APPEND    " 831 UC1-8",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has 
.  to be repeated with for each individual index
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC33-33,1-8",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC17-24,1-8",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC485-492,1-8",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC33-33,64-69,52-54,55-57,1-8",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC33-33,52-54,17-24,1-8",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC64-69,33-33,485-492,1-8",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC9-16,33-33,1-8",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
          OPEN      PATMI1A1,NEWFILE
.
. set the flag to say we want to continue
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
          ELSE
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
            ELSE
              DISPLAY   "has caused an IO error - ";
            ENDIF
          ENDIF
          DISPLAY   "File will be bypassed"
.
          IF        FORM2 < 17
            CALL      HITENTER
          ENDIF
          DISPLAY   *P1:23,*EF
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
.
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          PACK      KEY8,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
.  All necessary changes to the files field lengths, date changes
.  or any other sort of modification must be made here
.
          PACK      PTMICONB,SP70
          PACK      PTMIDATC,SP70
          PACK      PTMITIMC,SP70
          PACK      PTMIWEBC,SP70
          PACK      PTMIDATU,SP70
          PACK      PTMITIMU,SP70
          PACK      PTMIWEBU,SP70
          PACK      ADIAG1,OLDDIAG1,SP70
          PACK      ADIAG2,OLDDIAG2,SP70
          PACK      PTMI3BDY,SP70
          PACK      PTMI3BAD,SP70
.                                               * Pack key here
          PACK      KEY8,DAADMNO,SP70
          CALL      WRMISS1                    * write to new file
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     PATMI1A1                      * close new file
          CLOSE     OLDFILE1                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
READ9999  RETURN
.
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
VALD0000  MOVE      ZERO,EXIT
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or compress save file                                *
.**********************************************************************
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "sptmi1af*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "compress -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not compressed or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.         Get the default directory
.**********************************************************************
DEFT0000  EXECUTE   "ldat patmi1af.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding 'patmi1af'.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
DEFT9999  RETURN
+
.
.                  ******************************    OLD IO ROUTINES
READSOLD RESET     KEY8
         READ      OLDFILE1,KEY8;;
         RETURN
.
READKOLD MOVE      ZERO,OVRCD
         READKS    OLDFILE1;DAADMNO,AURNO,ADATE,ATIME:
                                 DASTAT,APORT,ATRANNO,AINVPRT,ALACDTE:
                                 ACANCEL,AWARD,ABED,ADOCTR,ADOCTA:
                                 ADOCTL,ASRCE,ATYPE,ACLSS,ACARE,AFUNDH:
                                 AFNDTB,AFNDMM,AELIG,AVISIT,AALERG:
                                 AILLN,OLDDIAG1,OLDDIAG2,ADISC,ADOCTT,ACLSSFT:
                                 AALLOW,AMBS,ACLAIM,ADIET,APLOCCR,ASTAY:
                                 ADYHOSP,AMEMB,AMEMBNO:
                                 ACOMM1,ACOMM2,APLUR,APURGE,AUSR1:
                                 AUSR2,AUSR3,AUSR4,AUSR5,AUSR6,AUSR7:
                                 ADSCHI,ARDRNAM,AFNDYY,AFNDCG:
                                 AOCCDTE,ACODDTE,ARETDTE,AMUMADM,DPTMIPVI:
                                 ABWGHT:
                                 PTMSRFGP,PTMSGPPC,PTMSFHAN,PTMSECRA:
                                 PTMSMGIN,DPTMIDDI,PTMIGPPT,PTMIDOFR:
                                 PTMIREFD,PTMIDFCN,PTMIPHPU,PTMIAGNC:
                                 PTMIESSF,PTMIUSR8,PTMIUSR9,PTMIUSR0:
                                 PTMIPDRG,PTMIOPER,PTMIXWRD,PTMIADOC:
                                 PTMIREGS,PTMIUYN1,PTMIUYN2,PTMIPANO:
                                 PTMIPADT,PTMIPCMX,PTMIGSTA,PTMICMXP:
                                 PTMIEBED,PTMIDMOV,PTMIDFAS,PTMICRCD:
                                 PTMIFINV,OLDSSPAR
         GOTO      OVERCOND IF OVER
         MOVE      DAADMNO,AADMNO
         MOVE      DASTAT,ASTAT
         MOVE      DPTMIPVI,PTMIPVIS
         MOVE      DPTMIDDI,PTMIDDIG
         MOVE      AADMNO,SSAADMNO
         MOVE      ASTAT,SSASTAT
         MOVE      ADATE,SSADATE
         RETURN
.
.
.                  ******************************    NEW IO ROUTINES
WRMISS1  MOVE      SP2,APORT
.
WRMISS1A RESET     KEY8
         MOVE      ZERO,OVRCD
.
         MOVE      AADMNO,DAADMNO
         MOVE      ASTAT,DASTAT
         MOVE      PTMIPVIS,DPTMIPVI
         MOVE      PTMIDDIG,DPTMIDDI
         MOVE      PASSCODE,PTMIOPER
         PACK      PTMIDATC,CCC,CYY,CMM,CDD
         REP       " 0",PTMIDATC
         CLOCK     TIME,PTMITIMC
.
         WRITE     PATMI1A1,KEY8;KEY8,AURNO,ADATE,ATIME:
                                 DASTAT,APORT,ATRANNO,AINVPRT,ALACDTE:
                                 ACANCEL,AWARD,ABED,ADOCTR,ADOCTA:
                                 ADOCTL,ASRCE,ATYPE,ACLSS,ACARE,AFUNDH:
                                 AFNDTB,AFNDMM,AELIG,AVISIT,AALERG:
                                 AILLN,ADIAG1,ADIAG2,ADISC,ADOCTT,ACLSSFT:
                                 AALLOW,AMBS,ACLAIM,ADIET,APLOCCR,ASTAY:
                                 ADYHOSP,AMEMB,AMEMBNO:
                                 ACOMM1,ACOMM2,APLUR,APURGE,AUSR1:
                                 AUSR2,AUSR3,AUSR4,AUSR5,AUSR6,AUSR7:
                                 ADSCHI,ARDRNAM,AFNDYY,AFNDCG:
                                 AOCCDTE,ACODDTE,ARETDTE,AMUMADM,DPTMIPVI:
                                 ABWGHT:
                                 PTMSRFGP,PTMSGPPC,PTMSFHAN,PTMSECRA:
                                 PTMSMGIN,DPTMIDDI,PTMIGPPT,PTMIDOFR:
                                 PTMIREFD,PTMIDFCN,PTMIPHPU,PTMIAGNC:
                                 PTMIESSF,PTMIUSR8,PTMIUSR9,PTMIUSR0:
                                 PTMIPDRG,PTMIOPER,PTMIXWRD,PTMIADOC:
                                 PTMIREGS,PTMIUYN1,PTMIUYN2,PTMIPANO:
                                 PTMIPADT,PTMIPCMX,PTMIGSTA,PTMICMXP:
                                 PTMIEBED,PTMIDMOV,PTMIDFAS,PTMICRCD:
                                 PTMIFINV,PTMICONB,PTMIDATC,PTMITIMC:
                                 PTMIWEBC,PTMIDATU,PTMITIMU,PTMIWEBU:
                                 PTMI3BDY,PTMI3BAD,PTMSSPAR
.
         RETURN
.
.
          INC       STD001IO
