.----------------------------------------------------------------------
. Include  : CCSOPTKY
.
. Function : Enter a Selected Operation Time 1 to 14
.
. Returns  : OPRTIM  FORM  2      * Value 1 to 14
.            OPRTIMD DIM   18     * Description of Time Selected
.            EXIT    FORM  1      * 0=Ok, 1=Nothing Entered
.----------------------------------------------------------------------
.----------------------------------------------------------------------
. Enter Theatre Operation Time Option
.----------------------------------------------------------------------
KOTIM000  CALL     DSPTIM00
.
KOTIM010  MOVE     ZERO,F2
          KEYIN    *PCCOL:CVERT,*V2LON,*RV,F2
          MOVE     F2,OPRTIM
          COMPARE  ZERO,F2
          GOTO     KOTIM900 IF EQUAL
          BRANCH   F2,KOTIM110,KOTIM110,KOTIM110,KOTIM110,KOTIM110:
                      KOTIM110,KOTIM110,KOTIM110,KOTIM110,KOTIM110:
                      KOTIM110,KOTIM110,KOTIM110,KOTIM120
          BEEP
          GOTO     KOTIM010
.
KOTIM110  LOAD     OPRTIMD,F2,OPCNDPR1,OPCNDPR2,OPCNDPR3,OPCNDPR4,OPCNDPR5:
                              OPCNDOP1,OPCNDOP2,OPCNDOP3,OPCNDOP4:
                              OPCNDPS1,OPCNDPS2,OPCNDPS3,OPCNDPS4
          GOTO     KOTIM990
.
KOTIM120  MOVE     "Actual Duration  ",OPRTIMD
          GOTO     KOTIM990
.
KOTIM900  CALL     PUTSCR00
          MOVE     ONE,EXIT
          GOTO     KOTIM999
.
KOTIM990  CALL     PUTSCR00
.
KOTIM999  RETURN
.----------------------------------------------------------------------
. Display Time Options for From and To Theatre Time Measurement
.----------------------------------------------------------------------
DSPTIM00  MOVE      "68",AUDTSECT
          READ      CONTROLF,AUDTSECT;OPCNDPR1,OPCNDPR2,OPCNDPR3,OPCNDPR4:
                                      OPCNDPR5,OPCNDOP1,OPCNDOP2,OPCNDOP3:
                                      OPCNDOP4,OPCNDPS1,OPCNDPS2,OPCNDPS3:
                                      OPCNDPS4
          MOVE      "02",HTOP
          MOVE      "01",HLEF
          MOVE      "18",HBOT
          MOVE      "30",HRIG
          CALL      GETSCR00
.
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          DISPLAY   *P6:3,*V2LON,*ULON,"Operation Times",*HOFF:
                    *P2:4,*V2LON," 1. ",*HOFF,OPCNDPR1:
                    *P2:5,*V2LON," 2. ",*HOFF,OPCNDPR2:
                    *P2:6,*V2LON," 3. ",*HOFF,OPCNDPR3:
                    *P2:7,*V2LON," 4. ",*HOFF,OPCNDPR4:
                    *P2:8,*V2LON," 5. ",*HOFF,OPCNDPR5:
                    *P2:9,*V2LON," 6. ",*HOFF,OPCNDOP1:
                    *P2:10,*V2LON," 7. ",*HOFF,OPCNDOP2:
                    *P2:11,*V2LON," 8. ",*HOFF,OPCNDOP3:
                    *P2:12,*V2LON," 9. ",*HOFF,OPCNDOP4:
                    *P2:13,*V2LON,"10. ",*HOFF,OPCNDPS1:
                    *P2:14,*V2LON,"11. ",*HOFF,OPCNDPS2:
                    *P2:15,*V2LON,"12. ",*HOFF,OPCNDPS3:
                    *P2:16,*V2LON,"13. ",*HOFF,OPCNDPS4:
                    *P2:17,*V2LON,"14. ",*HOFF,"Use Actual Duration"
DSPTIM99  RETURN
.
