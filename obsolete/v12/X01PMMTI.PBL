.*****************************************************************************
.*    Program      : X01PMMTI                                                *
.*    Description  : Conversion pmsmtiaf to new File layout                  *
.*                                                                           *
.*    Author       : Tracey Nguyen                                           *
.*    Date         : 19/02/2021                                              *
.*        V11.01.00 19/02/2021  Tracey Nguyen     TASK 0888639               *
.*                  Created Fixit - Added PMMITMNO for Theatre Team Number   *
.*                  (split up from PMMIPMBS)                                 *
.*                                                                           *
.*****************************************************************************
.
.  using vi Global change command      :%s/FromString/ToString/g
.
.  Global change progname  eg F57PTDOC
.  Global change logfname  eg f57ptdoc
.  Global change filefdnm  eg PATDOCFD (new layout)
.  Global change tabnamaf  eg patdoctf
.  Global change tablidx1  eg PATDOCT1
.  Global change keyxxin   eg KEY8
.  Global change keyxxout  eg KEY9
.  Global change newwrite  eg WRDOCT1
.  Global change savefile  eg sptdoctf
.
.  Global changes if Oracle Option is required -
.
.  Global Change answeryn  to Yes
.  Global change newRSstm  eg RSPTDOC1
.  Global change newRKstm  eg RKPTDOC1
.  Global change newUPstm  eg UPPTDOC1
.
.  Global changes if Oracle Option is NOT required -

.  Global Change answeryn  to No
.  Global change newRSstm  to OFDUMYIO
.  Global change newRKstm  to OFDUMYIO
.  Global change newUPstm  to OFDUMYIO
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
LOGFILE   FILE      ASCII, FIXED=256
.
. Enter the first or the shortest index from the original FD and rename
. the index OLDFILE1
.
.                                     ******** copy the old FD here *******
.                                     * comment out all continuing fields and
.                                     * prefix changing fields with "OLD" -
.                                     * remove old Indexes and Redefines     
.
OLDFILE1   IFILE    SQL, FIXED=435, KEY="U1-8,9-14"
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.PMMIVISN  DIM       8      8        1     Visit number
.PMMITRAN  DIM       6      6        9     Transaction Number
.PMMIURNO  DIM       8      8        15    U/R Number
.PMMISYST  DIM       2      2        23    System
.                                         1 = Emergency
.                                         2 = Outpatient
.                                         3 = Inpatient
.PMMIITEM  DIM       9      9        25    Item Number
.PMMIDESC  DIM       30     30       34    Description
.PMMIDES2  DIM       35     35       64    Description 2
.PMMITDAT  DIM       8      8        99    Transaction date
.PMMIRTYP  DIM       1      1        107   Record type
.                                         1 - ACCOM
.                                         2 - THEATRE
.                                         3 - MISCELLANEOUS
.                                         4 - DOCTORS FEES
.                                         5 - CASH RECEIPT
.                                         6 - JOURNALS
.                                         7 - DISCOUNT
.                                         8 - PROCEDURE
.PMMIREFN  DIM       12     12       108   Reference
.PMMIAMTT  FORM      12.2   8        120   Gross Total amount
.PMMIAMTG  FORM      12.2   8        128   Government Portion
.PMMIAMTH  FORM      12.2   8        136   Orig rate before any disc.allowance
.PMMIAMTP  FORM      12.2   8        144   Patient portion amount
.PMMIRBAT  FORM      12.2   8        152   Estimate Rebate amount
.PMMITDOC  DIM       6      6        160   Treating doctor
.PMMISERV  FORM      5      4        166   Service Number
.PMMIODOC  DIM       6      6        170   Operating Doctor
.PMMISESS  DIM       2      2        176   Session (Theatre only)
.PMMIMGRP  DIM       3      3        178   Misc.Group (Mis.&Theatre only)CatFI
.                                         JH-Discount (Cat FI)
.                                         JH-Misc.Item/Doctor Chrg Group(Cat FN)
.PMMIBTCH  DIM       8      8        181   Batch No for Misc.Items,Cash,Jrnl.
.                                         JH-Misc.Item/Doctor Chrg Group(Cat FI)
.PMMIINVN  FORM      1      2        189   No of Invoices
.PMMIGSTA  FORM      1      2        191   Is GST Applicable to this item
.PMMIGSTC  DIM       6      6        193   GST Payable code
.PMMIGSTM  FORM      12.2   8        199   GST Amount
.PMMIDUPD  DIM       8      8        207   Date record update
.PMMITUPD  DIM       8      8        215   Time record updated
.PMMIWUSR  DIM       10     10       223   WEB User ID
.PMMIMVBR  DIM       1      1        233   Mech.Vent.Billing Record? 1=Yes
.                                         NZ Private billing - 1 = Pack Item
.PMMIUNIQ  DIM       10     10       234   Unique id for theatre booking
.PMMICNTR  DIM       6      6        244   Contract/Health Fund
.PMMISUNQ  DIM       3      3        250   Subsequence Proc.Unique No(NZ Priv)
.                                         or Banding (CAT TB)(Theatre Only)
.PMMIINCT  DIM       1      1        253   Inclusion Type 0=Inc in contract
.                                                     0=Inc in contract
.                                                     1=Bill to Patient
.                                                     2=Extra to contract
.                                         OTHERS - Indicator to Print Procedure
.                                                  Date/Time on EMR Invoice
.                                                  1= Yes, 0=No
.                                         1=Community billing - item from Regime
.PMMISUBN  DIM       3      3        254   Sub Item Code
.PMMIEDAD  DIM       10     10       257   ED Attending Doctor
.PMMISVTM  DIM       8      8        267   Service Time (HH:MM:SS)
.PMMICCON  DIM       1      1        275   CMBS Confirmed 1=Yes
.PMMISCHF  FORM      3.2    4        276   Schedule Fee (AMA=0,MBS=0.75, 0.85)
.PMMIITYP  DIM       1      1        280   Item Type (0=MBS 1=AMA)
.                                   The following values are for NZ Priv.Billing
.                                         2=Exploding Misc.itm(No recalc.charge)
.                                         3=Not validating Misc.Code(HL7 msg)
.PMMIDTCR  DIM       8      8        281   Date Created
.PMMITMCR  DIM       8      8        289   Time Created
.PMMIIDCR  DIM       10     10       297   User ID Created
OLDIPMBS  DIM       3      3        307   Patient MBS Team & Counter
.                                         (oprpmbaf)
.PMMIRBRS  DIM       3      3        310   Rebate Reason (CAT Rr)
.PMMICTYP  DIM       1      1        313   Consumption Type
.                                         0 or Blank = Used
.                                         1 = Not Used
.                                         2 = Expired
.PMMIACOI  DIM       1      1        314   After Care Override Indicator
.                                         0-No
.                                         1-Yes
.PMMIDSOV  DIM       1      1        315   Duplicate Service Override
.                                         0-No
.                                         1-Yes
.PMMISTXT  DIM       50     50       316   Service Text
.PMMILSPN  DIM       6      6        366   Location Specific Practice No
.                                         - LSPN value (can not be 000000)
.PMMIMPOV  DIM       1      1        372   Multi Procedure Override
.                                         0-No
.                                         1-Yes
.PMMINMPT  DIM       2      2        373   Number of Patients Seen
.PMMISDCD  DIM       3      3        375   Self Deem Code (Cat Sd)
.PMMITDUR  DIM       3      3        378   Time Duration (Mins)
.PMMIROVR  DIM       3      3        381   Restrictive Override Code (Cat Ro)
.                                         Associated Field Position 1-2 values:
.                                         SP - Separate Sites
.                                         NR - Not Related (care plans)
.                                         NC - Not for Comparison
.PMMIHOSI  DIM       1      1        384   Hospital Indicator
.                                         0 or Blank = No
.                                         1 = Yes
.PMMISPAR  DIM       50     50       385   Spare variable
.
.End of Record                      435

.
.                                     ******** copy the new FD here *******
.
PMSMTIA1   IFILE    SQL, FIXED=435, KEY="U1-8,9-14"
PMSMTIA2   IFILE    SQL, FIXED=435, KEY="U1-8,107-107,9-14"
PMSMTIA3   IFILE    SQL, FIXED=435, KEY="U107-107,207-214,1-8,9-14"
PMSMTIA4   IFILE    SQL, FIXED=435, KEY="U1-8,107-107,178-180,9-14"
PMSMTIA5   IFILE    SQL, FIXED=435, KEY="U1-8,244-249,107-107,178-180,9-14"
PMSMTIA6   IFILE    SQL, FIXED=435, KEY="U1-8,257-266,9-14"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
PMMIVISN  DIM       8      8        1     Visit number
PMMITRAN  DIM       6      6        9     Transaction Number
PMMIURNO  DIM       8      8        15    U/R Number
PMMISYST  DIM       2      2        23    System
.                                         1 = Emergency
.                                         2 = Outpatient
.                                         3 = Inpatient
PMMIITEM  DIM       9      9        25    Item Number
PMMIDESC  DIM       30     30       34    Description
PMMIDES2  DIM       35     35       64    Description 2
PMMITDAT  DIM       8      8        99    Transaction date
PMMIRTYP  DIM       1      1        107   Record type
.                                         1 - ACCOM
.                                         2 - THEATRE
.                                         3 - MISCELLANEOUS
.                                         4 - DOCTORS FEES
.                                         5 - CASH RECEIPT
.                                         6 - JOURNALS
.                                         7 - DISCOUNT
.                                         8 - PROCEDURE
PMMIREFN  DIM       12     12       108   Reference
PMMIAMTT  FORM      12.2   8        120   Gross Total amount
PMMIAMTG  FORM      12.2   8        128   Government Portion
PMMIAMTH  FORM      12.2   8        136   Orig rate before any disc.allowance
PMMIAMTP  FORM      12.2   8        144   Patient portion amount
PMMIRBAT  FORM      12.2   8        152   Estimate Rebate amount
PMMITDOC  DIM       6      6        160   Treating doctor
PMMISERV  FORM      5      4        166   Service Number
PMMIODOC  DIM       6      6        170   Operating Doctor
PMMISESS  DIM       2      2        176   Session (Theatre only)
PMMIMGRP  DIM       3      3        178   Misc.Group (Mis.&Theatre only)CatFI
.                                         JH-Discount (Cat FI)
.                                         JH-Misc.Item/Doctor Chrg Group(Cat FN)
PMMIBTCH  DIM       8      8        181   Batch No for Misc.Items,Cash,Jrnl.
.                                         JH-Misc.Item/Doctor Chrg Group(Cat FI)
PMMIINVN  FORM      1      2        189   No of Invoices
PMMIGSTA  FORM      1      2        191   Is GST Applicable to this item
PMMIGSTC  DIM       6      6        193   GST Payable code
PMMIGSTM  FORM      12.2   8        199   GST Amount
PMMIDUPD  DIM       8      8        207   Date record update
PMMITUPD  DIM       8      8        215   Time record updated
PMMIWUSR  DIM       10     10       223   WEB User ID
PMMIMVBR  DIM       1      1        233   Mech.Vent.Billing Record? 1=Yes
.                                         NZ Private billing - 1 = Pack Item
PMMIUNIQ  DIM       10     10       234   Unique id for theatre booking
PMMICNTR  DIM       6      6        244   Contract/Health Fund
PMMISUNQ  DIM       3      3        250   Subsequence Proc.Unique No(NZ Priv)
.                                         or Banding (CAT TB)(Theatre Only)
PMMIINCT  DIM       1      1        253   Inclusion Type 0=Inc in contract
.                                                     0=Inc in contract
.                                                     1=Bill to Patient
.                                                     2=Extra to contract
.                                         OTHERS - Indicator to Print Procedure
.                                                  Date/Time on EMR Invoice
.                                                  1= Yes, 0=No
.                                         1=Community billing - item from Regime
PMMISUBN  DIM       3      3        254   Sub Item Code
PMMIEDAD  DIM       10     10       257   ED Attending Doctor
PMMISVTM  DIM       8      8        267   Service Time (HH:MM:SS)
PMMICCON  DIM       1      1        275   CMBS Confirmed 1=Yes
PMMISCHF  FORM      3.2    4        276   Schedule Fee (AMA=0,MBS=0.75, 0.85)
PMMIITYP  DIM       1      1        280   Item Type (0=MBS 1=AMA)
.                                         The following values are for NZ
.                                         Priv.Billing
.                                         2=Exploding Misc.itm(Norecalc.charge)
.                                         3=Not validating Misc.Code(HL7 msg)
PMMIDTCR  DIM       8      8        281   Date Created
PMMITMCR  DIM       8      8        289   Time Created
PMMIIDCR  DIM       10     10       297   User ID Created
PMMITMNO  DIM       1      1        307   Theatre Team Number (oprpmbaf)
PMMIPMBS  DIM       3      3        308   Patient Theater MBS Counter
.                                         (oprpmbaf)
PMMIRBRS  DIM       3      3        311   Rebate Reason (CAT Rr)
PMMICTYP  DIM       1      1        314   Consumption Type
.                                         0 or Blank = Used
.                                         1 = Not Used
.                                         2 = Expired
PMMIACOI  DIM       1      1        315   After Care Override Indicator
.                                         0-No
.                                         1-Yes
PMMIDSOV  DIM       1      1        316   Duplicate Service Override
.                                         0-No
.                                         1-Yes
PMMISTXT  DIM       50     50       317   Service Text
PMMILSPN  DIM       6      6        367   Location Specific Practice No
.                                         - LSPN value (can not be 000000)
PMMIMPOV  DIM       1      1        373   Multi Procedure Override
.                                         0-No
.                                         1-Yes
PMMINMPT  DIM       2      2        374   Number of Patients Seen
PMMISDCD  DIM       3      3        376   Self Deem Code (Cat Sd)
PMMITDUR  DIM       3      3        379   Time Duration (Mins)
PMMIROVR  DIM       3      3        382   Restrictive Override Code (Cat Ro)
.                                         Associated Field Position 1-2 values:
.                                         SP - Separate Sites
.                                         NR - Not Related (care plans)
.                                         NC - Not for Comparison
PMMIHOSI  DIM       1      1        385   Hospital Indicator
.                                         0 or Blank = No
.                                         1 = Yes
PMMISPAR  DIM       49     49       386   Spare variable
.
.End of Record                      435
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       200
.
DEFPATH   DIM       60
DIM60     DIM       60
DISNUM    FORM      4
.
ERORTEXT  DIM       80
.
INPFILE   DIM       8
.
MINUS3    FORM      "-3"
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
SAVEFLG   FORM      1
.
USINGORC  DIM       1
.
.
. PROGRAM CONSTANTS
. -----------------
CREATEXT  INIT    "Log file created "
DASHLINE  INIT      "--------------------------------------------------":
                    "------------------------------"
ENDGTEXT  INIT    "Ending   fixit (Option "
OPENTEXT  INIT    "Log file opened "
SP60      INIT    "                                                            "
STARTTXT  INIT    "Starting fixit (Option "
.
.
.
PRGNAM    INIT      "CONVERSION PMSMTIFD"
PRGID     INIT      "X01PMMTI"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
.
          CALL      OLOG0000                      * create/open log file
MAIN0500  CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000:             * run c-isam fixit
                            MAIN2000              * run oracle fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9000
.
MAIN2000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN0500:               * no
                          MAIN9000                * cancel
.
MAIN5000  BRANCH    COPTION,MAIN6000:             * c-isam fixit
                            MAIN7000              * oracle fixit
.
.         Running c-isam fix
.
MAIN6000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9000
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/zip saved file
          GOTO      MAIN9000
.
.         Running oracle fix
.
MAIN7000  CALL      OFIX0000                      * run oracle fix
.
MAIN9000  CALL      CLOG0000                      * close log file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
          
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  MOVE      "No",USINGORC
          REP       "yYnN",USINGORC
          MOVE      USINGORC,KEY1
          REP       "Y-N-",KEY1
          MATCH     "-",KEY1
          GOTO      OPTN9200 IF NOT EQUAL
.
          DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)"
          MATCH     "Y",USINGORC
          IF        @EQUAL
            DISPLAY   *P1:6,*V2LON,TWO,*HOFF,". Run Fixit (Oracle only)"
          ENDIF
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * run c-isam fixit
                            OPTN9000             * run oracle fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9200  DISPLAY   *P1:3,*EF,*P1:4," Oracle Option has not been set up ":
                          "correctly":
                    *P1:6," Program will terminate    - ";
          MOVE      "Oracle Option has not been set up correctly",ERORTEXT
          WRITE     LOGFILE,SEQ;*+,ERORTEXT
          CALL      HITENTER
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
.
PREP0000  CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
.
          CALL      SLOG0000                      * write start to log file
.
          MOVE      "pmsmtiaf",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat pmsmtiaf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "spmmtiaf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat pmsmtiaf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "spmmtiaf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "pmsmtiaf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Unable to remove the original file.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "spmmtiaf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            MOVE      "Unable to open saved original file.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "pmsmtiaf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.      eg    APPEND    " 195 UC9-16",CMDLINE
.
          APPEND    " 435 UC1-8,9-14",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has 
.  to be repeated with for each individual index
.
.-------- PMSMTIA2
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC1-8,107-107,9-14",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.-------- PMSMTIA3
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC107-107,207-214,1-8,9-14",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.-------- PMSMTIA4
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC1-8,107-107,178-180,9-14",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.-------- PMSMTIA5
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC1-8,244-249,107-107,178-180,9-14",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.-------- PMSMTIA6
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC1-8,257-266,9-14",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      PMSMTIA1,NEWFILE
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          GOTO      PREP9999
.
PREP5000  MOVE      "Old file not found.  ",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  MOVE      "Unable to copy original file.  (Error: ",ERORTEXT
          ENDSET    ERORTEXT
          APPEND    TASKID,ERORTEXT
          APPEND    ")  ",ERORTEXT
          RESET     ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
            MOVE      "File does not exist",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
              MOVE      "File has already been converted",ERORTEXT
              WRITE     LOGFILE,SEQ;ERORTEXT
            ELSE
              DISPLAY   "has caused an IO error - ";
              MOVE      "File has caused an IO Error",ERORTEXT
              WRITE     LOGFILE,SEQ;ERORTEXT
            ENDIF
          ENDIF
          MOVE      "File will be bypassed",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *+,ERORTEXT
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY14,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
.  All necessary changes to the files field lengths, date changes
.  or any other sort of modification must be made here
.
.    eg    PACK      DSPARE,SP70
.          PACK      PTDSDMDC,SP10
.          PACK      PTDSDDRG,SP10
.
          MOVE      SP70,PMMISPAR
          MOVE      SP70,PMMITMNO
          UNPACK    OLDIPMBS,PMMITMNO,D2
          PACK      PMMIPMBS,SP1,D2,SP70
.
.                                               * Pack key here
          PACK      KEY14,PMMIVISN,PMMITRAN,SP70
          CALL      WRPMMTI1                    * write to new file
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     PMSMTIA1                      * close new file
          CLOSE     OLDFILE1                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS:
                    *P1:24;
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
.
VALD0000  MOVE      ZERO,EXIT
.
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            MOVE      "Directory path must end with a slash (/) ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Directory does not exist ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or zip save file                                     *
.**********************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "spmmtiaf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "gzip -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Saved file not zipped or removed.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:24,*EL,*B,"Finished processing  ",CDATE,SP1,CTIMEIS:
                    ".  ";
          CALL      HITENTER
.
          CALL      ELOG0000                     * write an end log entry
.
ENDP9999  RETURN
+
.**********************************************************************
.*        DEFT0000 -  Get the default directory                       *
.**********************************************************************
.
DEFT0000  EXECUTE   "ldat pmsmtiaf.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  MOVE      "Error finding 'pmsmtiaf'.  ",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.*                            OLOG0000                                       *
.*                    Open the log file                                      *
.*****************************************************************************
.
OLOG0000  CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      LOGFILE,"x01pmmti.log"       * open log file
          TRAPCLR   IO
          IF        OVRCD = 1
            PREP      LOGFILE,"x01pmmti.log"     * create and open log file
            WRITE     LOGFILE,MINUS3;DASHLINE
            WRITE     LOGFILE,SEQ;CREATEXT,CDATE,SP1,CTIMEIS
          ELSE
            WRITE     LOGFILE,MINUS3;DASHLINE
            WRITE     LOGFILE,SEQ;OPENTEXT,CDATE,SP1,CTIMEIS
          ENDIF
.
OLOG9999  RETURN
+
.*****************************************************************************
.*                            CLOG0000                                       *
.*                    Close the log file                                     *
.*****************************************************************************
.
CLOG0000  CLOSE     LOGFILE
.
CLOG9999  RETURN
+
.*****************************************************************************
.*                            SLOG0000                                       *
.*                    Write a start log to the log file                      *
.*****************************************************************************
.
SLOG0000  WRITE     LOGFILE,SEQ;DASHLINE
          WRITE     LOGFILE,SEQ;STARTTXT,COPTION,RBRACK,SP1,CDATE,SP1:
                                   CTIMEIS
.
SLOG9999  RETURN
+
.*****************************************************************************
.*                            ELOG0000                                       *
.*                    Write an end log to the log file                       *
.*****************************************************************************
.
ELOG0000  WRITE     LOGFILE,SEQ;ENDGTEXT,COPTION,RBRACK,SP1,CDATE,SP1:
                                   CTIMEIS
ELOG9999  RETURN
+
.*****************************************************************************
.*                            OFIX0000                                       *
.*                    Oracle fix to "PMSMTIFD"                               *
.*****************************************************************************
.
OFIX0000  DISPLAY   *P1:24,*EL;
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,*EL,"Record No. :";
.
          CALL      SLOG0000                      * write start to log file
.
          OPEN      PMSMTIA1,"pmsmtiaf"
.
          PACK      KEY14,SP70
          CALL      OFDUMYIO                      * position at start of file
OFIX0500  CALL      OFDUMYIO                      * read next record
          BRANCH    OVRCD,OFIX9000                * eof - finished
.
          ADD       ONE,RECNUM                    * increment record counter
          ADD       ONE,DISNUM                    * Display counter
.         
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM 
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
.>>>>>>>  Add code here to populate/update variables
.
          CALL      OFDUMYIO
.
          GOTO      OFIX0500                     * get next record
.         
OFIX9000  CLOSE     PMSMTIA1
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Update completed ",CDATE,SP1,CTIMEIS:
                    *P1:24;
          CALL      HITENTER
.
          CALL      ELOG0000                     * write an end log entry
.
OFIX9999  RETURN
.
OFDUMYIO  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
.                  ******************************    OLD IO ROUTINES
.                  Copy the Positional Read (RS or RDS) from original I/O file,
.                  - change Label to READSOLD and the index name to OLDFILE1
.
READSOLD  RESET     KEY14
          READ      OLDFILE1,KEY14;;
          RETURN
.
.                  Copy the Read Next (RK or RDK) from the original I/O file
.                  - change Label to READKOLD and the index name to OLDFILE1
.                  - change any fields that are changing to a prefix of OLD,
.                    eg. PTEDSPAR becomes OLDDSPAR
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;PMMIVISN,PMMITRAN,PMMIURNO,PMMISYST,PMMIITEM:
                             PMMIDESC,PMMIDES2,PMMITDAT,PMMIRTYP,PMMIREFN:
                             PMMIAMTT,PMMIAMTG,PMMIAMTH,PMMIAMTP,PMMIRBAT:
                             PMMITDOC,PMMISERV,PMMIODOC,PMMISESS,PMMIMGRP:
                             PMMIBTCH,PMMIINVN,PMMIGSTA,PMMIGSTC,PMMIGSTM:
                             PMMIDUPD,PMMITUPD,PMMIWUSR,PMMIMVBR,PMMIUNIQ:
                             PMMICNTR,PMMISUNQ,PMMIINCT,PMMISUBN,PMMIEDAD:
                             PMMISVTM,PMMICCON,PMMISCHF,PMMIITYP,PMMIDTCR:
                             PMMITMCR,PMMIIDCR,OLDIPMBS,PMMIRBRS,PMMICTYP:
                             PMMIACOI,PMMIDSOV,PMMISTXT,PMMILSPN,PMMIMPOV:
                             PMMINMPT,PMMISDCD,PMMITDUR,PMMIROVR,PMMIHOSI:
                             PMMISPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
.
.                  ******************************    NEW IO ROUTINES
.                  Copy the Write statement from the new I/O file           
.                  (no changes are needed to this routine)               
.
WRPMMTI1  RESET     KEY14
          WRITE     PMSMTIA1,KEY14;PMMIVISN,PMMITRAN,PMMIURNO,PMMISYST,PMMIITEM:
                                   PMMIDESC,PMMIDES2,PMMITDAT,PMMIRTYP,PMMIREFN:
                                   PMMIAMTT,PMMIAMTG,PMMIAMTH,PMMIAMTP,PMMIRBAT:
                                   PMMITDOC,PMMISERV,PMMIODOC,PMMISESS,PMMIMGRP:
                                   PMMIBTCH,PMMIINVN,PMMIGSTA,PMMIGSTC,PMMIGSTM:
                                   PMMIDUPD,PMMITUPD,PMMIWUSR,PMMIMVBR,PMMIUNIQ:
                                   PMMICNTR,PMMISUNQ,PMMIINCT,PMMISUBN,PMMIEDAD:
                                   PMMISVTM,PMMICCON,PMMISCHF,PMMIITYP,PMMIDTCR:
                                   PMMITMCR,PMMIIDCR,PMMITMNO,PMMIPMBS,PMMIRBRS:
                                   PMMICTYP,PMMIACOI,PMMIDSOV,PMMISTXT,PMMILSPN:
                                   PMMIMPOV,PMMINMPT,PMMISDCD,PMMITDUR,PMMIROVR:
                                   PMMIHOSI,PMMISPAR
          RETURN
.
.                  For Oracle fixits, you will probably also need to copy
.                  the RS.., RK.... & UP.... I/O routines for the new FD.
.
          INC       STD001IO
