.***************************************************************************
.* System    :   Common                                                    *
.* Program   :   F02IBDPT                                                  *
.* Desc      :   This program will copy printer departments from ibacodef  *
.*               to ibadptaf                                               *
.***************************************************************************
.* Date      :   30/03/2011                                                *
.* Author    :   Jill Parkinson                                            *
.* Modificaitons :                                                         *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       IBAPRTFD/INC
          INC       IBADPTFD/INC
          INC       IBAPDDFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
RECNUM    FORM      8
.
PRGID     INIT      "F02IBDPT"
PRGNAM    INIT      "Copy printer departments from ibacodef"
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      IBACODE1,"ibacodef"
          OPEN      IBADPTA1,"ibadptaf"
          OPEN      IBAPDDA1,"ibapddaf"
          OPEN      IBAPDDA3,"ibapddaf"
          OPEN      IBAPRTA1,"ibaprtaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,RECNUM
          PACK      KEY5,SP70
          CALL      RDSICOD1
PROC1000  CALL      RDKICOD1
          BRANCH    OVRCD,PROC9000
.
          ADD       ONE,RECNUM
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MATCH     "~~",PRXCATA
          GOTO      PROC1000 IF EQUAL
.
          MATCH     "  ",PRXCATA
          GOTO      PROC1000 IF EQUAL
.
          MATCH     SP3,PRXCODE
          IF        !@EQUAL
            CALL      WRPDD000           * write to department details table
            GOTO      PROC1000
          ENDIF
.
          PACK      KEY6,PRXCATA,SP70
          CALL      RDIBDPT1           * write to department table
          IF        OVRCD<>1
            GOTO      PROC1000
          ENDIF
.
          PACK      IBDPDEPT,PRXCATA,SP70
          PACK      IBDPDESC,PRXDESC,SP70
          PACK      IBDPHOSP,SP70
          PACK      IBDPWARD,SP70
          PACK      IBDPEMRS,SP70
          PACK      IBDPOUTL,SP70
          PACK      IBDPSPAR,SP70
.
          PACK      KEY6,IBDPDEPT,SP70
          CALL      RAIBDPT1
          IF        OVRCD=1
            CALL      WRIBDPT1
          ENDIF
.
          GOTO      PROC1000
.
PROC9000  CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
PROC9999  RETURN
+
.**************************************************************************
.*                               WRPDD000              Called by: PROC0000*
.* Write to department details table                                      *
.**************************************************************************
WRPDD000  PACK      KEY6,PRXCODE,SP70
          CALL      RDPRTA1            * Does printer exist?
          IF        OVRCD<>1
            GOTO      WRPDD999
          ENDIF
.
          PACK      KEY12,PRXCATA,SP4,PRXCODE,SP70
          CALL      RDIBPDD1           * Already exists on department details
          IF        OVRCD<>1
            GOTO      WRPDD999
          ENDIF
.
          MOVE      ONE,F3
          PACK      KEY15,PRXCATA,SP4,PRXCODE,Z70
          CALL      RSIBPDD1
          CALL      RPIBPDD1
          BRANCH    OVRCD,WRPDD100
.
          MATCH     IBPDDEPT,PRXCATA
          GOTO      WRPDD100 IF NOT EQUAL
.
          MOVE      IBPDSEQU,F3 
.
WRPDD100  MOVE      F3,IBPDSEQU
          PACK      IBPDDEPT,PRXCATA,SP70
          PACK      IBPDPRTR,SP3,PRXCODE,SP70
          PACK      IBDPSPAR,SP70
.
          PACK      KEY12,IBPDDEPT,IBPDPRTR,SP70
          CALL      RAIBPDD1
          IF        OVRCD=1
            CALL      WRIBPDD1
          ENDIF
.
WRPDD999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       IBAPRTIO/INC
          INC       IBADPTIO/INC
          INC       IBAPDDIO/INC
