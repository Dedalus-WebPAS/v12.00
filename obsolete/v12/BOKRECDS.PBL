.******************************************************************************
.* System        :  Operating Rooms                                           *
.*                                                                            *
.* Include Name  :  BOKRECDS.PBL                                              *
.*                                                                            *
.* Function      :  Display booking numbers starting with the entered surname *
.*                                                                            *
.* Author        :  Justin Coates 17/12/1990                                  *
.*                                                                            *
.* Index used    :  BOKRC1A2,"bokrc1af"                                       *
.*                                                                            *
.* Parameters    :  none                                                      *
.*                                                                            *
.* Modifications :                                                            * 
.*         V5.07.01 16/06/99 Andrew Peel                                      *
.*                  Fixed display for bookings search, DOB & booking type     *
.*         V5.07    12/10/98 Jim Stathopoulos                                 *
.*                  507 Changes                                               *
.*         V5.01    22/09/92 J.Tan    SRF 117548                              *
.*                  Mods to stop processing as the surname changed            *
.******************************************************************************
.
BOKRECDS  DISPLAY   *P1:4,*EF:
                    *P30:4,*V2LON,"BOOKINGS",*HOFF:
                    *P10:5,"Enter Surname :":
                    *P55:5,"Booking Type : ",*V2LON,BTYPE,*HOFF
.
          KEYIN     *P26:5,*DV,UNDLN20:
                    *P26:5,*V2LON,SURNAME:
                    *P26:5,*DV,SURNAME
.         
          PACK      KEY28,SURNAME,SP10      * position at first surname
          CALL      RSBKREC2
.
          MOVE      FIVE,CVERT              * line counter
          MOVE      ONE,CCOL                * start on LHS of screen
.
          DISPLAY   *P1:5,*EF,*V2LON,*ULON:
                    *P1:5," Book No":
                    *P10:5," Exp Date ":
                    *P21:5,"Surname             ":
                    *P42:5,"Given Names              ":
                    *P68:5," D.O.B. ":
                    *P77:5,"Type";
.
. ----------------------------------------
. ------- next read on booking file ------
. ----------------------------------------
.
BKREC100  CALL      RKBKREC2  
          BRANCH    OVRCD,BKREC999          * end of file
.
          MATCH     BKRESNAM,SURNAME
          GOTO      BKREC999 IF NOT EQUAL   * not the same surname
.
. ----------------------------------
. ------- increment counters -------
. ----------------------------------
.
BKREC200  ADD       ONE,CVERT               * inc line counter
.
          COMPARE   "23",CVERT
          GOTO      BKREC400 IF NOT LESS    * need new page
.
. -------------------------------------------
. ------- display all the information -------
. -------------------------------------------
.
BKREC300  
          MATCH     ZEROUR,BKREURNO
          GOTO      BKREC310 IF EQUAL       * on pram file
.
          MOVE      BKREURNO,KEY8           * UR number
          CALL      RDMAST1                 * read master file
          BRANCH    OVRCD,BKREC100          * invalid UR number
          GOTO      BKREC350
.
BKREC310  MOVE      BKREBOOK,KEY8           * booking number
          CALL      RDPRAM1                 * read pre-admission file
          BRANCH    OVRCD,BKREC100          * invalid booking number
.
BKREC350  UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          MOVE      CPCDATE,DIM10            * store DOB as dd/mm/ccyy
.
          MOVE      SP10,CPCDATE
          MATCH     SP8,BKREEDAT
          GOTO      BKREC360 IF EQUAL       * no date
.
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE            * expected date
.
BKREC360  DISPLAY   *P1:CVERT,*V2LON,BKREBOOK,*HOFF:
                    *P10:CVERT,CPCDATE:
                    *P21:CVERT,PSNAME:
                    *P42:CVERT,PGNAME:
                    *P67:CVERT,DIM10:
                    *P78:CVERT,BKRETYPE
.
          GOTO      BKREC100
.
. -------------------------------
. ------- new page needed -------
. -------------------------------
.
BKREC400  DISPLAY   *P1:24,"( )ontinue, ( )irst, ( )ext ? ",*EL:
                    *V2LON,*P2:24,ANSC,*P14:24,ANSF,*P23:24,ANSN
.
          KEYIN     *P31:24,*DV,UNDLN1:
                    *P31:24,*V2LON,ANS
.
          REP       UPPLOW,ANS
.
          MATCH     ANSC,ANS
          GOTO      BKREC999 IF EQUAL       * Continue selected
.
          MATCH     ANSF,ANS
          GOTO      BOKRECDS IF EQUAL       * First selected
.
. --------------------------------------
. ------- (N)ext screen selected -------
. --------------------------------------
.
BKREC700  MOVE      SIX,CVERT               * first display row 
          MOVE      ONE,CCOL                * get back to LHS of screen
          DISPLAY   *P1:CVERT,*EF;
          GOTO      BKREC300                * display the data
.
BKREC999  RETURN
+
