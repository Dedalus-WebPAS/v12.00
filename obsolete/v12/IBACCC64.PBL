.***************************************************************************
.* System    :   CIAS                                                      *
.* Program   :   IBACCC64                                                  *
.* Desc      :   CIAS letter merge file generation                         *
.***************************************************************************
.* Date      :   18/03/2004                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will create a comma delimited export file    *
.*               for letter merges                                         *
.***************************************************************************
.* Mods      :                                                             *
.*              V11.00.01 01/04/2020  J.Tan             TSK 0868813        *
.*                        Changed PKNAME to DIM80                          *
.*              V10.03.01 25/02/2013 Ania P                CAR 281021      *
.*                        Removed unnecessary modification of constants.   *
.*               V9.03.00 18/03/2004 Ebon Clements         CAR 45491       *
.*                        Created program from IBACEA64                    *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       CCSCEAFD/INC
          INC       CCSCEBFD/INC
          INC       CCSCECFD/INC
          INC       CCSCEDFD/INC
          INC       CCSCEEFD/INC
          INC       CCSCEFFD/INC
          INC       CCSCEGFD/INC
          INC       CCSEPSFD/INC
          INC       CCSHCDFD/INC
          INC       CCSSTADF
          INC       CCSSTYFD/INC
          INC       CCSXHDFD/INC
          INC       PATMA1FD/INC
          INC       PATRE1FD/INC
.
DOWNFILE  FILE      ASCII, FIXED=200
.
. LOCAL VARIABLE DEFINITION
. -------------------------
ADMDATE   DIM       10
CMDLINE   DIM       127
CLV1DES   DIM       35
CLV2DES   DIM       35
CLV3DES   DIM       35
DISDATE   DIM       10
FROMDD    DIM       10
TODD      DIM       10
FROMAD    DIM       10
TOAD      DIM       10
DOWNNAME  DIM       40 
FILENAME  DIM       8
LEV1DESC  DIM       127
LEV2DESC  DIM       127
LEV3DESC  DIM       127
VISITDES  DIM       15
QUOTE     INIT      "#""
.
PRGID     INIT      "IBACCC64"
PRGNAM    INIT      "CIAS - Letter Merge Generation "
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000
.
ML1000    CALL      EXTR0000               * Create Letter Merge File
          BRANCH    EXIT,ML0100
          GOTO      ML9999
.
ML9999    CHAIN     PGM                    * chain back to program
.
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CCENTRY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          MOVE      "   64",AUDTSECT
          MOVE      " 37",AUDTPOST
          READ      CONTROLF,AUDTSECT;*AUDTPOST,AUDTFLAG
          IF        AUDTFLAG=1
            DISPLAY   *P56:24,"Opening ccsaudep";
            OPEN      CCSAUDEP,"ccsaudep"
          ENDIF
          DISPLAY   *P56:24,"Opening ccsceaaf";
          OPEN      CCSCEAA1,"ccsceaaf"
          DISPLAY   *P56:24,"Opening ccsepsaf";
          OPEN      CCSEPSA1,"ccsepsaf"
          DISPLAY   *P56:24,"Opening ccsepsaf";
          OPEN      CCSEPSA2,"ccsepsaf"
          DISPLAY   *P56:24,"Opening ccsepsaf";
          OPEN      CCSEPSA3,"ccsepsaf"
          DISPLAY   *P56:24,"Opening ccsepsaf";
          OPEN      CCSEPSA4,"ccsepsaf"
          DISPLAY   *P56:24,"Opening ccsepsaf";
          OPEN      CCSEPSA5,"ccsepsaf"
          DISPLAY   *P56:24,"Opening ccshcdaf";
          OPEN      CCSHCDA1,"ccshcdaf"
          DISPLAY   *P56:24,"Opening ccshcdaf";
          OPEN      CCSHCDA2,"ccshcdaf"
          DISPLAY   *P56:24,"Opening ccsstyaf";
          OPEN      CCSSTYA1,"ccsstyaf"
          DISPLAY   *P56:24,"Opening ccsxhdaf";
          OPEN      CCSXHDA1,"ccsxhdaf"
          DISPLAY   *P56:24,"Opening ccsxhdaf";
          OPEN      CCSXHDA2,"ccsxhdaf"
          DISPLAY   *P56:24,"Opening  patma1af           ";
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P56:24,"Opening  patma1af           ";
          OPEN      PATMA1A2,"patma1af"
          DISPLAY   *P56:24,"Opening  patmx1af           ";
          OPEN      PATMX1A1,"patmx1af"
          DISPLAY   *P56:24,"Opening  patre1af           ";
          OPEN      PATRE1A1,"patre1af"
          DISPLAY   *P1:24,*EL;
.
INIT9999  RETURN
.
****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Create Letter Merge File"
.
OPTN0500  KEYIN     *P1:16,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:16,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.**************************************************************************
.*                               EXTR0000              Called by: ML1000  *
.* Generate Letter Merge File                                             *
.**************************************************************************
.
EXTR0000  CALL      EEID0000                     * Keyin Extract Id
          BRANCH    EXIT,EXTR9999
.
          CALL      FILE0000                     * Keyin merge file name
.
          CALL      CONTQST                      * ok to continue
          BRANCH    CEXIT,EXTR5000,EXTR9999,EXTR9999    * 1=Yes, 2=No, 3=Cancel
.
EXTR5000  CALL      REPT0000                     * Create Merge Export File
.
EXTR9999  RETURN
.
.----------------------------------------------------------------------
. Enter Casemix Extract ID
.----------------------------------------------------------------------
EEID0000  CALL      CCSCOPCL         * Close Files
.
          DISPLAY   *P1:4,*EF
          KEYIN     *P1:4,"Extract ID : ",*V2LON,CCEAEID
          ENDSET    CCEAEID
          APPEND    SP70,CCEAEID
          RESET     CCEAEID
.
          MATCH     SP70,CCEAEID
          GOTO      EEID9000 IF EQUAL
.
          PACK      KEY4,CCEAEID,SP70
          CALL      RDCCEA1
          IF        OVRCD=1
            BEEP
            DISPLAY  *P1:24,"Invalid Extract ID";
            CALL      HITENTER
            GOTO      EEID0000
          ENDIF
.
          CALL      GETCME00
          CALL      CCSCOPOP         * Open Files
          MOVE      ZERO,EXIT
          GOTO      EEID9999
.
EEID9000  MOVE      TWO,EXIT
.
EEID9999  RETURN
.
.
.------------------------------------------------------------
.Keyin export data file name
.------------------------------------------------------------
FILE0000  DISPLAY   *P1:5,*EF
          KEYIN     *P1:5,"Export Data File Name : ",*V2LON,DOWNNAME
          PACK      DOWNNAME,DOWNNAME,SP70
.
          MATCH     SP70,DOWNNAME
          IF        @EQUAL
            DISPLAY  *P1:24,"Export data file name is mandatory";
            CALL      HITENTER
            GOTO      FILE0000
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      FILE9999
.
FILE9000  MOVE      ONE,EXIT
          GOTO      FILE9999
.
FILE9999  RETURN
.
.------------------------------------------------------------
. Print Report
.------------------------------------------------------------
REPT0000  CALL      OPEN0000
          BRANCH    EXIT,REPT9900
          DISPLAY  *P1:10,*EF
          MOVE      DOWNNAME,KEY80
          STRIP     KEY80
          ENDSET    KEY80
          APPEND    ".txt",KEY80
          RESET     KEY80
.
          DISPLAY  *P1:15,*EL,"Extracting Letter Merge File"
          DISPLAY  *P1:16,*EL,*V2LON,KEY80
.
          CALL      MAPEX000
.
          CALL      TRAN0000  * Transfer Files Now
          MOVE      ZERO,EXIT            * Exit
          GOTO      REPT9999
.
REPT9900  MOVE      THREE,EXIT            * Restore Screen
.
REPT9999  RETURN
.
.------------------------------------------------------------
. Open/Create Downlaod File
.------------------------------------------------------------
OPEN0000  OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATRE1A1,"patre1af"
.
          STRIP     DOWNNAME
          ENDSET    DOWNNAME
          APPEND    SP70,DOWNNAME
          RESET     DOWNNAME
.
          MATCH     SP70,DOWNNAME
          GOTO      OPEN9000 IF EQUAL
.
          STRIP     DOWNNAME
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      DOWNFILE,DOWNNAME
          TRAPCLR   IO
          BRANCH    OVRCD,OPEN1000
.
OPEN0100  BEEP
          KEYIN     *P1:24,*EL,"Export File Already Exists. Overwrite (":
                    *V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS
          REP       UPPLOW,ANS
          MOVE      ONE,EXIT
          MATCH     "N",ANS
          GOTO      OPEN9999 IF EQUAL
          MATCH     "Y",ANS
          GOTO      OPEN0100 IF NOT EQUAL
          CLOSE     DOWNFILE,DELETE
.
OPEN1000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PREP      DOWNFILE,DOWNNAME
          TRAPCLR   IO
          BRANCH    OVRCD,OPEN9100
          MOVE      ZERO,EXIT
          GOTO      OPEN9999
.
OPEN9000  BEEP
          MOVE      "Export Filename MUST be Specified - ",DISPMSG
          CALL      MESSAGE1
          MOVE      ONE,EXIT
          GOTO      OPEN9999
.
OPEN9100  BEEP
          MOVE      "Unable to Create Export File - ",DISPMSG
          CALL      MESSAGE1
          MOVE      ONE,EXIT
          GOTO      OPEN9999
.
OPEN9999  RETURN
.
.------------------------------------------------------------
. Extract Level 4
.------------------------------------------------------------
MAPEX000  CALL      WRTHED00
          DISPLAY   *P1:24,*EL,"Creating File       : ";
          MOVE      ZERO,SECTOR
          PACK      KEY18,SP70
          CALL      RSCCEC1
.
MAPEX100  CALL      RKCCEC1
          BRANCH    OVRCD,MAPEX900
          ADD       ONE,SECTOR
          IF        (SECTOR%50=0)
            DISPLAY   *P23:24,SECTOR
          ENDIF
          PACK      KEY18,CCECHCD,CCECEPS
          CALL      RDCCEP1
.
          PACK      KEY8,CCEPURN
          CALL      RDMAST1                                            
          IF        OVRCD=1
            MOVE      SP70,PTITL                                       
            MOVE      SP70,PADD1                                       
            MOVE      SP70,PADD2                                       
            MOVE      SP70,PSUBRB                                      
            MOVE      SP70,PADD4                                       
            MOVE      SP70,PPOST                                       
            MOVE      SP70,PNKNAME                                     
            MOVE      SP70,PNKADD1                                     
            MOVE      SP70,PNKADD2                                     
            MOVE      SP70,PNKSUBR                                     
            MOVE      SP70,PNKADD4                                     
            MOVE      SP70,PNKPOST                                     
          ENDIF
          STRIP     CCEPEPS
          MOVE      CCEPEPS,F8
          MOVE      F8,KEY8
          CALL      RDRESP1
          IF        OVRCD=1
            PACK      PKNAME,SP70,SP20                            
            MOVE      SP70,PKADD1                                      
            MOVE      SP70,PKADD2                                      
            MOVE      SP70,PKSUBR                                      
            MOVE      SP70,PKADD4                                      
            MOVE      SP70,PKPOST                                      
          ENDIF
.
          CALL      WRTXT000
          GOTO      MAPEX100
.
MAPEX900  MOVE      ZERO,EXIT
.
MAPEX999  RETURN
.
.----------------------------------------------------------------------
. Write Header Details
.----------------------------------------------------------------------
WRTHED00  PACK     LEV1DESC,SP70,SP70
          PACK     LEV2DESC,SP70,SP70
          PACK     LEV3DESC,SP70,SP70
          MOVE     CCEALV1,KEY3
          CALL     RDCCST1
          STRIP    CCSTDES
          MOVELPTR CCSTDES,F3
          SFORMAT  LEV1DESC,F3
          MOVE     CCSTDES,LEV1DESC
.
          MATCH    SP70,CCEALV2
          IF       !@EQUAL
            MOVE     CCEALV2,KEY3
            CALL     RDCCST1
            STRIP    CCSTDES
            MOVELPTR CCSTDES,F3
            SFORMAT  LEV2DESC,F3
            MOVE     CCSTDES,LEV2DESC
          ENDIF
.
          MATCH    SP70,CCEALV3
          IF       !@EQUAL
            MOVE     CCEALV3,KEY3
            CALL     RDCCST1
            MOVE     CCSTDES,LEV3DESC
            STRIP    CCSTDES
            MOVELPTR CCSTDES,F3
            SFORMAT  LEV3DESC,F3
            MOVE     CCSTDES,LEV3DESC
          ENDIF
          MATCH     SP70,LEV1DESC
          IF        @EQUAL
            MOVE     "5",F3
            SFORMAT  LEV1DESC,F3
            MOVE     "Blank",LEV1DESC
          ENDIF
          MATCH     SP70,LEV2DESC
          IF        @EQUAL
            MOVE     "5",F3
            SFORMAT  LEV2DESC,F3
            MOVE     "Blank",LEV2DESC
          ENDIF
          MATCH     SP70,LEV3DESC
          IF        @EQUAL
            MOVE     "5",F3
            SFORMAT  LEV3DESC,F3
            MOVE     "Blank",LEV3DESC
          ENDIF
.
          WRITE     DOWNFILE,SEQ;QUOTE,"Episode No.",QUOTE,COMMA:
                    QUOTE,"U/R No.",QUOTE,COMMA:
                    QUOTE,"Title",QUOTE,COMMA:
                    QUOTE,"Surname",QUOTE,COMMA:
                    QUOTE,"Given Names",QUOTE,COMMA:
                    QUOTE,"Age",QUOTE,COMMA:
                    QUOTE,"Gender",QUOTE,COMMA:
                    QUOTE,LEV1DESC,QUOTE,COMMA:
                    QUOTE,LEV2DESC,QUOTE,COMMA:
                    QUOTE,LEV3DESC,QUOTE,COMMA:
                    QUOTE,"Admitted",QUOTE,COMMA:
                    QUOTE,"Discharged",QUOTE,COMMA:
                    QUOTE,"Address 1",QUOTE,COMMA:
                    QUOTE,"Address 2",QUOTE,COMMA:
                    QUOTE,"Address 3",QUOTE,COMMA:
                    QUOTE,"Address 4",QUOTE,COMMA:
                    QUOTE,"Post Code",QUOTE,COMMA:
                    QUOTE,"Next of Kin",QUOTE,COMMA:
                    QUOTE,"NOK Address 1",QUOTE,COMMA:
                    QUOTE,"NOK Address 2",QUOTE,COMMA:
                    QUOTE,"NOK Address 3",QUOTE,COMMA:
                    QUOTE,"NOK Address 4",QUOTE,COMMA:
                    QUOTE,"NOK Post Code",QUOTE,COMMA:
                    QUOTE,"Person Resp. for A/c.",QUOTE,COMMA:
                    QUOTE,"PRA Address 1",QUOTE,COMMA:
                    QUOTE,"PRA Address 2",QUOTE,COMMA:
                    QUOTE,"PRA Address 3",QUOTE,COMMA:
                    QUOTE,"PRA Address 4",QUOTE,COMMA:
                    QUOTE,"PRA Post Code",QUOTE
.
WRTHED99  RETURN
.
.------------------------------------------------------------
. Print Line
.------------------------------------------------------------
WRTXT000  PACK      LEV1DESC,SP70,SP70
          PACK      LEV2DESC,SP70,SP70
          PACK      LEV3DESC,SP70,SP70
          PACK      KEY13,CCEALV1,CCECLV1
          CALL      RDCCXH1
          IF        OVRCD=0
            MOVE      CCXHDES,LEV1DESC
          ENDIF
          PACK      KEY13,CCEALV2,CCECLV2
          CALL      RDCCXH1
          IF        OVRCD=0
            MOVE      CCXHDES,LEV2DESC
          ENDIF
          PACK      KEY13,CCEALV3,CCECLV3
          CALL      RDCCXH1
          IF        OVRCD=0
            MOVE      CCXHDES,LEV3DESC
          ENDIF
          UNPACK    CCEPDDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DISDATE
          UNPACK    CCEPADAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,ADMDATE
.
          WRITE     DOWNFILE,SEQ;QUOTE,CCECEPS,QUOTE,COMMA:
                    QUOTE,CCEPURN,QUOTE,COMMA:
                    QUOTE,PTITL,QUOTE,COMMA:
                    QUOTE,CCEPSUR,QUOTE,COMMA:
                    QUOTE,CCEPGIV,QUOTE,COMMA:
                    QUOTE,CCEPAGEY,QUOTE,COMMA:
                    QUOTE,CCEPSEX,QUOTE,COMMA:
                    QUOTE,LEV1DESC,QUOTE,COMMA:
                    QUOTE,LEV2DESC,QUOTE,COMMA:
                    QUOTE,LEV3DESC,QUOTE,COMMA:
                    QUOTE,ADMDATE,QUOTE,COMMA:
                    QUOTE,DISDATE,QUOTE,COMMA:
                    QUOTE,PADD1,QUOTE,COMMA:
                    QUOTE,PADD2,QUOTE,COMMA:
                    QUOTE,PSUBRB,QUOTE,COMMA:
                    QUOTE,PADD4,QUOTE,COMMA:
                    QUOTE,PPOST,QUOTE,COMMA:
                    QUOTE,PNKNAME,QUOTE,COMMA:
                    QUOTE,PNKADD1,QUOTE,COMMA:
                    QUOTE,PNKADD2,QUOTE,COMMA:
                    QUOTE,PNKSUBR,QUOTE,COMMA:
                    QUOTE,PNKADD4,QUOTE,COMMA:
                    QUOTE,PNKPOST,QUOTE,COMMA:
                    QUOTE,PKNAME,QUOTE,COMMA:
                    QUOTE,PKADD1,QUOTE,COMMA:
                    QUOTE,PKADD2,QUOTE,COMMA:
                    QUOTE,PKSUBR,QUOTE,COMMA:
                    QUOTE,PKADD4,QUOTE,COMMA:
                    QUOTE,PKPOST,QUOTE
.
WRTXT999  RETURN
.
.--------------------------------------------------------------------------
. Execute Transfer Script eg softerm
.--------------------------------------------------------------------------
TRAN0000  STRIP     DOWNNAME
.
          CLEAR     CMDLINE
          APPEND    "ibaccc64.us1 ",CMDLINE
          APPEND    DOWNNAME,CMDLINE
          APPEND    ".txt",CMDLINE
          APPEND    SP1,CMDLINE
          APPEND    CCEAEID,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID   * ibaccc51.us1 fullpathname filename
.
TRAN9999  RETURN
.
.======================================================================
. Determine Display Values for a Valid Casemix Extract Code
.======================================================================
GETCME00  MATCH     SP70,CCEAFDD
          IF        @EQUAL
            MOVE      "Start     ",FROMDD
          ELSE
            UNPACK    CCEAFDD,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,FROMDD
          ENDIF
.
          MATCH     "99999999",CCEATDD
          IF        @EQUAL
            MOVE      "Finish    ",TODD
          ELSE
            UNPACK    CCEATDD,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,TODD
          ENDIF
.
          MATCH     SP70,CCEAFAD
          IF        @EQUAL
            MOVE      "Start     ",FROMAD
          ELSE
            UNPACK    CCEAFAD,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,FROMAD
          ENDIF
.
          MATCH     "99999999",CCEATAD
          IF        @EQUAL
            MOVE      "Finish    ",TOAD
          ELSE
            UNPACK    CCEATAD,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,TOAD
          ENDIF
.
          MOVE     CCEAVIS,F1
          BRANCH   F1,GETCME10,GETCME20,GETCME30,GETCME35
          MOVE     "All        ",VISITDES
          GOTO     GETCME40
GETCME10  MOVE     "A & E      ",VISITDES
          GOTO     GETCME40
GETCME20  MOVE     "Outpatients ",VISITDES
          GOTO     GETCME40
GETCME30  MOVE     "Inpatients",VISITDES
          GOTO     GETCME40
GETCME35  MOVE     "Allied Health",VISITDES
          GOTO     GETCME40
.
GETCME40  MOVE     SP70,CLV1DES
          MOVE     "Not Applicable",CLV2DES
          MOVE     "Not Applicable",CLV3DES
.
          MOVE     CCEALV1,KEY3
          CALL     RDCCST1
          MOVE     CCSTDES,CLV1DES
.
          MATCH    SP70,CCEALV2
          IF       !@EQUAL
            MOVE     CCEALV2,KEY3
            CALL     RDCCST1
            MOVE     CCSTDES,CLV2DES
          ENDIF
.
          MATCH    SP70,CCEALV3
          IF       !@EQUAL
            MOVE     CCEALV3,KEY3
            CALL     RDCCST1
            MOVE     CCSTDES,CLV3DES
          ENDIF
.
          MOVE     CCEAHCD,KEY2
          CALL     RDCCHC1
.
GETCME99  RETURN
.
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       CCSCEAIO/INC
          INC       CCSCEBIO/INC
          INC       CCSCECIO/INC
          INC       CCSCEDIO/INC
          INC       CCSCEEIO/INC
          INC       CCSCOPCD
          INC       CCSEPSIO/INC
          INC       CCSHCDIO/INC
          INC       CCSSTACD
          INC       CCSSTYIO/INC
          INC       CCSXHDIO/INC
          INC       PATMA1IO/INC
          INC       PATRE1IO/INC
