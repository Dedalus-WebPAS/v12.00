.******************************************************************************
.* System         : Patient Billing                                           *
.* Program        : FXICD105.PBL                                              *
.* Name           : Convert ICD10 V5 file                                     *
.******************************************************************************
.* Date           : 10/05/2006                                                *
.* Author         : Mike Laming                                               *
.* Function       : Convert ICD10 Edition 5 file.  Should be a similar format *
.*                  to the Victorian HCS file.  Check before running.  This   *
.*                  originally copied from (FXICD113.PBL) FXICD104.PBL        *
.* Modifications  :                                                           *
.*                                                                            *
.*        V9.06.01  16/06/2006  Mike Laming   CAR 105244     HDP 2006         *
.*                  Fixes to ICD10Ed5 changes - increase file length, add OPEN*
.*        V9.06.00  15/05/2006  Mike Laming   CAR 105244     HDP 2006         *
.*                  Changes to upload to create ICD10Ed5 file "pat10d5f"      *
.******************************************************************************
.*        V9.04.02  10/06/2005  Mike Laming   CAR 60167                       *
.*                  Changes to upload to create ICD10V4 file "pat10d4f"       *
.*        V9.04.01  13/12/2004  Henry Son   CAR 55406                         *
.*                  Changes for upload of ICD Hsmart demo.                    *
.*        C.11.02   16/06/2004  Henry Son   CAR 49037                         *
.*                  Using Asterisk code for Diagnosis Prefix.                 *
.*        C.11.01   10/05/2004  Henry Son   CAR 49037                         *
.*                   Convert ICD10 version 4 file for 2004/2005 HDP changes.  *
.*                                                                            *
.******************************************************************************
+
          INC       STD001FD
          INC       PATCANFD/INC
          INC       PATCMPFD/INC
          INC       PATCONFD/INC
          INC       PATICDFD/INC
          INC       PATICOFD/INC
.
. ----- Tempfile variables -----
.
ICDFILE1  FILE      ASCII, FIXED=372
.
.Name     Type   Length  Physical  Start Loc.  Description
.----     ----   ------  --------  ----------  -----------
ICDTYP    DIM       1        1          1      Code type
ICDCODE   DIM       9        9          2      Code
ICDVALID  DIM       1        1         11      Code validity flag
ICD1AGE   DIM       1        1         12      1st age edit
ICD1LOW   DIM       2        2         13      1st low limit
ICD1HIGH  DIM       2        2         15      1st high limit
ICD2AGE   DIM       1        1         17      2nd age edit
ICD2LOW   DIM       2        2         18      2nd low limit
ICD2HIGH  DIM       2        2         20      2nd high limit
ICDSEX    DIM       1        1         22      Sex edit
ICDCDPR   DIM       1        1         23      Coding practices edit
ICDAREA   DIM       1        1         24      Area edit
ICDDGAK   DIM       2        2         25      Dagger & asterisk edit
ICDACRQ   DIM       1        1         27      Additional code req edit
ICDORPRO  DIM       1        1         28      OR Procedure
ICDBKNO   DIM       4        4         29      Block no
ICD104MP  DIM       9        9         33      ICD10 v4 map code      *I-105244
ICD103MP  DIM       9        9         42      ICD10 v3 map code
ICD102MP  DIM       9        9         51      ICD10 v2 map code
ICD101MP  DIM       9        9         60      ICD10 v1 map code
ICD9MAP   DIM       9        9         69      ICD9 map code
ICDSDSC   DIM      40       40         78      Short description
ICDLDSC1  DIM      127      127        118     Long description 1
ICDLDSC2  DIM      127      127        245     Long description 2
ICDLDSC3  DIM       1        1         372     Long description 3
.
.End of Record                         373
.
. ----- Program variables -----
.
CMPFLG    FORM      1
FORM3     FORM      3
ICDFILE   DIM       12
KEY127    DIM       127
RECOUNT   FORM      6
TEMP1     FILE      ASCII, FIXED=256
.
. ----- Program constants -----
.
SP127     INIT      "                                          ":
                    "                                          ":
                    "                                           "
PRGID     INIT      "FXICD105"
PRGNAM    INIT      "Convert ICD10 Ed.5 file"
VERSION   INIT      "V12.00.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
ML1000    CALL      OPTN0000                * Select option
          BRANCH    EXIT,ML9999
.
ML2000    CALL      FILE0000                * Keyin ICD10 filename
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML3000,ML2000,ML1000
.
ML3000    CALL      OPEN0000                * Open files
          CALL      CONV0000                * Convert the file
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                      Initialise Variables & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen header
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patcanaf";
          OPEN      PATCANA1,"patcanaf"
.
          DISPLAY   *P64:24,"patcmpaf";
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATCMPA2,"patcmpaf"
          TRAPCLR   IO
          MOVE      OVRCD,CMPFLG            * Complex flag
.
          READ      CONTROLF,SEVENTY9;*222,PTCNI10D
.
INIT9000  MOVE      ZERO,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Run Conversion":
                    *P1:7,"Select Option :";
.
OPTN1000  KEYIN     *P17:7,*DV,UNDLN2,*P17:7,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  FILE0000              Called by: ML0000   *
.*                            Keyin ICD10 Filename                            *
.******************************************************************************
FILE0000  MOVE      SP20,ICDFILE
          DISPLAY   *P1:9,"ICD10 filename : ";
.
FILE1000  KEYIN     *P18:9,*V2LON,ICDFILE;
.
          ENDSET    ICDFILE
          APPEND    SP20,ICDFILE
          RESET     ICDFILE
.
          MATCH     SP20,ICDFILE
          GOTO      FILE9500 IF EQUAL
.
          SCAN      DOT,ICDFILE
          IF        !@EQUAL
            STRIP     ICDFILE
            ENDSET    ICDFILE
            APPEND    ".txt",ICDFILE
            APPEND    SP20,ICDFILE
          ENDIF
          RESET     ICDFILE
.
          DISPLAY   *P18:9,*EL,*V2LON,ICDFILE;
          REP       "./",ICDFILE
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      ICDFILE1,ICDFILE
          TRAPCLR   IO
          COMPARE   ONE,OVRCD
          GOTO      FILE9000 IF NOT EQUAL   * File exists
.
          DISPLAY   *P1:24,*EL,*B,"Filename not found.  ";
          CALL      HITENTER
          GOTO      FILE1000
.
FILE9000  MOVE      ZERO,EXIT
          GOTO      FILE9999
.
FILE9500  MOVE      ONE,EXIT
FILE9999  RETURN
+
.******************************************************************************
.*                                  OPEN0000              Called by: ML0000   *
.*                                 Open Files                                 *
.******************************************************************************
OPEN0000  DISPLAY   *P1:23,*EF,"Setting up ICD10 files ...":
                    *P1:24,"Initialising : ";
          OPEN      PATI15D1,"pat10d5f"
          OPEN      PATI15D2,"pat10d5f"
.
          MOVE      ZERO,RECOUNT
          PACK      KEY9,SP9
.                                           * Position on ICD10V5 dis file
OPEN1000  CALL      RSPT15D1
.
.                                           * Read ICD10V5 disease record
OPEN2000  CALL      RKPT15D1
          BRANCH    OVRCD,OPEN3000
.
          ADD       ONE,RECOUNT
          IF        RECOUNT%100=1
            DISPLAY   *P16:24,*V2LON,DPCODE,*P74:24,RECOUNT;
          ENDIF
.
....      IF        PT0DV4CD=1 & PT0DV3CD=0 & PT0DV2CD=0 & PT0DV1CD=0
          IF        PT0DV5CD=1 & PT0DV4CD=0 & PT0DV3CD=0&PT0DV2CD=0&PT0DV1CD=0
            PACK      KEY9,DPCODE
            CALL      DEPT15D1              * Delete ICD10 V5 new codes C-60167
            GOTO      OPEN1000
          ENDIF
.
.******************************************** start previous versions
...       MOVE      SP9,PT0DV1MP
...       MOVE      SP9,PT0DV2MP
...       MOVE      ZERO,PT0DV1CD
...       MOVE      ZERO,PT0DV2CD
...       MOVE      ZERO,PT0DV3CD
...       MOVE      ZERO,PT0DV4CD
.********************************************   end previous versions
          MOVE      ZERO,PT0DV5CD
.
          CALL      UPPT15D1              * Update ICD10V5 Disease recd
          GOTO      OPEN2000
.
OPEN3000  OPEN      PATI15O1,"pat10o5f"
          OPEN      PATI15O2,"pat10o5f"
          OPEN      PATI15O3,"pat10o5f"
.
          MOVE      ZERO,RECOUNT
          PACK      KEY9,SP9
.                                           * Position on ICD10 Opr file    
OPEN4000  CALL      RSPT15O1                * ICD10V5
.
.                                           * Read ICD10 Operation rec
OPEN5000  CALL      RKPT15O1                * ICD10V5
          BRANCH    OVRCD,OPEN9000
.
          ADD       ONE,RECOUNT
          IF        RECOUNT%100=1
            DISPLAY   *P16:24,*V2LON,OPCODE,*P74:24,RECOUNT;
          ENDIF
.
....      IF        PT0OV4CD=1 & PT0OV3CD=0 & PT0OV2CD=0 & PT0OV1CD=0
          IF        PT0OV5CD=1 & PT0OV4CD=0 & PT0OV3CD=0&PT0OV2CD=0&PT0OV1CD=0
            PACK      KEY9,OPCODE
            CALL      DEPT15D1              * Delete ICD10 V5 new code
            GOTO      OPEN4000
          ENDIF
. 
.******************************************** start previous versions
...       MOVE      SP9,PT0OV1MP
...       MOVE      SP9,PT0OV2MP
...       MOVE      ZERO,PT0OV1CD
...       MOVE      ZERO,PT0OV2CD
...       MOVE      ZERO,PT0OV3CD
...       MOVE      ZERO,PT0OV4CD
.********************************************   end previous versions
          MOVE      ZERO,PT0OV5CD
.
          CALL      UPPT15O1              * Update ICD10 operation recd
          GOTO      OPEN5000
.                              
.
OPEN9000  MOVE      ZERO,EXIT
OPEN9999  RETURN
+
.******************************************************************************
.*                                  CONV0000              Called by: ML0000   *
.*                               Run Conversion                               *
.******************************************************************************
CONV0000  MOVE      ZERO,RECOUNT
          DISPLAY   *P1:24,*EL,"Processing : ";
.
CONV1000  READ      ICDFILE1,SEQ;ICDTYP,ICDCODE,ICDVALID,ICD1AGE,ICD1LOW:
                                 ICD1HIGH,ICD2AGE,ICD2LOW,ICD2HIGH,ICDSEX:
                                 ICDCDPR,ICDAREA,ICDDGAK,ICDACRQ,ICDORPRO:
                                 ICDBKNO,ICD104MP,ICD103MP,ICD102MP,ICD101MP:
                                 ICD9MAP,ICDSDSC:
                                 ICDLDSC1,ICDLDSC2,ICDLDSC3
          GOTO      CONV8000 IF OVER
.
          ADD       ONE,RECOUNT
          IF        RECOUNT%100=1
            REP       UPPLOW,ICDSDSC
            DISPLAY   *P14:24,*V2LON,ICDCODE,SP2,ICDSDSC,SP2,RECOUNT;
          ENDIF
.
          MOVE      ICDCODE,KEY9
          CALL      FCOD0000                * Format code
          MOVE      KEY9,DPCODE
.
          MATCH     "P",ICDTYP
          IF        !@EQUAL
            MOVE      ZERO,PT0DV1CD         * Clear valid ICD10 version 1 code
            MOVE      ZERO,PT0DV2CD         * Clear valid ICD10 version 2 code
            MOVE      ZERO,PT0DV3CD         * Clear valid ICD10 version 3 code
            MOVE      ZERO,PT0DV4CD         * Clear valid ICD10 version 4 code
            MOVE      ZERO,PT0DV5CD         * Clear valid ICD10 Edition 5 code
.
            CALL      RDPT15D1              * Read ICD10 disease file
          ELSE
            MOVE      ZERO,PT0OV1CD         * Clear valid ICD10 version 1 code
            MOVE      ZERO,PT0OV2CD         * Clear valid ICD10 version 2 code
            MOVE      ZERO,PT0OV3CD         * Clear valid ICD10 version 3 code
            MOVE      ZERO,PT0OV4CD         * Clear valid ICD10 version 4 code
            MOVE      ZERO,PT0OV5CD         * Clear valid ICD10 Edition 5 code
.
            CALL      RDPT15O1              * Read ICD10 operation recd
          ENDIF
          CALL      CLER0000                * Clear variables
.
          MOVE      ICDLDSC1,DDESC          * Long description
          MOVE      ICDLDSC1,KEY127
          STRIP     KEY127
          MOVELPTR  KEY127,FORM3
          IF        FORM3>70
            PACK      DDESC,ICDSDSC,SP30    * Short description
          ENDIF
          REP       UPPLOW,DDESC
.
          MOVE      ICDVALID,DFLAG          * Validity of code
          REP       "YPNH",DFLAG
.
          MOVE      ICD1AGE,DAGEGP          * 1st age edit
.
          MOVE      ZERO,DLOW
          MOVE      ICD1LOW,DLOW            * 1st age low limit
          MOVE      ZERO,DHIGH
          MOVE      ICD1HIGH,DHIGH          * 1st age high limit
.
          MOVE      ICD2AGE,PT0D2AGP        * 2nd age edit
.
          MOVE      ZERO,PT0D2ALL
          MOVE      ICD2LOW,PT0D2ALL        * 2nd age low limit
          MOVE      ZERO,PT0D2AHL
          MOVE      ICD2HIGH,PT0D2AHL       * 2nd age high limit
.
          MOVE      ICDSEX,DSEX             * Sex edit
          MATCH     SP1,ICDSEX
          GOTO      CONV2000 IF EQUAL       * No sex restraint
.
          REP       "1A2B3C4D5E6F",DSEX
          REP       "A3B4C1D2E5F6",DSEX     * Map to IBA sex restraints
.
CONV2000  MOVE      SP1,PT0DADTP            * Default no asterisk/dagger type
          MOVE      SP9,PT0DSACD            * Default no suggested asterisk code
          SCAN      "+",ICDCODE
          GOTO      CONV3000 IF NOT EQUAL   * Not a dagger code
.
          MOVE      ANSD,PT0DADTP           * Dagger code
          MOVE      ICDLDSC1,KEY127
          CALL      GASC0000                * Get asterisk code
.
          MOVE      ICDLDSC2,KEY127
          CALL      GASC0000                * Get asterisk code
          GOTO      CONV4000
.
CONV3000  RESET     ICDCODE
          SCAN      "*",ICDCODE
          GOTO      CONV4000 IF NOT EQUAL   * Not an asterisk code
.
          MOVE      ANSA,PT0DADTP           * Asterisk code
.
CONV4000  RESET     ICDCODE
...       MOVE      ICDDGAK,DDAGGER         * Asterisk/dagger type
          MOVE      ICDDGAK,PT0DPRFX        * Diagnoses prefix code
          MOVE      ICDAREA,DAREA           * Area edit
.
.******************************************** Verify ICD9 codes
          CALL      F9MC0000                * Format ICD9 map code
          CALL      GCMF0000                * Get the complex map format code
.
.******************************************** Verify ICD10 Ver.1 codes
          MOVE      ICD101MP,KEY9
          CALL      FCOD0000                * Format code
          MOVE      KEY9,PT0DV1MP           * ICD10 version 1 map code
.
          MATCH     SP9,KEY9                * ICD10 version 1 valid?
          IF        @EQUAL
            MOVE      ZERO,PT0DV1CD     
          ELSE
            MOVE      ONE,PT0DV1CD     
          ENDIF
.
.******************************************** Verify ICD10 Ver.2 codes
          MOVE      ICD102MP,KEY9
          CALL      FCOD0000                * Format code
          MOVE      KEY9,PT0DV2MP           * ICD10 version 2 map code
.
          MATCH     SP9,KEY9                * ICD10 version 2 valid?
          IF        @EQUAL
            MOVE      ZERO,PT0DV2CD     
          ELSE
            MOVE      ONE,PT0DV2CD     
          ENDIF
.
.******************************************** Verify ICD10 Ver.3 codes
          MOVE      ICD103MP,KEY9
          CALL      FCOD0000                * Format code
.         MOVE      KEY9,PT0DV3MP           * ICD10 version 3 map code
.                                           * don't map anymore!
          MATCH     SP9,KEY9                * ICD10 version 3 valid?
          IF        @EQUAL
            MOVE      ZERO,PT0DV3CD     
          ELSE
            MOVE      ONE,PT0DV3CD     
          ENDIF
.
.******************************************** Verify ICD10 Ver.4 codes
          MATCH     SP9,ICD104MP                                      *I-105244
          IF        !@EQUAL
            MOVE      ONE,PT0DV4CD            * Valid ICD10 version 4 code
            MOVE      PT0DV4CD,DPT0DV4C
          ENDIF                                                       *I-105244
.                                                                     *I-105244
.******************************************** Verify ICD10 Ed.5 codes
          MOVE      ONE,PT0DV5CD            * Valid ICD10 edition 5 code
          MOVE      PT0DV5CD,DPT0DV5C       * required for I/O module
.
          MATCH     "H",DFLAG
          IF        @EQUAL
            MOVE      ZERO,PT0DV1CD
            MOVE      ZERO,PT0DV2CD
            MOVE      ZERO,PT0DV3CD
            MOVE      ZERO,PT0DV4CD
            MOVE      PT0DV4CD,DPT0DV4C                                *I-60167
            MOVE      ZERO,PT0DV5CD                                   *I-105244
            MOVE      PT0DV5CD,DPT0DV5C
          ENDIF
.
          MATCH     ANSP,ICDTYP
          GOTO      CONV5000 IF EQUAL       * Procedure code
.
          MOVE      ICDCDPR,PT0DCPRA        * Code practices edit
          MOVE      ICDACRQ,PT0DACRQ        * Additional code requirements edit
          MOVE      SP70,PT0DSPAR
.
CONV4500  PACK      KEY9,DPCODE
          CALL      RAPT15D1                * Pos read an ICD10 dis file rec
          IF        OVRCD=1
            CALL      WRPT15D1              * Write ICD10 Ed.5 disease file
          ELSE
            CALL      UPPT15D1              * Update ICD10 Ed.5 disease file
          ENDIF
.
          MATCH     "3",ICDACRQ
          GOTO      CONV1000 IF NOT EQUAL   * Not a cancer code
.
          MOVE      DPCODE,PTCCCODE
          MOVE      ONE,PTCCLATR
          PACK      PTCCSPAR,SP30
          PACK      KEY9,PTCCCODE
          CALL      RAPTCAN1                * Pos read a cancer code file rec
          COMPARE   ONE,OVRCD
          GOTO      CONV1000 IF NOT EQUAL   * Record already exists
.
          CALL      WRPTCAN1                * Write a cancer code file rec
          GOTO      CONV1000
.
CONV5000  MOVE      DPCODE,OPCODE
          MOVE      DDESC,ODESC
          MOVE      DFLAG,OFLAG
          MOVE      ICDBKNO,PT0OBLKN        * Block no
          MOVE      DAGEGP,OAGEGP
          MOVE      DLOW,OLOW
          MOVE      DHIGH,OHIGH
          MOVE      PT0D2AGP,PT0O2AGP
          MOVE      PT0D2ALL,PT0O2ALL
          MOVE      PT0D2AHL,PT0O2AHL
          MOVE      DSEX,OSEX
          MOVE      PT0DADTP,PT0OADTP
          MOVE      PT0DSACD,PT0OSACD
          MOVE      DDAGGER,ODAGGER
          MOVE      DAREA,OAREA
          MOVE      PT0DMI9C,PT0OMI9C
          MOVE      PT0DCMFT,PT0OCMFT
          MOVE      PT0DV1MP,PT0OV1MP
          MOVE      PT0DV2MP,PT0OV2MP
...       MOVE      PT0DV3MP,PT0OV3MP       * no version 3 mapping
          MOVE      PT0DV1CD,PT0OV1CD
          MOVE      PT0DV2CD,PT0OV2CD
          MOVE      PT0DV3CD,PT0OV3CD
          MOVE      PT0DV4CD,PT0OV4CD
          MOVE      PT0DV5CD,PT0OV5CD                                 *I-105244
.....          MOVE      ICDORPRO,PT0OORP
          MOVE      SP70,PT0OSPAR
.
          PACK      KEY9,OPCODE
          CALL      RAPT15O1                * Pos read an ICD10 opr file rec
          IF        OVRCD=1
            CALL      WRPT15O1              * Write ICD10 Ed.5 Operation recd
          ELSE
            CALL      UPPT15O1              * Update ICD10 Ed.5 Operation recd 
          ENDIF
          GOTO      CONV1000
.
CONV8000  DISPLAY   *P1:24,*EL,*B,"Conversion finished.  ",*V2LON,RECOUNT,*HOFF:
                    " records converted.  ";
          CALL      HITENTER
.
CONV9000  MOVE      ZERO,EXIT
CONV9999  RETURN
+
.******************************************************************************
.*                                  FCOD0000              Called by: CONV0000 *
.*                                 Format Code                                *
.******************************************************************************
FCOD0000  MATCH     SP9,KEY9
          GOTO      FCOD9000 IF EQUAL       * Blank code
.
          MATCH     ANSD,ICDTYP
          IF        @EQUAL
            SCAN      ".",KEY9
            IF        !@EQUAL
              UNPACK    KEY9,KEY3,KEY6
              MATCH     SP6,KEY6
              IF        !@EQUAL
                PACK      KEY9,KEY3,DOT,KEY6 * Disease code
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     ANSM,ICDTYP
          IF        @EQUAL
            SCAN      "/",KEY9
            IF        !@EQUAL
              UNPACK    KEY9,KEY5,KEY4
              MATCH     SP4,KEY4
              IF        !@EQUAL
                PACK      KEY9,KEY5,SLASH,KEY4 * Morphology code
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     ANSP,ICDTYP
          IF        @EQUAL
            SCAN      "-",KEY9
            IF        !@EQUAL
              UNPACK    KEY9,KEY5,KEY4
              MATCH     SP4,KEY4
              IF        !@EQUAL
                MOVE      "-",KEY1
                PACK      KEY9,KEY5,KEY1,KEY4 * Operation code
              ENDIF
            ENDIF
          ENDIF
.
          RESET     KEY9
          PACK      KEY9,KEY9,SP9
          REP       "+ ",KEY9
          REP       "* ",KEY9
          PACK      KEY9,KEY9,SP9
.
FCOD9000  MOVE      ZERO,EXIT
FCOD9999  RETURN
+
.******************************************************************************
.*                                  CLER0000              Called by: CONV0000 *
.*                               Clear Variables                              *
.******************************************************************************
CLER0000  UNPACK    SP127,DDESC,DFLAG,DAGEGP,PT0D2AGP,DSEX
          UNPACK    SP127,PT0DADTP,PT0DSACD,DDAGGER,DAREA,PT0DCPRA
          UNPACK    SP127,PT0DACRQ,PT0DMI9C,PT0DV1MP,PT0DV2MP
          MOVE      ZERO,DLOW
          MOVE      ZERO,DHIGH
          MOVE      ZERO,PT0D2ALL
          MOVE      ZERO,PT0D2AHL
          MOVE      ZERO,PT0DCMFT
.
          MOVE      SP9,PT0OBLKN
.
CLER9000  MOVE      ZERO,EXIT
CLER9999  RETURN
+
.******************************************************************************
.*                                  GASC0000              Called by: CONV0000 *
.*                              Get Asterisk Code                             *
.******************************************************************************
GASC0000  MATCH     SP127,KEY127
          GOTO      GASC9000 IF EQUAL       * Blank description
.
          SCAN      "*)",KEY127
          GOTO      GASC9000 IF NOT EQUAL   * No asterisk code found
.
          RESET     KEY127
          STRIP     KEY127
          ENDSET    KEY127
GASC1000  BUMP      KEY127,-1
          GOTO      GASC9000 IF EOS         * Cant find code
.
          CMATCH    "(",KEY127
          GOTO      GASC1000 IF NOT EQUAL   * Not the start of the code
.
          MOVE      SP9,KEY9
          CLEAR     KEY9
GASC2000  BUMP      KEY127
          GOTO      GASC9000 IF EOS         * Cant find code
.
          CMATCH    "*",KEY127
          GOTO      GASC3000 IF EQUAL       * End of code
.
          MOVE      KEY127,KEY1
          APPEND    KEY1,KEY9
          GOTO      GASC2000
.
GASC3000  APPEND    SP9,KEY9
          RESET     KEY9
          MOVE      KEY9,PT0DSACD           * Suggested asterisk code
.
GASC9000  MOVE      ZERO,EXIT
GASC9999  RETURN
+
.******************************************************************************
.*                                  F9MC0000              Called by: CONV0000 *
.*                            Format ICD9 Map Code                            *
.******************************************************************************
F9MC0000  MOVE      SP9,PT0DMI9C
          MATCH     SP9,ICD9MAP
          GOTO      F9MC9000 IF EQUAL       * Blank code
.
          MATCH     "M",ICDTYP
          GOTO      F9MC1000 IF EQUAL       * Morphology code
.
          MATCH     "P",ICDTYP
          GOTO      F9MC2000 IF EQUAL       * Operation code
.
          SCAN      ".",ICD9MAP
          GOTO      F9MC3000 IF EQUAL       * Code already formatted
.
          PACK      PT0DMI9C,ICD9MAP,SP9    * Normal disease code
          UNPACK    ICD9MAP,KEY1,KEY8
          TYPE      KEY1
          IF        @EQUAL
            UNPACK    ICD9MAP,KEY3,KEY6
            MATCH     SP6,KEY6
            IF        !@EQUAL
              PACK      PT0DMI9C,KEY3,DOT,KEY6 * Normal disease code
            ENDIF
          ENDIF
.
          MATCH     "E",ICD9MAP
          IF        @EQUAL
            UNPACK    ICD9MAP,KEY4,KEY5
            MATCH     SP5,KEY5
            IF        !@EQUAL
              PACK      PT0DMI9C,KEY4,DOT,KEY5 * E disease code
            ENDIF
          ENDIF
.
          MATCH     "V",ICD9MAP
          IF        @EQUAL
            UNPACK    ICD9MAP,KEY3,KEY6
            MATCH     SP6,KEY6
            IF        !@EQUAL
              PACK      PT0DMI9C,KEY3,DOT,KEY6 * V disease code
            ENDIF
          ENDIF
          GOTO      F9MC3000
.
F9MC1000  SCAN      "/",ICD9MAP
          GOTO      F9MC3000 IF EQUAL       * Code already formatted
.
          UNPACK    ICD9MAP,KEY5,KEY4
          MATCH     SP4,KEY4
          IF        !@EQUAL
            PACK      PT0DMI9C,KEY5,SLASH,KEY4 * Morphology code
          ENDIF
          GOTO      F9MC3000
.
F9MC2000  SCAN      ".",ICD9MAP
          GOTO      F9MC3000 IF EQUAL       * Code already formatted
.
          UNPACK    ICD9MAP,KEY2,KEY7
          MATCH     SP7,KEY7
          IF        !@EQUAL
            PACK      PT0DMI9C,KEY2,DOT,KEY7 * Operation code
          ENDIF
.
F9MC3000  RESET     PT0DMI9C
          PACK      PT0DMI9C,PT0DMI9C,SP9
.
F9MC9000  MOVE      ZERO,EXIT
F9MC9999  RETURN
+
.******************************************************************************
.*                                  GCMF0000              Called by: CONV0000 *
.*                     Get The Complex Mapping Format Code                    *
.******************************************************************************
GCMF0000  MOVE      ZERO,PT0DCMFT
          BRANCH    CMPFLG,GCMF9000         * Not using complex mapping
.
          MATCH     "H",DFLAG
          GOTO      GCMF9000 IF EQUAL       * Header record, no complex map
.
          PACK      KEY20,DPCODE,SP20
          CALL      RSPTCMP2                * Position on & read a complex
          CALL      RKPTCMP2                  mapping file record
          BRANCH    OVRCD,GCMF9000
.
          MATCH     DPCODE,PTCP1PCD
          GOTO      GCMF9000 IF NOT EQUAL   * Different ICD code
.
          MOVE      PTCPCMFT,PT0DCMFT       * Complex mapping format code
.
GCMF9000  MOVE      ZERO,EXIT
GCMF9999  RETURN
+
.=============================================================================
.
          INC       STD001IO
          INC       PATCANIO/INC
          INC       PATCMPIO/INC
          INC       PATICDIO/INC
          INC       PATICOIO/INC
+
.

