.******************************************************************************
.*    Program      : F07WTTX1                                                 *
.*    Description  : FIXIT program for wattx1af - move WTTXNWHC to WTTXUD09   *
.*                                                                            *
.*    Author       : Don Nguyen                                               *
.*    Date         : 30/10/2015                                               *
.*    Modifications:                                                          *
.*        V10.07.00  30/10/2015  Don Nguyen      CAR 320394                   *
.*                   Created New                                              *
.******************************************************************************
.
.
          INC       STD001FD
          INC       WATTX1FD/INC
.
.
. LOCAL VARIABLES
. ---------------
DISNUM    FORM      4
RECNUM    FORM      8
.
.
PRGNAM    INIT      "FIXIT for wattx1af - move WTTXNWHC to WTTXUD09           "
PRGID     INIT      "F07WTTX1"
VERSION   INIT      "V12.00.00"
+
.******************************************************************************
.*                           MAIN0000                                         *
.*                  Program Mainline                                          *
.******************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:               * yes
                          MAIN0000:               * no
                          MAIN9999                * cancel
.
MAIN2000  CALL      READ0000                      * run fixit
.
MAIN9999  CHAIN     PGM
+
.******************************************************************************
.*                           INIT0000                                         *
.*        Display heading and check that the files needed exist&open them     *
.******************************************************************************
.
INIT0000  CALL      DISPHEAD
          OPEN      WATTX1A1,"wattx1af"
.
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
          
INIT9999  RETURN
+
.******************************************************************************
.*                           OPTN0000                   Called by: MAIN0000   *
.*                  Get user options or exit                                  *
.*                                                                            *
.*        Returns:  EXIT    = FALSE (0)  Ok to proceed                        *
.*                            TRUE  (1)  Exit option selected                 *
.*                  COPTION - option selected                                 *
.******************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run c-isam fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.******************************************************************************
.*                           READ0000                                         *
.*        Loop through "wattx1af" and update WTTXUD09 with WTTXNWHC (V10.06)  *
.******************************************************************************
.
READ0000  DISPLAY   *P1:24,*EL;
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started    : ",CDATE,SP1,CTIMEIS:
                    *P1:20,*EL,"Record No. :";
.
          PACK      KEY19,SP20
          CALL      RSWTTX11                * position at start of file
READ0500  CALL      RKWTTX11                * read next record
          BRANCH    OVRCD,READ9000          * eof - finished
.
          ADD       ONE,RECNUM              * increment record counter
          ADD       ONE,DISNUM              * Display counter
.         
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM 
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MOVE      WTTXNWHC,WTTXUD09       * WTTXNWHC previously used in V10.06
          PACK      WTTXNWHC,SP20           * clear it now
          PACK      WTTXSPAR,SP20
          CALL      UPWTTX11
.
          GOTO      READ0500                * get next record
.         
READ9000  CLOSE     WATTX1A1
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Update completed ",CDATE,SP1,CTIMEIS:
                    *P1:24;
          CALL      HITENTER
.
READ9999  RETURN
+
          INC       STD001IO
          INC       WATTX1IO/INC
