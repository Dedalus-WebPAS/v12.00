.
.       PATMMRDS : Display MHMS Report Date Entries
.
.       This include is used to display various options available, using
.       whatever file it needs, along with any special parameter supplied
.       by the calling program.
.
.     Modifications: 28/11/89 Graeme Williams
.                    Added the parameter CNEWDS to determine if the include
.                    is to use the new standards for "?" routines
.
.        Subroutine to display the available MHMS reports
.
PATMMRDS  MOVE       FIVE,CVERT         * Starting line number
          MOVE       ONE,CCOL           * Starting column
          DISPLAY    *P1:3,*EF,*P20:4,*V2LON,*ULON,"MHMS REPORTS"
.
.>>>>>>>  TRAP       ENDMMRA0 IF F1
.
          PACK       KEY6,SP6           * Start from beginning of file
          CALL       RDSMMRA1           * Position to start of file
.
.         Loop over file
.
MMRALP1   CALL       RDKMMRA1           * Read the next date range record
          BRANCH     OVRCD,ENDMMRA1     * No more records. Finish.
.
          REP        " 0",MMRYRMN
          UNPACK     MMRYRMN,CCENT,CYEAR,CMON
          DISPLAY    *PCCOL:CVERT,*V2LON,CMON,SLASH,CCENT,CYEAR
.
          UNPACK     MMRFDTE,CCENT,CYEAR,CMON,CDAY
          ADD        TEN1,CCOL          * Shift across
          MOVE       ONE,CHIGHLT        * No highlighting
          CALL       DSPDATE            * Display the from date
.
          ADD        TEN1,CCOL          * Shift across
          DISPLAY    *PCCOL:CVERT,"to "
.
          UNPACK     MMRTDTE,CCENT,CYEAR,CMON,CDAY
          ADD        THREE,CCOL         * Shift across
          MOVE       ONE,CHIGHLT        * No highlighting
          CALL       DSPDATE            * Display the to date
.
          SUB        TWENTY5,CCOL       * Shift back
.
          ADD        ONE,CVERT
.
          COMPARE    TWENTY4,CVERT
          GOTO       MMRALP1 IF LESS
.
          MOVE       FIVE,CVERT
          ADD        "40",CCOL
          COMPARE    "70",CCOL
          GOTO       MMRALP1 IF LESS
.
.>>>>>>>  TRAPCLR    F1
.
.         Check for new standards question, or old question
.
          BRANCH    CNEWDS,DSMMR200
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSE,ANS
          GOTO      ENDMMR2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATMMRDS IF EQUAL
.
          GOTO      DSMMR400
.
.         Use the new standards question
.
DSMMR200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSC,ANS
          GOTO      ENDMMR2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATMMRDS IF EQUAL
.
DSMMR400  MOVE       FIVE,CVERT
          MOVE       ONE,CCOL
          DISPLAY    *P1:5,*EF
.
.>>>>>>>  TRAP       ENDMMRA0 IF F1
.
          GOTO       MMRALP1
.
ENDMMRA0  NORETURN
.>>>>>>>  TRAPCLR    F1
.
.      End of rates
.
ENDMMRA1  BRANCH     CNEWDS,ENDMMR2
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
ENDMMR2   RETURN
