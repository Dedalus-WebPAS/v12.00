.******************************************************************************
.* System         : Outpatients                                               *
.*                                                                            *
.* Include Name   : KYOUTXSC.PBL                                              *
.*                                                                            *
.* Function       : To Keyin all the variables on OUTXSCFD file               *
.*                                                                            *
.* Author         : Justin Coates           04/02/1992                        *
.*                                                                            *
.* Parameters     : SCRPSTFD (field positions) record                         *
.*                                                                            *
.* Modifications  : 01/06/1992    Justin Coates                               *
.*                  fixed row keyin for codes 2 - 20                          *
.*                  set ABORT = 1 if exitchar entered                         *
.*                  15/10/1992    Justin Coates  (HONG KONG changes)          *
.*                  changed to use KFREEF00 (in keyinpmi) instead on scrkyfre *
.*                  26/10/1992    Justin Coates                               *
.*                  set WORDFLAG to "2" for word processor for correct redisp *
.*                  15/12/1992    Justin Coates  (DUDLEY changes)             *
.*                  added KYOUTXSC label and change keyin format              *
.*                  19/05/93 J.Tan   SRF 122413                               *
.*                  Fixed the mandatory variable for keyincodes               *
.*                  05/04/1994  Steve Armstrong                               *
.*                  Mods for PATCODKY to PATCODKY                             *
.*                  and PATDOCKY to PATDOCKY                                  *
.******************************************************************************
.*        V5.02.01  03/05/1995    Justin Coates                               *
.*                  made it call SCRKYFRE and not KFREEF00                    *
.******************************************************************************
.
KYOUTXSC  MOVE      SCPSROW,CVERT           * set row
          MOVE      SCPSCOL,CCOL            * set column
          MOVE      SCPSROW,EVERT           * set row
          MOVE      SCPSCOL,ECOL            * set column
          MOVE      SCPSMAN,CKYIMAND        * mandatory ??
          MOVE      SCPSITM,FIELDNO         * which item to keyin
          SFORMAT   KEYFIELD,127            * reformat string to full size
          MOVE      ZERO,EXIT               * set exit flag
.
          PERFORM   FIELDNO,KOTXSDT0,KOTXSDT0,KOTXSDT0,KOTXSDT0,KOTXSDT0:
                            KOTXSDT0,KOTXSCD0,KOTXSCD9,KOTXSCD0,KOTXSCD9:
                            KOTXSCD0,KOTXSCD9,KOTXSCD0,KOTXSCD9,KOTXSCD0:
                            KOTXSCD9,KOTXSCD0,KOTXSCD9,KOTXSCD0,KOTXSCD9:
                            KOTXSCD0,KOTXSCD9,KOTXSCD0,KOTXSCD9,KOTXSCD0:
                            KOTXSCD9,KOTXSCD0,KOTXSCD9,KOTXSCD0,KOTXSCD9:
                            KOTXSCD0,KOTXSCD9,KOTXSCD0,KOTXSCD9,KOTXSCD0:
                            KOTXSCD9,KOTXSCD0,KOTXSCD9,KOTXSCD0,KOTXSCD9:
                            KOTXSCD0,KOTXSCD9,KOTXSCD0,KOTXSCD9,KOTXSCD0:
                            KOTXSCD9,KTXDOC10,KTXDOC19,KTXDOC20,KTXDOC29:
                            KTXDOC30,KTXDOC39,KTXAMT10,KTXAMT20,KOTXSFF0:
                            KOTXSFF0,KOTXSFF0,KOTXSFF0,KOTXSFF0,KOTXSFF0:
                            KTXWORD0,KOTXSTM0,KOTXSTM0,KOTXSTM0,KOTXSTM0:
                            KOTXSTM0,KOTXSTM0
          RETURN
+
.---------------------------------------------------------------------
.         Dates 1 - 6
.---------------------------------------------------------------------
KOTXSDT0  LOAD      KEY8,FIELDNO,OTXSDTE1,OTXSDTE2,OTXSDTE3:
                                 OTXSDTE4,OTXSDTE5,OTXSDTE6
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          MATCH     SP2,CCENT
          IF        @EQUAL
            MOVE      CCC,CCENT               * set default cent
          ENDIF
          CALL      KEYDATE
          BRANCH    CQUEST,KOTXSDT0         * ? entered
          BRANCH    OVRCD,KOTXSDT5          * nothing entered
.    
          PACK      KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KEY8
          GOTO      KOTXSDT7
.
KOTXSDT5  MOVE      SP8,KEY8
          COMPARE   ZERO,SCPSMAN
          GOTO      KOTXSDT7 IF EQUAL       * not mandatory
.
          DISPLAY   *P1:24,*B,"Field is mandatory.  ",*EL;
          CALL      HITENTER
          GOTO      KOTXSDT0
.
KOTXSDT7  STORE     KEY8,FIELDNO,OTXSDTE1,OTXSDTE2,OTXSDTE3:
                                 OTXSDTE4,OTXSDTE5,OTXSDTE6
          CALL      DISPVARS
.
KOTXSDT9  RETURN
.
.--------------------------------------------------------------------
.         Codes 1 - 20
.--------------------------------------------------------------------
KOTXSCD0  ASSIGN    ((FIELDNO-5)/2),FORM2
          LOAD      CODE,FORM2,OTSPXC01,OTSPXC02,OTSPXC03,OTSPXC04,OTSPXC05:
                               OTSPXC06,OTSPXC07,OTSPXC08,OTSPXC09,OTSPXC10:
                               OTSPXC11,OTSPXC12,OTSPXC13,OTSPXC14,OTSPXC15:
                               OTSPXC16,OTSPXC17,OTSPXC18,OTSPXC19,OTSPXC20
          LOAD      CKYIDEF3,FORM2,OTXSCO01,OTXSCO02,OTXSCO03,OTXSCO04,OTXSCO05:
                                   OTXSCO06,OTXSCO07,OTXSCO08,OTXSCO09,OTXSCO10:
                                   OTXSCO11,OTXSCO12,OTXSCO13,OTXSCO14,OTXSCO15:
                                   OTXSCO16,OTXSCO17,OTXSCO18,OTXSCO19,OTXSCO20
.
          CALL      PATCODKY                * keyin code
.
          COMPARE   TWO,EXIT
          GOTO      KOTXSCD8 IF EQUAL       * abort
.
          ASSIGN    ((FIELDNO-5)/2),FORM2
          STORE     ACODE,FORM2,OTXSCO01,OTXSCO02,OTXSCO03,OTXSCO04,OTXSCO05:
                                OTXSCO06,OTXSCO07,OTXSCO08,OTXSCO09,OTXSCO10:
                                OTXSCO11,OTXSCO12,OTXSCO13,OTXSCO14,OTXSCO15:
                                OTXSCO16,OTXSCO17,OTXSCO18,OTXSCO19,OTXSCO20
          CALL      DISPVARS
.
.         see if to display description
.
          ASSIGN    (FIELDNO+1),FORM5
          MOVE      FORM5,KEY5
          CALL      SCRCKFLD                * see if to display description
          BRANCH    EXIT,KOTXSCD7           * dont display
.
          PACK      VAR,TDESC,SP30,SP30
          CALL      DISPITEM                * display description
.
KOTXSCD7  MOVE      ZERO,EXIT
          GOTO      KOTXSCD9
.
KOTXSCD8  MOVE      ONE,EXIT
          MOVE      ONE,ABORT
.
KOTXSCD9  RETURN
+
.--------------------------------------------------------------------
.         Doctor 1 - OTXSDOC1
.--------------------------------------------------------------------
KTXDOC10  MOVE      OTXSDOC1,CKYIDEF6       * default code
          MOVE      SCPSMAN,CKYIMAND        * mandatory ??
.
          CALL      PATDOCKY                * keyin code 
          MOVE      DOCCODE,OTXSDOC1
          LOAD      OTXSDOC1,EXIT,SP6,SP6
          IF        EXIT = TWO
            MOVE      ONE,ABORT                * exitchar entered
          ENDIF
          BRANCH    ABORT,KTXDOC19          * abort
.
.         see if to display description
.
          MOVE      "   48",KEY5            * item number
          CALL      SCRCKFLD                * see if to display description
          BRANCH    EXIT,KTXDOC19           * dont display
.
          CALL      CONCATDR
          PACK      VAR,DIM40,SP30,SP30,SP20
          CALL      DISPITEM                * display description
.
KTXDOC19  RETURN
.
.--------------------------------------------------------------------
.         Doctor 2 - OTXSDOC2
.--------------------------------------------------------------------
KTXDOC20  MOVE      OTXSDOC2,CKYIDEF6       * default code
          MOVE      SCPSMAN,CKYIMAND        * mandatory ??
.
          CALL      PATDOCKY                * keyin code 
          MOVE      DOCCODE,OTXSDOC2
          IF        EXIT = TWO
            MOVE      ONE,ABORT               * exitchar entered
          ENDIF
          BRANCH    ABORT,KTXDOC29          * abort
.
.         see if to display description
.
          MOVE      "   50",KEY5            * item number
          CALL      SCRCKFLD                * see if to display description
          BRANCH    EXIT,KTXDOC29           * dont display
.
          CALL      CONCATDR
          PACK      VAR,DIM40,SP30,SP30,SP20
          CALL      DISPITEM                * display description
.
KTXDOC29  RETURN
.
.--------------------------------------------------------------------
.         Doctor 3 - OTXSDOC3
.--------------------------------------------------------------------
KTXDOC30  MOVE      OTXSDOC3,CKYIDEF6       * default code
          MOVE      SCPSMAN,CKYIMAND        * mandatory ??
.
          CALL      PATDOCKY                * keyin code 
          MOVE      DOCCODE,OTXSDOC3
          IF        EXIT = TWO
            MOVE      ONE,ABORT               * exitchar entered
          ENDIF
          BRANCH    ABORT,KTXDOC39          * abort
.
.         see if to display description
.
          MOVE      "   52",KEY5            * item number
          CALL      SCRCKFLD                * see if to display description
          BRANCH    EXIT,KTXDOC39           * dont display
.
          CALL      CONCATDR
          PACK      VAR,DIM40,SP30,SP30,SP20
          CALL      DISPITEM                * display description
.
KTXDOC39  RETURN
.
.--------------------------------------------------------------------
.         Amount 1 - OTXSAMT1
.--------------------------------------------------------------------
KTXAMT10  KEYIN     *PCCOL:CVERT,"________.__":
                    *PCCOL:CVERT,*V2LON,OTXSAMT1:
                    *PCCOL:CVERT,*DV,OTXSAMT1
KTXAMT19  RETURN
+
.--------------------------------------------------------------------
.         Amount 2 - OTXSAMT2
.--------------------------------------------------------------------
KTXAMT20  KEYIN     *PCCOL:CVERT,"________.__":
                    *PCCOL:CVERT,*V2LON,OTXSAMT2:
                    *PCCOL:CVERT,*DV,OTXSAMT2
KTXAMT29  RETURN
+
.--------------------------------------------------------------------
.         Free format 1 - 6
.--------------------------------------------------------------------
KOTXSFF0  ASSIGN    (FIELDNO-54),FORM2
          LOAD      KEYFIELD,FORM2,OTXSDSC1,OTXSDSC2,OTXSDSC3:
                                   OTXSDSC4,OTXSDSC5,OTXSDSC6
          PACK      KEYFIELD,KEYFIELD,SP30,SP30,SP30 
.
          CALL      SCRKYFRE                * Keyin to desired length
.
          ASSIGN    (FIELDNO-54),FORM2
          STORE     KEYFIELD,FORM2,OTXSDSC1,OTXSDSC2,OTXSDSC3:
                                   OTXSDSC4,OTXSDSC5,OTXSDSC6
          CALL      DISPVARS
.
KOTXSFF9  RETURN
+
.--------------------------------------------------------------------
.         Comments - OUTXWPFD
.         need two temp files in case abort selected and due to the
.         fact that the comments field is a screen selection field
.         CFNAMEW - contains the current comments
.         CFNAMET - is used by the word processor for the comments
.--------------------------------------------------------------------
KTXWORD0  PREP      XCOMMT,CFNAMET          * create temp temp file
          MOVE      ZERO,OVRCD              * clear flag
          TRAP      OVERCOND IF IO          * in case no file
          OPEN      XCOMMW,CFNAMEW          * start of working temp file
          TRAPCLR   IO
          BRANCH    OVRCD,KTXWORD3          * no current comments
.
.         copy the current comments to temp file CFNAMEW -> CFNAMET
.
KTXWORD1  READ      XCOMMW,SEQ;OTXWDESC
          GOTO      KTXWORD3 IF OVER        * no more comments to copy
.
          WRITE     XCOMMT,SEQ;OTXWDESC
          GOTO      KTXWORD1
.
.         enter new comments
.
KTXWORD3  MOVE      TWO,WORDFLAG            * set status flag to redraw
          CLOSE     XCOMMT                  * close the wp file
          WORD      CFNAMET,999,5,7,76,23,WORDFLAG
.
          COMPARE   ZERO,WORDFLAG
          GOTO      KTXWORD8 IF EQUAL       * abort selected
.
.         copy comments from temp file to work file (CFNAMET -> CFNAMEW)
.
          PREP      XCOMMW,CFNAMEW          * re-create working temp file
          OPEN      XCOMMT,CFNAMET          * start of comments for patient
.
KTXWORD4  READ      XCOMMT,SEQ;OTXWDESC
          GOTO      KTXWORD8 IF OVER        * no more comments to copy
.
          WRITE     XCOMMW,SEQ;OTXWDESC
          GOTO      KTXWORD4
.
KTXWORD8  CLOSE     XCOMMT,DELETE           * delete temp temp file
          CALL      DOTXS061                * display Yes/No for comments
.
KTXWORD9  RETURN
+
.--------------------------------------------------------------------
.         Times 1 - 6
.--------------------------------------------------------------------
KOTXSTM0  ASSIGN    (FIELDNO-61),FORM2
          LOAD      KEY5,FORM2,OTXSTIM1,OTXSTIM2,OTXSTIM3:
                               OTXSTIM4,OTXSTIM5,OTXSTIM6
          UNPACK    KEY5,CHOUR,ANS,CMIN
          CALL      KEYTIME
          BRANCH    CQUEST,KOTXSTM0          * ? entered
          BRANCH    OVRCD,KOTXSTM5           * nothing entered
.
          PACK      KEY5,CHOUR,COLON,CMIN
          REP       " 0",KEY5
          GOTO      KOTXSTM7
.
KOTXSTM5  MOVE      SP5,KEY5
          COMPARE   ZERO,SCPSMAN
          GOTO      KOTXSTM7 IF EQUAL        * not mandatory
.
          DISPLAY   *P1:24,*B,"Field is mandatory.  ",*EL;
          CALL      HITENTER
          GOTO      KOTXSTM0
.
KOTXSTM7  ASSIGN    (FIELDNO-61),FORM2
          STORE     KEY5,FORM2,OTXSTIM1,OTXSTIM2,OTXSTIM3:
                               OTXSTIM4,OTXSTIM5,OTXSTIM6
          CALL      DISPVARS
.
KOTXSTM9  RETURN
.
.*********************************************************************
.         load the variable for the mandatory check
.         Para's  : FORM5         the variable number
.*********************************************************************
MYOUTXSC  LOAD      DIM80,FORM5,OTXSDTE1,OTXSDTE2,OTXSDTE3,OTXSDTE4,OTXSDTE5:
                                OTXSDTE6,OTXSCO01,ANSA,OTXSCO02,ANSA:
                                OTXSCO03,ANSA,OTXSCO04,ANSA,OTXSCO05:
                                ANSA,OTXSCO06,ANSA,OTXSCO07,ANSA:
                                OTXSCO08,ANSA,OTXSCO09,ANSA,OTXSCO10:
                                ANSA,OTXSCO11,ANSA,OTXSCO12,ANSA:
                                OTXSCO13,ANSA,OTXSCO14,ANSA,OTXSCO15:
                                ANSA,OTXSCO16,ANSA,OTXSCO17,ANSA:
                                OTXSCO18,ANSA,OTXSCO19,ANSA,OTXSCO20:
                                ANSA,OTXSDOC1,ANSA,OTXSDOC2,ANSA:
                                OTXSDOC3,ANSA,OTXSAMT1,OTXSAMT2,OTXSDSC1:
                                OTXSDSC2,OTXSDSC3,OTXSDSC4,OTXSDSC5,OTXSDSC6:
                                ANSA,OTXSTIM1,OTXSTIM2,OTXSTIM3,OTXSTIM4:
                                OTXSTIM5,OTXSTIM6
          RETURN
.............................................................................
