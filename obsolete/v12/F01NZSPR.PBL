.*****************************************************************************
.*    Program      : F01NZSPR                                                *
.*    Description  : Conversion nzpspraf to new File layout                  *
.*                                                                           *
.*    Author       : Jill Parkinson CAR 234014                               *
.*    Date         : 12/01/2011                                              *
.*    Modifications:                                                         *
.*****************************************************************************
.
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
.
OLDFILE1   IFILE    SQL, FIXED=433, KEY="U1-8,31-33"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.NZSPADMN  DIM       8      8        1     Admission Number
.NZSPCLMN  DIM       3      3        9     Claim Code (Cat CL)
.NZSPSURG  DIM       10     10       12    Surgeon/Consultant (pmshcpaf)
.NZSPPROC  DIM       9      9        22    Procedure Code (patitemn)
.NZSPUNIQ  DIM       3      3        31    Counter/Unique Number
.NZSPCNTR  DIM       6      6        34    Contract/Fund/Expected Payer
.NZSPCPRC  DIM       9      9        40    Contract Procedure Code (patcmcaf)
.NZSPPRIM  FORM      1      2        49    Primary Procedure 1=Yes
.                                         0=Bill separate 2=Include with primary
.NZSPANAE  DIM       10     10       51    Anaesthetist
OLDPPDES  DIM       55     55       61    Full Procedure Description
.NZSPICAP  FORM      1      2        116   RP Invoice Capture Status
.NZSPPIND  FORM      1      2        118   Planned/Performed Ind
.                                         0=Planned 1=Performed 2=Not Performed
.NZSPPINS  FORM      1      2        120   Patient Insurance 1=Yes 0=No
.NZSPINSR  DIM       3      3        122   Insurer (Catg I)
.NZSPAPRV  DIM       20     20       125   Approval Number
.NZSPAPSI  FORM      1      2        145   Approval Sighted 1=Yes 0=No
.NZSPCLNO  DIM       20     20       147   Claim Number
.NZSPAPNT  DIM       30     30       167   Approval Notes
.NZSPCMNT  DIM       30     30       197   Comments
.NZSPTDUR  FORM      5      4        227   Chargeable Theatre Duration-Minutes
.NZSPTFEE  FORM      12.2   8        231   Theatre Fee
.NZSPFFEE  FORM      14.2   9        239   Fixed Fee
.NZSPHPAY  FORM      14.2   9        248   Funder Contribution/Funder to pay
.NZSPCPAY  FORM      14.2   9        257   Co-payment
.NZSPFADJ  FORM      14.2   9        266   Fixed Fee Adjustment
.NZSPHADJ  FORM      14.2   9        275   Funder Contr./Funder to pay Adjustm
.NZSPCADJ  FORM      14.2   9        284   Co-Payment Adjustment
.NZSPUDF1  DIM       3      3        293   User Defined 1 (Cat UJ)
.NZSPUDF2  DIM       3      3        296   User Defined 2 (Cat UK)
.NZSPCDAT  DIM       8      8        299   Date Record Created/Added(ccyymmdd)
.NZSPCTIM  DIM       8      8        307   Time Record Created/Added(hh:mm:ss)
.NZSPCUID  DIM       10     10       315   WEB User ID Created/Added(websecaf)
.NZSPUDAT  DIM       8      8        325   Date Record Updated (ccyymmdd)
.NZSPUTIM  DIM       8      8        333   Time Record Updated (hh:mm:ss)
.NZSPUUID  DIM       10     10       341   WEB User ID Updated (websecaf)
.NZSPADTE  DIM       8      8        351   Approval Date
.NZSPCONT  DIM       1      1        359   Maximum funder contribution 1=Yes
.NZSPINVD  DIM       1      1        360   Invoiced -
.                                         0=No Invoice Raised
.                                         1=Patient Invoice raised/Not Required
.                                         2=Funder Invoice raised/Not Required
.                                         3=All Invoices raised
.NZSPUNTH  DIM       10     10       361   Unique theatre booking ID
OLDPSPAR  DIM       62     62       371   Spare
.
.End of Record                      433
.
NZPSPRA1   IFILE    SQL, FIXED=496, KEY="U1-8,31-33"
NZPSPRA2   IFILE    SQL, FIXED=496, KEY="U1-8,34-39,31-33"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
NZSPADMN  DIM       8      8        1     Admission Number
NZSPCLMN  DIM       3      3        9     Claim Code (Cat CL)
NZSPSURG  DIM       10     10       12    Surgeon/Consultant (pmshcpaf)
NZSPPROC  DIM       9      9        22    Procedure Code (patitemn)
NZSPUNIQ  DIM       3      3        31    Counter/Unique Number
NZSPCNTR  DIM       6      6        34    Contract/Fund/Expected Payer
NZSPCPRC  DIM       9      9        40    Contract Procedure Code (patcmcaf)
NZSPPRIM  FORM      1      2        49    Primary Procedure 1=Yes
.                                         0=Bill sep 2=Include with primary
NZSPANAE  DIM       10     10       51    Anaesthetist
NZSPPDES  DIM       80     80       61    Full Procedure Description
NZSPICAP  FORM      1      2        141   RP Invoice Capture Status
NZSPPIND  FORM      1      2        143   Planned/Performed Ind
.                                         0=Planned 1=Performed 2=Not Performed
NZSPPINS  FORM      1      2        145   Patient Insurance 1=Yes 0=No
NZSPINSR  DIM       3      3        147   Insurer (Catg I)
NZSPAPRV  DIM       20     20       150   Approval Number
NZSPAPSI  FORM      1      2        170   Approval Sighted 1=Yes 0=No
NZSPCLNO  DIM       20     20       172   Claim Number
NZSPAPNT  DIM       30     30       192   Approval Notes
NZSPCMNT  DIM       30     30       222   Comments
NZSPTDUR  FORM      5      4        252   Chargeable Theatre Duration-Minutes
NZSPTFEE  FORM      12.2   8        256   Theatre Fee
NZSPFFEE  FORM      14.2   9        264   Fixed Fee
NZSPHPAY  FORM      14.2   9        273   Funder Contribution/Funder to pay
NZSPCPAY  FORM      14.2   9        282   Co-payment
NZSPFADJ  FORM      14.2   9        291   Fixed Fee Adjustment
NZSPHADJ  FORM      14.2   9        300   Funder Contr./Funder to pay Adjustm
NZSPCADJ  FORM      14.2   9        309   Co-Payment Adjustment
NZSPUDF1  DIM       3      3        318   User Defined 1 (Cat UJ)
NZSPUDF2  DIM       3      3        321   User Defined 2 (Cat UK)
NZSPCDAT  DIM       8      8        324   Date Record Created/Added(ccyymmdd)
NZSPCTIM  DIM       8      8        332   Time Record Created/Added(hh:mm:ss)
NZSPCUID  DIM       10     10       340   WEB User ID Created/Added(websecaf)
NZSPUDAT  DIM       8      8        350   Date Record Updated (ccyymmdd)
NZSPUTIM  DIM       8      8        358   Time Record Updated (hh:mm:ss)
NZSPUUID  DIM       10     10       366   WEB User ID Updated (websecaf)
NZSPADTE  DIM       8      8        376   Approval Date
NZSPCONT  DIM       1      1        384   Maximum funder contribution 1=Yes
NZSPINVD  DIM       1      1        385   Invoiced - 0=No Invoice Raised
.                                         1=Patient Invoice raised/Not Required
.                                         2=Funder Invoice raised/Not Required
.                                         3=All Invoices raised
NZSPUNTH  DIM       10     10       386   Unique theatre booking ID
NZSPSPAR  DIM       100    100      396   Spare
.
.End of Record                      496
.
.
.
CMDLINE   DIM       100
DIM4      DIM       4
DIM4A     DIM       4
DIM6      DIM       6
DIM6A     DIM       6
DIM60     DIM       60
DDCENT    DIM       2
.
INPFILE   DIM       8
RECNUM    FORM      8
NEWFILE   DIM       60
NEWPATH   DIM       60
OLDPATH   DIM       60
DEFPATH   DIM       60
SAVEFLG   FORM      1
SP50      INIT    "                                                  "
SP60      INIT    "                                                            "
.
PRGNAM    INIT      "CONVERSION NZPSPRFD"
PRGID     INIT      "F01NZSPR"
VERSION   INIT      "V12.00.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN1000,MAIN0000,MAIN9999 * Yes, no, cancel
.
MAIN1000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
. Loop thru old file and write records to new file
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/compress saved file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          
INIT9999  RETURN
.
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
PREP0000 
. open old file
          MOVE      "nzpspraf",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
. Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat nzpspraf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "snzspraf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat nzpspraf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "snzspraf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
. remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "nzpspraf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "snzspraf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "nzpspraf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
          APPEND    " 496 UC1-8,31-33",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has 
.  to be repeated with for each individual index
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC1-8,34-39,31-33",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
          OPEN      NZPSPRA1,NEWFILE
.
. set the flag to say we want to continue
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
          ELSE
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
            ELSE
              DISPLAY   "has caused an IO error - ";
            ENDIF
          ENDIF
          DISPLAY   "File will be bypassed"
.
          IF        FORM2 < 17
            CALL      HITENTER
          ENDIF
          DISPLAY   *P1:23,*EF
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
.
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          PACK      KEY11,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
.  All necessary changes to the files field lengths, date changes
.  or any other sort of modification must be made here
.
          PACK      NZSPPDES,OLDPPDES,SP70
          PACK      NZSPSPAR,SP70,SP70
.
          PACK      KEY11,NZSPADMN,NZSPCLMN,SP70
          CALL      WRNZSPR1                    * write to new file
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     NZPSPRA1                      * close new file
          CLOSE     OLDFILE1                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
READ9999  RETURN
.
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
VALD0000  MOVE      ZERO,EXIT
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or compress save file                                *
.**********************************************************************
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "snzspraf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "compress -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not compressed or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.         Get the default directory
.**********************************************************************
DEFT0000  EXECUTE   "ldat nzpspraf.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding 'nzpspraf'.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
DEFT9999  RETURN
+
.
.                  ******************************    OLD IO ROUTINES
READSOLD  RESET     KEY11
          READ      OLDFILE1,KEY11;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;NZSPADMN,NZSPCLMN,NZSPSURG,NZSPPROC,NZSPUNIQ:
                        NZSPCNTR,NZSPCPRC,NZSPPRIM,NZSPANAE,OLDPPDES,NZSPICAP:
                        NZSPPIND,NZSPPINS,NZSPINSR,NZSPAPRV,NZSPAPSI,NZSPCLNO:
                        NZSPAPNT,NZSPCMNT,NZSPTDUR,NZSPTFEE,NZSPFFEE,NZSPHPAY:
                        NZSPCPAY,NZSPFADJ,NZSPHADJ,NZSPCADJ,NZSPUDF1,NZSPUDF2:
                        NZSPCDAT,NZSPCTIM,NZSPCUID,NZSPUDAT,NZSPUTIM,NZSPUUID:
                        NZSPADTE,NZSPCONT,NZSPINVD,NZSPUNTH,OLDPSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
WRNZSPR1  RESET     KEY11
          WRITE     NZPSPRA1,KEY11;NZSPADMN,NZSPCLMN,NZSPSURG,NZSPPROC,NZSPUNIQ:
                        NZSPCNTR,NZSPCPRC,NZSPPRIM,NZSPANAE,NZSPPDES,NZSPICAP:
                        NZSPPIND,NZSPPINS,NZSPINSR,NZSPAPRV,NZSPAPSI,NZSPCLNO:
                        NZSPAPNT,NZSPCMNT,NZSPTDUR,NZSPTFEE,NZSPFFEE,NZSPHPAY:
                        NZSPCPAY,NZSPFADJ,NZSPHADJ,NZSPCADJ,NZSPUDF1,NZSPUDF2:
                        NZSPCDAT,NZSPCTIM,NZSPCUID,NZSPUDAT,NZSPUTIM,NZSPUUID:
                        NZSPADTE,NZSPCONT,NZSPINVD,NZSPUNTH,NZSPSPAR
          RETURN
.
          INC       STD001IO
