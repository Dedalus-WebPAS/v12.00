.*******************************************************************************
.*                                                                             *
.*                      P R O G R A M   S U M M A R Y                          *
.*                      -------------   -------------                          *
.*                                                                             *
.*     PROGRAM NAME :  EMRSCH88                                                *
.*                                                                             *
.*     FUNCTION     :  Emergency Department Fax Summary Scheduler              *
.*                                                                             *
.*     DATE CREATED :  03/02/2014                                              *
.*                                                                             *
.*     MODIFICATIONS:                                                          *
.*          V10.04.02  13/06/2014  Jill Parkinson   CAR 275582                 *
.*                     Added KPRINTER                                          *
.*          V10.04.01  13/06/2014  Jill Parkinson   CAR 275582                 *
.*                     Added packing of key5 before read emlpcaf               *
.*                     Added K variables to read in values instead of file vars*
.*          V10.03.00  03/02/2014   Ania P          CAR 275582                 *
.*                     Created program.                                        *
.*******************************************************************************

          INC       STD001FD
.         INC       IBACOMM/INC
          INC       EMRLPCFD/INC
          INC       WEBSCHFD/INC
          INC       WEBSECFD/INC
          INC       EMRCONFD/INC
          INC       EMRCONTD/INC
.
.
ADMISSNO  DIM       8
KEMLPURN  DIM       8
KEMLPADM  DIM       8
KPRINTER  DIM       6
UNIQF5    FORM      5
USERID    DIM       10
.
.
PRGID     INIT      "EMRSCH88"
PRGNAM    INIT      "ED Fax Summary Scheduler"
VERSION   INIT      "V12.00.00"

.*******************************************************************************
.                   MAINLINE                                                   *
.                   Main Controlling Logic                                     *
.*******************************************************************************
ML0000    CALL      INIT0000                     * Init and open files
ML1000    CALL      OPTN0000                     * Select options
          BRANCH    OPTION,ML2000,ML2000
          GOTO      ML9999
.
ML2000    CALL      KEYA0000                     * Keyin Admission No. 
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST                      * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML5000,ML9999,ML1000
.                         Yes    No     Cancel
.
ML5000    CALL      PROC0000
          BRANCH    EXIT,ML1000
.
ML9999    CHAIN     PGM
          STOP
+
.*******************************************************************************
.                   INIT0000                                  Called by ML1000 *
.                   Initialization routine                                     *
.*******************************************************************************
INIT0000  CALL      DISPHEAD
          CALL      IBACLOCK
          DISPLAY   *P56:24,"Opening emrlpcaf";
          OPEN      EMRLPCA1,"emrlpcaf"
.
          DISPLAY   *P64:24,"webschaf";
          OPEN      WEBSCHA1,"webschaf"
.
          DISPLAY   *P64:24,"websecaf";
          OPEN      WEBSECA1,"websecaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,EIGHTY9;*241,EMCNEDFD,*244,EMCNEDFP,*250,EMCNEDST
.
INIT9999  RETURN
.
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Schedule ED Summary Fax"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION                  * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    OPTION,OPTN9000              * Schedule Fax
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*******************************************************************************
.                   KEYA0000                                  Called by ML2000 *
.                   Keyin Admission Number                                     *
.*******************************************************************************
KEYA0000  KEYIN     *P1:24,*EL,"Keyin Admission Numnber ",*V2LON,KEMLPADM;
          KEYIN     *P1:24,*EL,"Keyin UR Numnber ",*V2LON,KEMLPURN;
          KEYIN     *P1:24,*EL,"Keyin User ID ",*V2LON,USERID;
          KEYIN     *P1:24,*EL,"Printer",*V2LON,KPRINTER;
.
KEYA9999  RETURN
.
.*******************************************************************************
.                   PROC0000                                 Called by ML50000 *
.                   Create New emrschaf job                                    *
.*******************************************************************************
PROC0000  MOVE      ZERO,UNIQF5                  * Find next job number
          PACK      KEY5,Z70
          CALL      RSEMLP1
          CALL      RPEMLP1
          BRANCH    OVRCD,PROC1000
.
          MOVE      EMLPUNI,UNIQF5
.
PROC1000  ADD       ONE,UNIQF5
          MOVE      UNIQF5,EMLPUNI
.
          MOVE      SP70,EMLPLPN                 * Fill in other fields
          MOVE      ONE,EMLPCNO
          MOVE      EMCNEDST,EMLPSTC
          MOVE      TWO,EMLPREP
          MOVE      SP70,EMLPLTY
          MOVE      EMCNEDFP,EMLPFPN
          MATCH     SP70,KPRINTER
          IF        !@EQUAL
            MOVE      KPRINTER,EMLPFPN
          ENDIF
          MOVE      KEMLPURN,EMLPURN
          MOVE      KEMLPADM,EMLPADM
.
          PACK      KEY10,USERID,SP70            * Read to get userID passcode
          CALL      RDWBSE1
          BRANCH    OVRCD,PROC8000
.
          MOVE      WBSEPCD,EMLPPSC
          MOVE      SP70,EMLPSPA
. 
PROC5000  PACK      KEY5,EMLPUNI,SP70
          CALL      RAEMLP1
          IF          OVRCD = 1
            CALL      WREMLP1
          ELSE
            ADD       ONE,UNIQF5
            MOVE      UNIQF5,EMLPUNI
            GOTO      PROC5000
          ENDIF     
.
          DISPLAY   *P1:22,*B,*EL," Discharge Fax Scheduled. "
          CALL      HITENTER
          GOTO      PROC9000
.
PROC8000  DISPLAY   *P1:22,*B,*EL," Invalid UserID ",*V2LON,USERID,*HOFF:
                    *V2LON,USERID,*HOFF
.
          MOVE      ONE,EXIT
          GOTO      PROC9999
.
PROC9000  MOVE      ZERO,EXIT
PROC9999  RETURN
.
.-------------------------------------------------------------------------------

          INC       STD001IO
          INC       EMRLPCIO/INC
          INC       WEBSCHIO/INC
          INC       WEBSECIO/INC
