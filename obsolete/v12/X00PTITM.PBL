.*****************************************************************************
.*    Program      : X00PTITM                                                *
.*    Description  : Conversion patitemn to new File layout                  *
.*                                                                           *
.*    Author       : J.Tan  TASK 0838262                                     *
.*    Date         :                                                         *
.*    Modifications:                                                         *
.*        V11.00.01 11/03/2020  J.Tan                                        *
.*                  Removed oracle option & date keyin - default to 1/1/1900 *
.*        V11.00.00 26/02/2020  J.Tan                                        *
.*                  Added Effective from and to date to Item file            *
.*                                                                           *
.*****************************************************************************
.
.  using vi Global change command      :%s/FromString/ToString/g
.
.  Global change X00PTITM  eg F57PTDOC
.  Global change x00ptitm  eg f57ptdoc
.  Global change PATITMFD  eg PATDOCFD (new layout)
.  Global change patitemn  eg patdoctf
.  Global change PATITEM1  eg PATDOCT1
.  Global change KEY9   eg KEY8
.  Global change KEY17  eg KEY9
.  Global change WRITEM1  eg WRDOCT1
.  Global change sptitemn  eg sptdoctf
.
.  Global changes if Oracle Option is required -
.
.  Global Change Yes  to Yes
.  Global change RDSITEM1  eg RSPTDOC1
.  Global change RDKITEM1  eg RKPTDOC1
.  Global change UPITEM1  eg UPPTDOC1
.
.  Global changes if Oracle Option is NOT required -

.  Global Change Yes  to No
.  Global change RDSITEM1  to OFDUMYIO
.  Global change RDKITEM1  to OFDUMYIO
.  Global change UPITEM1  to OFDUMYIO
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
LOGFILE   FILE      ASCII, FIXED=256
.
. Enter the first or the shortest index from the original FD and rename
. the index OLDFILE1
.
.                                     ******** copy the old FD here *******
.                                     * comment out all continuing fields and
.                                     * prefix changing fields with "OLD" -
.                                     * remove old Indexes and Redefines     
.
..OLDFILE1  IFILE     SQL, FIXED=xxx, KEY="Uxx-xx"
.
OLDFILE1   IFILE SQL, FIXED=272, KEY="U1-9"
.
. NAME     TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION
. ----     ----   ------   --------  ----------   -----------
.ITEMNO     DIM       9           9         1      ITEM NUMBER
.IALPHA     DIM       6           6        10      ITEM ALPHA KEY
.IDESC      DIM      40          40        16      ITEM DESCRIPTION
.QIAMT      FORM     12.2         8        56      MBS AMOUNT
.IMISGRP    DIM       3           3        64      MISCELLANEOUS GROUP (CAT FI)
.ICLSS      DIM       3           3        67      SUGGESTED CLASS (CAT A )
.IBAND1     DIM       3           3        70      THEATRE CLASS 1 (CAT TB)
.IBAND2     DIM       3           3        73      THEATRE CLASS 2 (CAT TB)
.IBAND3     DIM       3           3        76      THEATRE CLASS 3 (CAT TB)
.IBAND4     DIM       3           3        79      THEATRE CLASS 4 (CAT TB)
.IBAND5     DIM       3           3        82      THEATRE CLASS 5 (CAT TB)
.IBAND6     DIM       3           3        85      THEATRE CLASS 6 (CAT TB)
.IBAND7     DIM       3           3        88      THEATRE CLASS 7 (CAT TB)
.IBAND8     DIM       3           3        91      THEATRE CLASS 8 (CAT TB)
.IBAND9     DIM       3           3        94      THEATRE CLASS 9 (CAT TB)
.IBAND10    DIM       3           3        97      THEATRE CLASS 10(CAT TB)
.IBAND11    DIM       3           3       100      THEATRE CLASS 11(CAT TB)
.IBAND12    DIM       3           3       103      THEATRE CLASS 12(CAT TB)
.IBAND13    DIM       3           3       106      THEATRE CLASS 13(CAT TB)
.IBAND14    DIM       3           3       109      THEATRE CLASS 14(CAT TB)
.IBAND15    DIM       3           3       112      THEATRE CLASS 15(CAT TB)
.IBAND16    DIM       3           3       115      THEATRE CLASS 16(CAT TB)
.PTITACTV   DIM       1           1       118      Active indicator
.                                                   " " = Active
.                                                   "I" = Inactive
.PTITLINK   DIM       9           9       119      Linked item code (inactive
.                                                 items only).
.PTITEXCL   FORM      1           2       128      Exclusion list item ?
.                                                    1 = Yes
.                                                    2 = No
.PTITIWRN   DIM       1           1       130      Warning to enter a Prosthetic
.PTITGSTA   FORM      1           2       131      Is GST applicable?
.                                                 0 = GST Free
.                                                 1 = GST Payable
.                                                 2 = Either
.PTITGSTC   DIM       6           6       133      GST Payable Code
.PTITDCSC   DIM       3           3       139      Daycase Suggested Class.
.                                                     (CAT A)
.PTITDESC   DIM       60          60      142      Long Item Description
.PTITTCER   DIM       3           3       202      Type of Certificate (CAT cr)
.PTITMSCH   DIM       1           1       205      Misc Service Charge Indicator
.PTITTCCD   DIM       1           1       206      Theatre Category Code
.                                                 0/Blank = Simple, 1 = Complex
.PTITSERV   DIM       9           9       207      DVA Service Code
.PTITECEQ   DIM       11          11      216      Eclipse Equivalent
.PTITECTS   DIM       2           2       227      Eclipse Type of Service
.PTITLWRD   DIM       1           1       229      Eclipse Labour Ward Item
.                                                   Blank or 0 = No
.                                                   1 = Yes
.PTITTACB   DIM       3           3       230      Theatre Average Calc. Band
.                                                   (Cat TB)
.PTITSPAR   DIM       39          39      233      Spare
.
.End of Record                           272
.
.  redefine form fields from key
.  -----------------------------
. NAME     TYPE      LENGTH     START LOC.   DESCRIPTION
. ----     ----      ------     ----------   -----------
IAMT       FORM        5             56      MBS AMOUNT
.
SIALPHA    DIM         6            10      ITEM ALPHA KEY
.
.                                     ******** copy the new FD here *******
.
PATITEM1   IFILE SQL, FIXED=351, KEY="U1-9,233-240"
PATITEM2   IFILE SQL, FIXED=351, KEY="U10-15,1-9,233-240"
PATITEM3   IFILE SQL, FIXED=351, KEY="U16-55,1-9,233-240"
.
. NAME     TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION
. ----     ----   ------   --------  ----------   -----------
ITEMNO     DIM       9           9         1      ITEM NUMBER
IALPHA     DIM       6           6        10      ITEM ALPHA KEY
IDESC      DIM      40          40        16      ITEM DESCRIPTION
QIAMT      FORM     12.2         8        56      MBS AMOUNT
IMISGRP    DIM       3           3        64      MISCELLANEOUS GROUP (CAT FI)
ICLSS      DIM       3           3        67      SUGGESTED CLASS (CAT A )
IBAND1     DIM       3           3        70      THEATRE CLASS 1 (CAT TB)
IBAND2     DIM       3           3        73      THEATRE CLASS 2 (CAT TB)
IBAND3     DIM       3           3        76      THEATRE CLASS 3 (CAT TB)
IBAND4     DIM       3           3        79      THEATRE CLASS 4 (CAT TB)
IBAND5     DIM       3           3        82      THEATRE CLASS 5 (CAT TB)
IBAND6     DIM       3           3        85      THEATRE CLASS 6 (CAT TB)
IBAND7     DIM       3           3        88      THEATRE CLASS 7 (CAT TB)
IBAND8     DIM       3           3        91      THEATRE CLASS 8 (CAT TB)
IBAND9     DIM       3           3        94      THEATRE CLASS 9 (CAT TB)
IBAND10    DIM       3           3        97      THEATRE CLASS 10(CAT TB)
IBAND11    DIM       3           3       100      THEATRE CLASS 11(CAT TB)
IBAND12    DIM       3           3       103      THEATRE CLASS 12(CAT TB)
IBAND13    DIM       3           3       106      THEATRE CLASS 13(CAT TB)
IBAND14    DIM       3           3       109      THEATRE CLASS 14(CAT TB)
IBAND15    DIM       3           3       112      THEATRE CLASS 15(CAT TB)
IBAND16    DIM       3           3       115      THEATRE CLASS 16(CAT TB)
PTITACTV   DIM       1           1       118      Active indicator
.                                                   " " = Active
.                                                   "I" = Inactive
PTITLINK   DIM       9           9       119      Linked item code (inactive
.                                                 items only).
PTITEXCL   FORM      1           2       128      Exclusion list item ?
.                                                    1 = Yes
.                                                    2 = No
PTITIWRN   DIM       1           1       130      Warning to enter a Prosthetic
PTITGSTA   FORM      1           2       131      Is GST applicable?
.                                                 0 = GST Free
.                                                 1 = GST Payable
.                                                 2 = Either
PTITGSTC   DIM       6           6       133      GST Payable Code
PTITDCSC   DIM       3           3       139      Daycase Suggested Class.
.                                                     (CAT A)
PTITDESC   DIM       60          60      142      Long Item Description
PTITTCER   DIM       3           3       202      Type of Certificate (CAT cr)
PTITMSCH   DIM       1           1       205      Misc Service Charge Indicator
PTITTCCD   DIM       1           1       206      Theatre Category Code
.                                                 0/Blank = Simple, 1 = Complex
PTITSERV   DIM       9           9       207      DVA Service Code
PTITECEQ   DIM       11          11      216      Eclipse Equivalent
PTITECTS   DIM       2           2       227      Eclipse Type of Service
PTITLWRD   DIM       1           1       229      Eclipse Labour Ward Item
.                                                   Blank or 0 = No
.                                                   1 = Yes
PTITTACB   DIM       3           3       230      Theatre Average Calc. Band
.                                                   (Cat TB)
PTITEFDT   DIM       8           8       233      Effective from Date (CCYYMMDD)
PTITETDT   DIM       8           8       241      Effective to Date (CCYYMMDD)
PTITCDAT   DIM       8           8       249      Date record created (CCYYMMDD)
PTITCTIM   DIM       8           8       257      Time record created (hh:mm:ss)
PTITCUID   DIM       10          10      265      Record created by (websecaf)
PTITUDAT   DIM       8           8       275      Date record updated (CCYYMMDD)
PTITUTIM   DIM       8           8       283      Time record updated (hh:mm:ss)
PTITUUID   DIM       10          10      291      Record updated by (websecaf)
PTITSPAR   DIM       50          50      301      Spare
.
.End of Record                           351
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       200
.
DEFPATH   DIM       60
DIM60     DIM       60
DISNUM    FORM      4
.
ERORTEXT  DIM       80
EFFRDATE  DIM       8
.
INPFILE   DIM       8
.
MINUS3    FORM      "-3"
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
SAVEFLG   FORM      1
.
USINGORC  DIM       1
.
.
. PROGRAM CONSTANTS
. -----------------
CREATEXT  INIT    "Log file created "
DASHLINE  INIT      "--------------------------------------------------":
                    "------------------------------"
ENDGTEXT  INIT    "Ending   fixit (Option "
OPENTEXT  INIT    "Log file opened "
SP60      INIT    "                                                            "
STARTTXT  INIT    "Starting fixit (Option "
.
.
.
PRGNAM    INIT      "CONVERSION PATITMFD"
PRGID     INIT      "X00PTITM"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
.
          CALL      OLOG0000                      * create/open log file
MAIN0500  CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000:             * run c-isam fixit
                            MAIN2000              * run oracle fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9000
.
MAIN2000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN0500:               * no
                          MAIN9000                * cancel
.
MAIN5000  BRANCH    COPTION,MAIN6000:             * c-isam fixit
                            MAIN7000              * oracle fixit
.
.         Running c-isam fix
.
MAIN6000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9000
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/zip saved file
          GOTO      MAIN9000
.
.         Running oracle fix
.
MAIN7000  CALL      OFIX0000                      * run oracle fix
.
MAIN9000  CALL      CLOG0000                      * close log file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
          MOVE      "19000101",EFFRDATE
          
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  MOVE      "No ",USINGORC
          REP       "yYnN",USINGORC
          MOVE      USINGORC,KEY1
          REP       "Y-N-",KEY1
          MATCH     "-",KEY1
          GOTO      OPTN9200 IF NOT EQUAL
.
          DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)"
          MATCH     "Y",USINGORC
          IF        @EQUAL
            DISPLAY   *P1:6,*V2LON,TWO,*HOFF,". Run Fixit (Oracle only)"
          ENDIF
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * run c-isam fixit
                            OPTN9000             * run oracle fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9200  DISPLAY   *P1:3,*EF,*P1:4," Oracle Option has not been set up ":
                          "correctly":
                    *P1:6," Program will terminate    - ";
          MOVE      "Oracle Option has not been set up correctly",ERORTEXT
          WRITE     LOGFILE,SEQ;*+,ERORTEXT
          CALL      HITENTER
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
.
PREP0000  CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
.
          CALL      SLOG0000                      * write start to log file
.
          MOVE      "patitemn",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat patitemn.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sptitemn.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat patitemn.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sptitemn.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "patitemn",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Unable to remove the original file.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "sptitemn",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            MOVE      "Unable to open saved original file.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "patitemn",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.      eg    APPEND    " 195 UC9-16",CMDLINE
.
          APPEND    " 351 UC1-9,233-240",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has 
.  to be repeated with for each individual index
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC10-15,1-9,233-240",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC16-55,1-9,233-240",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
          OPEN      PATITEM1,NEWFILE
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          GOTO      PREP9999
.
PREP5000  MOVE      "Old file not found.  ",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  MOVE      "Unable to copy original file.  (Error: ",ERORTEXT
          ENDSET    ERORTEXT
          APPEND    TASKID,ERORTEXT
          APPEND    ")  ",ERORTEXT
          RESET     ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
            MOVE      "File does not exist",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
              MOVE      "File has already been converted",ERORTEXT
              WRITE     LOGFILE,SEQ;ERORTEXT
            ELSE
              DISPLAY   "has caused an IO error - ";
              MOVE      "File has caused an IO Error",ERORTEXT
              WRITE     LOGFILE,SEQ;ERORTEXT
            ENDIF
          ENDIF
          MOVE      "File will be bypassed",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *+,ERORTEXT
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY9,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
.  All necessary changes to the files field lengths, date changes
.  or any other sort of modification must be made here
.
.    eg    PACK      DSPARE,SP70
.          PACK      PTDSDMDC,SP10
.          PACK      PTDSDDRG,SP10
.                                               * Pack key here
.
          PACK      PTITEFDT,EFFRDATE,SP70      * Effective from Date (CCYYMMDD)
          PACK      PTITETDT,SP70               * Effective to Date (CCYYMMDD)
          PACK      PTITCDAT,CCC,CYY,CMM,CDD    * Date record created (CCYYMMDD)
          REP       " 0",PTITCDAT
          PACK      PTITCTIM,SP70               * Time record created (hh:mm:ss)
          PACK      PTITCUID,SP70               * Record created by (websecaf)
          PACK      PTITUDAT,SP70               * Date record updated (CCYYMMDD)
          PACK      PTITUTIM,SP70               * Time record updated (hh:mm:ss)
          PACK      PTITUUID,SP70               * Record updated by (websecaf)
.
          PACK      KEY17,ITEMNO,PTITEFDT,SP70
          CALL      WRITEM1                     * write to new file
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     PATITEM1                      * close new file
          CLOSE     OLDFILE1                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS:
                    *P1:24;
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
.
VALD0000  MOVE      ZERO,EXIT
.
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            MOVE      "Directory path must end with a slash (/) ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Directory does not exist ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or zip save file                                     *
.**********************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "sptitemn*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "gzip -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Saved file not zipped or removed.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:24,*EL,*B,"Finished processing  ",CDATE,SP1,CTIMEIS:
                    ".  ";
          CALL      HITENTER
.
          CALL      ELOG0000                     * write an end log entry
.
ENDP9999  RETURN
+
.**********************************************************************
.*        DEFT0000 -  Get the default directory                       *
.**********************************************************************
.
DEFT0000  EXECUTE   "ldat patitemn.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  MOVE      "Error finding 'patitemn'.  ",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.*                            OLOG0000                                       *
.*                    Open the log file                                      *
.*****************************************************************************
.
OLOG0000  CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      LOGFILE,"x00ptitm.log"       * open log file
          TRAPCLR   IO
          IF        OVRCD = 1
            PREP      LOGFILE,"x00ptitm.log"     * create and open log file
            WRITE     LOGFILE,MINUS3;DASHLINE
            WRITE     LOGFILE,SEQ;CREATEXT,CDATE,SP1,CTIMEIS
          ELSE
            WRITE     LOGFILE,MINUS3;DASHLINE
            WRITE     LOGFILE,SEQ;OPENTEXT,CDATE,SP1,CTIMEIS
          ENDIF
.
OLOG9999  RETURN
+
.*****************************************************************************
.*                            CLOG0000                                       *
.*                    Close the log file                                     *
.*****************************************************************************
.
CLOG0000  CLOSE     LOGFILE
.
CLOG9999  RETURN
+
.*****************************************************************************
.*                            SLOG0000                                       *
.*                    Write a start log to the log file                      *
.*****************************************************************************
.
SLOG0000  WRITE     LOGFILE,SEQ;DASHLINE
          WRITE     LOGFILE,SEQ;STARTTXT,COPTION,RBRACK,SP1,CDATE,SP1:
                                   CTIMEIS
.
SLOG9999  RETURN
+
.*****************************************************************************
.*                            ELOG0000                                       *
.*                    Write an end log to the log file                       *
.*****************************************************************************
.
ELOG0000  WRITE     LOGFILE,SEQ;ENDGTEXT,COPTION,RBRACK,SP1,CDATE,SP1:
                                   CTIMEIS
ELOG9999  RETURN
+
.*****************************************************************************
.*                            OFIX0000                                       *
.*                    Oracle fix to "PATITMFD"                               *
.*****************************************************************************
.
OFIX0000  DISPLAY   *P1:24,*EL;
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,*EL,"Record No. :";
.
          CALL      SLOG0000                      * write start to log file
.
          OPEN      PATITEM1,"patitemn"
.
          PACK      KEY17,SP70
          CALL      RDSITEM1                      * position at start of file
OFIX0500  CALL      RDKITEM1                      * read next record
          BRANCH    OVRCD,OFIX9000                * eof - finished
.
          ADD       ONE,RECNUM                    * increment record counter
          ADD       ONE,DISNUM                    * Display counter
.         
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM 
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
.>>>>>>>  Add code here to populate/update variables
.
          PACK      PTITEFDT,EFFRDATE,SP70      * Effective from Date (CCYYMMDD)
          PACK      PTITETDT,SP70               * Effective to Date (CCYYMMDD)
          PACK      PTITCDAT,CCC,CYY,CMM,CDD    * Date record created (CCYYMMDD)
          REP       " 0",PTITCDAT
          PACK      PTITCTIM,SP70               * Time record created (hh:mm:ss)
          PACK      PTITCUID,SP70               * Record created by (websecaf)
          PACK      PTITUDAT,SP70               * Date record updated (CCYYMMDD)
          PACK      PTITUTIM,SP70               * Time record updated (hh:mm:ss)
          PACK      PTITUUID,SP70               * Record updated by (websecaf)
.
          CALL      UPITEM1
.
          GOTO      OFIX0500                     * get next record
.         
OFIX9000  CLOSE     PATITEM1
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Update completed ",CDATE,SP1,CTIMEIS:
                    *P1:24;
          CALL      HITENTER
.
          CALL      ELOG0000                     * write an end log entry
.
OFIX9999  RETURN
.
OFDUMYIO  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
.                  ******************************    OLD IO ROUTINES
.                  Copy the Positional Read (RS or RDS) from original I/O file,
.                  - change Label to READSOLD and the index name to OLDFILE1
.
.READSOLD  MOVE      ZERO,OVRCD
.          RESET     KEY6
.          PACK      KEY8,SP2,KEY6
.          READ      OLDFILE1,KEY8;;
.          RETURN
.
READSOLD  MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      OLDFILE1,KEY9;;
          RETURN
.
.
.                  Copy the Read Next (RK or RDK) from the original I/O file
.                  - change Label to READKOLD and the index name to OLDFILE1
.                  - change any fields that are changing to a prefix of OLD,
.                    eg. PTEDSPAR becomes OLDDSPAR
.
.READKOLD  MOVE      ZERO,OVRCD
.          READKS    OLDFILE1;DDURNO,DDADMNO:
.                             DIM6,DTIME,DSTAT,DDEST:
.                             PTDSVOGU,DSPARE
.          GOTO      OVERCOND IF OVER
.          MOVE      QDURNO,DURNO
.          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;ITEMNO,IALPHA,IDESC,QIAMT,IMISGRP:
                             ICLSS,IBAND1,IBAND2,IBAND3,IBAND4:
                             IBAND5,IBAND6,IBAND7,IBAND8:
                             IBAND9,IBAND10,IBAND11,IBAND12:
                             IBAND13,IBAND14,IBAND15,IBAND16:
                             PTITACTV,PTITLINK,PTITEXCL,PTITIWRN:
                             PTITGSTA,PTITGSTC,PTITDCSC,PTITDESC:
                             PTITTCER,PTITMSCH,PTITTCCD,PTITSERV:
                             PTITECEQ,PTITECTS,PTITLWRD,PTITTACB,PTITSPAR
          GOTO      OVERCOND IF OVER
          MOVE      QIAMT,IAMT
          MOVE      IALPHA,SIALPHA  *** FOR SECOND KEY
          RETURN
.
.                  ******************************    NEW IO ROUTINES
.                  Copy the Write statement from the new I/O file           
.                  (no changes are needed to this routine)               
.
.                  For Oracle fixits, you will probably also need to copy
.                  the RS.., RK.... & UP.... I/O routines for the new FD.
.
RDSITEM1  RESET     KEY17
          READ      PATITEM1,KEY17;;
          RETURN
.
RDKITEM1  MOVE      ZERO,OVRCD
          READKS    PATITEM1;ITEMNO,IALPHA,IDESC,QIAMT,IMISGRP:
                             ICLSS,IBAND1,IBAND2,IBAND3,IBAND4:
                             IBAND5,IBAND6,IBAND7,IBAND8:
                             IBAND9,IBAND10,IBAND11,IBAND12:
                             IBAND13,IBAND14,IBAND15,IBAND16:
                             PTITACTV,PTITLINK,PTITEXCL,PTITIWRN:
                             PTITGSTA,PTITGSTC,PTITDCSC,PTITDESC:
                             PTITTCER,PTITMSCH,PTITTCCD,PTITSERV:
                             PTITECEQ,PTITECTS,PTITLWRD,PTITTACB:
                             PTITEFDT,PTITETDT,PTITCDAT,PTITCTIM:
                             PTITCUID,PTITUDAT,PTITUTIM,PTITUUID,PTITSPAR
          GOTO      OVERCOND IF OVER
          MOVE      QIAMT,IAMT
          MOVE      IALPHA,SIALPHA  *** FOR SECOND KEY
          RETURN
.
UPITEM1   UPDATE    PATITEM1;ITEMNO,IALPHA,IDESC,QIAMT,IMISGRP:
                             ICLSS,IBAND1,IBAND2,IBAND3,IBAND4:
                             IBAND5,IBAND6,IBAND7,IBAND8:
                             IBAND9,IBAND10,IBAND11,IBAND12:
                             IBAND13,IBAND14,IBAND15,IBAND16:
                             PTITACTV,PTITLINK,PTITEXCL,PTITIWRN:
                             PTITGSTA,PTITGSTC,PTITDCSC,PTITDESC:
                             PTITTCER,PTITMSCH,PTITTCCD,PTITSERV:
                             PTITECEQ,PTITECTS,PTITLWRD,PTITTACB:
                             PTITEFDT,PTITETDT,PTITCDAT,PTITCTIM:
                             PTITCUID,PTITUDAT,PTITUTIM,PTITUUID,PTITSPAR
.
          RETURN
.
.
WRITEM1   RESET     KEY17
          WRITE     PATITEM1,KEY17;ITEMNO,IALPHA,IDESC,QIAMT,IMISGRP:
                                  ICLSS,IBAND1,IBAND2,IBAND3,IBAND4:
                                  IBAND5,IBAND6,IBAND7,IBAND8:
                                  IBAND9,IBAND10,IBAND11,IBAND12:
                                  IBAND13,IBAND14,IBAND15,IBAND16:
                                  PTITACTV,PTITLINK,PTITEXCL,PTITIWRN:
                                  PTITGSTA,PTITGSTC,PTITDCSC,PTITDESC:
                                  PTITTCER,PTITMSCH,PTITTCCD,PTITSERV:
                                  PTITECEQ,PTITECTS,PTITLWRD,PTITTACB:
                                  PTITEFDT,PTITETDT,PTITCDAT,PTITCTIM:
                                  PTITCUID,PTITUDAT,PTITUTIM,PTITUUID,PTITSPAR
.
          RETURN
.
          INC       STD001IO
