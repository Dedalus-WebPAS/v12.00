.**************************************************************************
.                       DFNP0000
.         get the default National U/R Number for the patient
.         Para's  : PURNO           the U/R Number
.                   PVIBILL         the Admission Number for a "0" UR
.                   NMPNUMB         the national U/R Number (right just)
.         Returns : NMPNUMB         the national U/R Number (right just)
.**************************************************************************
DFNP0000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,EIGHTY;*138,PTCNNZUR,PTCNNUMH
.
          COMPARE   ONE,PTCNNZUR
          GOTO      DFNP0100 IF NOT EQUAL   * not storing NMPI in U/R
.
.         we are storing NMPI number as the Local U/R Number
.
          PACK      NMPNUMB,NMPNUMB,SP20
          MATCH     SP20,NMPNUMB
          GOTO      DFNP9999 IF NOT EQUAL   * already have an NMPI number
.
          COMPARE   ZERO,PTCNNUMH
          GOTO      DFNP0100 IF EQUAL       * get NMPI from patnmpaf
.
.         we are running as multi hospital/control files so hard code the 
.         NMPI number to the U/R Number (if they have a U/R Number)
.
          MATCH     ZEROUR,PURNO
          GOTO      DFNP0100 IF EQUAL       * dont have a UR number
.
          PACK      NMPNUMB,SP10,SP2,PURNO  * set NMPI number to UR number
          GOTO      DFNP9999
.
. -----------------------------------------------------------------
. ------- get the national number for the patient from file -------
. -----------------------------------------------------------------
.
DFNP0100  UNPACK    SP70,NMPNUMB,NHMANMPI,PTNINMPI
          COMPARE   ONE,PTCNNHII            * using NZHIS/IBA Interface
          GOTO      DFNP9999 IF EQUAL       
.
          MATCH     ZEROUR,PURNO
          GOTO      DFNP2000 IF NOT EQUAL   * U/R Number ?
.
. ------- get NMPNUMB from patpniaf -------
.
DFNP1000  OPEN      PATPNIA1,"patpniaf"     * Open PATPNIFD file 1
          PACK      KEY10,CHOSINDX,PVIBILL  * No. zero U/R
          CALL      RDPTPNI1                * On PATPNI file ?
          BRANCH    OVRCD,DFNP9999          * No. Exit
          MOVE      PTPNNMPI,NMPNUMB        * Yes.
          CLOSE     PATPNIA1                * close file
          GOTO      DFNP9999
.
. ------- get NMPNUMB from patnmpaf or patnidaf -------
.
DFNP2000  COMPARE   ONE,PTCNNMPI            * Yes. U/R Number
          GOTO      DFNP3000 IF NOT EQUAL   * using patnidfd
          COMPARE   ONE,PTCNNHII            * Yes. U/R Number
          GOTO      DFNP3000 IF NOT EQUAL   * using patnidfd
.
.         using NHIMASFD
.
          OPEN      NHIMASA2,"nhimasaf"
          PACK      KEY8,PURNO
          CALL      RDNHMAS2
          PACK      NMPNUMB,SP10,SP2,NHMANMPI
          GOTO      DFNP9999
.
.         using PATNIDFD
.
DFNP3000  OPEN      PATNIDA1,"patnidaf"
          PACK      KEY10,CHOSINDX,PURNO
          CALL      RDPTNID1
          MOVE      PTNINMPI,NMPNUMB
          CLOSE     PATNIDA1
.
DFNP9999  RETURN
