. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS86                                             *
. * Desc      :   Statistical Budget Input                             *
. **********************************************************************
. * Date      :   02.10.1990                                           *
. * Author    :   Glen Hobby                                           *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
. * VF.00.01  05.01.2000 Charles Handaya                               *
. *           Fix writing to Audit Files                               *
. **********************************************************************
.$$$$$$
.  Statistical Budget Input (IBAFMS89)
.  -----------------------------------
.             open files and initialise variables
.             WHILE user wants to add/modify/delete DO
.                  get desired option (add, modify or delete)
.                  CASE option OF
.                       ADD     : WHILE user wants to add records DO
.                                      accept initial values
.                                      WHILE user wants modify screen data DO
.                                           get number of item to be modified
.                                           allow user to modify option
.                                      END WHILE
.                                      IF user wants to post data
.                                           THEN write new data
.                                      END IF
.                                 END WHILE
.                       MODIFY  : WHILE user wants to modify records DO
.                                      get and display desired record
.                                      WHILE user wants modify screen data DO
.                                           get number of item to be modified
.                                           allow user to modify option
.                                      END WHILE
.                                      IF user wants to post data
.                                           THEN update data
.                                      END IF
.                                 END WHILE
.                       DELETE  : WHILE user wants to delete records DO
.                                      get and display desired record
.                                      IF user wants to delete data
.                                           THEN delete data
.                                      END IF
.                                 END WHILE
.                  END CASE
.             END WHILE
.             delete temporary files
.        END
.$$$$$$
. Standard FMS Accounting System Include
.---------------------------------------
          INC       FMSSTDDF
          INC       FMSUPDDF           * standard update include
          INC       FMSUDADF           * standard budget audit update include
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
          INC       FMSBPFFD/INC
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
ANBUDGET  FORM      12.5
.
BUDFILE   DIM       8
FILENAME  DIM       8
.
FORM4P2   FORM      4.2 
XF12P5    FORM      12.5
FORM12P5  FORM      12.5
FORM13    FORM      13.5
FORM13P5  FORM      13.5
FORM14P5  FORM      14.5
.
MODE      FORM      1
.
NOPERD    FORM      2
.
STATFILE  DIM       8
.
PRGID     INIT      "IBAFMS86"
PRGNAM    INIT      "Statistical Budget Input"
VERSION   INIT      "V12.00.00"
.
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML0100    CALL      SELOPTN            * get option
          COMPARE   ZERO,OPTION        * exit?
          GOTO      ML9999 IF EQUAL
.
ML1100    CALL      CLR0000            * clear variables
          CALL      GBTY0000           * get Budget Type
          BRANCH    EXIT,ML0100        * exit program
.
ML1200    CALL      GLED0000           * get Ledger
          BRANCH    EXIT,ML1100,ML0100 * Return to Budget Type or main menu
.
ML1300    CALL      GACC0000           * get Account
          BRANCH    EXIT,ML1200,ML0100 * Return to ledger or main menu
.
          CALL      CKBL0000           * check for budget link
          BRANCH    EXIT,ML1300        * Return to account
.
          CALL      PROC0000           * process record
          GOTO      ML1300             * get next entry
.
ML9999    MOVE      PORT,PORTCODE
          CALL      CTUP0000           * Update all changed accounts
          CHAIN     PGM                * chain back to menu
.
.******************************************************************************
.* INIT - Open Files                             Called by ML0000             *
.******************************************************************************
INIT0000  CALL      DISPHEAD                     * display screen
          CALL      OUPD0000                     * open files for update procs
          CALL      OUDA0000                     * open files for audit updates
.
          DISPLAY   *P56:24,"Opening fmsbpfaf";  * 
          OPEN      FMSBPFA1,"fmsbpfaf"
.
INIT9999  RETURN
.
.*************************************************************************
.* CLR - Clear Variables                Called by ML0100                 *
.*************************************************************************
CLR0000   UNPACK    SP30,FMBTCODE,FMLALEDG,FMAMACCT
          UNPACK    SP70,FMBTDESC,FMLALEDG
          UNPACK    SP70,FMAMACCT,FMBPDESC
          MOVE      ZERO,POSTFLAG
.
          MOVE      ZERO,FMBPPC1
          MOVE      ZERO,FMBPPC2
          MOVE      ZERO,FMBPPC3
          MOVE      ZERO,FMBPPC4
          MOVE      ZERO,FMBPPC5
          MOVE      ZERO,FMBPPC6
          MOVE      ZERO,FMBPPC7
          MOVE      ZERO,FMBPPC8
          MOVE      ZERO,FMBPPC9
          MOVE      ZERO,FMBPPC10
          MOVE      ZERO,FMBPPC11
          MOVE      ZERO,FMBPPC12
          MOVE      ZERO,FMBPPC13
.
CLR9999   RETURN
.******************************************************************************
.* GBTY - Enter the Budget Type                  Called by ML0000             *
.******************************************************************************
GBTY0000  CALL      SCRA0000           * display screen
.
GBTY1000  MOVE      "28",CCOL
          MOVE      "4",CVERT
          CALL      KBTYFM00           * keyin budget type code
          BRANCH    EXIT,GBTY9000,GBTY9000
.
          DISPLAY   *P28:4,*V2LON,FMBTCODE,*HOFF,*P41:4,FMBTDESC
.
          MATCH     "2",FMBTSTAT
          GOTO      GBTY8500 IF EQUAL
.
.** open the budget file for this budget type
.
          CLOSE     FMSSBUA1
          CLOSE     FMSBUDA1
          PACK      BUDTYPE,FMBTCODE,SP70
          PACK      STATFILE,FMSC,FMBTCODE
          PACK      BUDFILE,FMSB,FMBTCODE
          REP       " 0",STATFILE
          REP       " 0",BUDFILE
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FMSSBUA1,STATFILE
          TRAPCLR   IO
          BRANCH    OVRCD,GBTY8000
          MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FMSBUDA1,BUDFILE
          TRAPCLR   IO
          BRANCH    OVRCD,GBTY8100
          MOVE      ZERO,EXIT
.
          GOTO      GBTY9999
.
GBTY8000  DISPLAY   *P1:24,*EL,*B,"Budget Stat File ",STATFILE:
                    " Not Available - ";
          CALL      HITENTER
          GOTO      GBTY0000
.
GBTY8100  DISPLAY   *P1:24,*EL,*B,"Budget File ",BUDFILE:
                    " Not Available - ";
          CALL      HITENTER
          GOTO      GBTY0000
.
GBTY8500  DISPLAY   *P1:24,*EL,*B,"Budget File Locked. No Maintenance ":
                    "Allowed - ";
          CALL      HITENTER
          GOTO      GBTY0000
.
GBTY9000  MOVE      ONE,EXIT
.
GBTY9999  RETURN
.
.******************************************************************************
.* GLED - Enter the Ledger Code                  Called by ML0000             *
.******************************************************************************
GLED0000  MOVE      "5",CVERT
          MOVE      "28",CCOL
.
          CALL      KLMAFM00              * keyin ledger code
          BRANCH    EXIT,GLED9999,GLED9999
.
          DISPLAY   *P28:5,*V2LON,FMLALEDG,*HOFF,*P41:5,FMLADESC
          MOVE      "12",NOPERD           * calculate number of periods
          ADD       FMLAPERS,NOPERD
          MOVE      ZERO,EXIT
          GOTO      GLED9999
.
GLED9999  RETURN
.
.******************************************************************************
.* GACC - Enter the Account Code                 Called by ML0000             *
.******************************************************************************
GACC0000  MOVE      "6",CVERT
          MOVE      "28",CCOL
          DISPLAY   *P1:7,*EF
          MOVE      "6",CACCTIND
.
          CALL      KAMAFM00              * keyin account code
          BRANCH    EXIT,GACC9999,GACC9999
.
          DISPLAY   *P41:6,FMAMDESC
          MOVE      FMAMDPLA,DECPLACE        * Number of decimal places
.
          MOVE      ZERO,EXIT
          MOVE      FMAMTYPE,FORM1
.
          COMPARE   EIGHT,FORM1
          GOTO      GACC1000 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Invalid Account Type. ";
          CALL      HITENTER
          GOTO      GACC0000
.
GACC1000  PACK      KEY14,FMLALEDG,FMAMACCT
          CALL      RDFMST1         * is there a record on file for these values
          BRANCH    OVRCD,GACC2000
.
          PACK      FMBPCODE,FMSTCUPR,SP70
          COMPARE   OPTION,ONE      * there is one - can't be in insert mode
          GOTO      GACC9999 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Budget already on file. ";
          CALL      HITENTER
          GOTO      GACC0000
.
GACC2000  PACK      FMBPCODE,SP70
          BRANCH    OPTION,GACC9999  * not present - must be in insert mode
.
          DISPLAY   *P1:24,*EL,*B,"Statistical budget not on file. ";
          CALL      HITENTER
          GOTO      GACC0000
.
GACC9999  RETURN
.**********************************************************************
.  CKBL - check for budget link                        Called By ML
.        Requires - FMBTCODE, FMAMLEDG, FMAMACCT
.        Returns  - EXIT     (1=quit)
.**********************************************************************
CKBL0000  PACK      FILENAME,FMSL,FMBTCODE,SP70  * get desired filename
          REP       " 0",FILENAME
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO               * look for file opening error
          OPEN      FMSLBUA1,FILENAME            * open file (if possible)
          TRAPCLR   IO
          BRANCH    OVRCD,CKBL9000               * file not found ? (continue)
          OPEN      FMSLBUA2,FILENAME            * open file (key2 for PRCFMSAA)
.
          PACK      KEY14,FMAMLEDG,FMAMACCT,SP70
          CALL      RDFMBL1
          BRANCH    OVRCD,CKBL9000               * record not found ? (continue)
          GOTO      CKBL9500                     * record found ? (quit)
.
CKBL9000  MOVE      ZERO,EXIT          * continue
          GOTO      CKBL9999
.
CKBL9500  MOVE      ONE,EXIT           * quit
          DISPLAY   *P1:24,*B,*EF,"Budget Link Exists for this Account - ";
          CALL      HITENTER
.
CKBL9999  RETURN
.*************************************************************************
.* PROC - Keyin the budget profile and process the record                *
.*                     called by : ML                                    *
.*************************************************************************
PROC0000  MOVE      ZERO,MODE
          PACK      KEY6,FMLALEDG,FMBTYEAR
          CALL      RDFMLC1
          COMPARE   ZERO,OVRCD          * record present?
          GOTO      PROC0100 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"WARNING - Budget year not present. ";
          CALL      HITENTER
.         
          PACK      KEY6,FMLALEDG,CCC,CYY
          CALL      RDFMLC1
.
PROC0100  
          CALL      SCRB0000            * display screen
          MOVE      ZERO,MODE
.
PROC0500  COMPARE   ONE,OPTION          * insert mode?
          GOTO      PROC0600 IF EQUAL
.
          CALL      DBUD0000            * display the budget thats on file
.         
          COMPARE   TWO,OPTION          * Change mode?
          GOTO      PROC5000 IF EQUAL
.
          CALL      DEL0000             * ok to delete
          GOTO      PROC9999
.
PROC0600  MOVE      "8",CVERT           * standard keyin variables
          MOVE      "28",CCOL
          CALL      KBPFFM00            * keyin budget profile
          BRANCH    EXIT,PROC1000,PROC9000
.
          DISPLAY   *P28:8,*EL,*V2LON,FMBPCODE,*HOFF,*P41:8,FMBPDESC
          CALL      DOPT0000            * display valid options
          COMPARE   ZERO,MODE           * Inserting ?
          GOTO      PROC1100 IF EQUAL
          CALL      CBUD0000            * calculate budget
          GOTO      PROC5000
.
PROC1000  CALL      DOPT0000            * display valid options
          COMPARE   ZERO,MODE             * Inserting ?
          GOTO      PROC1100 IF EQUAL
.
          DISPLAY   *P46:10,*EL,*P46:11,*EL,*P46:12,*EL,*P46:13,*EL:
                    *P46:14,*EL,*P46:15,*EL,*P46:16,*EL,*P46:17,*EL:
                    *P46:18,*EL,*P46:19,*EL,*P46:20,*EL,*P46:21,*EL:
                    *P46:22
.
          GOTO      PROC5000
.
PROC1100  CALL      KABD0000           * keyin annual budget
          BRANCH    EXIT,PROC2000
.
PROC1150  CALL      CBUD0000           * calculate budget
          GOTO      PROC5000
.
PROC2000  CALL      KBSP0000           * keyin budgets for each period
.
PROC5000  CALL      MAINQST
          MOVE      ONE,MODE
          COMPARE   "0",CCITEM             * post?
          GOTO      PROC8000 IF EQUAL 
          COMPARE   "-1",CCITEM             * post?
          GOTO      PROC9000 IF EQUAL 
.
          BRANCH    CCITEM,PROC0600,PROC1100
.
          MOVE      NOPERD,FORM2          * work out how many selections on scr
          ADD       TWO,FORM2
          COMPARE   CCITEM,FORM2          * is the choice too big?
          GOTO      PROC5000 IF LESS
.
          ENDSET    FMBPCODE
          APPEND    SP30,FMBPCODE
          RESET     FMBPCODE               * is profile code null?
.
          MATCH     SP3,FMBPCODE
          GOTO      PROC6000 IF EQUAL      * yes - you can chose any field
.
          BEEP
          GOTO      PROC5000
.
PROC6000  SUB       TWO,CCITEM
          CALL      KBUD0000              * keyin budget for one period
          CALL      CANB0000              * recalculate annual budget
          GOTO      PROC5000
.
PROC8000  CALL      POST0000
.
PROC9000  MOVE      ONE,POSTFLAG
.
PROC9999  RETURN
.
.*************************************************************************
.*  KABD - Keyin annual budget                       Called by PROC2000  *
.*************************************************************************
KABD0000  
....      MOVE      FMAMDPLA,DECPLACE        * Number of decimal places
.
          MOVE      "28",CCOL
          MOVE      "9",CVERT
          CALL      AMTR0000                
          MOVE      KAMOUNT,ANBUDGET
.
          COMPARE   ZERO,ANBUDGET             * was an annual budget entered?
          GOTO      KABD9000 IF EQUAL
          MOVE      ZERO,EXIT
          GOTO      KABD9999
.
KABD9000  MATCH     SP3,FMBPCODE
          GOTO      KABD0000 IF NOT EQUAL
          MOVE      ONE,EXIT
.
KABD9999  RETURN
.*************************************************************************
.* KBSP - keyin the budget for each month seperately                     *
.*                     called by : PROC3000                              *
.*************************************************************************
KBSP0000  MOVE      ONE,CCITEM
.
KBSP1000  MOVE      SP30,FMBPDESC
          CALL      KBUD0000               * keyin budget
.
          COMPARE   NOPERD,CCITEM            * have we done all 13
          GOTO      KBSP9000 IF EQUAL
.
          ADD       ONE,CCITEM             * increment ccitem for insert mode
          GOTO      KBSP1000
.
KBSP9000  CALL      CANB0000               * calculate annual budget
KBSP9999  RETURN
.*************************************************************************
.* CABD - Calculate annual budget           Called by KBSP9000           *
.*************************************************************************
CANB0000  MOVE      ZERO,FORM2
          MOVE      ZERO,ANBUDGET
.
CANB1000  ADD       ONE,FORM2
          LOAD      FORM12P5,FORM2,FMSTCA01,FMSTCA02,FMSTCA03,FMSTCA04:
                                   FMSTCA05,FMSTCA06,FMSTCA07,FMSTCA08:
                                   FMSTCA09,FMSTCA10,FMSTCA11,FMSTCA12:
                                   FMSTCA13
.
          ADD       FORM12P5,ANBUDGET
          COMPARE   NOPERD,FORM2
          GOTO      CANB1000 IF NOT EQUAL
.
          MOVE      "9",LINENO
          MOVE      "28",COLNO
          MOVE      ANBUDGET,KAMOUNT
          CALL      AMTD0000
.
CANB9999  RETURN
.*************************************************************************
.* CLRB - Clear all budget file variables          Called by PROC3400    *
.*************************************************************************
CLRB0000  MOVE      ZERO,FMSTCA01
          MOVE      ZERO,FMSTCA02
          MOVE      ZERO,FMSTCA03
          MOVE      ZERO,FMSTCA04
          MOVE      ZERO,FMSTCA05
          MOVE      ZERO,FMSTCA06
          MOVE      ZERO,FMSTCA07
          MOVE      ZERO,FMSTCA08
          MOVE      ZERO,FMSTCA09
          MOVE      ZERO,FMSTCA10
          MOVE      ZERO,FMSTCA11
          MOVE      ZERO,FMSTCA12
          MOVE      ZERO,FMSTCA13
.
CLRB9999  RETURN
.*************************************************************************
.* CLRS - Clear the bottom right of the screen   Called by PROC2000      *
.*************************************************************************
CLRS0000  MOVE      "9",CVERT
.
CLRS1000  DISPLAY   *P28:CVERT,SP30
          ADD       ONE,CVERT
.
          COMPARE   TWENTY3,CVERT
          GOTO      CLRS1000 IF NOT EQUAL
.
          PACK      FMBPCODE,FMBPCODE,SP5
          MATCH     SP3,FMBPCODE             * are we using a budget profile
          GOTO      CLRS9999 IF NOT EQUAL    * no - get out
.
CLRS9999  RETURN
.
.*************************************************************************
.* CBUD - Calculate the budget for each period    Called by PROC3000     *
.*************************************************************************
CBUD0000  CALL      CLRB0000                 * Clear variables
          MATCH     SP3,FMBPCODE
          GOTO      CABD0000 IF EQUAL
.
          MOVE      ANBUDGET,FORM14P5   * move the annual budget into each field
          MULT      FMBPPC1,FORM14P5    * Multiply each period by its percentage
          DIV       "100",FORM14P5      * Divide each period by 100 to get value
          MOVE      FORM14P5,FMSTCA01   * move the annual budget into each field
.
          MOVE      ANBUDGET,FORM14P5
          MULT      FMBPPC2,FORM14P5
          DIV       "100",FORM14P5
          MOVE      FORM14P5,FMSTCA02
.
          MOVE      ANBUDGET,FORM14P5
          MULT      FMBPPC3,FORM14P5
          DIV       "100",FORM14P5
          MOVE      FORM14P5,FMSTCA03
.
          MOVE      ANBUDGET,FORM14P5
          MULT      FMBPPC4,FORM14P5
          DIV       "100",FORM14P5
          MOVE      FORM14P5,FMSTCA04
.
          MOVE      ANBUDGET,FORM14P5
          MULT      FMBPPC5,FORM14P5
          DIV       "100",FORM14P5
          MOVE      FORM14P5,FMSTCA05
.
          MOVE      ANBUDGET,FORM14P5
          MULT      FMBPPC6,FORM14P5
          DIV       "100",FORM14P5
          MOVE      FORM14P5,FMSTCA06
.
          MOVE      ANBUDGET,FORM14P5
          MULT      FMBPPC7,FORM14P5
          DIV       "100",FORM14P5
          MOVE      FORM14P5,FMSTCA07
.
          MOVE      ANBUDGET,FORM14P5
          MULT      FMBPPC8,FORM14P5
          DIV       "100",FORM14P5
          MOVE      FORM14P5,FMSTCA08
.
          MOVE      ANBUDGET,FORM14P5
          MULT      FMBPPC9,FORM14P5
          DIV       "100",FORM14P5
          MOVE      FORM14P5,FMSTCA09
.
          MOVE      ANBUDGET,FORM14P5
          MULT      FMBPPC10,FORM14P5
          DIV       "100",FORM14P5
          MOVE      FORM14P5,FMSTCA10
.
          MOVE      ANBUDGET,FORM14P5
          MULT      FMBPPC11,FORM14P5
          DIV       "100",FORM14P5
          MOVE      FORM14P5,FMSTCA11
.
          MOVE      ANBUDGET,FORM14P5
          MULT      FMBPPC12,FORM14P5
          DIV       "100",FORM14P5
          MOVE      FORM14P5,FMSTCA12
.
          CALL      NRND0000                * do rounding
.
          MOVE      ANBUDGET,FORM12P5       * calculate the budget for the last
          SUB       FMSTCA01,FORM12P5       * period of the year
          SUB       FMSTCA02,FORM12P5
          SUB       FMSTCA03,FORM12P5
          SUB       FMSTCA04,FORM12P5
          SUB       FMSTCA05,FORM12P5
          SUB       FMSTCA06,FORM12P5
          SUB       FMSTCA07,FORM12P5
          SUB       FMSTCA08,FORM12P5
          SUB       FMSTCA09,FORM12P5
          SUB       FMSTCA10,FORM12P5
          SUB       FMSTCA11,FORM12P5
.
          BRANCH    FMLAPERS,CBUD1000
          MOVE      FORM12P5,FMSTCA12
          GOTO      CBUD2000
.
CBUD1000  SUB       FMSTCA12,FORM12P5
          MOVE      FORM12P5,FMSTCA13
          GOTO      CBUD2000
.
.
CBUD2000  CALL      DISB0000
.
CBUD9999  RETURN
.**********************************************************************
.  NRND - Round Data                                   Called By lots
.        Requires - DECPLACE
.**********************************************************************
NRND0000  MOVE      "12",F2
          ADD       FMLAPERS,F2
          MOVE      DECPLACE,F1
          ADD       ONE,F1
.
NRND1000  COMPARE   ONE,F2
          GOTO      NRND9999 IF LESS
          LOAD      XF12P5,F2,FMSTCA01,FMSTCA02,FMSTCA03,FMSTCA04:
                                FMSTCA05,FMSTCA06,FMSTCA07,FMSTCA08:
                                FMSTCA09,FMSTCA10,FMSTCA11,FMSTCA12,FMSTCA13
.
          STORE     XF12P5,F1,F12,F12P1,F12P2,F12P3,F12P4,F12P5,F12P6
          LOAD      XF12P5,F1,F12,F12P1,F12P2,F12P3,F12P4,F12P5,F12P6
.
          STORE     XF12P5,F2,FMSTCA01,FMSTCA02,FMSTCA03,FMSTCA04:
                                FMSTCA05,FMSTCA06,FMSTCA07,FMSTCA08:
                                FMSTCA09,FMSTCA10,FMSTCA11,FMSTCA12,FMSTCA13
.
          SUB       ONE,F2
          GOTO      NRND1000
.
NRND9999  RETURN
.*************************************************************************
.* KBUD - keyin a period budget               Called by PROC4000         *
.*************************************************************************
KBUD0000  MOVE      CCITEM,CVERT
          ADD       NINE,CVERT
.
....      MOVE      FIVE,DECPLACE
          CALL      AMTR0000                 * Standard keyin
          MOVE      KAMOUNT,FORM12P5
.
          STORE     FORM12P5,CCITEM,FMSTCA01,FMSTCA02,FMSTCA03,FMSTCA04:
                                    FMSTCA05,FMSTCA06,FMSTCA07,FMSTCA08:
                                    FMSTCA09,FMSTCA10,FMSTCA11,FMSTCA12:
                                    FMSTCA13
.
KBUD9999  RETURN                  
         
.*************************************************************************
.* CABD - Calculate the Period budegts from annual budget entered        *
.*        With NO Profile Code     GOTO from CBUD0000                    *
.*************************************************************************
CABD0000  MOVE      ANBUDGET,FORM13
          DIV       NOPERD,FORM13
          MOVE      ANBUDGET,FORM13P5
          MOVE      FORM13,FMSTCA01
          SUB       FORM13,FORM13P5
.
          MOVE      FORM13,FMSTCA02
          SUB       FORM13,FORM13P5
.
          MOVE      FORM13,FMSTCA03
          SUB       FORM13,FORM13P5
.
          MOVE      FORM13,FMSTCA04
          SUB       FORM13,FORM13P5
.
          MOVE      FORM13,FMSTCA05
          SUB       FORM13,FORM13P5
.
          MOVE      FORM13,FMSTCA06
          SUB       FORM13,FORM13P5
.
          MOVE      FORM13,FMSTCA07
          SUB       FORM13,FORM13P5
.
          MOVE      FORM13,FMSTCA08
          SUB       FORM13,FORM13P5
.
          MOVE      FORM13,FMSTCA09
          SUB       FORM13,FORM13P5
.
          MOVE      FORM13,FMSTCA10
          SUB       FORM13,FORM13P5
.
          MOVE      FORM13,FMSTCA11
          SUB       FORM13,FORM13P5
.
          CALL      NRND0000
.         
          BRANCH    FMLAPERS,CABD0500
          MOVE      FORM13P5,FMSTCA12
          GOTO      CABD1000
.   
CABD0500  MOVE      FORM13,FMSTCA12
          SUB       FORM13,FORM13P5
          MOVE      FORM13P5,FMSTCA13
.
CABD1000  DISPLAY   *P28:10,*EL,*V2LON,FMSTCA01:
                    *P28:11,*EL,*V2LON,FMSTCA02:
                    *P28:12,*EL,*V2LON,FMSTCA03:
                    *P28:13,*EL,*V2LON,FMSTCA04:
                    *P28:14,*EL,*V2LON,FMSTCA05:
                    *P28:15,*EL,*V2LON,FMSTCA06:
                    *P28:16,*EL,*V2LON,FMSTCA07:
                    *P28:17,*EL,*V2LON,FMSTCA08:
                    *P28:18,*EL,*V2LON,FMSTCA09:
                    *P28:19,*EL,*V2LON,FMSTCA10:
                    *P28:20,*EL,*V2LON,FMSTCA11:
                    *P28:21,*EL,*V2LON,FMSTCA12
.
          COMPARE   ZERO,FMLAPERS          * are there thirteen periods?
          GOTO      CABD9999 IF EQUAL
.
          DISPLAY   *P28:22,*V2LON,FMSTCA13
.
CABD9999  RETURN
.*************************************************************************
.* POST - Write to the budget file           Called by PROC8000          *
.*************************************************************************
POST0000  DISPLAY   *P50:24,*V2LON,*BLINKON,"*** Posting ***"
          MOVE      ONE,POSTFLAG
          PACK      KEY14,FMLALEDG,FMAMACCT
          MOVE      FMLALEDG,FMSTCULD               * set up file variables
          MOVE      FMAMACCT,FMSTCUAC
          MOVE      FMBPCODE,FMSTCUPR
.
          CALL      RAFMST1                         * is record already there?
          BRANCH    OVRCD,POST1000
.
          CALL      UPSBU000                        * yes - update it
          GOTO      POST2000
.
POST1000  MOVE      ONE,AUDTTYPE
          CALL      WAFMST00                        * Write audit record
          CALL      WRFMST1                         * no - write it
.
POST2000  COMPARE   ONE,FMCOCALC
          GOTO      POST9999 IF NOT EQUAL
.
          MOVE      FMSTCULD,LEDGER               * set up file variables
          MOVE      FMSTCUAC,ACCOUNT
          PACK      UTYPE,BUDTYPE,SP70
          PROC      FMSUA000           * set for update
.
POST9999  RETURN
.*************************************************************************
.* DBUD - Display the budget that is on file for these parametres        *
.*        Requires a valid record to have been read
.*                     called by : PROC1000                              *
.*************************************************************************
DBUD0000  PACK      KEY9,FMSTCULD,FMLACYRR,FMSTCUPR
          CALL      RDFMBP1           * get profile description
          BRANCH    OVRCD,DBUD0500
          DISPLAY   *P28:8,*V2LON,FMSTCUPR,*HOFF,*P41:8,FMBPDESC
          MOVE      ZERO,POSTFLAG
          GOTO      DBUD0600
.
DBUD0500  MOVE      ONE,POSTFLAG
.
DBUD0600  MOVE      ZERO,CCITEM
          MOVE      NINE,CVERT
.
.** Calculate the annual budget as we display the budget for each period
.
          MOVE      ZERO,ANBUDGET
.
          ADD       FMSTCA01,ANBUDGET
          ADD       FMSTCA02,ANBUDGET
          ADD       FMSTCA03,ANBUDGET
          ADD       FMSTCA04,ANBUDGET
          ADD       FMSTCA05,ANBUDGET
          ADD       FMSTCA06,ANBUDGET
          ADD       FMSTCA07,ANBUDGET
          ADD       FMSTCA08,ANBUDGET
          ADD       FMSTCA09,ANBUDGET
          ADD       FMSTCA10,ANBUDGET
          ADD       FMSTCA11,ANBUDGET
          ADD       FMSTCA12,ANBUDGET
          ADD       FMSTCA13,ANBUDGET
.
          MOVE      "9",LINENO
          MOVE      "28",COLNO
          MOVE      ANBUDGET,KAMOUNT
          CALL      AMTD0000                 * Display amount 
          CALL      DISB0000                 * Display data
.
DBUD9999  RETURN
.
.**********************************************************************
.*                           DISB0000                                 *
.*  Display values                                                    *
.**********************************************************************
DISB0000  MOVE      "28",COLNO
          MOVE      "10",LINENO
          MOVE      FMSTCA01,KAMOUNT
          CALL      AMTD0000
.
          MOVE      "11",LINENO
          MOVE      FMSTCA02,KAMOUNT
          CALL      AMTD0000
.
          MOVE      "12",LINENO
          MOVE      FMSTCA03,KAMOUNT
          CALL      AMTD0000
.
          MOVE      "13",LINENO
          MOVE      FMSTCA04,KAMOUNT
          CALL      AMTD0000
.
          MOVE      "14",LINENO
          MOVE      FMSTCA05,KAMOUNT
          CALL      AMTD0000
.
          MOVE      "15",LINENO
          MOVE      FMSTCA06,KAMOUNT
          CALL      AMTD0000
.
          MOVE      "16",LINENO
          MOVE      FMSTCA07,KAMOUNT
          CALL      AMTD0000
.
          MOVE      "17",LINENO
          MOVE      FMSTCA08,KAMOUNT
          CALL      AMTD0000
.
          MOVE      "18",LINENO
          MOVE      FMSTCA09,KAMOUNT
          CALL      AMTD0000
.
          MOVE      "19",LINENO
          MOVE      FMSTCA10,KAMOUNT
          CALL      AMTD0000
.
          MOVE      "20",LINENO
          MOVE      FMSTCA11,KAMOUNT
          CALL      AMTD0000
          MOVE      "21",LINENO
          MOVE      FMSTCA12,KAMOUNT
          CALL      AMTD0000
.
          MATCH     SP70,FMBPCODE
          GOTO      DISB1100 IF EQUAL
          DISPLAY   *P47:10,FMBPPC1,"%";
          DISPLAY   *P47:11,FMBPPC2,"%";
          DISPLAY   *P47:12,FMBPPC3,"%";
          DISPLAY   *P47:13,FMBPPC4,"%";
          DISPLAY   *P47:14,FMBPPC5,"%";
          DISPLAY   *P47:15,FMBPPC6,"%";
          DISPLAY   *P47:16,FMBPPC7,"%";
          DISPLAY   *P47:17,FMBPPC8,"%";
          DISPLAY   *P47:18,FMBPPC9,"%";
          DISPLAY   *P47:19,FMBPPC10,"%";
          DISPLAY   *P47:20,FMBPPC11,"%";
          DISPLAY   *P47:21,FMBPPC12,"%";
.
DISB1100  COMPARE   ZERO,FMLAPERS          * are there thirteen periods?
          GOTO      DISB9999 IF EQUAL
.
          MOVE      "22",LINENO
          MOVE      FMSTCA13,KAMOUNT
          CALL      AMTD0000
          MATCH     SP70,FMBPCODE
          GOTO      DISB9999 IF EQUAL
          DISPLAY   *P47:22,FMBPPC13,"%";
.
DISB9999  RETURN
.
.*************************************************************************
.* DEL - OK to delete the budget for this account? - and delete it       *
.*                     called by : PROC2000                              *
.*************************************************************************
DEL0000   CALL      DELQST           * ok to delete
          BRANCH    CEXIT,DEL1000,DEL8000,DEL9000
.
DEL1000   DISPLAY   *P50:24,*V2LON,*BLINKON,"*** Deleting ***"
          PACK      KEY14,FMSTCULD,FMSTCUAC
          CALL      DEFMST1
          MOVE      ZERO,EXIT
          MOVE      ONE,POSTFLAG   * we want to go back to account prompt
.
          MOVE      FOUR,AUDTTYPE
          CALL      WAFMST00                      * Write audit record
.
          COMPARE   ONE,FMCOCALC
          GOTO      DEL9999 IF NOT EQUAL
.
          MOVE      FMSTCULD,LEDGER               * set up file variables
          MOVE      FMSTCUAC,ACCOUNT
          PACK      UTYPE,BUDTYPE,SP70
          PROC      FMSUA000           * set for update
          GOTO      DEL9999
.
DEL8000 
          MOVE      ONE,POSTFLAG   * we want to go back to account prompt
          GOTO      DEL9999
.
DEL9000   MOVE      TWO,EXIT
.
DEL9999   RETURN
.
.**********************************************************************
.* Basic Screen Display                                               *
.**********************************************************************
SCRA0000  DISPLAY   *P1:4,*EF,*P5:4,"Budget Type",*P26:4,": ":
                    *P5:5,"Ledger",*P26:5,": ":
                    *P5:6,"Account Code",*P26:6,": "
          RETURN
. 
SCRB0000  CALL      DOPT0000
          DISPLAY   *P5:8,"Budget Profile",*P26:8,":":
                    *P5:9,"Annual Budget",*P26:9,":":
                    *P5:10,FMLC01DE,*P26:10,":":
                    *P5:11,FMLC02DE,*P26:11,":":
                    *P5:12,FMLC03DE,*P26:12,":":
                    *P5:13,FMLC04DE,*P26:13,":":
                    *P5:14,FMLC05DE,*P26:14,":":
                    *P5:15,FMLC06DE,*P26:15,":":
                    *P5:16,FMLC07DE,*P26:16,":":
                    *P5:17,FMLC08DE,*P26:17,":":
                    *P5:18,FMLC09DE,*P26:18,":":
                    *P5:19,FMLC10DE,*P26:19,":":
                    *P5:20,FMLC11DE,*P26:20,":":
                    *P5:21,FMLC12DE,*P26:21,":" 
.
          COMPARE   ZERO,FMLAPERS  * are there thirteen periods?
          GOTO      SCRB9999 IF EQUAL
.
          DISPLAY   *P5:22,FMLC13DE,*P26:22,":"
.
SCRB9999  RETURN
.
DOPT0000  MATCH     SP70,FMBPCODE
          GOTO      DOPT1000 IF NOT EQUAL
.
          DISPLAY    *P1:8,*V2LON," 1",*HOFF,". ":
                     *P1:9,*V2LON," 2",*HOFF,". ":
                    *P1:10,*V2LON," 3",*HOFF,". ":
                    *P1:11,*V2LON," 4",*HOFF,". ":
                    *P1:12,*V2LON," 5",*HOFF,". ":
                    *P1:13,*V2LON," 6",*HOFF,". ":
                    *P1:14,*V2LON," 7",*HOFF,". ":
                    *P1:15,*V2LON," 8",*HOFF,". ":
                    *P1:16,*V2LON," 9",*HOFF,". ":
                    *P1:17,*V2LON,"10",*HOFF,". ":
                    *P1:18,*V2LON,"11",*HOFF,". ":
                    *P1:19,*V2LON,"12",*HOFF,". ":
                    *P1:20,*V2LON,"13",*HOFF,". ":
                    *P1:21,*V2LON,"14",*HOFF,". ";
.
          COMPARE   ZERO,FMLAPERS  * are there thirteen periods?
          GOTO      DOPT9999 IF EQUAL
.
          DISPLAY   *P1:22,*V2LON,"15",*HOFF,". ";
          GOTO      DOPT9999
.
DOPT1000  DISPLAY   *P1:8,*V2LON," 1",*HOFF,". ":
                    *P1:9,*V2LON," 2",*HOFF,". ":
                    *P1:10,"   ":
                    *P1:11,"   ":
                    *P1:12,"   ":
                    *P1:13,"   ":
                    *P1:14,"   ":
                    *P1:15,"   ":
                    *P1:16,"   ":
                    *P1:17,"   ":
                    *P1:18,"   ":
                    *P1:19,"   ":
                    *P1:20,"   ":
                    *P1:21,"   ":
                    *P1:22,"   ";
.
DOPT9999  RETURN
.
.******************************************************************************
.* REDISPLAY ROUTINES FOR STANDARD KEYINS                                     *
.******************************************************************************
RBTYFM00  CALL      SCRA0000
          DISPLAY   *P28:5,*V2LON,FMLALEDG,*HOFF,*P41:5,FMLADESC
          DISPLAY   *P28:6,*V2LON,FMAMACCT,*HOFF,*P41:6,FMAMDESC
.
RBTYFM99  RETURN
.
RLMAFM00  CALL      SCRA0000
          DISPLAY   *P28:4,*V2LON,FMBTCODE,*HOFF,*P41:4,FMBTDESC
.
RLMAFM99  RETURN
.
RAMAFM00  CALL      SCRA0000
          DISPLAY   *P28:4,*V2LON,FMBTCODE,*HOFF,*P41:4,FMBTDESC:
                    *P28:5,*V2LON,FMLALEDG,*HOFF,*P41:5,FMLADESC:
                    *P28:6,*V2LON,FMAMACCT
.
RAMAFM99  RETURN
.
RBPFFM00  CALL      SCRA0000
          DISPLAY   *P28:4,*V2LON,FMBTCODE,*HOFF,*P41:4,FMBTDESC:
                    *P28:5,*V2LON,FMLALEDG,*HOFF,*P41:5,FMLADESC:
                    *P28:6,*V2LON,FMAMACCT,*HOFF,*P41:6,FMAMDESC
.
          CALL      SCRB0000
          COMPARE   ZERO,MODE           * do we need to display any values
          GOTO      RBPFFM99 IF EQUAL

          MOVE      "9",LINENO
          MOVE      "28",COLNO
          MOVE      ANBUDGET,KAMOUNT
          CALL      AMTD0000                 * Display amount 
          CALL      DISB0000                 * Display data
.
RBPFFM99  RETURN
.
.******************************************************************************
.* INCLUDES FOR I/O'S                                                         *
.******************************************************************************
.
          INC       FMSBPFIO/INC
.
          INC       FMSAMAKY
          INC       FMSBTYKY
          INC       FMSBPFKY
          INC       FMSLMAKY
.
          INC       FMSUPDCD           * standard update routines
          INC       FMSSTDCD           * FMS Acc. standard routines
          INC       FMSUDACD           * standard budget audit update include
