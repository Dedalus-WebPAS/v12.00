.***************************************************************************
.* System    :   Patient Legacy Visits                                     *
.* Program   :   F93PTLEG.PBL                                              *
.* Desc      :   Fix program - set LEGACY VISITS indicator.                *
.***************************************************************************
.* Date      :   12/05/2004                                                *
.* Author    :   Sylvek Litewka                                            *
.* Function  :   This program sets the LEGACY VISITS indicator (PTMXSIN5)  *
.*               on patient master extension file (patmx1af) if there are  *
.*               legacy visits for a given patient (UR number).            *
.*               To determine whether there are legacy visits for a given  *
.*               patient, the legacy visits file (legvisaf) is read. If    *
.*               there is at least one legacy visit for this patient then  *
.*               the indicator (patmx1af.PTMXSIN5) is set to ONE.          *
.*                                                                         * 
.*               NOTE: This program does not save the original patmx1af    *
.*                     file. This file should be backed-up before running  *
.*                     this program.                                       *
.*                                                                         *
.* Modifications:                                                          *
.*     V9.03.00  12/05/2004  Sylvek Litewka  CAR 49209                     *
.*               Created fix program.                                      *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATMA1FD/INC
          INC       LEGVISFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
RECNUM    FORM      8
.
PRGID     INIT      "F93PTLEG"                    
PRGNAM    INIT      "Legacy Visits - Set Legacy Visits Indicator on patmx1af"
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  MOVE      ZERO,RECNUM
.
          CALL      DISPHEAD                  * display heading
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      LEGVISA2,"legvisaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P3:4,*EF,"This program sets the Legacy Visits indicator ":
                           "(patmx1af.PTMXSIN5) if there are":
                    *P3:5,"any legacy visits for a given patient (UR number)."
.
          DISPLAY   *P5:9,*EF,*V2LON,"Warning: ",*HOFF:
                    *P14:9,"This program does not save the current version":
                    *P14:10,"of patmx1af. Please Back-Up this file before":
                    *P14:11,"running this fix program!": 
                    *P5:13,*V2LON,ZERO,*HOFF,". Exit":
                    *P5:14,*V2LON,ONE,*HOFF,". Continue"
.
OPTN0500  KEYIN     *P5:16,*EF,"Select Option : ",*DV,UNDLN1:
                    *P21:16,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P3:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P3:20,"Record : "
.
          PACK      KEY8,SP70               * Precess all patma1af records
          CALL      RDSMAST1
PROC0100  CALL      RDKMAST1
          BRANCH    OVRCD,PROC9000
          ADD       ONE,RECNUM
.
          IF        (RECNUM%500) = 0
            DISPLAY   *P18:20,*V2LON,RECNUM
          ENDIF
.
.
.         Check for Legacy Visits
.
          PACK      KEY24,PURNO,SP70
          CALL      RSLGVIS2
          CALL      RKLGVIS2
          BRANCH    OVRCD,PROC0100          * Not found, get next patient
.
          MATCH     LVIURNO,PURNO
          GOTO      PROC0100 IF NOT EQUAL   * Different UR, get next patient
. 
          MOVE      ONE,PTMXSIN5            * Set Legacy Visits flag
          CALL      UPPTMX1                 * Update master extension file
.
          GOTO      PROC0100                * Fetch next oprsemaf record
.
PROC9000  CLOSE     PATMA1A1
          CLOSE     PATMX1A1
          CLOSE     LEGVISA2
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
.******************************************************************************
. Update procedure for Patient Master Extension file
.******************************************************************************
UPPTMX1   MOVE      ZERO,OVRCD
          CLOCK     FULLDATE,CPDATE
          UNPACK    CPDATE,CMON,CDAY,CCENT,CYEAR
          PACK      CPDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CPDATE
          CLOCK     TIME,KEY12
          REP       " 0",KEY12
          PACK      PTMXCTIM,CPDATE,KEY12
          UPDATE    PATMX1A1;PURNO,PTMXRDAT,PTMXMEAL,PTMXLGA,PTMXLS:
                             PTMXLSDT,PTMXFHMD,PTMXMHU1,PTMXMHU2,PTMXMHU3:
                             PTMXECNM,PTMXECA1,PTMXECA2,PTMXECA3,PTMXECA4:
                             PTMXECPC,PTMXECPP,PTMXECBP,PTMXECRE:
                             PTMXNRNM,PTMXNRA1,PTMXNRA2,PTMXNRA3,PTMXNRA4:
                             PTMXNRPC,PTMXNRPP,PTMXNRBP,PTMXNRRE:
                             PTMX3BEX,PTMXCARD,PTMXCEXP,PTMXCXMP,PTMXFMLY:
                             PTMXCHNM,PTMXDOFR,PTMXREGP,PTMXGPPC,PTMXSPID:
                             PTMXGPPT,PTMXOSDT,PTMXOPER,PTMXUKDO,PTMXCELL:
                             PTMXADD1,PTMXADD2,PTMXADD3,PTMXADD4,PTMXPOST:
                             PTMXMCCD,PTMXSIN1,PTMXSIN2,PTMXSIN3,PTMXSIN4:
                             PTMXSIN5,PTMXCTIM,PTMXSLAC,PTMXFILN,PTMXSPAR
          RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATMA1IO/INC
          INC       LEGVISIO/INC
