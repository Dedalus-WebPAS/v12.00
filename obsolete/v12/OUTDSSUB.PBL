.*********************************************************************
.         maintain the Outpatient Discharge Screen
.         Para's  : SCRID         current screen id
.*********************************************************************
DSCMAINT  
.
DSCMNT10  CALL      BDSP0000                * display background
          BRANCH    EXIT,DSCMNT99           * no screen
.
          MATCH     SP3,OBADISCH
          IF        !@EQUAL
            CALL      DSPFIELD              * have details so display
            GOTO      DSCMNT20              * have details - change mode
          ENDIF
.
          CALL      DSCINS00                * insert DSC details
          BRANCH    ABORT,DSCMNT99          * abort entered
          BRANCH    EXIT,DSCMNT99           * cancel
.
.         change the details
.
DSCMNT20  CALL      DSCCHG00                * change OUT details
          BRANCH    ABORT,DSCMNT99          * abort entered
          BRANCH    EXIT,DSCMNT99           * cancel
.
DSCMNT99  RETURN
+
.*********************************************************************
.         insert mode 
.*********************************************************************
DSCINS00  MOVE      ZERO,YESDFLTS           * set to not to default
.
. if using NZHIS/IBA interface and we have a zero U/R and a patient on the
. National database was selected, then use defaults from the National database
.
          IF        (PTCNNHII=1) & (NZIBVNEW=1)
            MOVE      ONE,YESDFLTS
          ENDIF
.
          MOVE      ONE,ACCEPT
          PACK      KEY13,PRGID,SCRID,SP3
          CALL      RSSCSL1
DSCINS10  CALL      RKSCSL1
          BRANCH    OVRCD,DSCINS99
          MATCH     PRGID,SCSLPID
          GOTO      DSCINS99 IF NOT EQUAL   * different program id
          MATCH     SCRID,SCSLSID
          GOTO      DSCINS99 IF NOT EQUAL   * different screen id
.
          PACK      KEY15,PRGID,SCRID,SCSLROW,SCSLCOL
          CALL      RDSCPS1
          BRANCH    OVRCD,DSCINS10          * invalid details
.
          CALL      KEYNA000                * enter the correct keyin
          BRANCH    ABORT,DSCINS99          * abort
          BRANCH    EXIT,DSCINS99           * exitchar
          GOTO      DSCINS10
.
DSCINS99  RETURN
+
.*********************************************************************
.         change mode 
.*********************************************************************
DSCCHG00  MOVE      ZERO,ACCEPT
          CALL      MAINQST  
          COMPARE   ZERO,CCITEM
          GOTO      DSCCHG90 IF EQUAL       * Post
          GOTO      DSCCHG91 IF LESS        * Cancel
.
          PACK      KEY13,PRGID,SCRID,CCITEM
          CALL      RDSCSL1
          BRANCH    OVRCD,DSCCHG15          * invalid selection
.
          PACK      KEY15,PRGID,SCRID,SCSLROW,SCSLCOL
          CALL      RDSCPS1
          BRANCH    OVRCD,DSCCHG15          * invalid selection
.
          CALL      KEYNA000                * enter the correct keyin
          BRANCH    ABORT,DSCCHG99          * abort
          BRANCH    EXIT,DSCCHG91           * abort
          GOTO      DSCCHG00
.
DSCCHG15  BEEP
          GOTO      DSCCHG00
.
.         post selected
.
DSCCHG90  PROC      SCRCKMAN                * check all mand. fields entered
          BRANCH    EXIT,DSCCHG00           * need to enter more fields
.
          MOVE      ZERO,EXIT
          GOTO      DSCCHG99
.
DSCCHG91  MOVE      ONE,EXIT
.
DSCCHG99  RETURN
.
          INC       KYOUTDSC
          INC       DXOUTDSC
