.******************************************************************************
.* Include Name   : KYOPRCLI.PBL                                              *
.* Description    : Keyin of an Theatre Clinic Id from OPRCLIFD               *
.* Author         : Justin Coates           27/04/1992                        *
.*                                                                            *
.* Parameters     : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF6 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  OPCNCLSU - using clinic or surgeon                        *
.*                                                                            *
.* Returns        : OPRCLIN  - Keyed in code                                  *
.*                  PACFNAME - Description                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Spaces Input                                   *
.*                       = 2   EXITCHAR input                                 *
.*                       = 3   Nothing  input                                 *
.*                                                                            *
.* On return      : You must display the description yourself.                *
.*                                                                            *
.******************************************************************************
KYOPRCLI  MOVE      ONE,FORM1               * set ? as not entered
          MOVE      ECOL,CCOL
          MOVE      EVERT,CVERT
.
KOPCI100  MATCH     SP6,CKYIDEF6
          GOTO      KOPCI120 IF EQUAL       * no default
.
          MOVE      CKYIDEF6,OPRCLIN
          KEYIN     *PCCOL:CVERT,*DV,OPRCLIN:
                    *PCCOL:CVERT,*V2LON,*RV,OPRCLIN:
                    *PCCOL:CVERT,*DV,OPRCLIN
          GOTO      KOPCI150
.
KOPCI120  MOVE      SP6,OPRCLIN
          KEYIN     *PCCOL:CVERT,*DV,UNDLN6:
                    *PCCOL:CVERT,*V2LON,OPRCLIN:
                    *PCCOL:CVERT,*DV,OPRCLIN
.
          RESET     OPRCLIN
          GOTO      KOPCI780 IF EOS         * return hit
.
KOPCI150  PACK      OPRCLIN,OPRCLIN,SP6
          MATCH     SP6,OPRCLIN
          GOTO      KOPCI790 IF EQUAL       * spaces entered
          MATCH     EXITCHAR,OPRCLIN
          GOTO      KOPCI820 IF EQUAL       * exitchar entered
          MATCH     QUEST,OPRCLIN
          GOTO      KOPCI200 IF NOT EQUAL   * ? not entered
.
.         ? entered
.
          IF        FORM1=1
            MOVE      "0",HLEF                * save all of the screen
            CALL      GETSCR00                * save screen
            MOVE      ZERO,FORM1              * set as ? performed
          ENDIF
.
          IF        OPCNCLSU=1
            CALL      GETSCR00
            MOVE      ONE,FORM1
            CALL      PATDOCDS
            CALL      PUTSCR00                      * restore screen
            BRANCH    EXIT,KOPCI790
            MOVE      DOCCODE,OPRCLIN
            PACK      KEY6,OPRCLIN
            GOTO      KOPCI300
          ELSE
            CALL      OPRCLIDS                * display valid entries
          ENDIF
.
          IF        CNEWDS = 0
            CALL      PUTSCR00                * redisplay screen
            MOVE      ONE,FORM1               * set as ? not performed
            MOVE      ECOL,CCOL               * reset column
            MOVE      EVERT,CVERT             * reset row
          ELSE
            DISPLAY   *P1:24,"Code : ",*EL;
            MOVE      "24",CVERT
            MOVE      "8",CCOL
            MOVE      ZERO,FORM1              * set ? as entered
          ENDIF
          GOTO      KOPCI100
.
.         validate what was entered
.
KOPCI200  MOVE      OPRCLIN,KEY6
          BRANCH    OPCNCLSU,KOPCI300
.
          CALL      RDOPCLI1
          BRANCH    OVRCD,KOPCI400          * invalid code
.
          MOVE      ZERO,EXIT               * set as valid code
          MOVE      OPCLDESC,PACFNAME
          MATCH     SP6,OPCLDOCT
          GOTO      KOPCI800 IF EQUAL       * valid code
          MOVE      OPCLDOCT,KEY6
.
.         have a doctor code for the clinic so use this for description
.
KOPCI300  CALL      RDDOCT1
          BRANCH    OVRCD,KOPCI400          * invalid doctor code
.
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          GOTO      KOPCI800
.
KOPCI400  DISPLAY   *P1:24,*B,"Invalid code.  ",*EL;
          CALL      HITENTER
          BRANCH    FORM1,KOPCI100
          DISPLAY   *P1:24,"Code : ",*EL;
          GOTO      KOPCI100
.
.         nothing entered
.
KOPCI780  MOVE      SP30,OCADESC            * clear desc
          MOVE      "3",EXIT                * set exit flag
          COMPARE   ZERO,CKYIMAND
          GOTO      KOPCI800 IF EQUAL       * valid entry
.
          DISPLAY   *P1:24,*B,"Field is mandatory.  ",*EL;
          CALL      HITENTER
          BRANCH    FORM1,KOPCI100
          DISPLAY   *P1:24,"Code : ",*EL;
          GOTO      KOPCI100
.
.         spaces entered
.
KOPCI790  MOVE      SP30,PACFNAME           * clear desc
          MOVE      ONE,EXIT                * set exit flag
          COMPARE   ZERO,CKYIMAND
          GOTO      KOPCI800 IF EQUAL       * valid entry
.
          DISPLAY   *P1:24,*B,"Field is mandatory.  ",*EL;
          CALL      HITENTER
          BRANCH    FORM1,KOPCI100
          DISPLAY   *P1:24,"Code : ",*EL;
          GOTO      KOPCI100
.
.         valid entry
.
KOPCI800  IF        FORM1=0
            CALL      PUTSCR00                * redisplay screen
            MOVE      ONE,FORM1               * set as ? not entered
          ENDIF
          DISPLAY   *PECOL:EVERT,*V2LON,OPRCLIN
          GOTO      KOPCI999 
.
.         exitchar entered
.
KOPCI820  MOVE      TWO,EXIT                * EXITCHAR entered
          IF        FORM1=0
            CALL      FRESCR00                * free the screen
          ENDIF
.
KOPCI999  RETURN
+
