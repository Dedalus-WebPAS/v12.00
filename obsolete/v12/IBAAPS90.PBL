.-------------------------------------------------------
          INC       STDGENDF     * Required Include
.
VERSION   INIT      "V12.00.00"
.-------------------------------------------------------
. V9.02.00  03.05.2002  Glenn Saunders
.           Export to new release i.e. v9.02 from vf.09
.-------------------------------------------------------
. VF.00.03  05.01.2000 Charles Handaya
.           Recompiled for APSMASIO change
. VF.00.02  02.09.1999 Sandra Barcham
.           Add in Creditor Keyword Search
.           srf 631485
. VF.00.01  15.04.1999 Sandra Barcham
.           Add accounts payable batch purge
.-------------------------------------------------------
BALANCE   FORM      11.2
CON       DIM       5
DISPDR    DIM       18
DISPCR    DIM       18
ENDCRD    DIM       5
FORMAT    INIT      "(9,999,999,999.99 "
OBAL      DIM       18
PBAL      DIM       18
REPTOTC   FORM      11.2
REPTOTD   FORM      11.2
REPTOTO   FORM      11.2
RTOT0     FORM      11.2
RTOT1     FORM      11.2
RTOT2     FORM      11.2
RTOT3     FORM      11.2
RTOT4     FORM      11.2
SAVKEY20  DIM       20
STOT0     FORM      11.2
STOT1     FORM      11.2
STOT2     FORM      11.2
STOT3     FORM      11.2
STOT4     FORM      11.2
STRCRD    DIM       5
TOTALC    FORM      11.2
TOTALD    FORM      11.2
TRNAMT    FORM      10.2
.-----------------------------------------
.@@@@@ Start of Generated Code @@@@@
. NB - Code & Comments Included Before the above line 
.      will not be overwritten by the generation of a 
.      program. The STDGENDF file and VERSION must appear
.      before the line. 
.-----------------------------------------
.   Program       :   IBAAPS90
.   Function      :   Accounts Payable Purge                  
.   Generated  On :   13/12/2005
.-----------------------------------------
.=========================
.File Description Includes
.-------------------------
          INC       APSBMFFD/INC
          INC       APSCTSFD/INC
          INC       APSIMSFD/INC
          INC       APSINPFD/INC
          INC       APSMASFD/INC
          INC       APSMXRFD/INC
.=========================
.Local Variable Definition
.-------------------------
FROMCRD   DIM      40 * Scr 00 Fld     4
TOCRD     DIM      40 * Scr 00 Fld     5
PURGDATE  DIM       8 * Scr 00 Fld     6
.
ACCEPT    FORM      1
SACCEPT   FORM      1
.
PRGID     INIT      "IBAAPS90"
PRGNAM    INIT      "Accounts Payable Purge                  "
.
.--------------------------------
. MAINLINE - Controlling Logic
.--------------------------------
          TOPIC     PRGID
          MOVE      PORT,PARENTID
          CLOCK     PORT,DIM2
          MOVE      DIM2,PORT
ML0000
          CALL      INIT0000
          CALL      SC00L000
          CHAIN     PGM
.---------------------------------------
. INIT - Open Files & Display Heading
.---------------------------------------
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CCENTRY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          DISPLAY   *P56:24,"Opening scrsbga1";
          OPEN      SCRSBGA1,"scrsbgaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA1,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA2,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrslta1";
          OPEN      SCRSLTA1,"scrsltaf"
          MOVE      "   51",AUDTSECT
          MOVE      " 48",AUDTPOST
          READ      CONTROLF,AUDTSECT;*AUDTPOST,AUDTFLAG
          IF        AUDTFLAG=1
            DISPLAY   *P56:24,"Opening apsaudma";
            OPEN      APSAUDMA,"apsaudma"
          ENDIF
          DISPLAY   *P56:24,"Opening apsbmfaf";
          OPEN      APSBMFA1,"apsbmfaf"
          DISPLAY   *P56:24,"Opening apsbmfaf";
          OPEN      APSBMFA2,"apsbmfaf"
          DISPLAY   *P56:24,"Opening apsbmfaf";
          OPEN      APSBMFA3,"apsbmfaf"
          DISPLAY   *P56:24,"Opening apsctsaf";
          OPEN      APSCTSA1,"apsctsaf"
          DISPLAY   *P56:24,"Opening apsimsaf";
          OPEN      APSIMSA1,"apsimsaf"
          DISPLAY   *P56:24,"Opening apsimsaf";
          OPEN      APSIMSA2,"apsimsaf"
          DISPLAY   *P56:24,"Opening apsimsaf";
          OPEN      APSIMSA3,"apsimsaf"
          DISPLAY   *P56:24,"Opening apsimsaf";
          OPEN      APSIMSA4,"apsimsaf"
          DISPLAY   *P56:24,"Opening apsimsaf";
          OPEN      APSIMSA5,"apsimsaf"
          DISPLAY   *P56:24,"Opening apsimsaf";
          OPEN      APSIMSA6,"apsimsaf"
          DISPLAY   *P56:24,"Opening apsinpaf";
          OPEN      APSINPA1,"apsinpaf"
          DISPLAY   *P56:24,"Opening apsinpaf";
          OPEN      APSINPA2,"apsinpaf"
          DISPLAY   *P56:24,"Opening apsmasaf";
          OPEN      APSMASA1,"apsmasaf"
          DISPLAY   *P56:24,"Opening apsmasaf";
          OPEN      APSMASA2,"apsmasaf"
          DISPLAY   *P56:24,"Opening apsmasaf";
          OPEN      APSMASA3,"apsmasaf"
          DISPLAY   *P56:24,"Opening apsmxraf";
          OPEN      APSMXRA1,"apsmxraf"
          DISPLAY   *P56:24,"Opening apsmxraf";
          OPEN      APSMXRA2,"apsmxraf"
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.
.------------------------------------
. Called for Print Screen Routine 
.------------------------------------
PRTSVARS
          MATCH     "00",SCRID
          IF         @EQUAL
            CALL       PS00L000   * Accounts Payable Purge Screen      
          ENDIF
          RETURN
.--------------------------------
. Screen - Accounts Payable Purge Screen      
.--------------------------------
SC00L000
SC00L100
          MOVE      "00",SCRID
          MOVE      ZERO,PRCOL
          MOVE      ZERO,PRVERT
          MOVE      "    1",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SCPSCOL,PRCOL
            MOVE     SCPSROW,PRVERT
          ENDIF
          CALL      SBACK000 * Display Background
.
SC00L200
          CALL      CF00L000 * Clear Fields
          CALL      SETSCR00
SC00L300
          CALL      KY00L000 * Enter Key Fields
          BRANCH    EXIT,SC00L999,SC00L999
SC00L400
          CALL      WP00L000 * Load any WP Details
SC00L500
          CALL      LM00L000 * Load any Batch Details
SC00L600
          CALL      DF00L000 * Display Fields
SC00L700
          CALL      SF00L000 * Select Fields
          BRANCH    EXIT,SC00L100,SC00L100,SC00L200
          IF        EXIT=4
            MOVE      ONE,EXIT
          ENDIF
SC00L800
SC00L900
SC00L999  RETURN
.--------------------------------------------------------
. Clear Fields for Screen : Accounts Payable Purge Screen      
.-------------------------------------------------------
CF00L000
          MOVE      SP70,FROMCRD 
          MOVE      SP70,PURGDATE
CF00L999  RETURN
.--------------------------------------------------------
. Enter Key Fields for Accounts Payable Purge Screen      
.--------------------------------------------------------
KY00L000
          MOVE      ZERO,EXIT
          GOTO      KY00L999
.
KY00L900  MOVE      ONE,EXIT
KY00L999  RETURN
.---------------------------------------------------------
. Load WP files for : Accounts Payable Purge Screen      
.---------------------------------------------------------
WP00L000
          RETURN
.---------------------------------------------------------
. Load Batch Screen for : Accounts Payable Purge Screen      
.---------------------------------------------------------
LM00L000
          RETURN
.---------------------------------------------------------
. Display Fields for : Accounts Payable Purge Screen      
.---------------------------------------------------------
DF00L000
          BRANCH   ACCEPT,DF00L999
          PACK     KEY15,PRGID,SCRID,SP70
          CALL     RSSCPS1
DF00L010
          CALL     RKSCPS1
          BRANCH   OVRCD,DF00L900
          MATCH    PRGID,SCPSPID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SCRID,SCPSSID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SP70,SCPSMTY
          GOTO     DF00L010 IF NOT EQUAL
.
          CALL     PS00L000 * Display Field
          GOTO     DF00L010
DF00L900
DF00L999
          RETURN
.---------------------------------------------------------
. Display/Print Fields for : Accounts Payable Purge Screen      
.---------------------------------------------------------
PS00L000   PACK      VAR,SP70,SP70
          PACK     PRINTVAR,SP70,SP70
          MOVE     SCPSITM,FIELDNO
          BRANCH    FIELDNO,PS00L999,PS00L999,PS00L999,PS00L101:
                            PS00L102,PS00L103
          GOTO     PS00L999
.
PS00L101
          MOVE      FROMCRD,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L102
          MOVE      TOCRD,VAR        
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L103
          UNPACK    PURGDATE,CCENT,CYEAR,CMON,CDAY          
          CALL      PACDATE
          MOVE      CPCDATE,VAR
          CALL      DISPITEM
          GOTO     PS00L999
PS00L999
          RETURN
.---------------------------------------------------------
. Selections for : Accounts Payable Purge Screen      
.---------------------------------------------------------
SF00L000  PACK     KEY13,PRGID,SCRID,SP70
          CALL     RSSCSL1
SF00L001  BRANCH   ACCEPT,SF00L020
.
SF00L005  CALL     MTSTQ000
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L006,SF00L101,SF00L102,SF00L103:
                            SF00L006,SF00L104
SF00L006  BEEP
          GOTO     SF00L005
SF00L020  CALL     RKSCSL1
          BRANCH   OVRCD,SF00L090
          MATCH    PRGID,SCSLPID
          GOTO     SF00L090 IF NOT EQUAL
          MATCH    SCRID,SCSLSID
          GOTO     SF00L090 IF NOT EQUAL
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L020,SF00L020,SF00L020,SF00L103:
                            SF00L020,SF00L104
          GOTO     SF00L020
SF00L090  MOVE     ZERO,ACCEPT
          GOTO     SF00L001
.
SF00L101
          MOVE      ZERO,EXIT
          GOTO      SF00L999
.
SF00L102
            MOVE      ZERO,HLEF
            CALL      GETSCR00
          CALL      PURG0000
          MOVE      EXIT,SECTOR
          IF        SECTOR=0
            CALL      FRESCR00
            GOTO      SF00L999
          ENDIF
          IF        SECTOR=1
            CALL      FRESCR00
            MOVE      ZERO,EXIT
            GOTO      SF00L001
          ENDIF
          IF        SECTOR=2
            CALL      FRESCR00
            MOVE      ONE,EXIT
            GOTO      SF00L999
          ENDIF
          MOVE      "00",SCRID
          MOVE      ZERO,PRCOL
          MOVE      ZERO,PRVERT
          MOVE      "    1",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SCPSCOL,PRCOL
            MOVE     SCPSROW,PRVERT
          ENDIF
          CALL      PUTSCR00
          IF        SECTOR=4
            CALL      DF00L000
          ENDIF
          GOTO      SF00L001
.
SF00L103
.
.
          MOVE      "    5",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,TOCRD   
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      KFCRD000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L103
            ENDIF
            BRANCH    EXIT,SF00L103,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      FROMCRD,VAR      
          CALL      DISPITEM
.
.
          MOVE      "    5",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      TOCRD,VAR        
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L104
          CALL     SELECTED
          CALL      KEYDATE
          PACK      PURGDATE,CCENT,CYEAR,CMON,CDAY          
          UNPACK    PURGDATE,CCENT,CYEAR,CMON,CDAY          
          CALL      PACDATE
          MOVE      CPCDATE,VAR
          CALL      DISPITEM
          GOTO     SF00L001
SF00L990  MOVE      FOUR,EXIT * Exit Char Exit
SF00L999  RETURN
.@@@@@ Function Restart Point @@@@@
SETSCR00   MOVE      "Start     ",FROMCRD
           MOVE      "Finish    ",TOCRD
           MOVE      SP70,STRCRD
           MOVE      SP70,ENDCRD
           PACK      KEY4,CCC,CYY
           REP       " 0",KEY4
           MOVE      KEY4,FORM4
           SUB       ONE,FORM4
           PACK      PURGDATE,FORM4,CMM,CDD
           REP       " 0",PURGDATE
           UNPACK    PURGDATE,CCENT,CYEAR,CMON,CDAY
           MOVE      ONE,ACCEPT
           RETURN
.----------------------------------------------------------------------
. PURG0000 - Purge Creditor Transactions
.----------------------------------------------------------------------
PURG0000  MOVE      ZERO,CPAGENO
          MOVE      "99",CLNO
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:24,*EL,"Creditor Code : ";
.
          PACK      KEY5,STRCRD,SP70
          CALL      RDAPMA1
          COMPARE   ZERO,OVRCD
          GOTO      PURG2000 IF EQUAL
.
          CALL      RSAPMA1
.
PURG1000  CALL      RKAPMA1
          BRANCH    OVRCD,PURG5000
.
PURG2000  DISPLAY   *P17:24,*EL,APMACRD
.
          MATCH     APMACRD,ENDCRD
          GOTO      PURG5000 IF LESS
.
          MOVE      "99",CLNO
          CALL      SELEC000
          GOTO      PURG1000
.
PURG5000  PRINT     *N,*N,*1,"***** End of Report *****"
.
          MOVE      "99",CLNO
          PACK      KEY5,SP70
          CALL      RSAPBM1
.
PURG5100  CALL      RKAPBM1
          BRANCH    OVRCD,PURG9000
.
          PACK      KEY40,APBMBCH,SP70
          CALL      RSAPIM1
          CALL      RKAPIM1
          BRANCH    OVRCD,PURG6000
.
          MATCH     APIMBCH,APBMBCH
          GOTO      PURG5100 IF EQUAL
.
PURG6000  PACK      KEY5,APBMBCH,SP70
          CALL      DEAPBM1
          CALL      RSAPBM1
.
          COMPARE   "56",CLNO
          CALL      BHED0000 IF NOT LESS
.
          UNPACK    APBMADT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *1,APBMBCH,*10,APBMTYP,*17,CPCDATE,*30,APBMTOT
          ADD       ONE,CLNO
          GOTO      PURG5100
.
PURG9000
.
PURG9999  RETURN
.
.----------------------------------------------------------------------
. SELEC000 - Purge Transactions for a Creditor
.----------------------------------------------------------------------
SELEC000  MOVE      ZERO TO TOTALC
          MOVE      ZERO TO TOTALD
          MOVE      ZERO,SECTOR
.
          PACK      KEY40,APMACRD,SP70
          CALL      RSAPIM3
.
SELEC100  CALL      RKAPIM3
          BRANCH    OVRCD,SELEC900
.
          MATCH     APMACRD,APIMCRD
          GOTO      SELEC900 IF NOT EQUAL
.
          MOVE      SP70,APBMUDT
          PACK      KEY5,APIMBCH,SP70
          CALL      RDAPBM1
.
          MATCH     SP70,APBMUDT
          GOTO      SELEC100 IF EQUAL
.
          MATCH     APBMADT,PURGDATE
          GOTO      SELEC100 IF LESS
.
          MATCH     "0",APIMOUT
          GOTO      SELEC100 IF NOT EQUAL
          MATCH     "1",APIMUPD
          GOTO      SELEC100 IF NOT EQUAL
.
          PACK      KEY40,APIMCRD,APIMREF,APIMDOC,APIMBCH,SP70
          PACK      SAVKEY20,APIMCRD,APIMREF,SP70
.
          PACK      KEY41,ONE,APIMCRD,APIMREF,SP70
          CALL      RSAPIM6
          CALL      RKAPIM6
          BRANCH    OVRCD,SELEC150
          PACK      KEY20,APIMCRD,APIMREF,SP70
          MATCH     KEY20,SAVKEY20
          GOTO      SELEC100 IF EQUAL
.
SELEC150  CALL      RDAPIM3
          BRANCH    OVRCD,SELEC100
.
          CALL      DELET000
          PACK      KEY40,APIMCRD,APIMREF,APIMDOC,APIMBCH
          CALL      DEAPIM3
          ADD       ONE,SECTOR
          IF        SECTOR%100=1
            DISPLAY   *P25:24,SECTOR," - ",KEY40
          ENDIF
          CALL      RSAPIM3
.
          MOVE      APIMTYP,F1
          ADD       ONE,F1
          BRANCH    F1,SELEC200,SELEC300
          GOTO      SELEC100
.
SELEC200  ASSIGN    APIMTOT+APIMGST+APMAPPT,APMAPPT
          GOTO      SELEC100
.
SELEC300  ASSIGN    APMAPPT-(APIMTOT+APIMGST),APMAPPT
          GOTO      SELEC100
.
SELEC900  CALL      TOTAL000
          RETURN
.
.------------------------------------------------------------
. TOTAL000 - Print Creditor Total
.------------------------------------------------------------
.
TOTAL000  MOVE      FORMAT,DISPDR
          FEDIT     TOTALD,DISPDR
          MOVE      FORMAT,DISPCR
          FEDIT     TOTALC,DISPCR
          MOVE      TOTALC,BALANCE
          SUB       TOTALD,BALANCE
          ADD       APMAPOT,BALANCE
          ADD       BALANCE,RTOT1
          ADD       BALANCE,STOT1
.
          MOVE      FORMAT,PBAL
          FEDIT     BALANCE,PBAL
          MOVE      FORMAT,OBAL
          FEDIT     APMAPOT,OBAL
.
          COMPARE   ZERO,TOTALD
          GOTO      TOTAL100 IF NOT EQUAL
          COMPARE   ZERO,TOTALC
          GOTO      TOTAL200 IF EQUAL
.
TOTAL100  PRINT     *N,"Balance at last Purge : ",OBAL:
                    "   New Purge Balance : ",PBAL:
                    *88,"Totals ",DISPDR,SP1,DISPCR
.
TOTAL200  ADD       TOTALD,REPTOTD
          ADD       TOTALC,REPTOTC
          ADD       APMAPOT,REPTOTO
          MOVE      ZERO,TOTALD
          MOVE      ZERO,TOTALC
.
          MOVE      BALANCE,APMAPOT   * Set Outstanding at Last Purge
          CALL      UPAPMA1
.
TOTAL999  RETURN
.
.------------------------------------------------------------
. DELET000 - Delete Transaction
.------------------------------------------------------------
.
DELET000  PACK      KEY40,APIMBCH,APIMCRD,APIMREF,APIMDOC,SP70
          PACK      KEY51,APIMBCH,APIMCRD,APIMREF,APIMDOC,SP70
          CALL      RSAPIN1
.
DELET100  CALL      RKAPIN1
          BRANCH    OVRCD,DELET999
.
          PACK      KEY51,APINBCH,APINCRD,APINREF,APINDOC,APINORD,APINLIN,SP70
          MATCH     KEY40,KEY51
          GOTO      DELET999 IF NOT EQUAL
.
          ASSIGN    APINAMT+APINGST,TRNAMT
          CALL      CHKTRN00
.
          COMPARE   "56" TO CLNO
          CALL      PHED0000 IF NOT LESS
.
          MOVE      APINDES,KEY20
          UNPACK    APIMIDT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *1,APIMREF,*17,APIMDOC,*33,APIMBCH:
                    *39,CPCDATE,*50,CON,*56,APIMTER,*59,APINORD,*68,APINLIN:
                    *73,KEY20,*95,DISPDR,*114,DISPCR
          ADD       ONE TO CLNO
.
          CALL      DEAPIN1
          CALL      RSAPIN1
          GOTO      DELET100
.
DELET999  RETURN
.
.------------------------------------------------------------
. CHKTRN00 - Format Transaction
.------------------------------------------------------------
.
CHKTRN00  MOVE      APIMTYP,F1
          BRANCH    F1,CHKTRN20,CHKTRN30,CHKTRN40,CHKTRN50
.
CHKTRN10  MOVE      "Inv  ",CON
          MOVE      SP70,DISPDR
          MOVE      FORMAT,DISPCR
          FEDIT     TRNAMT,DISPCR
          ADD       TRNAMT,TOTALC
          ADD       TRNAMT,RTOT3
          ADD       TRNAMT,STOT3
          GOTO      CHKTRN99
.
CHKTRN20  MOVE      "Cred ",CON
          MOVE      SP70,DISPCR
          MOVE      FORMAT,DISPDR
          FEDIT     TRNAMT,DISPDR
          ADD       TRNAMT,TOTALD
          ADD       TRNAMT,RTOT2
          ADD       TRNAMT,STOT2
          GOTO      CHKTRN99
.
CHKTRN30  MOVE      "Paymt",CON
          MOVE      SP70,DISPCR
          MOVE      FORMAT,DISPDR
          FEDIT     TRNAMT,DISPDR
          ADD       TRNAMT,TOTALD
          ADD       TRNAMT,RTOT2
          ADD       TRNAMT,STOT2
          GOTO      CHKTRN99
.
CHKTRN40  MOVE      "Cancl",CON
          MOVE      FORMAT,DISPDR
          MOVE      FORMAT,DISPCR
          FEDIT     TRNAMT,DISPDR
          FEDIT     TRNAMT,DISPCR
          ADD       TRNAMT,TOTALD
          ADD       TRNAMT,TOTALC
          ADD       TRNAMT,RTOT2
          ADD       TRNAMT,STOT2
          ADD       TRNAMT,RTOT3
          ADD       TRNAMT,STOT3
          GOTO      CHKTRN99
.
CHKTRN50  MOVE      "Reins",CON
          MOVE      SP70,DISPDR
          MOVE      FORMAT,DISPCR
          FEDIT     TRNAMT,DISPCR
          ADD       TRNAMT,TOTALC
          ADD       TRNAMT,RTOT3
          ADD       TRNAMT,STOT3
.
CHKTRN99  RETURN
.----------------------------------------------------------------------
. PHED0000 - Print Page Heading
.----------------------------------------------------------------------
PHED0000  CALL      HEAD132
          UNPACK    PURGDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *1,"Creditor Code From : ",FROMCRD:
                    *65,"Purge Date : ",CPCDATE:
                    *N,*1,"Creditor Code To   : ",TOCRD
          CALL      UND132
          PRINT     *1,"Invoice Ref.",*17,"Document Ref.",*33,"Batch":
                    *39,"Inv. Date",*50,"Type",*56,"PP",*59,"Ord",*68,"Line":
                    *73,"Description",*95,"            Debit":
                    *114,"           Credit"
          CALL      UND132
          ADD       "3" TO CLNO
.
          PACK      APCTDESC,SP70
          PACK      KEY3,APMATYP,SP70
          CALL      RDAPCT1
          PRINT     "Creditor : ",APMACRD,*31,APMACN1:
                    *70,"Type : ",APMATYP,SP1,APCTDESC:
                    *N,*31,APMACN2
          ADD       TWO,CLNO
PHED9999  RETURN
.----------------------------------------------------------------------
. BHED0000 - Print Page Heading
.----------------------------------------------------------------------
BHED0000  CALL      HEAD80
.
          UNPACK    PURGDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          PRINT     *1,"Creditor Code From : ",FROMCRD:
                    *N,*1,"Creditor Code To   : ",TOCRD:
                    *N,*1,"Purge Date         : ",CPCDATE
          CALL      UND80
          PRINT     *1,"Batch",*9,"Type",*15,"Posting Date":
                    *34,"Batch Total"
          CALL      UND80
          ADD       FOUR TO CLNO
.
BHED9999  RETURN
.--------------------------------------------------
. Enter Range
.  where APMA    = System & File for Keyin Routines
.        CRD     = Field ID
.        KFCRD   = Call Routine Label
.        "    1" = Screen Item Number for "from" Variable
.        "    2" = Screen Item Number for "to" Variable
.        yyyyy.. = Description of Variable
.
.  Return Values
.        FROMCRD  Code & Description for Start of Range
.        TOCRD    Code & Description for End of Range
.        STRCRD   Code for Start of Range
.        ENDCRD   Code for End of Range
.--------------------------------------------------
KFCRD000   MOVE     "    4",FLDID
           CALL     GETPOSIT
           MOVE     SP70,VAR
           CALL     DISPITEM
           CALL     KAPMAA00
           BRANCH   EXIT,KFCRD100,KFCRD999
           MOVE     APMACRD,STRCRD
           PACK     FROMCRD,APMACRD,SP1,APMACN1
           GOTO     KFCRD140
.
KFCRD100   MOVE     "Start             ",FROMCRD
           MOVE     SP70,STRCRD
.
KFCRD140   MOVE     FROMCRD,VAR
           CALL     DISPITEM
.
           MOVE     "    5",FLDID
           CALL     GETPOSIT
           MOVE     SP70,VAR
           CALL     DISPITEM
           CALL     KAPMAA00
           BRANCH   EXIT,KFCRD200,KFCRD999
           MOVE     APMACRD,ENDCRD
           PACK     TOCRD,APMACRD,SP1,APMACN1
           MATCH    STRCRD,ENDCRD
           GOTO     KFCRD999 IF NOT LESS
.
           BEEP
           MOVE     "To Creditor Code Must Be > From Creditor Code - ",DISPMSG
           CALL     MESSAGE1
           MOVE     "    5",FLDID
           CALL     GETPOSIT
           MOVE     SP70,VAR
           CALL     DISPITEM
           GOTO     KFCRD000
.
KFCRD200   MOVE     "Finish             ",TOCRD
           MOVE     Z70,ENDCRD
.
KFCRD999   RETURN
.
.-------------------------------
. Includes for I/O and Keyins
.-------------------------------
          INC       APSBMFIO/INC
          INC       APSCKIKY    
          INC       APSCTSIO/INC
          INC       APSIMSIO/INC
          INC       APSINPIO/INC
          INC       APSMASIO/INC
          INC       APSMASKY    
          INC       APSMXRIO/INC
.
          INC       STDGENCD
.
