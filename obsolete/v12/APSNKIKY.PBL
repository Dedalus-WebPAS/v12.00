.------------------------------------------------------------
. Function          : Procedure for Seach of 
.                     Fill in description of Search
.
. Parameters        : CKEYSUSP    1=Show Suspended 
.                   : CKEYORDR    0=Key Word Search, 1=Alphabetic Search
.                     Fill in Other Parameters Required
.
. Includes Required :
.                     Fill in Includes Required
. Open Tables Req   :
.                     Fill in Tables that Must be Open
. Returns Fields    : OVRCD    0=Valid Record Selected, 1=No Record Selected
.                     Record Read from xxxxxxx Table
.                     Fill in Other Return Values
.
. Modifications :
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Routine to Seach for
.  a coded field via either a Key Word Index or Alphabetical
.  Index and output a ? Selection List.
.  
.  A Key Word Index Table is Defined in the following Manner
.
.  APNKfile
.  APNKidx1
.  APNKidx2    
.  
.  APNKcode     DIM       xx    * Code Field 
.  APNKword     DIM       15    * Key Word
.
.  Index 1   APNKcode, APNKword
.  Index 2   APNKword, APNKxxxx
.
.To convert this Procedure the following should be performed in this sequence
.    Global Change alphafil to name of Alphabetic Search Table   eg patdoctf
.    Global Change alphaidx to index of Alphabetic Search Table  eg PATDOCT2
.    Global Change rdalpha  to the Read on Alpha Search Index    eg RDDOCT2
.    Global Change rdkalpha to the Read Next Alpha Search Index  eg RDKDOCT2
.    Global Change rdpalpha to the Read Prev Alpha Search Index  eg RDPDOCT2
.    Global Change rdsalpha to the Read Post Alpha Search Index  eg RDSDOCT2
.    Global Change alphacod to the Code Field from the Table     eg DCODE
.    Global Change alphades to the Name Field from the Table     eg DSNAME
.    Global Change KEYzz    to the Key for the Aplha Index       eg KEY26
.    Global Change kkkkfile to name of Key Word Table            eg patdkiaf
.    Global Change kkkkidx1 to index 1 of Key Word Table         eg PATDKIA1
.    Global Change kkkkidx2 to index 2 of Key Word Table         eg PATDKIA2
.    Global Change kkkkcode to Key Word Index Code Field         eg PTDKDOC
.    Global Change kkkkword to Key Word Index Word Field         eg PTDKKWD
.    Global Change Hkklab   to Routine Label                     eg HPTDKA00
.    Global Change KEYxx    to the Key needed for Key Word Table eg KEY21
.    Global Change KEYyy    to the Key for the Code Field        eg KEY6
.    Global Change readmast to the Read on Master Table          eg RDDOCT1
.    Global Change kkkk     to the IO Call ID for the Key Words  eg PTDK
.    Global Change mastactv to the field to Indicate Inactive Code eg DRSTAT
.
.    Modify List Display to Suit
.
.    Note : vi Global change command   :%s/FromString/ToString/g
.  
.  A Alphabetic Index on a Master Table is Defined in the following Manner
.                              
.  alphacod    DIM       xx    * Code Field 
.  alphades    DIM       xx    * Name or Description Field
.
.------------------------------------------------------------
           DEFPROC   HAPNKA00
.
.-------------------------------------------------------------
. Include  : STDKWSDF.PBL
.
. Function : Variables for Standard Key Word Search
.
.-------------------------------------------------------------
F1        FORM      1
F3        FORM      3
.
DISPMSG   DIM       55       * Message display on line 24
LINE24    DIM       80       * Save Display for Line 24
ATTR24    DIM       80       * Save Attributes for Line 24
.
SELITMNO  FORM      2        * item number
SAVKEY80  DIM       80
SCCITEM   FORM      3        * Save Item Number
CSEARCH   FORM      1        * Search Flag
.
DEFAULT3  DIM       3        * 3 character default value
DEFAULT6  DIM       6        * 6 character default value
DEFAULT9  DIM       9        * 9 character default value
MANDTORY  FORM      1        * Mandatory flag (0 = No; 1 = Yes)
.
ST1       DIM       80       * save keys for "?" options
ST2       DIM       80
ST3       DIM       80
ST4       DIM       80
ST5       DIM       80
ST6       DIM       80
ST7       DIM       80
ST8       DIM       80
ST9       DIM       80
ST10      DIM       80
ST11      DIM       80
ST12      DIM       80
ST13      DIM       80
ST14      DIM       80
ST15      DIM       80
ST16      DIM       80
ST17      DIM       80
.
STDROW    FORM      2        * screen address variables
STDCOL    FORM      3
.
Z70        INIT      "~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~":
                     "~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~"
SP70      INIT      "                                   ":
                    "                                   "
UNDLN70   INIT      "___________________________________":
                    "___________________________________"
.
CKEYTYP   FORM      1     * Keyin Type 0=Keyin  1=Accept(Insert)
CSCRSIZ   FORM      1     * Full Screen Display Mode 1=yes
.
STOP      FORM      2     * Save Window Position
SBOT      FORM      2
SLEF      FORM      3
SRIG      FORM      3
.
CTOP      FORM      2    * Clipped Window Size
CBOT      FORM      2
CLEF      FORM      3
CRIG      FORM      3
.
MAXITEM   FORM      2    * Maximum Number of Items Displayed
MINITEM   FORM      2    * Minmum Number of Items Displayed
DITEM     FORM      2    * Current Item
DROW      FORM      2    * Current Item Display Position
.
STDCOL00  FORM      3    * Column Positions for Help Display
STDCOL01  FORM      3
STDCOL02  FORM      3
STDCOL03  FORM      3
STDCOL04  FORM      3
STDCOL05  FORM      3
STDCOL06  FORM      3
STDCOL07  FORM      3
STDCOL08  FORM      3
STDCOL09  FORM      3
STDCOL10  FORM      3
.
SCHFLD01  DIM       10
SCHFLD02  DIM       10
SCHFLD03  DIM       10
SCHFLD04  DIM       10
SCHFLD05  DIM       10
SCHFLD06  DIM       10
SCHFLD07  DIM       10
SCHFLD08  DIM       10
SCHFLD09  DIM       10
SCHFLD10  DIM       10
.
SCHTST01  FORM      1  * Search Test 0=Match, 1=Scan
SCHTST02  FORM      1   
SCHTST03  FORM      1 
SCHTST04  FORM      1 
SCHTST05  FORM      1 
SCHTST06  FORM      1 
SCHTST07  FORM      1 
SCHTST08  FORM      1 
SCHTST09  FORM      1 
SCHTST10  FORM      1 
.
TOTAMONT  FORM      12.2
.
F12       FORM      12
F2        FORM      2
CHECKSUS  FORM      1
KEYDES60  DIM       60
SIXTY     FORM      "60"
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
KEYWRDXX  DIM       15
SHOWSUSP  FORM      1    * Show Suspended or Inactive Codes 0=No 1=Yes
SEARCHTY  FORM      1    * Search Type  0=Key Word,  1=Alphabetic
SHMGORSC  FORM      1    * Show Miscellaneous Group or Suggested Class
FLAGDESC  DIM       127  * Description of Flag Settings
DISPVAR   DIM       127
FLAGROW   FORM      3
FLAGCOL   FORM      3
DOAGAIN   FORM      1    * Enter New Search String
.
           INC       APSNKIFD/INC
.
SAVKEY23  DIM       23       * Key Word Table Index 
SAVKEY8   DIM       8        * Code Length
.------------------------------------------------------------
. Search 
.------------------------------------------------------------
HAPNKA00  
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      APSNKIA1,"apsnkiaf"
          TRAPCLR   IO
          IF        OVRCD=1
            BEEP
            MOVE      "ERROR: Search Table Missing (apsnkiaf)- ",DISPMSG
            CALL      MESSAGE1
            GOTO      HAPNKAZZ
          ENDIF
.
          OPEN      APSNKIA2,"apsnkiaf"
          MOVE      ZERO,CSCRSIZ
.
          MOVE      "03",HTOP
          MOVE      "24",HBOT
          MOVE      "01",HLEF
          MOVE      "80",HRIG
.
HAPNKA10  CALL      KWINT000
          BRANCH    EXIT,HAPNKA99
          MOVE      "2",STDCOL01
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON,"Batch"            
          MOVE      "8",STDCOL02
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"Name",*HOFF
          MOVE      "35",STDCOL03
          DISPLAY   *PSTDCOL03:STDROW,*V2LON,*ULON,"Invoice",*HOFF
          MOVE      "46",STDCOL04
          DISPLAY   *PSTDCOL04:STDROW,*V2LON,*ULON,"  Cheque",*HOFF
          MOVE      "55",STDCOL05
          DISPLAY   *PSTDCOL05:STDROW,*V2LON,*ULON,"  Date",*HOFF
          MOVE      "65",STDCOL06
          DISPLAY   *PSTDCOL06:STDROW,*V2LON,*ULON,"         Amount",*HOFF
.
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
HAPNKA20  CALL      KEYSRH00              * Key Word Search
          BRANCH    DOAGAIN,HAPNKA10,HAPNKA20
.
          PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
.
HAPNKA99  CLOSE     APSNKIA1
          CLOSE     APSNKIA2
          GOTO      HAPNKAZZ           * End Procedure
.------------------------------------------------------------
. Perform Key Word Search
.------------------------------------------------------------
KEYSRH00  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    STDROW+1,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-13,FLAGCOL      
          DISPLAY   *PFLAGCOL:FLAGROW,*V2LON,*ULON:
                    "Enter Key Words/Invoice for Search"
          ASSIGN    STDROW+3,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-30,FLAGCOL
          KEYIN     *PFLAGCOL:FLAGROW,*V2LON,KEYWRD00
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ENDSET    KEYWRD00
          APPEND    SP70,KEYWRD00
          RESET     KEYWRD00
          CALL      GETWRD00     * Break Words into Seprate Fields
.
KEYSRH09  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          MOVE      SP70,APNKBATC 
          MOVE      SP70,APNKUNIQ 
.
          STRIP     KEYWRD01
          PACK      KEY23,KEYWRD01,SP70
          CALL      RSAPNK2 
KEYSRH10  CALL      RKAPNK2 
          BRANCH    OVRCD,KEYSRH15
.
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH10,KEYSRH15
.
          ADD       ONE,SELITMNO
          ADD       ONE,STDROW
          PACK      KEY23,APNKKWD,APNKBATC,APNKUNIQ
          STORE     KEY23,SELITMNO,ST1,ST2,ST3,ST4,ST5:
                                   ST6,ST7,ST8,ST9,ST10:
                                   ST11,ST12,ST13,ST14,ST15:
                                   ST16,ST17
.         DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,SELITMNO,DOT;
          IF        CHECKSUS<>0
            DISPLAY   *V2LON,*BLINKON,"*"
          ENDIF
          DISPLAY   *PSTDCOL01:STDROW,APNKBATC
.
          PACK      KEY8,APNKBATC,APNKUNIQ
          CALL      RDAPNS1
.
          MOVE      APNSNA1,D27
          DISPLAY   *PSTDCOL02:STDROW,D27
.
          CLEAR     KEY5
          APPEND    "~~",KEY5
          APPEND    APNKUNIQ,KEY5
          RESET     KEY5
          SUB       ONE,STDROW
          PACK      KEY40,APNKBATC,KEY5,SP70
          CALL      RSAPIM1
.
KEYSRH11  CALL      RKAPIM1
          BRANCH    OVRCD,KEYSRH14
.
          MATCH     APNKBATC,APIMBCH
          GOTO      KEYSRH14 IF NOT EQUAL
          MATCH     KEY5,APIMCRD
          GOTO      KEYSRH14 IF NOT EQUAL
.
          MOVE      APIMREF,D10
          UNPACK    APIMDOC,D7,D8
          UNPACK    APIMDDT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.                                                SRF 4752
          ASSIGN    APIMTOT+APIMGST-APIMDIS,TOTAMONT
          ADD       ONE,STDROW
          DISPLAY   *PSTDCOL03:STDROW,D10:
                    *PSTDCOL04:STDROW,D8:
                    *PSTDCOL05:STDROW,CPCDATE:
                    *PSTDCOL06:STDROW,TOTAMONT
.
          COMPARE   MAXITEM,SELITMNO
          GOTO      KEYSRH11 IF NOT EQUAL
.
KEYSRH14  COMPARE   MAXITEM,SELITMNO
          GOTO      KEYSRH10 IF NOT EQUAL
.
KEYSRH15  MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
.         DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
.
KEYSRH20  CALL      KWSEL000
          BRANCH    EXIT,KEYSRH30,KEYSRH35,KEYSRH40,KEYSRH45,KEYSRH92:
                                  KEYSRH91,KEYSRH00
.                        Next     Last     Home     End      Expand/Reduce
.                                 Exit     Search
          GOTO      KEYSRH92
.
KEYSRH30  CALL      RKAPNK2 
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH30,KEYSRH85
.
          PACK      KEY23,APNKKWD,APNKBATC,APNKUNIQ,SP70                                  
          CALL      RPAPNK2 
          MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      KEYSRH10
.
KEYSRH35  MOVE      ST1,KEY23
          CALL      RDAPNK2 
.
KEYSRH36  CALL      RPAPNK2 
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH36,KEYSRH85
          PACK      KEY23,APNKKWD,APNKBATC,APNKUNIQ,SP70                                  
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
.
KEYSRH37  CALL      RPAPNK2 
          BRANCH    OVRCD,KEYSRH38
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH37,KEYSRH38
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      KEYSRH37 IF NOT EQUAL
.
KEYSRH38  MOVE      ZERO,SELITMNO
          GOTO      KEYSRH10
.
KEYSRH40  MOVE      ST1,KEY23
          CALL      RDAPNK2 
KEYSRH41  CALL      RPAPNK2 
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH41,KEYSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      KEYSRH09
.
KEYSRH45  CALL      RKAPNK2 
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH45,KEYSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,SELITMNO
          PACK      KEY23,KEYWRD01,Z70                                          
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
          CALL      RSAPNK2 
.
KEYSRH46  CALL      RPAPNK2 
          BRANCH    OVRCD,KEYSRH47
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH46,KEYSRH47
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      KEYSRH46 IF NOT EQUAL
.
KEYSRH47  MOVE      ZERO,SELITMNO
          GOTO      KEYSRH10
.
KEYSRH85  LOAD      KEY23,SELITMNO,ST1,ST2,ST3,ST4,ST5:
                                   ST6,ST7,ST8,ST9,ST10:
                                   ST11,ST12,ST13,ST14,ST15:
                                   ST16,ST17
          UNPACK    KEY23,APNKKWD,APNKBATC,APNKUNIQ
          CALL      RDAPNK2 
.
KEYSRH90  BEEP
          GOTO      KEYSRH20
.
KEYSRH91  MOVE      SP70,KEY23
          MOVE      SP70,APNKKWD 
          MOVE      SP70,APNKBATC 
          MOVE      SP70,APNKUNIQ 
          MOVE      ONE,OVRCD
          GOTO      KEYSRH96
.
KEYSRH92  MOVE      SP70,KEY23
          MOVE      SP70,APNKBATC 
          MOVE      SP70,APNKUNIQ 
          LOAD      KEY23,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                 ST6,ST7,ST8,ST9,ST10:
                                 ST11,ST12,ST13,ST14,ST15:
                                 ST16,ST17
          UNPACK    KEY23,APNKKWD,APNKBATC,APNKUNIQ
          PACK      KEY8,APNKBATC,APNKUNIQ,SP70
          CALL      RDAPNS1
.
KEYSRH96  MOVE      ZERO,DOAGAIN
          GOTO      KEYSRH99
.
KEYSRH98  MOVE      ONE,DOAGAIN
.
KEYSRH99  RETURN
.------------------------------------------------------------
. Check Record
.------------------------------------------------------------
CHREC000  MATCH     KEYWRD01,APNKKWD
          GOTO      CHREC920 IF NOT EQUAL                                       
          IF        KEYWRDNO>1
            PACK      SAVKEY23,APNKKWD,APNKBATC,APNKUNIQ,SP70
            CALL      MCHWRD00
            BRANCH    EXIT,CHREC910
            MOVE      SAVKEY23,KEY23
            CALL      RDAPNK2
          ENDIF
.
          PACK      KEY8,APNKBATC,APNKUNIQ,SP70
          CALL      RDAPNS1
          BRANCH    OVRCD,CHREC910
          GOTO      CHREC900
.
CHREC900  MOVE      ZERO,OVRCD
          GOTO      CHREC999
CHREC910  MOVE      ONE,OVRCD
          GOTO      CHREC999
CHREC920  MOVE      TWO,OVRCD
CHREC999  RETURN
.------------------------------------------------------------
. Check for Matching Words
.------------------------------------------------------------
MCHWRD00  PACK      SAVKEY8,APNKBATC,APNKUNIQ,SP70
          MOVE      ONE,F2
          MOVE      ZERO,EXIT
.
MCHWRD10  COMPARE   KEYWRDNO,F2
          GOTO      MCHWRD99 IF EQUAL
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY23,SAVKEY8,KEYWRDXX
          CALL      RDAPNK1
          COMPARE   ZERO,OVRCD
          GOTO      MCHWRD10 IF EQUAL
.
          CALL      RKAPNK1
          BRANCH    OVRCD,MCHWRD90
          PACK      KEY8,APNKBATC,APNKUNIQ,SP70
          MATCH     SAVKEY8,KEY8 
          GOTO      MCHWRD90 IF NOT EQUAL
.
          STRIP     KEYWRDXX
          MATCH     APNKKWD,KEYWRDXX
          GOTO      MCHWRD90 IF NOT EQUAL
          GOTO      MCHWRD10
.
MCHWRD90  MOVE      ONE,EXIT
MCHWRD99  RETURN
.
.------------------------------------------------------------
. Routine   : KWINT000
.
. Function  : Initialisation for Help Screen Display.
.              - Save Screen
.              - Display Box Details
.
. Called by : All Computer Generated Help Screens
.------------------------------------------------------------
KWINT000  MOVE      ZERO,EXIT
          MOVE      ONE,CSEARCH
.
          CALL      GETSCR00
          BRANCH    EXIT,KWINT999
.
          MOVE      HTOP,CTOP
          MOVE      HLEF,CLEF
          MOVE      HBOT,CBOT
          MOVE      HRIG,CRIG
.
          ADD       TWO,CTOP       * Cxxx Clip Box Size
          ADD       ONE,CLEF
          SUB       THREE,CBOT
          SUB       ONE,CRIG
          MOVE      HBOT,MAXITEM
          SUB       HTOP,MAXITEM
          SUB       "4",MAXITEM
.
          SUSPEND
.
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          ADD       ONE,CBOT
          DISPLAY   *PHRIG:CBOT,*G34,*PHLEF:CBOT,*G33;
          HLINE     *G37,CBOT,CLEF,CRIG
          ADD       ONE,CBOT
.
          CLIP      1,1,CRIG,24    * Start Inside of Box Displays
.
          DISPLAY   *PCLEF:CBOT,"e",*V2LON,"X",*HOFF,"it, ":
                                    *V2LON,"U",*HOFF,"p, ":
                                    *V2LON,"D",*HOFF,"own, ":
                                    *V2LON,"H",*HOFF,"ome, ":
                                    *V2LON,"E",*HOFF,"nd, ":
                                    *V2LON,"S",*HOFF,"earch ";
.exit, up, down, home, end 
          SUB       TWO,CBOT
.
          MOVE      HTOP,MINITEM
          ADD       ONE,MINITEM
          MOVE      MINITEM,STDROW
.
          MOVE      HLEF,STDCOL00
          ADD       ONE,STDCOL00
.         DISPLAY   *PSTDCOL00:STDROW,*V2LON,*ULON,"Item"
.
KWINT999  RETURN
.------------------------------------------------------------
. Function    : Select Option Function for Help Screens
.
. Called by   : All Computer Generated Help Screens
.
. Exit Values : 
.               1 = Page Up
.               2 = Page Down            
.               3 = Home
.               4 = End      
.               5 = Display Screen from start
.               6 = Exit
.               7 = Search
.------------------------------------------------------------
KWSEL000  MOVE      ZERO,CCITEM
          MOVE      SP2,CCITEMD
.
          KEYIN     *P36:23,*V2LON,ANS
          GOTO      KWSEL300 IF PAGEDOWN
          GOTO      KWSEL350 IF PAGEUP
          GOTO      KWSEL400 IF HOME
          GOTO      KWSEL450 IF END
          RESET     ANS
.
KWSEL220  REP       UPPLOW,ANS
          MATCH     "X",ANS             * Exit
          GOTO      KWSEL910 IF EQUAL
          MATCH     "D",ANS             * Page Down
          GOTO      KWSEL300 IF EQUAL
          MATCH     "U",ANS             * Page Up
          GOTO      KWSEL350 IF EQUAL
          MATCH     "H",ANS             * Home
          GOTO      KWSEL400 IF EQUAL
          MATCH     "E",ANS             * End
          GOTO      KWSEL450 IF EQUAL
          MATCH     "S",ANS             * Search
          GOTO      KWSEL480 IF EQUAL
          GOTO      KWSEL900               * Invalid Entry
.
. Page Down
.
KWSEL300  MOVE      ONE,EXIT
          GOTO      KWSEL999
.
. Page Up
.
KWSEL350  MOVE      TWO,EXIT
          GOTO      KWSEL999
.
. Home
.
KWSEL400  MOVE      THREE,EXIT
          GOTO      KWSEL999
.
. End
.
KWSEL450  MOVE      FOUR,EXIT
          GOTO      KWSEL999
.
. Search
.
KWSEL480  MOVE      SEVEN,EXIT
          GOTO      KWSEL999
.
KWSEL900  BEEP
          GOTO      KWSEL000
.
KWSEL910  MOVE      SIX,EXIT
.
KWSEL999  RETURN
.------------------------------------------------------------
. Break Keywords into Words
.------------------------------------------------------------
GETWRD00  MOVE      ZERO,KEYWRDNO
          MOVE      SP70,KEYWRD01
          MOVE      SP70,KEYWRD02
          MOVE      SP70,KEYWRD03
          MOVE      SP70,KEYWRD04
          MOVE      SP70,KEYWRD05
          MOVE      SP70,KEYWRD06
          MOVE      SP70,KEYWRD07
          MOVE      SP70,KEYWRD08
          MOVE      SP70,KEYWRD09
          MOVE      SP70,KEYWRD10
GETWRD10  CMATCH    SP1,KEYWRD00             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWRD00
            GOTO      GETWRD10 IF NOT EOS
            GOTO      GETWRD99               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWRD00,SP30        * reset form pointer
          MOVE      KEY60,KEYWRD00
.
          SCAN      SP1,KEYWRD00               * Locate the first blank
          GOTO      GETWRD20 IF NOT EQUAL
          BUMP      KEYWRD00,-1              * go back 1 to end of name
          MOVEFPTR  KEYWRD00,CCITEM          * another handly form field
          RESET     KEYWRD00                 * reset the form pointer
          SETLPTR   KEYWRD00,CCITEM          * set logical length to end of name
.
.         Save this Word
.
GETWRD20  MOVE      KEYWRD00,KEY15           
          ADD       ONE,KEYWRDNO
          REP       UPPLOW,KEY15
          STORE     KEY15,KEYWRDNO,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                                   KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10 
          GOTO      GETWRD19
.
.         Check for any more words
.
GETWRD19  SETLPTR   KEYWRD00,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWRD00,CCITEM        * reset to this point
          PACK      KEY60,KEYWRD00,SP30    * reset form pointer
          MOVE      KEY60,KEYWRD00
.
          GOTO      GETWRD10
.
GETWRD99  RETURN
.------------------------------------------------------------
. Display Message on Line 24
.------------------------------------------------------------
MESSAGE1  GETVAR    LINE24,ATTR24,1,24
          RESET     DISPMSG
          STRIP     DISPMSG
          ENDSET    DISPMSG
          APPEND    SP1,DISPMSG
          RESET     DISPMSG
          DISPLAY   *P1:24,*EL,*+,DISPMSG;
          CALL      HITENTER
          PUTVAR    LINE24,ATTR24,1,24
          RETURN
.
          INC       APSNKIIO/INC
          INC       IBAOVRIO/INC
.
HAPNKAZZ  ENDPROC   
