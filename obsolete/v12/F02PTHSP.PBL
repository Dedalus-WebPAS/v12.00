.*****************************************************************************
.*    Program      : F02PTHSP                                                *
.*    Description  : Conversion pathspaf to new File layout                  *
.*                                                                           *
.*    Author       : Jill Parkinson                                          *
.*    Date         : 31/03/2011                                              *
.*    Modifications:                                                         *
.*    V10.03.01      25/02/2013 Ania P         CAR 281021                    *
.*                   Removed unnecessary modification of constants.          *
.*    V10.02.02      27/07/2011 Peter McMullen                               *
.*                   Added fields PTHSTORD, PTHSHHOS                         *
.*    V10.02.01      31/03/2011 Peter McMullen CAR 239549                    *
.*                   Populate new short name PTHSSNAM from PTHSNAME          *
.*    V10.02.00      31/03/2011 Jill Parkinson CAR 239574                    *
.*                   Created                                                 *
.*****************************************************************************
.
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
.
. Enter the first or the shortest index from the original FD and rename
. the index OLDFILE1
OLDFILE1   IFILE    SQL, FIXED=504, KEY="U1-3"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.
.PTHSHOSP  DIM       3         3       1   Hospital ID
.PTHSNAME  DIM       50       50       4   Hospital Name
.PTHSADD1  DIM       35       35      54   Hospital Address line 1
.PTHSADD2  DIM       35       35      89   Hospital Address line 2
.PTHSADD3  DIM       35       35     124   Hospital Address line 3
.PTHSADD4  DIM       35       35     159   Hospital Address line 4
.PTHSPCOD  DIM       8         8     194   Hospital Post Code
.PTHSTELE  DIM       20       20     202   Hospital Telephone number
.PTHSFAXN  DIM       20       20     222   Hospital Fax Number
.PTHSUNIT  DIM       5         5     242   Unit
.PTHSHDPC  DIM       8         8     247   Health Department code
OLDSMRPR  DIM       3         3     255   Medical Records Request Printer
.PTHSPACT  DIM       12       12     258   Posting Account
.PTHSHSNO  DIM       2         2     270   Hospital No
.PTHSPMBS  DIM       1         1     272   Print MBS item code on invoice (Y/N)
..                                         1=Yes, 0=No
.PTHSTFEE  DIM       1         1     273   Using theatre fees file (pattfees) ?
..                                         1=Yes, Space=No
.PTHSHLOC  DIM       4         4     274   Default MR home location
.PTHSRCTY  DIM       3         3     278   Default MR record type
.PTHSVOLM  DIM       2         2     281   Default MR volume
.PTHSSTAT  DIM       3         3     283   Default MR Record Status
.PTHSDCLM  DIM       3         3     286   Default Claim Code (Cat CL)
.PTHSAPPR  DIM       10       10     289   Approval Number
.PTHSUFMS  DIM       1         1     299   Using IBA FMS system
..                                         0=No, 1=Yes(IBA), 2=Yes(Other)
.PTHSABNN  DIM       11       11     300   Hospital ABN Number
.PTHSHSID  DIM       5         5     311   Health Service Identifier
.PTHSHCEN  DIM       3         3     316   Health Care Establishment Number
.PTHSPTIN  DIM       1         1     319   Patient G/L Interface
..                                         1=Yes, Space=No
.PTHSSPID  DIM       1         1     320   SACS Processing ID
..                                         (P)roduction, (T)est and (D)ebugging
.PTHSEDIP  DIM       2         2     321   Multi Hosp EDI File Prefix
.PTHSORID  DIM       8         8     323   Organisation ID (GXXNNN-C)
..                                         G - a constant prefix
..                                         XX - Alpha or Numeric
..                                         C - a Check Digit
.PTHSMORG  DIM       1        1      331   Using Morgue Management
..                                         1=Yes, 0=No
.PTHSCHOT  DIM       1        1      332   Capture HouseKeeping on Transfer
...                                         1=Yes, 0=No
.PTHSCHOD  DIM       1        1      333   Capture HouseKeeping on Discharge
..                                         1=Yes, 0=No
.PTHSHOST  DIM       1        1      334   Hospital Type
..                                         0 = Private (Non Day Facility)
..                                         1 = Public  (Non Day Facility)
..                                         2 = Private (Day Facility)
..                                         3 = Public  (Day Facility)
.PTHSUDVA  DIM       1        1      335   Using Cat CL for DVA Eclipe Claiming
..                                         1=Yes, 0=No
.PTHSLOCN  DIM       8        8      336   Eclipse Location
.PTHSERRE  DIM       3        3      344   Eclipse Remittance Release Register
.PTHSERCD  DIM       6        6      347   Eclipse Remittance Release Cash Drawer
.PTHSERCA  DIM       15       15     353   Eclipse Remittance Release Chq Accnt
.PTHSCFCD  DIM       6        6      368   Cancer Facility Code
.PTHSEMAL  DIM       80       80     374   Email address
.PTHSERSR  DIM       3        3      454   Eclipse Remittance Suspense Register
OLDSSPAR  DIM       47       47     457   Spare
..
.End of Record                      504
.
.                                     ******** copy the old FD here *******
.                                     * comment out all continuing fields and
.                                     * prefix changing fields with "OLD" -
.                                     * remove old Indexes and Redefines     
.
.
.                                     ******** copy the new FD here *******
PATHSPA1   IFILE    SQL, FIXED=504, KEY="U1-3"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.
PTHSHOSP  DIM       3         3       1   Hospital ID
PTHSNAME  DIM       50       50       4   Hospital Name
PTHSADD1  DIM       35       35      54   Hospital Address line 1
PTHSADD2  DIM       35       35      89   Hospital Address line 2
PTHSADD3  DIM       35       35     124   Hospital Address line 3
PTHSADD4  DIM       35       35     159   Hospital Address line 4
PTHSPCOD  DIM       8         8     194   Hospital Post Code
PTHSTELE  DIM       20       20     202   Hospital Telephone number
PTHSFAXN  DIM       20       20     222   Hospital Fax Number
PTHSUNIT  DIM       5         5     242   Unit
PTHSHDPC  DIM       8         8     247   Health Department code
PTHSMRPR  DIM       6         6     255   Medical Records Request Printer
PTHSPACT  DIM       12       12     261   Posting Account
PTHSHSNO  DIM       2         2     273   Hospital No
PTHSPMBS  DIM       1         1     275   Print MBS item code on invoice (Y/N)
.                                         1=Yes, 0=No
PTHSTFEE  DIM       1         1     278   Using theatre fees file (pattfees) ?
.                                         1=Yes, Space=No
PTHSHLOC  DIM       4         4     277   Default MR home location
PTHSRCTY  DIM       3         3     281   Default MR record type
PTHSVOLM  DIM       2         2     284   Default MR volume
PTHSSTAT  DIM       3         3     287   Default MR Record Status
PTHSDCLM  DIM       3         3     289   Default Claim Code (Cat CL)
PTHSAPPR  DIM       10       10     291   Approval Number
PTHSUFMS  DIM       1         1     302   Using IBA FMS system
.                                         0=No, 1=Yes(IBA), 2=Yes(Other)
PTHSABNN  DIM       11       11     303   Hospital ABN Number
PTHSHSID  DIM       5         5     314   Health Service Identifier
PTHSHCEN  DIM       3         3     317   Health Care Establishment Number
PTHSPTIN  DIM       1         1     322   Patient G/L Interface
.                                         1=Yes, Space=No
PTHSSPID  DIM       1         1     323   SACS Processing ID
.                                         (P)roduction, (T)est and (D)ebugging
PTHSEDIP  DIM       2         2     324   Multi Hosp EDI File Prefix
PTHSORID  DIM       8         8     326   Organisation ID (GXXNNN-C)
.                                         G - a constant prefix
.                                         XX - Alpha or Numeric
.                                         C - a Check Digit
PTHSMORG  DIM       1        1      334   Using Morgue Management
.                                         1=Yes, 0=No
PTHSCHOT  DIM       1        1      335   Capture HouseKeeping on Transfer
.                                         1=Yes, 0=No
PTHSCHOD  DIM       1        1      336   Capture HouseKeeping on Discharge
.                                         1=Yes, 0=No
PTHSHOST  DIM       1        1      337   Hospital Type
.                                         0 = Private (Non Day Facility)
.                                         1 = Public  (Non Day Facility)
.                                         2 = Private (Day Facility)
.                                         3 = Public  (Day Facility)
PTHSUDVA  DIM       1        1      338   Using Cat CL for DVA Eclipe Claiming
.                                         1=Yes, 0=No
PTHSLOCN  DIM       8        8      339   Eclipse Location
PTHSERRE  DIM       3        3      347   Eclipse Remittance Release Register
PTHSERCD  DIM       6        6      350   Eclipse Remittance Release Cash Drawer
PTHSERCA  DIM       15       15     356   Eclipse Remittance Release Chq Accnt
PTHSCFCD  DIM       6        6      371   Cancer Facility Code
PTHSEMAL  DIM       80       80     377   Email address
PTHSERSR  DIM       3        3      457   Eclipse Remittance Suspense Register
PTHSSNAM  DIM       10       10     460   Hospital Short name 
PTHSTORD  DIM       1        1      470   MRT Tracking Sort Order                
.                                         0 = Straight Number                    
.                                         1 = Terminal Digit                     
.                                         2 = Middle Digit                       
.                                         3 = Original Terminal (see CTYPEUR)    
.                                         4 = Original Default  (see CTYPEUR)    
PTHSHHOS  DIM       3        3      471   Default MR Home Hospital Id.(pathspaf)
PTHSSPAR  DIM       34       34     470   Spare
.
.End of Record                      504
.
. Redefine form fields
. --------------------
PTHSNUMB  FORM      2                    * Hospital No. as form field
.
.
CMDLINE   DIM       100
DIM4      DIM       4
DIM4A     DIM       4
DIM6      DIM       6
DIM6A     DIM       6
DIM60     DIM       60
DDCENT    DIM       2
.
INPFILE   DIM       8
RECNUM    FORM      8
NEWFILE   DIM       60
NEWPATH   DIM       60
OLDPATH   DIM       60
DEFPATH   DIM       60
SAVEFLG   FORM      1
SP50      INIT    "                                                  "
SP60      INIT    "                                                            "
.
PRGNAM    INIT      "CONVERSION PATHSPFD"
PRGID     INIT      "F02PTHSP"
VERSION   INIT      "V12.00.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.                                                                                
          CALL      OPTN0000                      * get option                   
          BRANCH    COPTION,MAIN1000:             * run c-isam fixit             
                            MAIN5000              * run oracle fixit             
          GOTO      MAIN9999                      * exit selected                
. 
MAIN1000  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000,MAIN0000,MAIN9999 * Yes, no, cancel
.
MAIN5000  BRANCH    COPTION,MAIN6000:             * c-isam fixit                 
                            MAIN7000              * oracle fixit                 
.
MAIN6000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
. Loop thru old file and write records to new file
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/compress saved file
          GOTO      MAIN9999
.         Running oracle fix                                                     
.                                                                                
MAIN7000  CALL      OFIX0000                      * run oracle fix    
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          
INIT9999  RETURN
+                                                                                
.*****************************************************************************   
.*                                OPTN0000             Called by: MAIN0000   *   
.*                        Get user options or exit                           *   
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *   
.*                        TRUE  (1)  Exit option selected                    *   
.*              COPTION - option selected                                    *   
.*****************************************************************************   
.                                                                                
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":                  
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)":          
                    *P1:6,*V2LON,TWO,*HOFF,". Run Fixit (Oracle only)"           
.                                                                                
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":                                
                    *P17:8,*V2LON,COPTION                                        
.                                                                                
          COMPARE   ZERO,COPTION                 * exit selected ?               
          GOTO      OPTN9500 IF EQUAL            * yes                           
.                                                                                
          BRANCH    COPTION,OPTN9000:            * run c-isam fixit              
                            OPTN9000             * run oracle fixit              
.                                                                                
          BEEP                                                                   
          GOTO      OPTN0500                                                     
.                                                                                
OPTN9000  MOVE      ZERO,EXIT                                                    
          GOTO      OPTN9999                                                     
.                                                                                
OPTN9500  MOVE      ONE,EXIT                                                     
.                                                                                
OPTN9999  RETURN                                                                 
+ 
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
PREP0000 
. open old file
          MOVE      "pathspaf",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
. Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat pathspaf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "spthspaf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat pathspaf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "spthspaf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
. remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "pathspaf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "spthspaf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "pathspaf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.      eg    APPEND    " 195 UC9-16",CMDLINE
.
          APPEND    " 504 UC1-3",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      PATHSPA1,NEWFILE
.
. set the flag to say we want to continue
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
            ELSE
              DISPLAY   "has caused an IO error - ";
            ENDIF
          ENDIF
          DISPLAY   "File will be bypassed"
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
.
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          PACK      KEY3,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
.  All necessary changes to the files field lengths, date changes
.  or any other sort of modification must be made here
.
          PACK       PTHSMRPR,SP3,OLDSMRPR,SP70
          PACK       PTHSSNAM,PTHSNAME
          PACK       PTHSTORD,SP70
          PACK       PTHSHHOS,PTHSHOSP
          PACK       PTHSSPAR,SP70
.                                               * Pack key here
          PACK      KEY3,PTHSHOSP,SP70
          CALL      WRPTHSP1                    * write to new file
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     PATHSPA1                      * close new file
          CLOSE     OLDFILE1                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
READ9999  RETURN
.
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
VALD0000  MOVE      ZERO,EXIT
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or compress save file                                *
.**********************************************************************
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "spthspaf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "compress -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not compressed or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.         Get the default directory
.**********************************************************************
DEFT0000  EXECUTE   "ldat pathspaf.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding 'pathspaf'.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
DEFT9999  RETURN
+
.
.                  ******************************    OLD IO ROUTINES
READSOLD  RESET     KEY3
          READ      OLDFILE1,KEY3;;
          RETURN
+                                                                                
.*****************************************************************************   
.*                            OFIX0000                                       *   
.*            Oracle fix to.......                                           *   
.*****************************************************************************   
.                                                                                
OFIX0000  DISPLAY   *P1:20,*EL,"Record No. :";                                   
          OPEN      PATHSPA1,"pathspaf"                                          
.                                                                                
          PACK      KEY3,SP70                                                    
          CALL      RSPTHSP1                      * position at start of file    
OFIX0500  CALL      RKPTHSP1                      * read next record             
          BRANCH    OVRCD,OFIX9000                * eof - finished               
.                                                                                
          ADD       ONE,RECNUM                    * increment record counter     
.                                                                                
          IF        (RECNUM%1000) = 0 | (RECNUM%1000) = 1                        
            DISPLAY   *P15:20,*V2LON,RECNUM                                      
          ENDIF                                                                  
.                                                                                
.>>>>>>>  Add code here to populate/update variables                             
          PACK       PTHSMRPR,SP3,PTHSMRPR,SP70
          PACK       PTHSSNAM,PTHSNAME
          PACK       PTHSTORD,SP70
          PACK       PTHSHHOS,PTHSHOSP
          PACK       PTHSSPAR,SP70
.                                                                                
          CALL      UPPTHSP1                                                     
.                                                                                
          GOTO      OFIX0500                     * get next record               
.                                                                                
OFIX9000  CLOSE     PATHSPA1                                                     
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:                                 
                    *P1:24,*EL,"Update completed.  ";                            
          CALL      HITENTER                                                     
.                                                                                
OFIX9999  RETURN                                                                 
+ 
.
.                  Copy the Positional Read (RS or RDS) from original I/O file,
.                  - change Label to READSOLD and the index name to OLDFILE1
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;PTHSHOSP,PTHSNAME,PTHSADD1,PTHSADD2:
                             PTHSADD3,PTHSADD4,PTHSPCOD,PTHSTELE:
                             PTHSFAXN,PTHSUNIT,PTHSHDPC,OLDSMRPR:
                             PTHSPACT,PTHSHSNO,PTHSPMBS,PTHSTFEE:
                             PTHSHLOC,PTHSRCTY,PTHSVOLM,PTHSSTAT:
                             PTHSDCLM,PTHSAPPR,PTHSUFMS,PTHSABNN:
                             PTHSHSID,PTHSHCEN,PTHSPTIN,PTHSSPID:
                             PTHSEDIP,PTHSORID,PTHSMORG,PTHSCHOT:
                             PTHSCHOD,PTHSHOST,PTHSUDVA,PTHSLOCN:
                             PTHSERRE,PTHSERCD,PTHSERCA,PTHSCFCD:
                             PTHSEMAL,PTHSERSR,OLDSSPAR
          GOTO      OVERCOND IF OVER
          MOVE      PTHSHSNO,PTHSNUMB
          RETURN
.
.
.                  ******************************    NEW IO ROUTINES
WRPTHSP1  MOVE      ZERO,OVRCD
          RESET     KEY3
          TRAP      OVERLOCK IF IO
          MOVE      PTHSNUMB,PTHSHSNO
          WRITE     PATHSPA1,KEY3;PTHSHOSP,PTHSNAME,PTHSADD1,PTHSADD2:
                                  PTHSADD3,PTHSADD4,PTHSPCOD,PTHSTELE:
                                  PTHSFAXN,PTHSUNIT,PTHSHDPC,PTHSMRPR:
                                  PTHSPACT,PTHSHSNO,PTHSPMBS,PTHSTFEE:
                                  PTHSHLOC,PTHSRCTY,PTHSVOLM,PTHSSTAT:
                                  PTHSDCLM,PTHSAPPR,PTHSUFMS,PTHSABNN:
                                  PTHSHSID,PTHSHCEN,PTHSPTIN,PTHSSPID:
                                  PTHSEDIP,PTHSORID,PTHSMORG,PTHSCHOT:
                                  PTHSCHOD,PTHSHOST,PTHSUDVA,PTHSLOCN:
                                  PTHSERRE,PTHSERCD,PTHSERCA,PTHSCFCD:
                                  PTHSEMAL,PTHSERSR,PTHSSNAM,PTHSTORD:
                                  PTHSHHOS,PTHSSPAR
          TRAPCLR   IO
          RETURN
.
.                                                                                
RSPTHSP1  RESET     KEY3                                                         
          READ      PATHSPA1,KEY3;;                                              
          RETURN                                                                 
. 
RKPTHSP1  MOVE      ZERO,OVRCD                                                   
          READKS    PATHSPA1;PTHSHOSP,PTHSNAME,PTHSADD1,PTHSADD2:                
                             PTHSADD3,PTHSADD4,PTHSPCOD,PTHSTELE:                
                             PTHSFAXN,PTHSUNIT,PTHSHDPC,PTHSMRPR:                
                             PTHSPACT,PTHSHSNO,PTHSPMBS,PTHSTFEE:                
                             PTHSHLOC,PTHSRCTY,PTHSVOLM,PTHSSTAT:                
                             PTHSDCLM,PTHSAPPR,PTHSUFMS,PTHSABNN:                
                             PTHSHSID,PTHSHCEN,PTHSPTIN,PTHSSPID:                
                             PTHSEDIP,PTHSORID,PTHSMORG,PTHSCHOT:                
                             PTHSCHOD,PTHSHOST,PTHSUDVA,PTHSLOCN:                
                             PTHSERRE,PTHSERCD,PTHSERCA,PTHSCFCD:                
                             PTHSEMAL,PTHSERSR,PTHSSNAM,PTHSTORD:                
                             PTHSHHOS,PTHSSPAR                                   
          GOTO      OVERCOND IF OVER                                             
          RETURN 
.
UPPTHSP1  MOVE      PTHSNUMB,PTHSHSNO                                            
          UPDATE    PATHSPA1;PTHSHOSP,PTHSNAME,PTHSADD1,PTHSADD2:                
                             PTHSADD3,PTHSADD4,PTHSPCOD,PTHSTELE:                
                             PTHSFAXN,PTHSUNIT,PTHSHDPC,PTHSMRPR:                
                             PTHSPACT,PTHSHSNO,PTHSPMBS,PTHSTFEE:                
                             PTHSHLOC,PTHSRCTY,PTHSVOLM,PTHSSTAT:                
                             PTHSDCLM,PTHSAPPR,PTHSUFMS,PTHSABNN:                
                             PTHSHSID,PTHSHCEN,PTHSPTIN,PTHSSPID:                
                             PTHSEDIP,PTHSORID,PTHSMORG,PTHSCHOT:                
                             PTHSCHOD,PTHSHOST,PTHSUDVA,PTHSLOCN:                
                             PTHSERRE,PTHSERCD,PTHSERCA,PTHSCFCD:                
                             PTHSEMAL,PTHSERSR,PTHSSNAM,PTHSTORD:                
                             PTHSHHOS,PTHSSPAR                                   
          RETURN                                                                 
. 
          INC       STD001IO
