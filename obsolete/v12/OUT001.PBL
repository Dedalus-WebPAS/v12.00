+
.******************************************************************************
.*       V5.07.01   18/11/1999  J.Tan                                         *
.*                  Mods to check the from and to date                        *
.*       V5.07.B01  18/01/1999  Nicole Harrington 507 rebound 095             *
.*                  Mods to use HEAD132                                       *
.*       V5.07.00   15/10/1998  Jim Stathopoulos                              *
.*                  507 Changes                                               *
.******************************************************************************
.*                  13/11/1995  Steve O'Gorman                                *
.*                  Modified the OUT001 routine to close the outbokaf file    *
.*                  and re-open it for each different session. This was       *
.*                  because the bulk delete of sessions was still causing the *
.*                  outpatient system to hang when accessing the outbokaf file*
.*                  PB believed that this was because of C-ISAM record locking*
.*                  and closing and re-opening should release the locks.      *
.*                  21/11/1995  Steve O'Gorman                                *
.*                  Added a Wait 5 between the close and open from above      *
.*                  This was to allow the other program to process before the *
.*                  next delete started to put lock back in the file.         *
.******************************************************************************
.
.         OUT001.PBL  -  Cancel Sessions for a clinic id in a date range.
.                        Used by IBAOUT03 - assumes all relevant files open
.                                           & parameters read in.
.                        Usage : CALL DSES0000
.          
.------------------------------------------------------------------------------
.         Mainline
.------------------------------------------------------------------------------
.
DSES0000  CALL      DSES1000       * Check that this is the correct clinic
          BRANCH    EXIT,DSES0999  * No - get next clinic
.
          DISPLAY   *P79:23,SP1;
          DISPLAY   *P79:23,SP1;
.
DSES0100  CALL      DSES2000       * Get next session
          BRANCH    EXIT,DSES0900  * No more sessions for this clinic id
.
          DISPLAY   *P79:23,SP1;
          DISPLAY   *P79:23,SP1;
.
          CALL      DSES4000       * Check session for booked/attended slots
          BRANCH    EXIT,DSES0200  * Some slots used - session not deleted
.
          DISPLAY   *P79:23,SP1;
          DISPLAY   *P79:23,SP1;
.
          CALL      DOSF0000       * Delete Open Session File
          CALL      DSES6000       * Delete all slots in this session
          MOVE      ZERO,NODEL     * Session deleted flag for audit print
          GOTO      DSES0300       * Print audit report
.
DSES0200  MOVE      ONE,NODEL      * Session not deleted flag for audit print
.
DSES0300  CALL      DSES8000       * Print audit report
          MOVE      UID,CFNAME
.
          CLOSE     OUTBOKA1               * SRF ??? To stop boka file from
          DISPLAY   *P1:23,*W5;
          PACK      CFNAME,UID,FILBOKA1    * hanging the system for bulk deletes
          OPEN      OUTBOKA1,CFNAME
.
          GOTO      DSES0100       * Get next session
.
DSES0900  KEYIN     *P1:24,*EL,"Tap ",*V2LON,"ENTER",*HOFF," to continue ... ":
                    ANS;
DSES0999  RETURN
.
.------------------------------------------------------------------------------
.         Check that this is the correct clinic & get date range
.------------------------------------------------------------------------------
.
DSES1000  MOVE      ONE,EXIT
          KEYIN     *P1:24,*EL,"Is this the correct record (",*V2LON,*DV,ANSY:
                    *HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF,") ? ",*V2LON,ANS;
          CMATCH    ANSY,ANS
          GOTO      DSES1100 IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      DSES1999 IF EQUAL
          BEEP
          GOTO      DSES1000
.
DSES1100  DISPLAY   *P1:8,*EF,"Delete Sessions from",*P33:8,"to"
.
DSES1200  UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      TWENTY2,CCOL
          MOVE      EIGHT,CVERT
          MOVE      ZERO,CDEFDTE
          MOVE      ZERO,CHIGHLT
          CALL      KEYDATE
          BRANCH    OVRCD,DSES1999
          PACK      FROMDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FROMDATE
.
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      "36",CCOL
          CALL      KEYDATE
          BRANCH    OVRCD,DSES1200
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
.
          MATCH     FROMDATE,TODATE
          GOTO      DSES1300 IF NOT LESS
          DISPLAY   *P1:24,*EL,*B,"Todate must be after Fromdate. ";
          CALL      HITENTER
          GOTO      DSES1100
.
DSES1300  MOVE      SP1,ANS
          KEYIN     *P1:24,*EL,"Ok to proceed (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH:
                    *V2LON,*DV,ANSN,*HOFF,*DV,SLASH:
                    *V2LON,*DV,ANSC,*HOFF,") ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
          CMATCH    ANSY,ANS
          GOTO      DSES1900 IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      DSES1100 IF EQUAL
          CMATCH    ANSC,ANS
          GOTO      DSES1999 IF EQUAL
          BEEP
          GOTO      DSES1300
.
DSES1900  MOVE      ZERO,EXIT
          DISPLAY   *P1:10,*ULON,"Session Date",*P15:10,"Time ":
                    *P22:10,"Status     ",*P41:10,"Session Date":
                    *P55:10,"Time ",*P62:10,"Status     "
          MOVE      TEN,CVERT
          MOVE      TWO,CCOL1
          MOVE      TEN5,CCOL2
          MOVE      TWENTY2,CCOL3
.
DSES1999  RETURN
.
.------------------------------------------------------------------------------
.         Get next session
.------------------------------------------------------------------------------
.
DSES2000  MOVE      ONE,EXIT
.
          PACK      KEY28,OMASITE,OMACLIN,FROMDATE,SP30
          CALL      RDSBOKA1
DSES2100  CALL      RDKBOKA1
          BRANCH    OVRCD,DSES2999      * No record - finished
.
          MATCH     OMASITE,OBASITE       * Correct SITE of session ?
          GOTO      DSES2999 IF NOT EQUAL
          MATCH     OMACLIN,OBACLIN       * Correct CLINIC of session ?
          GOTO      DSES2999 IF NOT EQUAL
          MATCH     OMASTART,OBASTRT      * Correct START TIME of session ?
          GOTO      DSES2100 IF NOT EQUAL
          MATCH     OBADATE,TODATE        * DATE in range ?
          GOTO      DSES2999 IF LESS
          COMPARE   OBADAY,OMADAY         * Correct DAY of session ?
          GOTO      DSES2100 IF NOT EQUAL
.
          MOVE      ZERO,EXIT             * Ok to keep on going
          MOVE      OBASITE,VSITE
          MOVE      OBACLIN,VCLIN
          MOVE      OBADATE,VDATE
          MOVE      OBASTRT,VSTRT
          MOVE      OBACTYP,VTYPE
.
DSES2999  RETURN
.
.------------------------------------------------------------------------------
.         Check session for booked/attended slots
.------------------------------------------------------------------------------
.
DSES4000  MOVE      ONE,EXIT
.
          DISPLAY   *P1:24,*EL,"Checking Date : ",*P30:24,"Slot : "
.
          PACK      KEY28,VSITE,VCLIN,VDATE,VSTRT,SP30
          CALL      RDSBOKA1
DSES4100  CALL      RDKBOKA1
          BRANCH    OVRCD,DSES4300
.
          MATCH     VSITE,OBASITE
          GOTO      DSES4300 IF NOT EQUAL
          MATCH     VCLIN,OBACLIN
          GOTO      DSES4300 IF NOT EQUAL
          MATCH     VSTRT,OBASTRT
          GOTO      DSES4300 IF NOT EQUAL
          MATCH     VDATE,OBADATE
          GOTO      DSES4300 IF NOT EQUAL
.
          UNPACK    OBADATE,DIM4,DIM2B,DIM2A
          DISPLAY   *P18:24,*V2LON,DIM2A,SLASH,DIM2B,SLASH,DIM4:
                    *P37:24,OBASLOT
.
.         Check that slot not locked by someone else
.
          MATCH     SP2,OBALOCK
          GOTO      DSES4999 IF NOT EQUAL
.
.         Gets through only if slot not booked or not used
.
          BRANCH    OBASTAT,DSES4999,DSES4100,DSES4100,DSES4999,DSES4999
          GOTO      DSES4100
.
DSES4300  MOVE      ZERO,EXIT
.
DSES4999  RETURN
.
.------------------------------------------------------------------------------
.         Delete all slots in this session
.------------------------------------------------------------------------------
.
DSES6000  DISPLAY   *P1:24,*EL,"Deleting Date : ",*P30:24,"Slot : "
.
          PACK      KEY28,VSITE,VCLIN,VDATE,VSTRT,SP30
DSES6100  CALL      RDSBOKA1
          CALL      RDKBOKA1
          BRANCH    OVRCD,DSES6999
.
          MATCH     VSITE,OBASITE
          GOTO      DSES6999 IF NOT EQUAL
          MATCH     VCLIN,OBACLIN
          GOTO      DSES6999 IF NOT EQUAL
          MATCH     VSTRT,OBASTRT
          GOTO      DSES6999 IF NOT EQUAL
          MATCH     VDATE,OBADATE
          GOTO      DSES6999 IF NOT EQUAL
.
          UNPACK    OBADATE,DIM4,DIM2B,DIM2A
          DISPLAY   *P18:24,*V2LON,DIM2A,SLASH,DIM2B,SLASH,DIM4:
                    *P37:24,OBASLOT
.
          MOVE      OBASLOT,DIM3
          PACK      KEY28,OBASITE,OBACLIN,OBADATE,OBASTRT,DIM3
          CALL      DEBOKA1
          GOTO      DSES6100
.
DSES6999  RETURN
.
.------------------------------------------------------------------------------
.         Print audit report
.------------------------------------------------------------------------------
.
DSES8000  ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      DSES8200 IF LESS
          MOVE      TEN1,CVERT
          COMPARE   "42",CCOL1
          GOTO      DSES8100 IF EQUAL
          MOVE      "42",CCOL1
          MOVE      "55",CCOL2
          MOVE      "62",CCOL3
          GOTO      DSES8200
DSES8100  MOVE      TWO,CCOL1
          MOVE      TEN5,CCOL2
          MOVE      TWENTY2,CCOL3
          DISPLAY   *P1:CVERT,*EF;
.
DSES8200  BRANCH    NODEL,DSES8300
.
.         Session Deleted
.
          UNPACK    VDATE,CCENT,CYEAR,CMON,CDAY
          DISPLAY   *PCCOL1:CVERT,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                    *PCCOL2:CVERT,VSTRT,*PCCOL3:CVERT,*V2LON,"Deleted"
          CALL      DSES8600            * Print permanent record
          GOTO      DSES8400
.
.         Session Not Deleted
.
DSES8300  UNPACK    VDATE,CCENT,CYEAR,CMON,CDAY
          DISPLAY   *PCCOL1:CVERT,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                    *PCCOL2:CVERT,VSTRT,*PCCOL3:CVERT,*V2LON,"Not Deleted"
          CALL      DSES8600            * Print permanent record
.
.         Get next date (One day after processed date)
.
DSES8400  MOVE      VDATE,FORM8
          ADD       ONE,FORM8
          MOVE      FORM8,FROMDATE
.
          RETURN
.
.         Printing subroutine
.
DSES8600  COMPARE   CLNO,SIXTY
          CALL      DSES8700 IF LESS        * Print page heading
.
          PRINT     *2,VCLIN,*13,CLIDESC,*47,VTYPE,*60,CDAY,SLASH,CMON:
                    SLASH,CCENT,CYEAR,*75,DDESC,*90,VSTRT;
.
          BRANCH    NODEL,DSES8610
.
          PRINT     *101,"Deleted"
          GOTO      DSES8620
.
DSES8610  PRINT     *101,"*** Not Deleted ***"
.
DSES8620  ADD       ONE,CLNO
          RETURN
.
.         Print page heading
.
DSES8700  MOVE      ONE,CNOUNDLN
          MOVE      " - DELETE SESSIONS",CPHDROPT
          CALL      IBACLOCK
          CALL      HEAD132
.
          PRINT     *N,*N,*1,"CLINIC ID",*13,"DESCRIPTION",*45,"CLINIC TYPE":
                       *59,"SESSION DATE",*74,"SESSION DAY",*88,"START TIME":
                       *101,"STATUS":
                    *N,*1,"---------",*13,"-----------------------------":
                       *45,"-----------",*59,"------------",*74,"-----------":
                       *88,"----------",*101,"-------------------"
.
          MOVE      FIVE,CLNO
.
          RETURN
