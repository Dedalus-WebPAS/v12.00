. **********************************************************************
. * System    :   Web Finance System                                   *
. * Program   :   FMSWEB01                                             *
. * Desc      :   Financial Transaction Details                        *
. * Mods      :                                                        *
. **********************************************************************
. V10.03.03 16/07/2013  Jill Parkinson CAR 287923
.           Added include of PATCONFD
. * V9.03.02  23.09.2003 Steve Downing  CAR 42285    (VF.11.01)        *
. *           Recompiled for FMSWEBCD                                  *
. * V9.03.01  21.10.2003 Ebon Clements  CAR 44461                      *
. *           Changed server to use the finance passcode WBSEFPCD      *
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
. * VF.01.01  26.10.2000 Sandra Barcham                                *
. *           Added account search                                     *
. **********************************************************************
. * VF.00.04  21.01.2000 Sandra Barcham                                *
. *           Display non-specified creditor name                      *
. *           srf 635491                                               *
. * VF.00.03  05.01.2000 Charles Handaya                               *
. *           Recompiled for APSMASIO change                           *
. * VF.00.02  23.12.1999 Sandra Barcham                                *
. *           Recompiled for change to FMSCCAFD.INC                    *
. *           srf 646145                                               *
. * VF.00.01  05.11.1999 B.G.Ackland                                   *
. *           Changed Table Format                                     *
. **********************************************************************
.
          INC       IBAWEBDF    
          INC       FMSWEBDF    
.
          INC       PATCONFD/INC
          INC       APSCEAFD/INC       * committed expenditure
          INC       APSCEBFD/INC       * committed expenditure
          INC       APSCIOFD/INC       * committed expenditure
          INC       APSMASFD/INC
          INC       APSNSCFD/INC
          INC       APSORDFD/INC       * committed expenditure
          INC       APSPPDFD/INC
          INC       FMSBPFFD/INC
          INC       FMSAMAFD/INC       * account
          INC       FMSBNKFD/INC       * bank rec
          INC       FMSBTYFD/INC       * budget data
          INC       FMSBUDFD/INC       * budget data
          INC       FMSCHQFD/INC       * cheque
          INC       FMSCONFD/INC       * controlf
          INC       FMSCSAFD/INC
          INC       FMSDSFFD/INC       * dissection accrual summary file
          INC       FMSFPSFD/INC       * period summary file
          INC       FMSLMAFD/INC       * ledger
          INC       FMSLMCFD/INC       * ledger financial calendar
          INC       FMSSQUFD/INC       * enquiry security
          INC       FMSSBUFD/INC       * stat budget
          INC       FMSSMAFD/INC       * stat summary
          INC       FMSTRNFD/INC       * transaction
          INC       FMSUWPFD/INC       * wp file
          INC       FMSCODFD/INC       * codes file
          INC       FMSLMBFD/INC       * report
          INC       FMSONCFD/INC       * oncost code
          INC       FMSPRCFD/INC       * print code
          INC       FMSLEVFD/INC       * level code
          INC       FMSRSFFD/INC       * account report seq
          INC       FMSSTRFD/INC       * stats transactions
          INC       FMSTCFFD/INC       * total calc
          INC       FMSDISFD/INC       * dissection
          INC       FMSRESFD/INC       * responsibility
          INC       FMSCTRFD/INC       * cash transactions
          INC       FMSCCAFD/INC       * cost centre
          INC       FMSSBAFD/INC       * subjective
          INC       FMSWASFD/INC       * Web Access Security
          INC       FMSWBCFD/INC       * Web User Comments
.
LOCKFLAG  FORM      1
NEXTYEAR  DIM       4  
LASTYEAR  DIM       4  
NEXTPERD  DIM       2
LASTPERD  DIM       2
SELNAME   DIM       12
SELPERD   FORM      2
SELYEAR   DIM       4
YEAREND   FORM      2
.
START     DIM       8                  * start date (for screen Z)
END       DIM       8                  * end date (for screen Z)
DDEBIT    DIM       15
XIM11     DIM       11
XIM12     DIM       12
XIM13     DIM       13
XIM14     DIM       14
XIM15     DIM       15
XIM16     DIM       16
XIM17     DIM       17
DCREDIT   DIM       15
FORMAT    FORM      1                  * normal account format
ZIM11     DIM       11
ZIM12     DIM       12
ZIM13     DIM       13
ZIM14     DIM       14
ZIM15     DIM       15
ZIM16     DIM       16
ZIM17     DIM       17
TRANTYPE  DIM       12                 * transaction description
TYPEDESC  DIM       13
FORMFLAG  FORM      1                  * to format or not to format !!
F17       FORM      17
F15P1     FORM      15.1
F14P2     FORM      14.2
F13P3     FORM      13.3
F11P5     FORM      11.5
F10P6     FORM      10.6
.
FORMATP0  INIT      "(999,999,999,999 "     * format variables
FORMATP1  INIT      "(9,999,999,999.9 "
FORMATP2  INIT      " (999,999,999.99 "
FORMATP3  INIT      "(999,999,999.999 "
FORMATP4  INIT      "(99,999,999.9999 "
FORMATP5  INIT      "(9,999,999.99999 "
FORMATP6  INIT      " (999,999.999999 "
.
ZORMATP0  INIT      "99999999999999999"     * format variables
ZORMATP1  INIT      "999999999999999.9"
ZORMATP2  INIT      "99999999999999.99"
ZORMATP3  INIT      "9999999999999.999"
ZORMATP4  INIT      "999999999999.9999"
ZORMATP5  INIT      "99999999999.99999"
ZORMATP6  INIT      "9999999999.999999"
.
LINKREPT  DIM       1
EDITCUR   DIM       20
EDITYTD   DIM       20
DSETNUM   DIM       1
DSETAXIS  DIM       1
DSETTYPE  DIM       10
F1P2      FORM      1.2
NEXTLINK  DIM       127
LASTLINK  DIM       127
LASTARRY  FORM      2
LEDGCODE  DIM       2
ACCTCODE  DIM       12
INCOMEFL  FORM      2
LASTCC    DIM       12
NEXTCC    DIM       12
ACCOUNTN  DIM       35
BGRED     INIT      "#"##FF0000#""
BGGREEN   INIT      "#"##008000#""
YTDBGC    DIM       9
CURBGC    DIM       9
.
SRCHLEDG  DIM       2 
SRCHACCT  DIM       12 
SRCHTYPE  DIM       1 
SRCHDESC  DIM       35
.
ACCTKEYS  DIM       14
FINCYEAR  DIM       4
FINCPERD  DIM       2
MAXVALUE  FORM      10
STEPVAL   FORM      10
INDEX2    FORM      2
JAVATMPN  DIM       30
JAVATMPF  FILE      ASCII, FIXED=250
PERNAME   DIM       12
PERNAME3  DIM       3 
.
ACTUALCP  FORM      12.2
BUDGETCP  FORM      12.2
VARIANCP  FORM      12.2
PERVARCP  FORM      6.1 
.
ACTUALYD  FORM      12.2
BUDGETYD  FORM      12.2
VARIANYD  FORM      12.2
PERVARYD  FORM      6.1 
.
CREDIT    FORM     12.2                * credit amount
DEBIT     FORM     12.2                * debit amount
DEBCRED   FORM      1                  * 1=debit, 2=credit
TRANFILE  DIM       8
PERIODNO  FORM      2                  * current period number
TCREDIT   FORM     12.2
TDEBIT    FORM     12.2
.
INCOME    INIT     "Income"
EXPEN     INIT     "Expense"
ASSET     INIT     "Asset"
LIAB      INIT     "Liability"
EQUIT     INIT     "Equity"
TOTAL     INIT     "Total"
HEAD      INIT     "Heading"
STATPOST  INIT     "Stat.Posting"
STATTOT   INIT     "Stat.Total"
.
PRGID     INIT      "FMSWEB01"
PRGNAM    INIT      "Financial Transaction Details"
VERSION   INIT      "V12.00.00"
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          MATCH     SP70,WBSEFPCD       
          IF        !@EQUAL
            MOVE      WBSEFPCD,PASSCODE     * Web security finance passcode
          ENDIF
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200
.
          MOVE      "INVALID OPTION",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      SHWTRN00    * Drill down on selected month
          GOTO      MAIN1000
.
MAIN1200  CALL      ACCSRH00
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
.------------------------------------------------------------
. Search for a Account Code
.------------------------------------------------------------
ACCSRH00  MOVE      "Account Code Search",WEBTITLE
          CALL      WEBHED00
          BRANCH    EXIT,ACCSRH99
.
          CALL      WEBBOD00
.
          CALL      WEBEND00
.
ACCSRH99  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  OPEN      CONTROLF,"controlf"
          CALL      RDFMCO50
          CALL      RDFMCO51
          CALL      RDFMCO52
          CALL      RDFMCO65
          CALL      RDFMCO66
.
          OPEN      APSCEAA1,"apsceaaf"
          OPEN      APSCEBA2,"apscebaf"
          OPEN      APSCIOA1,"apscioaf"
          OPEN      APSORDA2,"apsordaf"
          OPEN      APSMASA1,"apsmasaf"
          OPEN      APSNSCA1,"apsnscaf"
          OPEN      FMSAMAA1,"fmsamaaf"
          OPEN      FMSAMAA2,"fmsamaaf"
          OPEN      FMSAMAA3,"fmsamaaf"
          OPEN      FMSBNKA2,"fmsbnkaf"
          OPEN      FMSBPFA1,"fmsbpfaf"
          OPEN      FMSBTYA1,"fmsbtyaf"
          OPEN      FMSCCAA1,"fmsccaaf"
          OPEN      FMSCHQA1,"fmschqaf"
          OPEN      FMSCODA1,"fmscodaf"
          OPEN      FMSCSAA1,"fmscsaaf"
          OPEN      FMSDISA1,"fmsdisaf"
          OPEN      FMSLEVA1,"fmslevaf"
          OPEN      FMSLMAA1,"fmslmaaf"
          OPEN      FMSLMBA1,"fmslmbaf"
          OPEN      FMSLMCA1,"fmslmcaf"
          OPEN      FMSONCA1,"fmsoncaf"
          OPEN      FMSPRCA1,"fmsprcaf"
          OPEN      FMSRESA1,"fmsresaf"
          OPEN      FMSRSFA1,"fmsrsfaf"
          OPEN      FMSRSFA2,"fmsrsfaf"
          OPEN      FMSSBAA1,"fmssbaaf"
          OPEN      FMSSQUA1,"fmssquaf"
          OPEN      FMSTCFA1,"fmstcfaf"
          OPEN      FMSTCFA2,"fmstcfaf"
          OPEN      FMSUWPA1,"fmsuwpaf"
          OPEN      FMSWASA1,"fmswasaf"
          OPEN      FMSWBCA1,"fmswbcaf"
.
          MOVE      SP70,TRANFILE
.
          RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      SP70,ACCTKEYS
          MOVE      SP70,FINCYEAR
          MOVE      SP70,FINCPERD
          MOVE      SP70,SRCHLEDG
          MOVE      SP70,SRCHACCT
          MOVE      SP70,SRCHTYPE
          MOVE      SP70,SRCHDESC
          MOVE      "000",TEMPLATE
          MOVE      "10",NORECORD
          MOVE      "0",STARTNUM
          RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchacct=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SRCHACCT
            REP       "a&",SRCHACCT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchledg=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SRCHLEDG
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SRCHTYPE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "srchdesc=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SRCHDESC
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "acctkeys=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ACCTKEYS
            REP       "a&",ACCTKEYS
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fincyear=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FINCYEAR
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fincperd=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FINCPERD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "norecord=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,NORECORD
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "startnum=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,STARTNUM
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.------------------------------------------------------------
. Create Links to Next and Last Period
.------------------------------------------------------------
NXTLST00  MOVE      PERIODNO,F2
          MOVE      CURYEAR,F4
          SUB       ONE,F2
          IF        F2=0
            MOVE      "12",F2
            SUB       ONE,F4
          ENDIF
          MOVE      F2,LASTPERD
          REP       " 0",LASTPERD
          MOVE      F4,LASTYEAR
          REP       " 0",LASTYEAR
.
          MOVE      PERIODNO,F2
          MOVE      CURYEAR,F4
          ADD       ONE,F2
          IF        F2=13
            MOVE      "1",F2
            ADD       ONE,F4
          ENDIF
          MOVE      F2,NEXTPERD
          REP       " 0",NEXTPERD
          MOVE      F4,NEXTYEAR
          REP       " 0",NEXTYEAR
.
NXTLST99  RETURN
.------------------------------------------------------------
. Account Account Details
.------------------------------------------------------------
SHWTRN00  PACK      KEY14,ACCTKEYS,SP70
          CALL      RDFMAM1
          BRANCH    OVRCD,SHWTRN80
.
          MOVE      FMAMDESC,WEBTITLE
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,SHWTRN99
.
          MOVE      ACCTKEYS,FMLALEDG
          CALL      SETPER00   * Set Period and Year
.
          MOVE      FINCPERD,F2
          LOAD      PERNAME,F2,FMLC01DE,FMLC02DE,FMLC03DE,FMLC04DE,FMLC05DE:
                               FMLC06DE,FMLC07DE,FMLC08DE,FMLC09DE,FMLC10DE:
                               FMLC11DE,FMLC12DE
          LOAD      KEY8,PERIODNO,FMLC01TO,FMLC02TO,FMLC03TO,FMLC04TO:
                                  FMLC05TO,FMLC06TO,FMLC07TO,FMLC08TO:
                                  FMLC09TO,FMLC10TO,FMLC11TO,FMLC12TO,FMLC13TO
          LOAD      LOCKFLAG,F2,FMLC01IN,FMLC02IN,FMLC03IN,FMLC04IN,FMLC05IN:
                                FMLC06IN,FMLC07IN,FMLC08IN,FMLC09IN,FMLC10IN:
                                FMLC11IN,FMLC12IN
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          CALL      NXTLST00
.
          CALL      WEBBOD00
          GOTO      SHWTRN90
.
SHWTRN80  MOVE      "INVALID ACCOUNT",WEBTITLE
          CALL      WEBERR00
          GOTO      SHWTRN99
.
SHWTRN90  CALL      WEBEND00
.
SHWTRN99  RETURN
.------------------------------------------------------------
. Process Fields in Template
.------------------------------------------------------------
PROFLD00  BRANCH   REPORTNO,PROFLD10,PROFLD20
.
PROFLD10  CALL     SUMP0000
          GOTO     PROFLD99
.
PROFLD20  CALL     SRCH0000
          GOTO     PROFLD99
.
PROFLD99  RETURN
.------------------------------------------------------------
. Output Account Search 
.------------------------------------------------------------
SRCH0000  BRANCH    FLDITMNO,SRCH0100,SRCH0200,SRCH0300,SRCH0400:
                             SRCH0500,SRCH0600
          GOTO      SRCH9999
.
SRCH0100  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          REP       " +",SRCHACCT
          REP       " +",SRCHDESC
          WRITE     HTMLFILE;"fmsweb01.pbl?reportno=2":
                                 "&template=",TEMPLATE:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&srchledg=",SRCHLEDG:
                                 "&srchtype=",SRCHTYPE:
                                 "&srchacct=",SRCHACCT:
                                 "&srchdesc=",SRCHDESC;
          REP       "+ ",SRCHACCT
          REP       "+ ",SRCHDESC
          GOTO      SRCH9999
.
SRCH0200  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          REP       " +",SRCHACCT
          REP       " +",SRCHDESC
          WRITE     HTMLFILE;"fmsweb01.pbl?reportno=2":
                                 "&template=",TEMPLATE:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&srchledg=",SRCHLEDG:
                                 "&srchtype=",SRCHTYPE:
                                 "&srchacct=",SRCHACCT:
                                 "&srchdesc=",SRCHDESC;
          REP       "+ ",SRCHACCT
          REP       "+ ",SRCHDESC
          GOTO      SRCH9999
.
SRCH0300  CALL      LEDOPT00
          GOTO      SRCH9999
.
SRCH0400  CALL      ACTTYP00
          GOTO      SRCH9999
.
SRCH0500  CALL      ACTDES00
          GOTO      SRCH9999
.
SRCH0600  CALL      ASRCH000
          GOTO      SRCH9999
.
SRCH9999  RETURN
.
LEDOPT00  PACK      KEY2,SP70
          CALL      RSFMLA1
LEDOPT10  CALL      RKFMLA1
          BRANCH    OVRCD,LEDOPT99
          MATCH     FMLALEDG,SRCHLEDG
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",FMLALEDG," selected>":
                               FMLALEDG,SP1,FMLADESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",FMLALEDG,">":
                               FMLALEDG,SP1,FMLADESC,"</option>"
          ENDIF
          GOTO      LEDOPT10
LEDOPT99  RETURN
.
ACTTYP00  MOVE      ZERO,F1
.
ACTTYP10  ADD       ONE,F1
          LOAD      TYPEDESC,F1,INCOME,EXPEN,ASSET,LIAB,EQUIT,TOTAL,HEAD:
                                STATPOST,STATTOT
          MOVE      F1,DIM1
          MATCH     SRCHTYPE,DIM1
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=#"",F1,"#" selected>":
                             TYPEDESC,"</option>" 
          ELSE
            WRITE     HTMLFILE;"<option value=#"",F1,"#">":
                             TYPEDESC,"</option>" 
          ENDIF
.
          MATCH    "9",DIM1
          GOTO     ACTTYP10 IF NOT EQUAL
.
ACTTYP99  RETURN
.
ACTDES00
ACTDES99  RETURN
.
ASRCH000  MOVE      ZERO,RECNUMB 
          MOVE      ZERO,DISNUMB 
          PACK      KEY50,SRCHLEDG,SRCHTYPE,SRCHDESC,SP70
          CALL      RSFMAM3
.
ASRCH100  CALL      RKFMAM3
          BRANCH    OVRCD,ASRCH999
          MATCH     SRCHLEDG,FMAMLEDG
          GOTO      ASRCH999 IF NOT EQUAL
          MATCH     SRCHTYPE,FMAMTYPE
          GOTO      ASRCH999 IF NOT EQUAL
.
          ADD       ONE,RECNUMB 
          IF        STARTNUM>=RECNUMB
            GOTO      ASRCH100
          ENDIF
.
          MOVE      FMAMTYPE,F1
          LOAD      TYPEDESC,F1,INCOME,EXPEN,ASSET,LIAB,EQUIT,TOTAL,HEAD:
                                STATPOST,STATTOT
.
          WRITE     HTMLFILE;"<tr><td align=center>",FMAMLEDG,"</td>":
                                 "<td align=center>",FMAMACCT,"</td>":
                                 "<td align=center>",TYPEDESC,"</td>":
                                 "<td>",FMAMDESC,"</td></tr>" 
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      ASRCH100 IF NOT EQUAL
.
ASRCH999  RETURN
.
.------------------------------------------------------------
. Output Transaction Fields
.------------------------------------------------------------
SUMP0000  BRANCH    FLDITMNO,SUMP0100,SUMP0200,SUMP0300,SUMP0400,SUMP0500:
                             SUMP0600,SUMP0700,SUMP0800,SUMP0900,SUMP1000:
                             SUMP1100,SUMP1200,SUMP1300,SUMP1400
          GOTO      SUMP9999
.
SUMP0100  CALL      DATZ0000    * Table of Accounts
          GOTO      SUMP9999
.
SUMP0200  WRITE     HTMLFILE;FMAMLEDG;
          GOTO      SUMP9999
.
SUMP0300  WRITE     HTMLFILE;FMAMACCT;
          GOTO      SUMP9999
.
SUMP0400  WRITE     HTMLFILE;FMAMDESC;
          GOTO      SUMP9999
.
SUMP0500  MOVE      FMAMTYPE,F1
          BRANCH    F1,SUMP0510,SUMP0520,SUMP0530,SUMP0540,SUMP0550:
                       SUMP0560,SUMP0570,SUMP0580,SUMP0590
SUMP0510  WRITE     HTMLFILE;"Income";
          GOTO      SUMP9999
SUMP0520  WRITE     HTMLFILE;"Expense";
          GOTO      SUMP9999
SUMP0530  WRITE     HTMLFILE;"Asset";
          GOTO      SUMP9999
SUMP0540  WRITE     HTMLFILE;"Liability";
          GOTO      SUMP9999
SUMP0550  WRITE     HTMLFILE;"Equity";
          GOTO      SUMP9999
SUMP0560  WRITE     HTMLFILE;"Total";
          GOTO      SUMP9999
SUMP0570  WRITE     HTMLFILE;"Heading";
          GOTO      SUMP9999
SUMP0580  WRITE     HTMLFILE;"Statistic";
          GOTO      SUMP9999
SUMP0590  WRITE     HTMLFILE;"Calculated Statistic";
          GOTO      SUMP9999
.
SUMP0600  IF        LOCKFLAG=0
            WRITE     HTMLFILE;"Interim Results";
          ENDIF
          GOTO      SUMP9999 
.
SUMP0700  WRITE     HTMLFILE;LASTPERD;
          GOTO      SUMP9999 
.
SUMP0800  WRITE     HTMLFILE;LASTYEAR;
          GOTO      SUMP9999 
.
SUMP0900  WRITE     HTMLFILE;NEXTPERD;
          GOTO      SUMP9999 
.
SUMP1000  WRITE     HTMLFILE;NEXTYEAR;
          GOTO      SUMP9999 
.
SUMP1100  WRITE     HTMLFILE;PERIODNO;
          GOTO      SUMP9999 
.
SUMP1200  WRITE     HTMLFILE;CURYEAR;
          GOTO      SUMP9999 
.
SUMP1300  CALL      SELPER00     * Select Period Options
          GOTO      SUMP9999 
.
SUMP1400  REP       " +",ACCTKEYS
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
          MOVE      PERIODNO,KEY2
          REP       " 0",KEY2
          WRITE     HTMLFILE;LINKPRG,".pbl?reportno=",LINKREP:
                                 "&template=",LINKTEM:
                                 "&fincyear=",CURYEAR:
                                 "&fincperd=",KEY2:
                                 "&acctkeys=",ACCTKEYS;
          REP       "+ ",ACCTKEYS
          GOTO      SUMP9999
.
SUMP9999  RETURN
.------------------------------------------------------------
. Output Heading
.------------------------------------------------------------
HEDROW00  MOVE      FMAMTYPE,F1
          IF        F1<6
            WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell>Date</td>":
                               "<td class=HeadingCell>Description</td>":
                               "<td class=HeadingCell>Reference</td>": 
                               "<td class=HeadingCell>Tr. Type</td>":
                               "<td class=HeadingCell>Resp.</td>":
                               "<td class=HeadingCell>Dissection</td>":
                               "<td class=HeadingCell align=right>Debit</td>":
                               "<td class=HeadingCell align=right>Credit</td>":
                               "</tr>"
          ELSE
            WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell>Date</td>":
                               "<td class=HeadingCell>Batch</tD>":
                               "<td class=HeadingCell>Reference</td>":
                               "<td class=HeadingCell align=right>Amount</td>":
                               "</tr>"
          ENDIF
          RETURN
.******************************************************************************
.* DATZ - display Screen Z data                  Called By MQSZ,SELZ          *
.*       Requires - PAGENO   (page to display)                                *
.*                  MAXITEMZ (maximum no. of items per page + 1)              *
.*        Returns - ITEMNUM  (number of data items displayed)                 *
.*                  ST1..ST17 (save keys for displayed data)                  *
.*                  MORE     (1=more data)                                    *
.*        Uses    - CURPAGE  (page counter)                                   *
.*                  LINENO   (line to display data)                           *
.******************************************************************************
DATZ0000  CALL      HEDROW00
          MOVE      "0",TDEBIT
          MOVE      "0",TCREDIT
          MATCH     "8",FMAMTYPE
          GOTO      DATZ0005 IF NOT LESS    * stat type ?
.
.---- open normal transactions file ----
.
          MOVE      FMST,KEY4
          LOAD      KEY4,FMCOUST,FMSR
          PACK      FILENAME,KEY4,CURYEAR         * open file (if possible)
          REP       " 0",FILENAME
          MATCH     FILENAME,TRANFILE
          GOTO      DATZ0010 IF EQUAL            * file already open ?
.
          MOVE      FILENAME,TRANFILE             * save filename
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FMSTRNA1,FILENAME             * open current year files
          TRAPCLR   IO
          BRANCH    OVRCD,DATZ8500               * data not found ?
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FMSTRNA2,FILENAME             * open current year files
          TRAPCLR   IO
          BRANCH    OVRCD,DATZ8500               * data not found ?
          GOTO      DATZ0010
.
.---- open stat transactions file ----
.
DATZ0005  MOVE      FMSA,KEY4
          LOAD      KEY4,FMCOUST,FMSZ
          PACK      FILENAME,KEY4,CURYEAR         * open file (if possible)
          REP       " 0",FILENAME
          MATCH     FILENAME,TRANFILE
          GOTO      DATZ0010 IF EQUAL            * file already open ?
.
          MOVE      FILENAME,TRANFILE             * save filename
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FMSSTRA1,FILENAME             * open current year files
          TRAP      OVERCOND IF IO
          OPEN      FMSSTRA2,FILENAME             * open current year files
          TRAPCLR   IO
          BRANCH    OVRCD,DATZ8500               * data not found ?
.
DATZ0010  LOAD      START,PERIODNO,FMLC01FR,FMLC02FR,FMLC03FR,FMLC04FR:
                                   FMLC05FR,FMLC06FR,FMLC07FR,FMLC08FR:
                                   FMLC09FR,FMLC10FR,FMLC11FR,FMLC12FR,FMLC13FR
          LOAD      END,PERIODNO,FMLC01TO,FMLC02TO,FMLC03TO,FMLC04TO:
                                 FMLC05TO,FMLC06TO,FMLC07TO,FMLC08TO:
                                 FMLC09TO,FMLC10TO,FMLC11TO,FMLC12TO,FMLC13TO
          PACK      KEY14,ACCTKEYS,SP70
          PACK      KEY32,KEY14,START,SP70
          PACK      KEY30,KEY14,START,SP70
          MATCH     "8",FMAMTYPE
          GOTO      DATZ1400 IF LESS             * stats record ?
.
.---- stat rec ----
.
          CALL      RSFMSR2                      * go to start of records
DATZ1000  CALL      RKFMSR2                      * get next record
          BRANCH    OVRCD,DATZ9999               * no more records ?
          PACK      KEY14,FMSRLEDG,FMSRACCT,SP70
          MATCH     KEY14,KEY32
          GOTO      DATZ9999 IF NOT EQUAL        * no more records ?
          MATCH     FMSRDATE,END
          GOTO      DATZ9999 IF LESS             * out of range ?
.
          ADD       FMSRUNIT,TDEBIT
          MOVE      "&nbsp;",DDEBIT
          MOVE      "&nbsp;",DCREDIT
.
          UNPACK    FMSRDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      FMSRUNIT,KAMOUNT
          MOVE      ZERO,FORMAT
          CALL      FORM0000
          WRITE     HTMLFILE;"<tr><td>",CPCDATE,"</td>":
                                 "<td>",FMSRBAT,"</td>":
                                 "<td>",FMSRREF,"</td>":
                                 "<td align=right>",XIM12,"</td></tr>"
          GOTO      DATZ1000
.
.---- normal rec ----
.
DATZ1400  CALL      RSFMTR2                      * go to start of records
DATZ1500  CALL      RKFMTR2
          BRANCH    OVRCD,DATZ9000               * no more records ?
          PACK      KEY14,FMTRLEDG,FMTRACCT,SP70
          MATCH     KEY14,KEY32
          GOTO      DATZ9000 IF NOT EQUAL        * no more records ?
          MATCH     FMTRPDAT,END
          GOTO      DATZ9000 IF LESS             * out of range ?
.
          MOVE      ZERO,DEBIT
          MOVE      ZERO,CREDIT
          CALL      GTYP0000                     * get transaction type
          STORE     FMTRAMT,DEBCRED,DEBIT,CREDIT
          ADD       DEBIT,TDEBIT
          ADD       CREDIT,TCREDIT
          UNPACK    FMTRPDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          MOVE      "&nbsp;",DDEBIT
          MOVE      "&nbsp;",DCREDIT
          BRANCH    DEBCRED,DATZ8100,DATZ8200
.
DATZ8100  MOVE      DEBIT,KAMOUNT      * debit format
          MOVE      ONE,FORMAT
          CALL      FORM0000
          MOVE      XIM15,DDEBIT
          LOAD      DDEBIT,FORMFLAG,ZIM15
          GOTO      DATZ8300
.
DATZ8200  MOVE      CREDIT,KAMOUNT     * credit format 
          MOVE      ONE,FORMAT
          CALL      FORM0000
          MOVE      XIM15,DCREDIT
          LOAD      DCREDIT,FORMFLAG,ZIM15
.
DATZ8300  MOVE      FMTRDISS,KEY5
          CALL      RDFMDS1
          IF        OVRCD=1
            MOVE      SP70,FMDSDESC
          ENDIF
          MOVE      FMTRRESP,KEY4
          CALL      RDFMRS1
          IF        OVRCD=1
            MOVE      SP70,FMDSDESC
          ENDIF
          MOVE      FMTRCRED,KEY5
          CALL      RDAPMA1
          IF        OVRCD=1
            MOVE      SP70,APMACN1
          ENDIF
          UNPACK    FMTRCRED,KEY2,KEY3
          MATCH     "~~",KEY2
          IF        @EQUAL
            PACK      KEY8,FMTRBAT,KEY3,SP70
            CALL      RDAPNS1
            IF        OVRCD=1
              MOVE      SP70,APNSNA1
            ENDIF
            MOVE      APNSNA1,APMACN1
          ENDIF
          WRITE     HTMLFILE;"<tr valign=top><td>":
                             CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                             "<td>",FMTRDESC;
          MATCH     SP70,APMACN1
          IF        !@EQUAL
             WRITE     HTMLFILE;"<br>Crd-",APMACN1;
          ENDIF
          WRITE     HTMLFILE;"</td>":
                             "<td>Bch-",FMTRBAT,"/",FMTRUNIQ:
                             "<br>Ref-",FMTRDOCR;
          MATCH     SP70,FMTRORD
          IF        !@EQUAL
             WRITE     HTMLFILE;"<br>Ord-",FMTRORD;
          ENDIF
          WRITE     HTMLFILE;"</td>":
                             "<td>",TRANTYPE,"</td>":
                             "<td>",FMRSDESC,"</td>":
                             "<td>",FMDSDESC,"</td>":
                             "<td align=right>",DDEBIT,"</td>":
                             "<td align=right>",DCREDIT,"</td>":
                             "</tr>"
          GOTO      DATZ1500                     * get next record
.
.---- file not found ----
.
DATZ8500  MOVE      SP70,TRANFILE
.
.---- reached EOF before EOP ----
.
DATZ9000  MOVE      TDEBIT,KAMOUNT 
          MOVE      ONE,FORMAT
          CALL      FORM0000
          MOVE      XIM15,DDEBIT
          LOAD      DDEBIT,FORMFLAG,ZIM15
.
          MOVE      TCREDIT,KAMOUNT 
          MOVE      ONE,FORMAT
          CALL      FORM0000
          MOVE      XIM15,DCREDIT
          LOAD      DCREDIT,FORMFLAG,ZIM15
.
          WRITE     HTMLFILE;"<tr><td colspan=6><b>Totals</b></td>":
                                     "<td align=right><b>",DDEBIT,"</b></td>":
                                     "<td align=right><b>",DCREDIT,"</b></td>"
DATZ9999  RETURN
.**********************************************************************
. GTYP - get transaction type                         Called By ML
.        Requires - FMTRTYPE (transaction code)
.        Returns  - DEBCRED  (1=debit type, 2=credit type)
.                   TRANTYPE (transaction description)
.**********************************************************************
GTYP0000  MOVE      ZERO,DEBCRED
          MOVE      SP70,TRANTYPE
.
          MATCH     "IN",FMTRTYPE
          GOTO      GTYP0100 IF NOT EQUAL
          MOVE      "1",DEBCRED
          MOVE      "Invoice     ",TRANTYPE
          GOTO      GTYP9999
.
GTYP0100  MATCH     "PY",FMTRTYPE
          GOTO      GTYP0200 IF NOT EQUAL
          MOVE      "1",DEBCRED
          MOVE      "Payment     ",TRANTYPE
          GOTO      GTYP9999
.
GTYP0200  MATCH     "JA",FMTRTYPE
          GOTO      GTYP0300 IF NOT EQUAL
          MOVE      "1",DEBCRED
          MOVE      "Journal Dr  ",TRANTYPE
          GOTO      GTYP9999
.
GTYP0300  MATCH     "JC",FMTRTYPE
          GOTO      GTYP0400 IF NOT EQUAL
          MOVE      "2",DEBCRED
          MOVE      "Journal Cr  ",TRANTYPE
          GOTO      GTYP9999
.
GTYP0400  MATCH     "CS",FMTRTYPE
          GOTO      GTYP0500 IF NOT EQUAL
          MOVE      "2",DEBCRED
          MOVE      "Cash Rec    ",TRANTYPE
          GOTO      GTYP9999
.
GTYP0500  MATCH     "CR",FMTRTYPE
          GOTO      GTYP0600 IF NOT EQUAL
          MOVE      "2",DEBCRED
          MOVE      "Credit      ",TRANTYPE
          GOTO      GTYP9999
.
GTYP0600  MATCH     "RF",FMTRTYPE
          GOTO      GTYP0700 IF NOT EQUAL
          MOVE      "2",DEBCRED
          MOVE      "Refund      ",TRANTYPE
          GOTO      GTYP9999
.
GTYP0700  MATCH     "RI",FMTRTYPE
          GOTO      GTYP0800 IF NOT EQUAL
          MOVE      "2",DEBCRED
          MOVE      "Reinst Inv  ",TRANTYPE
          GOTO      GTYP9999
.
GTYP0800  MATCH     "CC",FMTRTYPE
          GOTO      GTYP0900 IF NOT EQUAL
          MOVE      "2",DEBCRED
          MOVE      "Cancel Cheq ",TRANTYPE
          GOTO      GTYP9999
.
GTYP0900  
.
GTYP9999  RETURN
.******************************************************************************
.* FORM - Format  amount                         Called by lots               *
.*       Requires : DECPLACE      (amount number)                             *
.*                  KAMOUNT       (amount)                                    *
.*                  FORMAT        (format for norm acc (0=$, 1=$+c))          *
.*       Returns  : XIM11         (formatted 11 char)                         *
.*                  XIM12         (formatted 12 char)                         *
.*                  XIM13         (formatted 13 char)                         *
.*                  XIM14         (formatted 14 char)                         *
.*                  XIM15         (formatted 15 char)                         *
.*                  XIM16         (formatted 16 char)                         *
.*                  XIM17         (formatted 17 char)                         *
.******************************************************************************
FORM0000  MATCH     "8",FMAMTYPE
          GOTO      FORM0010 IF NOT LESS    * stat type ?
.
          BRANCH    FORMAT,FORM0200         * what is the format for norm acc ?
          GOTO      FORM0050
.
FORM0010  MOVE      FMAMDPLA,F1
          BRANCH    F1,FORM0100,FORM0200,FORM0300:
                       FORM0400,FORM0500,FORM0600
.
.---- no decimal places ----
.
FORM0050  MOVE      KAMOUNT,F17
          MOVE      FORMATP0,XIM17
          FEDIT     F17,XIM17
          MOVE      ZORMATP0,ZIM17
          FEDIT     F17,ZIM17
          GOTO      FORM1000
.
.---- 1 decimal places ----
.
FORM0100  MOVE      KAMOUNT,F15P1
          MOVE      FORMATP1,XIM17
          FEDIT     F15P1,XIM17
          MOVE      ZORMATP1,ZIM17
          FEDIT     F15P1,ZIM17
          GOTO      FORM1000
.
.---- 2 decimal places ----
.
FORM0200  MOVE      KAMOUNT,F14P2
          MOVE      FORMATP2,XIM17
          FEDIT     F14P2,XIM17
          MOVE      ZORMATP2,ZIM17
          FEDIT     F14P2,ZIM17
          GOTO      FORM1000
.
.---- 3 decimal places ----
.
FORM0300  MOVE      KAMOUNT,F13P3
          MOVE      FORMATP3,XIM17
          FEDIT     F13P3,XIM17
          MOVE      ZORMATP3,ZIM17
          FEDIT     F13P3,ZIM17
          GOTO      FORM1000
.
.---- 4 decimal places ----
.
FORM0400  MOVE      KAMOUNT,F12P4
          MOVE      FORMATP4,XIM17
          FEDIT     F12P4,XIM17
          MOVE      ZORMATP4,ZIM17
          FEDIT     F12P4,ZIM17
          GOTO      FORM1000
.
.---- 5 decimal places ----
.
FORM0500  MOVE      KAMOUNT,F11P5
          MOVE      FORMATP5,XIM17
          FEDIT     F11P5,XIM17
          MOVE      ZORMATP5,ZIM17
          FEDIT     F11P5,ZIM17
          GOTO      FORM1000
.
.---- 6 decimal places ----
.
FORM0600  MOVE      KAMOUNT,F10P6
          MOVE      FORMATP6,XIM17
          FEDIT     F10P6,XIM17
          MOVE      ZORMATP6,ZIM17
          FEDIT     F10P6,ZIM17
          GOTO      FORM1000
.
.---- check if out of range ----
.
FORM1000
          UNPACK    ZIM17,ANS,ZIM16
          UNPACK    XIM17,ANS,XIM16
          MATCH     SP1,ANS
          GOTO      FORM1100 IF NOT EQUAL
.
          UNPACK    ZIM16,ANS,ZIM15
          UNPACK    XIM16,ANS,XIM15
          MATCH     SP1,ANS
          GOTO      FORM1200 IF NOT EQUAL
.
          UNPACK    ZIM15,ANS,ZIM14
          UNPACK    XIM15,ANS,XIM14
          MATCH     SP1,ANS
          GOTO      FORM1300 IF NOT EQUAL
.
          UNPACK    ZIM14,ANS,ZIM13
          UNPACK    XIM14,ANS,XIM13
          MATCH     SP1,ANS
          GOTO      FORM1400 IF NOT EQUAL
.
          UNPACK    ZIM13,ANS,ZIM12
          UNPACK    XIM13,ANS,XIM12
          MATCH     SP1,ANS
          GOTO      FORM1500 IF NOT EQUAL
.
          UNPACK    ZIM12,ANS,ZIM11
          UNPACK    XIM12,ANS,XIM11
          MATCH     SP1,ANS
          GOTO      FORM1600 IF NOT EQUAL
          GOTO      FORM9999
.
FORM1100  MOVE      ZIM16,XIM16
          UNPACK    ZIM16,ANS,ZIM15
          MATCH     SP1,ANS
          GOTO      FORM2100 IF NOT EQUAL
.
FORM1200  MOVE      ZIM15,XIM15
          UNPACK    ZIM15,ANS,ZIM14
          MATCH     SP1,ANS
          GOTO      FORM2200 IF NOT EQUAL
.
FORM1300  MOVE      ZIM14,XIM14
          UNPACK    ZIM14,ANS,ZIM13
          MATCH     SP1,ANS
          GOTO      FORM2300 IF NOT EQUAL
.
FORM1400  MOVE      ZIM13,XIM13
          UNPACK    ZIM13,ANS,ZIM12
          MATCH     SP1,ANS
          GOTO      FORM2400 IF NOT EQUAL
.
FORM1500  MOVE      ZIM12,XIM12
          UNPACK    ZIM12,ANS,ZIM11
          MATCH     SP1,ANS
          GOTO      FORM2500 IF NOT EQUAL
.
FORM1600  MOVE      ZIM11,XIM11
          GOTO      FORM9999
.
FORM2100  MOVE      "********************",XIM15
FORM2200  MOVE      "********************",XIM14
FORM2300  MOVE      "********************",XIM13
FORM2400  MOVE      "********************",XIM12
FORM2500  MOVE      "********************",XIM11
.
FORM9999  RETURN
.------------------------------------------------------------
. Create Select Period Options List
.------------------------------------------------------------
SELPER00  MOVE      "12",YEAREND
          ADD       FMLAPERS,YEAREND
          PACK      KEY6,FMLALEDG,CURYEAR
          CALL      RDFMLC1
          CALL      RPFMLC1
          BRANCH    OVRCD,SELPER10
.
          MOVE      ZERO,SELPERD
          CALL      ADDPER00      * Last Year
.
SELPER10  CALL      RKFMLC1
          BRANCH    OVRCD,SELPER99
.
          MOVE      PERIODNO,SELPERD
          CALL      ADDPER00     * Current Year
          
          CALL      RKFMLC1
          BRANCH    OVRCD,SELPER99
.
          MOVE      ZERO,SELPERD
          CALL      ADDPER00     * Next Year
.
          CALL      RPFMLC1
.
SELPER99  RETURN
.------------------------------------------------------------
. Add all Periods for a Year
.------------------------------------------------------------
ADDPER00  MOVE      ONE,F2
ADDPER10  LOAD      SELNAME,F2,FMLC01DE,FMLC02DE,FMLC03DE,FMLC04DE,FMLC05DE:
                               FMLC06DE,FMLC07DE,FMLC08DE,FMLC09DE,FMLC10DE:
                               FMLC11DE,FMLC12DE
          LOAD      KEY8,F2,FMLC01TO,FMLC02TO,FMLC03TO,FMLC04TO:
                                  FMLC05TO,FMLC06TO,FMLC07TO,FMLC08TO:
                                  FMLC09TO,FMLC10TO,FMLC11TO,FMLC12TO,FMLC13TO
          MOVE      KEY8,SELYEAR 
          PACK      KEY6,F2,FMLCYEAR
          REP       " 0",KEY6
          IF        F2=SELPERD
            WRITE     HTMLFILE;"<option value=#"",KEY6,"#" selected>":
                               SELNAME,SP1,SELYEAR,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=#"",KEY6,"#">":
                               SELNAME,SP1,SELYEAR,"</option>"
          ENDIF
          ADD       ONE,F2
          IF        F2<=YEAREND
            GOTO      ADDPER10
          ENDIF
ADDPER99  RETURN
.
          INC       APSCEAIO/INC       * committed expenditure
          INC       APSCEBIO/INC       * committed expenditure
          INC       APSCIOIO/INC       * committed expenditure
          INC       APSMASIO/INC
          INC       APSNSCIO/INC
          INC       APSORDIO/INC       * committed expenditure
          INC       APSPPDIO/INC
          INC       FMSBPFIO/INC
          INC       FMSAMAIO/INC       * account
          INC       FMSBNKIO/INC       * bank rec
          INC       FMSBTYIO/INC       * budget data
          INC       FMSBUDIO/INC       * budget data
          INC       FMSCHQIO/INC       * cheque
          INC       FMSCONIO/INC       * controlf
          INC       FMSCSAIO/INC
          INC       FMSDSFIO/INC       * dissection accrual summary file
          INC       FMSFPSIO/INC       * period summary file
          INC       FMSLMAIO/INC       * ledger
          INC       FMSLMCIO/INC       * ledger financial calendar
          INC       FMSSQUIO/INC       * enquiry security
          INC       FMSSBUIO/INC       * stat budget
          INC       FMSSMAIO/INC       * stat summary
          INC       FMSTRNIO/INC       * transaction
          INC       FMSUWPIO/INC       * wp file
          INC       FMSCODIO/INC       * codes file
          INC       FMSLMBIO/INC       * report
          INC       FMSONCIO/INC       * oncost code
          INC       FMSPRCIO/INC       * print code
          INC       FMSLEVIO/INC       * level code
          INC       FMSRSFIO/INC       * account report seq
          INC       FMSSTRIO/INC       * stats transactions
          INC       FMSTCFIO/INC       * total calc
          INC       FMSDISIO/INC       * dissection
          INC       FMSRESIO/INC       * responsibility
          INC       FMSCTRIO/INC       * cash transactions
          INC       FMSCCAIO/INC       * cost centre
          INC       FMSSBAIO/INC       * subjective
          INC       FMSWASIO/INC       * web access security
          INC       FMSWBCIO/INC       * web user comments
.
          INC       FMSWEBCD
          INC       IBAWEBCD    
