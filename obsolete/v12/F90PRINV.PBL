.*****************************************************************************
.*    Program      : F90PRINV                                                *
.*    Description  : Conversion priinvof to new File layout                  *
.*                                                                           *
.*    Author       : J.Tan          CAR 169149                               *
.*    Date         : 23/05/2008                                              *
.*    Modifications:                                                         *
.*                 :                                                         *
.*****************************************************************************
.
          INC       STD001FD
. New FD
.
.Data File Definition
.--------------------
PRIINVO1   IFILE SQL, FIXED=430, KEY="U1-8"
PRIINVO2   IFILE SQL, FIXED=430, KEY="U9-16,1-8"
PRIINVO3   IFILE SQL, FIXED=430, KEY="U23-30,31-32,1-8"
PRIINVO4   IFILE SQL, FIXED=430, KEY="U41-46,47-52,23-30,31-32,1-8"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
PRINNUMB  DIM       8      8        1     Invoice Number
.
PRINDATE  DIM       8      8        9     Invoice Date (CCYYMMDD)
PRINNAME  DIM       6      6        17    Alpha Sort on Patient Surname
.
PRINDEBT  DIM       8      8        23    Debtor Number
DPRINSCN  DIM       2      2        31    Scan Flag
.
DPRINUNQ  DIM       8      8        33    Unique Identifier
.
PRINPRAC  DIM       6      6        41    Medical Practice Code
PRINDOCT  DIM       6      6        47    Service Doctor
PRINCLAM  DIM       3      3        53    Claim Type (Category CL)
.
PRINITOT  FORM     12.2    8        56    Invoice Total
PRINPAMT  FORM     12.2    8        64    Amount Paid by Patient
PRINHAMT  FORM     12.2    8        72    Amount Paid by Health Fund
PRINIAMT  FORM     12.2    8        80    Amount Paid by Insurer
PRINMAMT  FORM     12.2    8        88    Amount Paid by Medicare
PRINVAMT  FORM     12.2    8        96    Amount Paid by Veterans Affairs
PRINOTHA  FORM     12.2    8        104   Other Amount Paid
PRINJAMT  FORM     12.2    8        112   Journal Amount
PRINPEND  FORM     12.2    8        120   Cash Pending
PRINTRAN  FORM      6      4        128   Next DTRAN Number
.
.         Person Responsible for A/C
.
PRINNAMR  DIM       45     45       132   P.R.A. Name
PRINPRA1  DIM       25     25       177   P.R.A. Address Line 1
PRINPRA2  DIM       25     25       202   P.R.A. Address Line 2
PRINPRA3  DIM       25     25       227   P.R.A. Address Line 3
PRINPOST  DIM       4      4        252   P.R.A. Postcode
PRINTELP  DIM       12     12       256   P.R.A. Telephone Private
PRINTELB  DIM       12     12       268   P.R.A. Telephone Business
PRINRELP  DIM       10     10       280   P.R.A. Relationship
.
PRINCNUM  FORM       8      5       290   Unique Claim Number
PRINADTE  DIM        8      8       295   Accident Date
.
PRINGSTA  FORM      12.2    8       303   GST Amount
PRINGSTJ  FORM      12.2    8       311   GST Journal Amount
.
PRINCNST  DIM       1       1       319   Credit Note Status
.                                         0=No Credit Note
.                                         1=Fully Credited
.                                         2=Credit by Item
PRINCNTT  FORM      12.2    8       320   Credit Note Total
PRINCDAT  DIM       8       8       328   Date Record Created (ccyymmdd)
PRINCTIM  DIM       8       8       336   Time Record Created (hh:mm:ss)
PRINCUID  DIM      10      10       344   WEB User ID Created (websecaf)
PRINUDAT  DIM       8       8       354   Date Record Updated (ccyymmdd)
PRINUTIM  DIM       8       8       362   Time Record Updated (hh:mm:ss)
PRINUUID  DIM      10      10       370   WEB User ID Updated (websecaf)
PRINSPAR  DIM      50      50       380   Spare Variable
.End of Record                      430
.
.
. Redefine form fields from key
. -----------------------------
.
PRINSCAN  FORM      2
PRINUNIQ  FORM      8
.
FINDFILE  FILE      ASCII, FIXED=256
.
. Enter the first or the shortest index from the original FD and rename
. the index OLDFILE
.   
OLDFILE   IFILE     SQL, FIXED=378, KEY="U1-8"
.
CMDLINE   DIM       100
DIM60     DIM       60
.
RECNUM    FORM      8
NEWFILE   DIM       60
NEWPATH   DIM       60
OLDPATH   DIM       60
DEFPATH   DIM       60
SAVEFLG   FORM      1
SP50      INIT    "                                                  "
SP60      INIT    "                                                            "
.
PRGNAM    INIT      "CONVERSION PRIINVFD"
PRGID     INIT      "F90PRINV"
VERSION   INIT      "V12.00.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN1000,MAIN0000,MAIN9999 * Yes, no, cancel
.
MAIN1000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
. Loop thru old file and write records to new file
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/compress saved file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          
INIT9999  RETURN
.
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
PREP0000 
. open old file
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE,"priinvof"
          TRAPCLR   IO
          BRANCH    OVRCD,PREP5000       * Original file does not exist
          CLOSE     OLDFILE
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
. Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat priinvof.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sprinvof.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat priinvof.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sprinvof.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
. remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "priinvof",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "sprinvof",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "priinvof",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.
          APPEND    " 430 UC1-8",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has
.  to be repeated with for each individual index
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " U9-16,1-8",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " U23-30,31-32,1-8",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " U41-46,47-52,23-30,31-32,1-8",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
          OPEN      PRIINVO1,NEWFILE
.
. set the flag to say we want to continue
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
PREP9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and WRFUND1 each record               *
.**********************************************************************
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          PACK      KEY8,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
          PACK      PRINCDAT,SP70       * Date Record Created (ccyymmdd)
          PACK      PRINCTIM,SP70       * Time Record Created (hh:mm:ss)
          PACK      PRINCUID,SP70       * WEB User ID Created (websecaf)
          PACK      PRINUDAT,SP70       * Date Record Updated (ccyymmdd)
          PACK      PRINUTIM,SP70       * Time Record Updated (hh:mm:ss)
          PACK      PRINUUID,SP70       * WEB User ID Updated (websecaf)
          PACK      PRINSPAR,SP70       * SPARE CHARACTERS
.
          PACK      KEY8,PRINNUMB,SP70
          CALL      WRPRIN1                       * write to new file
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     PRIINVO1                      * close new file
          CLOSE     OLDFILE                       * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
READ9999  RETURN
.
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
VALD0000  MOVE      ZERO,EXIT
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or compress save file                                *
.**********************************************************************
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "sprinvof*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "compress -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not compressed or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.         Get the default directory
.**********************************************************************
DEFT0000  EXECUTE   "ldat priinvof.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding priinvof.  "
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
DEFT9999  RETURN
+
.**********************************************************************
. ---- OLD IO ROUTINES ----
.**********************************************************************
.  Copy and paste the Positional Read routine from the original
.  IO file, rename the routine READSOLD and change the index name
.  to OLDFILE
.
READSOLD  RESET     KEY8
          READ      OLDFILE,KEY8;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE;PRINNUMB,PRINDATE,PRINNAME,PRINDEBT:
                            DPRINSCN,DPRINUNQ,PRINPRAC,PRINDOCT,PRINCLAM:
                            PRINITOT,PRINPAMT,PRINHAMT,PRINIAMT,PRINMAMT:
                            PRINVAMT,PRINOTHA,PRINJAMT,PRINPEND,PRINTRAN:
                            PRINNAMR,PRINPRA1,PRINPRA2,PRINPRA3,PRINPOST:
                            PRINTELP,PRINTELB,PRINRELP,PRINCNUM,PRINADTE:
                            PRINGSTA,PRINGSTJ,PRINCNST,PRINCNTT,PRINSPAR
          GOTO      OVERCOND IF OVER
          MOVE      DPRINSCN,PRINSCAN
          MOVE      DPRINUNQ,PRINUNIQ
          RETURN
.
. New IO write routine
.
WRPRIN1   RESET     KEY8
          MOVE      PRINSCAN,DPRINSCN
          MOVE      PRINUNIQ,DPRINUNQ
          WRITE     PRIINVO1,KEY8;KEY8,PRINDATE,PRINNAME,PRINDEBT:
                             DPRINSCN,DPRINUNQ,PRINPRAC,PRINDOCT,PRINCLAM:
                             PRINITOT,PRINPAMT,PRINHAMT,PRINIAMT,PRINMAMT:
                             PRINVAMT,PRINOTHA,PRINJAMT,PRINPEND,PRINTRAN:
                             PRINNAMR,PRINPRA1,PRINPRA2,PRINPRA3,PRINPOST:
                             PRINTELP,PRINTELB,PRINRELP,PRINCNUM,PRINADTE:
                             PRINGSTA,PRINGSTJ,PRINCNST,PRINCNTT:
                             PRINCDAT,PRINCTIM,PRINCUID:
                             PRINUDAT,PRINUTIM,PRINUUID,PRINSPAR
          RETURN
.
          INC       STD001IO
