.******************************************************************************
.                                 OPREXPDS
.                       Expensive item codes      
.
. Description : Display Expensive item Codes in surname order from the oprexpaf
.               file using the 2nd index.
.
. Requirements: This routine requires OPREXPFD and OPREXPIO.INC and KEY6
.               and KEY36
.
.             OPREXPDS was written by Monika Ohleiter on the 09/02/1990
.                        I.B.A. Development Department
.                             D. Jones supervisor
.
.   Modifications : V5.04.01  14/01/1997  Steve Armstrong
.                             Removed TRAP statements for TBC 7
.
.******************************************************************************
OPREXPDS 
.
          DISPLAY    *P1:4,*EF,*P34:4,*V2LON,*ULON,"EXPENSIVE ITEM CODES"
          MOVE       FIVE,CVERT
          MOVE       ONE,CCOL
.
          PACK       KEY36,SP30,SP6
          CALL       RSOPEXP2
EXPDS222  
          CALL       RKOPEXP2
          BRANCH     OVRCD,EXPDS990
.
          DISPLAY    *PCCOL:CVERT,*V2LON,OPEXCODE,SP2,*HOFF,OPEXDESC
          ADD        ONE,CVERT
          COMPARE    TWENTY3,CVERT
          GOTO       EXPDS222 IF LESS
          MOVE       FIVE,CVERT
          ADD        "40",CCOL
          COMPARE    "70",CCOL
          GOTO       EXPDS222 IF LESS
.
.         Check for old or new standards question
.
          BRANCH    CNEWDS,EXPDS200
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
          CMATCH    ANSF,ANS
          GOTO      OPREXPDS IF EQUAL
          CMATCH    ANSE,ANS
          GOTO      EXPDS999 IF EQUAL
          GOTO      EXPDS400
.
.         New standards question
.
EXPDS200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, ":
                    "(",*V2LON,"F",*HOFF,")irst Screen, ":
                    "(",*V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
          CMATCH    ANSC,ANS
          GOTO      EXPDS999 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      OPREXPDS IF EQUAL
.
.         Clear the screen
.
EXPDS400  MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:CVERT,*EF
          GOTO      EXPDS222
.
EXPDS990  BRANCH    CNEWDS,EXPDS999
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
EXPDS999  RETURN
