.-------------------------------------------------------------------------------
.   System    :   FMS Accounting System  
.                           
.   Program   :   FMSCATFM 
.                            
.   Routines  :   KCATFM00 Keyin Category   - Returns a valid record
.                 ACATFM00 Accept Category  - Returns a key NOT on file
.   
.   Return    :   EXIT = 1 null entry EXIT = 2 EXITCHAR entered
.             (Note : uses CATEGORY to ensure FMCDCATY not altered in redisplay)
.
.   Internal  :   HCATFM00 Help Display for Category  
.                            
.   Redisplay :   RCATFM00 Screen Redisplay Called for Category  
.-------------------------------------------------------------------------------
.   Mods      :    
.-------------------------------------------------------------------------------
. Normal Category Keyin - Category Must Exist Exit = 1 if Null Entry
.-------------------------------------------------------------------------------
KCATFM00  MOVE      UNDLN70,FMCDCATY
          KEYIN     *PCCOL:CVERT,*EL,*DV,FMCDCATY:
                    *PCCOL:CVERT,*V2LON,FMCDCATY: 
                    *PCCOL:CVERT,*EL,*DV,FMCDCATY
.
          ENDSET    FMCDCATY
          APPEND    SP70,FMCDCATY
          RESET     FMCDCATY
          MATCH     SP70,FMCDCATY             * Category Spaces so exit set
          GOTO      KCATFM90 IF EQUAL
.
          CMATCH    EXITCHAR,FMCDCATY         * Exit Selected
          GOTO      KCATFM95 IF EQUAL
.
          CMATCH    QUEST,FMCDCATY            * Help Required  
          GOTO      KCATFM10 IF NOT EQUAL
          CALL      HCATFM00                  * Display List Of FMCDCATYs
          MOVE      FMCDCATY,CATEGORY         * save category
          CALL      RCATFM00                  * Redisplay Routine
          MOVE      CATEGORY,FMCDCATY         * save category
.
          ENDSET    FMCDCATY
          APPEND    SP70,FMCDCATY
          RESET     FMCDCATY
          MATCH     SP70,FMCDCATY             * Category Spaces so exit set
          GOTO      KCATFM00 IF EQUAL
.
KCATFM10  PACK      KEY5,FMCDCATY,SP70
          CALL      RDFMCD1                                   
          BRANCH    OVRCD,KCATFM80
          MOVE      ZERO,EXIT                 * no entry so exit is set
          GOTO      KCATFM99
.
KCATFM80  DISPLAY   *P1:24,*EL,*B,"Category Does Not Exist - ";
          CALL      HITENTER
          GOTO      KCATFM00
.
KCATFM90  MOVE      ONE,EXIT                  * null entry so exit is set
          GOTO      KCATFM99
.
KCATFM95  MOVE      TWO,EXIT                  * Exit character entered    
.
KCATFM99  RETURN
.-------------------------------------------------------------------------------
. Accept Category Keyin - Category Must Not Exist 
.-------------------------------------------------------------------------------
ACATFM00  MOVE      UNDLN70,FMCDCATY
          KEYIN     *PCCOL:CVERT,*EL,*DV,FMCDCATY:
                    *PCCOL:CVERT,*V2LON,FMCDCATY: 
                    *PCCOL:CVERT,*EL,*DV,FMCDCATY
.
          ENDSET    FMCDCATY
          APPEND    SP70,FMCDCATY
          RESET     FMCDCATY
          MATCH     SP70,FMCDCATY             * Category Spaces so exit set
          GOTO      ACATFM90 IF EQUAL
.
          CMATCH    EXITCHAR,FMCDCATY         * Exit Selected
          GOTO      ACATFM95 IF EQUAL
.
          CMATCH    QUEST,FMCDCATY            * Help Required  
          GOTO      ACATFM10 IF NOT EQUAL
          CALL      HCATFM00                  * Display List Of FMCDCATYs
          MOVE      FMCDCATY,CATEGORY         * save category
          CALL      RCATFM00                  * Redisplay Routine
          MOVE      CATEGORY,FMCDCATY         * save category
.
          ENDSET    FMCDCATY
          APPEND    SP70,FMCDCATY
          RESET     FMCDCATY
          MATCH     SP70,FMCDCATY             * Category Spaces so exit set
          GOTO      ACATFM00 IF EQUAL
.
ACATFM10  PACK      KEY5,FMCDCATY,SP70
          MOVE      ZERO,EXIT                 * no entry so exit is set
          CALL      RDFMCD1                                   
          BRANCH    OVRCD,ACATFM99
.
          DISPLAY   *P1:24,*EL,*B,"Category Already Exists - ";
          CALL      HITENTER
          GOTO      ACATFM00
.
ACATFM90  MOVE      ONE,EXIT                  * null entry so exit is set
          GOTO      ACATFM99
.
ACATFM95  MOVE      TWO,EXIT                  * Exit character entered    
.
ACATFM99  RETURN
.------------------------------------------------------------------------------
. Display List of All CATes
.------------------------------------------------------------------------------
HCATFM00  DISPLAY   *P1:4,*EF,*P1:5,*V2LON,*ULON,"Item":
                    *P10:5,"Category":
                    *P25:5,"Description"
          MOVE      FIVE,STDROW
          MOVE      ZERO,XITEMNO
          MOVE      ZERO,CPAGENO
          MOVE      SP70,KEY5
          CALL      RSFMCD1
.
HCATFM10  CALL      RKFMCD1
          BRANCH    OVRCD,HCATFM20
          MATCH     FMCDCODE,SP70
          GOTO      HCATFM10 IF NOT EQUAL        * code record ?
.
          ADD       ONE,XITEMNO
          ADD       ONE,STDROW
          STORE     FMCDCATY,XITEMNO,ST1,ST2,ST3,ST4,ST5:
                                    ST6,ST7,ST8,ST9,ST10:
                                    ST11,ST12,ST13,ST14,ST15:
                                    ST16,ST17
          DISPLAY   *P2:STDROW,*V2LON,XITEMNO,DOT,*HOFF:
                    *P10:STDROW,FMCDCATY,*P25:STDROW,FMCDDESC
          COMPARE   "17",XITEMNO
          GOTO      HCATFM10 IF NOT EQUAL
.
HCATFM20  MOVE      ZERO,CCITEM
          MOVE      SP2,CCITEMD
          DISPLAY   *P1:24,*EL,"Select Item, (";
          COMPARE   "17",XITEMNO
          GOTO      HCATFM25 IF NOT EQUAL
          DISPLAY   *V2LON,"N",*HOFF,")ext page, (";
.
HCATFM25  COMPARE   ZERO,CPAGENO
          GOTO      HCATFM26 IF EQUAL
          DISPLAY   *V2LON,"P",*HOFF,")revious page, (";
.
HCATFM26  KEYIN     *V2LON,"E",*HOFF,")xit ? ",*JR,*V2LON,CCITEMD;
.
          RESET     CCITEMD
          GOTO      HCATFM30 IF EOS      * Next Page
          REP       UPPLOW,CCITEMD
          MATCH     "  N",CCITEMD        * Next Page
          GOTO      HCATFM30 IF EQUAL
          MATCH     "  P",CCITEMD        * Previous Page
          GOTO      HCATFM35 IF EQUAL
          MATCH     "  E",CCITEMD        * Exit
          GOTO      HCATFM91 IF EQUAL
.
          TYPE      CCITEMD
          GOTO      HCATFM90 IF NOT EQUAL  * Invalid Entry
.
          MOVE      CCITEMD,CCITEM
          COMPARE   ONE,CCITEM
          GOTO      HCATFM90 IF LESS       * Invalid Entry
          COMPARE   CCITEM,XITEMNO
          GOTO      HCATFM90 IF LESS       * Invalid Entry
          GOTO      HCATFM92               * Item Selected
.
HCATFM30  COMPARE   "17",XITEMNO            * Next Page Selected
          GOTO      HCATFM90 IF NOT EQUAL  * No Next Page Invalid Selection
          ADD       ONE,CPAGENO
          MOVE      ZERO,XITEMNO            
          MOVE      FIVE,STDROW
          DISPLAY   *P1:6,*EF
          GOTO      HCATFM10
.
HCATFM35  COMPARE   ZERO,CPAGENO           * Previous Page Selected
          GOTO      HCATFM90 IF EQUAL      * No Previous Page Invalid Selection
          DISPLAY   *P1:6,*EF
          SUB       ONE,CPAGENO
          COMPARE   "17",XITEMNO      
          GOTO      HCATFM36 IF EQUAL 
          CALL      RPFMCD1            * Initial reverse read
          COMPARE   ZERO,XITEMNO      
          GOTO      HCATFM37 IF EQUAL       * no data on screen ?
.
HCATFM36  CALL      RPFMCD1
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HCATFM36 IF NOT EQUAL
          COMPARE   FIVE,STDROW
          GOTO      HCATFM10 IF EQUAL   * Display Down The Page
HCATFM37  MOVE      FIVE,STDROW
          MOVE      "17",XITEMNO
          GOTO      HCATFM36
.
HCATFM90  BEEP                          * invalid entry
          GOTO      HCATFM20
.
HCATFM91  MOVE      SP70,FMCDCATY
          GOTO      HCATFM99
.
HCATFM92  MOVE      SP70,FMCDCATY
          LOAD      FMCDCATY,CCITEM,ST1,ST2,ST3,ST4,ST5:
                                  ST6,ST7,ST8,ST9,ST10:
                                  ST11,ST12,ST13,ST14,ST15:
                                  ST16,ST17
.
HCATFM99  RETURN
.
.-------------------------------------------------------------------------------
.   System    :   FMS Accounting System  
.                           
.   Program   :   FMSCODFM 
.                            
.   Routines  :   KCODFM00 Keyin Code   - Returns a valid record
.                 ACODFM00 Accept Code  - Returns a key NOT on file
.
.   Requires  :   CATEGORY - Current Category
.   
.   Return    :   EXIT = 1 null entry EXIT = 2 EXITCHAR entered
.                 (Note : uses CODE to ensure FMCDCODE not altered in redisplay)
.
.   Internal  :   HCODFM00 Help Display for Code  
.                            
.   Redisplay :   RCODFM00 Screen Redisplay Called for Code  
.-------------------------------------------------------------------------------
.   Mods      :    
.-------------------------------------------------------------------------------
. Normal Code Keyin - Code Must Exist Exit = 1 if Null Entry
.-------------------------------------------------------------------------------
KCODFM00  MOVE      UNDLN70,FMCDCODE
          KEYIN     *PCCOL:CVERT,*EL,*DV,FMCDCODE:
                    *PCCOL:CVERT,*V2LON,FMCDCODE: 
                    *PCCOL:CVERT,*EL,*DV,FMCDCODE
.
          ENDSET    FMCDCODE
          APPEND    SP70,FMCDCODE
          RESET     FMCDCODE
          MATCH     SP70,FMCDCODE             * System Code Spaces so exit set
          GOTO      KCODFM90 IF EQUAL
.
          CMATCH    EXITCHAR,FMCDCODE         * Exit Selected
          GOTO      KCODFM95 IF EQUAL
.
          CMATCH    QUEST,FMCDCODE            * Help Required  
          GOTO      KCODFM10 IF NOT EQUAL
          CALL      HCODFM00                  * Display List Of FMCDCODEs
          MOVE      FMCDCODE,CODE             * save code
          CALL      RCODFM00                  * Redisplay Routine
          MOVE      CODE,FMCDCODE             * retrieve code
.
          ENDSET    FMCDCODE
          APPEND    SP70,FMCDCODE
          RESET     FMCDCODE
          MATCH     SP70,FMCDCODE             * System Code Spaces so exit set
          GOTO      KCODFM00 IF EQUAL
.
KCODFM10  PACK      KEY5,CATEGORY,FMCDCODE,SP70
          CALL      RDFMCD1                                   
          BRANCH    OVRCD,KCODFM80
          MOVE      ZERO,EXIT                 * no entry so exit is set
          GOTO      KCODFM99
.
KCODFM80  DISPLAY   *P1:24,*EL,*B,"Code Does Not Exist - ";
          CALL      HITENTER
          GOTO      KCODFM00
.
KCODFM90  MOVE      ONE,EXIT                  * null entry so exit is set
          GOTO      KCODFM99
.
KCODFM95  MOVE      TWO,EXIT                  * Exit character entered    
.
KCODFM99  RETURN
.-------------------------------------------------------------------------------
. Accept Code Keyin - Code Must Not Exist 
.-------------------------------------------------------------------------------
ACODFM00  MOVE      UNDLN70,FMCDCODE
          KEYIN     *PCCOL:CVERT,*EL,*DV,FMCDCODE:
                    *PCCOL:CVERT,*V2LON,FMCDCODE: 
                    *PCCOL:CVERT,*EL,*DV,FMCDCODE
.
          ENDSET    FMCDCODE
          APPEND    SP70,FMCDCODE
          RESET     FMCDCODE
          MATCH     SP70,FMCDCODE             * System Code Spaces so exit set
          GOTO      ACODFM90 IF EQUAL
.
          CMATCH    EXITCHAR,FMCDCODE         * Exit Selected
          GOTO      ACODFM95 IF EQUAL
.
          CMATCH    QUEST,FMCDCODE            * Help Required  
          GOTO      ACODFM10 IF NOT EQUAL
          CALL      HCODFM00                  * Display List Of FMCDCODEs
          MOVE      FMCDCODE,CODE             * save code
          CALL      RCODFM00                  * Redisplay Routine
          MOVE      CODE,FMCDCODE             * retrieve code
.
          ENDSET    FMCDCODE
          APPEND    SP70,FMCDCODE
          RESET     FMCDCODE
          MATCH     SP70,FMCDCODE             * System Code Spaces so exit set
          GOTO      ACODFM00 IF EQUAL
.
ACODFM10  PACK      KEY5,CATEGORY,FMCDCODE,SP70
          MOVE      ZERO,EXIT                 * no entry so exit is set
          CALL      RDFMCD1                                   
          BRANCH    OVRCD,ACODFM99
.
          DISPLAY   *P1:24,*EL,*B,"Code Already Exists - ";
          CALL      HITENTER
          GOTO      ACODFM00
.
ACODFM90  MOVE      ONE,EXIT                  * null entry so exit is set
          GOTO      ACODFM99
.
ACODFM95  MOVE      TWO,EXIT                  * Exit character entered    
.
ACODFM99  RETURN
.------------------------------------------------------------------------------
. Display List of All Codes
.------------------------------------------------------------------------------
HCODFM00  DISPLAY   *P1:4,*EF,*P1:5,*V2LON,*ULON,"Item":
                    *P10:5,"Code":
                    *P25:5,"Description"
          MOVE      FIVE,STDROW
          MOVE      ZERO,XITEMNO
          MOVE      ZERO,CPAGENO
          PACK      KEY5,CATEGORY,SP70
          CALL      RSFMCD1
.
HCODFM10  CALL      RKFMCD1
          BRANCH    OVRCD,HCODFM20
          MATCH     KEY5,FMCDCATY
          GOTO      HCODFM20 IF NOT EQUAL        * end of category ?
          MATCH     SP70,FMCDCODE
          GOTO      HCODFM10 IF EQUAL            * category record ?
.
          ADD       ONE,XITEMNO
          ADD       ONE,STDROW
          STORE     FMCDCODE,XITEMNO,ST1,ST2,ST3,ST4,ST5:
                                    ST6,ST7,ST8,ST9,ST10:
                                    ST11,ST12,ST13,ST14,ST15:
                                    ST16,ST17
          DISPLAY   *P2:STDROW,*V2LON,XITEMNO,DOT,*HOFF:
                    *P10:STDROW,FMCDCODE,*P25:STDROW,FMCDDESC
          COMPARE   "17",XITEMNO
          GOTO      HCODFM10 IF NOT EQUAL
.
HCODFM20  MOVE      ZERO,CCITEM
          MOVE      SP2,CCITEMD
          DISPLAY   *P1:24,*EL,"Select Item, (";
          COMPARE   "17",XITEMNO
          GOTO      HCODFM25 IF NOT EQUAL
          DISPLAY   *V2LON,"N",*HOFF,")ext page, (";
.
HCODFM25  COMPARE   ZERO,CPAGENO
          GOTO      HCODFM26 IF EQUAL
          DISPLAY   *V2LON,"P",*HOFF,")revious page, (";
.
HCODFM26  KEYIN     *V2LON,"E",*HOFF,")xit ? ",*JR,*V2LON,CCITEMD;
.
          RESET     CCITEMD
          GOTO      HCODFM30 IF EOS      * Next Page
          REP       UPPLOW,CCITEMD
          MATCH     "  N",CCITEMD        * Next Page
          GOTO      HCODFM30 IF EQUAL
          MATCH     "  P",CCITEMD        * Previous Page
          GOTO      HCODFM35 IF EQUAL
          MATCH     "  E",CCITEMD        * Exit
          GOTO      HCODFM91 IF EQUAL
.
          TYPE      CCITEMD
          GOTO      HCODFM90 IF NOT EQUAL  * Invalid Entry
.
          MOVE      CCITEMD,CCITEM
          COMPARE   ONE,CCITEM
          GOTO      HCODFM90 IF LESS       * Invalid Entry
          COMPARE   CCITEM,XITEMNO
          GOTO      HCODFM90 IF LESS       * Invalid Entry
          GOTO      HCODFM92               * Item Selected
.
HCODFM30  COMPARE   "17",XITEMNO            * Next Page Selected
          GOTO      HCODFM90 IF NOT EQUAL  * No Next Page Invalid Selection
          ADD       ONE,CPAGENO
          MOVE      ZERO,XITEMNO            
          MOVE      FIVE,STDROW
          DISPLAY   *P1:6,*EF
          GOTO      HCODFM10
.
HCODFM35  COMPARE   ZERO,CPAGENO           * Previous Page Selected
          GOTO      HCODFM90 IF EQUAL      * No Previous Page Invalid Selection
          DISPLAY   *P1:6,*EF
          SUB       ONE,CPAGENO
          COMPARE   "17",XITEMNO      
          GOTO      HCODFM36 IF EQUAL 
          CALL      RPFMCD1            * Initial reverse read
.
HCODFM36  CALL      RPFMCD1
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HCODFM36 IF NOT EQUAL
          COMPARE   FIVE,STDROW
          GOTO      HCODFM10 IF EQUAL   * Display Down The Page
          MOVE      FIVE,STDROW
          MOVE      "17",XITEMNO
          GOTO      HCODFM36
.
HCODFM90  BEEP                          * invalid entry
          GOTO      HCODFM20
.
HCODFM91  MOVE      SP70,FMCDCODE
          GOTO      HCODFM99
.
HCODFM92  MOVE      SP70,FMCDCODE
          LOAD      FMCDCODE,CCITEM,ST1,ST2,ST3,ST4,ST5:
                                  ST6,ST7,ST8,ST9,ST10:
                                  ST11,ST12,ST13,ST14,ST15:
                                  ST16,ST17
.
HCODFM99  RETURN
.
