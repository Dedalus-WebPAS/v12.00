.*    Program      : X02OTMA1                                                *
.*    Description  : Conversion outma1af to new File layout                  *
.*                                                                           *
.*    Author       : Thanh T                                                 *
.*    Date         : 08/02/2022                                              *
.*    Modifications:                                                         *
.*---------------------------------------------------------------------------*
.* V11.02.01 18/03/2022  Thanh T    TSK 0917817                              *
.*           Changed to allow file conversion other than "out" prefix        *
.*****************************************************************************
.
.  using vi Global change command      :%s/FromString/ToString/g
.
.  Global change progname  eg F57PTDOC
.  Global change logfname  eg f57ptdoc
.  Global change filefdnm  eg PATDOCFD (new layout)
.  Global change tabnamaf  eg patdoctf
.  Global change tablidx1  eg PATDOCT1
.  Global change keyxxin   eg KEY8
.  Global change keyxxout  eg KEY9
.  Global change newwrite  eg WRDOCT1
.
.  Global change X02OTMA1  eg F57PTDOC
.  Global change x02otma1  eg f57ptdoc
.  Global change OUTMA1FD  eg PATDOCFD (new layout)
.  Global change outma1af  eg patdoctf
.  Global change OUTMA1A1  eg PATDOCT1
.  Global change KEY8      eg KEY8
.  Global change KEY8      eg KEY9
.  Global change WRMASA1  eg WRDOCT1
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       OUTSITFD/INC
.
FINDFILE  FILE      ASCII, FIXED=256
LOGFILE   FILE      ASCII, FIXED=256
.
. Enter the first or the shortest index from the original FD and rename
. the index OLDFILE1
.
.                                     ******** copy the old FD here *******
.                                     * comment out all continuing fields and
.                                     * prefix changing fields with "OLD" -
.                                     * remove old Indexes and Redefines     
.
..OLDFILE1  IFILE     SQL, FIXED=xxx, KEY="Uxx-xx"
OLDFILE1  IFILE    SQL, FIXED=593, KEY="U1-6,7-12,19-19,20-24"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.OMASITE   DIM       6         6        1  SITE ID
.OMACLIN   DIM       6         6        7  CLINIC ID
.OMATYP    DIM       6         6       13  CLINIC TYPE
.DOMADAY   DIM       1         1       19  DAY INDICATOR 1 = MONDAY
.                                                       2 = TUESDAY
.                                                       3 = WEDNESDAY
.                                                       4 = THURDAY
.                                                       5 = FRIDAY
.                                                       6 = SATURDAY
.                                                       7 = SUNDAY
.
.OMASTART  DIM       5         5       20  START TIME            (HH:MM)
.OMAFIN    DIM       5         5       25  FINISH BOOKING TIME   (HH:MM)
.OMAEND    DIM       5         5       30  END SESSION TIME      (HH:MM)
.
.OMACIND   DIM       3         3       35  CLINIC INDICATOR (CI) M=MEDICAL
.                                                               N=NON-MEDICAL
.OMAREG    DIM       1         1       38  REGISTRAR'S CLINIC(Y/N)
.OMAKDOC   DIM       1         1       39  KEYIN DOCTOR (Y/N)
.OMACOM1   DIM       40        40      40  COMMENTS LINE 1
.OMACOM2   DIM       40        40      80  COMMENTS LINE 2
.OMADURN   FORM      3         3      120  SLOT DURATION (MINS)
.OMANEWSL  FORM      1         2      123  # OF SLOTS FOR A NEW PATIENT
.OMASPCSL  FORM      1         2      125  # OF SLOTS FOR A SPECIAL PATIENT
.OMASTAT   FORM      1         2      127  STATUS   0 = ACTIVE
.                                                  1 = NON ACTIVE
.OMAFREQM  DIM       5         5      129  SESSION FREQUENCY  (MONTHLY)
.DOMASEQ   DIM       4         4      134  REPORT SEQUENCE
.OMALOCK   DIM       2         2      138  PORT NO. FOR LOCKING
.OMAFREQF  FORM      1         2      140  SESSION FREQUENCY FLAG
.                                               0 = MONTHLY
.                                               1 = YEARLY
.OMAFREQY  DIM       52        52     142  SESSION FREQUENCY  (YEARLY)
.OMADATE   DIM       8         8      194  LAST SESSION DATE OPENED
.OTMAAVIS  DIM       1         1      202  Allocate visit type when booking ?
.                                           'Y' = Yes; otherwise No
.OTMASTVT  DIM       1         1      203  Slot times visit type controlled ?
.                                           'Y' = Yes; otherwise No
.OTMASUPX  DIM       1         1      204  Suppress extended slots on displays ?
.                                           'Y' = Yes; otherwise No
.OTMARUSE  DIM       1         1      205  Using RUS Automated Extract
.                                           0=No, 1=Yes
.OTMADCMB  DIM       1         1      206  Display Comments at Booking Time
.                                           0=No, 1=Yes
.OTMAIXTA  DIM       1         1      207  Input EXTRA screen when Attending ?
.                                           'Y' = Yes; otherwise No
.OTMADEFD  DIM       6         6      208  Default Doctor Code
.OTMAUATR  DIM       1         1      214  Using Attendance Register (Y/N) ?
.                                           0=No, 1=Yes
.OTMADDNA  FORM      2         2      215  Disch. pat. after x no. of days
.OTMANDRP  FORM      2         2      217  No. of days for reappointment
.OTMALPTR  FORM      3         3      219  Letter to pat. on reappointment
.OTMALGPD  FORM      3         3      222  Letter to GP on discharge
.OTMALPTD  FORM      3         3      225  Letter to pat. on discharge
.OTMABKLT  FORM      3         3      228  Standard Booking Letter
.OTMARALT  FORM      3         3      231  Standard Reappointment Letter
.OTMARSLT  FORM      3         3      234  Standard Reschedule Letter (Patient)
.OTMACLSE  DIM       6         6      237  Clinic Session
.OTMAHOSP  DIM       3         3      243  Hospital code
.OTMACLOC  DIM       30        30     246  Clinic Location
.OTMAWDCL  DIM       1         1      276  Ward Clinic ? Y = yes , N = no
.OTMAWARD  DIM       3         3      277  Ward code if a ward clinic
.OTMALTYP  DIM       3         3      280  Location Type (Category CT)
.OTMADFSL  DIM       1         1      283  Default value for send letter
.OTMADFGN  DIM       1         1      284  Generate case notes on attendance(Y/N)
.OTMAGRPS  DIM       1         1      285  Group Session (Y/N) ?
.                                             0=No, 1=Yes
.DOTMASOV  DIM       1         1      286  Security Level to Overbook
.DOTMANOO  DIM       2         2      287  Number of overbookings allowed
.DOTMASLF  DIM       1         1      289  Security Level to Forcebook
.DOTMASLR  DIM       1         1      290  Security Level to use Reserved slots
.OTMASBKL  DIM       2         2      291  Booking Label Screen ID
.OTMASAPL  DIM       2         2      293  Appointment Label Screen ID
.OTMASPMM  DIM       2         2      295  PMI Mailing Label Screen ID
.OTMASGPM  DIM       2         2      297  G.P. Mailing Label Screen ID
.DOTMALPP  DIM       3         3      299  Letter to Patient on Cancellation
.DOTMASRL  DIM       3         3      302  Standard Reschedule Letter (Hospital)
.DOTMANCO  DIM       1         1      305  Non-Consultant Clinic (Y/N) ?
.                                             0=No, 1=Yes
.OTMALOCN  DIM       4         4      306  Location of Clinic
.OTMADTCO  DIM       8         8      310  Date Clinic Created
.OTMADTCC  DIM       8         8      318  Date Clinic Closed
.OTMAINS1  DIM      40        40      326  Instruction 1
.OTMAINS2  DIM      40        40      366  Instruction 2
.OTMAINS3  DIM      40        40      406  Instruction 3
.OTMALSER  DIM       3         3      446  Series booking letter
.OTMAUMCI  DIM       1         1      449  Using Medclaims (Y/N) ?
.                                             0=No, 1=Yes
.OTMAPROV  DIM      10        10      450  Provider Number
.OTMAMBST  DIM       4         4      460  Default CMBS item Template ID
.OTMAROOM  DIM       3         3      464  Room Number
.OTMALEDG  DIM       2         2      467  Ledger
.OTMACCTR  DIM      12        12      469  Cost centre
.OTMAMPRC  DIM       6         6      481  Medical Practice Code
.OTMAMBSA  DIM       1         1      487  Apply CMBS template to booking
.                                         0=No, 1=Yes
.OTMALDNA  DIM       3         3      488  Non Attendance Letter
.OTMAADEP  DIM       3         3      491  Allied Health Department
.OTMACENC  DIM       1         1      494  Collect Encounter Details
.                                         0=No, 1=Yes, 2=Yes plus DNA
.OTMACCAR  DIM       1         1      495  Collect Carer Details
.                                         0=No, 1=Yes
.OTMACTRN  DIM       1         1      496  Collect Transport Details
.                                         0=No, 1=Yes
.OTMADPSL  DIM       3         3      497  No. of Days Prior to Send Letter
.OTMACPMI  DIM       1         1      500  Using Confirm PMI
.                                         0=No, 1=Yes
.OTMACPAG  DIM       1         1      501  Using Check in Page
.                                         0=No, 1=Yes
.OTMACTEL  DIM       20        20     502  Clinic Telephone Number
.OTMANMDS  DIM       3         3      522  NMDS Code (Category NM)
.OTMACART  DIM       3         3      525  Care Type (Category CC)
.OTMASRVD  DIM       3         3      528  Service Delivery Mode (Category sd)
.OTMACLHR  DIM       1         1      531  Clinical High Risk
.                                         0=No, 1=Yes
.OTMAMLTD  DIM       1         1      532  Multidisciplinary Clinic
.                                         0=No, 1=Yes
.OTMAXPUL  DIM       1         1      533  Exclude from Pulling List
.                                         0=No, 1=Yes
.OTMAMREF  DIM       1         1      534  Mandatory Waiting/Active Referral
.                                         0=No, 1=Yes
.OTMATCOD  DIM       3         3      535  Tier 2 Code (category NC)
.OTMAHCLI  DIM       20        20     538  HERO Clinic Identifier
.                                         (SERVICE_PROVIDER_ID)
.OTMAHCAI  DIM       20        20     558  HERO Campus Identifier
.                                         (SERVICE_EVENT_ATTRIBUTE_TYPE_CODE -
.                                         04 -
.                                         Service Location ID)
.OTMAAOPN  DIM       1         1      578  Auto Open Clinic
.                                         Blank or 0 = No, 1 = Yes
.OTMAAOFR  DIM       1         1      579  Auto Open Frequency
.                                         1 = Weekly, 2 = 2 Weekly, 4 = 4 Weekly
.OTMAAOWM  DIM       5         5      580  Auto Open Week of Month
.                                         Blank or 0 = No, 1 = Yes
.                                         Position 1 to 5 = Week 1 to 5
.OTMAAOWA  DIM       3         3      585  Auto Open Number of Weeks in Advance
.OTMASPAR  DIM       5         5      588  Spare characters
.
.End of Record                       593
.
.                                     ******** copy the new FD here *******
.
OUTMA1A1  IFILE     SQL, FIXED=678, KEY="U1-6,7-12,19-19,20-24"
FILMA1A1  INIT      "ma1af"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
OMASITE   DIM       6         6        1  SITE ID
OMACLIN   DIM       6         6        7  CLINIC ID
OMATYP    DIM       6         6       13  CLINIC TYPE
DOMADAY   DIM       1         1       19  DAY INDICATOR 1 = MONDAY
.                                                       2 = TUESDAY
.                                                       3 = WEDNESDAY
.                                                       4 = THURDAY
.                                                       5 = FRIDAY
.                                                       6 = SATURDAY
.                                                       7 = SUNDAY
.
OMASTART  DIM       5         5       20  START TIME            (HH:MM)
OMAFIN    DIM       5         5       25  FINISH BOOKING TIME   (HH:MM)
OMAEND    DIM       5         5       30  END SESSION TIME      (HH:MM)
.
OMACIND   DIM       3         3       35  CLINIC INDICATOR (CI) M=MEDICAL
.                                                               N=NON-MEDICAL
OMAREG    DIM       1         1       38  REGISTRAR'S CLINIC(Y/N)
OMAKDOC   DIM       1         1       39  KEYIN DOCTOR (Y/N)
OMACOM1   DIM       40        40      40  COMMENTS LINE 1
OMACOM2   DIM       40        40      80  COMMENTS LINE 2
OMADURN   FORM      3         3      120  SLOT DURATION (MINS)
OMANEWSL  FORM      1         2      123  # OF SLOTS FOR A NEW PATIENT
OMASPCSL  FORM      1         2      125  # OF SLOTS FOR A SPECIAL PATIENT
OMASTAT   FORM      1         2      127  STATUS   0 = ACTIVE
.                                                  1 = NON ACTIVE
OMAFREQM  DIM       5         5      129  SESSION FREQUENCY  (MONTHLY)
DOMASEQ   DIM       4         4      134  REPORT SEQUENCE
OMALOCK   DIM       2         2      138  PORT NO. FOR LOCKING
OMAFREQF  FORM      1         2      140  SESSION FREQUENCY FLAG
.                                               0 = MONTHLY
.                                               1 = YEARLY
OMAFREQY  DIM       52        52     142  SESSION FREQUENCY  (YEARLY)
OMADATE   DIM       8         8      194  LAST SESSION DATE OPENED
OTMAAVIS  DIM       1         1      202  Allocate visit type when booking ?
.                                           'Y' = Yes; otherwise No
OTMASTVT  DIM       1         1      203  Slot times visit type controlled ?
.                                           'Y' = Yes; otherwise No
OTMASUPX  DIM       1         1      204  Suppress extended slots on displays ?
.                                           'Y' = Yes; otherwise No
OTMARUSE  DIM       1         1      205  Using RUS Automated Extract
.                                           0=No, 1=Yes
OTMADCMB  DIM       1         1      206  Display Comments at Booking Time
.                                           0=No, 1=Yes
OTMAIXTA  DIM       1         1      207  Input EXTRA screen when Attending ?
.                                           'Y' = Yes; otherwise No
OTMADEFD  DIM       6         6      208  Default Doctor Code
OTMAUATR  DIM       1         1      214  Using Attendance Register (Y/N) ?
.                                           0=No, 1=Yes
OTMADDNA  FORM      2         2      215  Disch. pat. after x no. of days
OTMANDRP  FORM      2         2      217  No. of days for reappointment
OTMALPTR  FORM      3         3      219  Letter to pat. on reappointment
OTMALGPD  FORM      3         3      222  Letter to GP on discharge
OTMALPTD  FORM      3         3      225  Letter to pat. on discharge
OTMABKLT  FORM      3         3      228  Standard Booking Letter
OTMARALT  FORM      3         3      231  Standard Reappointment Letter
OTMARSLT  FORM      3         3      234  Standard Reschedule Letter (Patient)
OTMACLSE  DIM       6         6      237  Clinic Session
OTMAHOSP  DIM       3         3      243  Hospital code
OTMACLOC  DIM       30        30     246  Clinic Location
OTMAWDCL  DIM       1         1      276  Ward Clinic ? Y = yes , N = no
OTMAWARD  DIM       3         3      277  Ward code if a ward clinic
OTMALTYP  DIM       3         3      280  Location Type (Category CT)
OTMADFSL  DIM       1         1      283  Default value for send letter
OTMADFGN  DIM       1         1      284  Generate case notes on attendance(Y/N)
OTMAGRPS  DIM       1         1      285  Group Session (Y/N) ?
.                                             0=No, 1=Yes
DOTMASOV  DIM       1         1      286  Security Level to Overbook
DOTMANOO  DIM       2         2      287  Number of overbookings allowed
DOTMASLF  DIM       1         1      289  Security Level to Forcebook
DOTMASLR  DIM       1         1      290  Security Level to use Reserved slots
OTMASBKL  DIM       2         2      291  Booking Label Screen ID
OTMASAPL  DIM       2         2      293  Appointment Label Screen ID
OTMASPMM  DIM       2         2      295  PMI Mailing Label Screen ID
OTMASGPM  DIM       2         2      297  G.P. Mailing Label Screen ID
DOTMALPP  DIM       3         3      299  Letter to Patient on Cancellation
DOTMASRL  DIM       3         3      302  Standard Reschedule Letter (Hospital)
DOTMANCO  DIM       1         1      305  Non-Consultant Clinic (Y/N) ?
.                                             0=No, 1=Yes
OTMALOCN  DIM       4         4      306  Location of Clinic
OTMADTCO  DIM       8         8      310  Date Clinic Created
OTMADTCC  DIM       8         8      318  Date Clinic Closed
OTMAINS1  DIM      40        40      326  Instruction 1
OTMAINS2  DIM      40        40      366  Instruction 2
OTMAINS3  DIM      40        40      406  Instruction 3
OTMALSER  DIM       3         3      446  Series booking letter
OTMAUMCI  DIM       1         1      449  Using Medclaims (Y/N) ?
.                                             0=No, 1=Yes
OTMAPROV  DIM      10        10      450  Provider Number
OTMAMBST  DIM       4         4      460  Default CMBS item Template ID
OTMAROOM  DIM       3         3      464  Room Number
OTMALEDG  DIM       2         2      467  Ledger
OTMACCTR  DIM      12        12      469  Cost centre
OTMAMPRC  DIM       6         6      481  Medical Practice Code
OTMAMBSA  DIM       1         1      487  Apply CMBS template to booking
.                                         0=No, 1=Yes
OTMALDNA  DIM       3         3      488  Non Attendance Letter
OTMAADEP  DIM       3         3      491  Allied Health Department
OTMACENC  DIM       1         1      494  Collect Encounter Details
.                                         0=No, 1=Yes, 2=Yes plus DNA
OTMACCAR  DIM       1         1      495  Collect Carer Details
.                                         0=No, 1=Yes
OTMACTRN  DIM       1         1      496  Collect Transport Details
.                                         0=No, 1=Yes
OTMADPSL  DIM       3         3      497  No. of Days Prior to Send Letter
OTMACPMI  DIM       1         1      500  Using Confirm PMI
.                                         0=No, 1=Yes
OTMACPAG  DIM       1         1      501  Using Check in Page
.                                         0=No, 1=Yes
OTMACTEL  DIM       20        20     502  Clinic Telephone Number
OTMANMDS  DIM       3         3      522  NMDS Code (Category NM)
OTMACART  DIM       3         3      525  Care Type (Category CC)
OTMASRVD  DIM       3         3      528  Service Delivery Mode (Category sd)
OTMACLHR  DIM       1         1      531  Clinical High Risk
.                                         0=No, 1=Yes
OTMAMLTD  DIM       1         1      532  Multidisciplinary Clinic
.                                         0=No, 1=Yes
OTMAXPUL  DIM       1         1      533  Exclude from Pulling List
.                                         0=No, 1=Yes
OTMAMREF  DIM       1         1      534  Mandatory Waiting/Active Referral
.                                         0=No, 1=Yes
OTMATCOD  DIM       3         3      535  Tier 2 Code (category NC)
OTMAHCLI  DIM       20        20     538  HERO Clinic Identifier
.                                         (SERVICE_PROVIDER_ID)
OTMAHCAI  DIM       20        20     558  HERO Campus Identifier
.                                         (SERVICE_EVENT_ATTRIBUTE_TYPE_CODE -
.                                         04 -
.                                         Service Location ID)
OTMAAOPN  DIM       1         1      578  Auto Open Clinic
.                                         Blank or 0 = No, 1 = Yes
OTMAAOFR  DIM       1         1      579  Auto Open Frequency
.                                         1 = Weekly, 2 = 2 Weekly, 4 = 4 Weekly
OTMAAOWM  DIM       5         5      580  Auto Open Week of Month
.                                         Blank or 0 = No, 1 = Yes
.                                         Position 1 to 5 = Week 1 to 5
OTMAAOWA  DIM       3         3      585  Auto Open Number of Weeks in Advance
OTMAHCP1  DIM       10        10     588  Additional HCP 1 Code
OTMAHCP2  DIM       10        10     598  Additional HCP 1 Code
OTMAHCP3  DIM       10        10     608  Additional HCP 1 Code
OTMAHCP4  DIM       10        10     618  Additional HCP 1 Code
OTMASPAR  DIM       50        50     628  Spare characters
.
.End of Record                       678
.
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       200
.
DEFPATH   DIM       60
DIM60     DIM       60
DISNUM    FORM      4
.
ERORTEXT  DIM       80
.
INPFILE   DIM       8
CFILEPRE  DIM       3   * Current Open File Prefix for Outpatient Files
VSITE     DIM       6
.
MINUS3    FORM      "-3"
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
SAVEFLG   FORM      1
.
USINGORC  DIM       1
.
.
. PROGRAM CONSTANTS
. -----------------
CREATEXT  INIT    "Log file created "
DASHLINE  INIT    "--------------------------------------------------":
                  "------------------------------"
ENDGTEXT  INIT    "Ending   fixit (Option "
OPENTEXT  INIT    "Log file opened "
SP60      INIT    "                                                            "
STARTTXT  INIT    "Starting fixit (Option "
.
.
.
PRGNAM    INIT      "CONVERSION OUTMA1FD"
PRGID     INIT      "X02OTMA1"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
.
          CALL      OLOG0000                      * create/open log file
MAIN0500  CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000              * run c-isam fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KSIT0000                      * Keyin OP Site Code
          BRANCH    EXIT,MAIN0000
.
          CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9000
.
.         Running c-isam fix
.
MAIN6000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9000
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/zip saved file
.
MAIN9000  CALL      CLOG0000                      * close log file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
.
          DISPLAY   *P56:24,"Opening outsitaf";
          OPEN      OUTSITA1,"outsitaf" * Open the site file
.          
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run c-isam fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                                KSIT0000             Called by: MAIN0000   *
.*  Keyin outpatient Site code to get file prefix                            *
.*****************************************************************************
.
KSIT0000  MOVE      SP70,VSITE
          KEYIN     *P1:10,*EL,"Outpatient Site Code : ",VSITE;
.
          PACK      VSITE,VSITE,SP70
.
          MATCH     SP6,VSITE           * Valid site ? Blank
          GOTO      KSIT9100 IF EQUAL   *
.
          MOVE      VSITE,KEY6          * Key for the site file
          CALL      RDSITA1             * Read the site record
          BRANCH    OVRCD,KSIT9000      * Invalid site.
.
          MOVE      OSTFILE,CFILEPRE    * Save Current File Prefix
.
          DISPLAY   *P35:10,"(Prefix: ",CFILEPRE,")";
          MOVE      ZERO,EXIT
          GOTO      KSIT9999
.
.         Invalid call to program. Common not set up with correct site info
.
KSIT9000  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "Invalid Site Code Entered. ";
          CALL      HITENTER
          GOTO      KSIT0000
.
KSIT9100  MOVE      ONE,EXIT
          GOTO      KSIT9999
.
KSIT9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:12,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  PACK      INPFILE,CFILEPRE,FILMA1A1,SP70
          CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:14,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:15,*EL,*DV,*RV,OLDPATH:
                    *P10:15,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:14,*EL,"Keyin path for saving original file: ":
                    *P10:15,*EL,*DV,*RV,OLDPATH:
                    *P10:15,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:15,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:17,*EL,"Keyin path for the new file: ":
                    *P10:18,*EL,*DV,*RV,NEWPATH:
                    *P10:18,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:18,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
.
PREP0000  CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
.
          CALL      SLOG0000                      * write start to log file
.
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat ",CMDLINE
          APPEND    INPFILE,CMDLINE
          APPEND    ".dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "s",CMDLINE
          APPEND    INPFILE,CMDLINE
          APPEND    ".dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat ",CMDLINE
          APPEND    INPFILE,CMDLINE
          APPEND    ".idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "s",CMDLINE
          APPEND    INPFILE,CMDLINE
          APPEND    ".idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    INPFILE,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Unable to remove the original file.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "s",DIM60
          APPEND    INPFILE,DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            MOVE      "Unable to open saved original file.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    INPFILE,NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.      eg    APPEND    " 195 UC9-16",CMDLINE
.
          APPEND    " 678 UC1-6,7-12,19-19,20-24",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has 
.  to be repeated with for each individual index
.
. Index 2
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC1-6,13-18,7-12,19-19,20-24",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
. Index 3
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC1-6,134-137,7-12,19-19,20-24",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.
          OPEN      OUTMA1A1,NEWFILE
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          GOTO      PREP9999
.
PREP5000  MOVE      "Old file not found.  ",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  MOVE      "Unable to copy original file.  (Error: ",ERORTEXT
          ENDSET    ERORTEXT
          APPEND    TASKID,ERORTEXT
          APPEND    ")  ",ERORTEXT
          RESET     ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
            MOVE      "File does not exist",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
              MOVE      "File has already been converted",ERORTEXT
              WRITE     LOGFILE,SEQ;ERORTEXT
            ELSE
              DISPLAY   "has caused an IO error - ";
              MOVE      "File has caused an IO Error",ERORTEXT
              WRITE     LOGFILE,SEQ;ERORTEXT
            ENDIF
          ENDIF
          MOVE      "File will be bypassed",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *+,ERORTEXT
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY18,SP70
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
.  All necessary changes to the files field lengths, date changes
.  or any other sort of modification must be made here
.
          PACK      OTMAHCP1,SP70               * Additional HCP 1 Code
          PACK      OTMAHCP2,SP70               * Additional HCP 2 Code
          PACK      OTMAHCP3,SP70               * Additional HCP 3 Code
          PACK      OTMAHCP4,SP70               * Additional HCP 4 Code
.
          CALL      WRMASA1                     * write to new file
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     OUTMA1A1                      * close new file
          CLOSE     OLDFILE1                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS:
                    *P1:24;
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
.
VALD0000  MOVE      ZERO,EXIT
.
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            MOVE      "Directory path must end with a slash (/) ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Directory does not exist ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or zip save file                                     *
.**********************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "s",DIM60
          APPEND    INPFILE,DIM60
          APPEND    "*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "gzip -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Saved file not zipped or removed.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:24,*EL,*B,"Finished processing  ",CDATE,SP1,CTIMEIS:
                    ".  ";
          CALL      HITENTER
.
          CALL      ELOG0000                     * write an end log entry
.
ENDP9999  RETURN
+
.**********************************************************************
.*        DEFT0000 -  Get the default directory                       *
.**********************************************************************
.
DEFT0000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "ldat ",CMDLINE
          APPEND    INPFILE,CMDLINE
          APPEND    ".dat > temp.txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  CLEAR     ERORTEXT 
          APPEND    "Error finding ",ERORTEXT
          APPEND    INPFILE,ERORTEXT
          APPEND    ".dat ",ERORTEXT
          RESET     ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.*                            OLOG0000                                       *
.*                    Open the log file                                      *
.*****************************************************************************
.
OLOG0000  CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      LOGFILE,"x02otma1.log"       * open log file
          TRAPCLR   IO
          IF        OVRCD = 1
            PREP      LOGFILE,"x02otma1.log"     * create and open log file
            WRITE     LOGFILE,MINUS3;DASHLINE
            WRITE     LOGFILE,SEQ;CREATEXT,CDATE,SP1,CTIMEIS
          ELSE
            WRITE     LOGFILE,MINUS3;DASHLINE
            WRITE     LOGFILE,SEQ;OPENTEXT,CDATE,SP1,CTIMEIS
          ENDIF
.
OLOG9999  RETURN
+
.*****************************************************************************
.*                            CLOG0000                                       *
.*                    Close the log file                                     *
.*****************************************************************************
.
CLOG0000  CLOSE     LOGFILE
.
CLOG9999  RETURN
+
.*****************************************************************************
.*                            SLOG0000                                       *
.*                    Write a start log to the log file                      *
.*****************************************************************************
.
SLOG0000  WRITE     LOGFILE,SEQ;DASHLINE
          WRITE     LOGFILE,SEQ;STARTTXT,COPTION,RBRACK,SP1,CDATE,SP1:
                                   CTIMEIS
.
SLOG9999  RETURN
+
.*****************************************************************************
.*                            ELOG0000                                       *
.*                    Write an end log to the log file                       *
.*****************************************************************************
.
ELOG0000  WRITE     LOGFILE,SEQ;ENDGTEXT,COPTION,RBRACK,SP1,CDATE,SP1:
                                   CTIMEIS
ELOG9999  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
.                  ******************************    OLD IO ROUTINES
.                  Copy the Positional Read (RS or RDS) from original I/O file,
.                  - change Label to READSOLD and the index name to OLDFILE1
.
READSOLD  MOVE      ZERO,OVRCD
          READ      OLDFILE1,KEY18;;
          RETURN
.
.                  Copy the Read Next (RK or RDK) from the original I/O file
.                  - change Label to READKOLD and the index name to OLDFILE1
.                  - change any fields that are changing to a prefix of OLD,
.                    eg. PTEDSPAR becomes OLDDSPAR
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;OMASITE,OMACLIN,OMATYP,DOMADAY,OMASTART:
                                 OMAFIN,OMAEND,OMACIND,OMAREG,OMAKDOC:
                                 OMACOM1,OMACOM2,OMADURN,OMANEWSL,OMASPCSL:
                                 OMASTAT,OMAFREQM,DOMASEQ,OMALOCK,OMAFREQF:
                                 OMAFREQY,OMADATE,OTMAAVIS,OTMASTVT,OTMASUPX:
                                 OTMARUSE,OTMADCMB,OTMAIXTA,OTMADEFD,OTMAUATR:
                                 OTMADDNA,OTMANDRP,OTMALPTR,OTMALGPD,OTMALPTD:
                                 OTMABKLT,OTMARALT,OTMARSLT,OTMACLSE,OTMAHOSP:
                                 OTMACLOC,OTMAWDCL,OTMAWARD,OTMALTYP,OTMADFSL:
                                 OTMADFGN,OTMAGRPS,DOTMASOV,DOTMANOO,DOTMASLF:
                                 DOTMASLR,OTMASBKL,OTMASAPL,OTMASPMM,OTMASGPM:
                                 DOTMALPP,DOTMASRL,DOTMANCO,OTMALOCN,OTMADTCO:
                                 OTMADTCC,OTMAINS1,OTMAINS2,OTMAINS3,OTMALSER:
                                 OTMAUMCI,OTMAPROV,OTMAMBST,OTMAROOM,OTMALEDG:
                                 OTMACCTR,OTMAMPRC,OTMAMBSA,OTMALDNA,OTMAADEP:
                                 OTMACENC,OTMACCAR,OTMACTRN,OTMADPSL,OTMACPMI:
                                 OTMACPAG,OTMACTEL,OTMANMDS,OTMACART,OTMASRVD:
                                 OTMACLHR,OTMAMLTD,OTMAXPUL,OTMAMREF,OTMATCOD:
                                 OTMAHCLI,OTMAHCAI,OTMAAOPN,OTMAAOFR,OTMAAOWM:
                                 OTMAAOWA,OTMASPAR
.
          GOTO      OVERCOND IF OVER
          RETURN
.
.                  ******************************    NEW IO ROUTINES
.                  Copy the Write statement from the new I/O file           
.                  (no changes are needed to this routine)               
.
WRMASA1   RESET     KEY18
.
          WRITE     OUTMA1A1,KEY18;OMASITE,OMACLIN,OMATYP,DOMADAY,OMASTART:
                                 OMAFIN,OMAEND,OMACIND,OMAREG,OMAKDOC:
                                 OMACOM1,OMACOM2,OMADURN,OMANEWSL,OMASPCSL:
                                 OMASTAT,OMAFREQM,DOMASEQ,OMALOCK,OMAFREQF:
                                 OMAFREQY,OMADATE,OTMAAVIS,OTMASTVT,OTMASUPX:
                                 OTMARUSE,OTMADCMB,OTMAIXTA,OTMADEFD,OTMAUATR:
                                 OTMADDNA,OTMANDRP,OTMALPTR,OTMALGPD,OTMALPTD:
                                 OTMABKLT,OTMARALT,OTMARSLT,OTMACLSE,OTMAHOSP:
                                 OTMACLOC,OTMAWDCL,OTMAWARD,OTMALTYP,OTMADFSL:
                                 OTMADFGN,OTMAGRPS,DOTMASOV,DOTMANOO,DOTMASLF:
                                 DOTMASLR,OTMASBKL,OTMASAPL,OTMASPMM,OTMASGPM:
                                 DOTMALPP,DOTMASRL,DOTMANCO,OTMALOCN,OTMADTCO:
                                 OTMADTCC,OTMAINS1,OTMAINS2,OTMAINS3,OTMALSER:
                                 OTMAUMCI,OTMAPROV,OTMAMBST,OTMAROOM,OTMALEDG:
                                 OTMACCTR,OTMAMPRC,OTMAMBSA,OTMALDNA,OTMAADEP:
                                 OTMACENC,OTMACCAR,OTMACTRN,OTMADPSL,OTMACPMI:
                                 OTMACPAG,OTMACTEL,OTMANMDS,OTMACART,OTMASRVD:
                                 OTMACLHR,OTMAMLTD,OTMAXPUL,OTMAMREF,OTMATCOD:
                                 OTMAHCLI,OTMAHCAI,OTMAAOPN,OTMAAOFR,OTMAAOWM:
                                 OTMAAOWA,OTMAHCP1,OTMAHCP2,OTMAHCP3,OTMAHCP4:
                                 OTMASPAR
.
         RETURN
.
.
          INC       STD001IO
.
          INC       OUTSITIO/INC

