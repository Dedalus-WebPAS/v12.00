.******************************************************************************
.* Include Name   : KYPCPDSC.PBL                                              *
.*                                                                            *
.* Description    : Keyin of a code from the PCPDSCFD                         *
.*                                                                            *
.* Author         : LOFTY                                                     *
.*                                                                            *
.* Parameters     : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF9 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  PCDSTYPE - the type of code to enter                      *
.*                                                                            *
.* Returns        : PCDSCODE - Keyed in code                                  *
.*                  PCDSDESC - Description                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.* On return      : You must display the description yourself.                *
.*                                                                            *
.* Modifications  :                                                           *
.*      V9.02.00  30/12/2002  Tracey Nguyen  srf 23497                        *
.*                  r5.10 Care Plans Port to V9                               *
.******************************************************************************
KYPCPDSC  MOVE      ONE,CQUEST              * set ? as not entered
          MOVE      ECOL,CCOL
          MOVE      EVERT,CVERT
          MOVE      PCDSTYPE,CKYIDEF2       * save the type of code
.
KPCDS100  MOVE      CKYIDEF9,PCDSCODE
          KEYIN     *PCCOL:CVERT,*DV,PCDSCODE:
                    *PCCOL:CVERT,*V2LON,*RV,PCDSCODE:
                    *PCCOL:CVERT,*DV,PCDSCODE
          PACK      PCDSCODE,PCDSCODE,SP9
          MATCH     SP9,PCDSCODE
          GOTO      KPCDS790 IF EQUAL       * nothing entered
          MATCH     EXITCHAR,PCDSCODE
          GOTO      KPCDS820 IF EQUAL       * exitchar entered
          MATCH     QUEST,PCDSCODE
          GOTO      KPCDS200 IF NOT EQUAL   * ? not entered
.
.         ? entered
.
          IF        CQUEST = ONE
            MOVE      "0",HLEF                * save all of the screen
            CALL      GETSCR00                * save screen
            MOVE      ZERO,CQUEST             * set as ? performed
          ENDIF
.
          CALL      PCPDSCDS                * display valid entries
.
          IF        CNEWDS = ZERO
            CALL      PUTSCR00                * redisplay screen
            MOVE      ONE,CQUEST              * set as ? not performed
            MOVE      ECOL,CCOL
            MOVE      EVERT,CVERT
          ELSE
            DISPLAY   *P1:24,"Input the code : ",*EL;
            MOVE      "24",CVERT
            MOVE      "18",CCOL
            MOVE      ZERO,CQUEST             * set ? as entered
          ENDIF
          GOTO      KPCDS100
.
.         validate what was entered
.
KPCDS200  MOVE      ONE,PCDSLINE
          PACK      KEY13,CKYIDEF2,PCDSCODE,PCDSLINE
          CALL      RDPCDSC1
          BRANCH    OVRCD,KPCDS400          * invalid code
.
          MOVE      ZERO,EXIT               * set as valid code
          GOTO      KPCDS800
.
.         invalid code
.
KPCDS400  DISPLAY   *P1:24,*B,"Invalid Code.  ",*EL;
          CALL      HITENTER
          BRANCH    CQUEST,KPCDS100
          DISPLAY   *P1:24,"Input the code : ",*EL;
          GOTO      KPCDS100
.
.         nothing entered
.
KPCDS790  MOVE      SP30,PCDSDESC            * clear desc
          MOVE      ONE,EXIT                * set exit flag
          COMPARE   ZERO,CKYIMAND
          GOTO      KPCDS800 IF EQUAL       * valid entry
.
.         field is mandatory
.
          DISPLAY   *P1:24,*B,"Field is mandatory.  ",*EL;
          CALL      HITENTER
          BRANCH    CQUEST,KPCDS100
          DISPLAY   *P1:24,"Input the code : ",*EL;
          GOTO      KPCDS100
.
.         valid entry
.
KPCDS800  IF        CQUEST = ZERO
            CALL      PUTSCR00                * redisplay screen
          ENDIF
          DISPLAY   *PECOL:EVERT,*V2LON,PCDSCODE
          GOTO      KPCDS999 
.
.         exitchar entered
.
KPCDS820  MOVE      TWO,EXIT                * EXITCHAR entered
          MOVE      SP9,PCDSCODE            * clear code
          MOVE      SP30,PCDSDESC            * clear desc
          IF        CQUEST = ZERO
            CALL      FRESCR00                * free the screen
          ENDIF
.
KPCDS999  RETURN
+
