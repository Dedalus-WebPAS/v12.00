.*****************************************************************************
.*    Program      : F04PMELF                                                *
.*    Description  : Conversion pmselfaf to new File layout                  *
.*                 : Load existing records from pmsoecaf to pmselfaf to      *
.*                 : store OEC Details of Request                            *
.*                                                                           *
.*    Author       : Peter Vela       CAR 292257                             *
.*    Date         : 28/04/2014                                              *
.*                                                                           *
.*    Modifications: V10.04.02 22/05/2014 Peter Vela CAR 292257              *
.*                             Removed c-isam fix option                     *
.*                   V10.04.01 28/04/2014 Peter Vela CAR 292257              *
.*                             Created program                               *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001FD
.
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PMSOECFD/INC
          INC       PMSOESFD/INC
          INC       PMSELFFD/INC
          INC       PMSCCDFD/INC
          INC       PATELGFD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
          INC       PATMI1FD/INC
.
FINDFILE  FILE      ASCII, FIXED=256
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       200
.
DEFPATH   DIM       60
DIM8      DIM       8
DIM20     DIM       20
DIM60     DIM       60
DISNUM    FORM      4
.
INPFILE   DIM       8
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
SAVKEY11  DIM       11
SAVEFLG   FORM      1
.
.
. PROGRAM CONSTANTS
. -----------------
CATCT     INIT      "ct"
SP60      INIT    "                                                            "
REPUNIQ   INIT    "yzxywxvwuvtustrsqrpqopnomnlmkljkijhighfgefdecdbcab"
TILDA40   INIT      "~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~"
.
.
.
PRGNAM    INIT      "CONVERSION PMSELFFD"
PRGID     INIT      "F04PMELF"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000              * run c-isam fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN0000:               * no
                          MAIN9999                * cancel
.
.         Running fix program
.
MAIN5000  CALL      READ0000                      * read old records and write
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
.
          OPEN      PATCODE1,"patcodes"
          OPEN      PMSOECA1,"pmsoecaf"
          OPEN      PMSOESA1,"pmsoesaf"
          OPEN      PMSELFA1,"pmselfaf"         
          OPEN      PMSCCDA1,"pmsccdaf"
          OPEN      PATELGA1,"patelgaf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATMI1A1,"patmi1af"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,SEVENTY9;*82,PTCNDSCI
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run c-isam fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY11,SP60
          CALL      RSPMOEC1                      * position at start
READ1000  CALL      RKPMOEC1                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MOVE      PMOEVISN,DIM8
          LJUSTIFY  DIM8
          UNPACK    DIM8,DIM1
          TYPE      DIM1
          GOTO      READ1000 IF NOT EQUAL
.
          MATCH     SP70,PMOEELEG
          GOTO      READ1000 IF NOT EQUAL
.
. read all tables needed for write to pmselfaf
.
          MATCH     SP70,PMOEURNO
          GOTO      READ1000 IF EQUAL
.
          PACK      KEY8,PMOEURNO
          CALL      RDMAST1
          BRANCH    OVRCD,READ1000
.
          PACK      KEY8,PMOEURNO
          CALL      RDPMPX21 
          BRANCH    OVRCD,READ1000
.
          MATCH     SP70,PMOEVISN
          GOTO      READ1000 IF EQUAL
.
          PACK      KEY8,PMOEVISN,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,READ1000
.
          PACK      KEY8,PMOEVISN,SP70
          CALL      RDPTELG1
          BRANCH    OVRCD,READ1000
.
          PACK      KEY8,PMOEVISN,SP70
          CALL      RDMISS1
          IF        OVRCD=1
            CALL      CLPATMIS
          ENDIF
.
          CALL      CLRELF00
.
          PACK      SAVKEY11,PMOEVISN,PMOECNTR,SP70
.
READ2000  CALL      GELN0000           * Generate Eligibility Number
          BRANCH    EXIT,READ7000
.
          MOVE      SAVKEY11,KEY11
          CALL      RDPMOEC1
.
          MOVE      PMOEURNO,PMEFURNO
          MOVE      ZERO,PMEFOECC
          MOVE      PTITL,PMEFTITL
          PACK      PMEFFNAM,PMPXFNAM,SP70
          PACK      PMEFSNAM,PTMASNAM,SP70
          PACK      PMEFHFGN,PMPXHFGN,SP70
          PACK      PMEFHFSN,PMPXHFSN,SP70
          MOVE      PSEX,PMEFGEND
          MOVE      PBDATE,PMEFBDAT
          MOVE      ADATE,PMEFEADT
          MOVE      PMVXIDUS,PMEFISTY
          MOVE      SP70,PMEFDINT
          MOVE      ASTAY,PMEFPLOS
          MOVE      PTELSPEA,PMEFPEAI
          MOVE      PTELEADM,PMEFEMAD
          MOVE      PMVXPILC,PMEFPRIC
          MOVE      AMBS,PMEFPRIM
          MOVE      ACLAIM,PMEFCLTY
          MOVE      AFUNDH,PMEFHFND
          MOVE      AFNDTB,PMEFHFNT
          MOVE      AFNDMM,PMEFHFMN
          MOVE      ADIAG1,PMEFDIAG
.            
          MOVE      SP70,PMEFSRV1
          PACK      KEY14,PMOEVISN,PMOECNTR,SP1,SP1,ONE,SP70
          CALL      RDPMOES1
          IF        OVRCD=0
            MOVE      PMOSITEM,PMEFSRV1
          ENDIF
.
          MOVE      SP70,PMEFSRV2
          PACK      KEY14,PMOEVISN,PMOECNTR,SP1,SP1,TWO,SP70
          CALL      RDPMOES1
          IF        OVRCD=0
            MOVE      PMOSITEM,PMEFSRV2
          ENDIF
.
          MOVE      SP70,PMEFSRV3
          PACK      KEY14,PMOEVISN,PMOECNTR,SP1,SP1,THREE,SP70
          CALL      RDPMOES1
          IF        OVRCD=0
            MOVE      PMOSITEM,PMEFSRV3
          ENDIF
.
          MOVE      SP70,PMEFSRV4
          PACK      KEY14,PMOEVISN,PMOECNTR,SP1,SP1,FOUR,SP70
          CALL      RDPMOES1
          IF        OVRCD=0
            MOVE      PMOSITEM,PMEFSRV4
          ENDIF
.
          MOVE      SP70,PMEFSRV5
          PACK      KEY14,PMOEVISN,PMOECNTR,SP1,SP1,FIVE,SP70
          CALL      RDPMOES1
          IF        OVRCD=0
            MOVE      PMOSITEM,PMEFSRV5
          ENDIF
.
          MOVE      "F04PMELF   ",PMEFCUID
          MOVE      PMOECDTE,PMEFCDAT
          MOVE      PMOECTIM,PMEFCTIM
.
          CALL      CONC0000                       * DVA conc card on file ?
          IF        EXIT=1
            PACK      PMEFDVAN,PREPAT,SP20         * No
          ELSE
            MOVE      DIM20,PMEFDVAN               * yes
          ENDIF
.
          MOVE      PMOEVISN,PMEFVISN
          MOVE      PMOECNTR,PMEFCNTR
          MOVE      ONE,PMEFFLAG
          PACK      PMEFSPAR,SP70,SP70
.
          PACK      KEY8,PMEFELGN,SP70
          CALL      RAPMELF1
          IF        OVRCD=0
            GOTO      READ2000 
          ENDIF
.
          CALL      WRPMELF1
.
          MOVE      PMEFELGN,PMOEELEG
.
          CALL      UPPMOEC1
.
          GOTO      READ1000                      * get next record
.
READ6000  CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS:
                    *P1:24;
          CALL      HITENTER
.
          GOTO      READ9999 
.
READ7000  DISPLAY   *P1:22,"CONTACT iSoft - eligibility number allocation full.":
                    *P1:24;
          CALL      HITENTER
.
          GOTO      READ9999
.
READ9999  RETURN
+
.-----------------------------------------------------------
.         Clear free format OEC fields
.-----------------------------------------------------------
CLRELF00  MOVE      SP70,PMEFELGN
          MOVE      SP70,PMEFURNO
          MOVE      SP70,PMEFOECC
          MOVE      SP70,PMEFTITL
          MOVE      SP70,PMEFFNAM
          MOVE      SP70,PMEFSNAM
          MOVE      SP70,PMEFHFGN
          MOVE      SP70,PMEFHFSN
          MOVE      SP70,PMEFGEND
          MOVE      SP70,PMEFBDAT
          MOVE      SP70,PMEFEADT
          MOVE      SP70,PMEFISTY
          MOVE      SP70,PMEFPLOS
          MOVE      SP70,PMEFDINT
          MOVE      SP70,PMEFPEAI
          MOVE      SP70,PMEFEMAD
          MOVE      SP70,PMEFPRIC
          MOVE      SP70,PMEFPRIM
          MOVE      SP70,PMEFCLTY
          MOVE      SP70,PMEFHFND
          MOVE      SP70,PMEFHFNT
          MOVE      SP70,PMEFHFMN
          MOVE      SP70,PMEFDIAG
          MOVE      SP70,PMEFSRV1
          MOVE      SP70,PMEFSRV2
          MOVE      SP70,PMEFSRV3
          MOVE      SP70,PMEFSRV4
          MOVE      SP70,PMEFSRV5
          MOVE      SP70,PMEFCUID
          MOVE      SP70,PMEFCDAT
          MOVE      SP70,PMEFCTIM
          MOVE      SP70,PMEFDVAN
          MOVE      SP70,PMEFVISN
          MOVE      SP70,PMEFCNTR
          MOVE      SP70,PMEFFLAG
          PACK      PMEFSPAR,SP70,SP70
.
CLRELF99  RETURN
.
.-----------------------------------------------------------
.        Generate Free Format OEC Eligibility Number
.-----------------------------------------------------------
GELN0000  MOVE      ZERO,EXIT
.
          MOVE      "118",PRXCODE
          CALL      GETSLK00
          READ      CONTROLF,HUND18;*117,PTCNELGN
.
GELN0100  MOVE      PTCNELGN,PMEFELGN
          UNPACK    PTCNELGN,ANS,KEY7
          MATCH     "a",ANS
          GOTO      GELN1150 IF LESS
          MOVE      ZERO,F7
          MOVE      KEY7,F7
.
GELN1100  COMPARE   "9999999",F7               * at the end of the group
          GOTO      GELN1110 IF NOT EQUAL
.
          MATCH     "z",ANS
          GOTO      GELN9000 IF EQUAL
.
          REP       REPUNIQ,ANS
          MOVE      ZERO,F7
.
GELN1110  ADD       ONE,F7
          MOVE      F7,D7
          REP       " 0",D7
.
          PACK      KEY8,ANS,D7,SP70
          CALL      RDPMELF1
          IF        OVRCD=ONE
            PACK      KEY11,ANS,D7,SP1,SP1,ONE,SP70
            CALL      RDPMOEC1
            IF        OVRCD=ONE
              PACK      KEY14,ANS,D7,SP1,SP1,ONE,SP70
              CALL      RSPMOES1
              CALL      RKPMOES1
              BRANCH    OVRCD,GELN9500
.
              PACK      KEY8,ANS,D7,SP70
              MATCH     KEY8,PMOSVISN
              GOTO      GELN9500 IF NOT EQUAL
            ENDIF
          ENDIF
.
          GOTO      GELN1100
.
. no records
.
GELN1150  MOVE      ONE,F7
          MOVE      "a",ANS
.
          MOVE      F7,D7
          REP       " 0",D7
.
          PACK      KEY8,ANS,D7,SP70
          GOTO      GELN9500
.
GELN9000  MOVE      ONE,EXIT                  * Passcode allocation full
          GOTO      GELN9999
.
GELN9500  MOVE      KEY8,PTCNELGN
          WRITAB    CONTROLF,HUND18;*117,PTCNELGN
.
          MATCH     "00000000",PMEFELGN
          GOTO      GELN0100 IF EQUAL
.
GELN9999  CALL      RELSLK00
          RETURN
.
.*****************************************************************************
.*                              CONC0000           Called by: WPAT0000       *
.*     See if the patient has concession card of DVA type on file            *
.* Requires: PURNO - U/R number                                              *
.* Returns:  EXIT  0 = valid concession card found                           *
.*                 1 = no valid concession card found                        *
.*           DIM20 - Concession Card Number                                  *
.*****************************************************************************
.
CONC0000  PACK      KEY19,PURNO,TILDA40
          CALL      RSPMCCD1                     * position on U/R
CONC0500  CALL      RPPMCCD1                     * read previous record
          BRANCH    OVRCD,CONC9100               * eof - nothing found
.
          MATCH     PURNO,PMCDURNO               * same U/R still ?
          GOTO      CONC9100 IF NOT EQUAL        * no - nothing found
.
.         Check if the card type is DVA (Cat ct, Indicator 1)
.
          PACK      KEY5,CATCT,PMCDCTYP
          CALL      RDCODE1                      * code on file ?
          BRANCH    OVRCD,CONC0500               * no - ignore record
.
          MATCH     SP1,TCINDC1                  * blank indicator 1 ?
          GOTO      CONC0500 IF EQUAL            * yes - ignore record
.
          TYPE      TCINDC1                      * numeric indicator 1 ?
          GOTO      CONC0500 IF NOT EQUAL        * no - ignore record
.
          MATCH     "3",TCINDC1                  * DVA card type ?
          GOTO      CONC0500 IF NOT EQUAL        * no - ignore record
.
.         A DVA concession card has been found, so check
.         if there is an expiry date
.
          MATCH     SP8,PMCDEXDT                 * expiry date blank ?
          GOTO      CONC1000 IF EQUAL            * yes - assume valid
.
.         There is an expiry date for the card, so check that the
.         card is current
.
          CALL      IBACLOCK
          PACK      DIM8,CCC,CYY,CMM,CDD
          REP       " 0",DIM8
.
          MATCH     DIM8,PMCDEXDT                * card valid for date ?
          GOTO      CONC9100 IF LESS             * no - nothing found
.
.         Concession card is current
.
CONC1000  MATCH     SP20,PMCDCNUM                * card number blank ?
          GOTO      CONC9100 IF EQUAL            * yes - nothing found
.
          MOVE      PMCDCNUM,DIM20               * load concession card number
.
          MOVE      ZERO,EXIT
          GOTO      CONC9999
.
CONC9100  MOVE      ONE,EXIT
.
CONC9999  RETURN
+
.
.------------------------------------------------------------------------------
.
          INC       STD001IO
.
          INC       CLPATMIS
          INC       PATCODIO/INC
          INC       PMSOECIO/INC
          INC       PMSOESIO/INC
          INC       PMSELFIO/INC
          INC       PMSCCDIO/INC
          INC       PATELGIO/INC
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
          INC       PATMI1IO/INC
