.*******************************************************************************
.* System  : CCI                                                               *
.* Program : IBACCI26                                                          *
.* Desc    : CCI Detail Handover for the IBA Clinical Costing System           *
.*******************************************************************************
.* Date    : 03/10/92                                                          *
.* Author  : B.G.Ackland (copy of IBACCI24.PBL)                                *
.* Mods    :                                                                   *
.*      V10.04.01 07/03/2014  Ania P         CAR 261630                        *
.*                Remove the use of PORT for temp file names                   *
.*      V10.01.01 03/02/2011  Jill Parkinson CAR 233088                        *
.*                Added PMSVX1FD                                               *
.*      V9.02.00  01/05/2003  Tonii          CAR 38344                         *
.*                Ported over from 5.10                                        *
.*******************************************************************************
.*      V5.08.02  28/08/2000  Caleb Sharman                                    *
.*                Changed Health Fund variables to be 8 chars                  *
.*      V5.08.01  23/08/2000  Jill Habasque                                    *
.*                Added OPIP0000-if O/P & clinic is on ccicliaf, change to IP  *
.*      V5.08.B01 15/03/2000  Steve Armstrong     srf 643959 Q1176             *
.*                Mods for new parameter CCCNOUTL                              *
.*      V5.07.01  17/12/1999  J.Tan                                            *
.*                Mods CCPUCL1 to use the'default code for IP' from parameter  *
.*      V5.07.00  23/10/1998  Jim Stathopoulos                                 *
.*                507 Changes                                                  *
.*      V5.04.04  11/07/1997  Steve Armstrong      WHL Mods                    *
.*                Recompiled for CCIDEPDS                                      *
.*      V5.04.03  23/05/1997  Steve Armstrong      WHL Mods                    *
.*                Recompile for file length change in cciutlaf                 *
.*      V5.04.02  23.12.96 B.G.Ackland                                         *
.*                Handle Long File Names                                       *
.*      V5.04.01  24.06.96 B.G.Ackland                                         *
.*                Handle Multiple Databases for CHC                            *
.*      V5.01.02  07/12/92 B.G.Ackland                                         *
.*                Change to include patient location as Class 4                *
.*      V5.01.03  29.09.93 B.G.Ackland                                         *
.*                Fixed Sent to IBA Clinical Costing Flag in UTL FD&IO         *
.*      V5.01.04  29.09.93 B.G.Ackland                                         *
.*                Change to new UTL file layout                                *
.*      V5.01.05  25.10.93 B.G.Ackland                                         *
.*                Fix Pack Save Key42 for new file                             *
.*      V5.02.01  11.05.95 Justin Coates                                       *
.*                dont truncate CCUTPROC in WUTL0000                           *
.*                                                                             *
.*******************************************************************************
.
.$$$$$$
.     IBA Clinical Costing Procedure Handover
.     ---------------------------------------
.     - Initialisation
.
.           - Display Header
.           - Open Files
.                 CCIDEPP1
.                 CCIUTLL1
.                 CONTROLF
.           - Display Main Option Screen & Keyin Option
.
.           - Keyin Date for Handover
.                   - Validate this Date Exists on the cciutlaf File
.
.           - Keyin Depatment for the Handover
.                   - Default Department to be ALL
.
.           - Question if O.K. to Continue
.
.           - Loop through Data in Date/Dept Range
.                    - Write a Record to CCIUTL.txt for each record in cciutlaf
.                            for all A&E , OUT & In-Patients
.                    - Write or Update Temp file with Dept/Proc Quantity
.                    - Print cciutlaf Record on Report
.                    - Flag Record in the cciutlaf File
.
.           - End Transfer if out of Date/Dept Range
.
.     - Return to Main Screen
.
.$$$$$$
          INC       STD001FD
          INC       AAEDE1FD/INC
          INC       OUTBB1FD/INC
          INC       OUTSITFD/INC
          INC       PATCOMM/INC
          INC       PATCODFD/INC
          INC       PATDRGFD/INC
          INC       PATDSCFD/INC
          INC       PATMI1FD/INC
          INC       PATVISFD/INC
          INC       PMSVX1FD/INC
          INC       PATTRNFD/INC
.
          INC       CCICLIFD/INC
          INC       CCIDEPFD/INC
          INC       CCIUTLFD/INC
          INC       CCICTYFD/INC
          INC       CCIDPTFD/INC
          INC       CCICONFD/INC
.
          INC       CCSHCDFD/INC
          INC       CCSHLCFD/INC
.
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.------------------------------------------------------------
. Constants
.------------------------------------------------------------
PIPE      INIT      "|"
SEQ3      FORM      "-3"
UNDLN39   INIT      "---------------------------------------"
ZEDS      INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.------------------------------------------------------------
. Global Variables  
.------------------------------------------------------------
LASTHPRE  DIM       1
DEFTPREF  DIM       1
.
DISCHFLG  FORM      1
DTMPQUNT  DIM       6
RECORD    FORM      8
.
CMDLINE   DIM       50
CLASS1    DIM       2   * Patient Category
CLINDEPT  DIM       3   * Clinical Dept
LOCATION  DIM       4   * Location
.
DIM3      DIM       3
DIM4      DIM       4
DIM5      DIM       5
DIM6      DIM       6
DIM7      DIM       7
DIM8      DIM       8
DIM10A    DIM       10
DIM10B    DIM       10
DIM16     DIM       16
DOSV1     DIM       4         * date of service 1
DOSV2     DIM       5         * date of service 2
.
ENDDATE   DIM       8
ENDDEPT   DIM       3
.
FILEDATE  DIM       8
FISCPR    DIM       2         * fiscal period
FISCYR    DIM       2         * fiscal year
FLAGERRC  FORM      1
FNAMES    DIM       8
FORM6     FORM      6
FORM8     FORM      8
FLAGREPT  FORM      1         * Flag to ignore sent flag on file used for 
.                             * testing in initial version of program.
INPFLAG   FORM      1
OBAOUTNO  FORM      8
DOBAOUTN  DIM       8
OBADATE   DIM       8
.
PATYP     DIM       2         * patient type
.
PROCTOTL  FORM      9
.
CCIUTLNM  DIM       40
.
SAVDEPT   DIM       3
SAVKEY42  DIM       42
SCCUTEVN  FORM      8        * Save variable for event number for delete
STRTDATE  DIM       8
SAVEDATE  DIM       8
SAVADMN   DIM       8
SAVURNO   DIM       8
.
TEMEDATE  DIM       8
TEMPDEPT  DIM       3
TEMPPROC  DIM       7
TEMSDATE  DIM       8
TODAY     DIM       8
.------------------------------------------------------------
. Temporary File for Handover Report
.------------------------------------------------------------
CCTMP2X   IFILE     SQL, FIXED=34, KEY="U1-18"
.
.Name     Type   Length   Physical  Position
.----     ----   -------  --------  --------
TMP2DEPT  DIM       3        3        1
TMP2PROC  DIM       7        7        4
TMP2UR    DIM       8        8        11
TMP2TYP   DIM       4        4        19
TMP2FYER  DIM       4        4        23
TMP2FPER  DIM       2        2        27
TMP2QUAN  FORM      8        5        29
.
. End of Record                       34
.------------------------------------------------------------
. Format of Procedure Interface File
.------------------------------------------------------------
CCIUTLXX  FILE      ASCII, FIXED=120
.
. Name    Type    Length    Position  Description
.------   ----    ------    --------  --------------------------
CCPUFKY   DIM       20       1        Feeder Key - Department(3) Procedure(7)
CCPUENC   DIM       16       21       Encounter Code   
CCPUURN   DIM       10       37       U/R Code        
CCPUDAT   DIM       8        47       Service Date   (CCYYMMDD)
CCPUTIM   DIM       5        55       Service Time   (HH:MM)
CCPUQTY   FORM      12.2     60       Quantity       (999999999999.99)
CCPUCHG   FORM      12.2     75       Charge         (999999999999.99)
CCPUCL1   DIM       4        90       User Class 1 Patient Category    (3)
CCPUCL2   DIM       4        94                  2 Clinical Department (3)
CCPUCL3   DIM       4        98                  3 Service             (2)
CCPUCL4   DIM       4        102                 4 Blank
CCPUCL5   DIM       4        106                 5 Blank
CCPUSPA   DIM       10       110      Spare
.
. End of Record              120
.------------------------------------------------------------
PRGID     INIT      "IBACCI26"
PRGNAM    INIT      "Clinical Costing Procedure Handover File Generation"
VERSION   INIT      "V12.00.00"
.------------------------------------------------------------
. Mainline                     
.------------------------------------------------------------
ML0000    CALL      INIT0000           * initialization
.
ML1000    CALL      SCRN0000           * Display Main Screen
          BRANCH    EXIT,ML9999
.
ML2000    CALL      DATE0000           * Keyin Date Range for Handover
          BRANCH    EXIT,ML1000
.
          CALL      HOSP0000           * Keyin Default CCS Hospital for Handover
          BRANCH    EXIT,ML1000
.
          CALL      CONTQST            * Question O.K. to Continue (Y/N/C)
          BRANCH    CEXIT,ML3000,ML2000,ML1000
.
.         Create ccitm2xx, Create/Open CCIUTL.txt
.
ML3000    CALL      MAKE0000
.
.         Transfer the appropriate figures from cciutlaf to CCIUTL.txt, and
.         ccitm1xx if applicable.  Write records to ccitm2xx also for print.
.
          CALL      TRFR0000             * transfer all including Inpatients
.
.         Print handover report from ccitm2xx.
.
          CALL      PRNT0000
          GOTO      ML1000
.
ML9999    CALL      KILL0000                 * Remove temp files
          CHAIN     PGM
.------------------------------------------------------------
. Initialize Variables & Screen  
.------------------------------------------------------------
INIT0000  CALL      DISPHEAD
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
          CLOCK     TIME,CTIMEIS
.
          DISPLAY   *P57:24,"Opening ccidepaf"
          OPEN      CCIDEPA1,"ccidepaf"
.
          DISPLAY   *P65:24,"cciutlaf"
          OPEN      CCIUTLL1,"cciutlaf"
          OPEN      CCIUTLL3,"cciutlaf"
.
          DISPLAY   *P65:24,"ccicliaf"
          OPEN      CCICLIA1,"ccicliaf"
.
          DISPLAY   *P65:24,"ccictyaf"
          OPEN      CCICTYA1,"ccictyaf"
.
          DISPLAY   *P65:24,"ccidptaf"
          OPEN      CCIDPTA1,"ccidptaf"
.
          DISPLAY   *P65:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P65:24,"pattranf"
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P65:24,"aaede1af"
          OPEN      AAEDE1A1,"aaede1af"
.
          DISPLAY   *P65:24,"outsitaf"
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P65:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
.
          DISPLAY   *P65:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P65:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P65:24,"patvisaf"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P65:24,"controlf"
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,SEVENTY1;*48,CCCNAECD,*57,CCCNIPCD:
                                      *92,CCCNFYOS,CCCNCLFL,*199,CCCNOUTL
.
          MOVE      ZERO,PROCTOTL
          MOVE      ZERO,CPAGENO
.
. ------- Set up temp filename for handover report -------
          
          CALL      TFILENAM
          PACK      FNAMES,TFILNAME,SP70
.
INIT9999  RETURN
.
.**********************************************************************
.*                             SCRN0000                               *
.*                        Main Option Screen                          *
.**********************************************************************
.
SCRN0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Run Handover":
                    *P1:6,*V2LON,TWO,*HOFF," = Handover without Check on Sent":
                    *P1:8,"Select Option : ";
SCRN1000  KEYIN     *P17:8,*EL,*V2LON,OPTION;
          COMPARE   ZERO,OPTION
          GOTO      SCRN9000 IF EQUAL
          MOVE      ZERO,FLAGREPT
          COMPARE   ONE,OPTION
          GOTO      SCRN9999 IF EQUAL
          IF        OPTION=2
            MOVE      ONE,FLAGREPT
            MOVE      ONE,OPTION
            GOTO      SCRN9999
          ENDIF
          BEEP
          GOTO      SCRN1000
SCRN9000  MOVE      TRUE,EXIT
SCRN9999  RETURN
.
.********************************************************************** 
.*                             DATE0000                               *
.*                   Keyin Date for Data Transmission                 *
.**********************************************************************
DATE0000  MOVE      FALSE,EXIT
.
          DISPLAY   *P1:10,*EF,"Service Date From : ";
.
          UNPACK    SP6,CYEAR,CMON,CDAY          * Set-up Variables for the 
          MOVE      CCC,CCENT                    * KEYDATE Routine
          MOVE      TEN,CVERT
          MOVE      TWENTY1,CCOL
          MOVE      CVERT,EVERT
          MOVE      "40",ECOL
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
.
          CALL      KEYDATE
          BRANCH    OVRCD,DATE9000
          BRANCH    CQUEST,DATE0000                   * "?" Entered
.
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
.
          MATCH     STRTDATE,TODAY                    * Start Date > Today
          GOTO      DATE1000 IF NOT LESS
. 
          DISPLAY   *P1:24,*B,"From Date is Greater than Todays Date - ":
                           "Hit <",*V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      DATE0000
.
DATE1000  DISPLAY   *P1:11,*EL,"             To   : ";
.
          UNPACK    SP30,CYEAR,CMON,CDAY         * Set-up Variables for the
          MOVE      CCC,CCENT                    * KEYDATE Routine
          MOVE      TEN1,CVERT
          MOVE      TWENTY1,CCOL
          MOVE      CVERT,EVERT
          MOVE      "40",ECOL
          MOVE      ZERO,CHIGHLT
          MOVE      ZERO,CDEFDTE
.
          CALL      KEYDATE
          BRANCH    OVRCD,DATE0000
          BRANCH    CQUEST,DATE1000              * "?" Entered
.
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
.
          MATCH     ENDDATE,TODAY                * End Date > Today
          GOTO      DATE2000 IF NOT LESS
. 
          DISPLAY   *P1:24,*B,"To Date is Greater than Todays Date - ":
                           "Hit <",*V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      DATE1000
.
DATE2000  MATCH     STRTDATE,ENDDATE             * Force Start <= End Date
          GOTO      DATE3000 IF NOT LESS
.
          DISPLAY   *P1:24,*EL,*B,"From Date Must not be Greater than To Date":
                           " - Hit <",*V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
          DISPLAY   *P1:24,*EL;
          GOTO      DATE1000
.
. ------  Check if any details exist on the "cciutlaf" File ------
.
DATE3000  PACK      KEY42,STRTDATE,SP30
.
          CALL      RSCCUTL1
          CALL      RKCCUTL1
          BRANCH    OVRCD,DATE8000 
.
          MATCH     CCUTDATE,ENDDATE
          GOTO      DATE8000 IF LESS
.
          MOVE      FALSE,EXIT
          GOTO      DATE9999
.
DATE8000  DISPLAY   *P1:24,*B,"No Daily Input Details On File in this Date":
                    " Range - Hit <",*V2LON,"ENTER",*HOFF,"> to Continue ";
          KEYIN     *EOFF,ANS;
          DISPLAY   *P1:24,*EL;
.
DATE9000  MOVE      TRUE,EXIT
.
DATE9999  RETURN
.------------------------------------------------------------
.  Get Default Hospital for Prefix to Write
.------------------------------------------------------------
HOSP0000  MOVE      SP1,DEFTPREF
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      CCSHCDA1,"ccshcdaf"
          TRAPCLR   IO
          BRANCH    OVRCD,HOSP9000         * Check for CCS Files
.
          OPEN      CCSHCDA2,"ccshcdaf"
          OPEN      CCSHLCA1,"ccshlcaf"
.
HOSP0100  KEYIN     *P1:11,"Default CCS Hospital Code         : ",*RV,CCHCHCD
          ENDSET    CCHCHCD
          APPEND    SP20,CCHCHCD
          RESET     CCHCHCD
          MATCH     SP20,CCHCHCD
          GOTO      HOSP9100 IF EQUAL
.
          MOVE      CCHCHCD,KEY2
          CALL      RDCCHC1
          IF        OVRCD=1
            BEEP
            GOTO      HOSP0100
          ENDIF
          MOVE      CCHCHCD,KEY2
          CALL      RDCCHL1
          IF        OVRCD=1
            BEEP
            GOTO      HOSP0100
          ENDIF
          DISPLAY   *P1:11,"CCS Hospital Code               : ",*V2LON,CCHCHCD:
                    *HOFF,SP1,CCHCDES
.
          MOVE     CCHCPREF,DEFTPREF
.
HOSP9000  MOVE     ZERO,EXIT
          GOTO     HOSP9999
.
HOSP9100  MOVE     ONE,EXIT
HOSP9999  RETURN
.**********************************************************************
.*                             RDIS0000                               *
.*                    Redisplay Screen After a "?"                    *
.**********************************************************************
RDIS0000  UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          PACK      DIM10A,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
          PACK      DIM10B,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Run Handover":
                    *P1:7,"Select Option : ",*V2LON,OPTION,*HOFF:
                    *P1:10,"Service Date From : ",*V2LON,DIM10A,*HOFF:
                    *P1:11,"             To   : ",*V2LON,DIM10B,*HOFF:
                    *P1:13,*HOFF,"Department Code   : ";
          RETURN
.
.**********************************************************************
.*        MAKE0000 - Create the Indexed Temp Files                    *
.**********************************************************************
MAKE0000  CALL      KILL0000
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMES,CMDLINE
          APPEND    " 34 u1-18",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      CCTMP2X,FNAMES
          PACK      CCIUTLNM,SP20,SP20
.
MAKE1000  KEYIN     *P1:23,*EF,"Enter Extract File Name : ":
                    *V2LON,*MASK,*RV,CCIUTLNM
          ENDSET    CCIUTLNM
          APPEND    SP20,CCIUTLNM
          APPEND    SP20,CCIUTLNM
          RESET     CCIUTLNM
          MATCH     SP20,CCIUTLNM
          IF        @EQUAL
            DISPLAY   *P1:24,*B,*EL,"File Name Must Be Entered - ";
            CALL      HITENTER
            GOTO      MAKE1000
          ENDIF
.
MAKE1100  KEYIN     *P1:24,*EL,*B,"File Name Ok (":
                    *V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS
          REP       UPPLOW,ANS
          MATCH     "N",ANS
          GOTO      MAKE1000 IF EQUAL
          MATCH     "Y",ANS
          GOTO      MAKE1100 IF NOT EQUAL
.     
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      CCIUTLXX,CCIUTLNM        * Create a new file
          TRAPCLR   IO 
          BRANCH    OVRCD,MAKE1800
.
MAKE1400  KEYIN     *P1:24,*EL,*B,"File Already Exists. Delete File (":
                    *V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS
          REP       UPPLOW,ANS
          MATCH     "N",ANS
          GOTO      MAKE1000 IF EQUAL
          MATCH     "Y",ANS
          GOTO      MAKE1400 IF NOT EQUAL
          CLOSE     CCIUTLXX,DELETE
.
MAKE1800  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PREP      CCIUTLXX,CCIUTLNM        * Create a new file
          TRAPCLR   IO 
          IF        OVRCD=1
            DISPLAY   *P1:24,*EL,*B,"Unable to Create File - ";
            CALL      HITENTER
            GOTO      MAKE1000
          ENDIF
.
          DISPLAY   *P1:24,"File Created  - ";
          CALL      HITENTER
MAKE9999  RETURN
.
.**********************************************************************
.*        KILL0000 - Remove the Indexed Temp Files                    *
.**********************************************************************
KILL0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      CCTMP2X,FNAMES
          TRAPCLR   IO 
          IF        OVRCD=0
            CLOSE     CCTMP2X,DELETE
          ENDIF
          RETURN
.*******************************************************************************
.*        TRFR0000 - Hand over cciutlaf records to live files.                 *
.*        Requires : Records to be processed in cciutlaf                       *
.*                   Date range required in STRTDATE & ENDDATE                 *
.*                   Dept range required in TEMPDEPT & ENDDEPT                 *
.*        Returns  : Add processed records in CCIUTL.txt                       *
.*******************************************************************************
TRFR0000  PACK      KEY42,STRTDATE,SP30
          DISPLAY   *P1:18,"Date  : ",*P1:19,"Record: "
          MOVE      ZERO,RECORD
          MOVE      SP1,LASTHPRE
.
TRFR1000  CALL      RSCCUTL1                * Reposition file record pointer
.
TRFR2000  CALL      RKCCUTL1                * Read next cciutlaf record
          BRANCH    OVRCD,TRFR9999          * No more records - finished
          MATCH     CCUTDATE,ENDDATE        * Is date in req range ?
          GOTO      TRFR9999 IF LESS        * No - finished
          BRANCH    FLAGREPT,TRFR2010       * Ignore Sent Flag
          MATCH     "1",CCUTSIBA            * test if record has been sent
          GOTO      TRFR2000 IF EQUAL    
.
TRFR2010  PACK    SAVKEY42,CCUTDATE,CCUTDEPT,CCUTEVNT,CCUTPROC,CCUTURNO,DCCUTBAT
          MOVE      CCUTEVNT,SCCUTEVN       * Save current event no for delete
.
          MATCH     CCUTHPRE,LASTHPRE
          GOTO      TRFR2020 IF EQUAL
          MOVE      CCUTHPRE,LASTHPRE
          CALL      ALTOP000                * Open Alternate Database Files
.
TRFR2020  MOVE      SP20,CLINDEPT           * Clinical Department
          MOVE      "UNK ",LOCATION         * Location
.
          ADD       ONE,RECORD
          IF        (RECORD%100=0)
            UNPACK    CCUTDATE,CCENT,CYEAR,CMON,CDAY
            DISPLAY   *P9:18,*V2LON,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR:
                      *P9:19,RECORD
          ENDIF
.
          MOVE      ZERO,PVITYPE             * reset patient type
          MOVE      ZERO,INPFLAG             * initialise flag
.
. ------- For 0 event numbers, check if there is an actual visit -------
.         the operator may have missed.  However, do a further check
.         for outpatients for linked admission numbers.
.
          COMPARE   ZERO,CCUTEVNT            * Is event number zero ?
          GOTO      TRFR3000 IF EQUAL        * yes - post an error record.
.
.---- read the visit record -----
.
          PACK      KEY8,CCUTEVNT            * Event number is the key
          CALL      RDVISA1                  * Read visit 
          BRANCH    OVRCD,TRFR2000           * No record -use curr event
.
.----  if patient is an outpatient check for linked admission ----
.
          MOVE      PVIDATE,SAVEDATE * date of visit that we require -Outpatient
          BRANCH    PVITYPE,TRFR2100,TRFR2200,TRFR2300
          GOTO      TRFR3000 
.
.----- Set up A&E clinical department for CCIUTL.txt -----
.
TRFR2100  MOVE      "A&E ",LOCATION
          PACK      KEY8,PVIBILL             * Current event no
          CALL      RDDETA1                  * Read bookings extension file
          BRANCH    OVRCD,TRFR2900
          MOVE      ADACLASS,CLINDEPT
          GOTO      TRFR2900
.
. ------- Outpatient visit - get linked admission number if applicable -------
. ------- Set up clinical department for CCIUTL.txt                    -------
.
TRFR2200  MOVE      "OPD ",LOCATION
          PACK      KEY6,PVISITE     * Site code is the key
          CALL      RDSITA1          * Read site file for file prefix
          BRANCH    OVRCD,TRFR2900   * No site rec - use curr event no

          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO           * Set trap to catch missing files
          PACK      CFNAME,OSTFILE,FILBB1A1  * Set up physical filename
          CALL      OPOTBB11
          TRAPCLR   IO                       * Clear file open trap
          BRANCH    OVRCD,TRFR2900
.
          PACK      KEY8,PVIBILL             * Current event no
          CALL      RDBOKB1                  * Read bookings extension file
          CLOSE     OUTBB1A1                 * Close bookings extension
          BRANCH    OVRCD,TRFR2900           * No record - use current event no
.
          MOVE      OBACLASS,CLINDEPT        * Clinical Department
.
          MATCH     SP8,OBALADMN             * Is there a linked adm no ?
          GOTO      TRFR2900 IF EQUAL        * No - use current event no
.
          IF        CCCNOUTL = 0
            MOVE      OBALADMN,CCUTEVNT      * Else replace curr event no
          ELSE
            MOVE      PVIBILL,CCUTEVNT
          ENDIF
.
          PACK      KEY12,OSTSITE,OBBCTYP
          CALL      RDCCCLI1
          IF        OVRCD=0
            CALL      OPIP0000               * check if O/P should be I/P
          ENDIF
.
          PACK      KEY5,ANSP,SP1,OBACAT
          CALL      RDCODE1
          IF        OVRCD=0
            MATCH     "1",TCINDC2            * If indicator 2 = 1 (inpatient)
            IF        @EQUAL
              MOVE      ONE,INPFLAG          * inpatients
            ENDIF
          ENDIF
          GOTO      TRFR3000                 
.
. ---- if inpatient check for unadmit and cancellation ----
.
TRFR2300  PACK      KEY8,CCUTEVNT
          CALL      RDMISS1
          BRANCH    OVRCD,TRFR2000
          BRANCH    ASTAT,TRFR2000,TRFR2310,TRFR2310,TRFR2310,TRFR2000
          GOTO      TRFR2000
.
TRFR2310  MOVE      ATYPE,CLINDEPT
          UNPACK    CCUTDATE,CCENT,CYEAR,CMON,CDAY
          PACK      DIM8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DIM8             * set endate of period
          PACK      KEY30,CCUTEVNT,DIM8,ZEDS
          CALL      RDSTRAN2
          CALL      RDPTRAN2              * red bed transfer file
          BRANCH    OVRCD,TRFR2400
          COMPARE   CCUTEVNT,TADMN        * test same admission number
          GOTO      TRFR2400 IF NOT EQUAL
          MOVE      TATYPE,CLINDEPT       * Clinical Dept. at Date of Service
          MOVE      TWARD,LOCATION       * Location Dept. at Date of Service
          GOTO      TRFR2900
.
TRFR2400  PACK      KEY30,CCUTEVNT,DIM8,SP20
          CALL      RDSTRAN2
TRFR2450  CALL      RDKTRAN2              * red bed transfer file
          BRANCH    OVRCD,TRFR2900
          COMPARE   CCUTEVNT,TADMN        * test same admission number
          GOTO      TRFR2900 IF NOT EQUAL
          MATCH     "D",TMOVE
          GOTO      TRFR2450 IF NOT EQUAL
          MOVE      TATYPE,CLINDEPT       * Clinical Dept. at Date of Service
          MOVE      TWARD,LOCATION       * Location Dept. at Date of Service
          GOTO      TRFR2900
.
TRFR2900  MOVE      PVIBILL,CCUTEVNT      * Admission # is event number
.
. ------- Write a record to Transfer File -------
.
TRFR3000  
          CALL      WUTL0000
.
. ------- Write a record to 2nd temp file for print -------
.
          PACK      KEY18,CCUTDEPT,CCUTPROC,CCUTURNO
          CALL      RDTMP2                   * Do a full read
          IF        OVRCD=0
            ADD       CCUTQUNT,TMP2QUAN        * Add to the quantity
            CALL      UPTMP2                   * Update with the new quantity
          ELSE  
            UNPACK    KEY18,TMP2DEPT,TMP2PROC,TMP2UR
            IF        INPFLAG=1
              MOVE      CCCNIPCD,TMP2TYP       * use default code for IP
            ELSE
              MOVE      CCUTPTYP,TMP2TYP       * Patient type
            ENDIF
.
            MOVE      CCUTFSYR,TMP2FYER        * Fiscal Year
            MOVE      CCUTFSPR,TMP2FPER        * Fiscal Period
            MOVE      CCUTQUNT,TMP2QUAN        * Quantity
            CALL      WRTMP2                   * Write new ccitm2xx record
          ENDIF
          GOTO      TRFR2000                 * Read next cciutlaf record
.
TRFR9999  RETURN
+
.*******************************************************************************
.*        OPIP0000 - Check if Outpatient was actually in hospital at time      *
.*                   at time of outpatient visit, change to IP if applicable   *
.*******************************************************************************
OPIP0000  MATCH     SP8,PVIURNO
          GOTO      OPIP0000 IF EQUAL
.
          MOVE      DPVIBILL,SAVADMN
          MOVE      PVIURNO,SAVURNO
.
          PACK      KEY24,PVIURNO,SP20   * loop thru patvisaf to find admissions
          CALL      RSPTVIS2
OPIP1000  CALL      RKPTVIS2
          BRANCH    OVRCD,OPIP9999
. 
          MATCH     PVIURNO,SAVURNO
          GOTO      OPIP9999 IF NOT EQUAL
.
          PACK      KEY8,PVIBILL
          CALL      RDMISS1
          BRANCH    OVRCD,OPIP1000
.
          COMPARE   ONE,ASTAT           * only pre-admitted
          GOTO      OPIP1000 IF EQUAL
.
          COMPARE   FIVE,ASTAT
          GOTO      OPIP1000 IF EQUAL   * cancelled admission
.
          MATCH     ADATE,OBADATE
          IF        @LESS 
            GOTO      OPIP1000          * admitted before visit date
          ENDIF
.
          COMPARE   TWO,ASTAT           * still admitted?
          GOTO      OPIP9000 IF EQUAL
.
          COMPARE   THREE,ASTAT
          IF        @EQUAL
            CALL      RDDSCH1
            BRANCH    OVRCD,OPIP1000
            MATCH     OBADATE,DDATE
            GOTO      OPIP1000 IF LESS  * discharged before visit date
          ENDIF
.
OPIP9000  MOVE      ONE,INPFLAG
          MOVE      DAADMNO,CCUTEVNT
.
OPIP9999  PACK      KEY8,SAVADMN         * reposition on the visit file
          CALL      RDPTVIS1
          RETURN
+
.*******************************************************************************
.*        PRNT0000 - Print Handover Report                                     *
.*        Requires : ccitm2xx                                                  *
.*        Returns  : The report, of course !!!                                 *
.*******************************************************************************
.
PRNT0000  MOVE      ZERO,CPAGENO             * No pages yet
          MOVE      "80",CLNO                * Print header first
          MOVE      SP3,SAVDEPT              * Clear department save variable
.
          PACK      KEY18,SP20               * Grab all records
          CALL      RDSTMP2                  * Positioning read
PRNT1000  CALL      RDKTMP2                  * Read next record
          BRANCH    OVRCD,PRNT9000           * No more records - print totals
.
          MATCH     SP3,SAVDEPT              * 1st time round ?
          GOTO      PRNT2000 IF EQUAL        * Yes - print header
          MATCH     SAVDEPT,TMP2DEPT         * Same department ?
          GOTO      PRNT3000 IF EQUAL        * Yes - just print record
          CALL      TOTL0000                 * Print last department totals
.
PRNT2000  MOVE      TMP2DEPT,SAVDEPT         * Save current department code
          CALL      HEAD0000                 * Print new page header
.
. ------- Print record -------
.
PRNT3000  COMPARE   CLNO,SIXTY               * Printed past end of page ?
          GOTO      PRNT2000 IF LESS         * Yes - print header
.
          PRINT     *1,PIPE,SP1,TMP2PROC,*14,PIPE,SP1,TMP2QUAN:
                    *25,PIPE,SP1,TMP2TYP,*37,PIPE,SP1,TMP2UR:
                    *49,PIPE,SP5,TMP2FYER,*63,PIPE,SP6,TMP2FPER,*80,PIPE
          ADD       TMP2QUAN,PROCTOTL        * Add Totals
          ADD       ONE,CLNO                 * 1 more line printed
          GOTO      PRNT1000
.
PRNT9000  CALL      TOTL0000                 * Print final totals
.
.         Close off totals & advance to 1st char of 
.         next page in case of multiple prints.
.
          PRINT     *1,ASK,UNDLN39,UNDLN39,*80,ASK:
                    *N,*N,"*** END OF REPORT ***",*F,*1
.
PRNT9999  RETURN
.
.*******************************************************************************
.*        HEAD0000 - Heading for Report Layout                                 *
.*******************************************************************************
.
HEAD0000  UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          PACK      DIM10A,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
          PACK      DIM10B,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
.
          MOVE      ZERO,CNOUNDLN            * Print lines on last page
          CALL      HEAD80                   * Print 80 col std report header
.
          PRINT     *20,"Service Date From : ",DIM10A:
                    *N,*33,"To   : ",DIM10B:
                    *N,*1,ASK,UNDLN39,UNDLN39,ASK:
                    *N,*1,PIPE,*3,"Department : ",SAVDEPT,*80,PIPE:
                    *N,*1,PIPE,UNDLN39,UNDLN39,PIPE:
                    *N,*1,PIPE,SP1,"Procedure",*14,PIPE,SP1,"Quantity":
                       *25,PIPE,SP1,"Pat. Type",*37,PIPE,SP1,"NMPI No.":
                       *49,PIPE,SP1,"Fiscal Year":
                       *63,PIPE,SP1,"Fiscal Period",*80,PIPE:
                    *N,*1,PIPE,UNDLN39,UNDLN39,PIPE
.
          MOVE      TEN3,CLNO
          RETURN
.*******************************************************************************
.*        TOTL0000 - Print Department Totals                                   *
.*******************************************************************************
TOTL0000  PRINT     *1,PIPE,UNDLN39,UNDLN39,*80,PIPE:
                    *N,*1,PIPE,*7,"Total : ",PROCTOTL,*80,PIPE
          MOVE      ZERO,PROCTOTL
          ADD       THREE,CLNO
          RETURN
.*******************************************************************************
.*        WUTL0000 - Write a record to CCIUTL.txt if applicable                *
.*        Requires : cciutlaf variables                                        *
.*        Returns  : Nothing                                                   *
.*******************************************************************************
WUTL0000  PACK      CCPUFKY,CCUTDEPT,CCUTPROC,SP20  * Feeder Key
.
          MOVE      CCUTEVNT,KEY8
          SQUEEZE   KEY8
.
          MATCH     SP1,CCUTHPRE
          IF        @EQUAL
            PACK      KEY9,DEFTPREF,KEY8
            SQUEEZE   KEY9
          ELSE
            PACK      KEY9,CCUTHPRE,KEY8
          ENDIF
.           
          PACK      CCPUENC,KEY9,SP20
          IF        CCUTEVNT=0
            MOVE      SP20,CCPUENC
          ENDIF
.
          SQUEEZE   CCUTURNO                    * Remove Spaces from UR No.
          PACK      CCPUURN,CCUTURNO,SP20       * UR Number Left Justified
          MOVE      CCUTDATE,CCPUDAT            * Service Date
          REP       " 0",CCPUDAT                * Replace space with 0's
          MOVE      "12:00",CCPUTIM             * Time
          MOVE      CCUTQUNT,CCPUQTY            * Quantity
          MOVE      ZERO,CCPUCHG                * Charge  
          PACK      CCPUSPA,SP20,SP20           * Spare
.
. ----- Get the Service code for CCIUTL.txt       -----
.
          IF        INPFLAG=1
            PACK      CCPUCL1,CCCNIPCD,SP20       * use default code for IP
          ELSE
            PACK      CCPUCL1,CCUTPTYP,SP20       * Patient Category
          ENDIF
.
          PACK      CCPUCL2,CLINDEPT,SP20       * Clinical Department
          MOVE      SP20,CCPUCL3
          PACK      KEY3,CLINDEPT          
          CALL      RDCCDPT1
          IF        OVRCD=0
            PACK      CCPUCL3,CCDTSERV,SP20     * Service Code
          ENDIF
.
          MOVE      LOCATION,CCPUCL4
.
          MOVE      SP20,CCPUCL5
.
          CALL      WRCCIUTL         * Write Record to Transfer File  
.
. ------- Update cciutlaf to say record has been sent ------
.
          MOVE      SAVKEY42,KEY42
          CALL      RDCCUTL1
          IF        OVRCD=0
            MOVE      "1",CCUTSIBA
            CALL      UPCCUTL1
          ENDIF
.
WUTL9999  RETURN
.------------------------------------------------------------
. I/O Routines for the CCIUTL 
.------------------------------------------------------------
WRCCIUTL  WRITE     CCIUTLXX,SEQ;CCPUFKY,CCPUENC,CCPUURN,CCPUDAT,CCPUTIM:
                                 CCPUQTY,CCPUCHG,CCPUCL1,CCPUCL2,CCPUCL3:
                                 CCPUCL4,CCPUCL5,CCPUSPA
          RETURN
.
RKCCIUTL  MOVE      ZERO,OVRCD
          READ      CCIUTLXX,SEQ;CCPUFKY,CCPUENC,CCPUURN,CCPUDAT,CCPUTIM:
                                 CCPUQTY,CCPUCHG,CCPUCL1,CCPUCL2,CCPUCL3:
                                 CCPUCL4,CCPUCL5,CCPUSPA
          GOTO      OVERCOND IF OVER
          RETURN
.------------------------------------------------------------------------------
.         I/O routines for temp files
.------------------------------------------------------------------------------
RDSTMP2   MOVE      ZERO,OVRCD
          RESET     KEY18
          READ      CCTMP2X,KEY18;;
          RETURN
.
RDTMP2    MOVE      ZERO,OVRCD
          RESET     KEY18
          READ      CCTMP2X,KEY18;TMP2DEPT,TMP2PROC,TMP2UR,TMP2TYP,TMP2FYER:
                                  TMP2FPER,TMP2QUAN
          GOTO      OVERCOND IF OVER
          RETURN
.
RDKTMP2   MOVE      ZERO,OVRCD
          READKS    CCTMP2X;TMP2DEPT,TMP2PROC,TMP2UR,TMP2TYP,TMP2FYER:
                            TMP2FPER,TMP2QUAN
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMP2    MOVE      ZERO,OVRCD
          RESET     KEY18
          WRITE     CCTMP2X,KEY18;KEY18,TMP2TYP,TMP2FYER,TMP2FPER,TMP2QUAN
          RETURN
.
UPTMP2    UPDATE    CCTMP2X;TMP2DEPT,TMP2PROC,TMP2UR,TMP2TYP,TMP2FYER:
                            TMP2FPER,TMP2QUAN
          RETURN
.
DETMP2    RESET     KEY18
          DELETE    CCTMP2X,KEY18
          RETURN
.
.------------------------------------------------------------
. Open Alternate Database Files - by CCS Hospital
.------------------------------------------------------------
ALTOP000  MATCH    SP1,LASTHPRE           * Open Default in DPATH
          GOTO     ALTOP500 IF EQUAL
          MOVE     SP2,KEY2
          CALL     RSCCHC1
ALTOP100  CALL     RKCCHC1
          BRANCH   OVRCD,ALTOP500
          MATCH    LASTHPRE,CCHCPREF
          GOTO     ALTOP100 IF NOT EQUAL
          MOVE     CCHCHCD,KEY2
          CALL     RDCCHL1
          BRANCH   OVRCD,ALTOP500
.         
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          MATCH    SP20,CCHLPVI
          IF       @EQUAL
            MOVE     "patvisaf",KEY40
            OPEN     PATVISA1,KEY40
          ELSE
            MOVE     "patvisaf",KEY8 
            STRIP    CCHLPVI
            PACK     KEY40,CCHLPVI,KEY8
            OPEN     PATVISA1,KEY40
          ENDIF
          TRAPCLR  IO
.         
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          MATCH    SP20,CCHLPTR
          IF       @EQUAL
            MOVE     "pattranf",KEY40
            OPEN     PATTRAN2,KEY40
          ELSE
            MOVE     "pattranf",KEY8 
            STRIP    CCHLPTR
            PACK     KEY40,CCHLPTR,KEY8
            OPEN     PATTRAN2,KEY40
          ENDIF
          TRAPCLR  IO
          BRANCH   OVRCD,ALTOP900
.
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          MATCH    SP20,CCHLPMI
          IF       @EQUAL
            OPEN     PATMI1A1,"patmi1af"
          ELSE
            MOVE     "patmi1af",KEY8
            STRIP    CCHLPMI
            PACK     KEY40,CCHLPMI,KEY8
            OPEN     PATMI1A1,KEY40
          ENDIF
          TRAPCLR  IO
          BRANCH   OVRCD,ALTOP900
.
          MOVE      ZERO,EXIT
          GOTO      ALTOP999
.
ALTOP500  OPEN      PATMI1A1,"patmi1af"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATTRAN2,"pattranf"
          MOVE      ZERO,EXIT
          GOTO      ALTOP999
.
ALTOP900  DISPLAY   *P1:24,*EL,"Can't Open ",KEY40;
          CALL      HITENTER
          MOVE      ONE,EXIT
.
ALTOP999  RETURN
.
.*******************************************************************************
.*        I/O INCLUDES FOR FILES AND GENERAL ROUTINES                          *
.*******************************************************************************
.
          INC       STD001IO
          INC       CCIDEPDS
.
          INC       AAEDE1IO/INC
.          INC       AAEDI1IO/INC
          INC       OUTBB1IO/INC
          INC       OUTSITIO/INC
          INC       PATCODIO/INC
          INC       PATDRGIO/INC
          INC       PATDSCIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATVISIO/INC
          INC       PATTRNIO/INC
          INC       CCICLIIO/INC
          INC       CCIDEPIO/INC
          INC       CCIUTLIO/INC
          INC       CCIDPTIO/INC
          INC       CCICTYIO/INC
.
          INC       CCSHCDIO/INC
          INC       CCSHLCIO/INC
.
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
.

