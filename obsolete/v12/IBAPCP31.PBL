.*******************************************************************************
.* Program         : IBAPCP31                                                  *
.* System          : PATIENT CARE                                              *
.* Desc            : Print Database                                            *
.*                                                                             *
.* Date            : 21-12-89                                                  *
.* Programmer      : Paul Duncan                                               *
.* Modifications   :                                                           *
.*        V9.02.00  30/12/2002 Tracey Nguyen srf 23497                         *
.*                  r5.10 Care Plans Port to V9                                *
.*-----------------------------------------------------------------------------*
.*                                                                             *
.*                   V5.01.02  23/05/91  Steve O'Gorman                        *
.*                             Fixed Errors found by the New Compiler          *
.*                   V5.03.B1  31/05/95   Whirlpool                            *
.*                             Recompiled for PCPDSCDS                         *
.*                                                                             *
.*******************************************************************************
.
.$$$$$
. Print Database (IBAPCP31)
.--------------------------
.
.   -Initialization
.     
.         -Display Header
.         -Open Files
.             PCPDSCFD
.             PCPDBSFD
.
.   -Main Options
.       
.         -0 = Exit
.         -1 = Print a single care plan
.               -Enter diagnosis code, it is validated then all
.                its related fields are printed.
. 
.         -2 = Print care plans for a Diagnostic category
.
.               -Options
.                 -0 = exit
.                 -1 = Health Pattern category
.                       -enter health pattern code, it is validated
.                        and all the nurses diagnosis related to that
.                        code are printed.
.                 -2 = Clinic Conditions category
.                       -Type of clinic conditions is entered and
.                        validated(Question mark option available)
.                        The clinic condition is then entered and
.                        validated(Question mark option available)
.                        The combination is then validated against
.                        the pcpdbsaf file. If both entries are valid
.                        the nurses diagnosis related to that code
.                        are printed.
.                 -3 = Response Patterns category
.                       -response pattern code is entered and validated
.                        Any nurses diagnosis related to the valid 
.                        code is printed
.                 -4 = ABC PRN REST code category
.                       -ABC PRN REST code is entered and validated.
.                        Any nurses diagnosis related to the valid
.                        code is printed
.
.         -3 = Print all care plans
.               -Prints out all the nurses diagnosis
.
.  -All printouts are produced by looping through the file printing required
.                fields.
.
.  -End
.
.*******************************************************************************
          INC       STD001FD
          INC       PCPDSCFD/INC
          INC       PCPDBSFD/INC
.*******************************************************************************
.*                                 CONSTANTS                                   *
.*******************************************************************************
DCHEAD    INIT      "Characteristics  :"
DDHEAD    INIT      "Diff. Diagnosis  :"
DOHEAD    INIT      "Desired Outcomes :"
.
IVHEAD    INIT      "Interventions    :"
.
ONE2      FORM      " 1"
TWO2      FORM      " 2"
THREE2    FORM      " 3"
FOUR2     FORM      " 4"
FIVE2     FORM      " 5"
SIX2      FORM      " 6"
.
PRGID     INIT      "IBAPCP31"
PRGNAM    INIT      "Print Database"
.
RFHEAD    INIT      "Related Factors  :"
.
SP60      INIT      "                                                        ":
                    "    "
VERSION   INIT      "V12.00.00"
.
Z20       INIT      "ZZZZZZZZZZZZZZZZZZZZ"
.
.******************************************************************************* .*                             GLOBAL VARIABLES                               *
.*******************************************************************************
CHOOZ     FORM      1
CODED     DIM       9
CODEE     DIM       9
.
DESC      DIM       60             *Temporary Description
.
END       DIM       1
.
FLAGZ     FORM      2
.
HEAD      DIM       18             *Sub-Headings for printout 
HEADING   DIM       40             *Screen headings
.
IND       FORM      2     
.
.
LINE      FORM      2
.
LNO       FORM      2
.
PRHEAD    DIM       1              *Print Heading? Y=yes, N=no
SCREENNO  FORM      3
.
TEMP      DIM       9
TEMP5     DIM       9
TPCC      DIM       9               
TPCDE     DIM       9
TPCLS     DIM       9
TPTCC     DIM       9
TYPE      FORM      2
TYPNO     FORM      2
.  .                
.*******************************************************************************
.*                            MAINLINE                                         *
.*******************************************************************************
ML0000    CALL      INIT0000                     * Display header and open files
.
ML1000    CALL      SLCT0000                     * Select which printout or
                                                 * exit
          BRANCH    EXIT,ML9999
.
          BRANCH    OPTION,ML3000,ML4000,ML5000
          GOTO      ML1000
.
ML3000    CALL      PSCP0000                     * Call routine to print a
.                                                  single care plan
          GOTO      ML1000 
.
ML4000    CALL      PPDC0000                     * Call routine to print a
.                                                  diagnosis category
          GOTO      ML1000
.
ML5000    CALL      PACP0000                      * Call routine to print 
.                                                  all care plans
          GOTO      ML1000
.
ML9999    CHAIN     PGM                      * Return from whence thou comest
.
.*******************************************************************************
.*                                 INIT0000                                    *
.*                       Display header and open files                         *.*******************************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CNOUNDLN
          DISPLAY   *P50:24,"Opening pcpdbsaf";
          OPEN      PCPDBSA1,"pcpdbsaf"
          DISPLAY   *P50:24,"Opening pcpdscaf";
          OPEN      PCPDSCA1,"pcpdscaf"
.
INIT9999  RETURN
.
.*******************************************************************************
.*                                 SLCT0000                                    *
.*                  Select main options and store in exit                      *.*                   1. Print single Patient Care Plan                         *
.*                   2. Print PCPs for a Diagnostic category                   *
.*                   3. Print all Patient Care Plans                           *
.*******************************************************************************.
SLCT0000  CALL      SLSC0000
.
SLCT1000  DISPLAY   *P18:9,*EL,*P18:9,UNDLN2;
          KEYIN     *P18:9,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      SLCT2000 IF EQUAL
.
          DISPLAY   *P18:9,*V2LON,OPTION; 
.
          BRANCH    OPTION,SLCT3000,SLCT4000,SLCT5000
.
.
          BEEP
          GOTO      SLCT1000
.
SLCT2000  MOVE      TRUE,EXIT              * Set exit flag
          GOTO      SLCT9999
.
********** Set Screen Headings ************
.
SLCT3000  MOVE      "- Single Care Plan",HEADING
          GOTO      SLCT6000
.
SLCT4000  MOVE      "- Single Diagnostic Category",HEADING
          GOTO      SLCT6000
.
SLCT5000  MOVE      "- All Care Plans",HEADING
          GOTO      SLCT6000
.
SLCT6000  DISPLAY   *P47:2,*V2LON,*+,HEADING,SP1
          MOVE      FALSE,EXIT
.
SLCT9999  RETURN                   
.
.*******************************************************************************
.*                                 SLSC0000                                    *
.*                      Display main selection screen                          *
.*******************************************************************************
. 
SLSC0000  HLINE     *G37,2,47,80
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = Print a Single Care Plan":
                    *P2:6,*V2LON,TWO,*HOFF," = Print Care Plans for a ":
                    "Diagnostic Category":
                    *P2:7,*V2LON,THREE,*HOFF," = Print All Care Plans":
                    *P2:9,*EL,"Select Option : "
.
SLSC9999  RETURN
.
.*******************************************************************************
.*                                 PSCP0000                                    *
.*                        Print a single Care Plan                             *
.*******************************************************************************
.
PSCP0000  MOVE      SP9,TPCDE
          DISPLAY   *P1:3,*EF:
                    *P4:4,"Enter the diagnosis code you want to print : ":
                    *P49:4,UNDLN9;
          KEYIN     *P49:4,*V2LON,TPCDE;
          DISPLAY   *P49:4,*V2LON,TPCDE;
.
          ENDSET    TPCDE
          APPEND    SP9,TPCDE
          RESET     TPCDE
.
          MATCH     SP9,TPCDE                 * nothing entered
          GOTO      PSCP8000 IF EQUAL 
.
.********** validated tpcde ************* . 
.
          MOVE      SIX,TYPE
          MOVE      ONE,LINE
          PACK      KEY13,TYPE,TPCDE,LINE
          CALL      RAPCDSC1
          BRANCH    OVRCD,PSCP3000
          CALL      DSSP0000
          GOTO      PSCP4000                
.
PSCP3000  DISPLAY   *P1:24,*B,"Invalid code ";
          CALL      HITENTER   
          GOTO      PSCP0000
.
PSCP4000  DISPLAY   *P1:24,"Is this the nursing diagnosis that you want to ":
                    "print ( / ) ?":
                    *P55:24,*V2LON,"Y",*P57:24,"N",*P62:24,UNDLN1;
          KEYIN     *P62:24,*V2LON,ANS
.
          MATCH     ANSY,ANS
          GOTO      PSCP5000 IF EQUAL         * Go and Print
.
          MATCH     ANSN,ANS
          GOTO      PSCP0000 IF EQUAL         * Return for new code
.
          GOTO      PSCP4000                  * bad selection - get another
.
PSCP5000  DISPLAY   *P1:24,*EL:
                    *P10:24,"Printing code : ": * Code being printed
                    *P26:24,TPCDE
.
.************* Set HEAD80 parameters ****************
.
          MOVE      ZERO,CPAGENO
          MOVE      "- Single Care Plan",CPHDROPT
          CLOCK     TIME,CTIMEIS
.
          CALL      HEAD80                    * print heading
          CALL      PRNT0000                  * print care plan
          MOVE      ANSY,END 
          GOTO      PSCP0000
.
PSCP8000  MATCH     ANSY,END
          GOTO      PSCP9999 IF NOT EQUAL
.
          PRINT     *N,"*** End of Report ***"
.
PSCP9999  RETURN




.
.*******************************************************************************
.*                                 PRNT0000                                    *
.*                     Print a single Patient Care Plan                        *
.*******************************************************************************
.
PRNT0000  PRINT     *N,"Nursing Diagnosis: ",PCDSDESC 
          MOVE      EIGHT,TYPE
          CALL      PRTP0000                 * Print related factors
          MOVE      TEN,TYPE
          CALL      PRTP0000                 * Print defining characteristics
          MOVE      SIX,TYPE
          CALL      PRTP0000                 * Print diff. diagnosis
          MOVE      SEVEN,TYPE
          CALL      PRTP0000                 * Print desired outcomes
          MOVE      NINE,TYPE    
          CALL      PRTP0000                 * Print interventions
.
PRNT9999  RETURN
.
.*******************************************************************************
.*                                 PRTP0000                                    *
.*                       Print out a description type                          *
.*******************************************************************************
.
PRTP0000  PACK      KEY20,TYPE,TPCDE,SP10
          MOVE      ANSY,PRHEAD
          CALL      RSPCDBS1
.
PRTP1000  CALL      RKPCDBS1                 * Read next "pair"
          BRANCH    OVRCD,PRTP9999
.
          MATCH     PCDBCLSS,TPCDE           * make sure we haven't read too far
          GOTO      PRTP9999 IF NOT EQUAL
.
          COMPARE   PCDBTYPE,TYPE            * still making sure we haven't read
.                                              too far
          GOTO      PRTP9999 IF NOT EQUAL      
.
          PACK      KEY13,TYPE,PCDBCODE,SP2
          CALL      RSPCDSC1
          
.
PRTP1250  CALL      RKPCDSC1                 * Read record of type 'TYPE'
          BRANCH    OVRCD,PRTP1000 
.
          MATCH     PCDBCODE,PCDSCODE        * make sure we haven't read too far
          GOTO      PRTP1000 IF NOT EQUAL
.
          COMPARE   TYPE,PCDSTYPE            * yep ,I'm still making sure that
.                                              we havent read too far
          GOTO      PRTP9999 IF NOT EQUAL
.
          MATCH     ANSY,PRHEAD              * check if we want heading
          GOTO      PRTP2000 IF EQUAL
.
PRTP1500  COMPARE   "55",CLNO                * check if its time for a new page
          CALL      HEAD80 IF NOT LESS
.
          PRINT     *20,PCDSDESC             * print description
          GOTO      PRTP1250
.
PRTP2000  MOVE      TYPE,IND  
          MOVE      ANSN,PRHEAD
          SUB       FIVE,IND
.
.  *** Set which sub-heading is required ***
.
          LOAD      HEAD,IND,DDHEAD,DOHEAD,RFHEAD,IVHEAD,DCHEAD
          PRINT     *N,HEAD;                  * print heading
          GOTO      PRTP1500
.
PRTP9999  RETURN.
.*******************************************************************************
.*                                 DSSP0000                                    *
.*              Displays Diagnosis description for verification                *.*******************************************************************************
.
DSSP0000  PACK      KEY13,TYPE,TPCDE,SP2
          CALL      RSPCDSC1
.
DSSP1000  CALL      RKPCDSC1                  * read details for verification
          BRANCH    OVRCD,DSSP9999
.
          MATCH     PCDSCODE,TPCDE            * make sure we have the right code
          GOTO      DSSP9999 IF NOT EQUAL
.
          COMPARE   TYPE,PCDSTYPE             * make sure we have the right type
          GOTO      DSSP9999 IF NOT EQUAL
.
          COMPARE   ONE,PCDSLINE              * see if we have the first line
          GOTO      DSSP2000 IF EQUAL         * print code and headings if have
.
DSSP1500  DISPLAY   *P20:6,*V2LON,PCDSDESC
          MOVE      PCDSDESC,DESC             
          GOTO      DSSP1000
          
DSSP2000  DISPLAY   *P1:3,*EF:
                    *P1:4,"Diagnosis Code   : ",*V2LON,TPCDE,*HOFF:
                    *P1:6,"Description      : ";
          GOTO      DSSP1500
.
DSSP9999  MOVE      DESC,PCDSDESC             * reset PCDBTYPE
          RETURN
.
.*******************************************************************************
.*                                 PPDC0000                                    *
.*             Print patient care plans by diagnostic category                 *
.*******************************************************************************
.
PPDC0000  HLINE     *G37,2,47,80
          DISPLAY   *P2:4,*EF,*HOFF,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = Health Patterns":
                    *P2:6,*V2LON,TWO,*HOFF," = Clinic Conditions":
                    *P2:7,*V2LON,THREE,*HOFF," = Response Patterns":
                    *P2:8,*V2LON,FOUR,*HOFF," = ABC PRN REST codes"
PPDC0500  KEYIN     *P2:10,"Select Option : __",*P18:10,*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      PPDC8000 IF EQUAL
.
          DISPLAY   *P18:10,*V2LON,OPTION
          MOVE      ANSN,END
.
PPDC1000  LOAD      TYPNO,OPTION,THREE,ONE,FOUR,FIVE *set type no.
          BRANCH    OPTION,PPDC2000,PPDC3000,PPDC4000,PPDC5000
.

          GOTO      PPDC0000
.
.
.******************* Health Pattern Option *************************
.
.
PPDC2000  DISPLAY   *P1:4,*EF:
                    *P47:2,*V2LON,"- Single Health Pattern ",*HOFF:
                    *P4:4,"Enter the health pattern code you want to print :":
                    " _________"
          KEYIN     *P54:4,*V2LON,TPCLS       * temporary class
          DISPLAY   *P54:4,*V2LON,TPCLS
.
          ENDSET    TPCLS
          APPEND    SP9,TPCLS
          RESET     TPCLS
.
          MATCH     TPCLS,SP9                 * nothing entered?
          GOTO      PPDC0000 IF EQUAL
.
          MATCH     QUEST,TPCLS
          GOTO      PPDC2050 IF EQUAL
.
.*** Validate against file ***
.
          MOVE      ONE,LNO
          PACK      KEY13,TYPNO,TPCLS,LNO     
          CALL      RAPCDSC1
          BRANCH    OVRCD,PPDC2100
.
          GOTO      PPDC2200
.
PPDC2050  MOVE      TYPNO,PCDSTYPE
          CALL      PCPDSCDS
          DISPLAY   *P1:24,*EL,"Health Pattern : _________"
          KEYIN     *P18:24,TPCLS
.
          ENDSET    TPCLS
          APPEND    SP10,TPCLS
          RESET     TPCLS
.
          MATCH     SP9,TPCLS
          GOTO      PPDC0000 IF EQUAL
. 
*** Validate Against File ***
.
          MOVE      ONE,LNO
          PACK      KEY13,TYPNO,TPCLS,LNO 
          CALL      RAPCDSC1
          BRANCH    OVRCD,PPDC2060
          GOTO      PPDC2200
.
PPDC2060  DISPLAY   *P1:24,*B,"Invalid Code. ";
          CALL      HITENTER
          GOTO      PPDC2050
.
PPDC2100  DISPLAY   *P1:24,*B,"Invalid Code. ";
          CALL      HITENTER
          GOTO      PPDC2000
.
.*** Display code and desc for verification *** .
PPDC2200  CALL      RDPCDSC1 
                    DISPLAY   *P1:3,*EF:
                    *P1:4,"Health Pattern   : ",TPCLS:
                    *P1:6,"Description      : ",PCDSDESC
PPDC2250  KEYIN     *P1:24,"Is this the health pattern you want to print ( / )":
                    " ? _",*P47:24,*V2LON,"Y",*P49:24,"N",*P54:24,ANS 
.
          MATCH     ANS,ANSY                  * go and print
          GOTO      PPDC2300 IF EQUAL
.         
          MATCH     ANS,ANSN                  * ask for another code
          GOTO      PPDC2000 IF EQUAL
.
          GOTO      PPDC2250 
.
.*** set HEAD80 parameters ***
.
PPDC2300  CLOCK     TIME,CTIMEIS
          MOVE      "- Health Patterns ",CPHDROPT
.
PPDC2400  PACK      KEY20,TYPNO,TPCLS,SP10
          CALL      RSPCDBS1           
          DISPLAY   *P1:24,*EL:
                    *P10:24,"Printing code : "
.
PPDC2500  CALL      RKPCDBS1
          BRANCH    OVRCD,PPDC0000
.
          COMPARE   TYPNO,PCDBTYPE            * check if we've read too far
          GOTO      PPDC0000 IF NOT EQUAL
.
          MATCH     TPCLS,PCDBCLSS            * check again
          GOTO      PPDC0000 IF NOT EQUAL
.
          MOVE      PCDBCODE,TPCDE             
          PACK      KEY13,SIX2,TPCDE,ONE2     
          CALL      RDPCDSC1                  * read entire record 
          CALL      HEAD80                    * print heading
          DISPLAY   *P26:24,TPCDE
          CALL      PRNT0000                  * print diagnosis
          MOVE      ANSY,END
          PACK      KEY20,TYPNO,TPCLS,TPCDE
          CALL      RSPCDBS1
          GOTO      PPDC2500
.
.
.*******************Clinic Conditions Option******************************
.
.
PPDC3000  DISPLAY   *P47:2,*V2LON,"- Single Clinic Condition ",*HOFF:
                    *P1:3,*EF:
                    *P4:4,"Enter the type of clinic condition you ":
                    "want to print : _________"
          KEYIN     *P59:4,*V2LON,TPTCC       * temporary type of cl. cond.
.
          ENDSET    TPTCC
          APPEND    SP10,TPTCC
          RESET     TPTCC
.
          MATCH     TPTCC,SP10                * nothing entered
          GOTO      PPDC0000 IF EQUAL
.
          MOVE      ONE2,PCDSTYPE
          MATCH     QUEST,TPTCC               * check for ? option
          GOTO      PPDC3150 IF EQUAL
.
.*** Validate code against file ***
.
          MOVE      ONE,LNO
          PACK      KEY13,TYPNO,TPTCC,LNO
          CALL      RAPCDSC1
          BRANCH    OVRCD,PPDC3100
.
          GOTO      PPDC3200
.
.
PPDC3100  DISPLAY   *P1:24,*B,"Invalid Code ";
          CALL      HITENTER
          GOTO      PPDC3000
.
.*** Question mark handling routine ***
.
PPDC3150  CALL      PCPDSCDS                  * Questionmark include routine
          DISPLAY   *P1:24,*EL,"Type of Condition : _________"
          KEYIN     *P21:24,*V2LON,TPTCC      * prompt for TPTCC after displayed
.
          ENDSET    TPTCC
          APPEND    SP10,TPTCC
          RESET     TPTCC
.
          MATCH     TPTCC,SP10                * nothing entered
          GOTO      PPDC0000 IF EQUAL
.
.*** Validate against file ***
.
          MOVE      ONE,LNO
          PACK      KEY13,TYPNO,TPTCC,LNO
          CALL      RAPCDSC1
          BRANCH    OVRCD,PPDC3160
.
. * Re-display type of clinic condition
           DISPLAY  *P4:4,"Enter the type of clinic condition you ":
                    "want to print : _________":
                    *P59:4,*V2LON,TPTCC
.
.
          GOTO      PPDC3200
.
PPDC3160  DISPLAY   *P1:24,*B,"Invalid Code. ";
          CALL      HITENTER
          GOTO      PPDC3150
.
PPDC3200  DISPLAY   *P1:5,*EF:
                    *P4:6,"Enter the clinic condition you want to print ":
                    "        : _________"
          KEYIN     *P59:6,*V2LON,TPCC        * temporary clinic condition
.
          ENDSET    TPCC
          APPEND    SP10,TPCC
          RESET     TPCC
.
          MATCH     TPCC,SP10                 * nothing entered
          GOTO      PPDC3000 IF EQUAL
.
          MOVE      TWO2,PCDSTYPE
          MATCH     QUEST,TPCC                * check for ?
          GOTO      PPDC3250 IF EQUAL
.
.*** Validate against file ***
.
          MOVE      ONE,LNO
          PACK      KEY13,TWO2,TPCC,LNO
          CALL      RAPCDSC1
          BRANCH    OVRCD,PPDC3210
.
          GOTO      PPDC3300
.
PPDC3210  DISPLAY   *P1:24,*B,"Invalid Code. ";
          CALL      HITENTER
          GOTO      PPDC3200
.
.*** Question mark handling routine ***
.
PPDC3250  CALL      PCPDSCDS
          DISPLAY   *P1:24,*EL,"Clinic Condition : _________"
          KEYIN     *P20:24,*V2LON,TPCC
.
          ENDSET    TPCC
          APPEND    SP10,TPCC
          RESET     TPCC
.
          MATCH     TPCC,SP10                 * nothing entered
          GOTO      PPDC0000 IF EQUAL
.
.*** Validate against file ***
.
          MOVE      ONE,LNO
          PACK      KEY13,TWO2,TPCC,LNO
          CALL      RAPCDSC1
          BRANCH    OVRCD,PPDC3260
.
          GOTO      PPDC3300

.
PPDC3260  DISPLAY   *P1:24,*B,"Invalid Code. ";
          CALL      HITENTER
          GOTO      PPDC3250
.
.*** Validate the combination of TPCC and TPTCC ***
.
PPDC3300  PACK      KEY20,ONE2,TPTCC,TPCC
          CALL      RAPCDBS1 
          BRANCH    OVRCD,PPDC3310
.
          GOTO      PPDC3400 
.
PPDC3310  DISPLAY   *P1:24,*B,"Invalid Combination ";
          CALL      HITENTER
          GOTO      PPDC3000
.
PPDC3400  PACK      KEY13,ONE2,TPTCC,SP2
          CALL      RSPCDSC1
          CALL      RKPCDSC1                  * read type cl cond description
.
          DISPLAY   *P1:3,*EF:                * display it
                    *P4:4,"Type of Condition : ",TPTCC:
                    *P4:5,"Description       : ",PCDSDESC
.
          PACK      KEY13,TWO2,TPCC,SP2
          CALL      RSPCDSC1
          CALL      RKPCDSC1                  * read clinic cond. description
.                                               and display it
          DISPLAY   *P4:7,"Clinic Condition : ",TPCC:
                    *P4:8,"Description      : ",PCDSDESC          
.
PPDC3450  DISPLAY   *P1:24,"Is this the clinic condition you want to print "
          KEYIN     *P48:24,"( / ) ? _",*P49:24,*V2LON,"Y",*P51:24,"N":
                    *P56:24,ANS
.
          MATCH     ANS,ANSY                  * go on and print
          GOTO      PPDC3500 IF EQUAL
.   
          MATCH     ANS,ANSN                  * go back to get another CC
          GOTO      PPDC3200 IF EQUAL
. 
          GOTO      PPDC3450
.
.*** set HEAD80 parameters ***
.
PPDC3500  CLOCK     TIME,CTIMEIS
          MOVE      "- Clinic Conditions ",CPHDROPT
          DISPLAY   *P1:24,*EL:
                    *P10:24,"Printing code : "
.
PPDC3600  PACK      KEY20,TWO2,TPCC,SP10
          CALL      RSPCDBS1
.
PPDC3800  CALL      RKPCDBS1                  * read record to be printed
          BRANCH    OVRCD,PPDC0000
.
          COMPARE   TWO2,PCDBTYPE             * make sure its the right type
          GOTO      PPDC0000 IF NOT EQUAL 
.
          MATCH     TPCC,PCDBCLSS             * make sure its the right class
          GOTO      PPDC0000 IF NOT EQUAL
.
          MOVE      PCDBCODE,TPCDE            
          PACK      KEY13,SIX2,PCDBCODE,ONE2
          CALL      RDPCDSC1                  * read in description
          DISPLAY   *P26:24,TPCDE
          CALL      HEAD80                    * print heading
          CALL      PRNT0000                  * print diagnosis
          MOVE      ANSY,END
.
          PACK      KEY20,TWO2,TPCC,TPCDE 
          CALL      RDPCDBS1
.
          GOTO      PPDC3800
.
.
.*******************RESPONSE PATTERNS OPTION************************
.
.
PPDC4000  DISPLAY   *P1:4,*EF:
                    *P47:2,*V2LON,"- Single Response Pattern ",*HOFF:
                    *P4:4,"Enter the response pattern code you want to print :":
                    " _________"
          KEYIN     *P56:4,*V2LON,TPCLS       * temporary class
          DISPLAY   *P56:4,*V2LON,TPCLS
.
          ENDSET    TPCLS
          APPEND    SP9,TPCLS
          RESET     TPCLS
.
          MATCH     TPCLS,SP9                 * nothing entered
          GOTO      PPDC0000 IF EQUAL
. 
          MATCH     QUEST,TPCLS
          GOTO      PPDC4050 IF EQUAL
.
. 
.*** Validate Against file ***
.
          MOVE      ONE,LNO
          PACK      KEY13,TYPNO,TPCLS,LNO 
          CALL      RAPCDSC1
          BRANCH    OVRCD,PPDC4100
.
          GOTO      PPDC4200
.
PPDC4050  MOVE      TYPNO,PCDSTYPE
          CALL      PCPDSCDS
          KEYIN     *P1:24,*EL,"Response Pattern Code : _________ ":
                    *P25:24,*V2LON,TPCLS
. 
          ENDSET    TPCLS
          APPEND    SP10,TPCLS
          RESET     TPCLS
.
          MATCH     TPCLS,SP10
          GOTO      PPDC0000 IF EQUAL
.
          MOVE      ONE,LNO
          PACK      KEY13,TYPNO,TPCLS,LNO
          CALL      RAPCDSC1
          BRANCH    OVRCD,PPDC4060
.
          GOTO      PPDC4200
.
PPDC4060  DISPLAY   *P1:24,*B,"Invalid Code. ";
          CALL      HITENTER
          GOTO      PPDC4050
.
PPDC4100  DISPLAY   *P1:24,*B,"Invalid Code ";
          CALL      HITENTER
          GOTO      PPDC4000
.
.*** display for verification ***
.
PPDC4200  CALL      RDPCDSC1
          DISPLAY   *P1:3,*EF:
                    *P1:4,"Response Pattern   : ",*V2LON,TPCLS,*HOFF:
                    *P1:6,"Description      : ",*V2LON,PCDSDESC
PPDC4250  KEYIN     *P1:24,"Is this the response pattern you want to print ":
                    "( / ) ? _",*P49:24,*V2LON,"Y",*P51:24,"N",*P56:24,ANS 
.
          MATCH     ANS,ANSY                  * go on and print
          GOTO      PPDC4300 IF EQUAL
.         
          MATCH     ANS,ANSN                  * go back for another code
          GOTO      PPDC4000 IF EQUAL
.
          GOTO      PPDC4250 
.
.*** set HEAD80 parameters ***
.
PPDC4300  CLOCK     TIME,CTIMEIS
          MOVE      "- Response Patterns ",CPHDROPT
.
PPDC4400  PACK      KEY20,TYPNO,TPCLS,SP10
          CALL      RSPCDBS1
          DISPLAY   *P1:24,*EL:
                    *P10:24,"Printing code : "
                    
.
PPDC4500  CALL      RKPCDBS1                  * read record to be printed
          BRANCH    OVRCD,PPDC0000
.  
          COMPARE   TYPNO,PCDBTYPE            * make sure we have the right type
          GOTO      PPDC0000 IF NOT EQUAL
.
          MATCH     TPCLS,PCDBCLSS            * make sure we have the right code
          GOTO      PPDC0000 IF NOT EQUAL
.
          MOVE      PCDBCODE,TPCDE
          PACK      KEY13,SIX2,TPCDE,ONE2
          CALL      RDPCDSC1                  * read description in
          DISPLAY   *P26:24,TPCDE
          CALL      HEAD80                    * print heading
          CALL      PRNT0000                  * print nursing diagnosis
          MOVE      ANSY,END
          PACK      KEY20,TYPNO,TPCLS,TPCDE
          CALL      RSPCDBS1
.
          GOTO      PPDC4500
      
.
.
.******************* ABC PRN REST codes OPTION *********************
.
.
PPDC5000  DISPLAY   *P1:4,*EF:
                    *P47:2,*V2LON,"- Single ABC PRN REST code ",*HOFF:
                    *P4:4,"Enter the ABC PRN REST code you want to print :":
                    " _________"
          KEYIN     *P52:4,*V2LON,TPCLS       * temporary class
          DISPLAY   *P52:4,*V2LON,TPCLS
.
          ENDSET    TPCLS
          APPEND    SP9,TPCLS
          RESET     TPCLS 
.
          MATCH     TPCLS,SP9                 * nothing entered
          GOTO      PPDC0000 IF EQUAL
.
          MATCH     QUEST,TPCLS
          GOTO      PPDC5050 IF EQUAL
.
.*** Validate against file ***
.
          MOVE      ONE,LNO
          PACK      KEY13,TYPNO,TPCLS,LNO
          CALL      RAPCDSC1
          BRANCH    OVRCD,PPDC5100
.
          GOTO      PPDC5200
.
PPDC5050  MOVE      TYPNO,PCDSTYPE
          CALL      PCPDSCDS
          KEYIN     *P1:24,*EL,"ABC PRN REST Code : _________ ":
                    *P21:24,*V2LON,TPCLS
. 
          ENDSET    TPCLS
          APPEND    SP10,TPCLS
          RESET     TPCLS
.
          MATCH     TPCLS,SP10
          GOTO      PPDC0000 IF EQUAL
.
          MOVE      ONE,LNO
          PACK      KEY13,TYPNO,TPCLS,LNO
          CALL      RAPCDSC1
          BRANCH    OVRCD,PPDC5060
.
          GOTO      PPDC5200
.
PPDC5060  DISPLAY   *P1:24,*B,"Invalid Code. ";
          CALL      HITENTER
          GOTO      PPDC5050
PPDC5100  DISPLAY   *P1:24,*B,"Invalid Code ";
          CALL      HITENTER
          GOTO      PPDC5000
.
.*** read and display for verification ***
.
PPDC5200  CALL      RDPCDSC1
          DISPLAY   *P1:3,*EF:
                    *P1:4,"ABC PRN REST codes : ",*V2LON,TPCLS,*HOFF:
                    *P1:6,"Description        : ",*V2LON,PCDSDESC
PPDC5250  KEYIN     *P1:24,"Is this the ABC PRN REST codes you want to print ":
                    "( / ) ? _",*P51:24,*V2LON,"Y",*P53:24,"N":
                    *P58:24,*V2LON,ANS 
.
          MATCH     ANS,ANSY                  * go on and print
          GOTO      PPDC5300 IF EQUAL
.         
          MATCH     ANS,ANSN                  * go back for another code
          GOTO      PPDC5000 IF EQUAL
.
          GOTO      PPDC5250 
.
.*** set HEAD80 parameters ***
.
PPDC5300  CLOCK     TIME,CTIMEIS
          MOVE      "- ABC PRN REST code ",CPHDROPT
          DISPLAY   *P1:24,*EL:
                    *P10:24,"Printing code : "
.
PPDC5400  PACK      KEY20,TYPNO,TPCLS,SP10
          CALL      RSPCDBS1
.
PPDC5500  CALL      RKPCDBS1                  * read record to be printed
          BRANCH    OVRCD,PPDC0000
.
          COMPARE   TYPNO,PCDBTYPE            * got the right type?
          GOTO      PPDC0000 IF NOT EQUAL
.
          MATCH     TPCLS,PCDBCLSS            * got the right code?
          GOTO      PPDC0000 IF NOT EQUAL
.
          MOVE      PCDBCODE,TPCDE
          PACK      KEY13,SIX2,TPCDE,ONE2
          CALL      RDPCDSC1
          DISPLAY   *P26:24,TPCDE
          CALL      HEAD80                    * print heading
          CALL      PRNT0000                  * print nursing diagnosis
          MOVE      ANSY,END 
.
          PACK      KEY20,TYPNO,TPCLS,TPCDE
          CALL      RSPCDBS1
          GOTO      PPDC5500
.
PPDC8000  MATCH     ANSY,END
          GOTO      PPDC9999 IF NOT EQUAL
          PRINT     *N,"*** End of Report ***"
.
PPDC9999  RETURN
.
.*******************************************************************************
.*                                 PACP0000                                    *
.*                      Print all Patient Care Plan                            *
.*******************************************************************************
.
PACP0000  DISPLAY   *P1:24,"Do you want to print the care plans for all ":
                    "diagnosis codes ( / ) ?",*V2LON,*P62:24,"Y",*P64:24,"N":
                    *HOFF,*P69:24,UNDLN1;
          KEYIN     *P69:24,ANS;
          DISPLAY   *P69:24,*V2LON,ANS
.
          MATCH     ANS,ANSY                  * go on and print 
          GOTO      PACP1000 IF EQUAL
.
          MATCH     ANS,ANSN                  * go back to main menu
          GOTO      PACP9999 IF EQUAL
.
          GOTO      PACP0000          
.
.*** set HEAD80 parameters ***
.
PACP1000  MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
          MOVE      "- All Care Plans",CPHDROPT
.
          MOVE      SIX,TYPNO
          PACK      KEY13,TYPNO,SP20
          CALL      RSPCDSC1
.
PACP2000  CALL      RKPCDSC1                  * read next record
          BRANCH    OVRCD,PACP3000
.
          COMPARE   TYPNO,PCDSTYPE            * has it got the right type    
          GOTO      PACP3000 IF NOT EQUAL 
.                          
          MOVE      PCDSCODE,TPCDE
          DISPLAY   *P1:24,*EL:
                    *P10:24,"Printing code : ":
                    *P26:24,TPCDE
          CALL      HEAD80                    * print heading
          CALL      PRNT0000                  * print nursing diagnosis
          PACK      KEY13,TYPNO,TPCDE,Z20     * rest key
          CALL      RSPCDSC1
          GOTO      PACP2000 
.
PACP3000  PRINT     *N,"*** End of Report ***"
.
PACP9999  RETURN        
.*******************************************************************************
          INC       STD001IO
          INC       PCPDSCIO/INC
          INC       PCPDSCDS
          INC       PCPDBSIO/INC
