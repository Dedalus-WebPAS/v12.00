. ************************************************************
. *                I N C L U D E  S U M M A R Y              *
. *                - - - - - - -  - - - - - - -              *
. *                                                          *
. *     PROCEDURE NAME:        USRDSCOD                      *
. *                                                          *
. *     FUNCTION:              CODES FILE USER DEFINED FIELDS*
. *                           KEYIN     USER   KEYUCOD       *
. *                           DISPLAY   USER   DSPUCOD       *
. *                           PRINT DN  USER   PRTUCOD       *
. *                           PRINT AC  USER                 *
. *                               HEADINGS     MSTMCAT       *
. *                               UNDERLINE    UNDMCAT       *
. *                               CODES DESC   MSTMCOD       *
. *                                                          *
. *     AUTHOR:                MALCOLM HAYSE (I.B.A)         *
. *                                                          *
. *     WRITTEN:               16/02/88                      *
. *                                                          *
. *     DATA INCLUDE :         USRCODFD                      *
. *                                                          *
. *  .) DSPUCOD must be called first                         *
. * set TUTYPE :    The first letter of category  eg N       *
. *     TONOFF :    This says if its is ON or OFF            *
. *                 All 10 must be set TONOFF1,TONOFF2....10 *
. *             eg. TONOFF3=1, category N3 is ON             *
. *     TMANDT :    This says if its is Mandatory or Not     *
. *                 All 10 must be set TMANDT1,TMANDT2....10 *
. *             eg. TMANDT3=1, category N3 is Mandatory      *
. *                                                          *
. *     TVERT  :    Line Pos of FIRST User defined field     *
. *                 **** THIS MUST ALWAYS BE THE FIRST ****  *
. *     TCOL   :    Coloumn Postition of KEYIN               *
. *     TCOL1  :    Coloumn Postition of SCRDISP             *
. *     TFIELD :    Must be the FIRST Actual Starting Field  *
. *             eg. The first user defined field is "26"     *
. *     TDESCLN:    THREE description lengths ar available   *
. *                 TDESCLN=0 display TDESC 20 characters    *
. *                 TDESCLN=1         TDESC15  characters    *
. *                 TDESCLN=2         TDESC10  characters    *
. *                                                          *
. *  .) KEYUCOD must be called first                         *
. *     TCOL   :    Coloumn Postition of KEYIN               *
. *     TXFIELD :    Must be the Actual field of User field   *
. *                 The 3rd User defined Field FIELD=28      *
. *                 (FIELD-TFIELD+1=Xth User Defined Field  *
. *             eg. ( 28  -  26 + 1 =3rd User Defined Field) *
. *                                                          *
. ************************************************************
. ************************************************************
. *     KEYIN   DESCRIPTION                                  *
. *                                                          *
. *       FIELD                   COLON  CODE DESCRIPTION    *
. *       |   CATEGORY            | CODE |                   *
. *       |   |                   | |    |                   *
. *  EG.  26. MR CATEGORY         : 1    MR DESCRPION - TVERT*
. *       |                       | |    |                   *
. *       |                       | TCOL |                   *
. *       TCOL1                   TCOL-2 TCOL+4              *
. *                                                          *
. *     LET TCOL   IS THE COLUMN TO KEYIN AT                 *
. *         FIELD  FIELD NUMBER                              *
. *                                                          *
. * Mods : 12/04/1994  Steve Armstrong                       *
. *        Changed PATCODDS to PATCODDS                      *
. ************************************************************
.
.
.       Input User Defined Field
.
.      CALCULTATE WHICH CATALOG BY GETTING THE XTH TURNED ON CATA
.      FIELD IS THE IST CATA,2ND CATA,3rd CATA (TURNED ON)
.
KEYUCOD   MOVE      FIELD,TXFIELD
          SUB       TFIELD,TXFIELD
          ADD       ONE,TXFIELD               *** OFFSET
          MOVE      TVERT,CVERT
.
.   GET THOSE THAT EXIST AND ARE TURNED ON
.
          MOVE      ZERO,IDAY    ** DUMMY FORM 2 COUNTER
          MOVE      ZERO,IMON    ** DUMMY FORM 2 LIVE FIELD NUMBER X
.
NXTKCOD   COMPARE   TEN,IDAY
          GOTO      OVERCOND IF EQUAL *** NONE EXIST
.
          ADD       ONE,IDAY
          LOAD      TONOFF USING IDAY OF TONOFF1,TONOFF2,TONOFF3:
                    TONOFF4,TONOFF5,TONOFF6:
                    TONOFF7,TONOFF8,TONOFF9,TONOFF10
.
          BRANCH    TONOFF OF KEYUCOD0
          GOTO      NXTKCOD
.
.   IT IS TURNED ON
.
KEYUCOD0  LOAD      TUCATA USING IDAY INTO TUCATA1,TUCATA2,TUCATA3:
                    TUCATA4,TUCATA5,TUCATA6:
                    TUCATA7,TUCATA8,TUCATA9,TUCATA10
.
          MATCH     SP2,TUCATA
          GOTO      NXTKCOD IF EQUAL
.
.   IT EXISTS      (now is it the XTH FIELD)
.
          ADD       ONE,IMON
          COMPARE   TXFIELD,IMON
          GOTO      NXTKCOD IF NOT EQUAL
.
          LOAD      TUCODE USING IDAY INTO TUCODE1,TUCODE2,TUCODE3:
                    TUCODE4,TUCODE5,TUCODE6:
                    TUCODE7,TUCODE8,TUCODE9,TUCODE10
.
          CLEAR     AUCONBEF
          APPEND    " Cata : ",AUCONBEF
          APPEND    TUCATA,AUCONBEF
          APPEND    " Code : ",AUCONBEF
          APPEND    TUCODE,AUCONBEF
          APPEND    SP30,AUCONBEF
          RESET     AUCONBEF
.
          LOAD      TMANDT USING IDAY INTO TMANDT1,TMANDT2,TMANDT3:
                    TMANDT4,TMANDT5,TMANDT6:
                    TMANDT7,TMANDT8,TMANDT9,TMANDT10
.
          ADD       IMON,CVERT
          SUB       ONE,CVERT
.
KEYUCOD1  MOVE      SP3,TUCODE
.
          STORE     SP3 USING IDAY INTO TUCODE1,TUCODE2,TUCODE3:
                    TUCODE4,TUCODE5,TUCODE6:
                    TUCODE7,TUCODE8,TUCODE9,TUCODE10
.
          KEYIN     *PTCOL:CVERT,*EL,*V2LON,TUCODE
          RESET     TUCODE
          GOTO      CHKUCOD1 IF EOS
.
          ENDSET    TUCODE
          APPEND    SP3,TUCODE
          RESET     TUCODE
.
          CMATCH    EXITCHAR,TUCODE
          GOTO      OVERCOND IF EQUAL
.
          CMATCH    QUEST,TUCODE
          GOTO      KEYUCOD2 IF NOT EQUAL
.
          MOVE      TUCATA,CODE
          MATCH     "A3",CODE
          IF        @EQUAL
            IF        AECNSQA3=1
              BEEP
              GOTO      KEYUCOD1
            ENDIF
          ENDIF
          CALL      PATCODDS
.
          CALL      DISPSC
.
          STORE     SP3 USING IDAY INTO TUCODE1,TUCODE2,TUCODE3:
                    TUCODE4,TUCODE5,TUCODE6:
                    TUCODE7,TUCODE8,TUCODE9,TUCODE10
.
          CALL      DISPDA
          GOTO      KEYUCOD
.
KEYUCOD2  PACK      KEY5 WITH TUCATA,TUCODE
          CALL      RDCODE1
          BRANCH    OVRCD OF INVKCOD
.
          STORE     TUCODE USING IDAY INTO TUCODE1,TUCODE2,TUCODE3:
                    TUCODE4,TUCODE5,TUCODE6:
                    TUCODE7,TUCODE8,TUCODE9,TUCODE10
.
          MATCH     SP3,TUCODE      *** ALLOW 3 SPACES
          RETURN    IF EQUAL
.
          CALL      DSPUCOD
          RETURN
.
CHKUCOD1  MOVE      ZERO,OVRCD
          COMPARE   ONE,TMANDT
          RETURN    IF NOT EQUAL
.
          MOVE      "Mandatory",TDESC10
          MOVE      "** Mandatory **",TDESC15
          MOVE      "** Mandatory Field **",TDESC
          BEEP
          CALL      DSPCODX
          DISPLAY   *PTCOL3:CVERT,*W2,*EL;
          GOTO      KEYUCOD1
.
INVKCOD   MOVE      "*Invalid*",TDESC10
          MOVE      "** Invalid **",TDESC15
          MOVE      "** Invalid Code **   ",TDESC
          BEEP
          CALL      DSPCODX
          DISPLAY   *PTCOL3:CVERT,*W2,*EL;
          GOTO      KEYUCOD1
.
. ************************************************************
. *     DISPLAY DESCRIPTION                                  *
. *                                                          *
. *  EG.  12. MADATORY CATAGORY   : XXX DESCRIPTION -- TVERT *
. *       |                       | |    |                   *
. *       |                       | TCOL |                   *
. *       TCOL1                   TCOL-2 TCOL+4              *
. *                               (TCOL2)(TCOL3)             *
. *     LET TCOL1  BE THE START OF THE USER DEFINED FIELD    *
. *         TCOL   BE THE START OF THE USER DEFINED CODE     *
. ************************************************************
.
.       DISPLAY ANY USER IN CORRECT FORMAT
.
DSPUCOD   STORE     TDESC USING TDESCLN OF TDESC15,TDESC10
.
DSPCODX   BRANCH    TDESCLN OF DSPCOD1,DSPCOD2
          DISPLAY   *PTCOL3:CVERT,*V2LON,TDESC;
          RETURN
.
DSPCOD1   DISPLAY   *PTCOL3:CVERT,*V2LON,TDESC15;
          RETURN
.
DSPCOD2   DISPLAY   *PTCOL3:CVERT,*V2LON,TDESC10;
          RETURN
.
.        TONOFF1-10 MUST BE SET        ** ON OR OFF N1,N2,N3..N0
.        TMANDT1-10 MUST BE SET        ** MANDATORY OR NOT N1,N2,..N0
.
.        FIELD     STARTING "FIELD" MUST BE SET
.        TCOL      KEYIN COLOUMN POSTION
.        TCOL1     DISPLAY STARTING COL POSTION
.
SCRUCOD   MOVE      ZERO,IDAY
          MOVE      TFIELD,TXFIELD
          MOVE      TVERT,CVERT
.
          MOVE      TCOL,TCOL2
          SUB       TWO,TCOL2         * POSITION OF COLON
.
          MOVE      TCOL,TCOL3
          ADD       FOUR,TCOL3        * POSITION OF DESCRIPTION
.
.     READ 10 RECORDS   N1,N2,N3..............N0
.
NEXTCOD   COMPARE   TEN,IDAY
          RETURN    IF EQUAL
.
          ADD       ONE,IDAY
          STORE     SP2 USING IDAY INTO TUCATA1,TUCATA2,TUCATA3:
                    TUCATA4,TUCATA5,TUCATA6:
                    TUCATA7,TUCATA8,TUCATA9,TUCATA10
.
          MOVE      IDAY,FORM1
          PACK      KEY5 WITH TUTYPE,FORM1,SP3
          CALL      RDCODE1
          BRANCH    OVRCD OF NEXTCOD
.
.     IS It TURNED ON OR OFF
.
          LOAD      TONOFF USING IDAY OF TONOFF1,TONOFF2,TONOFF3:
                    TONOFF4,TONOFF5,TONOFF6:
                    TONOFF7,TONOFF8,TONOFF9,TONOFF10
.
          BRANCH    TONOFF OF ONUCOD
          GOTO      NEXTCOD
.
ONUCOD    STORE     TCODE USING IDAY INTO TUCATA1,TUCATA2,TUCATA3:
                    TUCATA4,TUCATA5,TUCATA6:
                    TUCATA7,TUCATA8,TUCATA9,TUCATA10
.
          COMPARE   ZERO,TFIELD            ** ENQUIRY PROG
          GOTO      ONUCOD1 IF EQUAL
.
.      MAINTENANCE PROGRAM
.
          DISPLAY   *PTCOL1:CVERT,TXFIELD,DOT,SP1,TDESC:
                    *PTCOL2:CVERT,COLON
          ADD       ONE,TXFIELD
          ADD       ONE,CVERT
          GOTO      NEXTCOD
.
.      ENQUIRY PROGRAM
.
ONUCOD1   DISPLAY   *PTCOL1:CVERT,SP4,TDESC:
                    *PTCOL2:CVERT,COLON
          ADD       ONE,CVERT
          GOTO      NEXTCOD
.
.          DISPLAY THE CATALOGS
.
.        TUTYPE   MUST BE SET          ** TYPE OF CATEGORY "N"=NEWBORN
.        TONOFF1-10 MUST BE SET        ** ON OR OFF N1,N2,N3..N0
.
DATUCOD   MOVE      ZERO,IDAY
          MOVE      TFIELD,TXFIELD
          MOVE      TVERT,CVERT
.
          MOVE      TCOL,TCOL3
          ADD       FOUR,TCOL3        * POSITION OF DESCRIPTION
.
.     READ 10 RECORDS   N1,N2,N3..............N0
.
NXTDCOD   ADD       ONE,IDAY
          COMPARE   TEN1,IDAY
          RETURN    IF EQUAL
.
          LOAD      TONOFF USING IDAY OF TONOFF1,TONOFF2,TONOFF3:
                    TONOFF4,TONOFF5,TONOFF6:
                    TONOFF7,TONOFF8,TONOFF9,TONOFF10
.
          BRANCH    TONOFF OF NXTDCOD1
          GOTO      NXTDCOD
.
NXTDCOD1  LOAD      TUCODE USING IDAY INTO TUCODE1,TUCODE2,TUCODE3:
                    TUCODE4,TUCODE5,TUCODE6:
                    TUCODE7,TUCODE8,TUCODE9,TUCODE10
.
          MATCH     SP3,TUCODE
          GOTO      NXTDCOD2 IF EQUAL
.
          MOVE      IDAY,FORM1
          PACK      KEY5 WITH TUTYPE,FORM1,TUCODE
          CALL      RDCODE1
          BRANCH    OVRCD OF NXTDCOD
.
          DISPLAY   *PTCOL:CVERT,*V2LON,TUCODE
          CALL      DSPUCOD
.
NXTDCOD2  ADD       ONE,CVERT
          GOTO      NXTDCOD
.
.        PRINT THE CATALOGS
.               TCOL1 & TCOL MUST BE SET   ALL TUCODES SET
.
PRTUCOD   MOVE      ZERO,IDAY
          MOVE      TVERT,CVERT
.
          MOVE      TCOL,TCOL2
          SUB       TWO,TCOL2        * POSITION OF COLON
.
          MOVE      TCOL,TCOL3
          ADD       FOUR,TCOL3        * POSITION OF DESCRIPTION
.
.     READ 10 RECORDS   N1,N2,N3..............N0
.
NXTPCOD0  ADD       ONE,IDAY
          COMPARE   TEN1,IDAY
          RETURN    IF EQUAL
.
          LOAD      TONOFF USING IDAY OF TONOFF1,TONOFF2,TONOFF3:
                    TONOFF4,TONOFF5,TONOFF6:
                    TONOFF7,TONOFF8,TONOFF9,TONOFF10
.
          BRANCH    TONOFF OF NXTPCOD1
          GOTO      NXTPCOD0
.
.     READ 10 RECORDS   N1,N2,N3..............N0
.
NXTPCOD1  MOVE      IDAY,FORM1
          PACK      TUCATA WITH TUTYPE,FORM1
          PACK      KEY5 WITH TUCATA,SP3
          CALL      RDCODE1
          BRANCH    OVRCD OF NXTPCOD0
.
          MOVE      TDESC,TDESC15
.
          LOAD      TUCODE USING IDAY INTO TUCODE1,TUCODE2,TUCODE3:
                    TUCODE4,TUCODE5,TUCODE6:
                    TUCODE7,TUCODE8,TUCODE9,TUCODE10
.
          MOVE      SP30,TDESC
.
          MATCH     SP3,TUCODE
          GOTO      NXTPCOD2 IF EQUAL
.
          MOVE      IDAY,FORM1
          PACK      KEY5 WITH TUTYPE,FORM1,TUCODE
          CALL      RDCODE1
.
NXTPCOD2  PRINT     *TCOL1,TDESC15,*TCOL2,COLON,*TCOL,TDESC
          ADD       ONE,CLNO
          ADD       ONE,CVERT
          GOTO      NXTPCOD0
.
.
.   ** Category Headings For Master File Listings
.
MSTUCAT   MOVE      ZERO,IDAY
          MOVE      CCOL,TCOL    ** Position of Description
.
.     READ 10 RECORDS   N1,N2,N3..............N0
.
NXTMCAT0  ADD       ONE,IDAY
          COMPARE   TEN1,IDAY
          RETURN    IF EQUAL
.
          LOAD      TONOFF USING IDAY OF TONOFF1,TONOFF2,TONOFF3:
                    TONOFF4,TONOFF5,TONOFF6:
                    TONOFF7,TONOFF8,TONOFF9,TONOFF10
.
          BRANCH    TONOFF OF NXTMCAT1
          GOTO      NXTMCAT0
.
.     READ 10 RECORDS   N1,N2,N3..............N0
.
NXTMCAT1  MOVE      IDAY,FORM1
          PACK      TUCATA WITH TUTYPE,FORM1
          PACK      KEY5 WITH TUCATA,SP3
          CALL      RDCODE1
          BRANCH    OVRCD OF OVERCOND
.
          MOVE      TDESC,TDESC10
          REP       UPPLOW,TDESC10
.
          PRINT     *CCOL,TDESC10;
.
          ADD       TEN4,CCOL
.
          GOTO      NXTMCAT0
.
.
.   ** Underline Category Headings For Master File Listings
.
UNDUCAT   MOVE      ZERO,IDAY
          MOVE      CCOL,TCOL    ** Position of Description
.
.     READ 10 RECORDS   N1,N2,N3..............N0
.
NXTUCAT0  ADD       ONE,IDAY
          COMPARE   TEN1,IDAY
          RETURN    IF EQUAL
.
          LOAD      TONOFF USING IDAY OF TONOFF1,TONOFF2,TONOFF3:
                    TONOFF4,TONOFF5,TONOFF6:
                    TONOFF7,TONOFF8,TONOFF9,TONOFF10
.
          BRANCH    TONOFF OF NXTUCAT1
          GOTO      NXTUCAT0
.
.     READ 10 RECORDS   N1,N2,N3..............N0
.
NXTUCAT1  MOVE      IDAY,FORM1
          PACK      TUCATA WITH TUTYPE,FORM1
          PACK      KEY5 WITH TUCATA,SP3
          CALL      RDCODE1
          BRANCH    OVRCD OF OVERCOND
.
          MOVE      "----------",TDESC10
.
          PRINT     *CCOL,TDESC10;
.
          ADD       TEN4,CCOL
.
          GOTO      NXTUCAT0
.
.
MSTMCOD   MOVE      ZERO,IDAY
          MOVE      CCOL,TCOL    ** Position of Description
.
.     READ 10 RECORDS   N1,N2,N3..............N0
.
NXTMCOD0  ADD       ONE,IDAY
          COMPARE   TEN1,IDAY
          RETURN    IF EQUAL
.
          LOAD      TONOFF USING IDAY OF TONOFF1,TONOFF2,TONOFF3:
                    TONOFF4,TONOFF5,TONOFF6:
                    TONOFF7,TONOFF8,TONOFF9,TONOFF10
.
          BRANCH    TONOFF OF NXTMCOD1
          GOTO      NXTMCOD0
.
.     READ 10 RECORDS   N1,N2,N3..............N0
.
NXTMCOD1  MOVE      IDAY,FORM1
.
          LOAD      TUCODE USING IDAY INTO TUCODE1,TUCODE2,TUCODE3:
                    TUCODE4,TUCODE5,TUCODE6:
                    TUCODE7,TUCODE8,TUCODE9,TUCODE10
.
          MOVE      SP30,TDESC
.
          MATCH     SP3,TUCODE
          GOTO      NXTMCOD2 IF EQUAL
.
          MOVE      IDAY,FORM1
          PACK      KEY5 WITH TUTYPE,FORM1,TUCODE
          CALL      RDCODE1
.
NXTMCOD2  MOVE      TDESC,TDESC10
.
          PRINT     *CCOL,TDESC10;
          ADD       TEN4,CCOL
.
          GOTO      NXTMCOD0
.
