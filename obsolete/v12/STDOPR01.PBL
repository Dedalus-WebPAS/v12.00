.******************************************************************************
.    STDOPR01.PBL
.    This include contains various routines which were commonly used in the
.    old theatre system thus instead of reproducing code I've compiled them
.    into a single include.
.
.         V9.04.00  01/09/2005 Ebon Clements CAR 73209
.                   Removed OPCNFTYP,OPCNFTTM,OPCNTTYP,OPCNTTTM               
.                   as they are not used                                     
.         V5.01.01  06/03/1991    J.Tan     SRF 107987
.                   Moved parameters of using operation usage field 6,7,8
.                   to sector 42
.         V5.03.01  04/01/1996  Greg Horvat
.                   Removed the menu program, security key, IBA screen password
.                   & data directory control file reads
.
.    ROUTINE ROSTER  DESCRIPTION
.    --------------  --------------------------------------------------------
.    DOCNM000        Set up doctors name after a successful read on doctor file.
.
.    GOPTN000        Get options 
.
.    OPADDTIM        Adds or subtracts durations to start time
.
.    RDOPRCON        Read all system parameters from sector 40 and 42 of 
.                    controlf regarding operating room system.
.
.    QURTN000        Question mark routine display codes from doctors or 
.                    clinic depending on sys params, also set the question 
.                    mark flag SCRNFLAG.
.
.    VDCLN000        Validate clinic or surgeon code Passed in SESNCODE
.                    And get description and store in DESC40.
.
.******************************************************************************
.                                 DOCNM000
.    Set up doctors name after a successful read on doctor file.
.****************************************************************************
DOCNM000
          MOVE      TWO,PACFRMT        * title name surname of doctor
          MOVE      DSNAME,PACSNAME    * use pacdate to fix doctors name
          MOVE      DGNAME,PACGNAME    * get first letter in given name
          MOVE      DTITL,PACTITLE
          CALL      PACNAME            * pack up doctors name
          MOVE      PACFNAME,DESC40
DOCNM999  RETURN
.*****************************************************************************
.                                 GOPTN000
.    Get options 
.    The Prompt line must be displayed and options coded in MASK and CCITEM
.    is a flag to allow numbers also XPOS keyin position.
.    EXIT=0 if return hit 
.*****************************************************************************
GOPTN000
          KEYIN     *PXPOS:24,*JR,*ZF,*V2LON,REPLY
          ENDSET    REPLY
          APPEND    SP3,REPLY
          RESET     REPLY
          REPLACE   UPPLOW,REPLY
.
          TYPE      REPLY               * a number was entered
          GOTO      GOPTN555 IF EQUAL
.
          REPLACE   MASK,REPLY          * lettter was entered
          TYPE      REPLY 
          GOTO      GOPTN888 IF EQUAL
          BEEP
          GOTO      GOPTN000
GOPTN555                               * number was entered
          BRANCH    CCITEM,GOPTN000
          MOVE      REPLY,CCITEM 
          COMPARE   CCITEM,ZERO
          GOTO      GOPTN000 IF NOT LESS
          MOVE      ZERO,EXIT          * valid number exit=0
          GOTO      GOPTN999
GOPTN888
          MOVE      REPLY,EXIT
          GOTO      GOPTN999
GOPTN999  RETURN
.*******************************************************************************
.
.                                 OPADDTIM
.
.    This function adds and subtracts durations to start time
.  
.    PARAMETERS.
.    EXPSTART : Expected Start time for duration to be added to
.    EXPDURA  : Expected Duration which is added to start time  (who is VGER)
.               To subtract EXPDURA is negative.
.
.    RETURN PARAMETERS.
.    EXPSTART : Contains the new start time.
.
.    It works somthing like this.
.
.    /* chour and cmin are expected hours and mins */
.
.    totmins = chour*60 + cmin + expdura;
.    if ( totmins < 0 ) totmins += 1440;
.    else totmins -= 1440;
.    newhour = (int)(totmins/60);
.    newmin  = totmins - newhour*60;
.
.*******************************************************************************
OPADDTIM
          UNPACK    EXPSTART,CHOUR,ANS,CMIN
          MOVE      CHOUR,FORM4        * FORM4 will be totmins
          MOVE      CMIN,FORM2
.
          MULT      "60",FORM4         * convert hours to minutes
          ADD       FORM2,FORM4        * add the minutes
          ADD       EXPDURA,FORM4      * add the duration for total mins
.
ADDTIM22
          COMPARE   ZERO,FORM4         * test if added or subtracted
          GOTO      ADDTIM33 IF NOT LESS
          ADD       "1440",FORM4       * add a day from totmins
          GOTO      ADDTIM22
.
ADDTIM33
          COMPARE   "1440",FORM4       * test if added or subtracted
          GOTO      ADDTIM44 IF LESS
          SUB       "1440",FORM4       * add a day from totmins
          GOTO      ADDTIM33
.
ADDTIM44
          MOVE      FORM4,SAVEF10      * save the new total mins
          DIV       "60",FORM4
          MOVE      FORM4,FORM2
          MOVE      FORM2,CHOUR        * save the new hours
.
          MULT      "60",FORM4         * calculate remaining minutes
          SUB       FORM4,SAVEF10
          MOVE      SAVEF10,FORM2
          MOVE      FORM2,CMIN         * save the new minutes
.
          PACK      EXPSTART,CHOUR,COLON,CMIN
          REP       " 0",EXPSTART
ADDTIM99  RETURN
.*******************************************************************************
.                                 RDOPRCON
.    Read all system parameters from sector 40 and 42 of controlf regarding
.    operating room system.
.******************************************************************************
RDOPRCON  READ      CONTROLF,FORTY;*43,OPCNAUDA,OPCNAUDB,OPCNAUDC:
                                       OPCNAUDD,OPCNAUDE,OPCNAUDF:
                                       OPCNAUDG,OPCNAUDH,OPCNAUDI:
                                       OPCNAUDJ,OPCNAUDK,OPCNAUDL:
                                       OPCNAUDM,OPCNAUDN,OPCNAUDO:
                                       OPCNAUDP,OPCNAUDQ,OPCNAUDR:
                                       OPCNAUDS,OPCNAUDT,OPCNAUDU:
                                       OPCNAUDV,OPCNAUDW,OPCNAUDX:
                                       OPCNAUDY,OPCNAUDZ
.
          READ      CONTROLF,FORTY;*69,OPCNEMER,*81,OPCNODSC,OPCNCDSC:
                                   *111,OPCNCLSU,OPCNCODE,OPCNRUSI:
                                   *114,OPCNSESS,OPCNNOOP,OPCNBOOK,OPCNMAXB:
                                   *119,OPCNNREQ,OPCNNREC
.
          READ      CONTROLF,FORTY;*143,OPCNBRKA,OPCNBRKB,OPCNBRKC:
                                        OPCNBRKD,OPCNBRKE,OPCNBRKF:
                                        OPCNBRKG,OPCNBRKH,OPCNBRKI:
                                        OPCNBRKJ,OPCNBRKK,OPCNBRKL
.
          READ      CONTROLF,FORTY;*203,OPCNUOU1,OPCNUOM1,OPCNUOU2:
                                        OPCNUOM2,OPCNUOU3,OPCNUOM3:
                                        OPCNUOU4,OPCNUOM4,OPCNUOU5:
                                        OPCNUOM5
.
          READ      CONTROLF,FORTY;*213,OPCNUNU1,OPCNUNM1,OPCNUNU2:
                                        OPCNUNM2,OPCNUNU3,OPCNUNM3:
                                        OPCNUNU4,OPCNUNM4,OPCNUNU5:
                                        OPCNUNM5
.
          READ      CONTROLF,FORTY;*223,OPCNUDU1,OPCNUDM1,OPCNUDU2:
                                        OPCNUDM2,OPCNUDU3,OPCNUDM3:
                                        OPCNUDU4,OPCNUDM4
.
          READ      CONTROLF,FORTY;*231,OPCNUTU1,OPCNUTM1,OPCNUTU2:
                                        OPCNUTM2,OPCNUTU3,OPCNUTM3:
                                        OPCNUTU4,OPCNUTM4
.
          READ      CONTROLF,FORTY;*239,OPCNUUU1,OPCNUUM1,OPCNUUU2:
                                        OPCNUUM2,OPCNUUU3,OPCNUUM3:
                                        OPCNUUU4,OPCNUUM4,OPCNUUU5:
                                        OPCNUUM5
.
          READ      CONTROLF,FORTY2;OPCNMDUR,OPCNMERG,OPCNTFLG,OPCNMREC:
                                   OPCNOPSU,OPCNPROV,OPCNBPIU,OPCNNCOP:
                                   *16,OPCNNPRV,OPCNUDSC,OPCNIDFL,OPCNFOTM:
                                   OPCNTOTM,*25,OPCNANAE,OPCNFULL,OPCNSUMM:
                                   OPCNUNIQ,*43,OPCNXCHG:
                                   *44,OPCNXAUT:
                                   *62,OPCNUUU6,OPCNUUM6:
                                   OPCNUUU7,OPCNUUM7,OPCNUUU8:
                                   OPCNUUM8
.
          READ      CONTROLF,SIXTY8;OPCNDPR1,OPCNDPR2,OPCNDPR3,OPCNDPR4:
                                    OPCNDPR5,OPCNDOP1,OPCNDOP2,OPCNDOP3:
                                    OPCNDOP4,OPCNDPS1,OPCNDPS2,OPCNDPS3:
                                    OPCNDPS4
.
          PACK      TODAY,CCC,CYY,CMM,CDD
          REPLACE   " 0",TODAY
          MOVE      ONE,CNEWDS
OPRCON99  RETURN
+
.****************************************************************************
.                                 QURTN000
.    Question mark routine display codes from doctors or clinic depending
.    on sys params, also set the question mark flag SCRNFLAG
.*****************************************************************************
QURTN000
          BRANCH    OPCNCLSU,QURTN555  * test if using surgeons or clinics
          CALL      OPRCLIDS           * codes ? routine
          GOTO      QURTN888
QURTN555
          CALL      PATDOCDS
QURTN888  MOVE      ZERO,SCRNFLAG      * set flag requiring redisplay
QURTN999  RETURN
+
.*****************************************************************************
.                   VDCLN000
.         Validate clinic or surgeon code Passed in KEY6
.         Para's  : KEY6          surgeon/clinic code
.         Returns : EXIT = 0      Code valid
.                   EXIT = 1      code invalid ( Doesn't exist )
.                   OPCLDESC      Clinic or Doctor Description
.                   DESC40        Clinic or Doctor Description
.*****************************************************************************
VDCLN000  MATCH     SP6,KEY6           * test key6 is non blank
          GOTO      VDCLN990 IF EQUAL
.
          BRANCH    OPCNCLSU,VDCLN555  * using doctor code
.
.         read clinic file
.
          CALL      RDOPCLI1           * check for valid clinic
          BRANCH    OVRCD,VDCLN990     * invalid code
.
          PACK      DESC40,OPCLDOCT,SP10    * set up the description
.
          MATCH     SP6,OPCLDOCT 
          GOTO      VDCLN900 IF EQUAL  * not using doctors code
.
          MOVE      OPCLDOCT,KEY6      * set up key
.
.         read doctors file
.
VDCLN555  CALL      RDDOCT1            * read doctors information
          BRANCH    OVRCD,VDCLN990
.
          CALL      DOCNM000           * setup doctors name in DESC40
          MOVE      DESC40,OPCLDESC    * set up doct name
.
VDCLN900  MOVE      ZERO,EXIT          * Yippee the operator entered valid code
          GOTO      VDCLN999
.
VDCLN990  MOVE      SP30,OPCLDESC      * clear the description
          PACK      DESC40,SP20,SP20   * clear the description
          MOVE      ONE,EXIT           * Code Not on File
.
VDCLN999  RETURN
+
.****************************************************************************
.                        AUDIT ROUTINES for Theatre
.                 Created by Bill Dew on the 31/05/1990
.
.    AUDIFLAG : 1   Write a A audit to file
.               2   Write a B audit to file
.               3   Write a C audit to file
.               4   Write a D audit to file
.               5   Delete  B audit to file
.****************************************************************************
.                                 AUDOPSES
.****************************************************************************
AUDOPSES
          BRANCH    OPCNAUDS,AUDSE999  * check audit is turn ON
.
          BRANCH    AUDIFLAG,AUDSE111,AUDSE111,AUDSE222,AUDSE111,AUDSE666
          DISPLAY   *P60:24,*V2LON,*BLINKON,"Audit flag not set"
          GOTO      AUDSE999
.
.    Get the current date and time, Only when writing a Add, Before and 
.    Delete Audits.  Date and time should remain the same when writing
.    a Change to and Deleting a Before audit.
.
AUDSE111
          CALL      IBACLOCK           * get current date
          PACK      OPSEAUDD,CCC,CYY,CMM,CDD
          REP       " 0",OPSEAUDD
          CLOCK     TIME,OPSEAUDT      * clock current time
AUDSE222
          MOVE      ONE,OPSEAUDS       * set print status
          MOVE      PASSCODE,OPSEAUDO  * get user ID
          MOVE      PORT,OPSEAUDP      * get port number
          MOVE      AUDIFLAG,OPSEAUDR
          REPLACE   "1A2B3C4D",OPSEAUDR
          PACK      KEY19,OPSEAUDD,OPSEAUDT,OPSEAUDP,OPSEAUDR  
          CALL      AWOPSES            * write to audit file
          GOTO      AUDSE999
AUDSE666                               * Delete B audit
          PACK      KEY19,OPSEAUDD,OPSEAUDT,OPSEAUDP,ANSB
          CALL      ADOPSES            * write to audit file
AUDSE999  RETURN
.******************************************************************************
.                                 AUDOPDEA
.****************************************************************************
AUDOPDEA
          BRANCH    OPCNAUDB,AUDDA999  * check audit is turn ON
.
          BRANCH    AUDIFLAG,AUDDA111,AUDDA111,AUDDA222,AUDDA111,AUDDA666
          DISPLAY   *P60:24,*V2LON,*BLINKON,"Audit flag not set"
          GOTO      AUDSE999
.
.    Get the current date and time, Only when writing a Add, Before and 
.    Delete Audits.  Date and time should remain the same when writing
.    a Change to and Deleting a Before audit.
.
AUDDA111
          CALL      IBACLOCK           * get current date
          PACK      OPDAAUDD,CCC,CYY,CMM,CDD
          REP       " 0",OPDAAUDD
          CLOCK     TIME,OPDAAUDT      * clock current time
AUDDA222
          MOVE      ONE,OPDAAUDS       * set print status
          MOVE      PASSCODE,OPDAAUDO  * get user ID
          MOVE      PORT,OPDAAUDP      * get port number
          MOVE      AUDIFLAG,OPDAAUDR
          REPLACE   "1A2B3C4D",OPDAAUDR
          PACK      KEY19,OPDAAUDD,OPDAAUDT,OPDAAUDP,OPDAAUDR  
          CALL      AWOPDEA            * write to audit file
          GOTO      AUDDA999
AUDDA666                               * Delete B audit
          PACK      KEY19,OPDAAUDD,OPDAAUDT,OPDAAUDP,ANSB
          CALL      ADOPDEA            * write to audit file
AUDDA999  RETURN
.******************************************************************************
.                                 AUDOPDEB
.****************************************************************************
AUDOPDEB
          BRANCH    OPCNAUDD,AUDDB999  * check audit is turn ON
.
          BRANCH    AUDIFLAG,AUDDB111,AUDDB111,AUDDB222,AUDDB111,AUDDB666
          DISPLAY   *P60:24,*V2LON,*BLINKON,"Audit flag not set"
          GOTO      AUDDB999
.
.    Get the current date and time, Only when writing a Add, Before and 
.    Delete Audits.  Date and time should remain the same when writing
.    a Change to and Deleting a Before audit.
.
AUDDB111
          CALL      IBACLOCK           * get current date
          PACK      OPDBAUDD,CCC,CYY,CMM,CDD
          REP       " 0",OPDBAUDD
          CLOCK     TIME,OPDBAUDT      * clock current time
AUDDB222
          MOVE      ONE,OPDBAUDS       * set print status
          MOVE      PASSCODE,OPDBAUDO  * get user ID
          MOVE      PORT,OPDBAUDP      * get port number
          MOVE      AUDIFLAG,OPDBAUDR
          REPLACE   "1A2B3C4D",OPDBAUDR
          PACK      KEY19,OPDBAUDD,OPDBAUDT,OPDBAUDP,OPDBAUDR  
          CALL      AWOPDEB            * write to audit file
          GOTO      AUDDB999
AUDDB666                               * Delete B audit
          PACK      KEY19,OPDBAUDD,OPDBAUDT,OPDBAUDP,ANSB
          CALL      ADOPDEB            * write to audit file
AUDDB999  RETURN
.******************************************************************************
.                                 AUDOPDEC
.****************************************************************************
AUDOPDEC
          BRANCH    OPCNAUDC,AUDDC999  * check audit is turn ON
.
          BRANCH    AUDIFLAG,AUDDC111,AUDDC111,AUDDC222,AUDDC111,AUDDC666
          DISPLAY   *P60:24,*V2LON,*BLINKON,"Audit flag not set"
          GOTO      AUDDC999
.
.    Get the current date and time, Only when writing a Add, Before and 
.    Delete Audits.  Date and time should remain the same when writing
.    a Change to and Deleting a Before audit.
.
AUDDC111
          CALL      IBACLOCK           * get current date
          PACK      OPDCAUDD,CCC,CYY,CMM,CDD
          REP       " 0",OPDCAUDD
          CLOCK     TIME,OPDCAUDT      * clock current time
AUDDC222
          MOVE      ONE,OPDCAUDS       * set print status
          MOVE      PASSCODE,OPDCAUDO  * get user ID
          MOVE      PORT,OPDCAUDP      * get port number
          MOVE      AUDIFLAG,OPDCAUDR
          REPLACE   "1A2B3C4D",OPDCAUDR
          PACK      KEY19,OPDCAUDD,OPDCAUDT,OPDCAUDP,OPDCAUDR  
          CALL      AWOPDEC            * write to audit file
          GOTO      AUDDC999
AUDDC666                               * Delete B audit
          PACK      KEY19,OPDCAUDD,OPDCAUDT,OPDCAUDP,ANSB
          CALL      ADOPDEC            * write to audit file
AUDDC999  RETURN
.******************************************************************************
