.******************************************************************************
.* Include Name   : KYAAEGDC.PBL                                              *
.*                                                                            *
.* Description    : Keyin of a Diagnosis code and link code                   *
.*                                                                            *
.* Author         : LOFTY                                                     *
.*                                                                            *
.* Parameters     : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF3 - default value                                  *
.*                  CKYIMAND - 1=mandatory                                    *
.*                                                                            *
.* Returns        : AEDCCODE - Keyed in diagnosis code                        *
.*                  AEGLCODE - Keyed in diagnosis link code                   *
.*                  AEDCDESC - Description                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.* On return      : You must display the description yourself.                *
.*                                                                            *
.******************************************************************************
KYAAEGDC  MOVE      ECOL,CCOL
          MOVE      EVERT,CVERT
          MOVE      CKYIDEF3,KEY3
          KEYIN     *PCCOL:CVERT,*V2LON,*RV,KEY3:
                    *PCCOL:CVERT,*DV,KEY3
          PACK      KEY3,KEY3,SP3
          MATCH     SP3,KEY3
          GOTO      KAEDC790 IF EQUAL       * nothing entered
          MATCH     EXITCHAR,KEY3
          GOTO      KAEDC820 IF EQUAL       * exitchar entered
          MATCH     QUEST,KEY3
          GOTO      KAEDC200 IF NOT EQUAL   * ? not entered
.
.         ? entered
.
          MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
          CALL      AAEGDCDS                * display valid entries
          CALL      PUTSCR00                * restore screen
          BRANCH    EXIT,KAEDC790           * (E)xit
.
.         validate what was entered
.
KAEDC200  UNPACK    KEY3,KEY2,KEY1
          CALL      RDAEGDC1
          BRANCH    OVRCD,KAEDC400          * invalid code
.
          MATCH     ANSY,AEDCSUBA           * using sub-analysis
          IF        @EQUAL
            MATCH     SP1,KEY1                * has a code been entered
            IF        !@EQUAL
              PACK      KEY3,AEDCCODE,KEY1
              CALL      RDAEGDL1
              BRANCH    OVRCD,KAEDC450          * invalid link
              MOVE      AEDLDESC,AEDCDESC
            ELSE
              MOVE      SP1,AEDLCODE            * clear link code
            ENDIF
          ELSE
            MOVE      SP1,AEDLCODE            * clear link code
          ENDIF
          MOVE      ZERO,EXIT               * set as valid code
          GOTO      KAEDC800
.
.         invalid code
.
KAEDC400  DISPLAY   *P1:24,*B,"Invalid Diagnosis Code.  ",*EL;
          CALL      HITENTER
          GOTO      KYAAEGDC
.
KAEDC450  DISPLAY   *P1:24,*B,"Invalid Sub-Analysis Code.  ",*EL;
          CALL      HITENTER
          GOTO      KYAAEGDC
.
.         nothing entered
.
KAEDC790  MOVE      ONE,EXIT                * set exit flag
          MOVE      SP2,AEDCCODE
          MOVE      SP1,AEDCSUBA
          MOVE      SP1,AEDLCODE
          COMPARE   ZERO,CKYIMAND
          GOTO      KAEDC800 IF EQUAL       * not mandatory
.
          DISPLAY   *P1:24,*B,"Code must be entered.  ",*EL;
          CALL      HITENTER
          GOTO      KYAAEGDC
.
.         valid entry
.
KAEDC800  DISPLAY   *PECOL:EVERT,*V2LON,AEDCCODE,AEDLCODE
          GOTO      KAEDC999 
.
.         exitchar entered
.
KAEDC820  MOVE      TWO,EXIT                * EXITCHAR entered
.
KAEDC999  RETURN
+
