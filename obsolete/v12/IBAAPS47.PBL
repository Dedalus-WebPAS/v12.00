.-------------------------------------------------------
          INC       STDGENDF     * Required Include
.
VERSION   INIT      "V12.00.00"
.-------------------------------------------------------
. V9.09.01  26.02.2008  Sandra Barcham   162067
.           Print direct debits correctly
.-------------------------------------------------------
. V9.03.01  29.01.2004  Mike Laming  CAR 45907
.           Add switch FMCOCSRL and change CASH Receipt printing at EXTR1150
.-------------------------------------------------------
. V9.02.00  03.05.2002  Glenn Saunders
.           Export to new release i.e. v9.02 from vf.09
.-------------------------------------------------------
. VF.00.03  08.03.2000 Sandra Barcham
.           Fix presented cheques and date update
.           srf 638306
. VF.00.02  08.02.2000 Sandra Barcham
.           Fix default date check
. VF.00.01  05.01.2000 Charles Handaya
.           Recompiled for APSMASIO change
.           22.11.1999 Sandra Barcham
.           Make program case tools and screen painted
.           Allow for cancelling direct debits for a creditor
.           srf 635048
.-------------------------------------------------------
ISBUILD   INIT      "isbuild "
ISERASE   INIT      "iserase "
.
FMSTMP01  IFILE     SQL, FIXED=150, KEY="U58-58,50-57,21-35,90-94,95-129"
FILEDAT1  INIT      " 150 UC58-58,50-57,21-35,90-94,95-129"
NAME1     INIT      "aps471"
FILENAM1  DIM       8
KILLTMP1  DIM       80
MAKETMP1  DIM       80
.
AGSTNAME  DIM       35
AMOUNT    FORM      12.2
ASATBAL   FORM      12.2
BALDATE   DIM       10
BANKNAME  DIM       35
BNKTOT    FORM      14.2
CCTOT     FORM      12.2
CGSTNAME  DIM       35
CLOSBAL   FORM      12.2
CSTOT     FORM      12.2
CREDNAME  DIM       35
DATRANGE  DIM       30
DEBTNAME  DIM       35
DISCNAME  DIM       35
ENDDAT    DIM       8
FILENAME  DIM       8
FMSR      INIT      "fmsr"
FORMAT    INIT      "(999,999,999,999.99 "
FROMDATE  DIM       10
JATOT     FORM      12.2
JCTOT     FORM      12.2
LASTCHEQ  DIM       15
LASTCHST  DIM       1
OUTCHQ    FORM      12.2
OUTREC    FORM      12.2
PAYMNAME  DIM       35
PYTOT     FORM      12.2
RECFLAG   FORM      1
STRDAT    DIM       8
TODATE    DIM       8
TOTAMT    FORM      12.2
TOTAL1    FORM      12.2
YEAREND   DIM       8
.-------------------------------------------------------
.@@@@@ Start of Generated Code @@@@@
. NB - Code & Comments Included Before the above line 
.      will not be overwritten by the generation of a 
.      program. The STDGENDF file and VERSION must appear
.      before the line. 
.-----------------------------------------
.   Program       :   IBAAPS47
.   Function      :   Bank Reconciliation Report              
.   Generated  On :   26/02/2008
.-----------------------------------------
.=========================
.File Description Includes
.-------------------------
          INC       APSMASFD/INC
          INC       FMSAMAFD/INC
          INC       FMSBCFFD/INC
          INC       FMSBNKFD/INC
          INC       FMSCAFFD/INC
          INC       FMSCHQFD/INC
          INC       FMSCONFD/INC
          INC       FMSCSAFD/INC
          INC       FMSCTRFD/INC
          INC       FMSGENDF    
          INC       FMSLMAFD/INC
          INC       FMSLMCFD/INC
.=========================
.Local Variable Definition
.-------------------------
TOBDATE   DIM      10 * Scr 00 Fld     8
.
ACCEPT    FORM      1
SACCEPT   FORM      1
.
PRGID     INIT      "IBAAPS47"
PRGNAM    INIT      "Bank Reconciliation Report              "
.
.--------------------------------
. MAINLINE - Controlling Logic
.--------------------------------
          TOPIC     PRGID
          MOVE      PORT,PARENTID
          CLOCK     PORT,DIM2
          MOVE      DIM2,PORT
ML0000
          CALL      INIT0000
          CALL      REDPAR00
          CALL      SC00L000
          CHAIN     PGM
.---------------------------------------
. INIT - Open Files & Display Heading
.---------------------------------------
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CCENTRY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          DISPLAY   *P56:24,"Opening scrsbga1";
          OPEN      SCRSBGA1,"scrsbgaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA1,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA2,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrslta1";
          OPEN      SCRSLTA1,"scrsltaf"
          DISPLAY   *P56:24,"Opening apsmasaf";
          OPEN      APSMASA1,"apsmasaf"
          DISPLAY   *P56:24,"Opening apsmasaf";
          OPEN      APSMASA2,"apsmasaf"
          DISPLAY   *P56:24,"Opening apsmasaf";
          OPEN      APSMASA3,"apsmasaf"
          DISPLAY   *P56:24,"Opening fmsamaaf";
          OPEN      FMSAMAA1,"fmsamaaf"
          DISPLAY   *P56:24,"Opening fmsamaaf";
          OPEN      FMSAMAA2,"fmsamaaf"
          DISPLAY   *P56:24,"Opening fmsamaaf";
          OPEN      FMSAMAA3,"fmsamaaf"
          DISPLAY   *P56:24,"Opening fmsamaaf";
          OPEN      FMSAMAA4,"fmsamaaf"
          MOVE      "   51",AUDTSECT
          MOVE      " 46",AUDTPOST
          READ      CONTROLF,AUDTSECT;*AUDTPOST,AUDTFLAG
          IF        AUDTFLAG=1
            DISPLAY   *P56:24,"Opening fmsaudla";
            OPEN      FMSAUDLA,"fmsaudla"
          ENDIF
          DISPLAY   *P56:24,"Opening fmsbcfaf";
          OPEN      FMSBCFA1,"fmsbcfaf"
          DISPLAY   *P56:24,"Opening fmsbcfaf";
          OPEN      FMSBCFA2,"fmsbcfaf"
          DISPLAY   *P56:24,"Opening fmsbnkaf";
          OPEN      FMSBNKA1,"fmsbnkaf"
          DISPLAY   *P56:24,"Opening fmsbnkaf";
          OPEN      FMSBNKA2,"fmsbnkaf"
          DISPLAY   *P56:24,"Opening fmsbnkaf";
          OPEN      FMSBNKA3,"fmsbnkaf"
          DISPLAY   *P56:24,"Opening fmsbnkaf";
          OPEN      FMSBNKA4,"fmsbnkaf"
          DISPLAY   *P56:24,"Opening fmsbnkaf";
          OPEN      FMSBNKA5,"fmsbnkaf"
          DISPLAY   *P56:24,"Opening fmsbnkaf";
          OPEN      FMSBNKA6,"fmsbnkaf"
          DISPLAY   *P56:24,"Opening fmscafaf";
          OPEN      FMSCAFA1,"fmscafaf"
          DISPLAY   *P56:24,"Opening fmscafaf";
          OPEN      FMSCAFA2,"fmscafaf"
          DISPLAY   *P56:24,"Opening fmschqaf";
          OPEN      FMSCHQA1,"fmschqaf"
          DISPLAY   *P56:24,"Opening fmscsaaf";
          OPEN      FMSCSAA1,"fmscsaaf"
          DISPLAY   *P56:24,"Opening fmslmaaf";
          OPEN      FMSLMAA1,"fmslmaaf"
          DISPLAY   *P56:24,"Opening fmslmcaf";
          OPEN      FMSLMCA1,"fmslmcaf"
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.
.------------------------------------
. Called for Print Screen Routine 
.------------------------------------
PRTSVARS
          MATCH     "00",SCRID
          IF         @EQUAL
            CALL       PS00L000   * Menu Options                       
          ENDIF
          RETURN
.--------------------------------
. Screen - Menu Options                       
.--------------------------------
SC00L000
SC00L100
          MOVE      "00",SCRID
          MOVE      ZERO,PRCOL
          MOVE      ZERO,PRVERT
          MOVE      "    1",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SCPSCOL,PRCOL
            MOVE     SCPSROW,PRVERT
          ENDIF
          CALL      SBACK000 * Display Background
.
SC00L200
          CALL      CF00L000 * Clear Fields
          CALL      DEFPAR00
SC00L300
          CALL      KY00L000 * Enter Key Fields
          BRANCH    EXIT,SC00L999,SC00L999
SC00L400
          CALL      WP00L000 * Load any WP Details
SC00L500
          CALL      LM00L000 * Load any Batch Details
SC00L600
          CALL      DF00L000 * Display Fields
SC00L700
          CALL      SF00L000 * Select Fields
          BRANCH    EXIT,SC00L100,SC00L100,SC00L200
          IF        EXIT=4
            MOVE      ONE,EXIT
          ENDIF
SC00L800
SC00L900
SC00L999  RETURN
.--------------------------------------------------------
. Clear Fields for Screen : Menu Options                       
.-------------------------------------------------------
CF00L000
          MOVE      SP70,FMCHBDT 
          MOVE      ZERO,FMCHBAL 
          MOVE      SP70,FMCHCHQ 
          MOVE      SP70,FMCHDES 
          MOVE      SP70,TOBDATE 
CF00L999  RETURN
.--------------------------------------------------------
. Enter Key Fields for Menu Options                       
.--------------------------------------------------------
KY00L000
          MOVE      ZERO,EXIT
          GOTO      KY00L999
.
KY00L900  MOVE      ONE,EXIT
KY00L999  RETURN
.---------------------------------------------------------
. Load WP files for : Menu Options                       
.---------------------------------------------------------
WP00L000
          RETURN
.---------------------------------------------------------
. Load Batch Screen for : Menu Options                       
.---------------------------------------------------------
LM00L000
          RETURN
.---------------------------------------------------------
. Display Fields for : Menu Options                       
.---------------------------------------------------------
DF00L000
          BRANCH   ACCEPT,DF00L999
          PACK     KEY15,PRGID,SCRID,SP70
          CALL     RSSCPS1
DF00L010
          CALL     RKSCPS1
          BRANCH   OVRCD,DF00L900
          MATCH    PRGID,SCPSPID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SCRID,SCPSSID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SP70,SCPSMTY
          GOTO     DF00L010 IF NOT EQUAL
.
          CALL     PS00L000 * Display Field
          GOTO     DF00L010
DF00L900
DF00L999
          RETURN
.---------------------------------------------------------
. Display/Print Fields for : Menu Options                       
.---------------------------------------------------------
PS00L000   PACK      VAR,SP70,SP70
          PACK     PRINTVAR,SP70,SP70
          MOVE     SCPSITM,FIELDNO
          BRANCH    FIELDNO,PS00L999,PS00L999,PS00L999,PS00L101:
                            PS00L102,PS00L103,PS00L104,PS00L105
          GOTO     PS00L999
.
PS00L101
          UNPACK    FMCHBDT,CCENT,CYEAR,CMON,CDAY           
          CALL      PACDATE
          MOVE      CPCDATE,VAR
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L102
          MOVE      FMCHBAL,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L103
          MOVE      FMCHCHQ,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L104
          MOVE      FMCHDES,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L105
          MOVE      TOBDATE,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
PS00L999
          RETURN
.---------------------------------------------------------
. Selections for : Menu Options                       
.---------------------------------------------------------
SF00L000  PACK     KEY13,PRGID,SCRID,SP70
          CALL     RSSCSL1
SF00L001  BRANCH   ACCEPT,SF00L020
.
SF00L005  CALL     MTSTQ000
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L006,SF00L101,SF00L102,SF00L006:
                            SF00L006,SF00L103,SF00L006,SF00L104
SF00L006  BEEP
          GOTO     SF00L005
SF00L020  CALL     RKSCSL1
          BRANCH   OVRCD,SF00L090
          MATCH    PRGID,SCSLPID
          GOTO     SF00L090 IF NOT EQUAL
          MATCH    SCRID,SCSLSID
          GOTO     SF00L090 IF NOT EQUAL
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L020,SF00L020,SF00L020,SF00L020:
                            SF00L020,SF00L103,SF00L020,SF00L104
          GOTO     SF00L020
SF00L090  MOVE     ZERO,ACCEPT
          GOTO     SF00L001
.
SF00L101
          MOVE      ZERO,EXIT
          GOTO      SF00L999
.
SF00L102
          CALL      REPT0000
          MOVE      ZERO,EXIT
          GOTO      SF00L999
.
SF00L103
.
.
          MOVE      "    4",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,FMCHBDT 
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
.
.
          MOVE      "    5",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,FMCHBAL 
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
.
.
          MOVE      "    7",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,FMCHDES 
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      KCHQ0000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L103
            ENDIF
            BRANCH    EXIT,SF00L103,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      FMCHCHQ,VAR      
          CALL      DISPITEM
.
.
          MOVE      "    4",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            UNPACK    FMCHBDT,CCENT,CYEAR,CMON,CDAY           
            CALL      PACDATE
            MOVE      CPCDATE,VAR
            CALL      DISPITEM
          ENDIF
.
.
          MOVE      "    5",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      FMCHBAL,VAR      
            CALL      DISPITEM
          ENDIF
.
.
          MOVE      "    7",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      FMCHDES,VAR      
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L104
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      KDAT0000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L104
            ENDIF
            BRANCH    EXIT,SF00L104,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      TOBDATE,VAR      
          CALL      DISPITEM
          GOTO     SF00L001
SF00L990  MOVE      FOUR,EXIT * Exit Char Exit
SF00L999  RETURN
.@@@@@ Function Restart Point @@@@@
.---------------------------------------
. Read Parameters in controlf
.---------------------------------------
REDPAR00  OPEN      CONTROLF,"controlf"
          CALL      RDFMCO50
          CALL      RDFMCO51
          CALL      RDFMCO52
          CALL      RDFMCO66                                           *I-f1101
          RETURN
.
.---------------------------------------
. Setup Defaults for Bank Reconciliation
.---------------------------------------
DEFPAR00  MOVE      ONE,ACCEPT
          UNPACK    FMCHBDT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,FROMDATE
          PACK      TODATE,CCC,CYY,CMM,CDD
          REP       " 0",TODATE
          UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,TOBDATE
          MOVE      ONE,CDEFDTE
          RETURN
.
.---------------------------------------
. Print Report
.---------------------------------------
REPT0000  STRIP     FROMDATE
          STRIP     TOBDATE
          MOVE      " to ",KEY4
          PACK      DATRANGE,FROMDATE,KEY4,TOBDATE
.
          CALL      EXTR0000
          CALL      PRNT0000
.
REPT9000  CLOSE     FMSTMP01
          EXECUTE   KILLTMP1,TASKID         * create file
.
REPT9999  RETURN
.---------------------------------------
.EXTR0000 - Extract Report
.---------------------------------------
EXTR0000  DISPLAY   *P1:24,"Clearing Temp File : ",*EL;
          PACK      FILENAM1,NAME1,PORT
          REP       " 0",FILENAM1
          PACK      KILLTMP1,ISERASE,FILENAM1
          PACK      MAKETMP1,ISBUILD,FILENAM1,FILEDAT1
          CLOSE     FMSTMP01
          EXECUTE   KILLTMP1,TASKID         * create file
          EXECUTE   MAKETMP1,TASKID
          OPEN      FMSTMP01,FILENAM1
.
          MOVE      ZERO,CSTOT
          MOVE      ZERO,JATOT
          MOVE      ZERO,CCTOT
          MOVE      ZERO,PYTOT
          MOVE      ZERO,JCTOT
          MOVE      ZERO,CLOSBAL
          MOVE      ZERO,OUTCHQ
          MOVE      ZERO,OUTREC
          MOVE      ZERO,ASATBAL
.
EXTR1000  DISPLAY   *P1:24,"Extracting Data    :",*EL;
          PACK      KEY24,FMCHCHQ,ZERO,SP70
          CALL      RDFMBN6            * get desired records
          BRANCH    OVRCD,EXTR1100
          GOTO      EXTR1110
.
EXTR1100  CALL      RKFMBN6            * get next record
          BRANCH    OVRCD,EXTR2000     * no more files ?
.
EXTR1110  MATCH     FMCHCHQ,FMBNBNK   * at end of account
          GOTO      EXTR2000 IF NOT EQUAL
          MATCH     "0",FMBNPRE
          GOTO      EXTR2000 IF NOT EQUAL
          MATCH     "2",FMBNTYP
          GOTO      EXTR1100 IF EQUAL
          MATCH     FMBNDAT,TODATE   * at end of date range
          GOTO      EXTR1100 IF LESS
.
          DISPLAY   *P22:24,*V2LON,FMBNREF,*EL;
.
          COMPARE   ZERO,FMBNAMT
          GOTO      EXTR1100 IF EQUAL
.
.* ****************************************** Start of Addition        *I-f1101
EXTR1150  MOVE      ZERO,OVRCD
          MATCH     "1",FMBNTYP
          IF        @EQUAL & FMCOCSRL = 1
            MOVE      FMBNUNQ,FMBNCRE       * Change key
          ENDIF
.* ******************************************   End of Addition        *I-f1101
.
          PACK      KEY64,FMBNTYP,FMBNDAT,FMBNREF,FMBNCRE,FMBNNAM,SP70
          CALL      RDTEMP1
          BRANCH    OVRCD,EXTR1200
          ADD       AMOUNT,FMBNAMT
          CALL      UPTEMP1
          SUB       AMOUNT,FMBNAMT
          GOTO      EXTR1210
.
EXTR1200  CALL      WRTEMP1            * delete record
.
EXTR1210  MATCH     "0",FMBNTYP
          IF        @EQUAL
            ADD       FMBNAMT,OUTCHQ
          ELSE
            ADD       FMBNAMT,OUTREC
          ENDIF
          GOTO      EXTR1100
.
EXTR2000  PACK      KEY44,FMCHCHQ,ZERO,FMCHBDT,SP70
          CALL      RSFMBN3            * get desired records
.
EXTR2100  CALL      RKFMBN3            * get next record
          BRANCH    OVRCD,EXTR3000     * no more files ?
          MATCH     FMCHCHQ,FMBNBNK   * at end of account
          GOTO      EXTR3000 IF NOT EQUAL
          MATCH     "0",FMBNTYP
          GOTO      EXTR3000 IF NOT EQUAL
          MATCH     FMBNDAT,TODATE   * at end of date range
          GOTO      EXTR3000 IF LESS
.
          DISPLAY   *P22:24,*V2LON,FMBNREF,*EL;
.
          COMPARE   ZERO,FMBNAMT
          GOTO      EXTR2100 IF EQUAL
.
          ADD       FMBNAMT,PYTOT
          GOTO      EXTR2100
.
EXTR3000  PACK      KEY44,FMCHCHQ,ONE,FMCHBDT,SP70
          CALL      RSFMBN3            * get desired records
.
EXTR3100  CALL      RKFMBN3            * get next record
          BRANCH    OVRCD,EXTR4000     * no more files ?
          MATCH     FMCHCHQ,FMBNBNK   * at end of account
          GOTO      EXTR4000 IF NOT EQUAL
          MATCH     "1",FMBNTYP
          GOTO      EXTR4000 IF NOT EQUAL
          MATCH     FMBNDAT,TODATE   * at end of date range
          GOTO      EXTR4000 IF LESS
.
          DISPLAY   *P22:24,*V2LON,FMBNREF,*EL;
.
          COMPARE   ZERO,FMBNAMT
          GOTO      EXTR3100 IF EQUAL
.
          ADD       FMBNAMT,CSTOT
          GOTO      EXTR3100
.
EXTR4000  PACK      KEY44,FMCHCHQ,TWO,FMCHBDT,SP70
          CALL      RSFMBN3            * get desired records
.
EXTR4100  CALL      RKFMBN3            * get next record
          BRANCH    OVRCD,EXTR5000     * no more files ?
          MATCH     FMCHCHQ,FMBNBNK   * at end of account
          GOTO      EXTR5000 IF NOT EQUAL
          MATCH     "2",FMBNTYP        * at end adj records
          GOTO      EXTR5000 IF NOT EQUAL
          MATCH     FMBNDAT,TODATE   * at end of date range
          GOTO      EXTR5000 IF LESS
.
          DISPLAY   *P22:24,*V2LON,FMBNREF,*EL;
.
EXTR4300  COMPARE   ZERO,FMBNAMT
          GOTO      EXTR4400 IF LESS
.
          ADD       FMBNAMT,JATOT
          GOTO      EXTR4100
.
EXTR4400  MULT      SEQ,FMBNAMT
          ADD       FMBNAMT,JCTOT
          GOTO      EXTR4100
.
EXTR5000  PACK      KEY24,FMCHCHQ,TWO,FMCHBDT,SP70
          CALL      RDFMBN6
          COMPARE   ZERO,OVRCD
          GOTO      EXTR5020 IF EQUAL
.
EXTR5010  CALL      RKFMBN6
          BRANCH    OVRCD,EXTR6000
.
EXTR5020  MATCH     FMBNBNK,FMCHCHQ
          GOTO      EXTR6000 IF NOT EQUAL
          MATCH     "2",FMBNPRE
          GOTO      EXTR6000 IF NOT EQUAL
.
          MATCH     FMBNPDT,TODATE
          GOTO      EXTR5030 IF LESS
.
          ADD       FMBNAMT,CCTOT
          GOTO      EXTR5010
.
EXTR5030  MATCH     FMBNDAT,TODATE
          GOTO      EXTR5010 IF LESS
.
          MOVE      ZERO,FMBNPRE
          MOVE      SP8,FMBNPDT
          PACK      KEY64,FMBNTYP,FMBNDAT,FMBNREF,FMBNCRE,FMBNNAM,SP70
          CALL      RDTEMP1
          BRANCH    OVRCD,EXTR5200
          ADD       AMOUNT,FMBNAMT
          CALL      UPTEMP1
          SUB       AMOUNT,FMBNAMT
          GOTO      EXTR5210
.
EXTR5200  CALL      WRTEMP1            * delete record
.
EXTR5210  MATCH     "0",FMBNTYP
          IF        @EQUAL
            ADD       FMBNAMT,OUTCHQ
          ELSE
            ADD       FMBNAMT,OUTREC
          ENDIF
          GOTO      EXTR5010
.
EXTR6000  PACK      KEY24,FMCHCHQ,ONE,TODATE,SP70
          CALL      RDFMBN6
          COMPARE   ZERO,OVRCD
          GOTO      EXTR6020 IF EQUAL
.
EXTR6010  CALL      RKFMBN6
          BRANCH    OVRCD,EXTR9999
.
EXTR6020  MATCH     FMBNBNK,FMCHCHQ
          GOTO      EXTR9999 IF NOT EQUAL
          MATCH     "1",FMBNPRE
          GOTO      EXTR9999 IF NOT EQUAL
.
          MATCH     FMBNPDT,TODATE
          GOTO      EXTR6010 IF EQUAL
.
          MATCH     TODATE,FMBNDAT
          GOTO      EXTR6030 IF EQUAL
          GOTO      EXTR6010 IF NOT LESS
.
EXTR6030  MOVE      ZERO,FMBNPRE
          MOVE      SP8,FMBNPDT
          PACK      KEY64,FMBNTYP,FMBNDAT,FMBNREF,FMBNCRE,FMBNNAM,SP70
          CALL      RDTEMP1
          BRANCH    OVRCD,EXTR6200
          ADD       AMOUNT,FMBNAMT
          CALL      UPTEMP1
          SUB       AMOUNT,FMBNAMT
          GOTO      EXTR6210
.
EXTR6200  CALL      WRTEMP1            * delete record
.
EXTR6210  MATCH     "0",FMBNTYP
          IF        @EQUAL
            ADD       FMBNAMT,OUTCHQ
          ELSE
            ADD       FMBNAMT,OUTREC
          ENDIF
          GOTO      EXTR6010
.
EXTR9999  RETURN
.---------------------------------------
. PRNT0000 - Print Report
.---------------------------------------
PRNT0000  DISPLAY   *P1:24,"Printing Report    : ",*EL;
          MOVE      ZERO,CPAGENO                 * reset page no.
          MOVE      ONE,CNOUNDLN
          MOVE      SP70,LASTCHEQ                * reset cheque no.
          MOVE      SP70,LASTCHST                * reset cheque no. stat
          CLOCK     TIME,CTIMEIS                 * get current time
.
          MOVE      "60",CLNO
          MOVE      ONE,RECFLAG
          MOVE      ZERO,TOTAL1                  * reset total
          PACK      KEY64,ZERO,SP70
          CALL      RSTEMP1
.
PRNT1000  CALL      RKTEMP1                      * get next code record
          BRANCH    OVRCD,PRNT2000               * end of file ?
.
          MATCH     "0",FMBNTYP
          GOTO      PRNT2000 IF NOT EQUAL
.
          COMPARE   "56",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      SP5,APMACRD
          MOVE      FMBNNAM,APMACN1
          MOVE      FMBNCRE,KEY5
          CALL      RDAPMA1
.
          UNPACK    FMBNDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MATCH     "DD-",FMBNREF
          IF        @EQUAL
            UNPACK    FMBNREF,KEY8,KEY7
          ELSE
            UNPACK    FMBNREF,KEY7,KEY8
          ENDIF
.
          MOVE      FORMAT,KEY20
          FEDIT     FMBNAMT,KEY20
          MOVE      APMACN1,KEY26
          PRINT     "| ",CPCDATE," |",KEY8," | ",APMACRD," ",KEY26:
                    " |",KEY20,"|"
          ADD       ONE TO CLNO
          ADD       FMBNAMT,TOTAL1
          GOTO      PRNT1000
.
PRNT2000  MOVE      FORMAT,KEY20
          FEDIT     TOTAL1,KEY20
          PRINT     "*------------------------------------------":
                       "------------------------------------*":
                    *N,"|                                          ":
                       "      Total     ",KEY20,"|":
                    *N,"*------------------------------------------":
                       "------------------------------------*"
.
          MOVE      "60",CLNO
          MOVE      TWO,RECFLAG
          MOVE      ZERO,TOTAL1                  * reset total
          PACK      KEY64,ONE,SP70
          CALL      RSTEMP1
.
PRNT2100  CALL      RKTEMP1                      * get next code record
          BRANCH    OVRCD,PRNT3000               * end of file ?
.
          MATCH     "1",FMBNTYP
          GOTO      PRNT3000 IF NOT EQUAL
.
          COMPARE   "56",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          UNPACK    FMBNDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      FORMAT,KEY20
          FEDIT     FMBNAMT,KEY20
          PRINT     *10,"| ",CPCDATE," | ",FMBNREF," |  ",KEY20,"|"
          ADD       ONE TO CLNO
          ADD       FMBNAMT,TOTAL1
          GOTO      PRNT2100
.
PRNT3000  MOVE      FORMAT,KEY20
          FEDIT     TOTAL1,KEY20
          PRINT     *10,"*----------------------------------------------":
                         "-------*":
                    *N,*10,"|                     TOTAL    |  ",KEY20,"|":
                    *N,*10,"*-------------------------------------------":
                           "----------*"
.
          MOVE      "60",CLNO
          MOVE      THREE,RECFLAG
          MOVE      ZERO,TOTAL1                  * reset total
.
          CALL      HEAD0000
.
          MOVE      FORMAT,KEY20
          FEDIT     FMCHBAL,KEY20
          DISPLAY   *P1:6,*EF,"Bank Reconciliation as at ",*V2LON,TOBDATE,*HOFF:
                    *P1:8,"Opening Balance Per Cash Book as at ":
                    *V2LON,FROMDATE,*P58:8,KEY20
.
          MOVE      FORMAT,KEY20
          FEDIT     CSTOT,KEY20
          DISPLAY   *P10:9,"Plus Receipts",*P58:9,*V2LON,KEY20
.
          MOVE      FORMAT,KEY20
          FEDIT     JATOT,KEY20
          DISPLAY   *P10:10,"     Debit Journals",*P58:10,*V2LON,KEY20
.
          MOVE      FORMAT,KEY20
          FEDIT     CCTOT,KEY20
          DISPLAY   *P10:11,"     Cancelled Payments",*P58:11,*V2LON,KEY20
.
          MOVE      FORMAT,KEY20
          FEDIT     PYTOT,KEY20
          DISPLAY   *P10:12,"Less Payments",*P58:12,*V2LON,KEY20
.
          MOVE      FORMAT,KEY20
          FEDIT     JCTOT,KEY20
          DISPLAY   *P10:13,"     Credit Journals",*P58:13,*V2LON,KEY20
.
          MOVE      FMCHBAL,CLOSBAL
          ADD       CSTOT,CLOSBAL
          ADD       JATOT,CLOSBAL
          ADD       CCTOT,CLOSBAL
          SUB       PYTOT,CLOSBAL
          SUB       JCTOT,CLOSBAL
.
          MOVE      FORMAT,KEY20
          FEDIT     CLOSBAL,KEY20
          DISPLAY   *P1:15,"Closing Balance Per Cash Book as at ":
                    *V2LON,TOBDATE,*P58:15,KEY20
.
          MOVE      FORMAT,KEY20
          FEDIT     OUTCHQ,KEY20
          DISPLAY   *P10:17,"Plus Unpresented Cheques",*P58:17,*V2LON,KEY20
.
          MOVE      FORMAT,KEY20
          FEDIT     OUTREC,KEY20
          DISPLAY   *P10:18,"Less Outstanding Deposits",*P58:18,*V2LON,KEY20
.
          MOVE      CLOSBAL,ASATBAL
          ADD       OUTCHQ,ASATBAL
          SUB       OUTREC,ASATBAL
.
          MOVE      FORMAT,KEY20
          FEDIT     ASATBAL,KEY20
          DISPLAY   *P1:20,"Balance as Per Bank Statement as at ":
                    *V2LON,TOBDATE,*P58:20,KEY20
.
          KEYIN     *P1:24,*EL,"Is this the Last Report for the Period (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*V2LON,ANS
          REP       UPPLOW,ANS
          RESET     ANS
          CMATCH    "Y" TO ANS
          GOTO      PRNT9999 IF NOT EQUAL
.
          MOVE      FMCHCHQ,KEY15
          CALL      RDFMCH1
          BRANCH    OVRCD,PRNT9999
.
          MOVE      CLOSBAL,FMCHBAL
          UNPACK    TODATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      FMCHBDT,CC,YY,MM,DD
          REP       " 0",FMCHBDT
          CALL      UPFMCH1
.
PRNT9999  RETURN
.---------------------------------------
. HEAD0000 - Print page heading                           Called By PRNT
.---------------------------------------
HEAD0000  CALL      HEAD80            * print header
          PRINT     "Cheque Account : ",FMCHCHQ,SP2,FMCHDES,*N:
                    "Period         : ",DATRANGE
.
          BRANCH    RECFLAG,HEAD1000,HEAD3000,HEAD5000
.
HEAD1000  PRINT     *N,"*------------------------------------------":
                       "------------------------------------*":
                    *N,"|                      Unpresented Cheques ":
                       "                                    |":
                    *N,"|   Date     | Cheque  | Creditor          ":
                       "               |             Amount |":
                    *N,"*------------------------------------------":
                       "------------------------------------*":
                    *N,"|            |         |                   ":
                       "               |                    |"
          ADD       FIVE TO CLNO
          GOTO      HEAD9999
.
HEAD3000  PRINT     *N,*10,"*-------------------------------------------":
                           "----------*":
                    *N,*10,"|           Unpresented Receipts            ":
                           "          |":
                    *N,*10,"|    Date    | Reference       |            ":
                           "   Amount |":
                    *N,*10,"*-------------------------------------------":
                           "----------*":
                    *N,*10,"|            |                 |            ":
                           "          |"
          ADD       FIVE TO CLNO
          GOTO      HEAD9999
.
HEAD5000  MOVE      FORMAT,KEY20
          FEDIT     FMCHBAL,KEY20
          PRINT     *N,*1,"Bank Reconciliation as at ",TOBDATE:
                    *N,*N,*N:
                    *1,"Opening Balance Per Cash Book as at ",FROMDATE:
                    *58,KEY20
.
          MOVE      FORMAT,KEY20
          FEDIT     CSTOT,KEY20
          PRINT     *N,*10,"Plus Receipts",*58,KEY20
.
          MOVE      FORMAT,KEY20
          FEDIT     JATOT,KEY20
          PRINT     *10,"     Debit Journals",*58,KEY20
.
          MOVE      FORMAT,KEY20
          FEDIT     CCTOT,KEY20
          PRINT     *10,"     Cancelled Payments",*58,KEY20
.
          MOVE      FORMAT,KEY20
          FEDIT     PYTOT,KEY20
          PRINT     *10,"Less Payments",*58,KEY20
.
          MOVE      FORMAT,KEY20
          FEDIT     JCTOT,KEY20
          PRINT     *10,"     Credit Journals",*58,KEY20:
                    *N,*59,"------------------"
.
          MOVE      FMCHBAL,CLOSBAL
          ADD       CSTOT,CLOSBAL
          ADD       JATOT,CLOSBAL
          ADD       CCTOT,CLOSBAL
          SUB       PYTOT,CLOSBAL
          SUB       JCTOT,CLOSBAL
.
          MOVE      FORMAT,KEY20
          FEDIT     CLOSBAL,KEY20
          PRINT     *1,"Closing Balance Per Cash Book as at ",TOBDATE:
                    *58,KEY20:
                    *N,*59,"------------------"
.
          MOVE      FORMAT,KEY20
          FEDIT     OUTCHQ,KEY20
          PRINT     *10,"Plus Unpresented Cheques",*58,KEY20
.
          MOVE      FORMAT,KEY20
          FEDIT     OUTREC,KEY20
          PRINT     *10,"Less Outstanding Deposits",*58,KEY20:
                    *N,*59,"------------------"
.
          MOVE      CLOSBAL,ASATBAL
          ADD       OUTCHQ,ASATBAL
          SUB       OUTREC,ASATBAL
.
          MOVE      FORMAT,KEY20
          FEDIT     ASATBAL,KEY20
          PRINT     *1,"Balance as Per Bank Statement as at ",TOBDATE:
                    *58,KEY20:
                    *N,*59,"------------------"
          GOTO      HEAD9999
.
HEAD9999  RETURN
.---------------------------------------
. Routines for temp file
.---------------------------------------
RSTEMP1   RESET     KEY64
          READ      FMSTMP01,KEY64;;
          RETURN
.
RDTEMP1   RESET     KEY64
          MOVE      ZERO,OVRCD
          READ      FMSTMP01,KEY64;FMBNBNK,FMBNUNQ,FMBNREF,FMBNLED:
                                   FMBNACC,FMBNDAT,FMBNTYP,FMBNPRE,FMBNPDT:
                                   AMOUNT,FMBNFYR,FMBNBCH,FMBNLNO,FMBNCRE:
                                   FMBNNAM,FMBNSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    FMSTMP01;FMBNBNK,FMBNUNQ,FMBNREF,FMBNLED:
                             FMBNACC,FMBNDAT,FMBNTYP,FMBNPRE,FMBNPDT:
                             FMBNAMT,FMBNFYR,FMBNBCH,FMBNLNO,FMBNCRE:
                             FMBNNAM,FMBNSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   UPDATE    FMSTMP01;FMBNBNK,FMBNUNQ,FMBNREF,FMBNLED:
                             FMBNACC,FMBNDAT,FMBNTYP,FMBNPRE,FMBNPDT:
                             FMBNAMT,FMBNFYR,FMBNBCH,FMBNLNO,FMBNCRE:
                             FMBNNAM,FMBNSPAR
          RETURN
.
RPTEMP1   MOVE      ZERO,OVRCD
          READKP    FMSTMP01;FMBNBNK,FMBNUNQ,FMBNREF,FMBNLED:
                             FMBNACC,FMBNDAT,FMBNTYP,FMBNPRE,FMBNPDT:
                             FMBNAMT,FMBNFYR,FMBNBCH,FMBNLNO,FMBNCRE:
                             FMBNNAM,FMBNSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY64
          WRITE     FMSTMP01,KEY64;FMBNBNK,FMBNUNQ,FMBNREF,FMBNLED:
                                   FMBNACC,FMBNDAT,FMBNTYP,FMBNPRE,FMBNPDT:
                                   FMBNAMT,FMBNFYR,FMBNBCH,FMBNLNO,FMBNCRE:
                                   FMBNNAM,FMBNSPAR
          RETURN
.
DETEMP1   RESET     KEY64
          DELETE    FMSTMP01,KEY64
          RETURN
.
.----------------------------------------------------------------------
. Enter Cheque Account
.----------------------------------------------------------------------
KCHQ0000  MOVE      ZERO,CKEYTYP
          CALL      KFMCHA00
          BRANCH    EXIT,KCHQ9500,KCHQ9500
.
          PACK      KEY29,FMCHCHQ,SP70
          CALL      RSFMCA2
          CALL      RKFMCA2
          BRANCH    OVRCD,KCHQ8000
.
          MATCH     FMCHCHQ,FMCACHQ
          GOTO      KCHQ8000 IF NOT EQUAL
.
          PACK      KEY2,FMCALED,SP70
          CALL      RDFMLA1
          BRANCH    OVRCD,KCHQ8000
.
          UNPACK    FMCHBDT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,FROMDATE
          MOVE      FMCHBDT,WORKDATE
          CALL      CFYR0000
          BRANCH    EXIT,KCHQ8100
          PACK      FILENAME,FMSR,CURYEAR
          BRANCH    FMLAPERS,KCHQ0500
.
          MOVE      FMLC12TO,YEAREND
          GOTO      KCHQ1000
.
KCHQ0500  MOVE      FMLC13TO,YEAREND
.
KCHQ1000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FMSCTRA1,FILENAME
          TRAPCLR   IO
          IF        OVRCD=1
            BEEP
            MOVE      "Transaction File not Available - ",DISPMSG
            CALL      MESSAGE1
            GOTO      KCHQ0000
          ENDIF
          GOTO      KCHQ9999
.
KCHQ8000  BEEP
          MOVE      "Cheque Account Not Attached to Ledger - ",DISPMSG
          CALL      MESSAGE1
          GOTO      KCHQ0000
.
KCHQ8100  BEEP
          MOVE      "Invalid Financial Year for Ledger - ",DISPMSG
          CALL      MESSAGE1
          GOTO      KCHQ0000
.
KCHQ9000  MOVE      ZERO,EXIT
          GOTO      KCHQ9999
.
KCHQ9500  MOVE      TWO,EXIT
          GOTO      KCHQ9999
.
KCHQ9999  RETURN
.
.------------------------------------------------
. Enter As At Date
.------------------------------------------------
KDAT0000  MOVE      CDD,CDAY
          MOVE      CMM,CMON
          MOVE      CYY,CYEAR
          MOVE      CCC,CCENT
          MOVE      ONE,CDEFDTE
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT9999
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODATE
          MATCH     TODATE,FMCHBDT
          GOTO      KDAT1000 IF EQUAL
          GOTO      KDAT1000 IF LESS
.
          BEEP
          MOVE      "Opening Date Must be < As At Date - ",DISPMSG
          CALL      MESSAGE1
          GOTO      KDAT0000
.
KDAT1000  MATCH     TODATE,YEAREND
          GOTO      KDAT9999 IF NOT LESS
.
          BEEP
          MOVE      "Closing Date Must be in the Same Financial Year - ",DISPMSG
          CALL      MESSAGE1
          GOTO      KDAT0000
.
KDAT9999  CALL      PACDATE
          MOVE      CPCDATE,TOBDATE
          RETURN
.-------------------------------
. Includes for I/O and Keyins
.-------------------------------
          INC       APSMASIO/INC
          INC       FMSAMAIO/INC
          INC       FMSBCFIO/INC
          INC       FMSBNKIO/INC
          INC       FMSCAFIO/INC
          INC       FMSCHQIO/INC
          INC       FMSCHQKY    
          INC       FMSCONIO/INC
          INC       FMSCSAIO/INC
          INC       FMSCTRIO/INC
          INC       FMSLMAIO/INC
          INC       FMSLMCIO/INC
          INC       FMSLMCKY    
.
          INC       STDGENCD
.
