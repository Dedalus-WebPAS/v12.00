.******************************************************************************
.*  Include Name  : KYPMHCPB.PBL                                              *
.*  Description   : Keyin of a GP Code (using PMSDSHCP)                       *
.*  Author        : LOFTY   28/03/1994  (copy of kypatrgp)                    *
.*                                                                            *
.*  Parameters    : CCOL     - column of keyin                                *
.*                  CVERT    - row of keyin                                   *
.*                  CKYIDEF8 - referring GP code                              *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                                                                            *
.*  Returns       : PMHCxxxx - record                                         *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input or Exit                *
.*                       = 2   Exitchar                                       *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Modifications :                                                           *
.*                  V9.02.00 12/12/2002 J.Tan                                 *
.*                           Ported from r5.10                                *
.*                  V5.04.01 24/05/1997  Steve Armstrong                      *
.*                           Fixed save of ccol prior to "?" routine          *
.*                  V5.01.01 14/04/1994  Steve Armstrong                      *
.*                           Fixed bug in returning to KYPMHCCB if error      *
.*                           occured.                                         *
.******************************************************************************
KYPMHCPB  PACK      PMHCHCPC,CKYIDEF8,SP10
          MOVE      ZERO,CQUEST
          MOVE      CVERT,SVERT
          MOVE      CCOL,SCCOL
.
          KEYIN     *PCCOL:CVERT,*V2LON,*RV,PMHCHCPC:
                    *PCCOL:CVERT,*DV,PMHCHCPC
.
          PACK      PMHCHCPC,PMHCHCPC,SP8
          MATCH     SP8,PMHCHCPC
          GOTO      KPHCPB10 IF NOT EQUAL
.
          BRANCH    CKYIMAND,KPHCPB05        * field is mandatory
          MOVE      SP30,PMHCGNAM
          MOVE      SP30,PMHCSNAM
          UNPACK    SP8,CKYIDEF6,CKYIDEF2
          MOVE      ONE,EXIT
          GOTO      KPHCPB99
.
KPHCPB05  DISPLAY   *P1:24,*B,*EL,"Field is mandatory. ";
          CALL      HITENTER
          GOTO      KYPMHCPB
.
KPHCPB10  MATCH     EXITCHAR,PMHCHCPC
          IF        @EQUAL
            MOVE      TWO,EXIT
            GOTO      KPHCPB99
          ENDIF
.
          MATCH     QUEST,PMHCHCPC
          GOTO      KPHCPB50 IF EQUAL
.
          PACK      KEY10,PMHCHCPC
          CALL      RDPMHCP1
          IF        OVRCD = 1
            DISPLAY   *P1:24,*B,*EL,"Registered GP is not on file.  ";
            CALL      HITENTER
            GOTO      KYPMHCPB
          ELSE
            MATCH     "1",PMHCSTTS
            IF        @EQUAL
              DISPLAY   *P1:24,*EL,*B,"GP is not active.  ";
              CALL      HITENTER
              GOTO      KYPMHCPB
            ENDIF
            MOVE      ZERO,EXIT
            GOTO      KPHCPB99
          ENDIF
.
.         ? entered, so show all GP's 
.         the ? routine will return CKYIDEF6/2
.
KPHCPB50  MOVE      ZERO,HLEF
          MOVE      CVERT,SVERT
          CALL      GETSCR00
KPHCPB55  CALL      PMSHCPDS
          IF        EXIT=1
            BRANCH    CKYIMAND,KPHCPB72        * field is mandatory
            MOVE      ONE,EXIT
            MOVE      SP30,PMHCGNAM
            MOVE      SP30,PMHCSNAM
            MOVE      SP10,PMHCHCPC
            GOTO      KPHCPB75
          ENDIF
.
          PACK      KEY10,PMHCHCPC
          CALL      RDPMHCP1
          IF        OVRCD = 1
            DISPLAY   *P1:24,*B,*EL,"Registered GP is not on file.  ";
            CALL      HITENTER
            GOTO      KPHCPB55
          ELSE
            MATCH     "1",PMHCSTTS
            IF        @EQUAL
              DISPLAY   *P1:24,*EL,*B,"GP is not active.  ";
              CALL      HITENTER
              GOTO      KPHCPB55
            ENDIF
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      KPHCPB75
.
KPHCPB72  CALL      PUTSCR00
          MOVE      SVERT,CVERT
          MOVE      SCCOL,CCOL
          GOTO      KYPMHCPB
.
KPHCPB75  CALL      PUTSCR00
          MOVE      SVERT,CVERT
          MOVE      SCCOL,CCOL
          DISPLAY   *PCCOL:CVERT,*V2LON,PMHCHCPC
.
KPHCPB99  RETURN
+
