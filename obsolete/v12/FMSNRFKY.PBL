.---------------------------------------------------------------------------
.   System    :   Financial Management System
.   Include   :   FMSNRFKY
.   Routine   :   KFMNFA00 Keyin Subjective
.   Parameter :   CKEYTYP 0 = Keyin , 1 = Accept, 2 = Either
.   Return    :   EXIT = 1 null entry, 2 EXITCHAR entered
.   Note      :   Uses HFMSAA00 and HFMNEA00
.-------------------------------------------------------------------------------
KFMNFA00
          MOVE      FMSUUD3A,CSTRLEN          * set up string length
          CALL      GSTR0000                  * get code
          PACK      FMNFSCOD,CSTRING,SP70
.
          ENDSET    FMNFSCOD
          APPEND    SP70,FMNFSCOD
          RESET     FMNFSCOD
.
          MATCH     SP70,FMNFSCOD
          GOTO      KFMNFA90 IF EQUAL
.
          CMATCH    EXITCHAR,FMNFSCOD
          GOTO      KFMNFA95 IF EQUAL
.
          CMATCH    QUEST,FMNFSCOD
          GOTO      KFMNFA10 IF NOT EQUAL
.
          PACK      DISPMSG,SP70,SP70
          CALL      MESSAGE2
          KEYIN     *P1:24,*EF,*V2LON:
                    "N",*HOFF,"ormal or ",*V2LON:
                    "T",*HOFF,"otal Subjective ? ",*V2LON,ANS
          CALL      MESSAGE3
          PACK      ANS,ANS,SP70
          REP       UPPLOW,ANS
.
          MOVE      CCITEM,SCCITEM
          MOVE      SP70,FMNFSCOD
.
          MATCH     "N",ANS
          IF        @EQUAL
            MOVE      "6",CSUBJIND
            PACK      FMSALEDG,FMNDLEDG,SP70
            CALL      HFMSAA00
            PACK      FMNFSCOD,FMSASUBJ,SP70
            MOVE      SP70,ANS
          ENDIF
.
          MATCH     "T",ANS
          IF        @EQUAL
            PACK      FMNECODE,FMNFCODE,SP70
            CALL      HFMNEA00
            PACK      FMNFSCOD,FMNETCOD,SP70
            MOVE      SP70,ANS
          ENDIF
.
          MOVE      SCCITEM,CCITEM
.
          ENDSET    FMNFSCOD
          APPEND    SP70,FMNFSCOD
          RESET     FMNFSCOD
.
          MATCH     SP70,FMNFSCOD
          GOTO      KFMNFA00 IF EQUAL
.
          DISPLAY   *PCCOL:CVERT,*V2LON,FMNFSCOD
KFMNFA10
          MOVE      "000",FMNETLEV
          PACK      FMSADESC,SP70
          PACK      KEY14,FMNDLEDG,FMNFSCOD,SP70
          CALL      RDFMSA1
          PACK      FMNEDESC,FMSADESC,SP70
          IF        OVRCD=1
            PACK      KEY15,FMNFCODE,FMNFSCOD,SP70
            CALL      RDFMNE1
          ENDIF
.
          BRANCH    OVRCD,KFMNFA80
          BRANCH    CKEYTYP,KFMNFA75
KFMNFA70
          MOVE      ZERO,EXIT
          GOTO      KFMNFA99
KFMNFA75
          MOVE      "Subjective Already Exists - ",DISPMSG
          CALL      MESSAGE1
          GOTO      KFMNFA00
KFMNFA80
          MOVE      ZERO,EXIT
          BRANCH    CKEYTYP,KFMNFA99,KFMNFA99
          MOVE      "Subjective Does Not Exist - ",DISPMSG
          CALL      MESSAGE1
          GOTO      KFMNFA00
KFMNFA90
          MOVE      ONE,EXIT
          GOTO      KFMNFA99
KFMNFA95
          MOVE      TWO,EXIT
KFMNFA99
          RETURN
