.******************************************************************************
.* Include Name   : KYAAEGRC.PBL                                              *
.*                                                                            *
.* Description    : Keyin of a Region code from AAERGCFD                      *
.*                                                                            *
.* Author         : LOFTY                                                     *
.*                                                                            *
.* Parameters     : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF2 - default value                                  *
.*                  CKYIMAND - 1=mandatory                                    *
.*                                                                            *
.* Returns        : AERCCODE - Keyed in region code                           *
.*                  AERCDESC - Description                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.* On return      : You must display the description yourself.                *
.*                                                                            *
.* Mods           : V5.05.01 23/12/1997  Steve Armstrong    EMR               *
.*                  Added setting of AERCFRGN prior to calling AAEGRCDS       *
.******************************************************************************
KYAAEGRC  MOVE      ECOL,CCOL
          MOVE      EVERT,CVERT
          MOVE      CKYIDEF2,AERCCODE
          KEYIN     *PCCOL:CVERT,*V2LON,*RV,AERCCODE:
                    *PCCOL:CVERT,*DV,AERCCODE
          PACK      AERCCODE,AERCCODE,SP2
          MATCH     SP2,AERCCODE
          GOTO      KAERC790 IF EQUAL       * nothing entered
          MATCH     EXITCHAR,AERCCODE
          GOTO      KAERC820 IF EQUAL       * exitchar entered
          MATCH     QUEST,AERCCODE
          GOTO      KAERC200 IF NOT EQUAL   * ? not entered
.
.         ? entered
.
          MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
          MOVE      SP1,AERCFRGN            * set to display all codes
          CALL      AAEGRCDS                * display valid entries
          CALL      PUTSCR00                * restore screen
          BRANCH    EXIT,KAERC790           * nothing entered
.
.         validate what was entered
.
KAERC200  MOVE      AERCCODE,KEY2
          CALL      RDAEGRC1
          BRANCH    OVRCD,KAERC400          * invalid code
.
          MOVE      ZERO,EXIT               * set as valid code
          GOTO      KAERC800
.
.         invalid code
.
KAERC400  DISPLAY   *P1:24,*B,"Invalid Code.  ",*EL;
          CALL      HITENTER
          GOTO      KYAAEGRC
.
.         nothing entered
.
KAERC790  MOVE      ONE,EXIT                * set exit flag
          MOVE      SP2,AERCCODE
          COMPARE   ZERO,CKYIMAND
          GOTO      KAERC800 IF EQUAL       * not mandatory
.
          DISPLAY   *P1:24,*B,"Code must be entered.  ",*EL;
          CALL      HITENTER
          GOTO      KYAAEGRC
.
.         valid entry
.
KAERC800  DISPLAY   *PECOL:EVERT,*V2LON,AERCCODE
          GOTO      KAERC999 
.
.         exitchar entered
.
KAERC820  MOVE      TWO,EXIT                * EXITCHAR entered
.
KAERC999  RETURN
+
