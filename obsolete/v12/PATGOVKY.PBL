.******************************************************************************
.*  Include Name  : PATGOVKY.PBL                                              *
.*  Description   : Keyin of allowance codes                                  *
.*  Author        : Matt Surridge                                             *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF6 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory  (0=No  1=Yes)              *
.*                  CKYIMODE - is keyin input mode (0=No  1=Yes)              *
.*                                                                            *
.*  Returns       : GCODE    - Keyed in code                                  *
.*                  GNAME    - Description                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.******************************************************************************
PATGOVKY
PTGVKY00  MATCH     SP6,CKYIDEF6                  * using a default ?
          GOTO      PTGVKY10 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN2:      * no default used
                    *PECOL:EVERT,*V2LON,GCODE:
                    *PECOL:EVERT,*DV,GCODE
          GOTO      PTGVKY20
.
PTGVKY10  MOVE      CKYIDEF6,GCODE                 * default
          KEYIN     *PECOL:EVERT,*DV,GCODE:     * default used
                    *PECOL:EVERT,*V2LON,*RV,GCODE:
                    *PECOL:EVERT,*DV,GCODE
.
.         check what was entered
.
PTGVKY20  CALL      GOVKY000
          BRANCH    EXIT,PTGVKY25,PTGVKY31,PTGVKY41,PTGVKY95
.                        "?"      valid    nothing  exitchar
          GOTO      PTGVKY00
.
.         a "?" was input
.
PTGVKY25  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
PTGVKY26  CALL      PATGOVDS
          BRANCH    CNEWDS,PTGVKY27 
.
          CALL      PUTSCR00                * redisplay screen
          GOTO      PTGVKY00                * rekey code
.
.         now ask for code while leaving displayed codes on screen
.
PTGVKY27  MATCH     SP6,CKYIDEF6                  * using a default
          GOTO      PTGVKY28 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Government Code : ":
                    *P19:24,*DV,UNDLN2:      * no default used
                    *P19:24,*V2LON,GCODE:
                    *P19:24,*DV,GCODE
          GOTO      PTGVKY29
.
PTGVKY28  MOVE      CKYIDEF6,GCODE
           KEYIN     *P1:24,*EL,*DV,"Government Code : ":
                    *P19:24,*DV,GCODE:     * default used
                    *P19:24,*V2LON,*RV,GCODE:
                    *P19:24,*DV,GCODE
.
.         repeat the validation of the code
.         check what was entered
.
PTGVKY29  CALL      GOVKY000
          BRANCH    EXIT,PTGVKY26,PTGVKY30,PTGVKY40,PTGVKY90
.                        "?"      valid    nothing  exitchar
.
          GOTO      PTGVKY27
.
.         a valid code was entered 
. 
PTGVKY30  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,GCODE
.
PTGVKY31  MOVE      FALSE,EXIT                    * valid input
          GOTO      PTGVKY99 
.
.         nothing was input
.
PTGVKY40  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,GCODE
.
PTGVKY41  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      PTGVKY99 
.
.         EXITCHAR input
.
PTGVKY90  CALL      FRESCR00                * free the screen saved
.
PTGVKY95  MOVE      TWO,EXIT                * EXITCHAR
.
PTGVKY99  RETURN
+
.*********************************************************************
.*                  GOVKY000                    Called by : PATCODKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
GOVKY000  ENDSET    GCODE
          APPEND    SP6,GCODE
          RESET     GCODE
.
          MATCH     EXITCHAR,GCODE
          GOTO      GOVKY600 IF EQUAL       * exit char
.
          MATCH     SP6,GCODE
          GOTO      GOVKY700 IF EQUAL       * nothing
.
          MATCH     QUEST,GCODE
          GOTO      GOVKY800 IF EQUAL       * ? mark
.
.         a code was entered so validate
. 
GOVKY500  PACK      KEY6,GCODE,SP6
          CALL      RDGOVR1
. 
          IF        CKYIMODE=0
            BRANCH    OVRCD,GOVKY900          * invalid code
          ELSE
            COMPARE   ZERO,OVRCD
            GOTO      GOVKY900 IF EQUAL
          ENDIF
.
          MOVE      TWO,EXIT                * valid code
          GOTO      GOVKY999
.
.         exitchar entered
.
GOVKY600  MOVE      FOUR,EXIT
          MOVE      SP6,GCODE
          MOVE      SP20,GNAME              * clear desc field
          GOTO      GOVKY999
.
.         nothing entered
.
GOVKY700  BRANCH    CKYIMAND,GOVKY950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP6,GCODE
          MOVE      SP20,GNAME              * clear desc field
          GOTO      GOVKY999
.
.         "?" entered
.
GOVKY800  MOVE      ONE,EXIT
          GOTO      GOVKY999
.
.         invalid code entered
.
GOVKY900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          IF        CKYIMODE=0
            DISPLAY   *P1:24,*EL,*B,"Invalid Government Code.  ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      GOVKY999
.
.         field is mandatory
.
GOVKY950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is mandatory.  ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
GOVKY999  RETURN
+
          INC       PATGOVDS
.
+
