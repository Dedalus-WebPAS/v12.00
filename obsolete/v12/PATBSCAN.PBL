.******************************************************************************
.* System         : Patient Management                                        *
.*                                                                            *
.* Include Name   : PATBSCAN.PBL                                              *
.*                                                                            *
.* Function       : Loop over the Ward/bed file and display details of the    *
.*                  patients in the wards.                                    *
.*                                                                            *
.* Index used     : PATWR1A1,"patwr1af"                                       *
.*                  PATWR1A2,"patwr1af"                                       *
.*                  PATTRAN2,"pattranf"                                       *
.*                  PATCODE1,"patcodes"                                       *
.*                                                                            *
.* Parameters     : LOOPTRN  = 0   display information                        *
.*                  LOOPTRN  = 1   print   information                        *
.*                  PROGFLAG = 0   there is a print option                    *
.*                  PROGFLAG = 1   there is not a print option                *
.*                  MHCNUSE  = 1   using Mental health system                 *
.*                                                                            *
.* NOTE           : If details are to be printed, the include goes back to the*
.*                  calling program where LOOPTRN is set to one and the       *
.*                  routine is re-called at the label RDFIRST.                *
.*                                                                            *
.* Modifications  :                                                           *
.*        V5.08.B01 10/04/2000  Greg Horvat                                   *
.*                  Removed any reference to matadmbf                         *
.*                  18/02/2000  Tonii           SRF 646462                    *
.*                              Changed so search for all beds is possible    *
.*                  08/10/1998  Glenn Berry      5.07 changes                 *
.******************************************************************************
.*                  17/06/89  Graeme Williams   SRF 101101                    *
.*                            Replaced the standby file (PATSBYFD) with the   *
.*                            variable WSTBY in the ward/bed file (PATWR1FD)  *
.*                  01/07/89  S.Barcham         SRF 101142                    *
.*                            Opened maternity files                          *
.*                  07/08/89  S.Barcham         SRF 101815                    *
.*                            Printed S or O for Standby or Onleave           *
.*                  28/03/91  i chung           SRF 107934                    *
.*                            Added exit option at start of scan              *
.*                  12/04/91  J.Tan             SRF 953                       *
.*                            Fixed displaying babies                         *
.*                  11/09/91  Justin Coates                                   *
.*                            Mental Health changes.                          *
.*                  11/06/93  J.Tan             SRF 121660                    *
.*                            Mods to check for active ward/bed               *
.*                  24/06/93  I.Rutt            Singapore Mods                *
.*                            Added checks for PTCNSMKR                       * 
.*                  07/02/94  Rob Leonard       SRF 127570                    *
.*                            Fixed displaying empty in a no bed ward         * 
.*                  28/06/94  Whirlpool                                       *
.*                            Changed display for multi hospital (see RENO)   *
.*                  19/05/97  Nick Read                                       *
.*                            Added ADIAG2 to report format making each       *
.*                            record print over two lines                     * 
.*                  06/01/1998  Steve Armstrong   SRF 643867                  *
.*                            Mods to load PDIAG1 with MH Legal Status        *
.*                            instead of ADIAG.                               *
.******************************************************************************
.
BSCAN     DISPLAY   *P1:3,*EF:
                    *P20:4,*V2LON,*ULON," Ward / Bed Scanning "
.
          CALL      DSPWDS00         * Display available wards
.
          MOVE      SP3,WWARD
          MOVE      SP3,WBED
          MOVE      ZERO,LOOPTRN 
          MOVE      ZERO,CPAGENO
.
.         Get the starting ward
.
BSCAN1    DISPLAY   *P1:24,*EL,"Starting Ward : ":
                    *P26:24,"<",*V2LON,"RETURN",*HOFF,"> for all Wards or, <":
                    *V2LON,"E",*HOFF,"> to Exit";
.
          KEYIN     *P17:24,*DV,SP3,*P17:24,*DV,UNDLN3,*P17:24,*V2LON,WWARD;
          ENDSET    WWARD
          APPEND    SP3,WWARD
          RESET     WWARD
.
          MATCH     SP3 TO WWARD
          GOTO      BS1 IF EQUAL   
.
          DISPLAY   *P17:24,*EL,*V2LON,WWARD;
          MATCH     "E  " TO WWARD
          GOTO      ENDBSCAN IF EQUAL   
          MOVE      WWARD,WSWARD
.
.         Check for a valid starting ward
.
          PACK      KEY6 WITH WWARD,SP3
RDFIRST   CALL      RDWARD1
          BRANCH    OVRCD OF INVWARD
.
          BRANCH    WACTIVE,NOTACTIV       * Ignore non-active wards
          GOTO      CNTWARD
.
NOTACTIV  KEYIN     *P1:24,*EL,*V2LON,*B,"** Ward is not active ** ";
          CALL      HITENTER
          GOTO      BSCAN
.
.         No ward entered, look at all wards. Read in first ward record
.
BS1       MOVE      SP6,KEY6
          CALL      RDSWARD1
BS2       CALL      RDKWARD1
          BRANCH    OVRCD OF INVWARD
.
          BRANCH    WACTIVE,BS2            * Ignore non-active wards
.
          PACK      KEY6 WITH WWARD,SP3
          MOVE      SP3,WSWARD
.
.         If we are displaying, ask if we only want empty beds
.
CNTWARD   BRANCH    LOOPTRN OF WNEWWRD      * printing
.
          MOVE      KEY6,SKEY6  * Save KEY6 for printing
.
          MOVE      ANSN,ANS
          DISPLAY   *P26:24,*EL,*P40:24,"Empty Beds only ? ",*V2LON,ANS;
          KEYIN     *P58:24,*RV,ANS;
          RESET     ANS
          REP       UPPLOW,ANS
.
          MOVE      ZERO,MTINDC               *** All Beds
          MATCH     ANSY,ANS
          GOTO      YMTBED1 IF NOT EQUAL
.
.         Empty beds only, use the second index to loop over
.
          MOVE      ONE,MTINDC                *** Empty Beds Only
          MOVE      ZERO,WADMNO
          PACK      KEY14 WITH WADMNO,WSWARD,SP3
          CALL      RDWARD2
          COMPARE   ZERO,OVRCD
          GOTO      YMTBED IF EQUAL
.
          CALL      RDSWARD2
IGNNAC    CALL      RDKWARD2
.
.         Display the headings
.
YMTBED    BRANCH    WACTIVE,IGNNAC2        * Ignore non-active wards
.
YMTBED1   MOVE      ZERO,WSOPT             * srf 646462
          CALL      SCRDISP
          GOTO      WNEWWRD
.
.         Read in next ward/bed record
.
NEXTW     BRANCH    MTINDC OF NEXTWMT
          CALL      RDKWARD1
          BRANCH    OVRCD OF ENDSCN 
          GOTO      CNTWSC
.
.         Ignore inactive ward/beds
.
IGNNAC2   MATCH     SP3,WBED                   * Is this a bed record ?
          GOTO      IGNNAC IF NOT EQUAL        * Yes - ignore record
          MOVE      ZERO,WADMNO                * For free beds
          PACK      KEY14,WADMNO,WWARD,ANSZ,ANSZ,ANSZ * Skip to next ward
          CALL      RDSWARD2
          GOTO      IGNNAC
.
.         Next record in second index (empty beds only wanted)
.
NEXTWMT   CALL      RDKWARD2
          BRANCH    OVRCD OF ENDSCN
.
          BRANCH    WACTIVE,NEXTWMT            * Ignore non-active wards
.
          COMPARE   ZERO,WADMNO
          GOTO      ENDSCN IF NOT EQUAL
.
.         Set the default action to be read next ward/bed record
.         This can be overwritten when a screen full has been read in
.
CNTWSC    MOVE      ZERO,WSOPT
.
.         Check for a new ward
.
          MATCH     SP3,WBED
          GOTO      WNEWWRD IF EQUAL
.
          BRANCH    WACTIVE,NEXTW        * Ignore non-active wards
.
.         Check if this bed is empty (WADMNO=0 means the bed is empty)
.
          MOVE      ZERO,AADMNO          * This is for the display routine
.
          COMPARE   ZERO,WADMNO
          GOTO      WDSPD IF EQUAL
.
.         Get the details of the person in this bed
.
          MOVE      WADMNO,KEY8
          CALL      WDETS
.
.         Display or Print the details for this person
.
WDSPD     CALL      WDSPBED
          BRANCH    WSOPT OF ENDBSCAN,BSCAN,PRTWRRD
.
.         Check if there is a standby patient for this bed
.
          COMPARE   ZERO,WSTBY
          GOTO      WONLV IF EQUAL
.
          MOVE      WSTBY,AADMNO
          MOVE      AADMNO,KEY8
          CALL      WDETS
.
          MOVE      "Standby Patient  ",ADDESC
          MOVE      "S" TO SORL
.
          CALL      WDSPBED
          BRANCH    WSOPT OF ENDBSCAN,BSCAN,PRTWRRD
.
.         Loop over the on-leave file to see if there is anyone on-leave for
.         this bed
.
WONLV     PACK      KEY14 WITH WWARD,WBED,SP6
          CALL      RDSONLV1
.
NEXTOL    CALL      RDKONLV1
          BRANCH    OVRCD OF NEXTW
.
          MATCH     WWARD,OWARD
          GOTO      NEXTW IF NOT EQUAL
.
          MATCH     WBED,OBED
          GOTO      NEXTW IF NOT EQUAL
.
.         We have some-one on-leave - get the details and display/print them
.
          MOVE      OADMNO,KEY8
.
. ------- only display patients on Short term leave ------
.
          PACK      KEY30,OADMNO,Z30
          CALL      RDSTRAN2                * positional read
          CALL      RDPTRAN2
          BRANCH    OVRCD,NEXTOL            * invalid admission number
.
          COMPARE   OADMNO,TADMN
          GOTO      NEXTOL IF NOT EQUAL     * invalid admission number
.
          MATCH     "L",TMOVE
          GOTO      NEXTOL IF NOT EQUAL     * invalid admission number
.
          MATCH     SP3,PTTRLTYP
          GOTO      VALDOL IF EQUAL         * no type of leave so valid
.
          PACK      KEY5,ANST,ANSL,PTTRLTYP
          CALL      RDCODE1
          BRANCH    OVRCD,NEXTOL            * invalid type of leave
.
          MATCH     SP1,TCINDC1
          GOTO      NEXTOL IF NOT EQUAL     * not short term leave
.
VALDOL    CALL      WDETS
.
          MOVE      "Patient on leave ",ADDESC
          MOVE      "O" TO SORL
.
          CALL      WDSPBED
          BRANCH    WSOPT OF ENDBSCAN,BSCAN,PRTWRRD
          GOTO      NEXTOL
.
.         Subroutine to check for a full screen, and ask what the user
.         wants to do
.
WSCRNFL   COMPARE   TWENTY3,CVERT
          RETURN    IF LESS
.
.         Check if the (P)rint option is available
.
          DISPLAY   *P1:24,*EL," (";
          MOVE      SP1,ANS
.
          BRANCH    PROGFLAG OF MORTYP1
.
          KEYIN     *V2LON,"P",*HOFF,")rint, (":
                    *V2LON,"N",*HOFF,")ext, (":
                    *V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"M",*HOFF,")ore ? ",*V2LON,ANS; 
          GOTO      MORCHK1
.
MORTYP1   KEYIN     *V2LON,"N",*HOFF,")ext, (":
                    *V2LON,"R",*HOFF,")eturn, (":
                    *V2LON,"M",*HOFF,")ore ? ",*V2LON,ANS; 
.
.         Check what was selected
.
MORCHK1   RESET     ANS
          TYPE      ANS
          GOTO      WNEWSCR IF EQUAL
.
.         Convert to a number
.
          REP       UPPLOW,ANS
          REP       "M0R1E1N2P3",ANS
.
          MOVE      ANS,WSOPT
          COMPARE   ZERO,WSOPT
          GOTO      WNEWSCR IF EQUAL
.
.         If printing is not available, then convert 3 to 0
.
          BRANCH    PROGFLAG OF WNOPRT
          RETURN
.
WNOPRT    COMPARE   THREE,WSOPT
          RETURN    IF NOT EQUAL
.
          MOVE      ZERO,WSOPT
.
.         Clear the screen and reset CVERT
.
WNEWSCR   MOVE      FIVE,CVERT
          DISPLAY   *P1:CVERT,*EF
.
          CALL      WDSPW
          RETURN
+
.
.         End of ward/bed file - ask the user what they want to do
.
ENDSCN    BRANCH    LOOPTRN OF ENDPRT
.
          COMPARE   FIVE,CVERT
          GOTO      INVWARD IF EQUAL
.
          BRANCH    PROGFLAG OF MORTYP2
          KEYIN     *P1:24,*EL," (",*V2LON,"P",*HOFF,")rint, (":
                               *V2LON,"E",*HOFF,")xit, (":
                               *V2LON,"N",*HOFF,")ext ? ",*V2LON,ANS; 
.
          REP       UPPLOW,ANS
          MATCH     ANSP TO ANS
          GOTO      PRTWRRD IF EQUAL
.
          MATCH     ANSE TO ANS
          GOTO      ENDBSCAN IF EQUAL
          GOTO      BSCAN 
.
MORTYP2   KEYIN     *P1:24,*EL," (",*V2LON,"R",*HOFF,")eturn, (":
                           *V2LON,"N",*HOFF,")ext ? ",*V2LON,ANS; 
          REP       UPPLOW,ANS
.
          MATCH     ANSR TO ANS
          GOTO      ENDBSCAN IF EQUAL
          GOTO      BSCAN 
.
.         Screen BHEADING displayed
.
SCRDISP   DISPLAY   *P1:4,*EF,*V2LON,*ULON,"Bed":
                    *P5:4,"     U/R",*P14:4,"    Admn":
                    *P23:4,"Patient's Name       ":
                    *P45:4,"Age";
          IF        PTCNSMKR = 0
            DISPLAY   *P49:4,*V2LON,*ULON,"Smk";
          ELSE
            DISPLAY   *P49:4,*V2LON,*ULON,"Typ";
          ENDIF
          DISPLAY   *P53:4,*V2LON,*ULON,"Diagnosis       ":
                    *P70:4,"Att. Doctor"
.
          MOVE      FIVE,CVERT
          RETURN
+
.
INVWARD   DISPLAY   *P1:24,*EL,*B,"Invalid Ward / Bed.  ";
          CALL      HITENTER
          GOTO      BSCAN1
.
.         Subroutine to get the details of the person whose admission number is
.         in KEY8
.
WDETS     CALL      RDMISS1
          BRANCH    OVRCD OF WNOADMN
          GOTO      RNXTGO
.
.         Invalid admission number in the ward/bed file
.
WNOADMN   MOVE      "       0",AURNO
          MOVE      NBADMNO,AADMNO
          MOVE      SP1,PSEX
          MOVE      "PATIENT RECORD MISSING",PNAME
          MOVE      "Contact I.B.A.        ",ADDESC
          PACK      PDIAG1 WITH SP30,SP30
          PACK      PDIAG2 WITH SP30,SP30
          MOVE      SP30,DOCNAME
          MOVE      SP30,DSMOK3
          MOVE      ZERO,PSAGE
          RETURN
.
.         Normal patient - get the master file details
.
RNXTGO    MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,WNOADMN
.
          MOVE      PGNAME,ANS
          PACK      PNAME WITH ANS,DOT,PSNAME
          REP       ", ",PNAME
.
          PACK      ADDESC,ADIAG1
          PACK      PDIAG1,ADIAG1
          PACK      PDIAG2,ADIAG2
.
          IF        PTCNSMKR = 0  
            MOVE      DUNK,DSMOK3
            LOAD      DSMOK3 FROM PSMOK OF DYES,DNO
          ELSE
              MOVE      WEFRBT,DSMOK3
          ENDIF 
.
WGETDOC   MOVE      SP30,DOCNAME
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF WENDGD
          MOVE      DGNAME,ANS
          PACK      DOCNAME WITH ANS,DOT,DSNAME
          MOVE      DOCNAME,DOCDISP
.
WENDGD    RETURN
.
.         New ward - check if inactive
.
WNEWWRD   BRANCH    WACTIVE,NXTWARD        * Ignore non-active wards
.
.         New Ward - Save the description and extension number
.
          MOVE      WBDESC,WRDESC
          MOVE      WEXTN,WREXTN
.
.         Check if the indicator says we are only interested in one ward
.
          COMPARE   ONE,CWRDSCN
          GOTO      WNEWWC IF NOT EQUAL
.
.         A blank save ward indicates that nothing was initially entered
.
          MATCH     SP3,WSWARD
          GOTO      WNEWWC IF EQUAL
.
.         If this is not the ward we initially entered, then we have finished
.
          MATCH     WWARD,WSWARD
          GOTO      ENDSCN IF NOT EQUAL
.
WNEWWC    BRANCH    LOOPTRN OF WPRTWRD
.
.         Put a blank line between wards on the screen
.
          COMPARE   FIVE,CVERT
          GOTO      WDSPWRD IF EQUAL
.
          COMPARE   TWENTY2,CVERT
          GOTO      WWRDF IF NOT LESS
          ADD       ONE,CVERT
.
.         Display the details of this ward
.
WDSPWRD   CALL      WDSPW
          GOTO      WENDWRD
.
.         Get next ward
.
NXTWARD   PACK      KEY6,WWARD,ANSZ,ANSZ,ANSZ
          CALL      RDSWARD1
          GOTO      NEXTW
.
WPRTWRD   COMPARE   "52",CLNO
          CALL      BHEAD IF NOT LESS
.
.         Print a line of '-'s to seperate wards on the printout
.
          COMPARE   SEVEN,CLNO
          CALL      PAGEND IF NOT EQUAL
.
          DISPLAY   *P65:24,*V2LON,WWARD;
.
          PRINT     *54,"Ward : ",WWARD:
                    SP1,WBDESC," Extension  - ",WEXTN:
                    *54,"Ward : ",WWARD:
                    SP1,WBDESC," Extension  - ",WEXTN
          ADD       ONE,CLNO
          CALL      PAGEND 
          GOTO      WENDWRD
.
.         No room on the screen for another ward
.
WWRDF     MOVE      TWENTY4,CVERT      * Dummy to force selection of option
          CALL      WSCRNFL
          BRANCH    WSOPT OF ENDBSCAN,BSCAN,PRTWRRD
.
.         We have finished displaying/printing ward header. Check if this is
.         a ward-only ward, and if so, loop over the NOBED file
.
WENDWRD   BRANCH    WINPUT OF WRDONLY
          GOTO      NEXTW
.
.         Check if we are only interested in empty beds
.
WRDONLY   BRANCH    MTINDC OF WRDOMT
.
.         Loop over the No Bed file to list the people for this ward
.
          MOVE      SP3,WBED
          PACK      KEY13 WITH WWARD,SP10
          CALL      RDSNOBE1
NEXTNB    CALL      RDKNOBE1
          BRANCH    OVRCD OF WONLV         * Check for any on-leave patients
.
          MATCH     WWARD,NBWARD
          GOTO      WONLV IF NOT EQUAL     * Check for any on-leave patients
.
          MOVE      ZERO,WSOPT
.
          MOVE      NBADMNO,KEY8
          CALL      WDETS
          CALL      WDSPBED
          BRANCH    WSOPT OF ENDBSCAN,BSCAN,PRTWRRD
          GOTO      NEXTNB
.
.         For ward only wards, always say there is an empty bed if we are looking
.         for empty beds
.
WRDOMT    MOVE      ZERO,WSOPT
          MOVE      ZERO,AADMNO
          CALL      WDSPBED
          BRANCH    WSOPT OF ENDBSCAN,BSCAN,PRTWRRD
          GOTO      NEXTW
.
.         Subroutine to display the ward details on the screen
.
WDSPW     DISPLAY   *P23:CVERT,*V2LON,"Ward : ",WWARD:
                    SP1,WRDESC,"Extension - ",WREXTN;
          ADD       ONE,CVERT
          RETURN
.
.         Subroutine to display/print the details for one person
.
WDSPBED   COMPARE   ZERO,AADMNO
          GOTO      WMTBED IF EQUAL
.
          CALL      CKMEH000                * set the descriptions correctly
.
          BRANCH    LOOPTRN OF FPRINT       * print the details
.
.         If the patient is on-leave, then highlight the ward/bed
.
          COMPARE   WADMNO,AADMNO
          GOTO      WSTDRD IF EQUAL
.
          COMPARE   FOUR,ASTAT
          GOTO      WHIGH IF EQUAL          * patient is on leave
.
          MATCH     SP3,WBED
          GOTO      WSTDRD IF EQUAL
.
WHIGH     DISPLAY   *P1:CVERT,*V2LON,WBED,*P53:CVERT,ADDESC;
          GOTO      WCNTD
.
WSTDRD    DISPLAY   *P1:CVERT,WBED,*P53:CVERT,ADDESC;
.
WCNTD     DISPLAY   *P5:CVERT,AURNO,*P14:CVERT,AADMNO:
                    *P23:CVERT,PNAME,*P44:CVERT,PSEX:
                    *P45:CVERT,PSAGE,*P49:CVERT,DSMOK3:
                    *P70:CVERT,DOCDISP;
          ADD       ONE,CVERT
          CALL      WSCRNFL
          RETURN
.
FPRINT    COMPARE   "55",CLNO
          CALL      BHEAD IF NOT LESS
.
          PRINT     *1,"| ",WBED,*7,"| ",AURNO,*18,"| ",AADMNO," ",SORL:
                    *31,"| ",PNAME,*56,"| ",PSEX,*61,"| ",PSAGE:
                    *66,"| ",DSMOK3,*71,"| ",PDIAG1,*111,"|",DOCNAME,*132,"|"
.
          PRINT     *1,"|",*7,"|",*18,"|",*31,"|",*56,"|",*61,"|",*66,"|":
                    *71,"| ",PDIAG2,"|",*132,"|"
.
          MOVE      SP1 TO SORL
          ADD       TWO,CLNO
          RETURN
.
.         Empty bed - display/print the details
.
WMTBED    BRANCH    LOOPTRN OF PRTMTB
          DISPLAY   *P1:CVERT,WBED,*P23:CVERT,"*** Empty ***";
          ADD       ONE,CVERT
          CALL      WSCRNFL
          RETURN
.
PRTMTB    COMPARE   "55",CLNO
          CALL      BHEAD IF NOT LESS
.
          PRINT     *1,"| ",WBED,*7,"|",*18,"|":
                    *31,"| ","*** Empty ***":
                    *56,"|",*61,"|",*66,"|",*71,"|",*111,"|",*132,"|"
          ADD       ONE,CLNO
          RETURN
.
.         Subroutine to find the end of the patients name
.
BUMPPN    CMATCH    SP1,PNAME
          RETURN    IF NOT EQUAL
          BUMP      PNAME BY -1
          RETURN    IF EOS
          GOTO      BUMPPN
.
.*********************************************************************
.                   DSPWDS00
.         Subroutine to display the available wards
.*********************************************************************
DSPWDS00  OPEN      PATWR1A3,"patwr1af"
          MOVE      SIX,CVERT
          MOVE      ONE,CCOL
.
.         Loop over the third index to find all the wards
.
          MOVE      SP6,KEY6
          CALL      RDSWARD3
.
DSPWDS10  CALL      RDKWARD3
          BRANCH    OVRCD OF DSPWDS90       * end of wards
.
          BRANCH    WACTIVE,DSPWDS10        * not active
.
          MATCH     SP3,WBED
          GOTO      DSPWDS90 IF NOT EQUAL   * finished wards
.
          COMPARE   TWENTY3,CVERT
          GOTO      DSPWDS30 IF LESS        * will fit on page
.
          IF        IBCNMHOS = 0
            MOVE      SIX,CVERT
            ADD       "40",CCOL
.
            COMPARE   "70",CCOL
            GOTO      DSPWDS30 IF LESS        * will fit on screen
          ENDIF
.
.         Screen full - ask if the next screen is to be displayed
.
          KEYIN     *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue Scan, (":
                    *V2LON,"M",*HOFF,")ore ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
.
          MATCH     ANSC,ANS
          GOTO      DSPWDS90 IF EQUAL       * continue
.
.         set up for new screen
.
          MOVE      SIX,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *PCCOL:CVERT,*EF;
.
DSPWDS30  IF        IBCNMHOS = 1
            PACK      PTHSNAME,SP30,SP30
            PACK      KEY3,PTWDHOSN
            CALL      RDPTHSP1
            DISPLAY   *PCCOL:CVERT,*V2LON,WWARD,*HOFF,SP3,WBDESC:
                      *P41:CVERT,PTHSNAME
          ELSE
            DISPLAY   *PCCOL:CVERT,*V2LON,WWARD,*HOFF,SP3,WBDESC
          ENDIF
          ADD       ONE,CVERT
          GOTO      DSPWDS10
.
DSPWDS90  CLOSE     PATWR1A3
.
DSPWDS99  RETURN
.
.         Other Programs require bottom four labels with a RETURN
.
.PRTWRRD   
.ENDPRT   GOTO      ENDW
.
.PAGEND
.PRTWRRD   
.BHEAD    RETURN
.
.*********************************************************************
.*                  CKMEH000                    Called by : xxxx0000 *
.*        Check if a Mental Health patient (if not on leave) and     *
.*        set up the legal status and visit status                   *
.*********************************************************************
CKMEH000  COMPARE   "4",ASTAT
          GOTO      CKMEH999 IF EQUAL       * don't do for mental health
.
          COMPARE   ONE,MHCNUSE
          GOTO      CKMEH999 IF NOT EQUAL   * not using mental health
.
          MOVE      AADMNO,KEY8
          CALL      RDMHVIS1
          BRANCH    OVRCD,CKMEH999          * no mental health visit for patient
.
          MOVE      "Restricted",TDESC
          BRANCH    MHVIVISI,CKMEH200
          MOVE      "Open",TDESC
.
CKMEH200  CLEAR     ADDESC
          APPEND    PTMXLS,ADDESC           * add legal status
          APPEND    " - ",ADDESC  
          APPEND    TDESC,ADDESC
          RESET     ADDESC
          MOVE      ADDESC,PDIAG1           * set up for print value
.                  
CKMEH999  RETURN
+
. 
.         End of Bed Scan
.
ENDBSCAN
..............................................................................
