.******************************************************************************
.* Include Name   : KYAAEGTC.PBL                                              *
.*                                                                            *
.* Description    : Keyin of a Treatment code from AAEGTCFD                   *
.*                                                                            *
.* Author         : LOFTY      (03/08/1993)                                   *
.*                                                                            *
.* Parameters     : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF3 - default value                                  *
.*                  CKYIMAND - 1=mandatory                                    *
.*                                                                            *
.* Returns        : AETCCODE - Keyed in treatment code                        *
.*                  AETCDESC - Description                                    *
.*                  AETLCODE - Keyed in treatment link code                   *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.* On return      : You must display the description yourself.                *
.*                                                                            *
.* Modifications  :                                                           *
.*        V5.03.01  23/06/1995    Justin Coates  SRF 133250                   *
.*                  if a code is selected that has links, but the main code   *
.*                     is selected, set AETLCODE to spaces                    *
.*                  fix the unpack of key6 after ? routine                    *
.******************************************************************************
KYAAEGTC  MOVE      ECOL,CCOL
          MOVE      EVERT,CVERT
          MOVE      CKYIDEF3,KEY3
          KEYIN     *PCCOL:CVERT,*V2LON,*RV,KEY3:
                    *PCCOL:CVERT,*DV,KEY3
          PACK      KEY3,KEY3,SP3
          MATCH     SP3,KEY3
          GOTO      KAETC790 IF EQUAL       * nothing entered
          MATCH     EXITCHAR,KEY3
          GOTO      KAETC820 IF EQUAL       * exitchar entered
          MATCH     QUEST,KEY3
          GOTO      KAETC200 IF NOT EQUAL   * ? not entered
.
.         ? entered
.
          MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
          CALL      AAEGTCDS                * display valid entries
          CALL      PUTSCR00                * restore screen
          BRANCH    EXIT,KAETC790           * nothing entered
          UNPACK    KEY6,KEY2,ANS,ANS,KEY1,ANS
          PACK      KEY3,KEY2,KEY1
.
.         validate what was entered
.
KAETC200  UNPACK    KEY3,KEY2,KEY1
          PACK      KEY4,KEY2,SP4 
          CALL      RDAEGTC1
          BRANCH    OVRCD,KAETC400          * invalid code
.
          MATCH     ANSY,AETCSUBA           * using sub-analysis
          IF        @EQUAL
            MATCH     SP1,KEY1                * has a code been entered
            IF        !@EQUAL
              PACK      KEY6,AETCCODE,KEY1,SP1
              CALL      RDAEGTL1
              BRANCH    OVRCD,KAETC450          * invalid link
            ELSE 
              MOVE      SP2,AETLCODE            * clear link code
            ENDIF
          ELSE
            MOVE      SP2,AETLCODE            * clear link code
          ENDIF
          MOVE      ZERO,EXIT               * set as valid code
          GOTO      KAETC800
.
.         invalid code
.
KAETC400  DISPLAY   *P1:24,*B,"Invalid Treatment Code.  ",*EL;
          CALL      HITENTER
          GOTO      KYAAEGTC
.
KAETC450  DISPLAY   *P1:24,*B,"Invalid Sub-Analysis Code.  ",*EL;
          CALL      HITENTER
          GOTO      KYAAEGTC
.
.         nothing entered
.
KAETC790  MOVE      ONE,EXIT                * set exit flag
          MOVE      SP4,AETCCODE
          MOVE      SP2,AETLCODE
          COMPARE   ZERO,CKYIMAND
          GOTO      KAETC800 IF EQUAL       * not mandatory
.
          DISPLAY   *P1:24,*B,"Code must be entered.  ",*EL;
          CALL      HITENTER
          GOTO      KYAAEGTC
.
.         valid entry
.
KAETC800  DISPLAY   *PECOL:EVERT,*V2LON,AETCCODE
          GOTO      KAETC999 
.
.         exitchar entered
.
KAETC820  MOVE      TWO,EXIT                * EXITCHAR entered
.
KAETC999  RETURN
+
