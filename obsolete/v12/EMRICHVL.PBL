.******************************************************************************
.*                                  EMRICHVL                                  *
.* Validate EMRACAFD effective from and to dates for overlaps                 *
.*                                                                            *
.* Requires:   KEY38 populated with -                                         *
.*                      - ECDG Subcode
.*                      - Service Date (defaults to current date)             *
.*             VALFDATE - Effictive date from date                            *
.*             VALTDATE - Effictive date to date                              *
.*                                                                            *
.* Returns:    EXIT  0 = Valid effective from and to dates                    *
.*                   1 = Invalid effective from and to dates                  *
.*             ENDKEY38 = Key to record that requires an auto end date update *
.*                                                                            *
.* Modifications:                                                             *
.* V11.01.00  16/08/2021  DA Sarkies      TSK 0905305                         *
.*            Created include                                                 *
.******************************************************************************
.
RIVL0000  MOVE      SP70,ENDKEY38           * Clear auto end key
          MOVE      KEY38,SAVKEY30          * Save ECDG Subcategory
.
          CALL      RDEMACA1                * Record on file so must be an
          BRANCH    OVRCD,RIVL5000          * update
.
          MATCH     VALTDATE,EMAATDAT       * No change in effective from and
          GOTO      RIVL8000 IF EQUAL       * to dates so exit
.
RIVL5000  CALL      RSEMACA1                * Check for the same ECDG sub cat
          CALL      RPEMACA1                * with an earlier effective from
          BRANCH    OVRCD,RIVL6000          * date
.
          MATCH     SAVKEY30,EMAACODE       * Same ECDG Subcategory
          GOTO      RIVL6000 IF NOT EQUAL
.
          MATCH     SP70,EMAATDAT           * No effective to date prev  record
          IF        @EQUAL
            PACK      ENDKEY38,EMAACODE,EMAAFDAT,SP70 * auto end date record key
            GOTO      RIVL8000 
          ENDIF
.
          MATCH     VALFDATE,EMAATDAT
          IF        !@LESS
            GOTO      RIVL9000              * Effective from date overlap
          ENDIF
.
          MATCH     SP70,VALTDATE           * Blank effective to date
          GOTO      RIVL6000 IF EQUAL
.
          MATCH     VALTDATE,EMAATDAT
          IF        !@LESS
            GOTO      RIVL9000              * Effective to date overlap
          ENDIF
.
RIVL6000  CALL      RSEMACA1                * Check for the same ECDG sub cat
          CALL      RKEMACA1                * with an earlier effective from
          BRANCH    OVRCD,RIVL8000          * date
.
          MATCH     SAVKEY30,EMAACODE       * Same ECDG Subcategory
          GOTO      RIVL8000 IF NOT EQUAL
.
          MATCH     SP70,VALTDATE           * Blank effective to date
          GOTO      RAVL9000 IF EQUAL
.
          MATCH     EMAAFDAT,VALFDATE
          IF        !@LESS
            GOTO      RIVL9000              * Effective from date overlap
          ENDIF
.
          MATCH     EMAATDAT,VALTDATE
          IF        !@LESS
            GOTO      RIVL9000              * Effective to date overlap
          ENDIF
.
RIVL8000  MOVE      ZERO,EXIT               * OK
          GOTO      RIVL9999
.
RIVL9000  MOVE      SP70,ENDKEY38
          MOVE      ONE,EXIT                * Error end date overlap
          GOTO      RIVL9999
.
RIVL9999  RETURN

.******************************************************************************
.
.* Validate EMRACCFD effective from and to dates for overlaps                 *
.
.******************************************************************************

RRVL0000  MOVE      SP70,ENDKEY48           * Clear auto end key
          MOVE      KEY48,SAVKEY40          * Save ECDG Subcategory
.
          CALL      RDEMACC1                * Record on file so must be an
          BRANCH    OVRCD,RRVL5000          * update
.
          MATCH     VALTDATE,EMACTDAT       * No change in effective from and
          GOTO      RRVL8000 IF EQUAL       * to dates so exit
.
RRVL5000  CALL      RSEMACC1                * Check for the same ECDG sub cat
          CALL      RPEMACC1                * with an earlier effective from
          BRANCH    OVRCD,RRVL6000          * date
.
          PACK      EMACTEMP,EMACCODE,EMACDMIN,SP70
          MATCH     SAVKEY40,EMACTEMP       * Same ECDG Subcategory
          GOTO      RRVL6000 IF NOT EQUAL
.
          MATCH     SP70,EMACTDAT           * No effective to date prev  record
          IF        @EQUAL
            PACK      ENDKEY48,EMACCODE,EMACFDAT,SP70 * auto end date record key
            GOTO      RRVL8000 
          ENDIF
.
          MATCH     VALFDATE,EMACTDAT
          IF        !@LESS
            GOTO      RRVL9000              * Effective from date overlap
          ENDIF
.
          MATCH     SP70,VALTDATE           * Blank effective to date
          GOTO      RRVL6000 IF EQUAL
.
          MATCH     VALTDATE,EMACTDAT
          IF        !@LESS
            GOTO      RRVL9000              * Effective to date overlap
          ENDIF
.
RRVL6000  CALL      RSEMACC1                * Check for the same ECDG sub cat
          CALL      RKEMACC1                * with an earlier effective from
          BRANCH    OVRCD,RRVL8000          * date
.
          MATCH     SAVKEY30,EMACCODE       * Same ECDG Subcategory
          GOTO      RRVL8000 IF NOT EQUAL
.
          MATCH     SP70,VALTDATE           * Blank effective to date
          GOTO      RRVL9000 IF EQUAL
.
          MATCH     EMACFDAT,VALFDATE
          IF        !@LESS
            GOTO      RRVL9000              * Effective from date overlap
          ENDIF
.
          MATCH     EMACTDAT,VALTDATE
          IF        !@LESS
            GOTO      RRVL9000              * Effective to date overlap
          ENDIF
.
RRVL8000  MOVE      ZERO,EXIT               * OK
          GOTO      RRVL9999
.
RRVL9000  MOVE      SP70,ENDKEY48
          MOVE      ONE,EXIT                * Error end date overlap
          GOTO      RRVL9999
.
RRVL9999  RETURN
