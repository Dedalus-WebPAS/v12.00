.***************************************************************************
.* System    :   Patient blank Medicare Numbers                            *
.* Program   :   F93PTMED.PBL                                              *
.* Desc      :   Fix program - Blank out Medicare Reference Number and     *
.*           :                 Valid to variables if Medicare Number is    *
.*           :                 blank.                                      *
.***************************************************************************
.* Date      :   30/11/2004                                                *
.* Author    :   Peter Vela                                                *
.* Function  :   This program assigns spaces to Medicare Reference         *
.*               (patmx1af.PTMXMCCD) and Valid to (pmspx2af.PMPXMEDC) if    *
.*               there is no Medicare Number (patma1af.PMEDI)              *
.*                                                                         * 
.*               NOTE: This program does not save the original patmx1af    *
.*                     file. This file should be backed-up before running  *
.*                     this program.                                       *
.*                     This program does not save the original pmspx2af    *
.*                     file. This file should be backed-up before running  *
.*                     this program.                                       *
.*                                                                         *
.* Modifications:                                                          *
.*     V9.04.00  30/11/2004  Peter Vela      CAR 55092                     *
.*               Created fix program.                                      *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
RECNUM    FORM      8
.
PRGID     INIT      "F93PTMED"                    
PRGNAM    INIT      "Medicare Numbers - Validate Medicare Reference and Valid to fields."
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  MOVE      ZERO,RECNUM
.
          CALL      DISPHEAD                  * display heading
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSPX2A1,"pmspx2af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P3:4,*EF,"This program assigns spaces to the Medicare ":
                            "Reference (patmx1af.PTMXMCCD) and ":
                    *P3:5,"Valid to (pmspx2af.PMPXMEDC) if there is no Medicare Number":
                            " (patma1af.PMEDI)."
.
          DISPLAY   *P5:9,*EF,*V2LON,"Warning: ",*HOFF:
                    *P14:9,"This program does not save the current versions":
          *P14:10,"of patmx1af and pmspx2af. Please Back-Up these files before":
                    *P14:11,"running this fix program!": 
                    *P5:13,*V2LON,ZERO,*HOFF,". Exit":
                    *P5:14,*V2LON,ONE,*HOFF,". Continue"
.
OPTN0500  KEYIN     *P5:16,*EF,"Select Option : ",*DV,UNDLN1:
                    *P21:16,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P3:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P3:20,"Record : "
.
          PACK      KEY8,SP70               * Process all patma1af records
          CALL      RDSMAST1
PROC0100  CALL      RDKMAST1
          BRANCH    OVRCD,PROC9000
          ADD       ONE,RECNUM
.
          IF        (RECNUM%500) = 0
            DISPLAY   *P18:20,*V2LON,RECNUM
          ENDIF
.
          PACK      KEY8,PURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,PROC0100 
.
          MATCH     SP70,PMEDI
          GOTO      PROC1000 IF EQUAL
          GOTO      PROC1000 IF EOS
.
          GOTO      PROC0100
.
PROC1000  MATCH     SP70,PTMXMCCD
          GOTO      PROC2000 IF EQUAL
          GOTO      PROC2000 IF EOS
          MOVE      SP70,PTMXMCCD
          CALL      UPPTMX1                 * Update master extension file
.
PROC2000  MATCH     SP70,PMPXMEDC
          GOTO      PROC3000 IF EQUAL
          GOTO      PROC3000 IF EOS
          MOVE      SP70,PMPXMEDC           * Update pmi extension file
          CALL      UPPMPX21
.
PROC3000  GOTO      PROC0100                * Fetch next patma1af record
.
PROC9000  CLOSE     PATMA1A1
          CLOSE     PATMX1A1
          CLOSE     PMSPX2A1
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
.******************************************************************************
. Update procedure for Patient Master Extension file
.******************************************************************************
UPPTMX1   MOVE      ZERO,OVRCD
          CLOCK     FULLDATE,CPDATE
          UNPACK    CPDATE,CMON,CDAY,CCENT,CYEAR
          PACK      CPDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CPDATE
          CLOCK     TIME,KEY12
          REP       " 0",KEY12
          PACK      PTMXCTIM,CPDATE,KEY12
          UPDATE    PATMX1A1;PURNO,PTMXRDAT,PTMXMEAL,PTMXLGA,PTMXLS:
                             PTMXLSDT,PTMXFHMD,PTMXMHU1,PTMXMHU2,PTMXMHU3:
                             PTMXECNM,PTMXECA1,PTMXECA2,PTMXECA3,PTMXECA4:
                             PTMXECPC,PTMXECPP,PTMXECBP,PTMXECRE:
                             PTMXNRNM,PTMXNRA1,PTMXNRA2,PTMXNRA3,PTMXNRA4:
                             PTMXNRPC,PTMXNRPP,PTMXNRBP,PTMXNRRE:
                             PTMX3BEX,PTMXCARD,PTMXCEXP,PTMXCXMP,PTMXFMLY:
                             PTMXCHNM,PTMXDOFR,PTMXREGP,PTMXGPPC,PTMXSPID:
                             PTMXGPPT,PTMXOSDT,PTMXOPER,PTMXUKDO,PTMXCELL:
                             PTMXADD1,PTMXADD2,PTMXADD3,PTMXADD4,PTMXPOST:
                             PTMXMCCD,PTMXSIN1,PTMXSIN2,PTMXSIN3,PTMXSIN4:
                             PTMXSIN5,PTMXCTIM,PTMXSLAC,PTMXFILN,PTMXSPAR
          RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC
