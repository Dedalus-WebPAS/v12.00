.******************************************************************************
.* System         : Patient Management                                        *
.*                                                                            *
.* Include Name   : PATBEDDS.PBL                                              *
.*                                                                            *
.* Function       : Display all available beds for the current ward.          *
.*                                                                            *
.* Author         : Justin Coates                                             *
.*                                                                            *
.* Date Written   : 18/10/1991                                                *
.*                                                                            *
.* Index used     : PATWR1A1,"patwr1af"                                       *
.*                                                                            *
.* Parameters     : KEY3          the current ward                            *
.*                                                                            *
.* Modifications  :                                                           *
.******************************************************************************
.
PATBEDDS  MOVE      FIVE,CVERT              * line counter
          MOVE      ONE,CCOL                * start on LHS of screen
          PACK      KEY6,KEY3,SP3           * key of ward
          CALL      RDSWARD1                * positional read
.
          DISPLAY   *P1:3,*EF,*V2LON,*P20:4,*ULON:
                    "AVAILABLE BEDS FOR WARD ",KEY3;
.
. -------------------------------------
. ------- next read on item file ------
. -------------------------------------
.
BEDDS100  CALL      RDKWARD1  
          BRANCH    OVRCD,BEDDS800          * end of file
.
          MATCH     WWARD,KEY3
          GOTO      BEDDS800 IF NOT EQUAL   * different ward
.
. ----------------------------------
. ------- increment counters -------
. ----------------------------------
.
BEDDS200  ADD       ONE,CVERT               * inc line counter
.
          COMPARE   "23",CVERT
          GOTO      BEDDS300 IF LESS        * will fit on the page
.
          ADD       "40",CCOL               * inc column counter
          MOVE      SIX,CVERT               * reset row position
.
          COMPARE   "80",CCOL  
          GOTO      BEDDS400 IF NOT LESS    * need a new page
.
.
. -------------------------------------------
. ------- display all the information -------
. -------------------------------------------
.
BEDDS300  DISPLAY   *PCCOL:CVERT,*V2LON,WBED,*HOFF,SP5,WBDESC
          GOTO      BEDDS100
.
. -------------------------------
. ------- new page needed -------
. -------------------------------
.
BEDDS400  DISPLAY   *P1:24,"( )ontinue, ( )irst, ( )ext ? ",*EL:
                    *V2LON,*P2:24,ANSC,*P14:24,ANSF,*P23:24,ANSN
.
          KEYIN     *P31:24,*DV,UNDLN1:
                    *P31:24,*V2LON,ANS
.
          REP       UPPLOW,ANS
.
          MATCH     ANSC,ANS
          GOTO      BEDDS999 IF EQUAL       * Continue selected
.
          MATCH     ANSF,ANS
          GOTO      PATBEDDS IF EQUAL       * First selected
.
. --------------------------------------
. ------- (N)ext screen selected -------
. --------------------------------------
.
BEDDS700  DISPLAY   *P1:6,*EF;
          MOVE      SIX,CVERT               * first display row 
          MOVE      ONE,CCOL                * get back to LHS of screen
          GOTO      BEDDS300                * display the data
.
BEDDS800  DISPLAY   *P1:24,*EL
          CALL      HITENTER
.
BEDDS999  RETURN
+
