.***************************************************************************
.* System    :   PMI Registration                                          *
.* Program   :   F98PMIX1                                                  *
.* Desc      :   Fix program to update web user id PMPXWBID(pmspx2af)      *
.*                                                                         *
.***************************************************************************
.* Date      :   12/02/2007                                                *
.* Author    :   Janet George                                              *
.* Function  :   This program will update the web user id PMPXWBID if it is*
.*               not populated by reading websecaf with the passcode       *
.*               ptmxoper                                                  *
.* Mods      :                                                             *
.* V9.08.00      12.02.2007  Janet George  CAR 114378                      *
.*               Updates the web user id PMPXWBID                          *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATMA1FD/INC                 * Patient Master File       
          INC       PMSPX2FD/INC                 * PMI Extension Table       
          INC       WEBSECFD/INC                 * User Security Table 
.
. LOCAL VARIABLE DEFINITION
. -------------------------
REDRCCNT  FORM      6                       * Read record counter
WRTRCCNT  FORM      6                       * Write record counter
.
PRGID     INIT      "F98PMIX1"
PRGNAM    INIT      "Web User Id Update"
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,"Opening pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      WEBSECA3,"websecaf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,REDRCCNT           * Reset the read record counter
          MOVE      ZERO,WRTRCCNT           * Reset the write record counter
          DISPLAY   *P1:24,*EL,"Scanning : ",*P35:24,"Writing : ";
.
          PACK      KEY8,SP70
          CALL      RDSMAST1                * Position on & read a master file
PROC1000  CALL      RDKMAST1                  record
          BRANCH    OVRCD,PROC9500      
.
          ADD       ONE,REDRCCNT            * Increment the read record counter
          IF        REDRCCNT%10=1
            DISPLAY   *P12:24,*V2LON,PURNO;
          ENDIF
.
          PACK      KEY8,PURNO,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,PROC1000
.
          MATCH     SP70,PMPXWBID           * Skip if web user id populated
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     SP70,PTMXOPER           * Skip if passcode not populated
          GOTO      PROC1000 IF EQUAL
.
          PACK      KEY14,PTMXOPER,SP70
          CALL      RSWBSE3
          CALL      RKWBSE3 
          BRANCH    OVRCD,PROC1000
.   
          MATCH     PTMXOPER,WBSEPCD
          GOTO      PROC1000 IF NOT EQUAL
.
PROC8000  MOVE      WBSEUID,PMPXWBID 
.
          CALL      UPPMPX21
.
          ADD       ONE,WRTRCCNT            * Increment write record counter
          IF        WRTRCCNT%10=1
            DISPLAY   *P45:24,*V2LON,PURNO;
          ENDIF
.
PROC9000  GOTO      PROC1000
. 
PROC9500  DISPLAY   *P1:23,*EF,*V2LON,REDRCCNT,*HOFF," records read.  ":
                               *V2LON,WRTRCCNT,*HOFF," records updated.  ":
                    *P1:24,*B;
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATMA1IO/INC                 * Patient Master File       
          INC       PMSPX2IO/INC                 * PMI Extension Table
          INC       WEBSECIO/INC                 * User Security Table
