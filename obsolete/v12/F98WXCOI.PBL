.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   F98WXCOI                                                  *
.* Desc      :   Fix to copy workers comp Cause of injury to acccmtaf      *
.***************************************************************************
.* Date      :   21/09/2007                                                *
.* Author    :   Jill Habasque                                             *
.* Function  :   This program will copy workers comp description of injury *    
.*               to acccmtaf                                               *
.***************************************************************************
.
          INC       STD001FD
          INC       PATWC1FD/INC
          INC       PMSWX1FD/INC
          INC       ACCCMTFD/INC
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      8
.
PRGID     INIT      "F98WXCOI"
PRGNAM    INIT      "Fix to copy WC Cause of Injury to acccmtaf"
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PMSWX1A1,"pmswx1af"
          OPEN      PATWC1A1,"patwc1af"
          OPEN      ACCCMTA1,"acccmtaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,COUNTER                 * initialise counter
.
          PACK      KEY8,SP70
          CALL      RSPMWX11
PROC1000  CALL      RKPMWX11
          BRANCH    OVRCD,PROC9000
.
          PACK      KEY8,PMWXVISN,SP70
          CALL      RDWCOM1
          BRANCH    OVRCD,PROC1000
.
          ADD       ONE,COUNTER
          IF        (COUNTER%100) = 0
            DISPLAY   *P13:24,*V2LON,PMWXVISN;
          ENDIF
.
          MATCH     SP70,PMWXALOC
          GOTO      PROC1000 IF EQUAL
.
          PACK      ACCMTYPE,ZERO,ZERO,ONE
          PACK      KEY26,WCCLMNO,ACCMTYPE,SP1,SP1,ONE,SP70
          CALL      RDACCMT1
          IF        OVRCD<>1
            GOTO      PROC1000
          ENDIF
.
          PACK      ACCMACCN,WCCLMNO,SP70
          PACK      ACCMTYPE,ZERO,ZERO,ONE
          MOVE      ONE,ACCMLINE
          PACK      ACCMDATA,PMWXALOC,SP70
          CALL      WRACCMT1
.
          GOTO      PROC1000
.
PROC9000  CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,"Finished processing ",CTIMEIS;
          CALL      HITENTER
. 
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PMSWX1IO/INC
          INC       PATWC1IO/INC
          INC       ACCCMTIO/INC
