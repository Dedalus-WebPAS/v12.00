.***************************************************************************
.* System    :   CIAS                                                      *
.* Program   :   IBACCC65                                                  *
.* Desc      :   CIAS extract trimming                                     *
.***************************************************************************
.* Date      :   19/02/2004                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program contains the standard CIAS extract           *
.*               trimming processes                                        *
.***************************************************************************
.* Mods      :   V9.03.00 19/02/2004 Ebon Clements         CAR 45487       *
.*                        Created program from IBACEA65                    *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       CCSCEAFD/INC
          INC       CCSCEBFD/INC
          INC       CCSCECFD/INC
          INC       CCSCEDFD/INC
          INC       CCSCEEFD/INC
          INC       CCSCEFFD/INC
          INC       CCSCEGFD/INC
          INC       CCSEPSFD/INC
          INC       CCSSTADF
          INC       CCSSTYFD/INC
          INC       CCSXHDFD/INC
          INC       CEASPCFD/INC
.
CCSTMPA1  IFILE    SQL, FIXED=29, KEY="U1-10,11-12,13-28"
.
CCTMKEY   DIM       10     10       1     * Sequence
CCTMHCD   DIM        2      2       11    * Casemix Code
CCTMEPS   DIM       16     16       13    * Casemix Code
.
.End of Record                      29
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
COMMAND   DIM       80
COSTFLAG  FORM      1
F7P2      FORM      7.2
F12P4     FORM      12.4
FILENAME  DIM       8
LQCNT     FORM      12.2
LQTRAMT   FORM      12.2
LQTRMAX   FORM      12.2
LQTRMIN   FORM      12.2
TRIMNUMB  FORM      5
TRIMLW    FORM      7.2
TRIMUP    FORM      7.2
TOTALEPS  FORM      6
TOTALEXT  FORM      6
UQCNT     FORM      12.2
UQTRAMT   FORM      12.2
UQTRMAX   FORM      12.2
UQTRMIN   FORM      12.2
TRIMLIM   DIM       1
TRIMLEVL  FORM      1
TRIMPERC  FORM      2.2
TRIMBASE  FORM      1
TRIMDESC  DIM      20
CALCPERC  FORM      1
TLDESC    DIM      25
SGENISB   INIT      "isbuild "
STDGISE   INIT      "iserase "
.
ACCEPT    FORM      1
SACCEPT   FORM      1
.
PRGID     INIT      "IBACCC65"
PRGNAM    INIT      "CIAS Extract Trimming"
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000
.
ML1000    CALL      TEXT0000               * Trimm Extract
          BRANCH    EXIT,ML0100
          GOTO      ML9999
.
ML9999    CHAIN     PGM                    * chain back to program
.
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                     * display heading
          MOVE      ONE,CCENTRY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          MOVE      "   64",AUDTSECT
          MOVE      " 37",AUDTPOST
          READ      CONTROLF,AUDTSECT;*AUDTPOST,AUDTFLAG
          IF        AUDTFLAG=1
            DISPLAY   *P56:24,"Opening ccsaudep";
            OPEN      CCSAUDEP,"ccsaudep"
          ENDIF
          DISPLAY   *P56:24,"Opening ccsceaaf";
          OPEN      CCSCEAA1,"ccsceaaf"
          DISPLAY   *P56:24,"Opening ccsepsaf";
          OPEN      CCSEPSA1,"ccsepsaf"
          DISPLAY   *P56:24,"Opening ccsepsaf";
          OPEN      CCSEPSA2,"ccsepsaf"
          DISPLAY   *P56:24,"Opening ccsepsaf";
          OPEN      CCSEPSA3,"ccsepsaf"
          DISPLAY   *P56:24,"Opening ccsepsaf";
          OPEN      CCSEPSA4,"ccsepsaf"
          DISPLAY   *P56:24,"Opening ccsepsaf";
          OPEN      CCSEPSA5,"ccsepsaf"
          DISPLAY   *P56:24,"Opening ccsstyaf";
          OPEN      CCSSTYA1,"ccsstyaf"
          DISPLAY   *P56:24,"Opening ccsxhdaf";
          OPEN      CCSXHDA1,"ccsxhdaf"
          DISPLAY   *P56:24,"Opening ccsxhdaf";
          OPEN      CCSXHDA2,"ccsxhdaf"
          DISPLAY   *P1:24,*EL;
.
          CALL      CF00L000                     * Clear Fields
.
INIT9999  RETURN
.
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Extract Trimming"
.
OPTN0500  KEYIN     *P1:16,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:16,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               TEXT0000              Called by: ML1000  *
.* Trim Casemix Extract                                                   *
.**************************************************************************
.
TEXT0000  CALL      KEXT0000                     * Keyin extract ID
          BRANCH    EXIT,TEXT9999
.
          CALL      ELEVL000                     * Trim at level
.
          CALL      EFORM000                     * Trim based on
.
          CALL      ETLIM000                     * Trim upper, lower or both
.
          CALL      UQRT0000                     * Use quartile trimming calc   
.
          CALL      EPERC000                     * Trim Percentage
.
          CALL      CONTQST                      * ok to continue
          BRANCH    CEXIT,TEXT5000,TEXT9999,TEXT9999    * 1=Yes, 2=No, 3=Cancel
.
TEXT5000  CALL      POST0000                     * Perform Trim 
.
TEXT9999  RETURN
.
.------------------------------------------------------------
. Keyin extract ID
.------------------------------------------------------------
KEXT0000  CALL      CCSCOPCL         * Close Files
.
          DISPLAY   *P1:4,*EF
          KEYIN     *P1:4,"Extract ID : ",*V2LON,CCEAEID
          ENDSET    CCEAEID
          APPEND    SP70,CCEAEID
          RESET     CCEAEID
.
          MATCH     SP70,CCEAEID
          GOTO      KEXT9000 IF EQUAL
.
          PACK      KEY4,CCEAEID,SP70
          CALL      RDCCEA1
          IF        OVRCD=1
            BEEP
            DISPLAY  *P1:24,"Invalid Extract ID";
            CALL      HITENTER
            GOTO      KEXT0000
          ENDIF
.
          DISPLAY   *P19:4,*V2LON,CCEADES
.
          CALL      CCSCOPOP         * Open Files
.
          MOVE      ZERO,EXIT
          GOTO      KEXT9999
.
KEXT9000  MOVE      ONE,EXIT
          GOTO      KEXT9999
.
KEXT9999  RETURN
.
.------------------------------------------------------------
. Enter Level
.------------------------------------------------------------
ELEVL000  DISPLAY   *P1:5,*EF
          KEYIN     *P1:5,"Trim at Extract Level :",*V2LON,TRIMLEVL
          BRANCH    TRIMLEVL,ELEVL110,ELEVL120,ELEVL130
          GOTO      ELEVL000
.
ELEVL110  MOVE      CCEALV1,KEY3
          CALL      RDCCST1
          GOTO      ELEVL999
.
ELEVL120  MOVE      CCEALV2,KEY3
          CALL      RDCCST1
          MATCH     SP70,CCEALV2
          IF        @EQUAL
            BEEP
            DISPLAY   *P1:24,"Extract Not Valid for this level"
            CALL      HITENTER
            GOTO      ELEVL000
          ENDIF
          GOTO      ELEVL999
.
ELEVL130  MOVE      CCEALV3,KEY3
          CALL      RDCCST1
          MATCH     SP70,CCEALV3
          IF        @EQUAL
            BEEP
            DISPLAY   *P1:24,"Extract Not Valid for this level"
            CALL      HITENTER
            GOTO      ELEVL000
          ENDIF
          GOTO      ELEVL999
.
ELEVL999  RETURN
.
.------------------------------------------------------------
. Enter Format
.------------------------------------------------------------
EFORM000  MOVE      ZERO,COSTFLAG
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      CEASPCA1,"ceaspcaf"
          TRAPCLR   IO
          MOVE      OVRCD,COSTFLAG
          DISPLAY   *P1:24,*EL,"Trim Based on : ":
                    *V2LON,"1",*HOFF,". Length of Stay, ":
                    *V2LON,"2",*HOFF,". Income";
          BRANCH    COSTFLAG,EFORM100
          DISPLAY   ", ",*V2LON,"3",*HOFF,". Cost"
EFORM100  KEYIN     *P1:6,"Trim Based On :",*V2LON,TRIMBASE
          BRANCH    TRIMBASE,EFORM110,EFORM120,EFORM130
EFORM105  BEEP
          GOTO      EFORM100
.
EFORM110  MOVE      "Length of Stay    ",TRIMDESC
          DISPLAY   *P19:6,TRIMDESC
          GOTO      EFORM999
.
EFORM120  MOVE      "Income      ",TRIMDESC
          DISPLAY   *P19:6,TRIMDESC
          GOTO      EFORM999
.
EFORM130  BRANCH    COSTFLAG,EFORM105          * Only If Costing Files
          MOVE      "Cost        ",TRIMDESC
          DISPLAY   *P19:6,TRIMDESC
          GOTO      EFORM999
.
EFORM999  RETURN
.
.--------------------------------------------------------
. Clear Fields for Screen : Options
.-------------------------------------------------------
CF00L000  MOVE      SP70,CCEAEID
          MOVE      SP70,TRIMLIM
          MOVE      ZERO,TRIMLEVL
          MOVE      ZERO,TRIMPERC
          MOVE      ZERO,TRIMBASE
          MOVE      SP70,TRIMDESC
          MOVE      SP70,CCEADES
          MOVE      ZERO,CALCPERC
          MOVE      SP70,TLDESC
          MOVE      SP70,CCSTDES
.
CF00L999  RETURN
.
ETLIM000  MOVE     "B",TRIMLIM
          KEYIN    *P1:7,"Trim for Upper, Lower or Both : ",*V2LON,*RV,TRIMLIM
.
          REP      UPPLOW,TRIMLIM
          MOVE     "Trim Upper Limit Only",TLDESC
          MATCH    "U",TRIMLIM
          GOTO     ETLIM900 IF EQUAL
.
          MOVE     "Trim Lower Limit Only",TLDESC
          MATCH    "L",TRIMLIM
          GOTO     ETLIM900 IF EQUAL
.
          MOVE     "Trim at Both Limits",TLDESC
          MATCH    "B",TRIMLIM
          GOTO     ETLIM900 IF EQUAL
.
          BEEP
          DISPLAY  *P1:24,"Valid Entries = U,L,B"
          CALL     HITENTER
          GOTO     ETLIM000
.
ETLIM900  DISPLAY  *P34:7,TLDESC
          GOTO     ETLIM999

ETLIM999  RETURN
.
.------------------------------------------------------------
. Use quartile trimming  
.------------------------------------------------------------
UQRT0000  DISPLAY   *P1:8,*EF
          KEYIN     *P1:8,*EL,"Use Quartile Trimming Calc ":
                    " (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          IF        @EQUAL
            MOVE      ONE,CALCPERC
            GOTO      UQRT9999
          ENDIF
.
          MATCH     ANSN,ANS
          IF        @EQUAL
            MOVE      ZERO,CALCPERC
            GOTO      UQRT9999
          ENDIF
.
          BEEP
          GOTO      UQRT0000
.
UQRT9999  RETURN
.
.
.------------------------------------------------------------
. Enter Percentage to Trim
.------------------------------------------------------------
EPERC000  MOVE      ZERO,TRIMPERC
          COMPARE   ONE,CALCPERC
          GOTO      EPERC999 IF EQUAL
.
          DISPLAY   *P1:9,*EF
          KEYIN     *P1:9,"Percentage to Trim :",*V2LON,*NOMASK,*RV,TRIMPERC
.
EPERC999  RETURN
.
.------------------------------------------------------------
. Save Trim Details
.------------------------------------------------------------
POST0000  CALL      UNTRIM00      * Untrim Extract Records.
.
          MOVE      "99",CLNO
          MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
.
          CALL      PROC0000      * Process.
.
          CALL      CLEAR000      * Clear Summary Files.
          BRANCH    EXIT,POST9999
.
          CALL      SUMM0000      * Summarize Levels.
.
          CALL      STAT0000      * Update Statistics.
.
          IF        CPAGENO>0
            CALL      UND132
            PRINT     *50,"****  End of Report ****"
          ENDIF
.
POST9999  RETURN
.
.------------------------------------------------------------
. Un Trim Extract ie Turn off Exclusion.
.------------------------------------------------------------
UNTRIM00  PACK      KEY48,SP70
          DISPLAY   *P1:24,*EL,"Clearing : "
          MOVE      ZERO,TOTALEXT
          CALL      RSCCEC1
.
UNTRIM10  CALL      RKCCEC1
          BRANCH    OVRCD,UNTRIM99
          ADD       ONE,TOTALEXT
          IF        (TOTALEXT%100=0)
            DISPLAY   *P20:24,*V2LON,TOTALEXT
          ENDIF
          MOVE      ZERO,CCECEXC
          PACK      KEY18,CCECHCD,CCECEPS
          CALL      RDCCEP1
          IF        OVRCD=0
            MOVE      CCEPEXC,CCECEXC
          ENDIF
          CALL      UPCCEC1
.
          GOTO      UNTRIM10
.
UNTRIM99  RETURN
.
.------------------------------------------------------------
. Process at Specified Trim Level
.------------------------------------------------------------
PROC0000  DISPLAY   *P1:24,*EL,"Processing : "
          MOVE      ZERO,TOTALEXT
          BRANCH    TRIMLEVL,PROC1000,PROC2000,PROC3000
.
PROC1000  PACK      KEY10,SP70
          CALL      RSCCEB1
PROC1100  CALL      RKCCEB1
          BRANCH    OVRCD,PROC9999
          ADD       ONE,TOTALEXT
          IF        (TOTALEXT%100=0)
            DISPLAY   *P23:24,*V2LON,TOTALEXT
          ENDIF
          PACK      KEY30,CCEBLV1,SP70
          CALL      ITEM0000
          GOTO      PROC1100
.
PROC2000  PACK      KEY20,SP70
          CALL      RSCCED1
PROC2100  CALL      RKCCED1
          BRANCH    OVRCD,PROC9999
          ADD       ONE,TOTALEXT
          IF        (TOTALEXT%100=0)
            DISPLAY   *P23:24,*V2LON,TOTALEXT
          ENDIF
          PACK      KEY30,CCEDLV1,CCEDLV2,SP70
          CALL      ITEM0000
          GOTO      PROC2100
.
PROC3000  PACK      KEY20,SP70
          CALL      RSCCEE1
PROC3100  CALL      RKCCEE1
          BRANCH    OVRCD,PROC9999
          ADD       ONE,TOTALEXT
          IF        (TOTALEXT%100=0)
            DISPLAY   *P23:24,*V2LON,TOTALEXT
          ENDIF
          PACK      KEY30,CCEELV1,CCEELV2,CCEELV3,SP70
          CALL      ITEM0000
          GOTO      PROC3100
.
PROC9999  RETURN
.
.------------------------------------------------------------
. Process an Item
.------------------------------------------------------------
ITEM0000  CALL      OPNTMP00
.
          CALL      SORT0000
          BRANCH    EXIT,ITEM1000         * Insufficent Data for Trim
.
          CALL      QUAR0000            * Calculate Quartiles and Trim Points
          BRANCH    EXIT,ITEM1000       * Insufficent Data for Trim
.
          CALL      TRIM0000              * Trim Epsiodes in CEC File
          BRANCH    EXIT,ITEM1000         * NO Episodes Trimmed
.
ITEM1000  CALL      CLSTMP00
.
ITEM9999  RETURN
.
.------------------------------------------------------------
. Clear Summary Files
.------------------------------------------------------------
CLEAR000  DISPLAY  *P1:24,*EL,"Clearing Summary : "
          MOVE     "cceb",KEY4
          PACK     FILENAME,KEY4,CCEAEID
          DISPLAY  *P26:24,*EL,FILENAME
          REP      " 0",FILENAME
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     CCSCEBA1,FILENAME
          TRAPCLR  IO
          IF       OVRCD=0
            CLOSE    CCSCEBA1,DELETE
          ENDIF
          PACK     COMMAND,SGENISB,FILENAME,CCEBIDX
          EXECUTE  COMMAND,TASKID
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     CCSCEBA1,FILENAME
          TRAPCLR  IO
          IF       OVRCD=1
            BEEP
            MOVE     "Unable to Create Casemix Extract Files - ",DISPMSG
            CALL     MESSAGE1
            GOTO     CLEAR900
          ENDIF
.
          MOVE     "cced",KEY4
          PACK     FILENAME,KEY4,CCEAEID
          DISPLAY  *P26:24,*EL,FILENAME
          REP      " 0",FILENAME
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     CCSCEDA1,FILENAME
          TRAPCLR  IO
          IF       OVRCD=0
            CLOSE    CCSCEDA1,DELETE
          ENDIF
          PACK     COMMAND,SGENISB,FILENAME,CCEDIDX
          EXECUTE  COMMAND,TASKID
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     CCSCEDA1,FILENAME
          TRAPCLR  IO
          IF       OVRCD=1
            BEEP
            MOVE     "Unable to Create Casemix Extract Files - ",DISPMSG
            CALL     MESSAGE1
            GOTO     CLEAR900
          ENDIF
.
          MOVE     "ccee",KEY4
          PACK     FILENAME,KEY4,CCEAEID
          DISPLAY  *P26:24,*EL,FILENAME
          REP      " 0",FILENAME
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     CCSCEEA1,FILENAME
          TRAPCLR  IO
          IF       OVRCD=0
            CLOSE    CCSCEEA1,DELETE
          ENDIF
          PACK     COMMAND,SGENISB,FILENAME,CCEEIDX
          EXECUTE  COMMAND,TASKID
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     CCSCEEA1,FILENAME
          TRAPCLR  IO
          IF       OVRCD=1
            BEEP
            MOVE     "Unable to Create Casemix Extract Files - ",DISPMSG
            CALL     MESSAGE1
            GOTO     CLEAR900
          ENDIF
          MOVE     ZERO,EXIT
          GOTO     CLEAR999
CLEAR900  MOVE     ONE,EXIT
CLEAR999  RETURN
.
.------------------------------------------------------------
. Close and Delete Temp Sort File
.------------------------------------------------------------
CLSTMP00  DISPLAY  *P1:10;
          CLOSE    CCSTMPA1,DELETE
CLSTMP99  RETURN
.
.------------------------------------------------------------
. Create Sort File
.------------------------------------------------------------
OPNTMP00  DISPLAY  *P1:10;
          CLEAR    FILENAME
          APPEND   "cia65a",FILENAME
          APPEND   PORT,FILENAME
          RESET    FILENAME
          REP      " 0",FILENAME
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     CCSTMPA1,FILENAME
          TRAPCLR  IO
          IF       OVRCD=0
            CLOSE    CCSTMPA1,DELETE
          ENDIF
.
          CLEAR    COMMAND
          APPEND   "isbuild ",COMMAND
          APPEND   FILENAME,COMMAND
          APPEND   " 29 UC1-10,11-12,13-28",COMMAND
          RESET    COMMAND
          EXECUTE  COMMAND,TASKID
          OPEN     CCSTMPA1,FILENAME
.
OPNTMP99  RETURN
.
.------------------------------------------------------------
. Calculate Quartile Positions from Top and Bottom of File
.---------------------------------------------------------
QUAR0000  COMPARE   ZERO,CALCPERC
          GOTO      QUAR8000 IF EQUAL
.
          ASSIGN    ((TOTALEPS)/4.0)+0.75,LQCNT
          ASSIGN    TOTALEPS-LQCNT+1.00,UQCNT
.
. Calculate Lower Quartile.
.---------------------------
          PACK      KEY28,SP70
          MOVE      ZERO,SECTOR
          CALL      RSCCTM1
QUAR1000  CALL      RKCCTM1
          BRANCH    OVRCD,QUAR9500   * Not Enough Data
          ADD       ONE,SECTOR
          IF        (SECTOR=LQCNT)
            SQUEEZE   CCTMKEY
            MOVE      CCTMKEY,LQTRAMT  * Quartile in exact position
            GOTO      QUAR1900
          ENDIF
          IF        (SECTOR>LQCNT)
            SQUEEZE   CCTMKEY
            MOVE      CCTMKEY,LQTRMAX  * Interpolated
            ASSIGN    LQTRMIN+((LQTRMAX-LQTRMIN)*(LQCNT-SECTOR+1)),LQTRAMT
            GOTO      QUAR1900
          ENDIF
          SQUEEZE   CCTMKEY
          MOVE      CCTMKEY,LQTRMIN
          GOTO      QUAR1000
QUAR1900
.
. Calculate Upper Quartile.
.---------------------------
          PACK      KEY28,Z70
          MOVE      TOTALEPS,SECTOR
          ADD       ONE,SECTOR
          CALL      RSCCTM1
QUAR2000  CALL      RPCCTM1
          BRANCH    OVRCD,QUAR9500   * Not Enough Data
          SUB       ONE,SECTOR
          IF        (SECTOR=UQCNT)
            SQUEEZE   CCTMKEY
            MOVE      CCTMKEY,UQTRAMT  * Quartile in exact position
            GOTO      QUAR2900
          ENDIF
          IF        (SECTOR<UQCNT)
            SQUEEZE   CCTMKEY
            MOVE      CCTMKEY,UQTRMIN  * Interpolated
            ASSIGN    UQTRMIN+((UQTRMAX-UQTRMIN)*(UQCNT-SECTOR)),UQTRAMT
            GOTO      QUAR2900
          ENDIF
          SQUEEZE   CCTMKEY
          MOVE      CCTMKEY,UQTRMAX
          GOTO      QUAR2000
.-------------------------------------------------------------------------
. Calculate Trim Points
.-----------------------
.
. Refer "DRGs 1991-92" Dept of Health, Vic. Nov 1992. page 60.
.
. Upper Trim Point TRIMUP = 2.5*UQTRAMT - 1.5*LQTRAMT
. Lower Trim Point TRIMLW = 2.5*LQTRAMT - 1.5*UQTRAMT
.
. LQTRAMT = Lower Quartile Amount
. UQTRAMT = Upper Quartile Amount
.-------------------------------------------------------------------------
QUAR2900  ASSIGN    (2.5*UQTRAMT)-(1.5*LQTRAMT),TRIMUP
          ASSIGN    (2.5*LQTRAMT)-(1.5*UQTRAMT),TRIMLW
          MOVE      ZERO,TRIMNUMB
          GOTO      QUAR9000
.
. Percenatge to Trim
.--------------------------------------
QUAR8000  ASSIGN    ((TOTALEPS*TRIMPERC)/100.0)/2.0,TRIMNUMB
          IF        TRIMNUMB<1
            GOTO       QUAR9500
          ENDIF
          MOVE      ZERO,TRIMUP
          MOVE      ZERO,TRIMLW
.
QUAR9000  CALL      PTRIM000
          MOVE      ZERO,EXIT
.
          GOTO      QUAR9999
QUAR9500  MOVE      ONE,EXIT
QUAR9999  RETURN
.
.------------------------------------------------------------
. Print Header Record for Trimming
.------------------------------------------------------------
PTRIM000  ADD       ONE,CLNO
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
          IF        CALCPERC=0
            PRINT     "| ",KEY30,SP4,TRIMNUMB,*63,"|",*132,"|"
          ELSE
            PRINT     "| ",KEY30,SP4,TRIMLW,SP4,TRIMUP,*63,"|",*132,"|"
          ENDIF
PTRIM999  RETURN
.
.------------------------------------------------------------
. Page Heading
.------------------------------------------------------------
HEAD0000  CALL      HEAD132
          PRINT     "Casemix Extract         : ",CCEAEID,SP1,CCEADES:
                    *80,"Trim Limits             : ",TLDESC
          PRINT     "Trim at Extract Level   : ",TRIMLEVL,SP4,CCSTDES:
                    *80,"Trim Based On           : ",TRIMDESC
          MOVE      NO,KEY3
          LOAD      KEY3,CALCPERC,YES
          PRINT     "Percentage to Trim      : ",TRIMPERC:
                    *80,"Use Quartile Calc.      : ",KEY3
          CALL      UND132
          IF        CALCPERC=0
            PRINT     "| Extract Key                   Number to Trim":
                      "                | Episode             L.O.S ":
                      "             Income           Excluded",*132,"|"
          ELSE
            PRINT     "| Extract Key                 Trim Point Lower":
                      "         Upper  | Episode             L.O.S ":
                      "             Income           Excluded",*132,"|"
          ENDIF
          CALL      UND132
HEAD9999  RETURN
.
.------------------------------------------------------------
. Sort Episodes into Temp file by Trim Basis
.------------------------------------------------------------
SORT0000  MOVE      ZERO,TOTALEPS
          PACK      KEY48,KEY30,SP70
          CALL      RSCCEC2
SORT1000  CALL      RKCCEC2
          BRANCH    OVRCD,SORT2000
          BRANCH    TRIMLEVL,SORT1010,SORT1020,SORT1030
.
SORT1010  PACK      KEY48,CCECLV1,SP70
          GOTO      SORT1090
.
SORT1020  PACK      KEY48,CCECLV1,CCECLV2,SP70
          GOTO      SORT1090
.
SORT1030  PACK      KEY48,CCECLV1,CCECLV2,CCECLV3,SP70
.
SORT1090  MATCH     KEY30,KEY48
          GOTO      SORT2000 IF NOT EQUAL
.
          BRANCH    TRIMBASE,SORT1100,SORT1200,SORT1300
.
SORT1100  MOVE      CCECLOS,F7P2
          PACK      KEY10,F7P2
          GOTO      SORT1900
.
SORT1200  ASSIGN    CCECFTC,F7P2
          PACK      KEY10,F7P2
          GOTO      SORT1900
.
SORT1300  ASSIGN    CCECTTC,F7P2
          PACK      KEY10,F7P2
          GOTO      SORT1900
.
SORT1900  PACK      KEY28,KEY10,CCECHCD,CCECEPS
          UNPACK    KEY28,CCTMKEY,CCTMHCD,CCTMEPS
          CALL      RDCCTM1
          IF        OVRCD=1
            CALL      WRCCTM1
            ADD       ONE,TOTALEPS
          ENDIF
          GOTO      SORT1000
.
SORT2000  IF        TOTALEPS<5
            GOTO      SORT9500
          ENDIF
SORT9000  MOVE      ZERO,EXIT
          GOTO      SORT9999
SORT9500  MOVE      ONE,EXIT
SORT9999  RETURN
.
.------------------------------------------------------------
. Calculate Statistics
.------------------------------------------------------------
STAT0000  PACK      KEY10,SP70
          DISPLAY   *P1:24,*EL,"Updating Statistics : "
          MOVE      ZERO,TOTALEXT
.
          CALL      STEB0000  * Level 1 Variance Calculation
          CALL      STED0000  * Level 2 Variance Calculation
          CALL      STEE0000  * Level 3 Variance Calculation
.
STAT9999  RETURN
.
.------------------------------------------------------------
. Update Variance Casemix Level 1
.------------------------------------------------------------
STEB0000  PACK      KEY10,SP70
          CALL      RSCCEB1
STEB1000  CALL      RKCCEB1
          BRANCH    OVRCD,STEB9999
.
          ADD       ONE,TOTALEXT
          IF        (TOTALEXT%100=0)
            DISPLAY   *P23:24,*V2LON,TOTALEXT
          ENDIF
.
          MOVE      ZERO,CCEBFSV
          MOVE      ZERO,CCEBTSV
          MOVE      ZERO,CCEBLSV
.
          PACK      KEY48,CCEBLV1,SP70
          CALL      RSCCEC2
STEB1100  CALL      RKCCEC2
          BRANCH    OVRCD,STEB1900
          MATCH     CCEBLV1,CCECLV1
          GOTO      STEB1900 IF NOT EQUAL
.
          MATCH     "1",CCECEXC         * Excluded from Summary
          GOTO      STEB1100 IF EQUAL
.
          ASSIGN    CCECFTC-(CCEBFTC/CCEBNEP),F12P4
          ASSIGN    CCEBFSV+(F12P4*F12P4),CCEBFSV
.
          ASSIGN    CCECTTC-(CCEBTTC/CCEBNEP),F12P4
          ASSIGN    CCEBTSV+(F12P4*F12P4),CCEBTSV
.
          ASSIGN    CCECLOS-(CCEBLTO/CCEBNEP),F12P4
          ASSIGN    CCEBLSV+(F12P4*F12P4),CCEBLSV
.
          GOTO      STEB1100
.
STEB1900  IF        CCEBNEP>1
            ASSIGN    CCEBFSV/(CCEBNEP-1),CCEBFSV
            ASSIGN    CCEBTSV/(CCEBNEP-1),CCEBTSV
            ASSIGN    CCEBLSV/(CCEBNEP-1),CCEBLSV
          ENDIF
          CALL      UPCCEB1
          GOTO      STEB1000
.
STEB9999  RETURN
.
.------------------------------------------------------------
. Update Variance Casemix Level 2
.------------------------------------------------------------
STED0000  PACK      KEY20,SP70
          CALL      RSCCED1
STED1000  CALL      RKCCED1
          BRANCH    OVRCD,STED9999
.
          ADD       ONE,TOTALEXT
          IF        (TOTALEXT%100=0)
            DISPLAY   *P23:24,*V2LON,TOTALEXT
          ENDIF
.
          MOVE      ZERO,CCEDFSV
          MOVE      ZERO,CCEDTSV
          MOVE      ZERO,CCEDLSV
.
          PACK      KEY48,CCEDLV1,CCEDLV2,SP70
          PACK      KEY20,CCEDLV1,CCEDLV2,SP70
          CALL      RSCCEC2
STED1100  CALL      RKCCEC2
          BRANCH    OVRCD,STED1900
          PACK      KEY48,CCECLV1,CCECLV2,SP70
          MATCH     KEY20,KEY48
          GOTO      STED1900 IF NOT EQUAL
.
          MATCH     "1",CCECEXC         * Excluded from Summary
          GOTO      STED1100 IF EQUAL
.
          ASSIGN    CCECFTC-(CCEDFTC/CCEDNEP),F12P4
          ASSIGN    CCEDFSV+(F12P4*F12P4),CCEDFSV
.
          ASSIGN    CCECTTC-(CCEDTTC/CCEDNEP),F12P4
          ASSIGN    CCEDTSV+(F12P4*F12P4),CCEDTSV
.
          ASSIGN    CCECLOS-(CCEDLTO/CCEDNEP),F12P4
          ASSIGN    CCEDLSV+(F12P4*F12P4),CCEDLSV
.
          GOTO      STED1100
.
STED1900  IF        CCEDNEP>1
            ASSIGN    CCEDFSV/(CCEDNEP-1),CCEDFSV
            ASSIGN    CCEDTSV/(CCEDNEP-1),CCEDTSV
            ASSIGN    CCEDLSV/(CCEDNEP-1),CCEDLSV
          ENDIF
          CALL      UPCCED1
          GOTO      STED1000
.
STED9999  RETURN
.
.------------------------------------------------------------
. Update Variance Casemix Level 3
.------------------------------------------------------------
STEE0000  PACK      KEY30,SP70
          CALL      RSCCEE1
STEE1000  CALL      RKCCEE1
          BRANCH    OVRCD,STEE9999
.
          ADD       ONE,TOTALEXT
          IF        (TOTALEXT%100=0)
            DISPLAY   *P23:24,*V2LON,TOTALEXT
          ENDIF
.
          MOVE      ZERO,CCEEFSV
          MOVE      ZERO,CCEETSV
          MOVE      ZERO,CCEELSV
.
          PACK      KEY48,CCEELV1,CCEELV2,CCEELV3,SP70
          PACK      KEY30,CCEELV1,CCEELV2,CCEELV3,SP70
          CALL      RSCCEC2
STEE1100  CALL      RKCCEC2
          BRANCH    OVRCD,STEE1900
          PACK      KEY48,CCECLV1,CCECLV2,CCECLV3,SP70
          MATCH     KEY30,KEY48
          GOTO      STEE1900 IF NOT EQUAL
.
          MATCH     "1",CCECEXC         * Excluded from Summary
          GOTO      STEE1100 IF EQUAL
.
          ASSIGN    CCECFTC-(CCEEFTC/CCEENEP),F12P4
          ASSIGN    CCEEFSV+(F12P4*F12P4),CCEEFSV
.
          ASSIGN    CCECTTC-(CCEETTC/CCEENEP),F12P4
          ASSIGN    CCEETSV+(F12P4*F12P4),CCEETSV
.
          ASSIGN    CCECLOS-(CCEELTO/CCEENEP),F12P4
          ASSIGN    CCEELSV+(F12P4*F12P4),CCEELSV
.
          GOTO      STEE1100
.
STEE1900  IF        CCEENEP>1
            ASSIGN    CCEEFSV/(CCEENEP-1),CCEEFSV
            ASSIGN    CCEETSV/(CCEENEP-1),CCEETSV
            ASSIGN    CCEELSV/(CCEENEP-1),CCEELSV
          ENDIF
          CALL      UPCCEE1
          GOTO      STEE1000
.
STEE9999  RETURN
.
.--------------------------------------------------
. Calculate Summary Income
.--------------------------------------------------
SUMM0000  DISPLAY   *P1:24,*EL,"Casemix Extract Level Summary : "
          PACK      KEY18,SP70
          MOVE      ZERO,TOTALEXT
          CALL      RSCCEC1
SUMM1000  CALL      RKCCEC1
          BRANCH    OVRCD,SUMM9000
          ADD       ONE,TOTALEXT
          IF        (TOTALEXT%100=0)
            DISPLAY   *P33:24,*V2LON,TOTALEXT
          ENDIF
.
          MATCH     "1",CCECEXC         * Excluded from Summary
          GOTO      SUMM1000 IF EQUAL
.
          CALL      UPDEB000
.
          MATCH     SP70,CCEALV2
          GOTO      SUMM1000 IF EQUAL
          CALL      UPDED000
.
          MATCH     SP70,CCEALV3
          GOTO      SUMM1000 IF EQUAL
          CALL      UPDEE000
          GOTO      SUMM1000
SUMM9000
SUMM9999  RETURN
.
.------------------------------------------------------------
. Update Casemix Level 3
.------------------------------------------------------------
UPDEE000  PACK      KEY30,CCECLV1,CCECLV2,CCECLV3,SP70
          MATCH     SP70,KEY30
          GOTO      UPDEE999 IF EQUAL
          CALL      RDCCEE1
          IF        OVRCD=1
            CALL      CLREE000
            UNPACK    KEY30,CCEELV1,CCEELV2,CCEELV3
            CALL      WRCCEE1
            CALL      RDCCEE1
          ENDIF
          ADD       ONE,CCEENEP    * Increase Number of Episodes
          IF        CCECFTC>CCEEFMX
            MOVE      CCECFTC,CCEEFMX
          ENDIF
          IF        CCECFTC<CCEEFMN
            MOVE      CCECFTC,CCEEFMN
          ENDIF
.
          IF        CCECTTC>CCEETMX
            MOVE      CCECTTC,CCEETMX
          ENDIF
          IF        CCECTTC<CCEETMN
            MOVE      CCECTTC,CCEETMN
          ENDIF
.
          IF        CCECLOS>CCEELMX
            MOVE      CCECLOS,CCEELMX
          ENDIF
          IF        CCECLOS<CCEELMN
            MOVE      CCECLOS,CCEELMN
          ENDIF
.
          ADD       CCECFTC,CCEEFTC
          ADD       CCECTTC,CCEETTC
          ADD       CCECLOS,CCEELTO
          CALL      UPCCEE1
.
UPDEE999  RETURN
.
CLREE000  MOVE      ZERO,CCEENEP
          MOVE      ZERO,CCEEFTC
          MOVE      "999999999999",CCEEFMN
          MOVE      ZERO,CCEEFMX
          MOVE      ZERO,CCEEFSV
          MOVE      ZERO,CCEETTC
          MOVE      "999999999999",CCEETMN
          MOVE      ZERO,CCEETMX
          MOVE      ZERO,CCEETSV
          MOVE      "999999999999",CCEELMN
          MOVE      ZERO,CCEELMX
          MOVE      ZERO,CCEELSV
          MOVE      ZERO,CCEELTO
          RETURN
.
.------------------------------------------------------------
. Update Casemix Level 2
.------------------------------------------------------------
UPDED000  PACK      KEY20,CCECLV1,CCECLV2,SP70
          MATCH     SP70,KEY20
          GOTO      UPDED999 IF EQUAL
          CALL      RDCCED1
          IF        OVRCD=1
            CALL      CLRED000
            UNPACK    KEY20,CCEDLV1,CCEDLV2
            CALL      WRCCED1
            CALL      RDCCED1
          ENDIF
          ADD       ONE,CCEDNEP    * Increase Number of Episodes
          IF        CCECFTC>CCEDFMX
            MOVE      CCECFTC,CCEDFMX
          ENDIF
          IF        CCECFTC<CCEDFMN
            MOVE      CCECFTC,CCEDFMN
          ENDIF
.
          IF        CCECTTC>CCEDTMX
            MOVE      CCECTTC,CCEDTMX
          ENDIF
          IF        CCECTTC<CCEDTMN
            MOVE      CCECTTC,CCEDTMN
          ENDIF
.
          IF        CCECLOS>CCEDLMX
            MOVE      CCECLOS,CCEDLMX
          ENDIF
          IF        CCECLOS<CCEDLMN
            MOVE      CCECLOS,CCEDLMN
          ENDIF
.
          ADD       CCECFTC,CCEDFTC
          ADD       CCECTTC,CCEDTTC
          ADD       CCECLOS,CCEDLTO
          CALL      UPCCED1
UPDED999  RETURN
.
CLRED000  MOVE      ZERO,CCEDNEP
          MOVE      ZERO,CCEDFTC
          MOVE      "999999999999",CCEDFMN
          MOVE      ZERO,CCEDFMX
          MOVE      ZERO,CCEDFSV
          MOVE      ZERO,CCEDTTC
          MOVE      "999999999999",CCEDTMN
          MOVE      ZERO,CCEDTMX
          MOVE      ZERO,CCEDTSV
          MOVE      "999999999999",CCEDLMN
          MOVE      ZERO,CCEDLMX
          MOVE      ZERO,CCEDLSV
          MOVE      ZERO,CCEDLTO
CLRED999  RETURN
.
.------------------------------------------------------------
. Update Casemix Level 1
.------------------------------------------------------------
UPDEB000  PACK      KEY10,CCECLV1
          CALL      RDCCEB1
          IF        OVRCD=1
            CALL      CLREB000
            MOVE      CCECLV1,CCEBLV1
            CALL      WRCCEB1
            CALL      RDCCEB1
          ENDIF
          ADD       ONE,CCEBNEP    * Increase Number of Episodes
          IF        CCECFTC>CCEBFMX
            MOVE      CCECFTC,CCEBFMX
          ENDIF
          IF        CCECFTC<CCEBFMN
            MOVE      CCECFTC,CCEBFMN
          ENDIF
.
          IF        CCECTTC>CCEBTMX
            MOVE      CCECTTC,CCEBTMX
          ENDIF
          IF        CCECTTC<CCEBTMN
            MOVE      CCECTTC,CCEBTMN
          ENDIF
.
          IF        CCECLOS>CCEBLMX
            MOVE      CCECLOS,CCEBLMX
          ENDIF
          IF        CCECLOS<CCEBLMN
            MOVE      CCECLOS,CCEBLMN
          ENDIF
.
          ADD       CCECFTC,CCEBFTC
          ADD       CCECTTC,CCEBTTC
          ADD       CCECLOS,CCEBLTO
          CALL      UPCCEB1
          RETURN
.
CLREB000  MOVE      ZERO,CCEBNEP
          MOVE      ZERO,CCEBFTC
          MOVE      "999999999999",CCEBFMN
          MOVE      ZERO,CCEBFMX
          MOVE      ZERO,CCEBFSV
          MOVE      ZERO,CCEBTTC
          MOVE      "999999999999",CCEBTMN
          MOVE      ZERO,CCEBTMX
          MOVE      ZERO,CCEBTSV
          MOVE      "999999999999",CCEBLMN
          MOVE      ZERO,CCEBLMX
          MOVE      ZERO,CCEBLSV
          MOVE      ZERO,CCEBLTO
CLREB999  RETURN
.
.------------------------------------------------------------
. Trim Data
.------------------------------------------------------------
TRIM0000  COMPARE   ZERO,CALCPERC      * Trim Based on Percentage at Top
          GOTO      TRIM2000 IF EQUAL  * and Bottom of Sorted List.
.
          PACK      KEY28,SP70
          MOVE      ZERO,SECTOR
          CALL      RSCCTM1
TRIM1000  CALL      RKCCTM1
          BRANCH    OVRCD,TRIM9999
          SQUEEZE   CCTMKEY
          MOVE      CCTMKEY,F7P2
          IF        (F7P2>TRIMUP)
            MATCH     "L",TRIMLIM      * If Lower Trim Only Don't Trim Upper
            IF        !@EQUAL
              PACK      KEY18,CCTMHCD,CCTMEPS
              CALL      RDCCEC1
              IF        OVRCD=0
                MOVE      ONE,CCECEXC * Exclude
                CALL      UPCCEC1
                CALL      PRTLIN00
              ENDIF
            ENDIF
          ENDIF
          IF        (F7P2<TRIMLW)
            MATCH     "U",TRIMLIM      * If Upper Trim Only Don't Trim Lower
            IF        !@EQUAL
              PACK      KEY18,CCTMHCD,CCTMEPS
              CALL      RDCCEC1
              IF        OVRCD=0
                MOVE      ONE,CCECEXC * Exclude
                CALL      UPCCEC1
                CALL      PRTLIN00
              ENDIF
            ENDIF
          ENDIF
          GOTO      TRIM1000
.
. Trim Based on Percentage - Lower Trim
.---------------------------------------
TRIM2000  ASSIGN    ((TOTALEPS*TRIMPERC)/100.0)/2.0,TRIMNUMB
          IF        TRIMNUMB<1
            GOTO       TRIM9999
          ENDIF
          MATCH     "U",TRIMLIM      * If Upper Trim Only Don't Trim Lower
          GOTO      TRIM3900 IF EQUAL
          PACK      KEY28,SP70
          MOVE      ZERO,SECTOR
          CALL      RSCCTM1
TRIM3000  CALL      RKCCTM1
          BRANCH    OVRCD,TRIM3900
          PACK      KEY18,CCTMHCD,CCTMEPS
          CALL      RDCCEC1
          IF        OVRCD=0
            MOVE      ONE,CCECEXC * Exclude
            CALL      UPCCEC1
            CALL      PRTLIN00
          ENDIF
          ADD       ONE,SECTOR
          IF        SECTOR<TRIMNUMB
            GOTO      TRIM3000
          ENDIF
.
. Trim Based on Percentage - Upper Trim
.---------------------------------------
TRIM3900  PACK      KEY28,Z70
          MATCH     "L",TRIMLIM      * If Lower Trim Only Don't Trim Upper
          GOTO      TRIM9999 IF EQUAL
          MOVE      ZERO,SECTOR
          CALL      RSCCTM1
TRIM4000  CALL      RPCCTM1
          BRANCH    OVRCD,TRIM9999
          PACK      KEY18,CCTMHCD,CCTMEPS
          CALL      RDCCEC1
          IF        OVRCD=0
            MOVE      ONE,CCECEXC * Exclude
            CALL      UPCCEC1
            CALL      PRTLIN00
          ENDIF
          ADD       ONE,SECTOR
          IF        SECTOR<TRIMNUMB
            GOTO      TRIM4000
          ENDIF
.
TRIM9999  RETURN
.
.------------------------------------------------------------
. Print Excluded Records
.------------------------------------------------------------
PRTLIN00  ADD       ONE,CLNO
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
          MOVE      NO,KEY3
          MATCH     "1",CCECEXC
          IF        @EQUAL
            MOVE      YES,KEY3
          ENDIF
          PRINT     "|",*63,"| ",CCECHCD,"/",CCECEPS,CCECLOS,SP5,CCECFTC,SP10:
                    SP3,KEY3,*132,"|"
PRTLIN99  RETURN
.
.-------------------------------
. Includes for I/O and Keyins
.-------------------------------
          INC       CCR65TIO
          INC       CCSCEAIO/INC
          INC       CCSCEBIO/INC
          INC       CCSCECIO/INC
          INC       CCSCEDIO/INC
          INC       CCSCEEIO/INC
          INC       CCSCOPCD
          INC       CCSEPSIO/INC
          INC       CCSSTACD
          INC       CCSSTYIO/INC
          INC       CCSXHDIO/INC
.
          INC       STD001IO
.
