. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAAPS45                                             *
. * Desc      :   Purge Bank Reconciliation                            *
. **********************************************************************
. * Date      :   01.11.1997                                           *
. * Author    :   B.G.Ackland                                          *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
.
          INC       FMSSTDDF   
.
          INC       FMSBNKFD/INC
.
TEMPFILE   FILE     ASCII, FIXED=100
OLDBNKA1   IFILE    SQL, FIXED=150, KEY="U1-15,16-20"
LASTBNK    DIM      15
NEXTUNI    DIM      5
REPUNIQ   INIT      "yzxywxvwuvtustrsqrpqopnomnlmkljkijhighfgefdecdbcabZa": 
                    "YZXYWXVWUVTUSTRSQRPQOPNOMNLMKLJKIJHIGHFGEFDECDBCAB9A": 
                    "8978675645342312 1"                                    
.
PURDATE   DIM      8
RECNO     FORM     8
.
PRGID     INIT     "IBAAPS45"
PRGNAM    INIT     "Purge Bank Reconciliation File"
VERSION   INIT      "V12.00.00"
.
          CALL      DISPHEAD
          DISPLAY   *P1:4,"This program will purge bank reconciliation.":
                    *P1:6,"Before Running this program":
                    *P1:8,"            mv fmsbnkaf.dat oldbnkaf.dat":
                    *P1:9,"            mv fmsbnkaf.idx oldbnkaf.idx":
                    *P1:10,"            and create a new fmsbnkaf file"
          DISPLAY   *P1:12,"Purge Prior to Date : "
          MOVE      "12",CVERT
          MOVE      "23",CCOL
          MOVE      "30",CDAY
          MOVE      "06",CMON
          PACK      KEY4,CCC,CYY
          REP       " 0",KEY4
          MOVE      KEY4,F4
          SUB       "2",F4
          MOVE      F4,KEY4
          REP       " 0",KEY4
          UNPACK    KEY4,CCENT,CYEAR
          MOVE      ONE,CCENTRY
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
          CALL      KEYDATE
          CALL      CONTQST
          BRANCH    CEXIT,START,END,END
          GOTO      END
.
START     DISPLAY   *P54:24,"Opening fmsbnkaf";
          OPEN      FMSBNKA1,"fmsbnkaf"
          OPEN      OLDBNKA1,"oldbnkaf"
.
          MOVE      ZERO,RECNO
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:24,*EL,*P20:24,RECNO,SP1,CTIMEIS
          PACK      PURDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",PURDATE
          MOVE      SP20,KEY20
          READ      OLDBNKA1,KEY20;;
.
LOOP      READKS    OLDBNKA1;FMBNBNK,FMBNUNQ,FMBNREF,FMBNLED,FMBNACC:
                             FMBNDAT,FMBNTYP,FMBNPRE,FMBNPDT,FMBNAMT,FMBNFYR:
                             FMBNBCH,FMBNLNO,FMBNCRE,FMBNNAM,FMBNSPAR
          GOTO      COMPLETE IF OVER
          ADD       ONE,RECNO
          IF        RECNO%1000=0
            CLOCK     TIME,CTIMEIS
            DISPLAY   *P20:24,RECNO,SP1,CTIMEIS
          ENDIF
          MATCH     "0",FMBNPRE
          GOTO      WRITE IF EQUAL
          REP       " 0",FMBNDAT
          MATCH     PURDATE,FMBNDAT
          GOTO      LOOP IF LESS
.
WRITE     MATCH     FMBNBNK,LASTBNK
          IF        !@EQUAL
            MOVE      SP70,NEXTUNI
            MOVE      FMBNBNK,LASTBNK
          ENDIF
          CALL      GNID0000
          MOVE      NEXTUNI,FMBNUNQ
          CALL      WRFMBN1
          GOTO      LOOP
.
COMPLETE  PREP     TEMPFILE,"ibaaps45"
          CLOCK    TIME,CTIMEIS
          WRITE    TEMPFILE,SEQ;"Completed at ",CTIMEIS
.
          DISPLAY  *P1:24,*EL,"Complete - ";
          CALL     HITENTER
END       CHAIN    PGM
.------------------------------------------------------------
. Get next bank rec unique id
.------------------------------------------------------------
GNID0000  UNPACK    NEXTUNI,ANS,KEY4
          MOVE      ZERO,F4
          MOVE      KEY4,F4
.
GNID1100  COMPARE   "9999",F4          * at end of group ?
          GOTO      GNID1110 IF NOT EQUAL
.
          MATCH     "z",ANS
          GOTO      BADERROR IF EQUAL
.
          REP       REPUNIQ,ANS
          MOVE      ZERO,F4
.
GNID1110  ADD       ONE,F4
          PACK      NEXTUNI,ANS,F4
GNID9999  RETURN
.
BADERROR  DISPLAY   *P1:24,"CONTACT IBA - fmsbnkaf full - ";
          CALL      HITENTER
          CHAIN     PGM

          INC      FMSBNKIO/INC
          INC      STDGENCD
.
