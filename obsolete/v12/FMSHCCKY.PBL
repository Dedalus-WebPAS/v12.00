.-------------------------------------------------------------------------------
.   System    :   FMS Financial System  
.                           
.   Program   :   FMSHCCFM 
.                            
.   Requires  :   FMHLINST (Institution Code)
.                            
.   Routines  :   KHCCFM00 Keyin HCA Cost Centre   - Returns a valid record
.                 AHCCFM00 Accept HCA Cost Centre  - Returns a key NOT on file
.   
.   Return    :   EXIT = 1 null entry EXIT = 2 EXITCHAR entered
.
.   Internal  :   HHCCFM00 Help Display for HCA Cost Centre  
.                            
.   Redisplay :   RHCCFM00 Screen Redisplay Called for HCA Cost Centre  
.-------------------------------------------------------------------------------
.   Mods      :   30.08.91 Neeriem Dye
.                          Fixed descriptions from HCA to HCS
.                  
.                 16.09.91 Neeriem Dye
.                          Modified for new FMSHCCFD format
.                  
.-------------------------------------------------------------------------------
. Normal Code Keyin - Code Must Exist Exit = 1 if Null Entry
.-------------------------------------------------------------------------------
KHCCFM00  MOVE      UNDLN70,FMHAHCCC
          KEYIN     *PCCOL:CVERT,*EL,*DV,FMHAHCCC:
                    *PCCOL:CVERT,*V2LON,FMHAHCCC: 
                    *PCCOL:CVERT,*EL,*DV,FMHAHCCC
.
          ENDSET    FMHAHCCC
          APPEND    SP70,FMHAHCCC
          RESET     FMHAHCCC
          MATCH     SP70,FMHAHCCC             * System Code Spaces so exit set
          GOTO      KHCCFM90 IF EQUAL
.
          CMATCH    EXITCHAR,FMHAHCCC         * Exit Selected
          GOTO      KHCCFM95 IF EQUAL
.
          CMATCH    QUEST,FMHAHCCC            * Help Required  
          GOTO      KHCCFM10 IF NOT EQUAL
          CALL      HHCCFM00                  * Display List Of FMHAHCCCs
          PACK      CDATASAV,FMHAHCCC,SP70    * save data entered
          CALL      RHCCFM00                  * Redisplay Routine
          PACK      FMHAHCCC,CDATASAV,SP70    * restore data entered
.
          ENDSET    FMHAHCCC
          APPEND    SP70,FMHAHCCC
          RESET     FMHAHCCC
          MATCH     SP70,FMHAHCCC             * System Code Spaces so exit set
          GOTO      KHCCFM00 IF EQUAL
.
KHCCFM10  PACK      KEY8,FMHLINST,FMHAHCCC
          CALL      RDFMHA1                                   
          BRANCH    OVRCD,KHCCFM80
          MOVE      ZERO,EXIT                 * no entry so exit is set
          GOTO      KHCCFM99
.
KHCCFM80  DISPLAY   *P1:24,*EL,*B,"HCS Cost Centre Does Not Exist - ";
          CALL      HITENTER
          GOTO      KHCCFM00
.
KHCCFM90  MOVE      ONE,EXIT                  * null entry so exit is set
          GOTO      KHCCFM99
.
KHCCFM95  MOVE      TWO,EXIT                  * Exit character entered    
.
KHCCFM99  RETURN
.-------------------------------------------------------------------------------
. Accept Code Keyin - Code Must Not Exist 
.-------------------------------------------------------------------------------
AHCCFM00  MOVE      UNDLN70,FMHAHCCC
          KEYIN     *PCCOL:CVERT,*EL,*DV,FMHAHCCC:
                    *PCCOL:CVERT,*V2LON,FMHAHCCC: 
                    *PCCOL:CVERT,*V2LON,*DV,FMHAHCCC
.
          ENDSET    FMHAHCCC
          APPEND    SP70,FMHAHCCC
          RESET     FMHAHCCC
          MATCH     SP70,FMHAHCCC             * System Code Spaces so exit set
          GOTO      AHCCFM90 IF EQUAL
.
          CMATCH    EXITCHAR,FMHAHCCC         * Exit Selected
          GOTO      AHCCFM95 IF EQUAL
.
          CMATCH    QUEST,FMHAHCCC            * Help Required  
          GOTO      AHCCFM10 IF NOT EQUAL
          CALL      HHCCFM00                  * Display List Of FMHAHCCCs
          CALL      RHCCFM00                  * Redisplay Routine
.
          ENDSET    FMHAHCCC
          APPEND    SP70,FMHAHCCC
          RESET     FMHAHCCC
          MATCH     SP70,FMHAHCCC             * System Code Spaces so exit set
          GOTO      AHCCFM00 IF EQUAL
.
AHCCFM10  PACK      KEY8,FMHLINST,FMHAHCCC
          MOVE      ZERO,EXIT                 * no entry so exit is set
          CALL      RDFMHA1                                   
          BRANCH    OVRCD,AHCCFM99
.
          DISPLAY   *P1:24,*EL,*B,"HCS Cost Centre Already Exists - ";
          CALL      HITENTER
          GOTO      AHCCFM00
.
AHCCFM90  MOVE      ONE,EXIT                  * null entry so exit is set
          GOTO      AHCCFM99
.
AHCCFM95  MOVE      TWO,EXIT                  * Exit character entered    
.
AHCCFM99  RETURN
.------------------------------------------------------------------------------
. Display List of All Codes
.------------------------------------------------------------------------------
HHCCFM00  DISPLAY   *P1:4,"   Institution Code      : ":
                    *V2LON,FMHLINST,*HOFF,SP10,SP1,FMHLDESC,*EF:
                    *P1:5,*V2LON,*ULON,"Item":
                    *P6:5,"HCS Cost Centre";
          MOVE      FIVE,STDROW
          MOVE      ZERO,XITEMNO
          MOVE      ZERO,CPAGENO
          PACK      KEY8,FMHLINST,SP70
          CALL      RSFMHA1
.
HHCCFM10  CALL      RKFMHA1
          BRANCH    OVRCD,HHCCFM20
          MATCH     FMHLINST,FMHAINST
          GOTO      HHCCFM20 IF NOT EQUAL
.
          ADD       ONE,XITEMNO
          ADD       ONE,STDROW
          STORE     FMHAHCCC,XITEMNO,ST1,ST2,ST3,ST4,ST5:
                                    ST6,ST7,ST8,ST9,ST10:
                                    ST11,ST12,ST13,ST14,ST15:
                                    ST16,ST17
          DISPLAY   *P2:STDROW,*V2LON,XITEMNO,DOT,*HOFF:
                    *P6:STDROW,FMHAHCCC
          COMPARE   "17",XITEMNO
          GOTO      HHCCFM10 IF NOT EQUAL
.
HHCCFM20  MOVE      ZERO,CCITEM
          MOVE      SP2,CCITEMD
          DISPLAY   *P1:24,*EL,"Select Item, (";
          COMPARE   "17",XITEMNO
          GOTO      HHCCFM25 IF NOT EQUAL
          DISPLAY   *V2LON,"N",*HOFF,")ext page, (";
.
HHCCFM25  COMPARE   ZERO,CPAGENO
          GOTO      HHCCFM26 IF EQUAL
          DISPLAY   *V2LON,"P",*HOFF,")revious page, (";
.
HHCCFM26  KEYIN     *V2LON,"E",*HOFF,")xit ? ",*JR,*V2LON,CCITEMD;
.
          RESET     CCITEMD
          GOTO      HHCCFM30 IF EOS      * Next Page
          REP       UPPLOW,CCITEMD
          MATCH     "  N",CCITEMD        * Next Page
          GOTO      HHCCFM30 IF EQUAL
          MATCH     "  P",CCITEMD        * Previous Page
          GOTO      HHCCFM35 IF EQUAL
          MATCH     "  E",CCITEMD        * Exit
          GOTO      HHCCFM91 IF EQUAL
.
          TYPE      CCITEMD
          GOTO      HHCCFM90 IF NOT EQUAL  * Invalid Entry
.
          MOVE      CCITEMD,CCITEM
          COMPARE   ONE,CCITEM
          GOTO      HHCCFM90 IF LESS       * Invalid Entry
          COMPARE   CCITEM,XITEMNO
          GOTO      HHCCFM90 IF LESS       * Invalid Entry
          GOTO      HHCCFM92               * Item Selected
.
HHCCFM30  COMPARE   "17",XITEMNO            * Next Page Selected
          GOTO      HHCCFM90 IF NOT EQUAL  * No Next Page Invalid Selection
          ADD       ONE,CPAGENO
          MOVE      ZERO,XITEMNO            
          MOVE      FIVE,STDROW
          DISPLAY   *P1:6,*EF
          GOTO      HHCCFM10
.
HHCCFM35  COMPARE   ZERO,CPAGENO           * Previous Page Selected
          GOTO      HHCCFM90 IF EQUAL      * No Previous Page Invalid Selection
          DISPLAY   *P1:6,*EF
          SUB       ONE,CPAGENO
          COMPARE   "17",XITEMNO      
          GOTO      HHCCFM36 IF EQUAL 
.
          CALL      RPFMHA1            * Initial reverse read
          BRANCH    OVRCD,HHCCFM00
          MATCH     FMHLINST,FMHAINST
          GOTO      HHCCFM00 IF NOT EQUAL
.
          COMPARE   ZERO,XITEMNO      
          GOTO      HHCCFM37 IF EQUAL 
.
HHCCFM36  CALL      RPFMHA1
          BRANCH    OVRCD,HHCCFM00
          MATCH     FMHLINST,FMHAINST
          GOTO      HHCCFM00 IF NOT EQUAL
.
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HHCCFM36 IF NOT EQUAL
          COMPARE   FIVE,STDROW
          GOTO      HHCCFM10 IF EQUAL   * Display Down The Page
HHCCFM37  MOVE      FIVE,STDROW
          MOVE      "17",XITEMNO
          GOTO      HHCCFM36
.
HHCCFM90  BEEP                          * invalid entry
          GOTO      HHCCFM20
.
HHCCFM91  MOVE      SP70,FMHAHCCC
          GOTO      HHCCFM99
.
HHCCFM92  MOVE      SP70,FMHAHCCC
          LOAD      FMHAHCCC,CCITEM,ST1,ST2,ST3,ST4,ST5:
                                  ST6,ST7,ST8,ST9,ST10:
                                  ST11,ST12,ST13,ST14,ST15:
                                  ST16,ST17
.
HHCCFM99  RETURN
.
