.***************************************************************************
.* System    :   CIAS                                                      *
.* Program   :   IBACCC69                                                  *
.* Desc      :   CIAS extract consolidation                                *
.***************************************************************************
.* Date      :   18/02/2004                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program contains the standard CIAS extract           *
.*               consolidation processes                                   *
.***************************************************************************
.* Mods      :   V9.03.00 18/02/2004 Ebon Clements         CAR 45487       *
.*                        Created program from IBACEA69                    *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       CCSCEAFD/INC
          INC       CCSCEBFD/INC
          INC       CCSCECFD/INC
          INC       CCSCEDFD/INC
          INC       CCSCEEFD/INC
          INC       CCSCEFFD/INC
          INC       CCSCEGFD/INC
          INC       CCSHCDFD/INC
          INC       CCSSTYFD/INC
          INC       CCSXHDFD/INC
          INC       CEASPCFD/INC
.
CCSTMPA1  IFILE    SQL, FIXED=21, KEY="U1-10,11-20"
.
CCTMSEQ   DIM       10     10       1     * Sequence
CCTMLV1   DIM       10     10       11    * Casemix Code
.
.End of Record                      21
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
ACCEPT    FORM      1
CLV1DES   DIM      35
CLV2DES   DIM      35
CLV3DES   DIM      35
COSTFLAG  FORM      1
COMMAND   DIM       80
COUNTER   FORM      6
DAYCDESC  DIM      25
FROMDD    DIM      10
FROMAD    DIM      10
FROMCMOD  DIM      35 
FILENAME  DIM       8
F12P4     FORM      12.4
ISBUILD   INIT      "isbuild "
NOCODLV1  FORM      3
NOCODLV2  FORM      3
NOCODLV3  FORM      3
REPTSEQ   FORM      2
SEQDESC   DIM      30
SGENISB   INIT      "isbuild "
STDGISE   INIT      "iserase "
TOAD      DIM      10
TODD      DIM      10
TOCMOD    DIM      35
TOTALEXT  FORM      8
VISITDES  DIM      15
.
PRGID     INIT      "IBACCC69"
PRGNAM    INIT      "CIAS Extract Consolidation"
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000
.
ML1000    CALL      CONS0000               * Consolidate Extract
          BRANCH    EXIT,ML0100
          GOTO      ML9999
.
ML9999    CHAIN     PGM                    * chain back to program
.
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *  
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *  
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          MOVE      ONE,CCENTRY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          DISPLAY   *P56:24,"Opening ccsceaaf";
          OPEN      CCSCEAA1,"ccsceaaf"
          DISPLAY   *P56:24,"Opening ccshcdaf";
          OPEN      CCSHCDA1,"ccshcdaf"
          DISPLAY   *P56:24,"Opening ccshcdaf";
          OPEN      CCSHCDA2,"ccshcdaf"
          DISPLAY   *P56:24,"Opening ccsstyaf";
          OPEN      CCSSTYA1,"ccsstyaf"
          DISPLAY   *P56:24,"Opening ccsxhdaf";
          OPEN      CCSXHDA1,"ccsxhdaf"
          DISPLAY   *P56:24,"Opening ccsxhdaf";
          OPEN      CCSXHDA2,"ccsxhdaf"
.
          CALL      CF00L000                     * Clear fields
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Consolidate Extract"
.
OPTN0500  KEYIN     *P1:16,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:16,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
+
.**************************************************************************
.*                               CONS0000              Called by: ML1000  *
.* Generate Casemix Extract                                               *
.**************************************************************************
.
CONS0000  CALL      KEXT0000                     * Keyin extract ID
          BRANCH    EXIT,CONS9999
.
          CALL      KRSEQ000                     * Consolidate By
          BRANCH    EXIT,CONS9999
.
          CALL      ELV10000                     * Top No. of codes level 1
          BRANCH    EXIT,CONS9999
.
          CALL      ELV20000                     * Top No. of codes level 2
          BRANCH    EXIT,CONS9999
.
          CALL      ELV30000                     * Top No. of codes level 3
          BRANCH    EXIT,CONS9999
.
          CALL      CONTQST                      * ok to continue
          BRANCH    CEXIT,CONS5000,CONS9999,CONS9999    * 1=Yes, 2=No, 3=Cancel
.
CONS5000  CALL      PROC0000                     * Process Consolidation
.
CONS9999  RETURN
.
.------------------------------------------------------------
. Keyin extract ID
.------------------------------------------------------------
KEXT0000  CALL      CCSCOPCL         * Close Files
.
          DISPLAY   *P1:4,*EF
          KEYIN     *P1:4,"Extract ID : ",*V2LON,CCEAEID
          ENDSET    CCEAEID
          APPEND    SP70,CCEAEID
          RESET     CCEAEID
.
          MATCH     SP70,CCEAEID
          GOTO      KEXT9000 IF EQUAL
.
          PACK      KEY4,CCEAEID,SP70
          CALL      RDCCEA1
          IF        OVRCD=1
            BEEP
            DISPLAY  *P1:24,"Invalid Extract ID";
            CALL      HITENTER
            GOTO      KEXT0000
          ENDIF
.
          DISPLAY   *P19:4,*V2LON,CCEADES
.
          CALL      GETCME00
          CALL      CCSCOPOP         * Open Files
          MOVE      ZERO,NOCODLV1
          MOVE      ZERO,NOCODLV2
          MOVE      ZERO,NOCODLV3
.
          MOVE      ZERO,EXIT
          GOTO      KEXT9999
.
KEXT9000  MOVE      ONE,EXIT
          GOTO      KEXT9999
.
KEXT9999  RETURN
.
.------------------------------------------------------------
. Enter Sequence
.------------------------------------------------------------
KRSEQ000  MOVE      ZERO,COSTFLAG
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      CEASPCA1,"ceaspcaf"
          TRAPCLR   IO
          MOVE      OVRCD,COSTFLAG
          DISPLAY   *P1:5,*EF,"Consolidate by : ":
                    *P1:24,*EL,"Sequence : ":
                    *V2LON,"1",*HOFF,".No. of Episodes, ":
                    *V2LON,"2",*HOFF,".Total Bed Days, ":
                    *V2LON,"3",*HOFF,".Total Income";
          BRANCH    COSTFLAG,KRSEQ100
          DISPLAY   ", ",*V2LON,"4",*HOFF,".Total Cost"

KRSEQ100  KEYIN     *P17:5,*V2LON,*RV,REPTSEQ
          BRANCH    REPTSEQ,KRSEQ110,KRSEQ120,KRSEQ130,KRSEQ140
KRSEQ105  BEEP
          GOTO      KRSEQ100
.
KRSEQ110  MOVE      "By Number of Episodes",SEQDESC
          DISPLAY   *P20:5,SEQDESC
          GOTO      KRSEQ999
.
KRSEQ120  MOVE      "By Total Bed Days",SEQDESC
          DISPLAY   *P20:5,SEQDESC
          GOTO      KRSEQ999
.
KRSEQ130  MOVE      "By Total Income",SEQDESC
          DISPLAY   *P20:5,SEQDESC
          GOTO      KRSEQ999
.
KRSEQ140  BRANCH    COSTFLAG,KRSEQ105          * Only If Costing Files
          MOVE      "By Total Cost",SEQDESC    * Available
          DISPLAY   *P20:5,SEQDESC
          GOTO      KRSEQ999
.
KRSEQ999  RETURN
.
.**************************************************************************
. Determine Display Values for a Valid Casemix Extract Code
.**************************************************************************
GETCME00  MATCH     SP70,CCEAFDD
          IF        @EQUAL
            MOVE      "Start     ",FROMDD
          ELSE
            UNPACK    CCEAFDD,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,FROMDD
          ENDIF
.
          MATCH     "99999999",CCEATDD
          IF        @EQUAL
            MOVE      "Finish    ",TODD
          ELSE
            UNPACK    CCEATDD,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,TODD
          ENDIF
.
          MATCH     SP70,CCEAFAD
          IF        @EQUAL
            MOVE      "Start     ",FROMAD
          ELSE
            UNPACK    CCEAFAD,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,FROMAD
          ENDIF
.
          MATCH     "99999999",CCEATAD
          IF        @EQUAL
            MOVE      "Finish    ",TOAD
          ELSE
            UNPACK    CCEATAD,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      CPCDATE,TOAD
          ENDIF
.
          MOVE     CCEAVIS,F1
          BRANCH   F1,GETCME10,GETCME20,GETCME30
          MOVE     "All        ",VISITDES
          GOTO     GETCME40
GETCME10  MOVE     "A & E      ",VISITDES
          GOTO     GETCME40
GETCME20  MOVE     "Outpatients ",VISITDES
          GOTO     GETCME40
GETCME30  MOVE     "Inpatients",VISITDES
          GOTO     GETCME40
.
GETCME40  MOVE     SP70,CLV1DES
          MOVE     SP70,CLV2DES
          MOVE     SP70,CLV3DES
.
          MOVE     CCEALV1,KEY3
          CALL     RDCCST1
          MOVE     CCSTDES,CLV1DES
.
          MATCH    SP70,CCEALV2
          IF       !@EQUAL
            MOVE     CCEALV2,KEY3
            CALL     RDCCST1
            MOVE     CCSTDES,CLV2DES
          ENDIF
.
          MATCH    SP70,CCEALV3
          IF       !@EQUAL
            MOVE     CCEALV3,KEY3
            CALL     RDCCST1
            MOVE     CCSTDES,CLV3DES
          ENDIF
.
          MOVE     CCEAHCD,KEY2
          CALL     RDCCHC1
.
          MOVE       CCEADAY,F1
          IF         F1=0
            MOVE       "Exclude Day Cases",DAYCDESC
          ELSE
            IF         F1=2
              MOVE       "Day Cases Only",DAYCDESC
            ELSE
              MOVE       "Include Day Cases",DAYCDESC
            ENDIF
          ENDIF
.
GETCME99  RETURN
.
.------------------------------------------------------------
. Top No. of codes level 1
.------------------------------------------------------------
ELV10000  DISPLAY   *P1:6,*EF
          KEYIN     *P1:6,"Top No. of Codes Level 1 : ",*V2LON,NOCODLV1
.
          MOVE      ZERO,EXIT
          GOTO      ELV19999
.
ELV19000  MOVE      ONE,EXIT
          GOTO      ELV19999
.
ELV19999  RETURN
.
.------------------------------------------------------------
. Top No. of codes level 2
.------------------------------------------------------------
ELV20000  DISPLAY   *P1:7,*EF
          KEYIN     *P1:7,"Top No. of Codes Level 2 : ",*V2LON,NOCODLV2
.
          MOVE      ZERO,EXIT
          GOTO      ELV29999
.
ELV29000  MOVE      ONE,EXIT
          GOTO      ELV29999
.
ELV29999  RETURN
.
.------------------------------------------------------------
. Top No. of codes level 3
.------------------------------------------------------------
ELV30000  DISPLAY   *P1:8,*EF
          KEYIN     *P1:8,"Top No. of Codes Level 3 : ",*V2LON,NOCODLV3
.
          MOVE      ZERO,EXIT
          GOTO      ELV39999
.
ELV39000  MOVE      ONE,EXIT
          GOTO      ELV39999
.
ELV39999  RETURN
.
.**************************************************************************
. Close & Delete Temp File
.**************************************************************************
CLSTMP00  DISPLAY  *P1:24;
          CLOSE    CCSTMPA1,DELETE
CLSTMP99  RETURN
.
.**************************************************************************
. Create Temp File
.**************************************************************************
OPNTMP00  DISPLAY  *P1:10;
          CLEAR    FILENAME
          APPEND   "cia69a",FILENAME
          APPEND   PORT,FILENAME
          RESET    FILENAME
          REP      " 0",FILENAME
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     CCSTMPA1,FILENAME
          TRAPCLR  IO
          IF       OVRCD=0
            CLOSE    CCSTMPA1,DELETE
          ENDIF
.
          CLEAR    COMMAND
          APPEND   ISBUILD,COMMAND
          APPEND   FILENAME,COMMAND
          APPEND   " 21 UC1-10,11-20",COMMAND
          RESET    COMMAND
          EXECUTE  COMMAND,TASKID
          OPEN     CCSTMPA1,FILENAME
.
OPNTMP99  RETURN
.**************************************************************************
. Clear Fields for Screen : Options
.**************************************************************************
CF00L000
          MOVE      SP70,CCEAEID
          MOVE      ZERO,NOCODLV1
          MOVE      ZERO,NOCODLV2
          MOVE      ZERO,NOCODLV3
          MOVE      SP70,DAYCDESC
          MOVE      SP70,CCEADES
          MOVE      SP70,CCEAINC
          MOVE      SP70,CCEAVIS
          MOVE      SP70,CCEADAY
          MOVE      SP70,CCEALV1
          MOVE      SP70,CCEALV2
          MOVE      SP70,CCEALV3
          MOVE      SP70,CCEAFCM
          MOVE      SP70,CCEAFYR
          MOVE      SP70,CCEATCM
          MOVE      SP70,CCEATYR
          MOVE      SP70,FROMDD
          MOVE      SP70,TODD
          MOVE      SP70,FROMAD
          MOVE      SP70,TOAD
          MOVE      SP70,VISITDES
          MOVE      SP70,CCHCDES
          MOVE      SP70,FROMCMOD
          MOVE      SP70,TOCMOD
          MOVE      ZERO,REPTSEQ
          MOVE      SP70,SEQDESC
          MOVE      SP70,CLV1DES
          MOVE      SP70,CLV2DES
          MOVE      SP70,CLV3DES
.
CF00L999  RETURN
.
.**************************************************************************
. Process Consolidation 
.**************************************************************************
PROC0000  MOVE      ZERO,CPAGENO          * Reset Page No.
          CLOCK     TIME,CTIMEIS          * Get Current Time
          MOVE      "99",CLNO             * Initialize Last Line Number
.
          COMPARE   ZERO,NOCODLV1
          GOTO      PROC1000 IF EQUAL
          CALL      L1CON000              * Consolidate Level 1
          CALL      SUMM0000              * Summarize Extract Again
.
PROC1000  COMPARE   ZERO,NOCODLV2
          GOTO      PROC2000 IF EQUAL
          CALL      L2CON000              * Consolidate Level 2
          CALL      SUMM0000              * Summarize Extract Again
.
PROC2000  COMPARE   ZERO,NOCODLV3
          GOTO      PROC3000 IF EQUAL
          CALL      L3CON000              * Consolidate Level 3
          CALL      SUMM0000              * Summarize Extract Again
.
PROC3000  MATCH     "0",CCEAUST
          GOTO      PROC9999 IF EQUAL   * Update Stats Y or N 0=N
.
          CALL      STAT0000              * Calculate Statistics
.
PROC9999  RETURN
.
.**************************************************************************
. Sort Level 2 CCED into Temp File
.**************************************************************************
L2SRT000  CALL      OPNTMP00              * Create Temp File for Sorting
          PACK      KEY20,CCEBLV1,SP70
          CALL      RDCCED1
          IF        OVRCD=0
            CALL      RPCCED1
          ENDIF
L2SRT100  CALL      RKCCED1
          BRANCH    OVRCD,L2SRT999
          MATCH     CCEDLV1,CCEBLV1
          GOTO      L2SRT999 IF NOT EQUAL
.
          MATCH     Z70,CCEDLV2           * Ignore Current Other Key Z70
          GOTO      L2SRT100 IF EQUAL
.
          LOAD      F10,REPTSEQ,CCEDNEP,CCEDLTO,CCEDFTC,CCEDTTC
          MOVE      F10,KEY10
.
          PACK      KEY20,KEY10,CCEDLV2
          UNPACK    KEY20,CCTMSEQ,CCTMLV1
          CALL      RDCCTM1
          IF        OVRCD=1
            CALL      WRCCTM1
          ENDIF
          GOTO      L2SRT100
.
L2SRT999  RETURN
.
.**************************************************************************
. Level 3 Procesing
.**************************************************************************
L3CON000  DISPLAY   *P1:24,*EL,"Consolidating Level 3 : ";
          PACK      KEY20,SP70
          MOVE      ZERO,SECTOR
          CALL      RSCCED1
L3CON100  CALL      RKCCED1
          BRANCH    OVRCD,L3CON999
.
          ADD       ONE,SECTOR
          IF        (SECTOR%100=0)
            DISPLAY   *P33:24,*V2LON,SECTOR
          ENDIF
.
          CALL      L3SRT000       * Sort Level 3 by Consolidation Sequence
.
. Scan by Consolidation Sequence
.
          PACK      KEY20,Z70
          MOVE      ZERO,COUNTER
          CALL      RSCCTM1
L3CON200  CALL      RPCCTM1
          BRANCH    OVRCD,L3CON299
          ADD       ONE,COUNTER
          IF        COUNTER<=NOCODLV3
            GOTO      L3CON200
          ENDIF
.
. Change all Episode Record in Extract to Other Key = Z70
.
L3CON210  PACK      KEY48,CCEDLV1,CCEDLV2,CCTMLV1,SP70
          CALL      RSCCEC2
          CALL      RKCCEC2
          BRANCH    OVRCD,L3CON200
          MATCH     CCEDLV1,CCECLV1
          GOTO      L3CON200 IF NOT EQUAL
          MATCH     CCEDLV2,CCECLV2
          GOTO      L3CON200 IF NOT EQUAL
          MATCH     CCTMLV1,CCECLV3
          GOTO      L3CON200 IF NOT EQUAL
          MOVE      Z70,CCECLV3
          CALL      UPCCEC2
          GOTO      L3CON210
.
L3CON299  CALL      CLSTMP00
          GOTO      L3CON100
.
L3CON999  RETURN
.
.**************************************************************************
. Sort Level 2 CCED into Temp File
.**************************************************************************
L3SRT000  CALL      OPNTMP00              * Create Temp File for Sorting
          PACK      KEY30,CCEDLV1,CCEDLV2,SP70
          CALL      RDCCEE1
          IF        OVRCD=0
            CALL      RPCCEE1
          ENDIF
L3SRT100  CALL      RKCCEE1
          BRANCH    OVRCD,L3SRT999
          MATCH     CCEELV1,CCEDLV1
          GOTO      L3SRT999 IF NOT EQUAL
          MATCH     CCEELV2,CCEDLV2
          GOTO      L3SRT999 IF NOT EQUAL
.
          MATCH     Z70,CCEELV2           * Ignore Current Other Key Z70
          GOTO      L3SRT100 IF EQUAL
.
          LOAD      F10,REPTSEQ,CCEENEP,CCEELTO,CCEEFTC,CCEETTC
          MOVE      F10,KEY10
.
          PACK      KEY20,KEY10,CCEELV3
          UNPACK    KEY20,CCTMSEQ,CCTMLV1
          CALL      RDCCTM1
          IF        OVRCD=1
            CALL      WRCCTM1
          ENDIF
          GOTO      L3SRT100
.
L3SRT999  RETURN
.
.**************************************************************************
. Level 1 Procesing
.**************************************************************************
L1CON000  CALL      OPNTMP00              * Create Temp File for Sorting
.
          DISPLAY   *P1:24,*EL,"Consolidating Level 1 : ";
          MOVE      ZERO,SECTOR
          PACK      KEY10,SP70
          CALL      RDCCEB1
          IF        OVRCD=0
            CALL      RPCCEB1
          ENDIF
L1CON100  CALL      RKCCEB1
          BRANCH    OVRCD,L1CON199
.
          ADD       ONE,SECTOR
          IF        (SECTOR%100=0)
            DISPLAY   *P33:24,*V2LON,SECTOR
          ENDIF
.
          MATCH     Z70,CCEBLV1             * Ignore Current Other Key Z70
          GOTO      L1CON100 IF EQUAL
.
          LOAD      F10,REPTSEQ,CCEBNEP,CCEBLTO,CCEBFTC,CCEBTTC
          MOVE      F10,KEY10
.
          PACK      KEY20,KEY10,CCEBLV1
          UNPACK    KEY20,CCTMSEQ,CCTMLV1
          CALL      RDCCTM1
          IF        OVRCD=1
            CALL      WRCCTM1
          ENDIF
          GOTO      L1CON100
.
L1CON199  PACK      KEY20,Z70
          MOVE      ZERO,COUNTER
          CALL      RSCCTM1
L1CON200  CALL      RPCCTM1
          BRANCH    OVRCD,L1CON299
          ADD       ONE,COUNTER
          IF        COUNTER<=NOCODLV1
            GOTO      L1CON200
          ENDIF
          ADD       ONE,SECTOR
          IF        (SECTOR%100=0)
            DISPLAY   *P33:24,*V2LON,SECTOR
          ENDIF
.
. Change all Episode Record in Extract to Other Key = Z70
.
L1CON210  PACK      KEY48,CCTMLV1,SP70
          CALL      RSCCEC2
          CALL      RKCCEC2
          BRANCH    OVRCD,L1CON200
          MATCH     CCTMLV1,CCECLV1
          GOTO      L1CON200 IF NOT EQUAL
          MOVE      Z70,CCECLV1
          CALL      UPCCEC2
          GOTO      L1CON210
.
L1CON299  CALL      CLSTMP00
.
L1CON999  RETURN
.
.**************************************************************************
. Level 2 Procesing
.**************************************************************************
L2CON000  DISPLAY   *P1:24,*EL,"Consolidating Level 2 : ";
          MOVE      ZERO,SECTOR
          PACK      KEY10,SP70
          CALL      RSCCEB1
L2CON100  CALL      RKCCEB1
          BRANCH    OVRCD,L2CON999
.
          ADD       ONE,SECTOR
          IF        (SECTOR%100=0)
            DISPLAY   *P33:24,*V2LON,SECTOR
          ENDIF
.
          CALL      L2SRT000       * Sort Level 2 by Consolidation Sequence
.
. Scan by Consolidation Sequence
.
          PACK      KEY20,Z70
          MOVE      ZERO,COUNTER
          CALL      RSCCTM1
L2CON200  CALL      RPCCTM1
          BRANCH    OVRCD,L2CON299
          ADD       ONE,COUNTER
          IF        COUNTER<=NOCODLV2
            GOTO      L2CON200
          ENDIF
.
. Change all Episode Record in Extract to Other Key = Z70
.
L2CON210  PACK      KEY48,CCEBLV1,CCTMLV1,SP70
          CALL      RSCCEC2
          CALL      RKCCEC2
          BRANCH    OVRCD,L2CON200
          MATCH     CCEBLV1,CCECLV1
          GOTO      L2CON200 IF NOT EQUAL
          MATCH     CCTMLV1,CCECLV2
          GOTO      L2CON200 IF NOT EQUAL
          MOVE      Z70,CCECLV2
          CALL      UPCCEC2
          GOTO      L2CON210
.
L2CON299  CALL      CLSTMP00
          GOTO      L2CON100
.
L2CON999  RETURN
.
.**************************************************************************
. Calculate Summary Costs
.**************************************************************************
SUMM0000  DISPLAY   *P1:24,*EL,"Casemix Extract Level Summary : "
          PACK      KEY18,SP70
.
          CALL      CREAT000      * Clear Summary Details Re Create Files
.
          MOVE      ZERO,SECTOR
          CALL      RSCCEC1
SUMM1000  CALL      RKCCEC1
          BRANCH    OVRCD,SUMM9000
          ADD       ONE,SECTOR
          IF        (SECTOR%100=0)
            DISPLAY   *P33:24,*V2LON,SECTOR
          ENDIF
.
          MATCH     "1",CCECEXC         * Excluded from Summary
          GOTO      SUMM1000 IF EQUAL
.
          CALL      UPDEB000
.
          MATCH     SP70,CCEALV2
          GOTO      SUMM1000 IF EQUAL
          CALL      UPDED000
.
          MATCH     SP70,CCEALV3
          GOTO      SUMM1000 IF EQUAL
          CALL      UPDEE000
          GOTO      SUMM1000
SUMM9000
SUMM9999  RETURN
.
.**************************************************************************
. Create and Open Files
.**************************************************************************
CREAT000  DISPLAY  *P1:3;
          CLOSE    CCSCEBA1,DELETE
          MOVE     "cceb",KEY4
          PACK     FILENAME,KEY4,CCEAEID
          PACK     COMMAND,SGENISB,FILENAME,CCEBIDX
          EXECUTE  COMMAND,TASKID
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     CCSCEBA1,FILENAME
          TRAPCLR  IO
          IF       OVRCD=1
            BEEP
            MOVE     "Unable to Create Casemix Extract Files - ",DISPMSG
            CALL     MESSAGE1
            GOTO     CREAT900
          ENDIF
.
          CLOSE    CCSCEDA1,DELETE
          MOVE     "cced",KEY4
          PACK     FILENAME,KEY4,CCEAEID
          PACK     COMMAND,SGENISB,FILENAME,CCEDIDX
          EXECUTE  COMMAND,TASKID
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     CCSCEDA1,FILENAME
          TRAPCLR  IO
          IF       OVRCD=1
            BEEP
            MOVE     "Unable to Create Casemix Extract Files - ",DISPMSG
            CALL     MESSAGE1
            GOTO     CREAT900
          ENDIF
.
          CLOSE    CCSCEEA1,DELETE
          MOVE     "ccee",KEY4
          PACK     FILENAME,KEY4,CCEAEID
          PACK     COMMAND,SGENISB,FILENAME,CCEEIDX
          EXECUTE  COMMAND,TASKID
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     CCSCEEA1,FILENAME
          TRAPCLR  IO
          IF       OVRCD=1
            BEEP
            MOVE     "Unable to Create Casemix Extract Files - ",DISPMSG
            CALL     MESSAGE1
            GOTO     CREAT900
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      CREAT999
.
CREAT900  MOVE      ONE,EXIT
.
CREAT999  RETURN
.
.------------------------------------------------------------
. Update Casemix Level 3
.------------------------------------------------------------
UPDEE000  PACK      KEY30,CCECLV1,CCECLV2,CCECLV3,SP70
          MATCH     SP70,KEY30
          GOTO      UPDEE999 IF EQUAL
          CALL      RDCCEE1
          IF        OVRCD=1
            CALL      CLREE000
            UNPACK    KEY30,CCEELV1,CCEELV2,CCEELV3
            CALL      WRCCEE1
            CALL      RDCCEE1
          ENDIF
          ADD       ONE,CCEENEP    * Increase Number of Episodes
          ADD       CCECFTC,CCEEFTC
          ADD       CCECTTC,CCEETTC
          ADD       CCECLOS,CCEELTO
          CALL      UPCCEE1
.
UPDEE999  RETURN
.
.------------------------------------------------------------
. Update Casemix Level 1
.------------------------------------------------------------
UPDEB000  PACK      KEY10,CCECLV1
          CALL      RDCCEB1
          IF        OVRCD=1
            CALL      CLREB000
            MOVE      CCECLV1,CCEBLV1
            CALL      WRCCEB1
            CALL      RDCCEB1
          ENDIF
          ADD       ONE,CCEBNEP    * Increase Number of Episodes
          ADD       CCECFTC,CCEBFTC
          ADD       CCECTTC,CCEBTTC
          ADD       CCECLOS,CCEBLTO
          CALL      UPCCEB1
          RETURN
.------------------------------------------------------------
. Update Casemix Level 2
.------------------------------------------------------------
UPDED000  PACK      KEY20,CCECLV1,CCECLV2,SP70
          MATCH     SP70,KEY20
          GOTO      UPDED999 IF EQUAL
          CALL      RDCCED1
          IF        OVRCD=1
            CALL      CLRED000
            UNPACK    KEY20,CCEDLV1,CCEDLV2
            CALL      WRCCED1
            CALL      RDCCED1
          ENDIF
          ADD       ONE,CCEDNEP    * Increase Number of Episodes
          ADD       CCECFTC,CCEDFTC
          ADD       CCECTTC,CCEDTTC
          ADD       CCECLOS,CCEDLTO
          CALL      UPCCED1
UPDED999  RETURN
.
.------------------------------------------------------------
. Clear Summary File Details
.------------------------------------------------------------
CLREB000  MOVE      ZERO,CCEBNEP
          MOVE      ZERO,CCEBFTC
          MOVE      "999999999999",CCEBFMN
          MOVE      ZERO,CCEBFMX
          MOVE      ZERO,CCEBFSV
          MOVE      ZERO,CCEBTTC
          MOVE      "999999999999",CCEBTMN
          MOVE      ZERO,CCEBTMX
          MOVE      ZERO,CCEBTSV
          MOVE      "999999999999",CCEBLMN
          MOVE      ZERO,CCEBLMX
          MOVE      ZERO,CCEBLSV
          MOVE      ZERO,CCEBLTO
          RETURN
.
CLRED000  MOVE      ZERO,CCEDNEP
          MOVE      ZERO,CCEDFTC
          MOVE      "999999999999",CCEDFMN
          MOVE      ZERO,CCEDFMX
          MOVE      ZERO,CCEDFSV
          MOVE      ZERO,CCEDTTC
          MOVE      "999999999999",CCEDTMN
          MOVE      ZERO,CCEDTMX
          MOVE      ZERO,CCEDTSV
          MOVE      "999999999999",CCEDLMN
          MOVE      ZERO,CCEDLMX
          MOVE      ZERO,CCEDLSV
          MOVE      ZERO,CCEDLTO
          RETURN
.
CLREE000  MOVE      ZERO,CCEENEP
          MOVE      ZERO,CCEEFTC
          MOVE      "999999999999",CCEEFMN
          MOVE      ZERO,CCEEFMX
          MOVE      ZERO,CCEEFSV
          MOVE      ZERO,CCEETTC
          MOVE      "999999999999",CCEETMN
          MOVE      ZERO,CCEETMX
          MOVE      ZERO,CCEETSV
          MOVE      "999999999999",CCEELMN
          MOVE      ZERO,CCEELMX
          MOVE      ZERO,CCEELSV
          MOVE      ZERO,CCEELTO
          RETURN
.
.**************************************************************************
. Calculate Statistics
.**************************************************************************
STAT0000  PACK      KEY10,SP70
          DISPLAY   *P1:24,*EL,"Calculating Statistics : "
          MOVE      ZERO,TOTALEXT
.
          CALL      STEB0000  * Level 1 Min , Max  & Variance Calculation
          CALL      STED0000  * Level 2 Min , Max  & Variance Calculation
          CALL      STEE0000  * Level 3 Min , Max  & Variance Calculation
.
STAT9999  RETURN
.
.**************************************************************************
. Update Variance Casemix Level 1
.**************************************************************************
STEB0000  PACK      KEY10,SP70
          CALL      RDCCEB1
          IF        OVRCD=0
            CALL      RPCCEB1
          ENDIF
.
STEB1000  CALL      RKCCEB1
          BRANCH    OVRCD,STEB9999
.
          ADD       ONE,TOTALEXT
          IF        (TOTALEXT%100=0)
            DISPLAY   *P26:24,*V2LON,TOTALEXT,"/1"
          ENDIF
.
          MOVE      "999999999999",CCEBFMN
          MOVE      ZERO,CCEBFMX
          MOVE      ZERO,CCEBFSV
          MOVE      "999999999999",CCEBTMN
          MOVE      ZERO,CCEBTMX
          MOVE      ZERO,CCEBTSV
          MOVE      "999999999999",CCEBLMN
          MOVE      ZERO,CCEBLMX
          MOVE      ZERO,CCEBLSV
.
          PACK      KEY48,CCEBLV1,SP70
          CALL      RSCCEC2
STEB1100  CALL      RKCCEC2
          BRANCH    OVRCD,STEB1900
          MATCH     CCEBLV1,CCECLV1
          GOTO      STEB1900 IF NOT EQUAL
.
          MATCH     "1",CCECEXC  * Excluded
          GOTO      STEB1100 IF EQUAL
.
          CALL      CHKEB000
.
          ASSIGN    CCECFTC-(CCEBFTC/CCEBNEP),F12P4
          ASSIGN    CCEBFSV+(F12P4*F12P4),CCEBFSV
.
          ASSIGN    CCECTTC-(CCEBTTC/CCEBNEP),F12P4
          ASSIGN    CCEBTSV+(F12P4*F12P4),CCEBTSV
.
          ASSIGN    CCECLOS-(CCEBLTO/CCEBNEP),F12P4
          ASSIGN    CCEBLSV+(F12P4*F12P4),CCEBLSV
.
          GOTO      STEB1100
.
STEB1900  IF        CCEBNEP>1
            ASSIGN    CCEBFSV/(CCEBNEP-1),CCEBFSV
            ASSIGN    CCEBTSV/(CCEBNEP-1),CCEBTSV
            ASSIGN    CCEBLSV/(CCEBNEP-1),CCEBLSV
          ENDIF
          CALL      UPCCEB1
          GOTO      STEB1000
.
STEB9999  RETURN
.
.**************************************************************************
. Update Variance Casemix Level 2
.**************************************************************************
STED0000  PACK      KEY20,SP70
          CALL      RDCCED1
          IF        OVRCD=0
            CALL      RPCCED1
          ENDIF
STED1000  CALL      RKCCED1
          BRANCH    OVRCD,STED9999
.
          ADD       ONE,TOTALEXT
          IF        (TOTALEXT%100=0)
            DISPLAY   *P26:24,*V2LON,TOTALEXT,"/2"
          ENDIF
.
          MOVE      "999999999999",CCEDFMN
          MOVE      ZERO,CCEDFMX
          MOVE      ZERO,CCEDFSV
          MOVE      "999999999999",CCEDTMN
          MOVE      ZERO,CCEDTMX
          MOVE      ZERO,CCEDTSV
          MOVE      "999999999999",CCEDLMN
          MOVE      ZERO,CCEDLMX
          MOVE      ZERO,CCEDLSV
.
          PACK      KEY48,CCEDLV1,CCEDLV2,SP70
          PACK      KEY20,CCEDLV1,CCEDLV2,SP70
          CALL      RSCCEC2
STED1100  CALL      RKCCEC2
          BRANCH    OVRCD,STED1900
          PACK      KEY48,CCECLV1,CCECLV2,SP70
          MATCH     KEY20,KEY48
          GOTO      STED1900 IF NOT EQUAL
.
          MATCH     "1",CCECEXC  * Excluded
          GOTO      STED1100 IF EQUAL
.
          CALL      CHKED000
.
          ASSIGN    CCECFTC-(CCEDFTC/CCEDNEP),F12P4
          ASSIGN    CCEDFSV+(F12P4*F12P4),CCEDFSV
.
          ASSIGN    CCECTTC-(CCEDTTC/CCEDNEP),F12P4
          ASSIGN    CCEDTSV+(F12P4*F12P4),CCEDTSV
.
          ASSIGN    CCECLOS-(CCEDLTO/CCEDNEP),F12P4
          ASSIGN    CCEDLSV+(F12P4*F12P4),CCEDLSV
.
          GOTO      STED1100
.
STED1900  IF        CCEDNEP>1
            ASSIGN    CCEDFSV/(CCEDNEP-1),CCEDFSV
            ASSIGN    CCEDTSV/(CCEDNEP-1),CCEDTSV
            ASSIGN    CCEDLSV/(CCEDNEP-1),CCEDLSV
          ENDIF
          CALL      UPCCED1
          GOTO      STED1000
.
STED9999  RETURN
.
.**************************************************************************
. Update Variance Casemix Level 3
.**************************************************************************
STEE0000  PACK      KEY30,SP70
          CALL      RDCCEE1
          IF        OVRCD=0
            CALL      RPCCEE1
          ENDIF
.
STEE1000  CALL      RKCCEE1
          BRANCH    OVRCD,STEE9999
.
          ADD       ONE,TOTALEXT
          IF        (TOTALEXT%100=0)
            DISPLAY   *P26:24,*V2LON,TOTALEXT,"/3"
          ENDIF
.
          MOVE      "999999999999",CCEEFMN
          MOVE      ZERO,CCEEFMX
          MOVE      ZERO,CCEEFSV
          MOVE      "999999999999",CCEETMN
          MOVE      ZERO,CCEETMX
          MOVE      ZERO,CCEETSV
          MOVE      "999999999999",CCEELMN
          MOVE      ZERO,CCEELMX
          MOVE      ZERO,CCEELSV
.
          PACK      KEY48,CCEELV1,CCEELV2,CCEELV3,SP70
          PACK      KEY30,CCEELV1,CCEELV2,CCEELV3,SP70
          CALL      RSCCEC2
STEE1100  CALL      RKCCEC2
          BRANCH    OVRCD,STEE1900
          PACK      KEY48,CCECLV1,CCECLV2,CCECLV3,SP70
          MATCH     KEY30,KEY48
          GOTO      STEE1900 IF NOT EQUAL
.
          MATCH     "1",CCECEXC  * Excluded
          GOTO      STEE1100 IF EQUAL
.
          CALL      CHKEE000
.
          ASSIGN    CCECFTC-(CCEEFTC/CCEENEP),F12P4
          ASSIGN    CCEEFSV+(F12P4*F12P4),CCEEFSV
.
          ASSIGN    CCECTTC-(CCEETTC/CCEENEP),F12P4
          ASSIGN    CCEETSV+(F12P4*F12P4),CCEETSV
.
          ASSIGN    CCECLOS-(CCEELTO/CCEENEP),F12P4
          ASSIGN    CCEELSV+(F12P4*F12P4),CCEELSV
.
          GOTO      STEE1100
.
STEE1900  IF        CCEENEP>1
            ASSIGN    CCEEFSV/(CCEENEP-1),CCEEFSV
            ASSIGN    CCEETSV/(CCEENEP-1),CCEETSV
            ASSIGN    CCEELSV/(CCEENEP-1),CCEELSV
          ENDIF
          CALL      UPCCEE1
          GOTO      STEE1000
.
STEE9999  RETURN
.
.**************************************************************************
. Check Level 3 Min & Max
.**************************************************************************
CHKEE000  IF        CCECFTC>CCEEFMX
            MOVE      CCECFTC,CCEEFMX
          ENDIF
          IF        CCECFTC<CCEEFMN
            MOVE      CCECFTC,CCEEFMN
          ENDIF
.
          IF        CCECTTC>CCEETMX
            MOVE      CCECTTC,CCEETMX
          ENDIF
          IF        CCECTTC<CCEETMN
            MOVE      CCECTTC,CCEETMN
          ENDIF
.
          IF        CCECLOS>CCEELMX
            MOVE      CCECLOS,CCEELMX
          ENDIF
          IF        CCECLOS<CCEELMN
            MOVE      CCECLOS,CCEELMN
          ENDIF
          RETURN
.**************************************************************************
. Check Level 1 Min & Max
.**************************************************************************
CHKEB000  IF        CCECFTC>CCEBFMX
            MOVE      CCECFTC,CCEBFMX
          ENDIF
          IF        CCECFTC<CCEBFMN
            MOVE      CCECFTC,CCEBFMN
          ENDIF
.
          IF        CCECTTC>CCEBTMX
            MOVE      CCECTTC,CCEBTMX
          ENDIF
          IF        CCECTTC<CCEBTMN
            MOVE      CCECTTC,CCEBTMN
          ENDIF
.
          IF        CCECLOS>CCEBLMX
            MOVE      CCECLOS,CCEBLMX
          ENDIF
          IF        CCECLOS<CCEBLMN
            MOVE      CCECLOS,CCEBLMN
          ENDIF
          RETURN
.**************************************************************************
. Check Min & Max for Level 2
.**************************************************************************
CHKED000  IF        CCECFTC>CCEDFMX
            MOVE      CCECFTC,CCEDFMX
          ENDIF
          IF        CCECFTC<CCEDFMN
            MOVE      CCECFTC,CCEDFMN
          ENDIF
.
          IF        CCECTTC>CCEDTMX
            MOVE      CCECTTC,CCEDTMX
          ENDIF
          IF        CCECTTC<CCEDTMN
            MOVE      CCECTTC,CCEDTMN
          ENDIF
.
          IF        CCECLOS>CCEDLMX
            MOVE      CCECLOS,CCEDLMX
          ENDIF
          IF        CCECLOS<CCEDLMN
            MOVE      CCECLOS,CCEDLMN
          ENDIF
          RETURN
.
.**************************************************************************
. Includes for I/O and Keyins
.**************************************************************************
          INC       CCSCEAIO/INC
          INC       CCSCEBIO/INC
          INC       CCSCECIO/INC
          INC       CCSCEDIO/INC
          INC       CCSCEEIO/INC
          INC       CCSCOPCD
          INC       CCSHCDIO/INC
          INC       CCSSTYIO/INC
          INC       CCSXHDIO/INC
          INC       CIALOSCD
.
          INC       STD001IO
