.------------------------------------------------------------
.  File          :  
.
.  Function      :  Update Keyword Index File for 
.
.  Parameters    : 
.
.  Modifications :
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Procedure to Update
.  a Key Word Index Table that can be used for searching 
.  Masterfiles. The procedure should be called from the 
.  Add, Update and Delete functions in the Masterfile 
.  Maintenance Function.
.  
.  A Key Word Index Table is Defined in the following Manner
.                              
.  DBDKxxxx    DIM       xx    * Code Field Segment 1
.  DBDKyyyy    DIM       xx    * Code Field Segment n
.  DBDKKEYW     DIM       15    * Key Word
.  DBDKSPA     DIM       10    * Spare
.
.  Index 1   DBDKxxxx, DBDKyyyy, DBDKKEYW
.  Index 2   DBDKKEYW, DBDKxxxx, DBDKyyyy
.
.  To convert this Procedure the following should be performed
.    Global Change DEB to the System ID eg :%s/DEB/PAT/g
.    Global Change DKI to the File   ID eg :%s/DKI/DKI/g
.    Global Change DBDK to the IO Call ID eg :%s/DBDK/PTDK/g
.    Modify the KEYINDEX defintion to combined Code Field Lengths eg 6
.    Global Change KEY23 to the Key for the Key Word Table eg KEY21
.    Change xxxxxxx to the Code Fields eg DCODEhh
.    Change the GENR0000 routine to call the BWORD000 for each
.    string of key words to be indexed for the Coded Field
.    eg MOVE    DSNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    DGNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    TDESC,KEYWORDS
.       CALL    BWORD000
.------------------------------------------------------------
          DEFPROC   DEBDKIUP
.
          INC       DEBDKIFD/INC
.
KEYINDEX  DIM       8         * Code Index in Table
KEYWORDS  DIM       60        * String Containing the Key Words to be Indexed
SIXTY     FORM      "60"
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
.------------------------------------------------------------
. Generate Word Index
.------------------------------------------------------------
GENR0000  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     DEBDKIA1,"debdkiaf"
          TRAPCLR  IO
          BRANCH   OVRCD,GENR9999
.
          MOVE     DBDBDEB,KEYINDEX
          CALL     CLEAR000           * Remove Current Key Word Index
.
          MOVE     DBDBDEB,KEY8      * Validate Record on File
          CALL     RDDBDB1 
          BRANCH   OVRCD,GENR1900     * If Not then it Must be a Delete/Clear
.
          MOVE     DBDBDEB,KEYINDEX
          MOVE     DBDBNA1,KEYWORDS
          CALL     BWORD000
.
          MOVE     DBDBDEB,KEYINDEX
          MOVE     DBDBNA2,KEYWORDS
          CALL     BWORD000
.
GENR1900  CLOSE    DEBDKIA1
.
GENR9999  GOTO     DEBDKIEP
.------------------------------------------------------------
. Clear Current Key Words
.------------------------------------------------------------
CLEAR000  PACK     KEY23,KEYINDEX,SP70
          CALL     RSDBDK1
          CALL     RKDBDK1
          BRANCH   OVRCD,CLEAR999
          MATCH    KEYINDEX,DBDKDEB
          GOTO     CLEAR999 IF NOT EQUAL
.
          PACK     KEY23,DBDKDEB,DBDKKWD,SP70
          CALL     DEDBDK1
          GOTO     CLEAR000
.
CLEAR999  RETURN
.-------------------------------------------------
. Determine Words to Index
.-------------------------------------------------
BWORD000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      BWORD000 IF NOT EOS
            GOTO      BWORD999               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWORDS,SP30      * reset form pointer
          MOVE      KEY60,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      BWORD100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Save this Word
.
BWORD100  MOVE      KEYWORDS,KEY15
          PACK      KEY15,KEY15,SP70
          REP       UPPLOW,KEY15             * Always Upper Case
          PACK      KEY23,KEYINDEX,KEY15
          UNPACK    KEY23,DBDKDEB,DBDKKWD
          CALL      RDDBDK1
          IF        OVRCD=1
            CALL      WRDBDK1
          ENDIF
          GOTO      BWORD190
.
.         Check for any more words
.
BWORD190  SETLPTR   KEYWORDS,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY60,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY60,KEYWORDS
          GOTO      BWORD000
.
BWORD999  RETURN
.
          INC       DEBDKIIO/INC
          INC       IBAOVRIO/INC
.
DEBDKIEP  ENDPROC                          * End of Procedure
.
