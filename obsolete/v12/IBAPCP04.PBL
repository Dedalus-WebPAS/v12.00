.******************************************************************************
.* System         : Patient Care Plans                                        *
.* Program        : IBAPCP04.PBL                                              *
.* Name           : Frequency Codes Maintenance                               *
.******************************************************************************
.* Date           : 19/11/1991    (Pinky's Birthday)                          *
.* Author         : Justin Coates                                             *
.* Function       : To maintain the PCPFRAFD and PCPFRBFD files               *
.* Modifications  :                                                           *
.*        V9.02.00  30/12/2002 Tracey Nguyen srf 23497                        *
.*                  r5.10 Care Plans Port to V9                               *
.*----------------------------------------------------------------------------*
.*        V5.01.02  07/02/1992 Darren Jones                                   *
.*                  Fix delete routine                                        *
.*        V5.01.03  21/04/1992    Justin Coates                               *
.*                  Check careplans/dep interface before doing second screen  *
.*        V5.01.04  01/12/1993    Justin Coates  Quote 7841 SRF 123724        *
.*                  add keyin of the once-only field                          *
.******************************************************************************
.
. ------- FD includes needed -------
.
          INC       STD001FD                * standard variables
          INC       PATCODFD/INC            * codes file
          INC       PCPFRAFD/INC            * frequency description file
          INC       PCPFRBFD/INC            * frequency/shift file
.
. ------- program variables -------
.
AMOUNT    FORM      3         * amount per shift
CODE      DIM       2         * for patdscod
CODEFREQ  DIM       9         * entered frequency code
CODESHFT  DIM       3         * entered shift code
CODEONCE  DIM       1         * entered once only code
DESCFREQ  DIM       30        * entered frequency description
DESCSHFT  DIM       30        * entered shift description
DESCONCE  DIM       3         * description for once only
DIM25     DIM       25
DPCNCARE  FORM      1         * using dependancy interface ??
SCRNFLAG  FORM      1         * ? performed flag
.
. ------- program constants -------
.
CATDS     INIT      "DS"
.
PRGID     INIT      "IBAPCP04"
PRGNAM    INIT      "Frequency File Maintenance"
VERSION   INIT      "V12.00.00"
+
.*********************************************************************
.*                  ML0000                                           *
.*        Mainline Code / Controlling Logic                          *
.*********************************************************************
ML0000    CALL      INIT0000               * initialization and open files
.
ML0100    CALL      MENU0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    MOPTION,ML1000,ML2000,ML3000
.
. ------- add an item code -------
.
ML1000    CALL      ADDI0000               * add an item
          GOTO      ML0100                 * go back to menu
.
. ------- change an item code -------
.
ML2000    CALL      MODI0000               * change an item
          GOTO      ML0100                 * go back to menu
.
. ------- delete an item code -------
.
ML3000    CALL      DELI0000               * delete an item
          GOTO      ML0100                 * go back to menu
.
ML9999    CHAIN     PGM                    * chain back to program
+
.*********************************************************************
.*                  INIT0000                    Called by: ML0000    *
.*        Initialization and open files                              *
.*********************************************************************
INIT0000  CALL      DISPHEAD                * display heading
          MOVE      ONE,CNEWDS              * new ? option
          DISPLAY   *P50:24,"Opening ",*EL;
          DISPLAY   *P58:24,"patcodes"
          OPEN      PATCODE1,"patcodes"     * codes file
          OPEN      PATCODE2,"patcodes"     * codes file
          DISPLAY   *P58:24,"pcpfraaf"
          OPEN      PCPFRAA1,"pcpfraaf"     * freq. description file
          DISPLAY   *P58:24,"pcpfrbaf"
          OPEN      PCPFRBA1,"pcpfrbaf"     * freq/shift file
.
          DISPLAY   *P58:24,"controlf"
          OPEN      CONTROLF,"controlf"     * freq/shift file
          READ      CONTROLF,SIXTY3;*86,DPCNCARE
.
INIT9999  RETURN
+
.*********************************************************************
.*                  MENU0000                    Called by: ML0000    *
.*        Enter the option to perform                                *
.*        Returns : EXIT = 1      Exit selected                      *
.*                  MOPTION = 1   Insert selected                    *
.*                            2   Change selected                    *
.*                            3   Delete selected                    *
.*********************************************************************
MENU0000  HLINE     *G37,2,53,80
          DISPLAY   *P1:3,*EF,*V2LON:
                    *P1:4,"0",*P1:5,"1",*P1:6,"2",*P1:7,"3",*HOFF:
                    *P2:4," = Exit":
                    *P2:5," = Insert":
                    *P2:6," = Change":
                    *P2:7," = Delete":
                    *P1:9,"Select option : "
.
MENU1000  KEYIN     *P17:9,*V2LON,MOPTION
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,MENU9999,MENU9999,MENU9999
          MOVE      ONE,EXIT
          COMPARE   ZERO,MOPTION
          GOTO      MENU9999 IF EQUAL
          BEEP
          GOTO      MENU1000
MENU9999  RETURN
+
.*********************************************************************
.*                  ADDI0000                    Called by : ML1000   *
.*        Add a frequency code and description and shift codes       *
.*********************************************************************
ADDI0000  DISPLAY   *P53:2,*V2LON,"- Insert "
          CALL      CLRA0000                * clear variables
          CALL      SCRA0000                * display screen
          CALL      KFRQ0000                * enter frequency code
          BRANCH    EXIT,ADDI9999           * nothing entered
.
ADDI1000  CALL      KFDX0000                * enter description
          BRANCH    EXIT,ADDI0000           * nothing entered
          CALL      KONC0000                * enter description
          BRANCH    EXIT,ADDI1000           * nothing entered
.
ADDI2000  CALL      MAINQST
          BRANCH    CCITEM,ADDI2100,ADDI2200
.
          COMPARE   ZERO,CCITEM
          GOTO      ADDI5000 IF EQUAL       * Post
          GOTO      ADDI0000 IF LESS        * Cancel
.
          BEEP
          GOTO      ADDI2000
.
ADDI2100  CALL      KFDX0000                * enter desc
          BRANCH    EXIT,ADDI2100           * must enter something
          GOTO      ADDI2000
ADDI2200  CALL      KONC0000                * enter once only
          BRANCH    EXIT,ADDI2200           * must enter something
          GOTO      ADDI2000
.
. ******* enter all the shift codes *******
.
ADDI5000  COMPARE   ONE,DPCNCARE
          GOTO      ADDI8000 IF NOT EQUAL   * not using interface
.
          CALL      PIOO0000                * post to frb if once only code
          BRANCH    EXIT,ADDI0000           * a once only 
.
          CALL      SCRB0000                * display screen
.
ADDI5500  CALL      KSFT0000                * enter shift
          BRANCH    EXIT,ADDI0000:          * nothing entered
                         ADDI7000           * code already on file
.
          CALL      KAMT0000                * enter amount
.
ADDI7000  CALL      MAINQST
          BRANCH    CCITEM,ADDI7100         * change amount
.
          COMPARE   ZERO,CCITEM
          GOTO      ADDI8000 IF EQUAL       * Post
          GOTO      ADDI0000 IF LESS        * Cancel
.
          BEEP 
          GOTO      ADDI7000
.
ADDI7100  CALL      KAMT0000                * enter amount
          GOTO      ADDI7000
.
.         Post selected
.
ADDI8000  CALL      UPDT0000                * write a new record
          BRANCH    DPCNCARE,ADDI5500
          GOTO      ADDI0000
.
ADDI9999  RETURN
+
.*********************************************************************
.*                  MODI0000                    Called by : ML2000   *
.*        Modify frequency codes and add/change freq/shift codes     *
.*********************************************************************
MODI0000  DISPLAY   *P53:2,*V2LON,"- Change "
          CALL      CLRA0000                * clear variables
          CALL      SCRA0000                * display screen
          CALL      KFRQ0000                * enter frequency code
          BRANCH    EXIT,MODI9999           * nothing entered
.
MODI2000  CALL      MAINQST
          BRANCH    CCITEM,MODI2100,MODI2200
.
          COMPARE   ZERO,CCITEM
          GOTO      MODI5000 IF EQUAL       * Post
          GOTO      MODI0000 IF LESS        * Cancel
.
          BEEP
          GOTO      MODI2000
.
MODI2100  CALL      KFDX0000                * enter desc
          BRANCH    EXIT,MODI2100           * must enter something
          GOTO      MODI2000
.
MODI2200  CALL      KONC0000                * enter once only
          BRANCH    EXIT,MODI2200           * must enter something
          GOTO      MODI2000
.
. ******* enter all the shift codes *******
.
MODI5000  COMPARE   ONE,DPCNCARE
          GOTO      MODI8000 IF NOT EQUAL   * not using interface
.
          CALL      PIOO0000                * post to frb if once only code
          BRANCH    EXIT,MODI0000           * a once only 
.
          CALL      SCRB0000                * display screen
.
MODI5500  CALL      KSFT0000                * enter shift
          BRANCH    EXIT,MODI0000:          * nothing entered
                         MODI7000           * code is on file
.
          CALL      KAMT0000                * enter amount
.
MODI7000  CALL      MAINQST
          BRANCH    CCITEM,MODI7100         * change amount
.
          COMPARE   ZERO,CCITEM
          GOTO      MODI8000 IF EQUAL       * Post
          GOTO      MODI0000 IF LESS        * Cancel
.
          BEEP 
          GOTO      MODI7000
.
MODI7100  CALL      KAMT0000                * enter amount
          GOTO      MODI7000
.
.         Post selected
.
MODI8000  CALL      UPDT0000                * update record
          BRANCH    DPCNCARE,MODI5500
          GOTO      MODI0000
.
MODI9999  RETURN
+
.*********************************************************************
.*                  DELI0000                    Called by : ML3000   *
.*        Delete an item code                                        *
.*********************************************************************
DELI0000  DISPLAY   *P53:2,*V2LON,"- Delete "
          CALL      CLRA0000                * clear variables
          CALL      SCRA0000                * display screen
          CALL      KFRQ0000                * enter frequency code
          BRANCH    EXIT,DELI9999           * nothing entered
.
DELI2000  CALL      DELQST 
          BRANCH    CEXIT,DELI8000,DELI0000,DELI9999
.
DELI8000  CALL      DELT0000                * delete a record
          GOTO      DELI0000
.
DELI9999  RETURN
+
.*********************************************************************
.*                  CLRA0000                    Called by : ADDI0000 *
.*        Clear all the input variables            MODI0000 DELI0000 *
.*********************************************************************
CLRA0000  MOVE      SP10,CODEFREQ
          MOVE      SP30,DESCFREQ
          MOVE      ZERO,AMOUNT
          UNPACK    SP30,CODESHFT,DESCSHFT
          MOVE      SP30,DESCFREQ
          UNPACK    SP5,CODEONCE,DESCONCE
CLRA9999  RETURN
+
.*********************************************************************
.*                  SCRA0000                    Called by : ADDI0000 *
.*        Display the screen for the key variables                   *
.*********************************************************************
SCRA0000  DISPLAY   *P1:3,*EF:
                    *P4:4,"Frequency Code        : ",*V2LON,CODEFREQ,*HOFF:
                    *P1:6,*V2LON,"1",*HOFF:
                    ". Frequency Description : ",*V2LON,DESCFREQ,*HOFF:
                    *P1:7,*V2LON,"2",*HOFF:
                    ". Once Only Code        : ",*V2LON,DESCONCE,*HOFF
                    
SCRA9999  RETURN
+
.*********************************************************************
.*                  KSFT0000                   Called by : ....0000 *
.*        Keyin the shift code                                      *
.*        Returns : EXIT  = 1     nothing entered                   *
.*                  CODESHFT      the entered code                  *
.*                  DESCSHFT      the description                   *
.********************************************************************
KSFT0000  MOVE      ONE,SCRNFLAG            * set ? as not performed
          MOVE      "5",CVERT               * row
          MOVE      "21",CCOL               * column
          MOVE      CATDS,CODE              * set the code
.
KSFT1000  KEYIN     *PCCOL:CVERT,*DV,UNDLN3,*EL:
                    *PCCOL:CVERT,*V2LON,CODESHFT:
                    *PCCOL:CVERT,*DV,CODESHFT;
.
          ENDSET    CODESHFT
          APPEND    SP3,CODESHFT
          RESET     CODESHFT
.
          MATCH     SP3,CODESHFT 
          GOTO      KSFT7900 IF EQUAL       * nothing entered
.
          MATCH     QUEST,CODESHFT
          GOTO      KSFT2000 IF NOT EQUAL   * ? not entered
.
.         ? entered
.
          CALL      PATCODDS                * ? routine
          MOVE      TWENTY4,CVERT           * row for input
          MOVE      TEN8,CCOL               * column for input
          MOVE      ZERO,SCRNFLAG           * set ? as performed
          DISPLAY   *P1:24,"Input the code : ",*EL
          GOTO      KSFT1000
.
.         validate what was entered
.
KSFT2000  PACK      KEY5,CODE,CODESHFT      * key of entered value
          CALL      RDCODE1                 * read code file
          BRANCH    OVRCD,KSFT7000          * invalid input
.
          MOVE      TDESC,DESCSHFT          * save description
          DISPLAY   *P32:5,DESCSHFT
.
.         get the amount if code is on file
.
          MOVE      FALSE,EXIT              * valid input
          MOVE      ZERO,PCFBNUMB           * clear amount
          PACK      KEY12,CODEFREQ,CODESHFT
          CALL      RDPCFRB1
          BRANCH    OVRCD,KSFT8000          * no amount on file - insert
.
          MOVE      PCFBNUMB,AMOUNT         * set amount
          DISPLAY   *P21:7,*V2LON,AMOUNT
          MOVE      TWO,EXIT                * change mode
          GOTO      KSFT8000                * valid input
.
.         ERROR: invalid code entered
.
KSFT7000  DISPLAY   *P1:24,*EL,*B,"Invalid code.  ",*EL;
          CALL      HITENTER
          BRANCH    SCRNFLAG,KSFT1000       * ? not entered
          DISPLAY   *P1:24,"Input the code : ",*EL
          GOTO      KSFT1000    
.
.         nothing was entered
.
KSFT7900  MOVE      SP20,DESCSHFT           * clear the description
          MOVE      SP3,CODESHFT            * clear the code
          MOVE      ONE,EXIT                * set exit flag
.
.         valid code entered
.
KSFT8000  BRANCH    SCRNFLAG,KSFT9999       * ? not entered
.
          CALL      SCRB0000                * redisplay the screen
.
KSFT9999  RETURN
+
.*********************************************************************
.*                  SCRB0000                    Called by : xxxx0000 *
.*        Display the screen for non-key information                 *
.*********************************************************************
SCRB0000  DISPLAY   *P1:3,*EF:
                    *P4:4,"Frequency Code : ",*V2LON,CODEFREQ,*HOFF,SP2:
                    DESCFREQ:
                    *P4:5,"Shift Code     : ",*V2LON,CODESHFT,*HOFF,SP2:
                    DESCSHFT:
                    *P1:7,*V2LON,"1",*HOFF,". Amount         : "
SCRB9999  RETURN
+
.*********************************************************************
.*                  KFRQ0000                    Called by : xxxx0000 *
.*        Keyin the frequency code making sure it doesn't exist for  *
.*        insert mode and it does exist for change/delete mode       *
.*        Returns : EXIT = 1      nothing entered                    *
.*********************************************************************
KFRQ0000  MOVE      "28",CCOL
          MOVE      "4",CVERT
          MOVE      ZERO,HLEF               * save whole screen
          MOVE      ONE,SCRNFLAG            * set as ? not entered
.
KFRQ1000  KEYIN     *PCCOL:CVERT,*V2LON,CODEFREQ:
                    *PCCOL:CVERT,*DV,CODEFREQ
.
          ENDSET    CODEFREQ
          APPEND    SP9,CODEFREQ
          RESET     CODEFREQ
.
          MATCH     SP9,CODEFREQ
          GOTO      KFRQ9100 IF EQUAL       * nothing entered
.
          MATCH     QUEST,CODEFREQ
          GOTO      KFRQ2000 IF NOT EQUAL   * ? entered
. 
.         ? entered
.
          PERFORM   SCRNFLAG,GETSCR00       * save screen if needed
          CALL      PCPFRADS                * display codes
          DISPLAY   *P1:24,*EL,"Enter the code : ";
          MOVE      "24",CVERT
          MOVE      "18",CCOL
          MOVE      ZERO,SCRNFLAG
          GOTO      KFRQ1000
.
.         validate code
.
KFRQ2000  MOVE      CODEFREQ,KEY9
          CALL      RDPCFRA1
          BRANCH    OVRCD,KFRQ5000          * code is not on file
.
. ******* code is on file *******
.
          MOVE      PCFADESC,DESCFREQ       * set the description
          MOVE      PCFAONCE,CODEONCE       * set the once only code
          MOVE      SP3,DESCONCE
          MATCH     ANSY,CODEONCE
          IF        @EQUAL
            MOVE      "Yes",DESCONCE
          ELSE 
            MOVE      "No ",DESCONCE
            MOVE      ANSN,CODEONCE
          ENDIF
.
          COMPARE   ONE,MOPTION
          GOTO      KFRQ9000 IF NOT EQUAL   * code should be on file
.         
          DISPLAY   *P1:24,*B,"Frequency code is already on file.  ",*EL;
          CALL      HITENTER
          BRANCH    SCRNFLAG,KFRQ1000
          DISPLAY   *P1:24,*EL,"Enter the code : ";
          GOTO      KFRQ1000
.
. ******* code isnt on file *******
.
KFRQ5000  COMPARE   ONE,MOPTION
          GOTO      KFRQ9000 IF EQUAL       * code shouldn't be on file
.
          DISPLAY   *P1:24,*B,"Invalid Frequency code.  ",*EL;
          CALL      HITENTER 
          BRANCH    SCRNFLAG,KFRQ1000
          DISPLAY   *P1:24,*EL,"Enter the code : ";
          GOTO      KFRQ1000
.
.         set exit flags
.
KFRQ9000  IF        SCRNFLAG = 0
            CALL      PUTSCR00
          ENDIF
          DISPLAY   *P28:4,*V2LON,CODEFREQ,*P28:6,DESCFREQ,*P28:7,DESCONCE
          MOVE      ZERO,EXIT
          GOTO      KFRQ9999
.
KFRQ9100  MOVE      ONE,EXIT
.
KFRQ9999  RETURN
+
.*********************************************************************
.*                  KFDX0000                    Called by : xxxx0000 *
.*        Keyin the frequency description                            *
.*        Returns : EXIT = 1      nothing entered                    *
.*********************************************************************
KFDX0000  KEYIN     *P28:6,*V2LON,DESCFREQ:
                    *P28:6,*DV,DESCFREQ
.
          PACK      DESCFREQ,DESCFREQ,SP30
          MOVE      ONE,EXIT
          MATCH     SP30,DESCFREQ
          GOTO      KFDX9999 IF EQUAL
          MOVE      ZERO,EXIT
.
KFDX9999  RETURN
+
.*********************************************************************
.*                  KONC0000                    Called by : xxxx0000 *
.*        Keyin the once only code                                   *
.*********************************************************************
KONC0000  KEYIN     *P28:7,*V2LON,CODEONCE:
                    *P28:7,*DV,CODEONCE
          REP       UPPLOW,CODEONCE
          MOVE      ONE,EXIT
          MOVE      SP3,DESCONCE
          MATCH     SP1,CODEONCE
          GOTO      KONC9000 IF EQUAL
.
          MOVE      ZERO,EXIT
          MOVE      "Yes",DESCONCE
          MATCH     ANSY,CODEONCE
          GOTO      KONC9000 IF EQUAL
          MOVE      "No ",DESCONCE
          MATCH     ANSN,CODEONCE
          GOTO      KONC9000 IF EQUAL
.
          BEEP
          GOTO      KONC0000
.
KONC9000  DISPLAY   *P28:7,*V2LON,DESCONCE
KONC9999  RETURN
+
.*********************************************************************
.*                  KAMT0000                    Called by : xxxx0000 *
.*        Keyin the amount                                           *
.*********************************************************************
KAMT0000  KEYIN     *P21:7,*DV,AMOUNT:
                    *P21:7,*V2LON,*RV,AMOUNT:
                    *P21:7,*DV,AMOUNT
KAMT9999  RETURN
+
.*********************************************************************
.         maintain the pcpfraaf
.*********************************************************************
MFRA0000  MOVE      CODEFREQ,KEY9
          CALL      RDPCFRA1
.
          MOVE      CODEFREQ,PCFACODE
          MOVE      DESCFREQ,PCFADESC
          MOVE      CODEONCE,PCFAONCE
.
          IF        OVRCD = ONE
            CALL      WRPCFRA1
          ELSE
            CALL      UPPCFRA1
          ENDIF
.
MFRA9999  RETURN
+
.*********************************************************************
.         maintain the pcpfrbaf
.*********************************************************************
MFRB0000  PACK      KEY12,CODEFREQ,CODESHFT
          CALL      RDPCFRB1
.
          MOVE      CODEFREQ,PCFBCODE
          MOVE      CODESHFT,PCFBSHFT
          MOVE      AMOUNT,PCFBNUMB
          MOVE      SP20,PCFBSPAR
.
          IF        OVRCD = ONE
            CALL      WRPCFRB1                * write to frb
          ELSE
            CALL      UPPCFRB1                * update frb
          ENDIF
.
MFRB9999  RETURN
+
.*********************************************************************
.         post to pcpfrbaf if a once only code
.*********************************************************************
PIOO0000  MOVE      ZERO,EXIT               * set as not once only
          MATCH     ANSY,CODEONCE
          GOTO      PIOO9999 IF NOT EQUAL   * not a once only
.
.         set up all the shifts with an amount of zero
.
          IF        OPTION = TWO
            CALL      MFRA0000                * maintain the pcpfraaf
          ENDIF
.
          MOVE      ONE,EXIT
          PACK      KEY5,CATDS,SP3
          CALL      RDCODE1
          BRANCH    OVRCD,PIOO9999          * no codes set up
.
PIOO1000  CALL      RDKCODE1
          BRANCH    OVRCD,PIOO9999          * end of file
          MATCH     CATDS,TCODE
          GOTO      PIOO9999 IF NOT EQUAL
.
          MOVE      ACODE,CODESHFT          * the shift code
          MOVE      ZERO,AMOUNT             * clear amount
          CALL      MFRB0000                * maintain the pcpfrbaf
          GOTO      PIOO1000
.
PIOO9999  RETURN
+
.*********************************************************************
.*                  FIWK0000                    Called by : MODI0000 *
.*        Copy FIle variables to WorK variables             DELI0000 *
.*********************************************************************
FIWK0000  MOVE      PCFACODE,CODEFREQ
          MOVE      PCFADESC,DESCFREQ
          MOVE      PCFBSHFT,CODESHFT
          MOVE      PCFBNUMB,AMOUNT
          MOVE      "Invalid Code",TDESC
          PACK      KEY5,CATDS,CODESHFT
          CALL      RDCODE1
          MOVE      TDESC,DESCSHFT
FIWK9999  RETURN
+
.*********************************************************************
.         maintain the two files
.*********************************************************************
UPDT0000  DISPLAY   *P50:24,*V2LON,"*** POSTING ***"
          CALL      MFRA0000                * maintain the pcpfraaf
.
          COMPARE   ONE,DPCNCARE
          GOTO      UPDT8000 IF NOT EQUAL   * not using interface
.
          CALL      MFRB0000                * maintain the pcpfrbaf
.
UPDT8000  DISPLAY   *P21:5,*EL,*P21:7,*EL,*P1:24,*EL;
.
UPDT9999  RETURN
+
.*********************************************************************
.*                  DELT0000                    Called by : DELI0000 *
.*        Delete a record                                            *
.*********************************************************************
DELT0000  DISPLAY   *P50:24,*V2LON,"*** DELETING ***"
          MOVE      CODEFREQ,KEY9
          CALL      DEPCFRA1                * delete the item
.
DELT1000  PACK      KEY12,CODEFREQ,SP10
          CALL      RSPCFRB1
          CALL      RKPCFRB1
          BRANCH    OVRCD,DELT9000
          MATCH     CODEFREQ,PCFBCODE
          GOTO      DELT9000 IF NOT EQUAL
          PACK      KEY12,PCFBCODE,PCFBSHFT
          CALL      DEPCFRB1                * delete the item
          GOTO      DELT1000
.
DELT9000  DISPLAY   *P21:5,*EL,*P21:7,*EL,*P1:24,*EL;
.
DELT9999  RETURN
+
.
. ------- I/O includes needed -------
.
          INC       STD001IO                * all standard routines
          INC       PATCODKY/PBL            * ? for codes file
          INC       PATCODIO/INC            * codes file
          INC       PCPFRADS/PBL            * ? for frequency description file
          INC       PCPFRAIO/INC            * frequency description file
          INC       PCPFRBIO/INC            * frequency/shift file
................................................................................
