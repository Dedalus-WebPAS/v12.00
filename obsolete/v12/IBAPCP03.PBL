.***************************************************************************
.* System    :   PATIENT CARE PLANS                                        *
.* Program   :   IBAPCP03                                                  *
.* Desc      :   Nurses Maintenance                                        *
.***************************************************************************
.* Date      :   17/01/90                                                  *
.* Author    :   Paul Duncan                                               *
.* Function  :   This program is used to maintain the patient care plan    *
.* Modifications :                                                         *
.*      V9.02.00  30/12/2002 Tracey Nguyen srf 23497                       *
.*                r5.10 Care Plans Port to V9                              *
.*-------------------------------------------------------------------------*
.*               nurses file.                                              *
.* Mods      :   V5.04.01 09/09/1996  Steve Armstrong     STH Mods         *
.*                        Added PCNUSECL                                   *
.***************************************************************************
.
         INC       STD001FD
         INC       PCPNURFD/INC
         INC       PATCODFD/INC
.
. *************************************************************************
. *                             CONSTANTS                                 *
. *************************************************************************
SD          INIT      "SD"  
. *************************************************************************
  *                             VARIABLES                                 *
. *************************************************************************
CODE        DIM       2
.
NCODE       DIM       6
NNAME       DIM       20
.
SECLEVL     FORM      1
SLEVEL      DIM       3
.
. *************************************************************************
.
PRGNAM    INIT      "Nurses Maintainence"
PRGID     INIT      "IBAPCP03"
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              MAINLINE                                  *
.*                            Controlling Logic                           *
.**************************************************************************
            CALL      INIT0000               * initialization and open files
.
ML0100      CALL      CLR0000                * clear input variables
            CALL      OPTN0000               * select options
            BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
            BRANCH    OPTION,ML1000,ML2000,ML3000
.
. ---- add a procedure code ----
.
ML1000     CALL       SCR0000                * display screen format
           CALL       PROC0000               * input procedure code
           BRANCH     EXIT,ML0100 
           CALL       ACPT0000               * accept initial values
ML1100     CALL       SEL0000                * select field to modify
           BRANCH     EXIT,ML1000
           CALL       WRT0000                * write to file
           GOTO       ML1000
.
. ---- modify a procedure code ----
.
ML2000     CALL       SCR0000                * display screen format 
           CALL       PROC0000               * input procedure code
           BRANCH     EXIT,ML0100
           CALL       DISP0000               * display details
ML2100     CALL       SEL0000                * select field to modify
           BRANCH     EXIT,ML2000
           CALL       UPD0000                * update record
           GOTO       ML2000
.
. ---- delete a procedure code ----
.
ML3000     CALL       SCR0000                * display screen format 
           CALL       PROC0000               * input procedure code
           BRANCH     EXIT,ML0100
           CALL       DISP0000               * display detail 
           CALL       DELQST                 * ok to delete ? 
           BRANCH     CEXIT,ML3100,ML3000,ML0100
ML3100     CALL       DEL0000                * delete this record
           GOTO       ML3000
.
ML9999     CHAIN      PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000                                  *
.*                             initialization                               *
.*  The initialization routine is used to display headings and open files.  *
.*  Any other general initialization can also be performed here.            *
.****************************************************************************
INIT0000   
. ----  set up headings ----
. 
           CALL      DISPHEAD                  * display heading
.
           DISPLAY   *P50:24,"Opening patcodes"
           OPEN      PATCODE1,"patcodes"
.
           DISPLAY   *P50:24,"Opening pcpnuraf"
           OPEN      PCPNURA1,"pcpnuraf"
.
          MOVE      ONE,CNEWDS
.
INIT9999   DISPLAY    *P1:24,*EL
           RETURN
+
.****************************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.*                                                                          *
.*    valid options     --->  (0) Exit                                      *
.*                            (1) Insert                                    *
.*                            (2) Change                                    *
.*                            (3) Delete                                    *
.****************************************************************************
.
. ---- display option screen ----
.
OPTN0000    CALL      SELOPTN                       * select option
            COMPARE   ZERO,COPTION
            GOTO      OPTN9000 IF EQUAL                 * exit      
            MOVE      FALSE,EXIT
            GOTO      OPTN9999
.
OPTN9000    MOVE      TRUE,EXIT                   * set EXIT = 1 
OPTN9999    RETURN
+
.**************************************************************************
.*                                  CLR0000                               *
.*                            clear global variables                      *
.**************************************************************************
.
CLR0000     UNPACK    SP30,NCODE,NNAME,SLEVEL
            MOVE      ZERO,SECLEVL
.
CLR9999     RETURN
+
.**************************************************************************
.*                               PROC0000                                 *
.*                       input indentification code                       *
.**************************************************************************
PROC0000    KEYIN     *P43:4,*EL,*DV,UNDLN6:
                      *P43:4,*V2LON,NCODE:
                      *P43:4,*DV,NCODE
.
            PACK      NCODE,NCODE,SP6
.
            MATCH     SP6,NCODE             *nothing entered?
            GOTO      PROC9000 IF EQUAL
.
            BRANCH    COPTION,PROC0200,PROC0300,PROC0300
.
PROC0200    MATCH     QUEST,NCODE           * questin mark not allowed
            GOTO      PROC1000 IF EQUAL
.
            MATCH     EXITCHAR,NCODE        * exitchar not allowed
            GOTO      PROC9000 IF EQUAL
.
            PACK      KEY6,NCODE            * validate against file
            CALL      RAPCNUR1
            BRANCH    OVRCD,PROC0500
            GOTO      PROC2000              * already on file for insert
.
PROC0300    MOVE      NCODE,KEY6
            CALL      RAPCNUR1
            BRANCH    OVRCD,PROC3000
            GOTO      PROC0500              * not on file for update
.
PROC0500    MOVE      ZERO,EXIT
            GOTO      PROC9999
.
PROC1000    DISPLAY   *P1:24,*B,*V2LON,"Invalid Code. ";
            CALL      HITENTER
            GOTO      PROC0000
.
PROC2000    DISPLAY   *P1:24,*B,*V2LON,"Code already on file. ";
            CALL      HITENTER
            GOTO      PROC0000
.
PROC3000    DISPLAY   *P1:24,*B,*V2LON,"Code not on file. ";
            CALL      HITENTER
            GOTO      PROC0000
.
PROC9000    MOVE     ONE,EXIT
.
PROC9999    RETURN

+
.**************************************************************************
.*                                  SCR0000                               *
.*                         display screen format                          *
.**************************************************************************
SCR0000     DISPLAY   *P1:3,*EF:
                      *P4:4,"Nurses code",*P41:4,COLON:
                      *P1:6,*V2LON,ONE,*HOFF,". Nurses Name",*P41:6,COLON:
                      *P1:7,*V2LON,TWO,*HOFF,". Staff Level",*P41:7,COLON:
                      *P1:8,*V2LON,THREE,*HOFF,". Security Level for ":
                      "Reactivate option : "
.
SCR9999  RETURN
+
.*************************************************************************
.*                                  ACPT0000                             *
.*   INPUT mode only :      accept initial values                        *
.*************************************************************************
ACPT0000    CALL      NAME0000              * get name
.
ACPT2000    CALL      SLEV0000              * get staff level
.
ACPT4000    CALL      SECU0000              * get security level for
.                                                  Reactivate option
.
ACPT9999    RETURN
.

.*************************************************************************
.*                                  NAME0000                             *
.*                                Get new name                           *
.*************************************************************************
NAME0000    KEYIN     *P43:6,"____________________":
                      *P43:6,*V2LON,NNAME,*DV,*P43:6,NNAME
.
            ENDSET    NNAME
            APPEND    SP20,NNAME
            RESET     NNAME
.
            MATCH     SP20,NNAME            * nothing entered
            GOTO      NAME0000 IF EQUAL
. 
            MOVE      NNAME,PCNUNAME
.
NAME9999    RETURN
. 
.*************************************************************************
.*                                  SLEV0000                             *
.*                                Get staff level                        *
.*************************************************************************
SLEV0000    KEYIN     *P43:7,*EL,"___":
                      *P43:7,*V2LON,SLEVEL,*P43:7,*DV,SLEVEL
.
            ENDSET    SLEVEL
            APPEND    SP5,SLEVEL
            RESET     SLEVEL
.
            MATCH     SP3,SLEVEL            * nothing entered?
            GOTO      SLEV9999 IF EQUAL
.
            MATCH     QUEST,SLEVEL          * quetion mark routine
            CALL      QEST0000 IF EQUAL
.
            PACK      KEY5,SD,SLEVEL        
            CALL      RDCODE1
            BRANCH    OVRCD,SLEV3000        * not on file
.
            DISPLAY   *P50:7,TDESC
.
            MOVE      SLEVEL,PCNUCLSS       * move to variable used in write
            GOTO      SLEV9999
.
SLEV3000    DISPLAY   *P1:24,*B,*V2LON,"Invalid Code .";
            CALL      HITENTER
            DISPLAY   *P43:7,*EL,"___"
            GOTO      SLEV0000  
.
SLEV9999    RETURN
+         
.**************************************************************************
.*                             SECU0000                                   *
.*                      get security level for Reactivate option          *
.**************************************************************************
.
SECU0000    KEYIN     *P43:8,*V2LON,*DE,SECLEVL:
                      *P43:8,*DV,SECLEVL
.
            MOVE      SECLEVL,PCNUSECL
.
SECU9999  RETURN
+
.**************************************************************************
.*                                   SEL0000                              *
.*                         select field to modify or post                 *
.**************************************************************************
SEL0000     CALL      MAINQST
.
            BRANCH    CCITEM,SEL1000,SEL1500,SEL1600
.
            COMPARE   ZERO,CCITEM           * post
            GOTO      SEL9000 IF EQUAL
.
            COMPARE   "-1",CCITEM           * cancel
            GOTO      SEL2000 IF EQUAL
.
            GOTO      SEL0000
.
.*** Change name ***
.
SEL1000     CALL      NAME0000
            GOTO      SEL0000  
.
.*** Change staff level ***
.
SEL1500     CALL      SLEV0000
            GOTO      SEL0000
.
.           Change Security Level for Reactivate option
.
SEL1600     CALL      SECU0000
            GOTO      SEL0000
.
SEL2000     MOVE      ONE,EXIT
            GOTO      SEL9999
.
SEL9000     MOVE      ZERO,EXIT
.
SEL9999  RETURN
+
.**************************************************************************
.*                              WRT0000                                   *
.*                          write a new record                            *
.**************************************************************************
.
WRT0000     MOVE      NCODE,PCNUCODE
            MOVE      PCNUCODE,KEY6  
            CALL      WRPCNUR1
.
WRT9999  RETURN
+
.*************************************************************************
.*                                DISP0000                               *
.*                   display details corresponse to procedure code.      *
.*                   and save values read from file                      *
.*************************************************************************
DISP0000    MOVE      NCODE,KEY6
            MOVE      NCODE,PCNUCODE
            CALL      RDPCNUR1
            MOVE      SP20,TDESC            * clear description variable
            MOVE      PCNUCLSS,SLEVEL
            MOVE      PCNUNAME,NNAME
.
            MATCH     SP3,PCNUCLSS          * no class on file for nurse
            GOTO      DISP1000 IF EQUAL     * skip read of codes file
            PACK      KEY5,SD,PCNUCLSS      * get staff lev. desc. from file
            CALL      RDCODE1
.
DISP1000    DISPLAY   *P43:6,*V2LON,*EL,PCNUNAME:
                      *P43:7,*EL,PCNUCLSS,*HOFF:
                      *P50:7,TDESC:
                      *P43:8,*V2LON,PCNUSECL
.
DISP9999 RETURN
+
.***************************************************************************
.*                               UPD0000                                   *
.*                      update record and insert key if part of            *
.*                      key has been modified.                             *
.***************************************************************************
UPD0000     MOVE      NCODE,PCNUCODE
            MOVE      NNAME,PCNUNAME
            MOVE      SLEVEL,PCNUCLSS
            MOVE      SECLEVL,PCNUSECL
.
            CALL      UPPCNUR1
.
UPD9999  RETURN
+
.****************************************************************************
.*                              DEL0000                                     *
.*                         delete an existing record                        *
.****************************************************************************
DEL0000     MOVE      NCODE,KEY6
            CALL      DEPCNUR1
.
DEL9999  RETURN
.
.****************************************************************************
.                               QEST0000                                    *
.****************************************************************************
QEST0000    MOVE      SD,CODE
.
            CALL      PATCODDS              * display options
.
QEST1000    KEYIN     *P1:24,"Staff level : ___",*P15:24,*V2LON,SLEVEL
.
            ENDSET    SLEVEL
            APPEND    SP3,SLEVEL
            RESET     SLEVEL
.
            MATCH     SP3,SLEVEL           * nothing entered?
            GOTO      QEST9999 IF EQUAL
.
            PACK      KEY5,SD,SLEVEL       * get description
            CALL      RDCODE1
            BRANCH    OVRCD,QEST9000
.
            CALL      SCR0000              * re-display screen
            DISPLAY   *P43:4,NCODE:
                      *P43:6,*V2LON,PCNUNAME:
                      *P43:7,SLEVEL,*HOFF,*P50:7,TDESC:  
                      *P43:8,*V2LON,PCNUSECL
.
            GOTO      QEST9999
.
QEST9000    DISPLAY   *P1:24,*B,*V2LON,"Invalid Code. ";
            CALL      HITENTER
            GOTO      QEST1000
.
QEST9999   RETURN
+
.****************************************************************************
         INC       STD001IO
         INC       PCPNURIO/INC
         INC       PATCODKY
         INC       PATCODIO/INC
