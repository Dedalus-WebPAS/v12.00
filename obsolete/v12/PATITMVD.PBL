. ****************************************************************************
. * Include Name    : PATITMVD                                               *
. * System          : Patient Management                                     *
. * Function        : Valid an MBS item code to make sure it is active       *
. ****************************************************************************
. * Subroutines     : PATITMVD - Validate MBS item code                      *
. * Procedures      :                                                        *
. * Includes Req.   : PATITMFD - MBS/Item file                               *
. *                   PATIOITM - MBS/Item file                               *
. *                                                                          *
. * System Params   :                                                        *
. ****************************************************************************
. * Modifications   :                                                        *
. *        V11.00.01  03/03/2020  J.Tan          TSK 0838262                 *
. *                   Added Effective from and to date to MBS Item file      *
. ****************************************************************************
.
. ****************************************************************************
. * Subroutine      : PATITMVD                                               *
. * Function        : Validate a key to make sure it is a valid MBS code     *
. * Parameters      : KEY17     - Code to be validated                       *
. * Returns         : OVRCD     - 0 if a valid code, 1 if invalid code       *
. *                   KEY17     - Key for record returned (if OVRCD=0)       *
. *                   PATITMFD record                                        *
. * NB: Please note that because of the linked MBS code, the record returned *
. *     may not be for the same MBS code as the original key                 *
. ****************************************************************************
.
PATITMVD  CALL      RDITEM1                  * check if key is valid
          BRANCH    OVRCD,ITMVD900           * invalid code -> return OVRCD=1
.
.         Check if this code is active. If it is, then return as this is a
.         valid code (key exists and is active).
.
          MATCH     SP1,PTITACTV             * code active ?
          GOTO      ITMVD800 IF EQUAL        * yes -> return OVRCD=0
.
.         We have an inactive record. If there is no linked MBS code, then
.         simply return with OVRCD=1 to say record does not exist.
.
ITMVD500  MATCH     SP9,PTITLINK             * have linked MBS code ?
          GOTO      ITMVD900 IF EQUAL        * no -> return OVRCD=1
.
.         Check the linked MBS code to make sure it exists
.
          MOVE      PTITLINK,KEY9
          PACK      KEY17,PTITLINK,PTITEFDT  * use linked MBS code as the key
          CALL      RDITEM1                  * read the link MBS record
          BRANCH    OVRCD,ITMVD900           * no linked MBS record
.
.         The linked MBS code exists. Check if this new MBS code is active
.
          MATCH     SP1,PTITACTV             * new MBS code active
          GOTO      ITMVD500 IF NOT EQUAL    * no -> check for a linked MBS
.
.         Tell the user that the MBS code has been changed
.
          DISPLAY   *P1:24,*EL,*B,"This MBS item has been changed to ":
                    *V2LON,KEY9,*HOFF,SP1;
          CALL      HITENTER
.
.         We have a valid item
.
ITMVD800  MOVE      ZERO,OVRCD
          GOTO      ITMVD999
.
.         We have an invalid item
.
ITMVD900  MOVE      ONE,OVRCD
.
ITMVD999  RETURN
...............................................................................
