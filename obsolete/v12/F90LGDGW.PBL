.*****************************************************************************
.*    Program      : F90LGDGW                                                *
.*    Description  : Conversion to new File layout (Cloned from FXPATDGW)    *
.*                                                                           *
.*    Author       : Mike Laming                                             *
.*    Date         : 22/05/2008                                              *
.*    Modifications:                                                         *
.*        V9.10.00  22/05/2008  Mike Laming   CAR 168959                     *
.*                  File fix to include LPTDWCLS before LPTDWDGV             *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001FD
.
.*********************************   New file format here **********
LEGDGWA1   IFILE SQL, FIXED=109, KEY="U1-4,85-87"
LEGDGWA2   IFILE SQL, FIXED=109, KEY="U5-8,1-4,85-87"
LEGDGWA3   IFILE SQL, FIXED=109, KEY="U9-68,1-4,85-87"
.
. NAME     TYPE  LENGTH  PHYSICAL  START LOC.   DESCRIPTION
. ----     ----  ------  --------  ----------   -----------
LPTDWDRG   DIM      4         4        1        DRG Number
LPTDWMDC   DIM      4         4        5        MDC Number
LPTDWDES   DIM      60        60       9        DRG Description
LPTDWLOW   FORM     3.2       4       69        Low Trim
LPTDWHIG   FORM     3.2       4       73        High Trim
LPTDWRWG   FORM     3.6       6       77        Resource Weight
LPTDWCLS   DIM      2         2       83        Clincal Speciality
LPTDWDGV   DIM      3         3       85        DRG Version (format "051" = 5.1)
LPTDWSPA   DIM      21        21      88        Spare Variable
.End of Record                       ----
                                     109
.
OLDFILE   IFILE     SQL, FIXED=109, KEY="U1-4,83-85"
OLDRFILE  IFILE     SQL, FIXED=109, KEY="U1-4"
.
CMDLINE   DIM       100
NEWLINNO  DIM       2
RECNUM    FORM      8
OLDSPARE  DIM       23                            * decreasing to 21
OLDDRGV   DIM       3                                                 *I-164269
.
PRGNAM    INIT      "CONVERSION PATDGWFD"
PRGID     INIT      "F90LGDGW"
VERSION   INIT      "V12.00.00"
.*****************************************************************************
.
. Start of Program
.
          CALL      INIT0000                      * init and open files
.
MAIN1000  CALL      OPTN0000
          BRANCH    EXIT,MAIN9999
.
          CALL      OPEN0000
          BRANCH    EXIT,MAIN1000
.
. Loop thru old file and write records to new file
.
MAIN5000  CALL      READ0000                      * read old records and write
          GOTO      MAIN1000
.
MAIN9999  CHAIN     PGM
+
.******************************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.******************************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
.
INIT9999  RETURN
.
.******************************************************************************
.*                          OPEN0000                                *
.*          Check that the files needed exist & open them           *
.******************************************************************************
OPEN0000  MOVE      ZERO,OVRCD
          DISPLAY   *P1:3,*EF;
.
. open old file
.
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE,"legdgwaf"
          TRAPCLR   IO
.
          BRANCH    OVRCD,OPEN5000                * check if Old file exists
.
. check if new file already exists
.
          MOVE      ZERO,OVRCD
.
          TRAP      OVERCOND IF IO
          OPEN      LEGDGWA1,"newdgwaf"
          TRAPCLR   IO
.
          BRANCH    OVRCD,OPEN2000
          CLOSE     LEGDGWA1
.
. File found. Tell users and check if they want to proceed
.
OPEN1000  KEYIN     *P1:10,*B,*EF,*V2LON,"New file already exists.":
                    *HOFF," Do you want to Proceed (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*V2LON,ANS
.
          REP       UPPLOW,ANS
.
          CMATCH    ANSY,ANS
          GOTO      OPEN2000 IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      OPEN9000 IF EQUAL
.
          GOTO      OPEN1000
.
. Create the new file
.
OPEN2000  EXECUTE   "iserase newdgwaf",TASKID
          CLEAR     CMDLINE
          APPEND    "isbuild newdgwaf 109 ",CMDLINE
          APPEND    "UC1-4,85-87 UC5-8,1-4,85-87 UC9-68,1-4,85-87",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      LEGDGWA1,"newdgwaf"
.
. set the flag to say we want to continue
.
          MOVE      ZERO,EXIT
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:12,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:14,"Record : "
.
          GOTO      OPEN9999
.
. old file not found
.
OPEN5000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDRFILE,"legdgwaf"
          TRAPCLR   IO
.
          IF        OVRCD = 0
            DISPLAY   *P10:10,*B,*V2LON,"File 'legdgwaf' has not had the ":
                      "V9.08/V9.09 patch applied. - ":
                      *P10:11,"Fix program F99LGDGW must be run ":
                      "before you can continue. ",*HOFF
          ELSE
            DISPLAY   *P10:10,*B,*V2LON,"Old 'legdgwaf' file not found.",*HOFF
          ENDIF
          KEYIN     *P1:24,"  Hit ",*V2LON,"<ENTER>",*HOFF," to exit  ":
                    *EOFF,ANS
.
. we don't want to continue
.
OPEN9000  MOVE      ONE,EXIT
.
OPEN9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Run Conversion":
                    *P1:7,"Select Option :";
.
OPTN1000  KEYIN     *P17:7,*DV,UNDLN2,*P17:7,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.******************************************************************************
READ0000  PACK      KEY7,SP10    
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
          PACK      LPTDWCLS,SP10                 * new field
          PACK      LPTDWSPA,SP30
          PACK      KEY7,LPTDWDRG,LPTDWDGV,SP10
          CALL      WRLGDGW1                      * write to new file
.
          IF        (RECNUM%50) = 0
            DISPLAY   *P10:14,*V2LON,RECNUM,*P20:14,KEY6
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     LEGDGWA1                      * close new file
          CLOSE     OLDFILE                       * close old file
          DISPLAY   *P10:14,*V2LON,RECNUM,*P20:14,KEY6
.
          EXECUTE   "cp `ldat legdgwaf.dat` savdgwaf.dat",TASKID
          EXECUTE   "cp `ldat legdgwaf.idx` savdgwaf.idx",TASKID
.
          CALL      IBACLOCK
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:18,"Ended   : ",CDATE,SP1,CTIMEIS
.
READ7000  KEYIN     *P1:23,*B,*EF,*V2LON,"Conversion is complete.":
                    *P1:24,*HOFF,"Copy back the new file (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? ",*V2LON,ANS
          REP       UPPLOW,ANS
.
          CMATCH    ANSY,ANS
          GOTO      READ8000 IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      READ9000 IF EQUAL
.
READ8000  EXECUTE   "mv newdgwaf.dat `ldat legdgwaf.dat`",TASKID
          EXECUTE   "mv newdgwaf.idx `ldat legdgwaf.idx`",TASKID
.
READ9000  DISPLAY   *P1:23,*EF
.
READ9999  RETURN

.
. ---- OLD IO ROUTINES ----
.
READSOLD  RESET     KEY7
          READ      OLDFILE,KEY7;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE;LPTDWDRG,LPTDWMDC,LPTDWDES,LPTDWLOW:
                            LPTDWHIG,LPTDWRWG,LPTDWDGV,OLDSPARE
          GOTO      OVERCOND IF OVER
          RETURN
.
. ---- NEW IO ROUTINES ----
.  
.         read to see if record exists
RALGDGW1  MOVE      ZERO,OVRCD
          RESET     KEY7
          READ      LEGDGWA1,KEY7;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
.         write
.
WRLGDGW1  RESET     KEY7
          WRITE     LEGDGWA1,KEY7;LPTDWDRG,LPTDWMDC,LPTDWDES,LPTDWLOW:
                                  LPTDWHIG,LPTDWRWG,LPTDWCLS,LPTDWDGV:
                                  LPTDWSPA
          RETURN
.
. ---- Other Stuff ----
          INC       STD001IO
