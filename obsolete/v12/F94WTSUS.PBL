.***************************************************************************
.* System    :   Waiting List                                              *
.* Program   :   F94WTSUS                                                  *
.* Desc      :                                                             *
.***************************************************************************
.* Date      :   09/06/2005                                                *
.* Author    :   Jill Habasque                                             *
.* Function  :   This program will write watsusaf records from watcataf    *
.*                                                                         *
.* Mods      :                                                             *
.*               V10.03.01  25/02/13  Ania P                  CAR 281021   *
.*               Removed unnecessary modification of constants.            *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC      WATCATFD/INC
          INC      WATSUSFD/INC
          INC      PATCODFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
CASEKEYS  DIM       21
CHECKKEY  DIM       21
CURRFLAG  FORM      1
NEWFALG   FORM      1
NRFCFLAG  FORM      1
SIND4     DIM       1
UPWRITE   FORM      1
.
PRGID     INIT      "F94WTSUS"
PRGNAM    INIT      "Write watsusaf records from watcataf"
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      WATCATA2,"watcataf"
          OPEN      WATSUSA1,"watsusaf"
          OPEN      PATCODE1,"patcodes"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,NRFCFLAG
          MOVE      SP70,CASEKEYS
          MOVE      SP70,CHECKKEY
          MOVE      SP70,SIND4
          PACK      KEY29,SP70
          CALL      RSWTCAT2
PROC1000  CALL      RKWTCAT2
          BRANCH    OVRCD,PROC9999
.
          MATCH     "TP",WTCACATF
          GOTO      PROC1000 IF NOT EQUAL
.
          PACK      CHECKKEY,WTCAURNO,WTCAPROC,DWTCAPCN,SP70
          MATCH     CASEKEYS,CHECKKEY
          IF        !@EQUAL
            PACK      CASEKEYS,WTCAURNO,WTCAPROC,DWTCAPCN,SP70
            MOVE      ZERO,NRFCFLAG
            MOVE      ZERO,CURRFLAG
            MOVE      ANSR,TCINDC4
          ENDIF
.
          PACK      KEY5,ANST,ANSP,WTCACODT,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PROC1000
.
          MATCH     ANSR,TCINDC4
          IF        @EQUAL
            MOVE      ZERO,NRFCFLAG
          ELSE
            MOVE      ONE,NRFCFLAG
          ENDIF
.
          MATCH     TCINDC4,SIND4
          IF        !@EQUAL
            CALL      WRSUS000
          ENDIF
.
          MOVE      NRFCFLAG,CURRFLAG
          MOVE      TCINDC4,SIND4
          GOTO      PROC1000
.
PROC9999  RETURN
+
.**************************************************************************
.*                               WRSUS000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
WRSUS000  MOVE      ZERO,UPWRITE
          COMPARE   ONE,CURRFLAG
          IF        @EQUAL
            COMPARE   ONE,NRFCFLAG
            IF        @EQUAL
              MOVE      ONE,UPWRITE
              GOTO      WRSUS100
            ENDIF
          ENDIF
.
          COMPARE   ONE,NRFCFLAG
          GOTO      WRSUS100 IF NOT EQUAL
.
WRSUS20   PACK      WTSUURNO,WTCAURNO,SP70
          PACK      WTSUCODE,WTCAPROC,SP70
          PACK      DWTSUCNT,DWTCAPCN,SP70
.
          MOVE      ZERO,F2
          PACK      KEY21,WTSUURNO,WTSUCODE,DWTSUCNT,Z70
          CALL      RSWTSUS1
WRSUS030  CALL      RPWTSUS1
          BRANCH    OVRCD,WRSUS040
.
          MATCH     WTCAURNO,WTSUURNO
          GOTO      WRSUS040 IF NOT EQUAL
.
          MATCH     WTCAPROC,WTSUCODE
          GOTO      WRSUS040 IF NOT EQUAL
.
          COMPARE   WTCAPCNT,WTSUCNT
          GOTO      WRSUS040 IF NOT EQUAL
          MOVE      WTSUSCNT,F2
.
WRSUS040  ADD       ONE,F2
          MOVE      F2,WTSUSCNT
          PACK      WTSUURNO,WTCAURNO,SP70
          PACK      WTSUCODE,WTCAPROC,SP70
          MOVE      DWTCAPCN,WTSUCNT
.
          MOVE      WTCACODT,WTSULSTS
          MOVE      WTCAEDAT,WTSUFDAT
          MOVE      ANSH,WTSUREAS
          MOVE      SP70,WTSUTDAT
.
          PACK      KEY21,WTSUURNO,WTSUCODE,WTSUCNT,WTSUSCNT,SP70
          CALL      RAWTSUS1
          IF        OVRCD=1
            CALL      WRWTSUS1
          ELSE
            GOTO      WRSUS999
          ENDIF
          GOTO      WRSUS999
.
WRSUS100  PACK      WTSUURNO,WTCAURNO,SP70
          PACK      WTSUCODE,WTCAPROC,SP70
          PACK      DWTSUCNT,DWTCAPCN,SP70
.
          PACK      KEY21,WTSUURNO,WTSUCODE,DWTSUCNT,Z70
          CALL      RSWTSUS1
          CALL      RPWTSUS1
          BRANCH    OVRCD,WRSUS999
.
          MATCH     WTCAURNO,WTSUURNO
          GOTO      WRSUS999 IF NOT EQUAL
.
          MATCH     WTCAPROC,WTSUCODE
          GOTO      WRSUS999 IF NOT EQUAL
.
          COMPARE   WTCAPCNT,WTSUCNT
          GOTO      WRSUS999 IF NOT EQUAL
.
          MOVE      WTCAEDAT,WTSUTDAT
          MOVE      "1",WTSUAUTO
          CALL      UPWTSUS1
          BRANCH    UPWRITE,WRSUS040
.
WRSUS999  RETURN
+
.
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       WATCATIO/INC
          INC       WATSUSIO/INC
          INC       PATCODIO/INC
