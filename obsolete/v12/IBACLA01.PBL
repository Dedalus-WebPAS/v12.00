.******************************************************************************
.* System         : Clincal Audits                                            *
.* Program        : IBACLA01.PBL                                              *
.* Name           : Clinical Audits System - Set up screen (similar to SCR13) *
.******************************************************************************
.* Date           : 30/03/1992                                                *
.* Author         : Justin Coates                                             *
.* Function       : To screen paint the screen for clinical audits            *
.* Modifications  :                                                           *
.*        V5.01.02  20/04/1994    WHIRLPOOL                                   *
.*                  removed updflock label because now in ibaioovr            *
.******************************************************************************
.
          INC       SYSSTDDF
          INC       CLACATFD/INC            * categories for each coded field
          INC       CLACODFD/INC            * codes file
          INC       CLASITFD/INC            * site file
          INC       CLAVARFD/INC            * variable file
          INC       PAINTDDF
          INC       SCRFLDFD/INC
          INC       SCRMASFD/INC
          INC       SCRPRGFD/INC
          INC       SCRPSTFD/INC
          INC       SCRSBGFD/INC
          INC       SCRSLTFD/INC
.
.         program constants
.
CLAAUD    INIT      "CLAAUD"      * part of screen id
PRGIDF    INIT      "IBACLAUD"    * program name for item file
.
.         program variables
.
ACCEPT    FORM      1         * insert/change a field
CATEG     DIM       2         * category for coded field
COLUMN    FORM      3
FORM5M    FORM      5         * determine if odd/even number
FORM5     FORM      5         * form of item number
KEYCAT    FORM      1         * whether keyin category for a field
KTYPDES   DIM       20
MOVEFLAG  FORM      1
PRGIDB    DIM       8         * program id for background file
SCRID     DIM       2         * screen id
SCRIDB    DIM       2         * screen id for background file
SCRIDF    DIM       2         * screen id for item file
VARBLENO  FORM      2         * which variable number (1-20)
.
. Save Variables
.----------------
SSCPSPID   DIM       8      8        1     Program
SSCPSSID   DIM       2      2        9     Screen
SSCPSITM   DIM       5      5        18    Field Number
SSCPSROW   DIM       2      2        23    Row
SSCPSCOL   DIM       3      3        25    Column
SSCPSMAN   FORM      1      2        28    Mandatory 0=No
SSCPSLEN   FORM      3      3        30    Display Length
SSCPSATT   DIM       1      1        33    Display Attributes
SSCPSNOR   FORM      2      1        33    Display Attributes
SSCPSMTY   DIM       5      1        33    Display Attributes
SSCPSFMT   DIM       25     1        33    Display Attributes
.
. Save Item Variables
.---------------------
SSCFLPID   DIM       8      8        1     Program ID
SSCFLSID   DIM       2      2        9     Screen Id
SSCFLFNO   DIM       5      5        11    Field Number
SSCFLDES   DIM       35     35       16    Description
SSCFLTYP   FORM      1      2        51    Type 0.Key 1.Sel 2.Dis 3.Fun 4.Scr
SSCFLFLD   DIM       8      8        53    Field
SSCFLCAL   DIM       8      8        61    Call Routine
SSCFLWPF   DIM       8      8        69    Word Processor File
SSCFLFSC   DIM       2      2        77    Screen to Call
SSCFLDEC   FORM      1      2        79    Declare Field in Program 0=No
SSCFLMIN   FORM      3      3        81    Minimum Screen Length
SSCFLMAX   FORM      3      3        84    Maximum Screen Length
SSCFLFLN   FORM      3.1    4        87    Field Length
SSCFLFTY   FORM      1      2        91    Field Type 0=Dim
SSCFLKTY   FORM      1      2        93    Keyin Type 0-6
SSCFLMAN   FORM      1      2        95    Mandatory 0=No
SSCFLFEX   FORM      1      2        97    Function Exit Type
SSCFLDAF   DIM       5      5        99    Display After Field
SSCFLMUL   FORM      1      2        99    Display After Field
SSCFLMTY   DIM       5      2        99    Display After Field
SSCFLKEY   FORM      1      2        99    Display After Field
SSCFLSPA   DIM       33     33       104   Spare
.
.
PRGNAM    INIT      "Audit Input Screen Set Up"
PRGID     INIT      "IBACLA01"
VERSION   INIT      "V12.00.00"
.
.*********************************************************************
.*                  ML0000                                           *
.*        MAINLINE CODE                                              *
.*********************************************************************
ML0000    TOPIC     PRGID
.
ML1000    CALL      INIT0000                * open files
.
ML2000    CALL      SITE0000                * Get Clinical Audit Site
          BRANCH    EXIT,ML9999
.
          CALL      SCRN0000                * get the screen id
          BRANCH    EXIT,ML2000             * no id entered
.
          CALL      SOPT0000                * Work On Screen 
          BRANCH    EXIT,ML3000
          GOTO      ML2000  
.
ML3000    CALL      UUSCPR1                 * Unlock Program
.
ML9999    CHAIN     PGM
.
.*********************************************************************
.                   INIT0000
.         initilization
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"opening"
          DISPLAY   *P58:24,"clacataf"
          OPEN      CLACATA1,"clacataf"
          DISPLAY   *P58:24,"clacodaf"
          OPEN      CLACODA1,"clacodaf"
          DISPLAY   *P58:24,"clasitaf"
          OPEN      CLASITA1,"clasitaf"
          DISPLAY   *P58:24,"clavaraf"
          OPEN      CLAVARA1,"clavaraf"
          DISPLAY   *P58:24,"clacodaf"
          OPEN      CLACODA1,"clacodaf"
          DISPLAY   *P58:24,"scrfldaf"
          OPEN      SCRFLDA1,"scrfldaf"
          OPEN      SCRFLDA2,"scrfldaf"
          DISPLAY   *P58:24,"scrmasaf"
          OPEN      SCRMASA1,"scrmasaf"
          DISPLAY   *P58:24,"scrprgaf"
          OPEN      SCRPRGA1,"scrprgaf"
          DISPLAY   *P58:24,"scrpstaf"
          OPEN      SCRPSTA1,"scrpstaf"
          OPEN      SCRPSTA2,"scrpstaf"
          DISPLAY   *P58:24,"scrsbgaf"
          OPEN      SCRSBGA1,"scrsbgaf"
          DISPLAY   *P58:24,"scrsltaf"
          OPEN      SCRSLTA1,"scrsltaf"
          OPEN      SCRSLTA2,"scrsltaf"
.
          CALL      GLOAD000     * Load Graphic Characters
.
INIT9999  RETURN
.
.*********************************************************************
.                   SITE0000
.         Get Clinical Audit Site
.*********************************************************************
SITE0000  MOVE      ZERO,EXIT
          MOVE      "8",MINLINE             * Screen Parameters
          MOVE      "23",MAXLINE
          MOVE      "1",MINCOL
          MOVE      "80",MAXCOL
          CALL      KYCLASIT                * keyin site
SITE9999  RETURN
.
.----------------------------------------
. LPRG - Lock Program to be Updated
.----------------------------------------
LPRG0000  
          PACK      KEY8,PRGIDB,SP70
          CALL      RLSCPR1
          BRANCH    OVRCD,LPRG9000,LPRG8000
          MOVE      ZERO,EXIT
          GOTO      LPRG9999
.
LPRG8000  KEYIN     *P1:24,*EL,*B,"Record Locked - (":
                    *V2LON,"R",*HOFF,")etry or (":
                    *V2LON,"E",*HOFF,")xit ? ",ANS
          REP       UPPLOW,ANS
          MATCH     ANSR,ANS
          GOTO      LPRG0000 IF EQUAL
          MATCH     ANSE,ANS
          GOTO      LPRG0000 IF NOT EQUAL
.
LPRG9000  MOVE      ONE,EXIT
.
LPRG9999  RETURN
+
.*********************************************************************
.                   SOPT0000
.         Screen Options
.*********************************************************************
SOPT0000  
SOPT1000  DISPLAY   *P1:1,*EF,"e",*V2LON,"X",*HOFF,"it, ":
                    *V2LON,"B",*HOFF,"ackground, ":
                    *V2LON,"F",*HOFF,"ields "
          HLINE     *G37,2,1,80
.
          MOVE      ZERO,ACCEPT
.
          KEYIN     *P1:24,*EL,*EOFF,*NOEDIT,*+,ANS
          REP       UPPLOW,ANS
          MATCH     ANSX,ANS
          GOTO      SOPT9000 IF EQUAL
          MATCH     ANSB,ANS
          GOTO      SOPT3000 IF EQUAL
          MATCH     ANSF,ANS
          GOTO      SOPT4000 IF EQUAL
          BEEP
          GOTO      SOPT1000
.
. Paint Screen Background 
.--------------------------
SOPT3000  CALL      RLOAD000
          CALL      PAINT000
          GOTO      SOPT1000
.
. Edit Fields 
.-------------
SOPT4000  CALL      RLOAD000
          CALL      PTFLD000
          GOTO      SOPT1000
.
.         exit back to entering SITE id
.
SOPT9000  CALL      DISPHEAD                * redisplay header
          MOVE      ZERO,EXIT
.
SOPT9999  RETURN
.
.------------------------------------
. Paint a Screen
.------------------------------------
PAINT000  MOVE      MINCOL,CCOL
          MOVE      MINLINE,CVERT
          MOVE      ONE,SCRNSAVE
.
PAINT090  DISPLAY   *P1:1,*EL
          DISPLAY   *P1:1,"e",*V2LON,"X",*HOFF,"it, ":
                    *V2LON,"S",*HOFF,"ave, ":
                    *V2LON,"C",*HOFF,"ancel, ":
                    *V2LON,"V",*HOFF,"ideo, ":
                    *V2LON,"E",*HOFF,"dit, ":
                    *V2LON,"G",*HOFF,"raphic, ":
                    *V2LON,"M",*HOFF,"ove"
          HLINE     *G37,2,1,80
.
PAINT100  KEYIN     *PCCOL:CVERT,*EOFF,*NOEDIT,*+,*CURSON,ANS
          CALL      KMOVE000
          BRANCH    EXIT,PAINT100
          REP       UPPLOW,ANS
.
          MATCH     ANSE,ANS
          GOTO      PAINT200 IF EQUAL
.
          MATCH     ANSV,ANS
          GOTO      PAINT300 IF EQUAL
.
          MATCH     ANSG,ANS
          GOTO      PAINT500 IF EQUAL
.
          MATCH     ANSM,ANS
          GOTO      PAINT600 IF EQUAL
.
          MATCH     ANSS,ANS
          GOTO      PAINT700 IF EQUAL
.
          MATCH     ANSX,ANS
          GOTO      PAINT800 IF EQUAL
.
          MATCH     ANSC,ANS
          GOTO      PAINT900 IF EQUAL
          GOTO      PAINT100
.
. Edit part of the Screen
.------------------------
PAINT200  CALL      EDIT0000
          GOTO      PAINT090
.
. Set Video Attributes
.---------------------
PAINT300  CALL      VIDEO000
          GOTO      PAINT090
.
. Graphic
.------------
PAINT500  CALL      GRAPH000
          GOTO      PAINT090
.
. Move
.------------
PAINT600  CALL      MOVE0000
          GOTO      PAINT090
.
. Save Screen
.-------------
PAINT700  CALL      SAVE0000
          DISPLAY   *P13:1,"Saved - ";
          CALL      HITENT01
          GOTO      PAINT090
.
. Exit - Disable if screen modified and not saved
.-------------------------------------------------
PAINT800  BRANCH    SCRNSAVE,PAINT999
          KEYIN     *P1:1,*EL:
                    *P1:1,"Screen Background must be Saved First - ";
          CALL      HITENT01
          GOTO      PAINT090
.
. Cancel - Reload Screen
.-----------------------
PAINT900  CALL      RLOAD000
          GOTO      PAINT090
.
PAINT999  RETURN
.
.----------------------------------------
. Edit Screen
.----------------------------------------
EDIT0000  MOVE      ZERO,SCRNSAVE
          DISPLAY   *P1:1,*EL
          DISPLAY   *P1:1,"Edit, <",*V2LON,"Enter",*HOFF,"> to Exit"
EDIT0100  GETVAR    CDATA,CATTR,CCOL,CVERT
          KEYIN     *PCCOL:CVERT,*EOFF,*NOEDIT,*+,*CURSON,ANS
          CALL      KMOVE000
          BRANCH    EXIT,EDIT0100
          RESET     ANS
          GOTO      EDIT0900 IF EOS
          MOVE      ANS,CDATA
          PUTVAR    CDATA,CATTR,CCOL,CVERT
          COMPARE   MAXCOL,CCOL
          GOTO      EDIT0100 IF EQUAL
          ADD       ONE,CCOL
          GOTO      EDIT0100
EDIT0900
          RETURN
.----------------------------------------
. Video Edit
.----------------------------------------
VIDEO000  MOVE      ZERO,SCRNSAVE
          DISPLAY   *P1:1,*EL
          DISPLAY   *P1:1,"e",*V2LON,"X",*HOFF,"it, ":
                    *V2LON,"H",*HOFF,"ighlight, ":
                    *V2LON,"R",*HOFF,"everse, ":
                    *V2LON,"B",*HOFF,"link, ":
                    *V2LON,"U",*HOFF,"nderline "
          MOVE      ZERO,FBLINK
          MOVE      ZERO,FV2LON
          MOVE      ZERO,FHON
          MOVE      ZERO,FULON
VIDEO100  GETVAR    CDATA,CATTR,CCOL,CVERT
          CALL      PUTCH000
          KEYIN     *PCCOL:CVERT,*EOFF,*NOEDIT,*+,*CURSON,ANS
          CALL      KMOVE000
          BRANCH    EXIT,VIDEO100
          REP       UPPLOW,ANS
          MATCH     ANSX,ANS
          GOTO      VIDEO900 IF EQUAL
          REP       "0z1z2z3z4z5z6z7z8z9z",ANS
          REP       "B1U2H3R4",ANS
          TYPE      ANS
          GOTO      VIDEO100 IF NOT EQUAL
          MOVE      ANS,F1
          LOAD      FLAG,F1,FBLINK,FULON,FV2LON,FHON
          BRANCH    FLAG,VIDEO200
          STORE     ONE,F1,FBLINK,FULON,FV2LON,FHON
          GOTO      VIDEO100
VIDEO200  STORE     ZERO,F1,FBLINK,FULON,FV2LON,FHON
          GOTO      VIDEO100
.
VIDEO900
          RETURN
.------------------------------------------
. Save Screen to ID
.------------------------------------------
SAVE0000  MOVE      CVERT,SVERT
          MOVE      CCOL,SCOL
          DISPLAY   *P1:1,*EL
          DISPLAY   *P1:1,*V2LON,PRGIDB,*HOFF,"/",*V2LON,SCRIDB
          MOVE      PRGIDB,SCSBPID
          MOVE      SCRIDB,SCSBSID
          ASSIGN    MINLINE-1,LINE
.
SAVE1000  ADD       ONE,LINE
.
          PACK      SCSBDAT,SP70,SP70
          PACK      SCSBATT,SP70,SP70
          GETVAR    SCSBDAT,SCSBATT,1,LINE
.
          MOVE      LINE,SCSBLIN
          PACK      KEY12,SCSBPID,SCSBSID,SCSBLIN
          CALL      DESCSB1
          MATCH     SP80,SCSBDAT
          GOTO      SAVE1100 IF EQUAL
          CALL      WRSCSB1
.
SAVE1100  COMPARE   MAXLINE,LINE
          GOTO      SAVE1000 IF NOT EQUAL
          MOVE      ONE,SCRNSAVE
.
          MOVE      SVERT,CVERT
          MOVE      SCOL,CCOL
          RETURN
.----------------------------------------
. Reload a Screen Display
.----------------------------------------
RLOAD000  DISPLAY   *P1:1,*EL,*P1:MINLINE,*EF
.
          PACK      KEY12,PRGIDB,SCRIDB,SP70
          CALL      RSSCSB1
RLOAD100  CALL      RKSCSB1
          BRANCH    OVRCD,RLOAD999
          MATCH     SCSBPID,PRGIDB
          GOTO      RLOAD999 IF NOT EQUAL
          MATCH     SCSBSID,SCRIDB
          GOTO      RLOAD999 IF NOT EQUAL
          MOVE      SCSBLIN,LINE
          PUTVAR    SCSBDAT,SCSBATT,1,LINE
          GOTO      RLOAD100
.
RLOAD999  MOVE      ONE,SCRNSAVE
          RETURN
.
.----------------------------------------
. Graphic Edit
.----------------------------------------
GRAPH000  MOVE      ZERO,SCRNSAVE
          MOVE      ONE,GCHARNUM
          MOVE      ZERO,LINETYPE
          MOVE      CCOL,SCOL
          MOVE      CVERT,SVERT
GRAPH090  DISPLAY   *P1:1,*EL
          DISPLAY   *P1:1,"e",*V2LON,"X",*HOFF,"it, ":
                    *V2LON,"E",*HOFF,"rase Line, ":
                    *V2LON,"S",*HOFF,"ingle Line, ":
                    *V2LON,"D",*HOFF,"ouble Line, ":
                    *V2LON,"C",*HOFF,"haracter Select"
GRAPH100  GETVAR    CDATA,CATTR,CCOL,CVERT
          KEYIN     *PCCOL:CVERT,*EOFF,*NOEDIT,*+,*CURSON,ANS
          CALL      GMOVE000
          BRANCH    EXIT,GRAPH100
          REP       UPPLOW,ANS
          MATCH     ANSX,ANS
          GOTO      GRAPH900 IF EQUAL
          MATCH     ANSS,ANS
          GOTO      GRAPH200 IF EQUAL
          MATCH     ANSD,ANS
          GOTO      GRAPH300 IF EQUAL
          MATCH     ANSE,ANS
          GOTO      GRAPH400 IF EQUAL
          MATCH     ANSC,ANS
          GOTO      GRAPH500 IF EQUAL
          GOTO      GRAPH100
GRAPH200  MOVE      ONE,LINETYPE
          GOTO      GRAPH100
GRAPH300  MOVE      TWO,LINETYPE
          GOTO      GRAPH100
GRAPH400  MOVE      ZERO,LINETYPE
          GOTO      GRAPH100
GRAPH500  DISPLAY   *P1:1,*EL
          KEYIN     *P1:1,"Character Number : ",GCHARNUM
          IF        GCHARNUM>45|GCHARNUM<1
              BEEP
              GOTO    GRAPH500
          ENDIF
          MOVE      THREE,LINETYPE
          GOTO      GRAPH090
.
GRAPH900  MOVE      SCOL,CCOL
          MOVE      SVERT,CVERT
          RETURN
.----------------------------------
. Move Display Area of the Screen
.----------------------------------
MOVE0000  CALL      MARK0000
          MOVE      ZERO,SCRNSAVE
          DISPLAY   *P1:1,*EL
          DISPLAY   *P1:1,"Move to the new Position, e":
                    *V2LON,"X",*HOFF,"it "
MOVE1000  KEYIN     *PCCOL:CVERT,*EOFF,*NOEDIT,*+,*CURSON,ANS
          CALL      KMOVE000
          BRANCH    EXIT,MOVE1000
          REP       UPPLOW,ANS
          MATCH     ANSX,ANS
          GOTO      MOVE1000 IF NOT EQUAL
          MOVE      SCOL,COLOFF
          SUB       CCOL,COLOFF
          MOVE      SVERT,VERTOFF
          SUB       CVERT,VERTOFF
          SUSPEND
          CALL      PUTSCR000
          ASSIGN    MINLINE-ONE,CVERT
          WHILE     CVERT<MAXLINE
            ADD       ONE,CVERT
            ASSIGN    MINCOL-ONE,CCOL
            WHILE     CCOL<MAXCOL
              ADD       ONE,CCOL
              IF        FARRAY[CCOL][CVERT]=1
                ASSIGN    CCOL-COLOFF,COLUMN
                ASSIGN    CVERT-VERTOFF,VERT
              IF        !(COLUMN<MINCOL|VERT<MINLINE|COLUMN>MAXCOL|VERT>MAXLINE)
                  PUTVAR    CARRAY[CCOL][CVERT],AARRAY[CCOL][CVERT],COLUMN,VERT
                ENDIF
              ENDIF
            DO
          DO
          FLUSH
          MOVE      SCOL,CCOL
          MOVE      SVERT,CVERT
          RETURN
.----------------------------------
. Mark area to Move
.----------------------------------
MARK0000  MOVE      CVERT,SVERT
          MOVE      CCOL,SCOL
          MOVE      MINCOL,HLEF
          MOVE      MAXCOL,HRIG
          MOVE      MINLINE,HTOP
          MOVE      MAXLINE,HBOT
          CALL      CLEAR000
          DISPLAY   *P1:1,*EL
          DISPLAY   *P1:1,"Mark Area to Move, e":
                    *V2LON,"X",*HOFF,"it "
MARK0900
          BRANCH    FARRAY[CCOL][CVERT],MARK0950
          MOVE      ONE,FARRAY[CCOL][CVERT]
          GETVAR    CARRAY[CCOL][CVERT],AARRAY[CCOL][CVERT],CCOL,CVERT
          MOVE      AARRAY[CCOL][CVERT],CATTR
          AND       ATTGRAPH,CATTR
          OR        ATTHON,CATTR
          OR        ATTV2LON,CATTR
          PUTVAR    CARRAY[CCOL][CVERT],CATTR,CCOL,CVERT
          GOTO      MARK1000
.
MARK0950  MOVE      ZERO,FARRAY[CCOL][CVERT]
          PUTVAR    CARRAY[CCOL][CVERT],AARRAY[CCOL][CVERT],CCOL,CVERT
.
MARK1000  KEYIN     *PCCOL:CVERT,*EOFF,*NOEDIT,*+,*CURSON,ANS
          CALL      KMOVE000
          BRANCH    EXIT,MARK0900
          REP       UPPLOW,ANS
          MATCH     ANSX,ANS
          GOTO      MARK1000 IF NOT EQUAL
          SUSPEND
          ASSIGN    MINLINE-ONE,CVERT
          WHILE     CVERT<MAXLINE
            ADD       ONE,CVERT
            ASSIGN    MINCOL-ONE,CCOL
            WHILE     CCOL<MAXCOL
              ADD       ONE,CCOL
              IF        FARRAY[CCOL][CVERT]=1
                DISPLAY   *PCCOL:CVERT,SP1
              ENDIF
            DO
          DO
          CALL      GETSCR00
          ASSIGN    MINLINE-ONE,CVERT
          WHILE     CVERT<MAXLINE
            ADD       ONE,CVERT
            ASSIGN    MINCOL-ONE,CCOL
            WHILE     CCOL<MAXCOL
              ADD       ONE,CCOL
              IF        FARRAY[CCOL][CVERT]=1
                MOVE      AARRAY[CCOL][CVERT],CATTR
                AND       ATTGRAPH,CATTR
                OR        ATTHON,CATTR
                OR        ATTV2LON,CATTR
                PUTVAR    CARRAY[CCOL][CVERT],CATTR,CCOL,CVERT
              ENDIF
            DO
          DO
          FLUSH
          MOVE      SCOL,CCOL
          MOVE      SVERT,CVERT
          RETURN
.
CLEAR000
          MOVE      CCOL,SCOL
          MOVE      CVERT,SVERT
          ASSIGN    MINLINE-ONE,CVERT
          WHILE     CVERT<MAXLINE
            ADD       ONE,CVERT
            ASSIGN    MINCOL-ONE,CCOL
            WHILE     CCOL<MAXCOL
              ADD       ONE,CCOL
              MOVE      ZERO,FARRAY[CCOL][CVERT]
              MOVE      SP1,CARRAY[CCOL][CVERT]
              MOVE      SP1,AARRAY[CCOL][CVERT]
            DO
          DO
          MOVE      SCOL,CCOL
          MOVE      SVERT,CVERT
          RETURN
.--------------------------------------------
. Display Fields over Backgraoud Screen
.--------------------------------------------
DPFLD000  MOVE      CVERT,SVERT
          MOVE      CCOL,SCOL
          PACK      KEY15,PRGIDB,SCRIDB,SP70
          CALL      RSSCPS1
DPFLD100  CALL      RKSCPS1
          BRANCH    OVRCD,DPFLD999
          MATCH     PRGIDB,SCPSPID
          GOTO      DPFLD999 IF NOT EQUAL
          MATCH     SCRIDB,SCPSSID
          GOTO      DPFLD999 IF NOT EQUAL
.
          CALL      DPITM000
          GOTO      DPFLD100
.
DPFLD999  MOVE      SVERT,CVERT
          MOVE      SCOL,CCOL
          RETURN
.-------------------------------------------
. Display a Field Item of the Screen Painter
.-------------------------------------------
DPITM000  
          MOVE      CVERT,SVERT
          MOVE      CCOL,SCOL
          PACK      KEY15,PRGIDF,SCRIDF,SCPSITM
          CALL      RDSCFL1
          BRANCH    MOVEFLAG,DPITM010
          IF        SCFLTYP=5
            GOTO      DPITM100
          ENDIF
DPITM010  MOVE      SCPSROW,CVERT
          MOVE      ONE,LLCNT
          WHILE     LLCNT<=SCPSLEN
            MOVE      SCPSCOL,CCOL
            ADD       LLCNT,CCOL
            SUB       ONE,CCOL
            PUTVAR    ANSX,SCPSATT,CCOL,CVERT
            ADD       ONE,LLCNT
          DO
          GOTO      DPITM999
.
DPITM100  MOVE      SCPSROW,CTOP
          MOVE      SCPSCOL,CLEF 
          MOVE      SCPSLEN,LLCNT
          MOVE      SCPSNOR,CVERT
          ASSIGN    CLEF+LLCNT+1,CRIG
          ASSIGN    CTOP+CVERT+1,CBOT
          MOVE      SCPSCOL,CCOL 
          BOX       16,CLEF,CTOP,CRIG,CBOT
          ADD       ONE,CLEF
          SUB       "12",CRIG
          DISPLAY   *PCLEF:CBOT,*HON,SP1,SP10:
                    *PCRIG:CBOT,SP5
          ADD       SIX,CRIG
          DISPLAY   *PCRIG:CBOT,*HON,SP5
.
DPITM999
          MOVE      SVERT,CVERT
          MOVE      SCOL,CCOL
          RETURN
.--------------------------------------------------------------
. Paint Fields on Screen
.--------------------------------------------------------------
PTFLD000  CALL      DPFLD000 * Display Current Fields
.
PTFLD090  DISPLAY   *P1:1,*EL
          DISPLAY   *P1:1,"e",*V2LON,"X",*HOFF,"it, ":
                    *V2LON,"E",*HOFF,"dit, ":
                    *V2LON,"S",*HOFF,"elections, ":
                    *V2LON,"V",*HOFF,"ideo, ":
                    *V2LON,"M",*HOFF,"ove, ":
                    *V2LON,"R",*HOFF,"edisplay"
.
PTFLD100  KEYIN     *PCCOL:CVERT,*EOFF,*NOEDIT,*+,*CURSON,ANS
          CALL      KMOVE000
          BRANCH    EXIT,PTFLD100
          REP       UPPLOW,ANS
.
          MATCH     ANSE,ANS
          GOTO      PTFLD200 IF EQUAL
.
          MATCH     ANSV,ANS
          GOTO      PTFLD300 IF EQUAL
.
          MATCH     ANSR,ANS
          GOTO      PTFLD400 IF EQUAL
.
          MATCH     ANSM,ANS
          GOTO      PTFLD500 IF EQUAL
.
          MATCH     ANSS,ANS
          GOTO      PTFLD850 IF EQUAL
.
          MATCH     ANSX,ANS
          GOTO      PTFLD900 IF EQUAL
          GOTO      PTFLD100
.
. Edit part of the Screen
.------------------------
PTFLD200  MOVE      CCOL,SCOL
          MOVE      CVERT,SVERT
          CALL      FEDIT000
          MOVE      SCOL,CCOL
          MOVE      SVERT,CVERT
          GOTO      PTFLD090
.
. Set Video Attributes
.---------------------
PTFLD300  MOVE      CCOL,SCOL
          MOVE      CVERT,SVERT
          CALL      FVIDE000
          MOVE      SCOL,CCOL
          MOVE      SVERT,CVERT
          GOTO      PTFLD090
.
. Redisplay Screen
.---------------------
PTFLD400  MOVE      CCOL,SCOL
          MOVE      CVERT,SVERT
          CALL      RLOAD000
          CALL      DPFLD000
          MOVE      SCOL,CCOL
          MOVE      SVERT,CVERT
          GOTO      PTFLD090
.
. Move A Field
.--------------
PTFLD500  CALL      FMOV0000
          GOTO      PTFLD090
.
. Selections
.-----------
PTFLD850  MOVE      CCOL,SCOL
          MOVE      CVERT,SVERT
          CALL      SEDIT000
          MOVE      SCOL,CCOL
          MOVE      SVERT,CVERT
          GOTO      PTFLD090
.
PTFLD900  RETURN
.----------------------------------
. Selection Edit
.----------------------------------
SEDIT000  MOVE      "13",HTOP
          MOVE      "1",HLEF
          MOVE      "24",HBOT
          MOVE      "80",HRIG
          CALL      GETSCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
SEDIT001  DISPLAY   *P1:14,*EF
          HLINE     *G37,14,1,80
          DISPLAY   *P4:15,*V2LON,*ULON," Selection Maintenance ",*HOFF:
                    *P1:17,"    Selection           :":
                    *P1:19,*V2LON," 1.",*HOFF," Item                :":
                    *P1:20,*V2LON," 2.",*HOFF," Description         :":
                    *P1:21,*V2LON," 3.",*HOFF," Security Level      :":
                    *P1:22,*V2LON," 4.",*HOFF," Position Row/Column :"
          MOVE      "27",CCOL
          MOVE      "17",CVERT
          MOVE      TWO,CKEYTYP
          MOVE      PRGIDB,SCSLPID
          MOVE      SCRIDB,SCSLSID
          CALL      KSCSLA00
          BRANCH    EXIT,SEDIT900,SEDIT900
          MATCH     "  0",SCSLSEL
          GOTO      SEDIT900 IF EQUAL       * exit
          PACK      KEY13,PRGIDB,SCRIDB,SCSLSEL
          CALL      RDSCSL1
          IF        OVRCD=0
            MOVE      ZERO,ACCEPT
            PACK      KEY15,PRGIDF,SCRIDF,SCPSITM
            CALL      RDSCFL1
            MOVE      SCSLSEC,F2
            MOVE      F2,F1
            DISPLAY   *P27:19,*V2LON,SCSLITM,SP1,*HOFF,SCFLDES:
                      *P27:20,*V2LON,SCSLDES:
                      *P27:21,*V2LON,F1
            IF        SCFLTYP=0|SCFLTYP=1|SCFLTYP=5
              DISPLAY   *P27:22,*V2LON,SCSLROW,*HOFF,"/",*V2LON,SCSLCOL
            ELSE
              DISPLAY   *P27:22,*V2LON,"N/A"
            ENDIF
          ELSE
            MOVE      ONE,ACCEPT
            MOVE      PRGIDB,SCSLPID
            MOVE      SCRIDB,SCSLSID
            MOVE      SP70,SCSLITM
            MOVE      SP70,SCSLDES
            MOVE      SP70,SCSLROW
            MOVE      SP70,SCSLCOL
            MOVE      SP70,SCSLSEC
          ENDIF
          MOVE      ZERO,CCITEM
.
SEDIT010  ADD       ONE,CCITEM
          BRANCH    ACCEPT,SEDIT020
.
          CALL      MDELQST
          COMPARE   ZERO,CCITEM
          GOTO      SEDIT800 IF EQUAL
.
          COMPARE   SEQ,CCITEM
          GOTO      SEDIT001 IF EQUAL
.
          COMPARE   "-2",CCITEM
          GOTO      SEDIT850 IF EQUAL
.
SEDIT020  BRANCH    CCITEM,SEDIT110,SEDIT120,SEDIT130,SEDIT140
          BRANCH    ACCEPT,SEDIT030
          BEEP
SEDIT030  MOVE      ZERO,ACCEPT
          GOTO      SEDIT010

.
. Option    1
.
SEDIT110  MOVE      "27",CCOL
          MOVE      "19",CVERT
          MOVE      ZERO,CKEYTYP
          MOVE      SP8,SCFLFNO
          MOVE      SP70,SCFLDES
.
          MOVE      PRGIDF,SCFLPID          * set program id
          MOVE      SCRIDF,SCFLSID          * set screen  id
          DISPLAY   *PCCOL:CVERT,*V2LON,SCFLFNO,SP1,*HOFF,SCFLDES
          CALL      KSCFLA00
          BRANCH    EXIT,SEDIT110,SEDIT001
.
. Invalid if field is a normal key field
. Invalid if field is a multi item and is not the key field
.
          IF        SCFLTYP=0
            IF        SCFLMUL=0
              DISPLAY   *P1:24,*B,*EL,"Key Field Item.  ";
              CALL      HITENTER
              GOTO      SEDIT110
            ENDIF
          ELSE
            IF       SCFLMUL=1
              DISPLAY   *P1:24,*B,*EL:
                        "You may only select the multiple key field.  ";
              CALL      HITENTER
              GOTO      SEDIT110
            ENDIF
          ENDIF
.
. Invalid for display or prompt type fields
.
          IF        SCFLTYP=2|SCFLTYP=6
            DISPLAY   *P1:24,*B,*EL,"Display Only Item.  ";
            CALL      HITENTER
            GOTO      SEDIT110
          ENDIF
.
          PACK      KEY20,PRGIDB,SCRIDB,SCFLFNO,SP70
          CALL      RSSCPS2
          CALL      RKSCPS2
          BRANCH    OVRCD,SEDIT116
          MATCH     PRGIDB,SCPSPID
          GOTO      SEDIT116 IF NOT EQUAL
          MATCH     SCRIDB,SCPSSID
          GOTO      SEDIT116 IF NOT EQUAL
          MATCH     SCFLFNO,SCPSITM
          GOTO      SEDIT116 IF NOT EQUAL
          MOVE      SCPSROW,SCSLROW
          MOVE      SCPSCOL,SCSLCOL
          DISPLAY   *P27:22,*V2LON,SCSLROW,*HOFF,"/",*V2LON,SCSLCOL
          GOTO      SEDIT117

SEDIT116  IF        SCFLTYP=3|SCFLTYP=4|SCFLTYP=5|SCFLTYP=7
            DISPLAY   *P27:22,*EL,*V2LON,"N/A"
          ELSE
            DISPLAY   *P1:24,*EL,*B,"Item Not on Screen.  ";
            CALL      HITENTER
            GOTO      SEDIT110
          ENDIF
.
SEDIT117  MOVE      SCFLFNO,SCSLITM
          DISPLAY   *PCCOL:CVERT,*V2LON,SCFLFNO,SP1,*HOFF,SCFLDES
          IF        ACCEPT=1
            MOVE      SCFLDES,SCSLDES
          ENDIF
          GOTO      SEDIT010
.
. Option    2
.
SEDIT120  MOVE      "27",CCOL
          MOVE      "20",CVERT
          KEYIN     *PCCOL:CVERT,*RV,*V2LON,SCSLDES:
                    *PCCOL:CVERT,*DV,*V2LON,SCSLDES
          GOTO      SEDIT010
.
.
. Option    3
.
SEDIT130  MOVE      "27",CCOL
          MOVE      "21",CVERT
          KEYIN     *PCCOL:CVERT,*RV,*V2LON,F1:
                    *PCCOL:CVERT,*DV,*V2LON,F1
          MOVE      F1,F2
          MOVE      F2,SCSLSEC
          GOTO      SEDIT010
.
. Option    4
.
SEDIT140  MOVE      "27",CCOL
          MOVE      "22",CVERT
          BRANCH    ACCEPT,SEDIT010
          IF        SCFLTYP=3|SCFLTYP=4|SCFLTYP=7
            DISPLAY   *P27:22,*V2LON,"N/A"
            GOTO      SEDIT010
          ENDIF
          CALL      RKSCPS2
          BRANCH    OVRCD,SEDIT145
          MATCH     PRGIDB,SCPSPID
          GOTO      SEDIT145 IF NOT EQUAL
          MATCH     SCRIDB,SCPSSID
          GOTO      SEDIT145 IF NOT EQUAL
          MATCH     SCSLITM,SCPSITM
          GOTO      SEDIT145 IF NOT EQUAL
          MOVE      SCPSROW,SCSLROW
          MOVE      SCPSCOL,SCSLCOL
          DISPLAY   *P27:22,*V2LON,SCSLROW,*HOFF,"/",*V2LON,SCSLCOL
          GOTO      SEDIT010
.
SEDIT145  PACK      KEY20,PRGIDB,SCRIDB,SCSLITM,SP70
          CALL      RSSCPS2
          CALL      RKSCPS2
          BRANCH    OVRCD,SEDIT148
          MATCH     PRGIDB,SCPSPID
          GOTO      SEDIT148 IF NOT EQUAL
          MATCH     SCRIDB,SCPSSID
          GOTO      SEDIT148 IF NOT EQUAL
          MATCH     SCSLITM,SCPSITM
          GOTO      SEDIT148 IF NOT EQUAL
          MOVE      SCPSROW,SCSLROW
          MOVE      SCPSCOL,SCSLCOL
          DISPLAY   *P27:22,*V2LON,SCSLROW,*HOFF,"/",*V2LON,SCSLCOL
          GOTO      SEDIT010

SEDIT148  IF        SCFLTYP=3|SCFLTYP=4|SCFLTYP=5|SCFLTYP=7
            DISPLAY   *P27:22,*EL,*V2LON,"N/A"
          ENDIF
          GOTO      SEDIT010
.
SEDIT800
          PACK      KEY13,PRGIDB,SCRIDB,SCSLSEL
          UNPACK    KEY13,SCSLPID,SCSLSID,SCSLSEL
          CALL      RASCSL1
          IF        OVRCD=1
            CALL      WRSCSL1
          ELSE
            CALL      UPSCSL1
          ENDIF
          GOTO      SEDIT001
.
SEDIT850
          PACK      KEY13,PRGIDB,SCRIDB,SCSLSEL
          CALL      RASCSL1
          IF        OVRCD=0
            CALL      DESCSL1
          ENDIF
          GOTO      SEDIT001
.
SEDIT900
          CALL      PUTSCR00
          RETURN
.----------------------------------------------
. Field Edit
.----------------------------------------------
FEDIT000
          PACK      KEY15,PRGIDB,SCRIDB,CVERT,CCOL
          CALL      RDSCPS1
          IF        OVRCD=1
            MOVE      ONE,ACCEPT
            GETVAR    CDATA,SCPSATT,1,1
            UNPACK    KEY15,SCPSPID,SCPSSID,SCPSROW,SCPSCOL
            MOVE      ZERO,SCPSLEN
            MOVE      SP70,SCPSMTY
            MOVE      SP70,SCPSFMT
            MOVE      SP70,SCPSITM
            MOVE      ZERO,SCPSMAN
            MOVE      ZERO,SCPSNOR
          ELSE
            MOVE      ZERO,ACCEPT
            CALL      UDIS0000                 * Undisplay Screen Item
          ENDIF
.
          CALL      SCRF0000                 * Edit Screen Display
          CALL      SELF0000                 * Modify Field Details
          BRANCH    EXIT,FEDIT900,FEDIT600,FEDIT500
.                        cancel   delete   repeat
.
.         post selected
.
          IF        SCFLTYP=3|SCFLTYP=4|SCFLTYP=7
            GOTO      FEDIT900
          ELSE
            PACK      KEY15,PRGIDB,SCRIDB,SCPSROW,SCPSCOL
            UNPACK    KEY15,SCPSPID,SCPSSID,SCPSROW,SCPSCOL
            CALL      RASCPS1
            IF        OVRCD=0
              CALL      UPSCPS1
            ELSE
              CALL      WRSCPS1
            ENDIF
          ENDIF
          CALL      PCACT000                * see if to post to clacatfd
          GOTO      FEDIT900
.
.         repeat
.
FEDIT500  IF        SCFLTYP=3|SCFLTYP=4|SCFLTYP=7
            GOTO      FEDIT900
          ELSE
            PACK      KEY15,PRGIDB,SCRIDB,SCPSROW,SCPSCOL
            UNPACK    KEY15,SCPSPID,SCPSSID,SCPSROW,SCPSCOL
            CALL      RASCPS1
            IF        OVRCD=0
              CALL      UPSCPS1
            ELSE
              CALL      WRSCPS1
            ENDIF
          ENDIF
          CALL      PUTSCR00
          PACK      KEY15,PRGIDB,SCRIDB,SCPSROW,SCPSCOL
          CALL      RDSCPS1
          IF        OVRCD=0
            CALL      DPITM000
          ENDIF
          MOVE      SCPSROW,CVERT
.
FEDIT550  KEYIN     *P1:1,*EL,"Number of Line for Repeat : ",*V2LON,F2
          IF        F2+CVERT>24
            DISPLAY   *P1:1,*B,*EL,"That Many would not Fit on the Screen - ";
            CALL      HITENT01
            GOTO      FEDIT550
          ENDIF
FEDIT560  IF        F2=0
            GOTO      FEDIT999
          ENDIF
          SUB       ONE,F2
          ADD       ONE,CVERT
          PACK      KEY15,PRGIDB,SCRIDB,CVERT,SCPSCOL
          UNPACK    KEY15,SCPSPID,SCPSSID,SCPSROW,SCPSCOL
          CALL      RASCPS1
          IF        OVRCD=0
            CALL      UPSCPS1
          ELSE
            CALL      WRSCPS1
          ENDIF
          CALL      DPITM000
          GOTO      FEDIT560
.
.         delete
.
FEDIT600  PACK      KEY15,PRGIDB,SCRIDB,SCPSROW,SCPSCOL
          CALL      RASCPS1
          IF        OVRCD=0
            CALL      DESCPS1
          ENDIF
          MOVE      SP2,CATEG               * clear category
          CALL      PCACT000                * see if to delete clacatfd item
          PACK      KEY18,PRGIDB,SCRIDB,SCPSITM,SP70
          CALL      RSSCSL2
FEDIT610  CALL      RKSCSL2
          BRANCH    OVRCD,FEDIT699
          MATCH     PRGIDB,SCSLPID
          GOTO      FEDIT699 IF NOT EQUAL
          MATCH     SCRIDB,SCSLSID
          GOTO      FEDIT699 IF NOT EQUAL
          MATCH     SCPSITM,SCSLITM
          GOTO      FEDIT699 IF NOT EQUAL
          MATCH     SCPSROW,SCSLROW
          GOTO      FEDIT610 IF NOT EQUAL
          MATCH     SCPSCOL,SCSLCOL
          GOTO      FEDIT610 IF NOT EQUAL
          PACK      KEY18,SCSLPID,SCSLSID,SCSLITM,SCSLSEL
          CALL      DESCPS1
          GOTO      FEDIT610
.
FEDIT699  CALL      PUTSCR00
          GOTO      FEDIT999
.
.         finished
.
FEDIT900  CALL      PUTSCR00
          PACK      KEY15,PRGIDB,SCRIDB,SCPSROW,SCPSCOL
          CALL      RDSCPS1
          IF        OVRCD=0
            CALL      DPITM000
          ENDIF
.
FEDIT999  RETURN
.-----------------------------------------
. Item Screen Display
.-----------------------------------------
SCRF0000  MOVE      "1",HLEF
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          MOVE      "1",HTOP
          CALL      GETSCR00
          MOVE      ZERO,HLEF
          MOVE      ZERO,HRIG
          MOVE      ZERO,HBOT
          MOVE      ZERO,HTOP
.
          DISPLAY   *P1:15,*EF
          HLINE     *G37,15,1,80
          DISPLAY   *P4:16,*V2LON,*ULON," Screen Position - ":
                    CCOL,"/",CVERT,SP1,*HOFF:
                    *P2:18,*V2LON,"1. ",*HOFF,"Item Number     : ":
                    *P2:19,*V2LON,"   ",*HOFF,"Field Type      : ":
                    *P2:20,*V2LON,"2. ",*HOFF,"Display Length  : ":
                    *P2:21,*V2LON,"3. ",*HOFF,"Mandatory (Y/N) : "
.
          BRANCH    ACCEPT,SCRF9999         * INSERT mode
.
.         item is already on the screen
.
          PACK      KEY15,PRGIDF,SCRIDF,SCPSITM
          CALL      RDSCFL1
          CALL      GTYPE000
          DISPLAY   *P23:18,*V2LON,SCPSITM,*HOFF,SP1,SCFLDES
          DISPLAY   *P23:19,*V2LON,KTYPDES
          IF        SCFLTYP=0|SCFLTYP=1|SCFLTYP=2|SCFLTYP=5
            DISPLAY   *P45:19,"Min/Max=",*V2LON,SCFLMIN,*HOFF,"/",*V2LON,SCFLMAX
          ENDIF
          DISPLAY   *P23:20,*V2LON,SCPSLEN
          MOVE      NO,YESNO
          LOAD      YESNO,SCPSMAN,YES
          DISPLAY   *P23:21,*V2LON,YESNO
.
          IF        SCFLTYP=2|SCFLTYP=6
            MOVE      SCFLMAN,SCPSMAN
            DISPLAY   *P23:21,*V2LON,"N/A"
          ENDIF
          IF        SCFLTYP=5
            DISPLAY   *P2:22,*V2LON,"4. ",*HOFF,"Number of Lines : "
            DISPLAY   *P23:22,*V2LON,SCPSNOR
          ENDIF
          IF        SCFLTYP=0|SCFLTYP=1|SCFLTYP=2
            IF        SCFLFTY=1
              DISPLAY   *P2:22,*V2LON,"4. ",*HOFF,"Format String   : "
              DISPLAY   *P23:22,*V2LON,SCPSFMT
            ENDIF
          ENDIF
          CALL      CKCAT000                * check if category is there
.
SCRF9999  RETURN
+
.----------------------------------------
. FVIDE Edit
.----------------------------------------
FVIDE000
          PACK      KEY15,PRGIDB,SCRIDB,CVERT,CCOL
          CALL      RDSCPS1
          BRANCH    OVRCD,FVIDE900
          PACK      KEY15,PRGIDF,SCRIDF,SCPSITM
          CALL      RDSCFL1
          BRANCH    OVRCD,FVIDE900
          IF        SCFLTYP=5
            DISPLAY   *P1:1,*B,*EL,"Function Not Available for WP Field - ";
            CALL      HITENT01
            GOTO      FVIDE999
          ENDIF
          PACK      KEY15,PRGIDB,SCRIDB,CVERT,CCOL
.
          DISPLAY   *P1:1,*EL
          DISPLAY   *P1:1,"e",*V2LON,"X",*HOFF,"it, ":
                    *V2LON,"H",*HOFF,"ighlight, ":
                    *V2LON,"R",*HOFF,"everse, ":
                    *V2LON,"B",*HOFF,"link, ":
                    *V2LON,"U",*HOFF,"nderline "
          MOVE      ZERO,FBLINK
          MOVE      ZERO,FV2LON
          MOVE      ZERO,FHON
          MOVE      ZERO,FULON
          GETVAR    CDATA,CATTR,CCOL,CVERT

FVIDE100
          PACK      KEY15,PRGIDB,SCRIDB,CVERT,CCOL
          CALL      RDSCPS1
          IF        OVRCD=0
            PACK      KEY15,PRGIDF,SCRIDF,SCPSITM
            CALL      RDSCFL1
            IF        OVRCD=0
              IF        !(SCFLTYP=5)
                CALL      PUTCH000
                GETVAR    CDATA,CATTR,CCOL,CVERT
                MOVE      CATTR,SCPSATT
                CALL      UPSCPS1
                CALL      DPITM000
              ENDIF
            ENDIF
          ENDIF
.
FVIDE110  KEYIN     *PCCOL:CVERT,*EOFF,*NOEDIT,*+,*CURSON,ANS
          CALL      KMOVE000
          BRANCH    EXIT,FVIDE100
          REP       UPPLOW,ANS
          MATCH     ANSX,ANS
          GOTO      FVIDE999 IF EQUAL
          REP       "0z1z2z3z4z5z6z7z8z9z",ANS
          REP       "B1U2H3R4",ANS
          TYPE      ANS
          GOTO      FVIDE110 IF NOT EQUAL
          MOVE      ANS,F1
          LOAD      FLAG,F1,FBLINK,FULON,FV2LON,FHON
          BRANCH    FLAG,FVIDE200
          STORE     ONE,F1,FBLINK,FULON,FV2LON,FHON
          GOTO      FVIDE100
FVIDE200  STORE     ZERO,F1,FBLINK,FULON,FV2LON,FHON
          GOTO      FVIDE100
.
FVIDE900  DISPLAY   *P1:1,*B,*EL,"Not on a Field - ";
          CALL      HITENT01
          GOTO      FVIDE999
FVIDE999  RETURN
.-------------------------------------------
. Routine to display blanks for an item
. ie undisplay it
.-------------------------------------------
UDIS0000
          MOVE      CVERT,SVERT
          MOVE      CCOL,SCOL
          PACK      KEY15,PRGIDF,SCRIDF,SCPSITM
          CALL      RDSCFL1
          IF        SCFLTYP=5
            GOTO      UDIS0100
          ENDIF
.
          MOVE      SCPSROW,CVERT
          GETVAR    CDATA,CATTR,1,1
          MOVE      ONE,LLCNT
          WHILE     LLCNT<=SCPSLEN
            MOVE      SCPSCOL,CCOL
            ADD       LLCNT,CCOL
            SUB       ONE,CCOL
            PUTVAR    SP1,CATTR,CCOL,CVERT
            ADD       ONE,LLCNT
          DO
          GOTO      UDIS9999
.
UDIS0100  MOVE      SCPSROW,CTOP
          MOVE      SCPSCOL,CLEF
          MOVE      SCPSLEN,LLCNT
          MOVE      SCPSNOR,CVERT
          ASSIGN    CLEF+LLCNT+1,CRIG
          ASSIGN    CTOP+CVERT+1,CBOT
          MOVE      SCPSCOL,CCOL
          BOX       0,CLEF,CTOP,CRIG,CBOT
UDIS9999
          MOVE      SVERT,CVERT
          MOVE      SCOL,CCOL
          RETURN
.
.-------------------------------------------------
. SELF - Select Field
.-------------------------------------------------
SELF0000  MOVE      ZERO,CCITEM
          MOVE      CCOL,SCOL
          MOVE      CVERT,SVERT
.
.---- determine option to execute ----
.
SELF0100  ADD       ONE,CCITEM
          BRANCH    ACCEPT,SELF0500
          CALL      IDELQST
.
          COMPARE   ZERO,CCITEM
          GOTO      SELF9000 IF EQUAL       * Post
.
          COMPARE   SEQ,CCITEM
          GOTO      SELF9500 IF EQUAL       * Cancel
.
          COMPARE   "-2",CCITEM
          GOTO      SELF9600 IF EQUAL       * Delete
          COMPARE   "-3",CCITEM
          GOTO      SELF9700 IF EQUAL       * Repeat
.
SELF0500  BRANCH    CCITEM,SELF1100,SELF1200,SELF1300,SELF1400
.
          BRANCH    ACCEPT,SELF0900
          BEEP
.
SELF0900  MOVE      ZERO,ACCEPT
          GOTO      SELF0100
.
. Option    1
.
SELF1100
          BRANCH    ACCEPT,SELF1110
          PACK      KEY15,PRGIDF,SCRIDF,SCPSITM
          CALL      RDSCFL1
          BRANCH    OVRCD,SELF1110
          COMPARE   ZERO,SCFLMAN
          GOTO      SELF1110 IF EQUAL
          DISPLAY   *P1:24,*EL,*B,"Mandatory Field.  ";
          CALL      HITENTER
          GOTO      SELF0100
.
SELF1110  MOVE      "023",CCOL
          MOVE      "18",CVERT
.
          MOVE      PRGIDF,SCFLPID          * set program id
          MOVE      SCRIDF,SCFLSID          * set screen  id
          MOVE      SCFLFNO,SSCFLFNO
          MOVE      "40",HLEF
          MOVE      "80",HRIG
          MOVE      "1",HTOP
          MOVE      "21",HBOT
          MOVE      ZERO,CKEYTYP
.
          CALL      KSCFLA00
          BRANCH    EXIT,SELF9999,SELF9999
          IF        SCFLTYP=3|SCFLTYP=4|SCFLTYP=7
            DISPLAY   *P1:24,*B,*EL,"Invalid Field Type for Display.  ";
            CALL      HITENTER
            GOTO      SELF1110
          ENDIF
.
          CALL      SPST0000     * Save Position Variables
          BRANCH    SCFLMUL,SELF1120
.
          CALL      COBS0000                * check if on both screens
          BRANCH    EXIT,SELF1110           * in other screen
          GOTO      SELF1190                * item is OK
.
. Multiple Item and not key field must be preceded by the key field
. for its Group
.
SELF1120  IF        SCFLTYP=0
            GOTO      SELF1190
          ENDIF
          PACK      KEY15,PRGIDB,SCRIDB,SCPSROW,SCPSCOL
          CALL      RSSCPS1
SELF1130  CALL      RPSCPS1
          BRANCH    OVRCD,SELF1180
          MATCH     PRGIDB,SCPSPID
          GOTO      SELF1180 IF NOT EQUAL
          MATCH     SCRIDB,SCPSSID
          GOTO      SELF1180 IF NOT EQUAL
          MATCH     SSCPSROW,SCPSROW
          GOTO      SELF1180 IF NOT EQUAL
          CALL      ISAVE000
          PACK      KEY15,PRGIDF,SCRIDF,SCPSITM
          CALL      RDSCFL1
          BRANCH    OVRCD,SELF1140
          COMPARE   ZERO,SCFLTYP
          GOTO      SELF1140 IF NOT EQUAL
          MATCH     SCFLMTY,SSCFLMTY
          GOTO      SELF1150 IF EQUAL
SELF1140  CALL      IREST000
          GOTO      SELF1130
SELF1150  CALL      IREST000
          GOTO      SELF1190
.
SELF1180  DISPLAY   *P1:24,*EL,*B:
                    "Multiple Field must be preceded by its Key Field.  ";
          CALL      HITENTER
          CALL      RPST0000     * Restore Position Variables
          GOTO      SELF1110
.
SELF1190  CALL      RPST0000     * Restore Position Variables
          MOVE      SCFLFNO,SCPSITM
          MOVE      SCFLMTY,SCPSMTY
          DISPLAY   *PCCOL:CVERT,*V2LON,SCPSITM,*HOFF,SP1,SCFLDES
.
          CALL      GTYPE000
          DISPLAY   *P23:19,*V2LON,KTYPDES
          IF        SCFLTYP=0|SCFLTYP=1|SCFLTYP=2|SCFLTYP=5
            DISPLAY   *P45:19,"Min/Max=",*V2LON,SCFLMIN,*HOFF,"/",*V2LON,SCFLMAX
          ENDIF
          IF        SCFLMAX=SCFLMIN
            MOVE      SCFLMAX,SCPSLEN
            DISPLAY   *P23:20,*V2LON,SCPSLEN
            ADD       ONE,CCITEM
            IF        SCFLMAN=1
              ADD       ONE,CCITEM
            ENDIF
          ENDIF
          IF        SCFLMAN=1
            MOVE      SCFLMAN,SCPSMAN
            DISPLAY   *P23:21,*V2LON,YES
          ENDIF
          IF        SCFLTYP=2|SCFLTYP=6
            MOVE      SCFLMAN,SCPSMAN
            DISPLAY   *P23:21,*V2LON,"N/A"
          ENDIF
          MOVE      SCFLFNO,SCPSITM
          MOVE      SCFLMTY,SCPSMTY
          IF        SCFLTYP=5
            DISPLAY   *P2:22,*V2LON,"4. ",*HOFF,"Number of Lines : "
          ENDIF
          IF        SCFLTYP=0|SCFLTYP=1|SCFLTYP=2
            IF        SCFLFTY=1
              DISPLAY   *P2:22,*V2LON,"4. ",*HOFF,"Format String   : "
            ENDIF
          ENDIF
.
          MOVE      SCPSITM,FORM5           * have added new field so check if 
          MOVE      ZERO,KEYCAT             * need this keyin
          IF        ((FORM5 >=1 & FORM5 <= 40) | (FORM5 >= 241 & FORM5 <= 280))
            ASSIGN    FORM5%2,FORM5M
            IF        FORM5M = ONE
              MOVE      ONE,KEYCAT
              DISPLAY   *P2:22,*V2LON,"4. ",*HOFF,"Code's Category : "
            ENDIF
          ENDIF
          GOTO      SELF0100
.
. Option    2
.
SELF1200  MOVE      "023",CCOL
          MOVE      "20",CVERT
          IF        SCFLMAX=SCFLMIN
            MOVE      SCFLMAX,SCPSLEN
            DISPLAY   *P1:24,*B,*EL,"Length Fixed.  ";
            CALL      HITENTER
            GOTO      SELF0100
          ENDIF
.
          KEYIN     *PCCOL:CVERT,*V2LON,SCPSLEN:
                    *PCCOL:CVERT,*DV,*V2LON,SCPSLEN:
                    *PCCOL:CVERT
.
          IF        SCPSLEN>SCFLMAX
            DISPLAY   *P1:24,*B,*EL,"Maximum Length = ",SCFLMAX,".  ";
            CALL      HITENTER
            GOTO      SELF1200
          ENDIF
          IF        SCPSLEN<SCFLMIN
            DISPLAY   *P1:24,*B,*EL,"Minmum Length = ",SCFLMIN,".  ";
            CALL      HITENTER
            GOTO      SELF1200
          ENDIF
          MOVE      SCPSCOL,CCOL
          ADD       SCPSLEN,CCOL
          IF        CCOL-1>80
            DISPLAY   *P1:24,*B,*EL,"This would not fit on the Screen.  ";
            CALL      HITENTER
            GOTO      SELF1200
          ENDIF
.
          GOTO      SELF0100
.
. Option    3
.
SELF1300  MOVE      "023",CCOL
          MOVE      "21",CVERT
          IF        SCFLTYP=2
            MOVE      SCFLMAN,SCPSMAN
            BEEP
            GOTO      SELF0100
          ENDIF
          IF        SCFLMAN=1
            MOVE      SCFLMAN,SCPSMAN
            DISPLAY   *P1:24,*B,*EL,"Field Must be Mandatory.  ";
            CALL      HITENTER
            GOTO      SELF0100
          ENDIF
          CALL      YORN0000
          MOVE      FYESNO,SCPSMAN
          DISPLAY   *PCCOL:CVERT,*V2LON,YESNO
          GOTO      SELF0100
.
.         Option  4
.
SELF1400  MOVE      "023",CCOL
          MOVE      "22",CVERT
          IF        SCFLTYP=5
            KEYIN     *PCCOL:CVERT,*V2LON,SCPSNOR:
                      *PCCOL:CVERT,*DV,*V2LON,SCPSNOR:
                      *PCCOL:CVERT
            MOVE      SCPSROW,CVERT
            ADD       SCPSNOR,CVERT
            IF        CVERT+1>24
              DISPLAY   *P1:24,*B,*EL,"This would not fit on the Screen.  ";
              CALL      HITENTER
              GOTO      SELF1400
            ENDIF
            MOVE      SP70,SCPSFMT
            GOTO      SELF0100
          ENDIF
          IF        SCFLTYP=0|SCFLTYP=1|SCFLTYP=2
            MOVE      ZERO,SCPSNOR
            IF        SCFLFTY=1
              KEYIN     *PCCOL:CVERT,*V2LON,SCPSFMT:
                        *PCCOL:CVERT,*DV,*V2LON,SCPSFMT:
                        *PCCOL:CVERT
              GOTO      SELF0100
            ENDIF
          ENDIF
.
          CALL      KCAT0000                * see if to keyin cat
          GOTO      SELF0100
.
.---- exit points ----
.
SELF9000  MOVE      ZERO,EXIT               * POST
          GOTO      SELF9999
.
SELF9500  MOVE      ONE,EXIT                * CANCEL
          GOTO      SELF9999
.
SELF9600  MOVE      TWO,EXIT                * DELETE
          COMPARE   ZERO,SCFLMAN
          GOTO      SELF9999 IF EQUAL
          COMPARE   ONE,SCFLMUL
          GOTO      SELF9999 IF EQUAL
          DISPLAY   *P1:24,*EL,*B,"IBA Mandatory Field. Deletion Denied.  ";
          CALL      HITENTER
          GOTO      SELF0100
.
SELF9700  MOVE      THREE,EXIT              * REPEAT
          COMPARE   ONE,SCFLMUL
          GOTO      SELF9999 IF EQUAL
          COMPARE   TWO,SCFLTYP
          GOTO      SELF9999 IF EQUAL
          DISPLAY   *P1:24,*EL,*B,"Field Not Repeatable.  ";
          CALL      HITENTER
          GOTO      SELF0100
.
SELF9999  MOVE      SCOL,CCOL
          MOVE      SVERT,CVERT
          RETURN
.--------------------------------
. Save Position Variables
.--------------------------------
SPST0000  MOVE      SCPSPID,SSCPSPID
          MOVE      SCPSSID,SSCPSSID
          MOVE      SCPSROW,SSCPSROW
          MOVE      SCPSCOL,SSCPSCOL
          MOVE      SCPSITM,SSCPSITM
          MOVE      SCPSMAN,SSCPSMAN
          MOVE      SCPSLEN,SSCPSLEN
          MOVE      SCPSATT,SSCPSATT
          MOVE      SCPSNOR,SSCPSNOR
          MOVE      SCPSFMT,SSCPSFMT
          MOVE      SCPSMTY,SSCPSMTY
          RETURN
.--------------------------------
. Restore Position Variables
.--------------------------------
RPST0000  MOVE      SSCPSPID,SCPSPID
          MOVE      SSCPSSID,SCPSSID
          MOVE      SSCPSROW,SCPSROW
          MOVE      SSCPSCOL,SCPSCOL
          MOVE      SSCPSITM,SCPSITM
          MOVE      SSCPSMAN,SCPSMAN
          MOVE      SSCPSLEN,SCPSLEN
          MOVE      SSCPSATT,SCPSATT
          MOVE      SSCPSNOR,SCPSNOR
          MOVE      SSCPSFMT,SCPSFMT
          MOVE      SSCPSMTY,SCPSMTY
          RETURN
.
.          PACK      KEY18,SCPSPID,SCPSSID,SCPSSEL,SP70
.-------------------------------------------------------
. FMOV Item
.-------------------------------------------------------
FMOV0000  DISPLAY   *P1:1,*EL,"e",*V2LON,"X",*HOFF,"it"
          MOVE      ONE,MOVEFLAG
          MOVE      CVERT,DIMCVERT
          MOVE      CCOL,DIMCCOL
          PACK      KEY15,PRGIDB,SCRIDB,CVERT,CCOL,SP70
          CALL      RDSCPS1
          BRANCH    OVRCD,FMOV9000
          PACK      KEY15,PRGIDB,SCRIDB,CVERT,CCOL
          CALL      DESCPS1
.
          CALL      UDIS0000
.
          DISPLAY   *PCCOL:CVERT,*HON,ANSX
          MOVE      SCPSATT,SSCPSATT
          GETVAR    CDATA,SCPSATT,CCOL,CVERT
          DISPLAY   *PCCOL:CVERT,SP1
          MOVE      MINCOL,HLEF
          MOVE      MAXCOL,HRIG
          MOVE      MINLINE,HTOP
          MOVE      MAXLINE,HBOT
          GETVAR    CDATA80,CATTR80,1,CVERT
.
FMOV1000  CALL      DPITM000
          KEYIN     *PCCOL:CVERT,*HOFF,*EOFF,*NOEDIT,*+,ANS
          PUTVAR    CDATA80,CATTR80,1,CVERT
          CALL      KMOVE000
          COMPARE   ZERO,EXIT
          GOTO      FMOV1100 IF EQUAL
          GETVAR    CDATA80,CATTR80,1,CVERT
          MOVE      CCOL,SCPSCOL
          MOVE      CVERT,SCPSROW
          GOTO      FMOV1000
.
FMOV1100  REP       UPPLOW,ANS
          MATCH     ANSX,ANS
          GOTO      FMOV1000 IF NOT EQUAL
.
          PACK      KEY15,PRGIDB,SCRIDB,CVERT,CCOL
          CALL      RASCPS1
          BRANCH    OVRCD,FMOV2000
          DISPLAY   *P1:24,*EL,*B,"Item At This Position.  ";
          CALL      HITENTER
          GOTO      FMOV1000
.
FMOV2000  MOVE      SSCPSATT,SCPSATT
          PACK      KEY15,PRGIDB,SCRIDB,CVERT,CCOL
          UNPACK    KEY15,SCPSPID,SCPSSID,SCPSROW,SCPSCOL
          CALL      WRSCPS1
          PACK      KEY18,PRGIDB,SCRIDB,SCPSITM,SP70
          CALL      RSSCSL2
FMOV2010  CALL      RKSCSL2
          BRANCH    OVRCD,FMOV2100
          MATCH     SCSLPID,PRGIDB
          GOTO      FMOV2100 IF NOT EQUAL
          MATCH     SCSLSID,SCRIDB
          GOTO      FMOV2100 IF NOT EQUAL
          MATCH     SCSLITM,SCPSITM
          GOTO      FMOV2100 IF NOT EQUAL
          MATCH     SCSLROW,DIMCVERT
          GOTO      FMOV2010 IF NOT EQUAL
          MATCH     SCSLCOL,DIMCCOL
          GOTO      FMOV2010 IF NOT EQUAL
          PACK      KEY13,SCSLPID,SCSLSID,SCSLSEL
          CALL      RDSCSL1
          IF        OVRCD=0
            MOVE      SCPSROW,SCSLROW
            MOVE      SCPSCOL,SCSLCOL
            CALL      UPSCSL1
          ENDIF
          GOTO      FMOV2010
.
FMOV2100
          MOVE      ZERO,MOVEFLAG
          CALL      DPITM000
          GOTO      FMOV9999
.
FMOV9000  DISPLAY   *P1:1,*B,*EL,"Not on a Field - ";
          CALL      HITENT01
          GOTO      FMOV9999
.
FMOV9999
          MOVE      ZERO,MOVEFLAG
          RETURN
.******************************************************************************
.         Subroutine to SELECT ITEM, POST, DELETE or CANCEL
.------------------------------------------------------------------------------
MDELQST   MOVE      ZERO,CCITEM
          MOVE      SP10,CCITEMD
          KEYIN     *P1:24,*EL,"Select Item, (",*V2LON,"P",*HOFF,")ost, (":
                    *V2LON,"C",*HOFF,")ancel, (":
                    *V2LON,"D",*HOFF,")elete ? ",*V2LON,*JR,CCITEMD;
.
          RESET     CCITEMD
          GOTO      MDELQST IF EOS
.
          REP       UPPLOW,CCITEMD
.
          MATCH     "  C",CCITEMD
          GOTO      MDELQST1 IF EQUAL
.
          MATCH     "  P",CCITEMD
          GOTO      MDELQST2 IF EQUAL
.
          MATCH     "  D",CCITEMD
          GOTO      MDELQST3 IF EQUAL
.
          TYPE      CCITEMD
          GOTO      MDELQSTE IF NOT EQUAL
.
          MOVE      CCITEMD,CCITEM
          COMPARE   ONE,CCITEM
          GOTO      MDELQSTE IF LESS
.
          RETURN
.
MDELQSTE  BEEP
          GOTO      MDELQST
.
MDELQST1  MOVE      "-1",CCITEM
          RETURN
.
MDELQST2  MOVE      "0",CCITEM
          RETURN
.
MDELQST3  MOVE      "-2",CCITEM
          RETURN
.
.******************************************************************************
.         Subroutine to SELECT ITEM, POST, DELETE or CANCEL
.------------------------------------------------------------------------------
IDELQST   MOVE      ZERO,CCITEM
          MOVE      SP10,CCITEMD
          KEYIN     *P1:24,*EL,"Select Item, (",*V2LON,"P",*HOFF,")ost, (":
                    *V2LON,"C",*HOFF,")ancel, (":
                    *V2LON,"R",*HOFF,")epeat, (":
                    *V2LON,"D",*HOFF,")elete ? ",*V2LON,*JR,CCITEMD;
.
          RESET     CCITEMD
          GOTO      IDELQST IF EOS
.
          REP       UPPLOW,CCITEMD
.
          MATCH     "  C",CCITEMD
          GOTO      IDELQST1 IF EQUAL
.
          MATCH     "  P",CCITEMD
          GOTO      IDELQST2 IF EQUAL
.
          MATCH     "  D",CCITEMD
          GOTO      IDELQST3 IF EQUAL
.
          MATCH     "  R",CCITEMD
          GOTO      IDELQST4 IF EQUAL
.
          TYPE      CCITEMD
          GOTO      IDELQSTE IF NOT EQUAL
.
          MOVE      CCITEMD,CCITEM
          COMPARE   ONE,CCITEM
          GOTO      IDELQSTE IF LESS
.
          RETURN
.
IDELQSTE  BEEP
          GOTO      IDELQST
.
IDELQST1  MOVE      "-1",CCITEM
          RETURN
.
IDELQST2  MOVE      "0",CCITEM
          RETURN
.
IDELQST3  MOVE      "-2",CCITEM
          RETURN
.
IDELQST4  MOVE      "-3",CCITEM
          RETURN
.
KTYPE000  DISPLAY  *P1:24,*EL,"Type : ",*V2LON,"0.",*HOFF,"Key, ":
                                        *V2LON,"1.",*HOFF,"Sel., ":
                                        *V2LON,"2.",*HOFF,"Dis., ":
                                        *V2LON,"3.",*HOFF,"Fun., ":
                                        *V2LON,"4.",*HOFF,"Scr., ":
                                        *V2LON,"5.",*HOFF,"WP, ":
                                        *V2LON,"6.",*HOFF,"Prompt, ":
                                        *V2LON,"7.",*HOFF,"Gen.Fun.";
          KEYIN    *PCCOL:CVERT,*V2LON,SCFLTYP:
                   *PCCOL:CVERT,*DV,SCFLTYP
          ADD      ONE,SCFLTYP
          BRANCH   SCFLTYP,KTYPE900,KTYPE900,KTYPE900,KTYPE900,KTYPE900:
                           KTYPE900,KTYPE900,KTYPE900
          BEEP
          GOTO     KTYPE000
.
KTYPE900  SUB      ONE,SCFLTYP
          RETURN

GTYPE000  BRANCH   SCFLTYP,GTYPE100,GTYPE200,GTYPE300,GTYPE400,GTYPE500:
                           GTYPE600,GTYPE700
          MOVE     "Key            ",KTYPDES
          GOTO     GTYPE999
GTYPE100  MOVE     "Selection      ",KTYPDES
          GOTO     GTYPE999
GTYPE200  MOVE     "Display        ",KTYPDES
          GOTO     GTYPE999
GTYPE300  MOVE     "Function       ",KTYPDES
          GOTO     GTYPE999
GTYPE400  MOVE     "Screen         ",KTYPDES
          GOTO     GTYPE999
GTYPE500  MOVE     "Word Processor ",KTYPDES
          GOTO     GTYPE999
GTYPE600  MOVE     "Prompt         ",KTYPDES
          GOTO     GTYPE999
GTYPE700  MOVE     "Generated Funct",KTYPDES
          GOTO     GTYPE999
GTYPE999  RETURN
.
KEXIT000  DISPLAY  *P1:24,*EL,"Type : ",*V2LON,"0.",*HOFF," Exit, ":
                                        *V2LON,"1.",*HOFF," Prompt, ":
                                        *V2LON,"2.",*HOFF," Restart, ":
                                        *V2LON,"3.",*HOFF," Restore";
          KEYIN    *PCCOL:CVERT,*V2LON,SCFLFEX:
                   *PCCOL:CVERT,*DV,SCFLFEX
          ADD      ONE,SCFLFEX
          BRANCH   SCFLFEX,KEXIT900,KEXIT900,KEXIT900,KEXIT900
          BEEP
          GOTO     KEXIT000
KEXIT900  SUB      ONE,SCFLFEX
          RETURN
.
HITENT01  KEYIN     "Hit <",*V2LON,"ENTER",*HOFF,"> to continue ":
                    *EOFF,ANS,*P1:01,*EL;
          RETURN
.
ISAVE000  MOVE      PRGIDF,SSCFLPID
          MOVE      SCFLSID,SSCFLSID
          MOVE      SCFLFNO,SSCFLFNO
          MOVE      SCFLDES,SSCFLDES
          MOVE      SCFLTYP,SSCFLTYP
          MOVE      SCFLFLD,SSCFLFLD
          MOVE      SCFLCAL,SSCFLCAL
          MOVE      SCFLWPF,SSCFLWPF
          MOVE      SCFLFSC,SSCFLFSC
          MOVE      SCFLDEC,SSCFLDEC
          MOVE      SCFLMIN,SSCFLMIN
          MOVE      SCFLMAX,SSCFLMAX
          MOVE      SCFLFLN,SSCFLFLN
          MOVE      SCFLFTY,SSCFLFTY
          MOVE      SCFLKTY,SSCFLKTY
          MOVE      SCFLMAN,SSCFLMAN
          MOVE      SCFLFEX,SSCFLFEX
          MOVE      SCFLDAF,SSCFLDAF
          MOVE      SCFLSPA,SSCFLSPA
          MOVE      SCFLMUL,SSCFLMUL
          MOVE      SCFLMTY,SSCFLMTY
          MOVE      SCFLKEY,SSCFLKEY
          RETURN

IREST000  
          MOVE      SSCFLPID,PRGIDF
          MOVE      SSCFLSID,SCFLSID
          MOVE      SSCFLFNO,SCFLFNO
          MOVE      SSCFLDES,SCFLDES
          MOVE      SSCFLTYP,SCFLTYP
          MOVE      SSCFLFLD,SCFLFLD
          MOVE      SSCFLCAL,SCFLCAL
          MOVE      SSCFLWPF,SCFLWPF
          MOVE      SSCFLFSC,SCFLFSC
          MOVE      SSCFLDEC,SCFLDEC
          MOVE      SSCFLMIN,SCFLMIN
          MOVE      SSCFLMAX,SCFLMAX
          MOVE      SSCFLFLN,SCFLFLN
          MOVE      SSCFLFTY,SCFLFTY
          MOVE      SSCFLKTY,SCFLKTY
          MOVE      SSCFLMAN,SCFLMAN
          MOVE      SSCFLFEX,SCFLFEX
          MOVE      SSCFLDAF,SCFLDAF
          MOVE      SSCFLSPA,SCFLSPA
          MOVE      SSCFLMUL,SCFLMUL
          MOVE      SSCFLMTY,SCFLMTY
          MOVE      SSCFLKEY,SCFLKEY
          RETURN
.
.*********************************************************************
.                   CKCAT000                    Called by : SCRF0000
.         when item is already on screen, check to see if
.         it should have a category to be keyed in and display the
.         current value if it is on the screen
.*********************************************************************
CKCAT000  MOVE      SCPSITM,FORM5           * field is already on screen
          MOVE      ZERO,KEYCAT
          IF        ((FORM5>=1 & FORM5<=40) | (FORM5>=241 & FORM5<=280))
            ASSIGN    FORM5%2,FORM5M
            IF        FORM5M=1
              MOVE      ONE,KEYCAT
              DISPLAY   *P2:22,*V2LON,"4. ",*HOFF,"Code's Category : "
              CALL      CVRB0000              * get variable number
              UNPACK    SP4,CACTCODE,CACTCHGE
              PACK      KEY8,CASTSITE,VARBLENO
              CALL      RDCACAT1
              MOVE      SCPSITM,FORM5           * field is already on screen
              IF        FORM5 <= 40
                MOVE      CACTCODE,CATEG
              ELSE
                MOVE      CACTCHGE,CATEG
              ENDIF
              MOVE      "Invalid Category",CACDDESC
              PACK      KEY5,CATEG,SP3
              CALL      RDCACOD1
              DISPLAY   *P23:22,*V2LON,CATEG,*HOFF,*P30:22,CACDDESC
            ENDIF
          ENDIF
.
CKCAT999  RETURN
+
.*********************************************************************
.                   CVRB0000                    Called by : CKCAT000
.         Calculate which variable number (1-20) an item is
.         Para's  : SCPSITM       the field number
.         Returns : VARBLENO      the variable number 1-20
.*********************************************************************
CVRB0000  MOVE      SCPSITM,FORM5           * get as a numeric
          MOVE      ZERO,VARBLENO           * clear variable number
.
          IF        FORM5 >= 241 & FORM5 <= 280
            SUB       "240",FORM5             * changeable code field
          ENDIF
.
          LOAD      VARBLENO,FORM5,ONE,ONE,TWO,TWO,THREE:
                                   THREE,FOUR,FOUR,FIVE,FIVE:
                                   SIX,SIX,SEVEN,SEVEN,EIGHT:
                                   EIGHT,NINE,NINE,TEN,TEN:
                                   TEN1,TEN1,TEN2,TEN2,TEN3:
                                   TEN3,TEN4,TEN4,TEN5,TEN5:
                                   TEN6,TEN6,TEN7,TEN7,TEN8:
                                   TEN8,TEN9,TEN9,TWENTY,TWENTY
.
CVRB9999  RETURN
+
.*********************************************************************
.                   PCACT000                    Called by : FEDIT000
.         maintain the record on clacatfd                   FEDIT600
.*********************************************************************
PCACT000  COMPARE   ONE,KEYCAT
          GOTO      PCACT999 IF NOT EQUAL   * not a category field
.
          MOVE      CASTSITE,CACTSITE       * set site
          MOVE      VARBLENO,CACTUNIQ       * set variable number
          PACK      KEY8,CACTSITE,CACTUNIQ
          UNPACK    SP4,CACTCODE,CACTCHGE
.
          CALL      RDCACAT1
          MOVE      SCPSITM,FORM5
          IF        FORM5 <= 40
            MOVE      CATEG,CACTCODE          * set code
          ELSE 
            MOVE      CATEG,CACTCHGE          * set changeable code
          ENDIF
.
          IF        OVRCD = 1
            CALL      WRCACAT1
          ELSE
            CALL      UPCACAT1
          ENDIF
.
PCACT999  RETURN
+
.*********************************************************************
.                   SCRN0000
.         see if there are two screens and enter which one you want
.*********************************************************************
SCRN0000  PACK      SCRIDF,ZERO,CASTTYPE    * set up screen id 
          REP       " 0",SCRIDF             * screen id for item file
.
          PACK      PRGIDB,CLAAUD,CASTFILE  * program id for background file
          MOVE      "01",SCRIDB             * screen id for background file
.
          BRANCH    CASTSCRN,SCRN9999       * only one screen
.
SCRN1000  DISPLAY   *P1:24,"Enter Screen Id : ",*EL;
          KEYIN     *P19:24,*DV,UNDLN1:
                    *P19:24,*V2LON,FORM1
.
          MOVE      ZERO,EXIT
          BRANCH    FORM1,SCRN9999,SCRN5000 * valid id's
.
          MOVE      ONE,EXIT                * set exit flag
          COMPARE   ZERO,FORM1
          GOTO      SCRN9995 IF EQUAL       * exit selected
.
          BEEP
          GOTO      SCRN1000
.
SCRN5000  MOVE      "02",SCRIDB             * set screen id
.
SCRN9995  DISPLAY   *P1:24,*EL;
.
SCRN9999  RETURN
+
.*********************************************************************
.                   KCAT0000                    Called by : SELF1400
.          keyin the category for a coded field
.*********************************************************************
KCAT0000  COMPARE   ONE,KEYCAT
          GOTO      KCAT9999 IF NOT EQUAL   * no category to keyin
.
          CALL      CVRB0000                * get variable number
          MOVE      CVERT,EVERT             * save row
          MOVE      ONE,FORM1               * set as ? not entered
.
KCAT1000  KEYIN     *PCCOL:CVERT,"__":
                    *PCCOL:CVERT,*V2LON,CATEG:
                    *PCCOL:CVERT,*DV,CATEG
.
          PACK      CATEG,CATEG,SP2
.
          MATCH     EXITCHAR,CATEG
          GOTO      KCAT1500 IF EQUAL       * exitchar entered
          MATCH     QUEST,CATEG
          GOTO      KCAT2000 IF NOT EQUAL
.
.         ? entered
.
          IF        FORM1 = ONE
            MOVE      ZERO,HLEF
            CALL      GETSCR00                * save screen if first ?
          ENDIF
          CALL      CLACDADS
          MOVE      ZERO,FORM1
          MOVE      "12",CCOL
          MOVE      "24",CVERT
          DISPLAY   *P1:24,"Category : ",*EL;
          GOTO      KCAT1000
.
.         exitchar entered
.
KCAT1500  UNPACK    SP30,CATEG,CACDDESC
          GOTO      KCAT2500
.
KCAT2000  PACK      KEY5,CATEG,SP3
          CALL      RDCACOD1
          BRANCH    OVRCD,KCAT3000
.
KCAT2500  IF        FORM1 = ZERO
            CALL      PUTSCR00
          ENDIF
          MOVE      EVERT,CVERT
          DISPLAY   *P23:CVERT,*V2LON,CATEG,*HOFF,*P30:CVERT,CACDDESC
          GOTO      KCAT9999
.
KCAT3000  DISPLAY   *P1:24,*B,"Invalid Category.  ",*EL;
          CALL      HITENTER
          BRANCH    FORM1,KCAT1000
          DISPLAY   *P1:24,"Category : ",*EL;
          GOTO      KCAT1000
.
KCAT9999  RETURN
+
.*********************************************************************
.                   COBS0000                    Called by : SELF1110 
.         see if a field is already on current screen and then if
.         it is on both screens if a two screen audit
.*********************************************************************
COBS0000  CALL      SPST0000                * save screen details
          MOVE      SCRIDB,SCRID            * work screen id
          MOVE      CASTSCRN,FORM1          * work number of screens
.
COBS1000  PACK      KEY20,PRGIDB,SCRID,SCFLFNO,SP70
          CALL      RSSCPS2
          CALL      RKSCPS2
          BRANCH    OVRCD,COBS5000          * not on this screen
.
          MATCH     PRGIDB,SCPSPID
          GOTO      COBS5000 IF NOT EQUAL
.
          MATCH     SCRID,SCPSSID
          GOTO      COBS5000 IF NOT EQUAL
.
          MATCH     SCFLFNO,SCPSITM
          GOTO      COBS5000 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Field already on Screen ",*V2LON,SCRID:
                    *HOFF,".  ",*EL;
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      COBS9500
.
.         field not on current screen, so check other screen if one
.
COBS5000  BRANCH    FORM1,COBS9000          * only one screen so valid
.
.         have a two screen site, check other screen
.
          MATCH     "01",SCRID
          IF        @EQUAL
            MOVE      "02",SCRID
          ELSE 
            MOVE      "01",SCRID
          ENDIF
          MOVE      ONE,FORM1               * set so will exit if valid
          GOTO      COBS1000
.
COBS9000  MOVE      ZERO,EXIT               * valid item
.
COBS9500  CALL      RPST0000                * Restore Position Variables
.
COBS9999  RETURN
+
.
.         I/O includes
.
          INC       SYSSTDCD
          INC       CLACATIO/INC            * code categories
          INC       CLACODIO/INC            * codes file
          INC       CLASITIO/INC
          INC       CLASITDS/PBL            * ? for site codes
          INC       CLAVARIO/INC
          INC       KYCLASIT                * keyin clinical audit site
          INC       PAINTDCD
          INC       CLACDADS/PBL            * display cats for clinical audit
          INC       SCRFLDIO/INC
          INC       SCRFLDKY                * keyin field id
          INC       SCRMASIO/INC
          INC       SCRPRGIO/INC
          INC       SCRPSTIO/INC
          INC       SCRSBGIO/INC
          INC       SCRSLTIO/INC
          INC       SCRSLTKY                * keyin selection
...............................................................................
