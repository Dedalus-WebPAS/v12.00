. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS13                                             *
. * Desc      :   Consolidated Report Sequence Maintenance             *
. **********************************************************************
. * Date      :   03.11.1990                                           *
. * Author    :   Peter Eddey                                          *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
.
          INC       FMSSTDDF
          INC       FMSAMAFD/INC
          INC       FMSRSFFD/INC  
          INC       FMSLMAFD/INC
          INC       FMSLMBFD/INC
          INC       FMSPRCFD/INC
          INC       FMSTCFFD/INC
          INC       FMSCONFD/INC
.
.**********************************************************************
.*                   Data variables required                          *
.**********************************************************************
.
DESC32    DIM       32    * Truncated Account Code Description
DIM24     DIM       24
FORM5     FORM      5
INSERTFL  FORM      1
ITEM      FORM      2
ITEM01    DIM       24
ITEM02    DIM       24
ITEM03    DIM       24
ITEM04    DIM       24
ITEM05    DIM       24
ITEM06    DIM       24
ITEM07    DIM       24
ITEM08    DIM       24
ITEM09    DIM       24
ITEM10    DIM       24
ITEM11    DIM       24
ITEM12    DIM       24
ITEM13    DIM       24
ITEM14    DIM       24
LEDGER    DIM       2
LEDGDESC  DIM       35
NACCOUNT  DIM       12
NACCDESC  DIM       35
NEWPOS    DIM       5
NEWSEQ    DIM       5
NEXTSCRF  FORM      1
PRINTPOS  DIM       5
PRNTCODE  DIM       3
CREDITYN  DIM       1
PRNTDESC  DIM       35
RECCOUNT  FORM      2
REPNUMB   DIM       3
ROW       FORM      2
REPDESC   DIM       35
SCRNUMB   FORM      4
SCRCOUNT  FORM      4
TYPEDESC  DIM       7
TOT       INIT      "Total  "
HED       INIT      "Heading"
STA       INIT      "Stat   "
PRINTSEQ  DIM       5
.
.
PRGID     INIT      "IBAFMS13"
PRGNAM    INIT      "Consolidated Report Sequence Maintenance"     
VERSION   INIT      "V12.00.00"
.**********************************************************************
.*                           ML0000                                   *
.*                       Mainline  Code                               *
.**********************************************************************
.
ML0000
          CALL      INIT0000             * initialisation
.
ML0500
          CALL      DHDS0000             * display header details screen
          CALL      CVAR0000             * clear the header variables
.
ML1000
          CALL      KREP0000             * keyin the report code
          BRANCH    EXIT,ML9999,ML9999
.
          MOVE      SIX,CVERT
          MOVE      ZERO,INSERTFL
          CALL      KPRP0000             * keyin the print position
.
          CALL      DRHD0000             * display report details head
.
          MOVE      ONE,SCRNUMB
          PACK      KEY26,REPNUMB,PRINTPOS,SP20
          CALL      DREP0000             * display the first screen
.
ML5000        
          CALL      SNPE0000             * select item , next etc,
          BRANCH    EXIT,ML6100,ML6200,ML6300,ML6400
          GOTO      ML0500
.
ML6100
          CALL      MRLI0000            * modify report lines
          GOTO      ML5000
.
ML6200
          CALL      NPAG0000             * next page routine
          GOTO      ML5000
ML6300
          CALL      PPAG0000             * previous page routine
          GOTO      ML5000 
ML6400
          CALL      IPRD0000             * insert report details
          GOTO      ML5000
.
ML9999    CHAIN     PGM
.
.*********************************************************************
.*                          INIT0000                                 *
.*                     initialisation                                *
.*********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening fmsamaaf"
          OPEN      FMSAMAA1,"fmsamaaf"  * account master file
          OPEN      FMSAMAA2,"fmsamaaf"
          OPEN      FMSAMAA3,"fmsamaaf"
.
          DISPLAY   *P64:24,"fmslmaaf"
          OPEN      FMSLMAA1,"fmslmaaf"  * ledger master file
.
          DISPLAY   *P64:24,"fmsrsfaf"
          OPEN      FMSRSFA1,"fmsrsfaf"  * report sequence file
          OPEN      FMSRSFA2,"fmsrsfaf"
          OPEN      FMSRSFA3,"fmsrsfaf"
.
          DISPLAY   *P64:24,"fmslmbaf"   * report file        
          OPEN      FMSLMBA1,"fmslmbaf"
.
          DISPLAY   *P64:24,"fmsprcaf"   * print codes file
          OPEN      FMSPRCA1,"fmsprcaf"
.
          DISPLAY   *P64:24,"fmstcfaf"   * total calculations file     
          OPEN      FMSTCFA1,"fmstcfaf"
          OPEN      FMSTCFA2,"fmstcfaf"
.
          DISPLAY   *P64:24,"controlf";  * control file
          OPEN      CONTROLF,"controlf"
          CALL      RDFMCO51
          CALL      RDFMCO52
.
INIT9999  RETURN
.
.*********************************************************************
.*                          DHDS0000                                 *
.*                display the header details screen                  *
.*********************************************************************
.
DHDS0000
          DISPLAY   *P1:3,*EF:
                    *P5:5,"Report Code          : ":
                    *P5:6,"From Print Position  : "
DHDS9999
          RETURN
.
.*********************************************************************
.*                          HDAT0000                                 *
.*                    display the header data                        *
.*********************************************************************
.
HDAT0000
          DISPLAY   *P28:5,*V2LON,REPNUMB,*HOFF,*P42:5,REPDESC:
                    *P28:6,*V2LON,PRINTPOS
HDAT9999  RETURN
.
.
.*********************************************************************
.*                          KLED0000                                 *
.*                Keyin the required ledger                          *
.*********************************************************************
.
KLED0000
          MOVE      TWENTY8,CCOL
          MOVE      EIGHT,CVERT
          MOVE      ZERO,EXIT
.
          CALL      KLMAFM00             * Keyin the required ledger
.
          BRANCH    EXIT,KLED9999,KLED9999
.
          MOVE      FMLALEDG,LEDGER
          MOVE      FMLADESC,LEDGDESC
.
          GOTO      KLED9999
.
KLED9999  RETURN
.        
.*********************************************************************
.*                          KACT0000                                 *
.*                    keyin an existing account                      *
.*********************************************************************
.
KACT0000
          CLEAR     NACCOUNT
          CLEAR     NACCDESC
.
          DISPLAY   *P30:8,*V2LON,SLASH
          MOVE      THIRTY1,CCOL
          MOVE      EIGHT,CVERT
          MOVE      ZERO,EXIT
.
          CALL      KAMAFM00             * Keyin  account       
.
          BRANCH    EXIT,KACT9999,KACT9999
          MATCH     "6",FMAMTYPE
          GOTO      KACT1000 IF EQUAL
          MATCH     "7",FMAMTYPE
          GOTO      KACT1000 IF EQUAL
          MATCH     "9",FMAMTYPE
          GOTO      KACT1000 IF EQUAL
          DISPLAY   *P1:24,*EL,"Account Must be a Total or Heading Account. - ";
          CALL      HITENTER
          GOTO      KACT0000
.
KACT1000  MOVE      FMAMACCT,NACCOUNT
          MOVE      FMAMDESC,NACCDESC
          DISPLAY   *P42:8,NACCDESC
.
          GOTO      KACT9999
.
KACT9999  RETURN
.
.*********************************************************************
.*                          KPRP0000                                 *
.*                Keyin in the required print position               *
.*********************************************************************
.
KPRP0000  
          MOVE      FMRFPOS,PRINTPOS
          BRANCH    INSERTFL,KPRP1000
          MOVE      "00000",PRINTPOS
KPRP1000  KEYIN     *P28:CVERT,*DV,UNDLN5:
                    *P28:CVERT,*V2LON,*JR,*ZF,*DE,*RV,PRINTPOS:
                    *P28:CVERT,*DV,PRINTPOS;
.
          BRANCH    INSERTFL,KPRP2000
.
          GOTO      KPRP9999
.
KPRP2000  
          MOVE      "00000",PRINTSEQ
          MATCH     "7",FMAMTYPE
          GOTO      KPRP3000 IF EQUAL
          MOVE      "99999",PRINTSEQ
KPRP3000  KEYIN     *P33:CVERT,"/",*DV,UNDLN5:
                    *P34:CVERT,*V2LON,*JR,*ZF,*DE,*RV,PRINTSEQ:
                    *P34:CVERT,*DV,PRINTSEQ;
          MOVE      PRINTPOS,NEWPOS  
          MOVE      PRINTSEQ,NEWSEQ  
.
KPRP9999  RETURN
.*********************************************************************
.*                          KREP0000                                 *
.*                      Keyin the report number                      *
.*********************************************************************
.
KREP0000
          CLEAR     REPNUMB
          CLEAR     REPDESC
          MOVE      ZERO,EXIT
          MOVE      TWENTY8,CCOL
          MOVE      FIVE,CVERT
.
          CALL      KLMBFM00             * keyin report number
          BRANCH    EXIT,KREP9999,KREP9999
.
          DISPLAY   *PCCOL:CVERT,*V2LON,FMLBRPNO,*HOFF,*P42:CVERT,FMLBDESC;
          MOVE      FMLBRPNO,REPNUMB 
          MOVE      FMLBDESC,REPDESC
.
KREP9999  RETURN 
.
.*********************************************************************
.*                          KPRC0000                                 *
.*                      Keyin the Print  code                        *
.*********************************************************************
.
KPRC0000
          CLEAR     PRNTCODE
          CLEAR     PRNTDESC
          MOVE      ZERO,EXIT
          MOVE      TWENTY8,CCOL
          MOVE      TEN,CVERT
.
          CALL      KPRCFM00             * keyin the print code
.
          BRANCH    EXIT,KPRC9000,KPRC1000
.
          MOVE      FMPRCODE,PRNTCODE
          MOVE      FMPRDESC,PRNTDESC
          DISPLAY   *P42:10,PRNTDESC
          GOTO      KPRC9999
.  
KPRC1000  
          BEEP 
          GOTO      KPRC0000
.
KPRC9000  MOVE      ZERO,EXIT
.
KPRC9999  RETURN 
.
.        
.*********************************************************************
.*                         CVAR0000                                  *
.*                  clear the keyin variables                        *
.*********************************************************************
.
CVAR0000
          UNPACK    SP70,REPNUMB,PRINTPOS
.
CVAR9999  RETURN
.
CPRD0000
          UNPACK    SP70,NACCOUNT,NACCDESC,NEWPOS
          UNPACK    SP70,PRNTCODE,PRNTDESC,LEDGER,NEWSEQ,CREDITYN
CPRD9999  RETURN
.        
.
.**********************************************************************
.*                         SREP0000                                   *
.*                 Store the sub-account details                      *
.**********************************************************************
.
SREP0000
          PACK      DIM24,FMRFPOS,FMRFSEQ,FMRFLED,FMRFACC        
          STORE     DIM24,RECCOUNT,ITEM01,ITEM02,ITEM03,ITEM04,ITEM05:
                                   ITEM06,ITEM07,ITEM08,ITEM09,ITEM10:
                                   ITEM11,ITEM12,ITEM13,ITEM14
SREP9999  RETURN 
.
LREP0000
          LOAD      DIM24,ITEM,ITEM01,ITEM02,ITEM03,ITEM04,ITEM05:
                               ITEM06,ITEM07,ITEM08,ITEM09,ITEM10:
                               ITEM11,ITEM12,ITEM13,ITEM14
LREP9999  RETURN
.
CREP0000
          MOVE      ZERO,ITEM
CREP1000
          ADD       ONE,ITEM
          PACK      DIM24,SP70
          STORE     DIM24,ITEM,ITEM01,ITEM02,ITEM03,ITEM04,ITEM05:
                                   ITEM06,ITEM07,ITEM08,ITEM09,ITEM10:
                                   ITEM11,ITEM12,ITEM13,ITEM14
          COMPARE   TEN4,ITEM
          GOTO      CREP9999 IF EQUAL
.
          GOTO      CREP1000
.
CREP9999  RETURN
.**********************************************************************
.*                        NPAG0000                                    *
.*               Display the next page                                *
.**********************************************************************
.
NPAG0000
          MOVE      TEN5,ITEM
          CALL      LREP0000             * get last item
.
.----   Pack the key and redisplay the screen -----
.
          PACK      KEY26,REPNUMB,DIM24
          CALL      DREP0000
.
NPAG9999  RETURN
.
.**********************************************************************
.*                        PPAG0000                                    *
.*                Display the previous page                           *
.**********************************************************************
.
PPAG0000
          MOVE      ONE,ITEM
          CALL      LREP0000
.
          PACK      KEY26,REPNUMB,DIM24
.
          CALL      RAFMRF1
          MOVE      ZERO,ITEM
.
.-----  read fifteen records back  -----
.
PPAG1000
          CALL      RPFMRF1
          BRANCH    OVRCD,PPAG6000
          ADD       ONE,ITEM
.
          COMPARE   TEN5,ITEM
          GOTO      PPAG5000 IF EQUAL
.
          GOTO      PPAG1000
.
PPAG5000
          PACK      KEY26,FMRFREP,FMRFPOS,FMRFSEQ,FMRFLED,FMRFACC             
          CALL      DREP0000
          GOTO      PPAG9999
PPAG6000
          PACK      KEY26,SP20,SP20
          CALL      DREP0000
.
PPAG9999  RETURN
.
.
.**********************************************************************
.*                        DREP0000                                    *
.*                  Display the sub-accounts                          *
.**********************************************************************
.
.----  The key should already be passed correctly packed -----
.
DREP0000
          DISPLAY   *P1:9,*EF
          MOVE      ZERO,RECCOUNT
          MOVE      ZERO,NEXTSCRF
          MOVE      EIGHT,ROW
          CALL      CREP0000
          CALL      RSFMRF1
DREP1000
          CALL      RKFMRF1
          BRANCH    OVRCD,DREP9999
.
          MATCH     REPNUMB,FMRFREP      * test if same report 
          GOTO      DREP9999 IF NOT EQUAL
.
          ADD       ONE,RECCOUNT         * increment the record count
.
          ADD       ONE,ROW              * increment the row
.
          COMPARE   TEN5,RECCOUNT        * test for a full screen
          GOTO      DREP8000 IF EQUAL
.
DREP4000
          CALL      SREP0000             * store the key
.
          CALL      DISS0000             * display the record
.
          GOTO      DREP1000
.
.----- Full screen ---
.
DREP8000        
          MOVE      ONE,NEXTSCRF         * set next screen flag
          CALL      RPFMTC1              * re-set record pointer
          SUB       ONE,ROW              * re-set the row
          SUB       ONE,RECCOUNT         * re-set the record count
.
DREP9999  RETURN
.
.**********************************************************************
.*                          DISS0000                                  8
.*                Display a sub-account record                        *
.**********************************************************************
.
DISS0000
.
.-----  get the account description -----
.
         PACK       KEY14,FMRFLED,FMRFACC
         CALL       RDFMAM1
         MOVE       FMAMDESC,KEY29
.
.-----  get the print code description ----
.
         CLEAR      FMPRDESC
         CLEAR      FMPRCODE
         PACK       KEY3,FMRFPRT
         CALL       RDFMPR1
.
.----- display the record -----
.
         MOVE       FMAMTYPE,F2
         SUB        "5",F2
         LOAD       TYPEDESC,F2,TOT,HED,STA,STA
.
.----- display the record -----
.
         MOVE       "N",ANS
         MATCH      "1",FMRFCRP
         IF         @EQUAL
           MOVE       "Y",ANS
         ENDIF
         DISPLAY    *P2:ROW,*V2LON,RECCOUNT,".",*HOFF:
                    *P6:ROW,FMRFPOS,"/",FMRFSEQ: 
                    *P18:ROW,FMRFLED,"/",FMRFACC,SP1,KEY29:
                    SP1,ANS,SP1,FMPRCODE,SP4,TYPEDESC
.
DISS9999 RETURN
.*********************************************************************
.*                        RREP0000                                   *
.*                 Redsiply the sub-accounts screen                  *
.*********************************************************************
.
RREP0000
         
          MOVE      ONE,ITEM             * get first item
          CALL      LREP0000
          PACK      KEY26,REPNUMB,DIM24
          CALL      RSFMRF1              * position in the file
.
          CALL      RPFMRF1              * read previous record
          BRANCH    OVRCD,RREP2000         if over use spaces
.
.---- record exists so pack the key ----
.
          PACK      KEY26,FMRFREP,FMRFPOS,FMRFSEQ,FMRFLED,FMRFACC             
          GOTO      RREP5000
.
.---- over condition reached so use spaces -----
.
RREP2000 
          PACK      KEY26,SP30,SP10
.
.-----  redisplay the screen -----
.
RREP5000 
          CALL      DREP0000
.
RREP9999  RETURN
.
.**********************************************************************
.*                         DRDH0000                                   *
.*                    display report details heading                  *
.**********************************************************************
.
DRHD0000  DISPLAY   *P1:8,*EF:
                    *P1:8,*V2LON,*ULON,"Item":
                    *P6:8,"Pos/Seq    ":
                    *P18:8,"Ledger/Account                              ":
                    *P63:8,"Cr":
                    *P66:8,"Print",*P73:8,"Type"                 
DRHD9999  RETURN
.**********************************************************************
.*                         SNPE0000                                   *
.*         This handles the select, next, previous, exit prompts      *
.**********************************************************************
.
SNPE0000
          BRANCH    NEXTSCRF,SNPE5000
.
          BRANCH    SCRNUMB,SNPE1000
.
.----- Prompt is - select, previous, exit -----
.
          CALL      PSPE0000
          GOTO      SNPE9999
.
.----  Prompt is - select, exit -----
.
SNPE1000
          CALL      PSEE0000
          GOTO      SNPE9999
.
	
SNPE5000
          BRANCH    SCRNUMB,SNPE6000
.
.----  Prompt is - select, next, previous, exit ----
.
          CALL      PALL0000
          GOTO      SNPE9999
.
.---- Prompt is - Select, next, exit ----
.
SNPE6000
          CALL      PSNE0000
          
SNPE9999  RETURN
.
.*********************************************************************
.*                         PSEE0000                                  *
.*                handles select exit prompt                         *
.*********************************************************************
.
PSEE0000
          MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL:
                    "Select Item, ":
                    "(",*V2LON,"I",*HOFF,")nsert, ":
                    "e(",*V2LON,"X",*HOFF,")it : "
.
PSEE1000
          KEYIN     *P33:24,*DV,UNDLN2:
                    *P33:24,*V2LON,DIM2:
                    *P33:24,*DV,DIM2
.
          TYPE      DIM2
          GOTO      PSEE8000 IF EQUAL
.
          REP       UPPLOW,DIM2
          MATCH     "X ",DIM2
          GOTO      PSEE9999 IF EQUAL
.
          MATCH     "I ",DIM2
          GOTO      PSEE7000 IF NOT EQUAL
          MOVE      FOUR,EXIT
          GOTO      PSEE9999
PSEE7000
          BEEP
          GOTO      PSEE1000
.
PSEE8000
          MOVE      ONE,EXIT
.
PSEE9999  RETURN
.
.*********************************************************************
.*                         PSNE0000                                  *
.*              handles select, next, exit prompt                    *
.*********************************************************************
.
PSNE0000
          MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL:
                    "Select Item, ":
                    "(",*V2LON,"N",*HOFF,")ext, ":
                    "(",*V2LON,"I",*HOFF,")nsert, ":
                    "e(",*V2LON,"X",*HOFF,")it : "
.
PSNE1000
          KEYIN     *P41:24,*DV,UNDLN2:
                    *P41:24,*V2LON,DIM2:
                    *P41:24,*DV,DIM2
.
          TYPE      DIM2
          GOTO      PSNE8000 IF EQUAL
.
          REP       UPPLOW,DIM2
          MATCH     "X ",DIM2
          GOTO      PSNE9999 IF EQUAL
.
          MATCH     "I ",DIM2
          GOTO      PSNE2000 IF NOT EQUAL
          MOVE      FOUR,EXIT
          GOTO      PSNE9999
PSNE2000
          MATCH     "N ",DIM2
          GOTO      PSNE3000 IF NOT EQUAL
. 
          MOVE      TWO,EXIT 
          ADD       ONE,SCRNUMB
.
          COMPARE   SCRNUMB,SCRCOUNT
          GOTO      PSNE9999 IF NOT EQUAL
.
          ADD       ONE,SCRCOUNT
          GOTO      PSNE9999
PSNE3000
          BEEP
          GOTO      PSNE1000
.
PSNE8000
          MOVE      ONE,EXIT
.
PSNE9999  RETURN
.
.*********************************************************************
.*                        PSPE0000                                   *
.*           handles select, previous, exit prompt                   *
.*********************************************************************
.
PSPE0000
          
          MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL:
                    "Select Item, ":
                    "(",*V2LON,"P",*HOFF,")revious, ":
                    "(",*V2LON,"I",*HOFF,")nsert, ":
                    "e(",*V2LON,"X",*HOFF,")it : "
.
PSPE1000
          KEYIN     *P45:24,*DV,UNDLN2:
                    *P45:24,*V2LON,DIM2:
                    *P45:24,*DV,DIM2
.
          TYPE      DIM2
          GOTO      PSPE8000 IF EQUAL
.
          REP       UPPLOW,DIM2
          MATCH     "X ",DIM2
          GOTO      PSPE9999 IF EQUAL
.
          MATCH     "I ",DIM2
          GOTO      PSPE2000 IF NOT EQUAL
          MOVE      FOUR,EXIT
          GOTO      PSPE9999
PSPE2000
          MATCH     "P ",DIM2
          GOTO      PSPE3000 IF NOT EQUAL
.
          SUB       ONE,SCRNUMB
          MOVE      THREE,EXIT 
          GOTO      PSPE9999
PSPE3000
          BEEP
          GOTO      PSPE1000
.
PSPE8000
          MOVE      ONE,EXIT
.
PSPE9999  RETURN
.
.**********************************************************************
.*                         PALL0000                                   *
.*            Handles select, next, previous, exit, prompt            *
.**********************************************************************
.
PALL0000
          MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL:
                    "Select Item, ":
                    "(",*V2LON,"P",*HOFF,")revious, ":
                    "(",*V2LON,"N",*HOFF,")ext, ":
                    "(",*V2LON,"I",*HOFF,")nsert, ":
                    "e(",*V2LON,"X",*HOFF,")it : "
.
PALL1000
          KEYIN     *P53:24,*DV,UNDLN2:
                    *P53:24,*V2LON,DIM2:
                    *P53:24,*DV,DIM2
.
          TYPE      DIM2
          GOTO      PALL8000 IF EQUAL
.
          REP       UPPLOW,DIM2
          MATCH     "X ",DIM2
          GOTO      PALL9999 IF EQUAL
.
          MATCH     "I ",DIM2
          GOTO      PALL2000 IF NOT EQUAL
          MOVE      FOUR,EXIT
          GOTO      PALL9999
PALL2000
          MATCH     "P ",DIM2
          GOTO      PALL3000 IF NOT EQUAL
.
          SUB       ONE,SCRNUMB
          MOVE      THREE,EXIT 
          GOTO      PALL9999
PALL3000
          MATCH     "N ",DIM2
          GOTO      PALL4000 IF NOT EQUAL
          MOVE      TWO,EXIT
          ADD       ONE,SCRNUMB
          COMPARE   SCRNUMB,SCRCOUNT
          GOTO      PALL9999 IF NOT EQUAL
.
          ADD       ONE,SCRCOUNT
          GOTO      PALL9999
PALL4000
          BEEP     
          GOTO      PALL1000
.
PALL8000
          MOVE      ONE,EXIT
.
PALL9999  RETURN
.
.
.**********************************************************************
.*                         DPRN0000                                   *
.*                display  print details sub screeng                  *
.**********************************************************************
.
DPRN0000
          DISPLAY   *P1:7,*EF:
                    *P5:8,"Ledger/Account       :":
                    *P2:9,*V2LON,"1.",*HOFF," Print Position       :":
                    *P2:10,*V2LON,"2.",*HOFF," Print Code           :":
                    *P2:11,*V2LON,"3.",*HOFF," Credit Print (Y/N)   :"
.
DPRN9999  RETURN
.
.-----  Set up the print data  -----
.
SPRD0000
.
.---- get the account description ----
.
          PACK      KEY14,FMRFLED,FMRFACC
          CALL      RDFMAM1
          MOVE      FMAMACCT,NACCOUNT
          MOVE      FMAMDESC,NACCDESC
.
.---- get the print code description ----
.
          UNPACK    SP70,PRNTCODE,PRNTDESC
          PACK      KEY3,FMRFPRT
          CALL      RDFMPR1
          BRANCH    OVRCD,SPRD1000
          MOVE      FMPRCODE,PRNTCODE
          MOVE      FMPRDESC,PRNTDESC
.
SPRD1000  MOVE      FMRFPOS,NEWPOS
          MOVE      FMRFSEQ,NEWSEQ
          MOVE      FMRFCRP,CREDITYN
.
          MOVE      FMRFLED,LEDGER
          MOVE      FMRFLED,FMLALEDG
.
SPRD9999  RETURN
.
.**********************************************************************
.*                         PDAT0000                                   *
.*                    display the print screen data                   *
.**********************************************************************
.
PDAT0000
          MOVE      ZERO,FYESNO
          MOVE      CREDITYN,FYESNO
          ADD       ONE,FYESNO
          LOAD      YESNO,FYESNO,NO,YES          * get description
          DISPLAY   *P28:8,*V2LON,LEDGER,SLASH,NACCOUNT,*HOFF:
                    *P42:8,NACCDESC:
                    *P28:9,*V2LON,NEWPOS,"/",NEWSEQ:
                    *P28:10,*V2LON,PRNTCODE,*HOFF,*P42:10,PRNTDESC:
                    *P28:11,*V2LON,YESNO
PDAT9999  RETURN
.
.**********************************************************************
.*                         IPRD0000                                   *
.*                  insert report details                             *
.**********************************************************************
.
IPRD0000
          CALL      DPRN0000             * display print position prompt
.
IPRD0100
          CALL      CPRD0000             * clear the data variables
          CALL      PDAT0000             * clear the screen area
.
          CALL      KLED0000
          BRANCH    EXIT,IPRD9999,IPRD9999
.
          CALL      KACT0000             * keyin account
          BRANCH    EXIT,IPRD9999,IPRD9999
.
          MOVE      NINE,CVERT
          MOVE      ONE,INSERTFL
          CALL      KPRP0000             * keyin print position
.
          CALL      KPRC0000             * keyin print code
.
          MOVE      TWENTY8,CCOL
          MOVE      "11",CVERT
          MOVE      "N",DEFYESNO
          CALL      YORN0000
          MOVE      FYESNO,CREDITYN 
.
IPRD0500
          CALL      MAINQST              * select item post cancel
.
          COMPARE   "-1",CCITEM          * test for cancel
          GOTO      IPRD0100 IF EQUAL
.
          COMPARE   ZERO,CCITEM          * test for cancel
          GOTO      IPRD9000 IF EQUAL
.
          BRANCH    CCITEM,IPRD1000,IPRD2000
          BEEP                           * invalid item so re-key
          GOTO      IPRD0500
.
IPRD1000
          MOVE      NINE,CVERT
          MOVE      ONE,INSERTFL
          CALL      KPRP0000             * keyin print position
          GOTO      IPRD0500
.
IPRD2000
          CALL      KPRC0000             * keyin print code
          GOTO      IPRD0500
.
.----- Test if new record exiats and write if it does not ----
.
IPRD9000
          PACK      KEY26,REPNUMB,NEWPOS,NEWSEQ,FMAMLEDG,FMAMACCT
          CALL      RAFMRF1
          BRANCH    OVRCD,IPRD9500
          BEEP
          DISPLAY   *P1:24,*EL,"Record already exists. ";
          CALL      HITENTER
          GOTO      IPRD0500
IPRD9500
          MOVE      LEDGER,FMRFLED
          MOVE      NACCOUNT,FMRFACC
          MOVE      REPNUMB,FMRFREP
          MOVE      NEWPOS,FMRFPOS
          MOVE      NEWSEQ,FMRFSEQ
          MOVE      PRNTCODE,FMRFPRT
          MOVE      CREDITYN,FMRFCRP
          CALL      WRFMRF1
          GOTO      IPRD0100
.
IPRD9999  
          CALL      DRHD0000             * Redisplay the screen
          CALL      RREP0000
          RETURN
.
.**********************************************************************
.*                        MRLI0000                                    *
.*                 modify an existing item                            *
.**********************************************************************
.
MRLI0000
          MOVE      DIM2,ITEM
.
          COMPARE   ITEM,RECCOUNT
          GOTO      MRLI9000 IF LESS 
.
          CALL      LREP0000
          PACK      KEY26,REPNUMB,DIM24
          CALL      RDFMRF1
.
          CALL      DPRN0000             * display prompts and set up
          CALL      SPRD0000               the data
          CALL      PDAT0000             * display the print data
          CALL      MPRD0000             * modify the print data
          GOTO      MRLI9999
.
MRLI9000  BEEP
.
MRLI9999  RETURN
.
.**********************************************************************
.*                        MPRD0000                                    *
.*                  modify print  details                             *
.**********************************************************************
.
MPRD0000
          CALL      MQST0000             * select
.
          COMPARE   "-2",CCITEM          * test for run report
          GOTO      MPRD8000 IF EQUAL
.
          COMPARE   "-1",CCITEM          * test for cancel
          GOTO      MPRD9999 IF EQUAL
.
          COMPARE   ZERO,CCITEM          * test for post
          GOTO      MPRD9000 IF EQUAL
.
          BRANCH    CCITEM,MPRD1000,MPRD2000,MPRD3000
          BEEP
          GOTO      MPRD0000
.
MPRD1000
          MOVE      NINE,CVERT
          MOVE      ONE,INSERTFL
          CALL      KPRP0000             * keyin the print position
          GOTO      MPRD0000
.
MPRD2000  CALL      KPRC0000             * keyin the print code
          GOTO      MPRD0000
.
MPRD3000  MOVE      TWENTY8,CCOL
          MOVE      "11",CVERT
          MOVE      "N",DEFYESNO
          CALL      YORN0000
          MOVE      FYESNO,CREDITYN
          GOTO      MPRD0000
.
MPRD8000
          PACK      KEY26,REPNUMB,FMRFPOS,FMRFSEQ,FMRFLED,FMRFACC
          CALL      DEFMRF1
          GOTO      MPRD9999
MPRD9000
          MATCH     NEWPOS,FMRFPOS       * test if position has change
          GOTO      MPRD9400 IF NOT EQUAL      (part of key)
.
          MATCH     NEWSEQ,FMRFSEQ       * test if position has change
          GOTO      MPRD9500 IF EQUAL      (part of key)
.
MPRD9400  PACK      KEY26,REPNUMB,NEWPOS,NEWSEQ,FMRFLED,FMRFACC 
          CALL      RAFMRF1
          BRANCH    OVRCD,MPRD9500
          BEEP
          DISPLAY   *P1:24,*EL,"Record already exists. ";
          CALL      HITENTER
          GOTO      MPRD0000
MPRD9500
          PACK      KEY26,REPNUMB,FMRFPOS,FMRFSEQ,FMRFLED,FMRFACC
          CALL      RAFMRF1
          MOVE      PRNTCODE,FMRFPRT
          MOVE      CREDITYN,FMRFCRP
          MOVE      NEWPOS,FMRFPOS
          MOVE      NEWSEQ,FMRFSEQ
          CALL      UPFMRF1
          GOTO      MPRD9999
MPRD9999 
          CALL      DRHD0000
          CALL      RREP0000
          RETURN
.
+
.*********************************************************************
.         Subroutine to SELECT ITEM, POST, CANCEL, RUN REPORT
.----------------------------------------------------------------------
.
MQST0000  MOVE      ZERO,CCITEM
          MOVE      SP10,CCITEMD
          KEYIN     *P1:24,*EL,"Select Item, (",*V2LON,"P",*HOFF,")ost, (":
                    *V2LON,"C",*HOFF,")ancel, (":
                    *V2LON,"R",*HOFF,")emove from Report ? ":
                    *V2LON,*JR,CCITEMD;
.
          RESET     CCITEMD
          GOTO      MQST0000 IF EOS
.
          REP       UPPLOW,CCITEMD
.
          MATCH     "  C",CCITEMD        * test for cancel
          GOTO      MQST1000 IF EQUAL
.
          MATCH     "  P",CCITEMD        * test for post
          GOTO      MQST2000 IF EQUAL
.
          MATCH     "  R",CCITEMD        * test for Run report
          GOTO      MQST3000 IF EQUAL
.
          TYPE      CCITEMD              * test for numeric
          GOTO      MQST0500 IF NOT EQUAL
.
          MOVE      CCITEMD,CCITEM
          COMPARE   ONE,CCITEM
          GOTO      MQST0500 IF LESS
.
          RETURN
.
MQST0500  BEEP
          GOTO      MQST0000
.
MQST1000  MOVE      "-1",CCITEM
          RETURN
.
MQST2000  MOVE      "0",CCITEM
          RETURN
MQST3000  
          MOVE      "-2",CCITEM
          RETURN
+
.**********************************************************************
.*                 Redisplay routine for standard keyins              *
.**********************************************************************
.
RLMAFM00
          MOVE      FMLALEDG,LEDGER
          CALL      DHDS0000            * display header screen
          CALL      HDAT0000            * display header data

          CALL      DPRN0000
          CALL      PDAT0000             * display print data
          RETURN
RAMAFM00  
          MOVE      FMAMACCT,NACCOUNT
          MOVE      FMAMDESC,NACCDESC
          CALL      DHDS0000            * display header screen
          CALL      HDAT0000            * display header data

          CALL      DPRN0000
          CALL      PDAT0000             * display print data
          RETURN
RPRCFM00
          MOVE      FMPRCODE,PRNTCODE
          MOVE      FMPRDESC,PRNTDESC
          CALL      DHDS0000            * display header screen
          CALL      HDAT0000            * display header data

          CALL      DPRN0000
          CALL      PDAT0000             * display print data
          RETURN
.
RLMBFM00  
          MOVE      FMLBRPNO,REPNUMB
          MOVE      FMLBDESC,REPDESC
          GOTO      RDIS0000
RDIS0000
          CALL      DHDS0000            * display header screen
          CALL      HDAT0000            * display header data
          RETURN
.
.**********************************************************************
          
          INC       FMSSTDCD
          INC       FMSAMAIO/INC
          INC       FMSLMAIO/INC
          INC       FMSRSFIO/INC
          INC       FMSLMBIO/INC
          INC       FMSPRCIO/INC
          INC       FMSTCFIO/INC
          INC       FMSCONIO/INC
          INC       FMSAMAKY
          INC       FMSLMBKY
          INC       FMSLMAKY
          INC       FMSPRCKY
