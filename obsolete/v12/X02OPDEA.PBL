.*    Program      : X02OPDEA                                                *
.*    Description  : Conversion oprdetaf to new File layout                  *
.*                                                                           *
.*    Author       : Thanh T                                                 *
.*    Date         : 16/03/2022                                              *
.*    Modifications:                                                         *
.*                                                                           *
.*****************************************************************************
.
.  using vi Global change command      :%s/FromString/ToString/g
.
.  Global change progname  eg F57PTDOC
.  Global change logfname  eg f57ptdoc
.  Global change filefdnm  eg PATDOCFD (new layout)
.  Global change tabnamaf  eg patdoctf
.  Global change tablidx1  eg PATDOCT1
.  Global change keyxxin   eg KEY8
.  Global change keyxxout  eg KEY9
.  Global change newwrite  eg WRDOCT1
.  Global change savefile  eg sptdoctf
.
.  Global change X02OPDEA  eg F57PTDOC
.  Global change x02opdea  eg f57ptdoc
.  Global change OPRDEAFD  eg PATDOCFD (new layout)
.  Global change oprdetaf  eg patdoctf
.  Global change OPRDETA1  eg PATDOCT1
.  Global change KEY23     eg KEY8
.  Global change KEY23     eg KEY9
.  Global change WROPDEA1  eg WRDOCT1
.  Global change soprdetf  eg sptdoctf
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
LOGFILE   FILE      ASCII, FIXED=256
.
. Enter the first or the shortest index from the original FD and rename
. the index OLDFILE1
.
.                                     ******** copy the old FD here *******
.                                     * comment out all continuing fields and
.                                     * prefix changing fields with "OLD" -
.                                     * remove old Indexes and Redefines     
.
..OLDFILE1  IFILE     SQL, FIXED=xxx, KEY="Uxx-xx"
OLDFILE1  IFILE     SQL, FIXED=894, KEY="U1-8,9-13,14-19,20-20,21-23"
.
. NAME    TYPE   LENGTH  PHYSICAL START LOC.   DESCRIPTION
. ----    ----   ------  -------- ----------   -----------
.OPDADATE  DIM       8        8        1        Session Date (CCYYMMDD)
.OPDATIME  DIM       5        5        9        Start Time (HH:MM)
.OPDACLIN  DIM       6        6        14       Clinic/Surgeon
.DOPDASTA  DIM       1        1        20       Booking Status
.                                                0 = Booked
.                                                1 = Preadmitted
.                                                2 = Admitted
.                                                3 = Cancelled
.                                                4 = Discharged
.OPDACASE  DIM       3        3        21       Case Number
.OPDAADMN  DIM       8        8        24       Admission Number
.
.OPDAUNIQ  DIM       10       10       32       Unique id for theatre booking
.OPDAEXPS  DIM       5        5        42       Expected Start Time
.OPDAEXPD  FORM      4        3        47       Expected Duration (min)
.OPDAANAE  DIM       3        3        50       Anaesthetic Code (Cat. OA )
.OPDATYPE  DIM       3        3        53       Operation Type   (Cat. OY )
.OPDATRAN  DIM       3        3        56       Transport Code   (Cat. OR )
.OPDAUSR1  DIM       3        3        59       Unplan return to theatre (Cat Y6)
.OPDAUSR2  DIM       3        3        62                    2 (Category Y7)
.OPDAUSR3  DIM       3        3        65                    3 (Category Y8)
.OPDAUSR4  DIM       3        3        68                    4 (Category Y9)
.OPDAWAIT  DIM       1        1        71       Waiting Lists (Y/N)
.OPDAPROV  DIM       9        9        72       Provisional item # 1 ( MBS or
.                                              ICD9CM)
.OPDAPRV2  DIM       9        9        81       Provisional item # 2 ( MBS or
.                                              ICD9CM)
.OPDAPRV3  DIM       9        9        90       Provisional item # 3 ( MBS or
.                                              ICD9CM)
.OPDADES1  DIM       80       80       99       Operation description line 1
.OPDADES2  DIM       80       80       179                                 2
.OPDADES3  DIM       80       80       259                                 3
.OPDACOMM  DIM       40       40       339       Comments
.OPDACANC  DIM       3        3        379      Cancellation Code (Category SC)
.OPDATHET  DIM       6        6        382      Operating Room Code
.OPDAPRE1  DIM       5        5        388      Pre-operative time 1 (HH:MM)
.OPDAPRE2  DIM       5        5        393      Pre-operative time 2 (HH:MM)
.OPDAPRE3  DIM       5        5        398      Pre-operative time 3 (HH:MM)
.OPDAPRE4  DIM       5        5        403      Pre-operative time 4 (HH:MM)
.OPDAPRE5  DIM       5        5        408      Pre-operative time 5 (HH:MM)
.OPDAPOS1  DIM       5        5        413      Post-operative time 1 (HH:MM)
.OPDAPOS2  DIM       5        5        418      Post-operative time 2 (HH:MM)
.OPDAPOS3  DIM       5        5        423      Post-operative time 3 (HH:MM)
.OPDAPOS4  DIM       5        5        428      Post-operative time 4 (HH:MM)
.OPDATDUR  FORM      4        3        433      Actual duration in theatre (mins)
.OPDAKNOW  DIM       3        3        436      Known Infection (Category OK)
.OPDACDAT  DIM       8        8        439      Cancellation Date
.OPDAEQR1  DIM       3        3        447      Equipment Required 1(Category XE)
.OPDAEQR2  DIM       3        3        450      Equipment Required 2(Category XE)
.OPDALEQR  DIM       3        3        453      Loan Equip. Required(Category XO)
.OPDAEQR3  DIM       3        3        456      Equipment Required 3(Category XE)
.OPDAUNIT  DIM       3        3        459      Specialty/Unit (Category ST)
.OPDAURNO  DIM       8        8        462      UR Number
.OPDAPROC  DIM       9        9        470      WL Procedure Code(watproaf)
.OPDACONT  DIM       2        2        479      WL Procedure Count
.OPDAASRC  DIM       3        3        481      Admission Source (Cat S)
.OPDACSST  DIM       1        1        484      Consent Obtained (N=No, Y=Yes)
.OPDACSDT  DIM       8        8        485      Consect Changed Date (ccyymmdd)
.OPDACRDT  DIM       8        8        493      Date record created (ccyymmdd)
.OPDACTIM  DIM       8        8        501      Time record created (hh:mm:ss)
.OPDAWEBC  DIM       10       10       509      WEB User Id rec.creator(websecaf)
.OPDAUDAT  DIM       8        8        519      Last Update Date (ccyymmdd)
.OPDAUTIM  DIM       8        8        527      Last Update Time (hh:mm:ss)
.OPDAWEBU  DIM       10       10       535      WEB User Id rec.updater(websecaf)
.OPDAPCOM  DIM       1        1        545      Prosthetic Items Complete
.                                              0=No 1=Yes
.OPDAVUN1  DIM       10       10       546      Verification User 1 (websecaf)
.OPDAVUN2  DIM       10       10       556      Verification User 2 (websecaf)
.OPDAPHMN  DIM       10       10       566      Prosthetics Manager (websecaf)
.OPDAREQN  DIM       10       10       576      Unique Request ID
.OPDADES4  DIM       80       80       586      Operation Description line 4
.OPDADES5  DIM       80       80       666      Operation Description line 5
.OPDADES6  DIM       80       80       746      Operation Description line 6
.OPDAFAST  DIM       8        8        826      Fasting Time (hh:mm:ss)
.OPDAAPRC  DIM       1        1        834      Adm Paperwork received (1=yes)
.OPDAACPD  DIM       1        1        835      All charges posted (1=yes)
.OPDARICM  DIM       3        3        836      Reason Incomplete (Category Rt)
.OPDAMHRC  DIM       1        1        839      Medical History Received
.                                               0 or Blank = No
.                                               1 = Yes
.OPDAPOWD  DIM       3        3        840      Expected Post-op Ward (patwr1af)
.OPDAPOBD  DIM       3        3        843      Expected Post-op Bed (patwr1af)
.OPDACTYP  DIM       3        3        846      Claim Type (Cat CL)
.OPDAATYP  DIM       3        3        849      Admission Type (Cat P)
.OPDADTFF  DIM       8        8        852      Date of Fasting Food (ccyymmdd)
.OPDADTFL  DIM       8        8        860      Date of Fasting Fluid (ccyymmdd)
.OPDASPAR  DIM       26       26       868      Spare
.
.End of record                        894
.
.                                     ******** copy the new FD here *******
.
OPRDETA1  IFILE     SQL, FIXED=945, KEY="U1-8,9-13,14-19,20-20,21-23"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
OPDADATE  DIM       8        8        1        Session Date (CCYYMMDD)
OPDATIME  DIM       5        5        9        Start Time (HH:MM)
OPDACLIN  DIM       6        6        14       Clinic/Surgeon
DOPDASTA  DIM       1        1        20       Booking Status
.                                                0 = Booked
.                                                1 = Preadmitted
.                                                2 = Admitted
.                                                3 = Cancelled
.                                                4 = Discharged
OPDACASE  DIM       3        3        21       Case Number
OPDAADMN  DIM       8        8        24       Admission Number
.
OPDAUNIQ  DIM       10       10       32       Unique id for theatre booking
OPDAEXPS  DIM       5        5        42       Expected Start Time
OPDAEXPD  FORM      4        3        47       Expected Duration (min)
OPDAANAE  DIM       3        3        50       Anaesthetic Code (Cat. OA )
OPDATYPE  DIM       3        3        53       Operation Type   (Cat. OY )
OPDATRAN  DIM       3        3        56       Transport Code   (Cat. OR )
OPDAUSR1  DIM       3        3        59       Unplan return to theatre (Cat Y6)
OPDAUSR2  DIM       3        3        62                    2 (Category Y7)
OPDAUSR3  DIM       3        3        65                    3 (Category Y8)
OPDAUSR4  DIM       3        3        68                    4 (Category Y9)
OPDAWAIT  DIM       1        1        71       Waiting Lists (Y/N)
OPDAPROV  DIM       9        9        72       Provisional item # 1 ( MBS or
.                                              ICD9CM)
OPDAPRV2  DIM       9        9        81       Provisional item # 2 ( MBS or
.                                              ICD9CM)
OPDAPRV3  DIM       9        9        90       Provisional item # 3 ( MBS or
.                                              ICD9CM)
OPDADES1  DIM       80       80       99       Operation description line 1
OPDADES2  DIM       80       80       179                                 2
OPDADES3  DIM       80       80       259                                 3
OPDACOMM  DIM       40       40       339       Comments
OPDACANC  DIM       3        3        379      Cancellation Code (Category SC)
OPDATHET  DIM       6        6        382      Operating Room Code
OPDAPRE1  DIM       5        5        388      Pre-operative time 1 (HH:MM)
OPDAPRE2  DIM       5        5        393      Pre-operative time 2 (HH:MM)
OPDAPRE3  DIM       5        5        398      Pre-operative time 3 (HH:MM)
OPDAPRE4  DIM       5        5        403      Pre-operative time 4 (HH:MM)
OPDAPRE5  DIM       5        5        408      Pre-operative time 5 (HH:MM)
OPDAPOS1  DIM       5        5        413      Post-operative time 1 (HH:MM)
OPDAPOS2  DIM       5        5        418      Post-operative time 2 (HH:MM)
OPDAPOS3  DIM       5        5        423      Post-operative time 3 (HH:MM)
OPDAPOS4  DIM       5        5        428      Post-operative time 4 (HH:MM)
OPDATDUR  FORM      4        3        433      Actual duration in theatre (mins)
OPDAKNOW  DIM       3        3        436      Known Infection (Category OK)
OPDACDAT  DIM       8        8        439      Cancellation Date
OPDAEQR1  DIM       3        3        447      Equipment Required 1(Category XE)
OPDAEQR2  DIM       3        3        450      Equipment Required 2(Category XE)
OPDALEQR  DIM       3        3        453      Loan Equip. Required(Category XO)
OPDAEQR3  DIM       3        3        456      Equipment Required 3(Category XE)
OPDAUNIT  DIM       3        3        459      Specialty/Unit (Category ST)
OPDAURNO  DIM       8        8        462      UR Number
OPDAPROC  DIM       9        9        470      WL Procedure Code(watproaf)
OPDACONT  DIM       2        2        479      WL Procedure Count
OPDAASRC  DIM       3        3        481      Admission Source (Cat S)
OPDACSST  DIM       1        1        484      Consent Obtained (N=No, Y=Yes)
OPDACSDT  DIM       8        8        485      Consect Changed Date (ccyymmdd)
OPDACRDT  DIM       8        8        493      Date record created (ccyymmdd)
OPDACTIM  DIM       8        8        501      Time record created (hh:mm:ss)
OPDAWEBC  DIM       10       10       509      WEB User Id rec.creator(websecaf)
OPDAUDAT  DIM       8        8        519      Last Update Date (ccyymmdd)
OPDAUTIM  DIM       8        8        527      Last Update Time (hh:mm:ss)
OPDAWEBU  DIM       10       10       535      WEB User Id rec.updater(websecaf)
OPDAPCOM  DIM       1        1        545      Prosthetic Items Complete
.                                              0=No 1=Yes
OPDAVUN1  DIM       10       10       546      Verification User 1 (websecaf)
OPDAVUN2  DIM       10       10       556      Verification User 2 (websecaf)
OPDAPHMN  DIM       10       10       566      Prosthetics Manager (websecaf)
OPDAREQN  DIM       10       10       576      Unique Request ID
OPDADES4  DIM       80       80       586      Operation Description line 4
OPDADES5  DIM       80       80       666      Operation Description line 5
OPDADES6  DIM       80       80       746      Operation Description line 6
OPDAFAST  DIM       8        8        826      Fasting Time (hh:mm:ss)
OPDAAPRC  DIM       1        1        834      Adm Paperwork received (1=yes)
OPDAACPD  DIM       1        1        835      All charges posted (1=yes)
OPDARICM  DIM       3        3        836      Reason Incomplete (Category Rt)
OPDAMHRC  DIM       1        1        839      Medical History Received
.                                               0 or Blank = No
.                                               1 = Yes
OPDAPOWD  DIM       3        3        840      Expected Post-op Ward (patwr1af)
OPDAPOBD  DIM       3        3        843      Expected Post-op Bed (patwr1af)
OPDACTYP  DIM       3        3        846      Claim Type (Cat CL)
OPDAATYP  DIM       3        3        849      Admission Type (Cat P)
OPDADTFF  DIM       8        8        852      Date of Fasting Food (ccyymmdd)
OPDADTFL  DIM       8        8        860      Date of Fasting Fluid (ccyymmdd)
OPDAPRV4  DIM       9        9        868      Provisional item # 4
OPDAPRV5  DIM       9        9        877      Provisional item # 5
OPDAPRV6  DIM       9        9        886      Provisional item # 6
OPDASPAR  DIM       50       50       895      Spare
.
.End of record                        945
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       200
.
DEFPATH   DIM       60
DIM60     DIM       60
DISNUM    FORM      4
.
ERORTEXT  DIM       80
.
INPFILE   DIM       8
.
MINUS3    FORM      "-3"
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
SAVEFLG   FORM      1
.
USINGORC  DIM       1
.
.
. PROGRAM CONSTANTS
. -----------------
CREATEXT  INIT    "Log file created "
DASHLINE  INIT    "--------------------------------------------------":
                  "------------------------------"
ENDGTEXT  INIT    "Ending   fixit (Option "
OPENTEXT  INIT    "Log file opened "
SP60      INIT    "                                                            "
STARTTXT  INIT    "Starting fixit (Option "
.
.
.
PRGNAM    INIT      "CONVERSION OPRDEAFD"
PRGID     INIT      "X02OPDEA"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
.
          CALL      OLOG0000                      * create/open log file
MAIN0500  CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000              * run c-isam fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9000
.
.         Running c-isam fix
.
MAIN6000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9000
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/zip saved file
.
MAIN9000  CALL      CLOG0000                      * close log file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
          
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run c-isam fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
.
PREP0000  CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
.
          CALL      SLOG0000                      * write start to log file
.
          MOVE      "oprdetaf",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat oprdetaf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "soprdetf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat oprdetaf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "soprdetf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "oprdetaf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Unable to remove the original file.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "soprdetf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            MOVE      "Unable to open saved original file.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "oprdetaf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.      eg    APPEND    " 195 UC9-16",CMDLINE
.
          APPEND    " 945 UC1-8,9-13,14-19,20-20,21-23",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has 
.  to be repeated with for each individual index
.
. Index 2
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC24-31,20-20,1-8,9-13,14-19,21-23",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
. Index 3
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC32-41",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
. Index 4
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC462-469,470-478,479-480,32-41",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
. Index 5
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC1-8,9-13,14-19,21-23,20-20",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.
          OPEN      OPRDETA1,NEWFILE
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          GOTO      PREP9999
.
PREP5000  MOVE      "Old file not found.  ",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  MOVE      "Unable to copy original file.  (Error: ",ERORTEXT
          ENDSET    ERORTEXT
          APPEND    TASKID,ERORTEXT
          APPEND    ")  ",ERORTEXT
          RESET     ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
            MOVE      "File does not exist",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
              MOVE      "File has already been converted",ERORTEXT
              WRITE     LOGFILE,SEQ;ERORTEXT
            ELSE
              DISPLAY   "has caused an IO error - ";
              MOVE      "File has caused an IO Error",ERORTEXT
              WRITE     LOGFILE,SEQ;ERORTEXT
            ENDIF
          ENDIF
          MOVE      "File will be bypassed",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *+,ERORTEXT
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY23,SP70
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
.  All necessary changes to the files field lengths, date changes
.  or any other sort of modification must be made here
.
          PACK      OPDAPRV4,SP70                 * Provisional item # 4
          PACK      OPDAPRV5,SP70                 * Provisional item # 5
          PACK      OPDAPRV6,SP70                 * Provisional item # 6
.
          CALL      WROPDEA1                      * write to new file
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     OPRDETA1                      * close new file
          CLOSE     OLDFILE1                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS:
                    *P1:24;
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
.
VALD0000  MOVE      ZERO,EXIT
.
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            MOVE      "Directory path must end with a slash (/) ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Directory does not exist ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or zip save file                                     *
.**********************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "soprdetf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "gzip -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Saved file not zipped or removed.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:24,*EL,*B,"Finished processing  ",CDATE,SP1,CTIMEIS:
                    ".  ";
          CALL      HITENTER
.
          CALL      ELOG0000                     * write an end log entry
.
ENDP9999  RETURN
+
.**********************************************************************
.*        DEFT0000 -  Get the default directory                       *
.**********************************************************************
.
DEFT0000  EXECUTE   "ldat oprdetaf.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  MOVE      "Error finding 'oprdetaf'.  ",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.*                            OLOG0000                                       *
.*                    Open the log file                                      *
.*****************************************************************************
.
OLOG0000  CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      LOGFILE,"x02opdea.log"       * open log file
          TRAPCLR   IO
          IF        OVRCD = 1
            PREP      LOGFILE,"x02opdea.log"     * create and open log file
            WRITE     LOGFILE,MINUS3;DASHLINE
            WRITE     LOGFILE,SEQ;CREATEXT,CDATE,SP1,CTIMEIS
          ELSE
            WRITE     LOGFILE,MINUS3;DASHLINE
            WRITE     LOGFILE,SEQ;OPENTEXT,CDATE,SP1,CTIMEIS
          ENDIF
.
OLOG9999  RETURN
+
.*****************************************************************************
.*                            CLOG0000                                       *
.*                    Close the log file                                     *
.*****************************************************************************
.
CLOG0000  CLOSE     LOGFILE
.
CLOG9999  RETURN
+
.*****************************************************************************
.*                            SLOG0000                                       *
.*                    Write a start log to the log file                      *
.*****************************************************************************
.
SLOG0000  WRITE     LOGFILE,SEQ;DASHLINE
          WRITE     LOGFILE,SEQ;STARTTXT,COPTION,RBRACK,SP1,CDATE,SP1:
                                   CTIMEIS
.
SLOG9999  RETURN
+
.*****************************************************************************
.*                            ELOG0000                                       *
.*                    Write an end log to the log file                       *
.*****************************************************************************
.
ELOG0000  WRITE     LOGFILE,SEQ;ENDGTEXT,COPTION,RBRACK,SP1,CDATE,SP1:
                                   CTIMEIS
ELOG9999  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
.                  ******************************    OLD IO ROUTINES
.                  Copy the Positional Read (RS or RDS) from original I/O file,
.                  - change Label to READSOLD and the index name to OLDFILE1
.
READSOLD  MOVE      ZERO,OVRCD
          READ      OLDFILE1,KEY23;;
          RETURN
.
.                  Copy the Read Next (RK or RDK) from the original I/O file
.                  - change Label to READKOLD and the index name to OLDFILE1
.                  - change any fields that are changing to a prefix of OLD,
.                    eg. PTEDSPAR becomes OLDDSPAR
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;OPDADATE,OPDATIME,OPDACLIN,DOPDASTA,OPDACASE:
                                   OPDAADMN,OPDAUNIQ,OPDAEXPS,OPDAEXPD:
                                   OPDAANAE,OPDATYPE,OPDATRAN,OPDAUSR1:
                                   OPDAUSR2,OPDAUSR3,OPDAUSR4,OPDAWAIT:
                                   OPDAPROV,OPDAPRV2,OPDAPRV3,OPDADES1:
                                   OPDADES2,OPDADES3,OPDACOMM,OPDACANC:
                                   OPDATHET,OPDAPRE1,OPDAPRE2,OPDAPRE3:
                                   OPDAPRE4,OPDAPRE5,OPDAPOS1,OPDAPOS2:
                                   OPDAPOS3,OPDAPOS4,OPDATDUR,OPDAKNOW:
                                   OPDACDAT,OPDAEQR1,OPDAEQR2,OPDALEQR:
                                   OPDAEQR3,OPDAUNIT,OPDAURNO,OPDAPROC:
                                   OPDACONT,OPDAASRC,OPDACSST,OPDACSDT:
                                   OPDACRDT,OPDACTIM,OPDAWEBC,OPDAUDAT:
                                   OPDAUTIM,OPDAWEBU,OPDAPCOM,OPDAVUN1:
                                   OPDAVUN2,OPDAPHMN,OPDAREQN,OPDADES4:
                                   OPDADES5,OPDADES6,OPDAFAST,OPDAAPRC:
                                   OPDAACPD,OPDARICM,OPDAMHRC,OPDAPOWD:
                                   OPDAPOBD,OPDACTYP,OPDAATYP,OPDADTFF:
                                   OPDADTFL,OPDASPAR
.
          GOTO      OVERCOND IF OVER
          RETURN
.
.                  ******************************    NEW IO ROUTINES
.                  Copy the Write statement from the new I/O file           
.                  (no changes are needed to this routine)               
.
WROPDEA1  RESET     KEY23
.
          WRITE     OPRDETA1,KEY23;OPDADATE,OPDATIME,OPDACLIN,DOPDASTA,OPDACASE:
                                   OPDAADMN,OPDAUNIQ,OPDAEXPS,OPDAEXPD:
                                   OPDAANAE,OPDATYPE,OPDATRAN,OPDAUSR1:
                                   OPDAUSR2,OPDAUSR3,OPDAUSR4,OPDAWAIT:
                                   OPDAPROV,OPDAPRV2,OPDAPRV3,OPDADES1:
                                   OPDADES2,OPDADES3,OPDACOMM,OPDACANC:
                                   OPDATHET,OPDAPRE1,OPDAPRE2,OPDAPRE3:
                                   OPDAPRE4,OPDAPRE5,OPDAPOS1,OPDAPOS2:
                                   OPDAPOS3,OPDAPOS4,OPDATDUR,OPDAKNOW:
                                   OPDACDAT,OPDAEQR1,OPDAEQR2,OPDALEQR:
                                   OPDAEQR3,OPDAUNIT,OPDAURNO,OPDAPROC:
                                   OPDACONT,OPDAASRC,OPDACSST,OPDACSDT:
                                   OPDACRDT,OPDACTIM,OPDAWEBC,OPDAUDAT:
                                   OPDAUTIM,OPDAWEBU,OPDAPCOM,OPDAVUN1:
                                   OPDAVUN2,OPDAPHMN,OPDAREQN,OPDADES4:
                                   OPDADES5,OPDADES6,OPDAFAST,OPDAAPRC:
                                   OPDAACPD,OPDARICM,OPDAMHRC,OPDAPOWD:
                                   OPDAPOBD,OPDACTYP,OPDAATYP,OPDADTFF:
                                   OPDADTFL,OPDAPRV4,OPDAPRV5,OPDAPRV6,OPDASPAR
.
         RETURN
.
          INC       STD001IO

