.******************************************************************************
.                                 OPRSSTDS
.                     Displays all session start times 
.
. Description : Display Operating room session status and start time for a given
.               clinic and date.  This routine is BULLSHIT FREE in otherwords
.               NO dead code or poor programming.
.
. Requirements: This routine requires OPRSESFD and OPRSESIO.INC and KEY19
.               CLINIC, SESNDATE
.
.             OPRSSTDS was programed by Bill Dew on the 06/03/1990
.                        I.B.A. Development Department
.                     D. Jones and G. Williams supervisor 
.
.   Modifications : 
.    V11.03.01 06/03/2023  Ebon Clements     TSK 0909393
.              Added hospital to oprsesaf(1-6) and oprdetfa(1&5) indexes
.
.******************************************************************************
OPRSSTDS 
.
          DISPLAY    *P1:4,*EF,*P28:4,*V2LON,*ULON,"SESSION MASTER RECORDS"
          DISPLAY    *ULON,*P1:6,"Operating Room                        ":
                     *P41:6,"Session status",*P57:6,"start time"
          MOVE       SEVEN,CVERT
          MOVE       ONE,CCOL
.
          PACK       KEY22,SESNHOSP,SESNDATE,SP20
          CALL       RSOPSES1
SSTDS222  
          CALL       RKOPSES1
          BRANCH     OVRCD,SSTDS990
.
          MATCH      OPSEDATE,SESNDATE
          GOTO       SSTDS990 IF NOT EQUAL
          MATCH      OPSECLIN,CLINIC
          GOTO       SSTDS222 IF NOT EQUAL
.
          MOVE       "Unknown",OPRMDESC     * get operating room description
          PACK       KEY6,OPSETHET,SP6
          CALL       RDOPOPR1
.
          BRANCH    OPSESTAT,SSTDS233,SSTDS244   * set session status
          MOVE      "Cancelled",KEY10
          GOTO      SSTDS333
SSTDS233  MOVE      "Booked",KEY10
          GOTO      SSTDS333
SSTDS244  MOVE      "Extra",KEY10
SSTDS333
          DISPLAY    *PCCOL:CVERT,*V2LON,OPRMDESC,SP2,*HOFF,KEY10,SP6,OPSETIME
          ADD        ONE,CVERT
          COMPARE    TWENTY3,CVERT
          GOTO       SSTDS222 IF LESS
.
.         New standards question
.
SSTDS200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, ":
                    "(",*V2LON,"F",*HOFF,")irst Screen, ":
                    "(",*V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
          CMATCH    ANSC,ANS
          GOTO      SSTDS999 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      OPRSSTDS IF EQUAL
.
.         Clear the screen
.
SSTDS400  MOVE      SEVEN,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:CVERT,*EF
          GOTO      SSTDS222
.
SSTDS990  
SSTDS999  RETURN
