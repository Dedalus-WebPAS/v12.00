.******************************************************************************
.* System         : CCI                                                       *
.* Program        : IBACCI79                                                  *
.* Desc           : Daily Extracts                                            *
.*                  - same us IBACCI69 but dates are keyed in                 *
.*                    -ie. it is NOT automatic                                *
.******************************************************************************
.* Date           : 08/04/92                                                  *
.* Author         : Peter Eddey                                               *
.* Mods     :                                                                 *
.******************************************************************************
.*        V11.03.01 20.03.2023  DA Sarkies    TSK 0909393                     *
.*                  Increased size of case keys                               *
.*        V11.02.01 09/02/2022  Thanh T       TSK 0905641                     *
.*                  Recompiled as OUTMA1FD changes                            *
.*        V11.00.01 20/03/2020  J.Tan          TSK 0838262                    *
.*                  Added PATITMRD                                            *
.*        V10.05.01 23/01/2015  Ania P         CAR 261630                     *
.*                  Removed use of PORT for temp file naming                  *
.*        V10.03.01 18/10/2012  Jill Parkinson CAR 273375                     *
.*                  Recompiled for DATECONV - first day of year calculation   *
.*        V10.01.01 23/02/2011  Peter Vela     CAR 233034                     *
.*                  Removed PATBF2FD and PATBF2IO                             *
.*        V9.12.01  21/09/2009  J.Tan          CAR 204390                     *
.*                  Recompiled for STEPDOWN - mods to old bed fees file       *
.*        V9.08.01  02/02/2007  Ebon Clements  CAR 120001                     *
.*                  Added nz private billing files                            *
.*        V9.04.01  06/10/2004  Davin  CAR 53798                              *
.*                  Use pthsdclm instead of ptcndclm for multihospital        *
.*        V9.03.01  30/10/2003  J.Tan  CAR 44172                              *
.*                  Mods for pmsmtiaf - Misc.Theatre item file                *
.*        V9.02.00  01/05/2003  Tonii  CAR 38344                              *
.*                  Ported over from 5.10                                     *
.******************************************************************************
.*        V5.10.05  21/09/2002  Tonii  Casemix mods                           *
.*                  Recompiled for STEPDOWN                                   *
.*        V5.10.04  27/08/2002  Peter Vela   SRF 19540                        *
.*                  Recompiled for STEPDOWN, PATDINVS                         *
.*        V5.10.03  21/08/2002  Guomin Fu    SRF 17469                        *
.*                  Recompiled for PATDINVS                                   *
.*        V5.10.02  14/08/2002  Mike Laming  SRF 17570                        *
.*                  Recompiled for Casemix port from V6.10 to V5.10           *
.*        V5.10.01  28/06/2002  Mike Laming  SRF 16830                        *
.*                  Recompiled for PATDINVS                                   *
.*        V5.09.01  22/09/2001  Tonii SRF 13075                               *
.*                  Recompiled for PATDINVS                                   *
.*        V5.08.01  16/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.07.01  16/07/1999  Steve Downing                                 *
.*                  Recompiled for changes to CCIXTRAC
.*        V5.07.B03 13/01/1999  Jim Stathopoulos                              *
.*                  Fixed Global Compile Errors                               *
.*        V5.07.B02 07/12/1998  Steve Armstrong    SRF 644142                 *
.*                  Recompiled for CCIXTRAC - added new implant items 6 - 10  *
.*        V5.07.B01 26/10/1998  Jim Stathopoulos                              *
.*                  507 Changes                                               *
.******************************************************************************
.*        V5.05.01  02/10/1997  Steve Armstrong    SRF 643603                 *
.*                  Mods to set fiscal year and period                        *
.******************************************************************************
.*        V5.04.05  03/08/1997  Steve Armstrong      WHL Mods                 *
.*                  Recompiled for CCIXTRAC                                   *
.*        V5.04.04  23/05/1997  Steve Armstrong      WHL Mods                 *
.*                  Recompile & mods for file length change in cciutlaf.      *
.*                  (Ordering Doctor).                                        *
.*        V5.04.03  09/12/1996  Steve Armstrong                               *
.*                  Recompiled for mods to CCIXTRAC                           *
.*        V5.04.02  23/09/1996  Steve Armstrong    SRF 642100                 *
.*                  Fixed bug in calculation of transfer hours where patient  *
.*                  is transferred out of admission ward, then back in.  Hours*
.*                  not accumulating.                                         *
.*        V5.04.01  24.06.1996  B.G.Ackland                                   *
.*                  Changed CCUTSPAR to CCUTHPRE for Multi Database at CHC    *
.******************************************************************************
.*        V5.03.08  28/02/1995  Matt        Quote 8587                        *
.*                  added procedures 37 and 38.                               *
.*        V5.03.07  18/12/1995  Whirlpool   CHC Mods                          *
.*                  added procedures 33,34,35,36 for district nursing and dep *
.*        V5.03.06  22/08/1995  B.G.Ackland                                   *
.*                  Change to CCIXTRAC to ensure IBA sent flag set to Zero.   *
.*                  Problem with transactions not being transferred to IBA CC *
.*        V5.03.05  01/08/1995  Greg Horvat      SRF 610897                   *
.*                  Recompiled for PATDINVS                                   *
.*        V5.03.04  28/07/1995    Justin Coates                               *
.*                  add the error report to Outpatient Extra Screen extract   *
.*        V5.03.03  21/07/1995    ROD                SRF 640537               *
.*                  fix I*C (outma1a1)  and fixed I*I while i was there       *
.*        V5.03.02  19/07/1995    Justin Coates                               *
.*                  added the set up of CCUTORIG                              *
.*        V5.03.01  17/07/1995    Justin Coates  (Quote 8436)                 *
.*                  Added var. 7 for procedure types 6 and 28                 *
.******************************************************************************
.*        V5.02.03  09/05/1995    Justin Coates                               *
.*                  re-compile for CCIXTRAC (remove debug)                    *
.*        V5.02.02  28/04/1995    Justin Coates  (Quote 8372)                 *
.*                  re-compile for CCIXTRAC (time calc'n for proc type 32)    *
.*        V5.02.01  24/04/1995    Justin Coates  (Quote 8372)                 *
.*                  added variables for types 4,5,9                           *
.*                  added procedure type 32 - same as type 9                  *
.******************************************************************************
.*        V5.01.01  14/04/92  Graeme Williams                                 *
.*                  Altered the quantity types                                *
.*        V5.01.02  30/4/92   Peter Eddey                                     *
.*                  re-compile for change to extract include                  *
.*        V5.01.03  17/08/92  Peter Eddey                                     *
.*                  Recompiled for changes to CCIUTL                          *
.*        V5.01.04  21/08/92  Graeme Williams           SRF 116849            *
.*                  Fixed age groups in procedure extracts                    *
.*        V5.01.05  02/10/1992 Peter Eddey                                    *
.*                  New procedures etc for Epworth                            *
.*        V5.01.06  05/11/1992 Peter Eddey                                    *
.*                  Changed existing dietary extract                          *
.*                  19/03/1993  Steve Armstrong                               *
.*                  Recompiled for STEPDOWN (new episode field)               *
.*        V5.01.07  25/06/1993  Glenn Berry                                   *
.*                  Added extra routine to do Procedure Type 25               *
.*                  (O/P Extra Screen)                                        *
.*        V5.01.08  06/09/93    DIG                    SRF : 125019           *
.*                  Re-Compiled for change to STEPDOWN                        *
.*        V5.01.09  02/03/1994    Justin Coats   (Hawkes Bay mods)            *
.*                  added procedure types 26 and 27                           *
.*        V5.01.10  06/05/1994    Justin Coates  (Hawkes Bay mods)            *
.*                  added procedure types 28 - same as type 6                 *
.******************************************************************************
.
.$$$$$
.  Daily Extracts (IBACCI79)
.  -------------------------
.
.  - Initialisation
.       - Clock Date
.       - Open Files
.           PATTRAN1
.           PATTRAN2
.           PATDSCH1
.           PATDSCH2
.           PATMI1A1
.           PATMI1A2
.           RUSUTLL1
.           AAEDI1A1
.           AAEDI1A2
.           AAEDE1A1
.           CONTROLF
.
.  -  set variables
.       - convert todays date to julian form, subtract one from the day, and
.         convert back to ddmmyy format to create the report date
.       - using this date, generate Fiscal period
.
.  - extract A & E details
.       - read through AAEDI1A2 using report date. For each record found
.         read AAEDE1A1 and write relevant details to RUSUTLL1
.
.  - extract Inpatient details
.       - extract current admission details
.           - read through PATMI1A2 for all current admissions
.           - check report date is >= admission date
.           - If O.K. see if patient was on leave
.                 - Position on last record on PATTRAN2 for given admisison
.                   number and work out if patient was on leave
.           - If not on leave write relevant details to RUSUTLL1
.
.       - extract current discharged details
.           - read through PATDSCH1 in report date order
.           - read PATMI1A1
.           - check report date is >= admission date
.           - If O.K. see if patient was on leave
.                 - Position on last record on PATTRAN2 for given admisison
.                   number and work out if patient was on leave
.           - If not on leave see if admission date = discharge date
.           - If it does then patient is a day case, if not then an inpatient
.           - Write relevant details to RUSUTLL1
.
.       - extract current on-leave details
.           - read through PATMI1A2 for all current on-leave patients
.           - check report date is >= admission date
.           - If O.K. see if patient was on leave
.                 - Position on last record on PATTRAN2 for given admisison
.                   number and work out if patient was on leave
.           - If not on leave write relevant details to RUSUTLL1
.
.  - END
.
.$$$$$
          INC       STD001FD
          INC       AAEDE1FD/INC
          INC       AAEDI1FD/INC
.         INC       CATCOMFD/INC
          INC       CCICONFD/INC
          INC       CCIDPTFD/INC
          INC       CCIGDPFD/INC
          INC       CCIGPRFD/INC
          INC       CCIPARFD/INC
          INC       CCIUTLFD/INC
          INC       CCIXGDFD/INC
          INC       CCIXGPFD/INC
          INC       CCIXPAFD/INC
          INC       DEPCATFD/INC
          INC       DEPCPHFD/INC
          INC       NZPBFEFD/INC
          INC       OPRDEAFD/INC            * op details file
          INC       OPRDEBFD/INC            * op team file
          INC       OPRDECFD/INC            * op usage file
          INC       OPREXPFD/INC            * expensive item
          INC       OPRSESFD/INC            * session file
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       OUTCANFD/INC
          INC       OUTCTYFD/INC
          INC       OUTMA1FD/INC
          INC       OUTSIPFD/INC
          INC       OUTSITFD/INC
          INC       OUTXSCFD/INC
          INC       PATBFEFD/INC
          INC       PATCALAG/INC            * vars for calcage
          INC       PATCFAFD/INC
          INC       PATCODFD/INC            * codes file
          INC       PATCOMM/INC
          INC       PATCONFD/INC
          INC       PATDHEAD/INC
          INC       PATDINVS/INC
          INC       PATDRGFD/INC
          INC       PATDSCFD/INC
          INC       PATDTRFD/INC
          INC       PATFN1FD/INC
          INC       PATFX1FD/INC
          INC       PATITMFD/INC
          INC       PATMA1FD/INC            * master file
          INC       PATMI1FD/INC
          INC       PATRCAUD/INC
          INC       PATTRNFD/INC
          INC       PATVISFD/INC
          INC       PATWR1FD/INC
          INC       PMSCIDFD/INC
          INC       PMSMTIFD/INC            * Miscellaneous theatre item file
          INC       PATHSPFD/INC
          INC       PMSVX1FD/INC
          INC       WEBSECFD/INC
          INC       IBASEQFD/INC  
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.
.         temp file for failed records error report
.
TEMPF     IFILE     SQL, FIXED=41, KEY="D1-3,4-4,5-13,14-22,23-31,32-40"
KEYF      INIT      " 41 D1-3,4-4,5-13,14-22,23-31,32-40"
.Name     Type      Length  Phys.  Start  Description
.-------  ----      ------  -----  -----  --------------------------------
XFPTYP    DIM       3           3      1  Procedure type
XFETYP    DIM       1           1      4  Error type
.                                           1 = no department translation
.                                           2 = no procedure  translation
.                                           3 = no quantity
XFCOD1    DIM       9           9      5  Code 1
XFCOD2    DIM       9           9     14  Code 2
XFCOD3    DIM       9           9     23  Code 3
XFCOD4    DIM       9           9     32  Code 4
.end of record                        41
.
.         SRF 642100 - Changed temp file to same as cciutlaf file so
.         it can be used to write all processed records prior to updating
.         the cciutlaf file.  This means that transfer record quantities
.         can be added and then written to the cciutlaf file.  The temp file
.         can also still be used for success reporting.
.
TEMPS     IFILE     SQL, FIXED=146, KEY="U1-8,9-11,12-19,20-26,27-34,81-88"
KEYS      INIT      " 146 U1-8,9-11,12-19,20-26,27-34,81-88"
.
.Name     Type      Length  Phys.  Start  Description
.-------  ----      ------  -----  -----  ----------------------------------
XSDATE    DIM       8          8       1  Date
XSDEPT    DIM       3          3       9  Department
XSEVNT    DIM       8          8      12  Event Number
XSPROC    DIM       7          7      20  Procedure
XSURNO    DIM       8          8      27  UR Number
XSQUNT    FORM      8          5      35  Quantity
XSPTYP    DIM       4          4      40  Patient Type
XSFSYR    DIM       4          4      44  Fiscal Year
XSFSPR    DIM       2          2      48  Fiscal Period
XSCOMM    DIM      30         30      50  Comments
XSSENT    DIM       1          1      80  Sent yet
.                                          blank = yes
.                                          "N" = no
DXSBATN   DIM       8          8      81  Batch Number
XSCHRG    FORM      8.2        6      89  Patient charge
XSORIG    DIM       3          3      95  Record Origin
.                                          DET = Detente Interface
.                                          PC  = PC      Interface
.                                          MAN = Manual Data Entry
.                                          OUT = Outpatient Manual Entry
.                                          nnn = Auto Ext. Proc. Type
XSHPRE    DIM       1          1      98  Hospital Episode Prefix Char.
XSSIBA    DIM       1          1      99  Sent IBA Clinical Costing 1=Yes
XSORDD    DIM       6          6     100  Ordering Doctor
XSSPAR    DIM      40         40     106  Spare Variable
.
.End of Record                       146
.
XSBATN    FORM      8
.
.**********************************************************************
.*                             CONSTANTS                              *
.**********************************************************************
CATCI     INIT      "CI"
CATCV     INIT      "CV"
CODECL    INIT      "CL"
DEFPRID   INIT      "IBAOUT66"
ISERASE   INIT      "iserase "
PRFIXF    INIT      "CCI79F"
PRFIXS    INIT      "CCI79S"
SP14      INIT      "              "
Z20       INIT      "ZZZZZZZZZZZZZZZZZZZZ"
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
ADMISDTE  DIM       8
AGEFIELD  DIM       9
BAND      DIM       3
CALDAYS   FORM      4 
CAT       DIM       2
CFNAMEF   DIM       8
CFNAMES   DIM       8
CHARGE    FORM      6.2
CMDLINE   DIM       80
CNTFAIL   FORM      8
CNTSUCC   FORM      8
CODE      DIM       2
COUNTER   FORM      2
CURCENT   FORM      2
CURSESS   DIM       22        * current theatre session
DATEDIM6  DIM       6
DEPTCODE  DIM       3
DIM18     DIM       18
DIM18A    DIM       18
DIM19A    DIM       19
DIM26     DIM       26
DIM2A     DIM       2
DIM4      DIM       4
DISDATE   DIM       8
DUMMY     DIM       1
DURATION  FORM      4         * calculated duration of operation
EFTDATE   DIM       8         * Effective date (ccyymmdd)
ERDEPPRO  FORM      1         * type of error for extract
FCC       FORM      2
FDD       FORM      2
FLAGONLV  FORM      1
FMM       FORM      2
FNAMEM    DIM       8
FNAMER    DIM       8
FORM1A    FORM      1
FORM3A    FORM      3
FROMHOUR  FORM      2
FROMMIN   FORM      2
FROMNUMB  FORM      1
FROMTIME  DIM       5
FROMTYPE  DIM       1
FYY       FORM      2
IBACODE   DIM       6
IBCNMHOS  FORM      1        * Multihospital indicator
IMPLCODE  DIM       6
INPTYPE   FORM      1
JYEAR     FORM      2
LSTDATE   DIM       8
MBSCODE   DIM       9
MNUOPT    FORM      1
ONLVDATE  DIM       8
OPNFLAG   FORM      1
ORDDOCT   DIM       6
PROCCODE  DIM       7
QUANTITY  FORM      4
REPDATE   DIM       8
REPDIM8   DIM       8
REPTTYPE  FORM      1         * 1=failed report, 2=success report
RFLVDATE  DIM       8
SAVALLW   FORM      6.2
SAVATYPE  DIM       3
SAVDOCT   DIM       6
SAVBEND   FORM      3
SAVDISC   FORM      6.2
SAVECOD1  DIM       9
SAVECOD2  DIM       9
SAVECOD3  DIM       9
SAVECOD4  DIM       9
SAVESCID  DIM       2
SAVETREF  DIM       8
SAVETTYP  DIM       2
SAVEXTRA  FORM      6.2
SAVKEY6   DIM       6
SAVKEY42  DIM       42
SCLAIM    DIM       3
SFUNDH    DIM       6
STEPDFLD  FORM      1
SUMMFULL  FORM      1         * 0=no report, 1=full report, 2=summary report
TCC       FORM      2
TDD       FORM      2
TEMP1     IFILE     SQL, FIXED=4, KEY="U1-3"
TIMESTOP  DIM       5         * ending time
TIMESTRT  DIM       5         * starting time
TMM       FORM      2
TMPPTYP   DIM       3
TODATE8   DIM       8
TOHOUR    FORM      2
TOMIN     FORM      2
TONUMB    FORM      1
TOTIME    DIM       5
TOTYPE    DIM       1
TYY       FORM      2
UAGEDEPT  FORM      1
UAGEPROC  FORM      1
UNIQCNT   DIM       3
USINGDEP  FORM      1
WDATE1    DIM       8
WDATE2    DIM       8
XDATE1    DIM       8
.
PRGID     INIT      "IBACCI79"
PRGNAM    INIT      "Daily Extracts"
VERSION   INIT      "V12.00.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.*                       Controlling Logic                            *
.**********************************************************************
ML0000    CALL      INIT0000            * initialization
.                                       * open files
.                                       * display header
.
ML0500    CALL      MENU0000            * Program menu
          BRANCH    EXIT,ML9999
.
          CALL      SET0000             * set up dates 
          BRANCH    EXIT,ML0500
.
          CALL      CTPF0000                * create failure temp file
          CALL      CTPS0000                * create success temp file
.
ML1000    CALL      LOOP0000
          BRANCH    EXIT,ML9500
.
          DISPLAY   *P1:24,"Processing Patient : ",*EL
          BRANCH    MNUOPT,ML2100,ML2200,ML2300,ML2400,ML2500,ML2600
.
ML2100    DISPLAY   *P1:22,*EL,*V2LON,"**** Processing A & E ****"
          CALL      AAE0000             * extract A & E details
          BRANCH    MNUOPT,ML1000,ML1000,ML1000,ML1000,ML1000,ML1000
.
ML2200    DISPLAY   *P1:22,*EL,*V2LON,"**** Processing Inpatients ****"
          CALL      INP0000             * extract Inpatient details
          BRANCH    MNUOPT,ML1000,ML1000,ML1000,ML1000,ML1000,ML1000
.
ML2300    DISPLAY   *P1:22,*EL,*V2LON,"**** Processing Outpatients ****"
          CALL      OUT0000             * extract Outpatient Details
          BRANCH    MNUOPT,ML1000,ML1000,ML1000,ML1000,ML1000,ML1000
.
ML2400    DISPLAY   *P1:22,*EL,*V2LON,"**** Processing Theatre ****"
          CALL      THET0000            * extract Theatre details
          BRANCH    MNUOPT,ML1000,ML1000,ML1000,ML1000,ML1000,ML1000
.
ML2500    DISPLAY   *P1:22,*EL,*V2LON,"**** Processing O/P Extra Screen ****"
          CALL      OPX0000             * extract O/P Extra Screen details
          BRANCH    MNUOPT,ML1000,ML1000,ML1000,ML1000,ML1000,ML1000
.
ML2600    GOTO      ML1000
.
ML9500    CALL      PROC0000              * update cciutlaf
          CALL      REPT0000
          GOTO      ML0500
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.*               Open files and display header                        *
.**********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P1:3,*EF
          OPEN      CONTROLF,"controlf"
          OPEN      AAEDE1A1,"aaede1af"
          OPEN      AAEDI1A1,"aaedi1af"
          OPEN      AAEDI1A2,"aaedi1af"
          OPEN      NZPBFEA1,"nzpbfeaf"
          OPEN      OUTSITA1,"outsitaf"
          OPEN      OUTSIPA1,"outsipaf"
          OPEN      PATBFEE1,"patbfeef"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
          OPEN      PATDTRA1,"patdtraf"
          OPEN      PATITEM1,"patitemn"
          OPEN      PATFN1A1,"patfn1af"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATTRAN1,"pattranf"
          OPEN      PATTRAN2,"pattranf"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATHSPA1,"pathspaf"
          OPEN      PMSCIDA1,"pmscidaf"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      CCIUTLL1,"cciutlaf"
          OPEN      CCIPARA1,"cciparaf"
          OPEN      CCIDPTA1,"ccidptaf"
          OPEN      CCIGDPA1,"ccigdpaf"
          OPEN      CCIGPRA1,"ccigpraf"
          OPEN      CCIXPAA1,"ccixpaaf"
          OPEN      CCIXGDA1,"ccixgdaf"
          OPEN      CCIXGPA1,"ccixgpaf"
.
          CALL      TFILENAM
          MOVE      TFILNAME,FNAMER
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      DEPCPHA1,"depcphaf"
          TRAPCLR   IO
          MOVE      OVRCD,USINGDEP      * dependency ?
          IF        OVRCD = 0
            OPEN      DEPCATA1,"depcataf"
          ENDIF
.
.
.------ read the control file ------
.
          READ      CONTROLF,ZERO;*8,CURCENT,*43,IBCNMHOS
          READ      CONTROLF,SEVENTY1;*51,CCCNBRDR,CCCNBDCD,CCCNIPCD:
                                          CCCNDCCD,CCCNOPCD,CCCNAECO:
                                          CCCNDELY,CCCNBTYP,CCCN009A:
                                          CCCN009B,CCCN012A,CCCN012B:
                                          CCCN013A,CCCN013B,CCCNOPO1:
                                          CCCNOPO2,CCCNOPO3,CCCNOPO4:
                                          CCCNOPO5,CCCNOPO6,CCCNOPO7:
                                      *95,CCCN008A,CCCN008B,*168,CCCNCEDT:
                                      *184,CCCNXRPT,*187,CCCN032A,CCCN032B
          READ      CONTROLF,TEN5;*247,CSTDOWN
          READ      CONTROLF,TEN9;*212,CFEETYP
          READ      CONTROLF,TWENTY;*164,PTRESDAY,*193,PTCNDCLM,*211,PTCNCNDY
          READ      CONTROLF,TEN;*194,CDIETCD
.         
          IF        CDIETCD = 1
.           OPEN      CATCOMA1,"catcomaf"
.           OPEN      CATCOMA2,"catcomaf"
          ENDIF
.
          MOVE      CCCNXRPT,SUMMFULL       * will set to NO report if zero
          CALL      TFILENAM
          MOVE      TFILNAME,CFNAMEF
          CALL      TFILENAM
          MOVE      TFILNAME,CFNAMES
.
INIT9999  RETURN
+
.**********************************************************************
.*                              MENU0000                              *
.*                   Do the extraction menu                           *
.**********************************************************************
MENU0000  HLINE     *G37,2,47,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = A&E Extract":
                    *P1:6,*V2LON,"2",*HOFF," = Inpatient Extract":
                    *P1:7,*V2LON,"3",*HOFF," = Outpatient Extract":
                    *P1:8,*V2LON,"4",*HOFF," = Theatre Extract":
                    *P1:9,*V2LON,"5",*HOFF," = O/P Extra Screen Extract":
                    *P1:10,*V2LON,"6",*HOFF," = District Nursing Extract":
                    *P1:11,*V2LON,"7",*HOFF," = Extract All":
                    *P1:13,"Select option : "
.
MENU1000  MOVE      ZERO,EXIT
          KEYIN     *P17:13,*V2LON,MNUOPT:
                    *P17:13,*DV,MNUOPT
.
         BRANCH    MNUOPT,MENU2100,MENU2200,MENU2300,MENU2400,MENU2500,MENU2600:
                          MENU2700
          IF        MNUOPT = 0
            CALL      DTPF0000
            CALL      DTPS0000
            CALL      KILL0000
            MOVE      ONE,EXIT
            GOTO      MENU9999
          ENDIF
          BEEP
          GOTO     MENU1000
.
MENU2100  DISPLAY  *P47:2,*V2LON,"- A&E Extract "
          GOTO     MENU9999
.
MENU2200  DISPLAY  *P47:2,*V2LON,"- Inpat. Extract "
          GOTO     MENU9999
.
MENU2300  DISPLAY  *P47:2,*V2LON,"- Outpat. Extract "
          GOTO     MENU9999
.
MENU2400  DISPLAY  *P47:2,*V2LON,"- Theatre Extract "
          GOTO     MENU9999
.
MENU2500  DISPLAY  *P47:2,*V2LON,"- O/P Extra Screen Extract "
          GOTO     MENU9999
.
MENU2600  GOTO     MENU9999
.
MENU2700  DISPLAY  *P47:2,*V2LON,"- Extract All "
          GOTO     MENU9999
.
MENU9999  RETURN
+
.**********************************************************************
.*                               SET0000                              *
.*                   Set up initial variables values                  *
.**********************************************************************
SET0000   DISPLAY   *P1:13,*EF:
                    *P1:14,"From Date : ":
                    *P1:16,"To   Date : " 
.
.         get the new default from date
.
          UNPACK    CCCNCEDT,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
.
          CALL      DMYCON
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      KEY8,CC,YY,MM,DD
          REP       " 0",KEY8
.
          MOVE      ZERO,EXIT
          MOVE      TEN3,CCOL
          MOVE      TEN4,CVERT
          MOVE      ONE,CCANLDTE
          MOVE      ZERO,CHIGHLT
.
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE
          BRANCH    OVRCD,SET9100
.
          PACK      REPDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",REPDATE
.
          MOVE      CDAY,DD
          MOVE      CMON,MM
          MOVE      CYEAR,YY
          MOVE      CCENT,CC
          CALL      DMYCON
          SUB       ONE,JULDAY
.
          MOVE      JULCC,JWKCC
          MOVE      JULYR,JWKYER
          MOVE      JULDAY,JWKDAY
          CALL      JULCON
          PACK      REPDATE,CC,YY,MM,DD
          REPLACE   " 0",REPDATE
.
SET6000   MOVE      TEN6,CVERT
          CALL      KEYDATE
          BRANCH    OVRCD,SET0000
.
          PACK      TODATE8,CCENT,CYEAR,CMON,CDAY
          REPLACE   " 0",TODATE8
.
          MATCH     REPDATE,TODATE8
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"To Date must be after From Date.  ";
            CALL      HITENTER
            GOTO      SET0000
          ENDIF
.
          CALL      DOSR0000                * do detailed or summary report
          MOVE      ZERO,CNTSUCC
          MOVE      ZERO,CNTFAIL
          CLOCK     TIME,CTIMEIS 
          MOVE      "70",CLNO
          MOVE      ZERO,CPAGENO
.
          CALL      CONTQST
          BRANCH    CEXIT,SET9999,SET0000
.
SET9100   MOVE      ONE,EXIT
.
SET9999   RETURN
.
.****************************************************************************
.*       LOOP0000                                                           *
.****************************************************************************
LOOP0000  MOVE      ZERO,EXIT
          CLOCK     TIME,CTIMEIS 
          MOVE      "70",CLNO
          MOVE      ZERO,CPAGENO
          UNPACK    REPDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON
          ADD       ONE,JULDAY
          MOVE      JULCC,JWKCC
          MOVE      JULYR,JWKYER
          MOVE      JULDAY,JWKDAY
          CALL      JULCON
          PACK      REPDATE,CC,YY,MM,DD
          REPLACE   " 0",REPDATE
          MATCH     REPDATE,TODATE8
          GOTO      LOOP9000 IF LESS
.
LOOP2000  MOVE      REPDATE,REPDIM8          * for dates without century
          MOVE      REPDATE,CPTDATE
          CALL      GPERD                    * calculate fiscal periods
          BRANCH    OVRCD,LOOP9000
          MOVE      CPTDATE,CCUTDATE
          MOVE      DRGYR,CCUTFSYR
          MOVE      DRGNUM,CCUTFSPR
          MOVE      ZERO,EXIT
          GOTO      LOOP9999
.
.------ date generated is not in the date range file ------
.
LOOP9000  MOVE      ONE,EXIT
.
.         if TO date > the default FROM date, write the TO date to file
.
          MATCH     CCCNCEDT,TODATE8
          GOTO      LOOP9999 IF LESS
.
          MOVE      TODATE8,CCCNCEDT
          WRITAB    CONTROLF,SEVENTY1;*168,CCCNCEDT
.
LOOP9999  RETURN
+
.*********************************************************************
.         enter Detailed or Summary report
.         Returns : SUMMFULL = 1 -> summary  report
.                   SUMMFULL = 2 -> detailed report
.*********************************************************************
DOSR0000  COMPARE   ZERO,SUMMFULL
          GOTO      DOSR9999 IF EQUAL       * not doing report
.
          DISPLAY   *P1:17,*EF,*P1:18,*V2LON,ANSS,*HOFF,"ummary or ":
                    *V2LON,ANSD,*HOFF,"etailed : "
          KEYIN     *P23:18,*V2LON,ANS:
                    *P23:18,*DV,ANS
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
          MOVE      TWO,SUMMFULL            * set for DETAILED
          MATCH     ANSD,ANS 
          GOTO      DOSR1000 IF EQUAL
          MOVE      ONE,SUMMFULL            * set for SUMMARY
          MATCH     ANSS,ANS 
          GOTO      DOSR1000 IF EQUAL
          BEEP
          GOTO      DOSR0000
.
DOSR1000  MOVE      "Detailed",KEY8
          IF        SUMMFULL = ONE
            MOVE      "Summary ",KEY8
          ENDIF
          DISPLAY   *P23:18,*V2LON,KEY8
.
DOSR9999  RETURN
.
.*******************************************************************************
.         Get the default charging claim code for multi hospital
.*******************************************************************************
DCLM0000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          IF        IBCNMHOS=1
            OPEN      PATHSPA1,"pathspaf"
            MOVE      PMVXMHOS,KEY3
            CALL      RDPTHSP1
            IF        OVRCD=0
              MOVE      PTHSDCLM,PTCNDCLM     * default claim code charging
            ENDIF
          ENDIF
DCLM9999  RETURN
+
.------   Main program include ------
.
          INC       CCIXTRAC                * Extraction program
.
.         I/O Includes
.
          INC       STD001IO
          INC       AAEDE1IO/INC
          INC       AAEDI1IO/INC
          INC       CALCAGE/PBL             * calculate age
.         INC       CATCOMIO/INC
          INC       CCIDPTIO/INC
          INC       CCIGDPIO/INC
          INC       CCIGPRIO/INC
          INC       CCIPARIO/INC
          INC       CCIUTLIO/INC
          INC       CCIXGDIO/INC
          INC       CCIXGPIO/INC
          INC       CCIXPAIO/INC
          INC       DATECONV
          INC       GETCNEFF
          INC       NZPBFEIO/INC
          INC       OPRDEAIO/INC            * op details file
          INC       OPRDEBIO/INC            * op team file
          INC       OPRDECIO/INC            * op usage file
          INC       OPREXPIO/INC            * expensive item
          INC       OPRSESIO/INC            * session file
          INC       OUTCANIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTCTYIO/INC
          INC       OUTMA1IO/INC
          INC       OUTSITIO/INC
          INC       OUTSIPIO/INC
          INC       OUTXSCIO/INC
          INC       PATCOMN3
          INC       PATDRGIO/INC
          INC       PATBFEIO/INC       
          INC       PATCFAIO/INC
          INC       PATCODIO/INC            * codes file
          INC       PATDSCIO/INC
          INC       PATDTRIO/INC
          INC       PATFN1IO/INC
          INC       PATFX1IO/INC
          INC       PATITMIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATRCAIO/INC
          INC       PATTRNIO/INC
          INC       PATVISIO/INC
          INC       PATWR1IO/INC
          INC       PMSCIDIO/INC
          INC       PMSMTIIO/INC            * Miscellaneous theatre item file
          INC       PATHSPIO/INC
          INC       PMSVX1IO/INC
          INC       DEPCATIO/INC
          INC       DEPCPHIO/INC
          INC       STEPDOWN
          INC       PATITMRD
          INC       VALCONTR
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
  
........................................................................
