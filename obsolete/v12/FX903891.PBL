.***************************************************************************
.* System    :   Inpatients                                               *
.* Program   :   FX903891                                                  *
.* Desc      :   Data Fix for CABRINI ONLY, to be run only ONCE            *
.*               Move data from email opt out to no contact fields         *
.*               From ptmas045 to pmpxuyn4                                 * 
.***************************************************************************
.* Date      :   15/03/2022                                                *
.* Author    :   DA Sarkies                                                *
.* Function  :   Moves PUYN1 into PMPXUYN4
.* Modifications  :                                                        *
.*        V11.02.00  15.03.2022  DA Sarkies    CAR 0903891                  *
.*                  Created program                                        *
.***************************************************************************
.
          INC       STD001FD
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC

.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
.
PRGID     INIT      "FX903891"
PRGNAM    INIT      "Data Fix for Cabrini         "
VERSION   INIT      "V12.00.00"
DISNUM    FORM      4
RECNUM    FORM      8
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          MOVE      ZERO,DISNUM
          MOVE      ZERO,RECNUM
.
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run":
                    *P1:10,*V2LON,*EL,"Data Fix for use in CABRINI only":
                    *P1:12,*V2LON,*EL,"Fix to be used ONLY ONCE" 
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  DISPLAY   *P1:20,*EL,"Record No. : ";     * Sets up counter
          PACK      KEY8,SP70  
          CALL      RDSMAST1                        * Read Master table
PROC1000  CALL      RDKMAST1
          BRANCH    OVRCD,PROC9999          
.
          PACK      KEY8,PURNO,SP70                 * Packs UR Number
          CALL      RDPMPX21                        * Reads sub table
          BRANCH    OVRCD,PROC1000                  
.
          MOVE      PUYN1,PMPXUYN4                  * Moves data to DNC
          MOVE      ZERO,PUYN1                      * clears EO-O
.
          CALL      UPPMPX21                        * Updates Table
          CALL      UPMAST1

          ADD       ONE,RECNUM                      * Increases record number
          ADD       ONE,DISNUM                      * Increases counter
.                                                   * Checks if reached value
          IF        DISNUM = 1000 | RECNUM = 1      
            MOVE      ZERO,DISNUM                   * Clears Counter
            DISPLAY   *P15:20,*V2LON,RECNUM         * Displays Record Number
          ENDIF
.
          GOTO      PROC1000
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC
