******************************************************************************
.*    Program      : F93ITPEN                                                 *
.*    Description  : Move all items pending from dtr to pmsmtiaf file         *
.*                                                                            *
.*    Author       : J.Tan                                                    *
.*    Date         : n4/10/2003     SRF 44182                                 *
.*    Modifications:                                                          *
.*       V9.04.02   04/08/2005  J.Tan        CAR 62750                        *
.*                  Mods not to use the redefined amount variables            *
.*                   V9.04.01 12/10/04  J.Tan   CAR 54207                     *
.*                   Fixed DTRP0000 - field for invoice number                *
.*                   V9.03.03 15/06/04  S.Barcham   50035                     *
.*                   Added PMMIMVBR                                           *
.*                   V9.03.02 07/01/04  J.Tan                                 *
.*                   Added new index to DTRFD file                            *
.*                   V9.03.01 08/12/03  J.Tan                                 *
.*                   Mods checking for oracle                                 *
.******************************************************************************
          INC       STD001FD
          INC       PATDTRFD/INC
          INC       OUTDTRFD/INC
          INC       AAEDTRFD/INC
          INC       PMSMTIFD/INC
          INC       PATVISFD/INC
.
RECNUM    FORM      8
CMDLINE   DIM       100
PRGNAM    INIT      "Update Items Pending file from DTR"
PRGID     INIT      "F93ITPEN"
VERSION   INIT      "V12.00.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
MAIN0100  CALL      OPTN0000                      * select options
          BRANCH    EXIT,MAIN9999                 * EXIT = 1 if 0 chosen in menu
.
          CALL      OFIL0000
          BRANCH    EXIT,MAIN9999                 * EXIT = 1 if 0 chosen in menu
.
          BRANCH    COPTION,MAIN0200              * proceed 
          GOTO      MAIN9999
.
MAIN0200  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN1000,MAIN0000,MAIN9999 * Yes, no, cancel
.
MAIN1000  CALL      UPEN0000                      * Update item pending file
          CALL      ENDP0000                      * save/compress saved file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
.
          DISPLAY   *P64:24,"pmsmtiaf";
          OPEN      PMSMTIA1,"pmsmtiaf" 
.
          DISPLAY   *P64:24,"patvisaf";
          OPEN      PATVISA1,"patvisaf" 
          MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9999  RETURN
+
.********************************************************************
.*                          OFIL0000                                *
.*                     Open files                                   *
.********************************************************************
OFIL0000  DISPLAY   *P56:24,"Opening outdtrof";
          OPEN      OUTDTRO1,"outdtrof"
          OPEN      OUTDTRO4,"outdtrof"
.
          DISPLAY   *P64:24,"aaedtref";
          OPEN      AAEDTRE1,"aaedtref" 
          OPEN      AAEDTRE4,"aaedtref" 
.
          DISPLAY   *P64:24,"patdtraf";
          OPEN      PATDTRA1,"patdtraf" 
          OPEN      PATDTRA5,"patdtraf" 
.
          MOVE      ZERO,EXIT
          GOTO      OFIL9999
.
OFIL9000  MOVE      ONE,EXIT
OFIL9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run update                             *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Update "
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**********************************************************************
.*                               UPEN0000                             *
.*             Update items pending file from DTR files               *
.**********************************************************************
UPEN0000  DISPLAY   *P1:20,"Looping outdtrof";
          CALL      DTRO0000                      * Generate Outpatient
.
          DISPLAY   *P1:20,"Looping aaedtref";
          CALL      DTRE0000                      * Generate Emergency
.
          DISPLAY   *P1:20,"Looping patdtraf";
          CALL      DTRP0000                      * Generate Patient 
.
UPEN9999  RETURN
+
.**********************************************************************
.*                               DTRO0000                             *
.*             loop thru Outpatient DTR                               *
.**********************************************************************
DTRO0000  DISPLAY   *P20:20,*EL,"Record No. : ";
          MOVE      ZERO,RECNUM
          PRINT     *N,*1,"Outpatient Number",*20,"Invoice Number":
                    *40,"Transaction Number"
.
DTRO1000  MOVE      "00000000",OTINVNO
          MOVE      "1",OTRECTYP
          PACK      KEY23,OTINVNO,OTRECTYP,SP70
          CALL      RDSDTRO4                      * position at start
          CALL      RDKDTRO4                      * read next record
          BRANCH    OVRCD,DTRO9999                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
          MATCH     "00000000",OTINVNO
          GOTO      DTRO9999 IF NOT EQUAL
          BRANCH    OTRECTYP,DTRO2000
          GOTO      DTRO9999
.
DTRO2000  MOVE      ZERO,PVIURNO
          MOVE      OTNUMB,KEY8
          CALL      RDVISA1
.
          MOVE      OTNUMB,PMMIVISN               * Outpatient no
          MOVE      OTTRANS,PMMITRAN              * Trans no
          MOVE      PVIURNO,PMMIURNO              * U/R no
          MOVE      " 2",PMMISYST                 * System
          MOVE      OTITEMNO,PMMIITEM
          MOVE      OTDDESC,PMMIDESC
          MOVE      OTDDATE,PMMITDAT
          MOVE      "3",PMMIRTYP
          MOVE      OTPATAMT,PMMIAMTT
          MOVE      ZERO,PMMIAMTG
          MOVE      ZERO,PMMIAMTH
          MOVE      OTPATPOR,PMMIAMTP
          MOVE      OTREBPOR,PMMIRBAT
          MOVE      SP10,PMMITDOC
          MOVE      OTSERVS,PMMISERV
          MOVE      SP10,PMMIODOC
          MOVE      SP10,PMMISESS
          MOVE      OTMISGRP,PMMIMGRP
          MOVE      OTBATCHN,PMMIBTCH
          MOVE      OTNINVS,PMMIINVN
          MOVE      SP70,PMMIDES2
          MOVE      OTDTGSTA,PMMIGSTA
          MOVE      OTDTGSTC,PMMIGSTC
          MOVE      OTDTGSTM,PMMIGSTM
          MOVE      OTCHGDTE,PMMIDUPD
          MOVE      OTCHGTIM,PMMITUPD
          MOVE      SP10,PMMIWUSR
          MOVE      ZERO,PMMIMVBR
.
          PACK      KEY14,PMMIVISN,PMMITRAN
          CALL      WRPMMTI1                      * Write to pending file
.
          PACK      KEY22,OTNUMB,OTINVNO,OTTRANS
          CALL      DEDTRO1
.
          PRINT     *1,OTNUMB,*20,OTINVNO,*40,OTTRANS
          IF        (RECNUM%1000) = 0
            DISPLAY   *P35:20,*V2LON,RECNUM
          ENDIF
          GOTO      DTRO1000                      * get next record
.
DTRO9999  RETURN
+
.**********************************************************************
.*                               DTRE0000                             *
.*             loop thru Emergency DTR                                *
.**********************************************************************
DTRE0000  DISPLAY   *P20:20,*EL,"Record No. : ";
          MOVE      ZERO,RECNUM
          PRINT     *N,*1,"Emergency Number",*20,"Invoice Number":
                    *40,"Transaction Number"
.
DTRE1000  MOVE      "00000000",ATINVNO
          MOVE      "1",ATRECTYP
          PACK      KEY23,ATINVNO,ATRECTYP,SP70
          CALL      RDSDTRE4                      * position at start
          CALL      RDKDTRE4                      * read next record
          BRANCH    OVRCD,DTRE9999                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
          MATCH     "00000000",ATINVNO
          GOTO      DTRE9999 IF NOT EQUAL
          BRANCH    ATRECTYP,DTRE2000
          GOTO      DTRE9999
.
DTRE2000  MOVE      ZERO,PVIURNO
          MOVE      ATNUMB,KEY8
          CALL      RDVISA1
.
          MOVE      ATNUMB,PMMIVISN               * admission no
          MOVE      ATTRANS,PMMITRAN              * Trans no
          MOVE      PVIURNO,PMMIURNO              * U/R no
          MOVE      " 1",PMMISYST                 * System
          MOVE      ATITEMNO,PMMIITEM
          MOVE      ATDDESC,PMMIDESC
          MOVE      ATDDATE,PMMITDAT
          MOVE      "3",PMMIRTYP
          MOVE      ATPATAMT,PMMIAMTT
          MOVE      ZERO,PMMIAMTG
          MOVE      ZERO,PMMIAMTH
          MOVE      ATPATPOR,PMMIAMTP
          MOVE      ATREBPOR,PMMIRBAT
          MOVE      SP10,PMMITDOC
          MOVE      ATSERVS,PMMISERV
          MOVE      SP10,PMMIODOC
          MOVE      SP10,PMMISESS
          MOVE      ATMISGRP,PMMIMGRP
          MOVE      ATBATCHN,PMMIBTCH
          MOVE      ATNINVS,PMMIINVN
          MOVE      SP70,PMMIDES2
          MOVE      ATDTGSTA,PMMIGSTA
          MOVE      ATDTGSTC,PMMIGSTC
          MOVE      ATDTGSTM,PMMIGSTM
          MOVE      ATCHGDTE,PMMIDUPD
          MOVE      ATCHGTIM,PMMITUPD
          MOVE      SP10,PMMIWUSR
.
          PACK      KEY14,PMMIVISN,PMMITRAN
          CALL      WRPMMTI1                      * Write to pending file
.
          PACK      KEY22,ATNUMB,ATINVNO,ATTRANS
          CALL      DEDTRE1
.
          PRINT     *1,ATNUMB,*20,ATINVNO,*40,ATTRANS
          IF        (RECNUM%1000) = 0
            DISPLAY   *P35:20,*V2LON,RECNUM
          ENDIF
          GOTO      DTRE1000                      * get next record
.
DTRE9999  RETURN
+
.**********************************************************************
.*                               DTRP0000                             *
.*             loop thru Inpatient DTR                                *
.**********************************************************************
DTRP0000  DISPLAY   *P20:20,*EL,"Record No. : ";
          MOVE      ZERO,RECNUM
          PRINT     *N,*1,"Inpatient Number",*20,"Invoice Number":
                    *40,"Transaction Number"
.
DTRP1000  MOVE      "00000000",TREF
          MOVE      "2",TRECTYPE
          PACK      KEY23,TREF,TRECTYPE,SP70
          CALL      RDSDTRN5                      * position at start
          CALL      RDKDTRN5                      * read next record
          BRANCH    OVRCD,DTRP9999                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
          MATCH     "00000000",TREF
          GOTO      DTRP9999 IF NOT EQUAL
          BRANCH    TRECTYPE,DTRP1000,DTRP2000,DTRP2000
          GOTO      DTRP9999
.
DTRP2000  MOVE      ZERO,PVIURNO
          MOVE      TADMNO,KEY8
          CALL      RDVISA1
.
          MOVE      TADMNO,PMMIVISN               * admission no
          MOVE      TTRANSN1,PMMITRAN              * Trans no
          MOVE      PVIURNO,PMMIURNO              * U/R no
          MOVE      " 3",PMMISYST                 * System
          MOVE      TITEMNO,PMMIITEM
          MOVE      TDDESC,PMMIDESC
          PACK      PMMITDAT,TFCENT,TFYEAR,TFMONTH,TFDAY
          REP       " 0",PMMITDAT
          MOVE      TRECTYPE,PMMIRTYP
          MOVE      TPATAMTT,PMMIAMTT
          MOVE      TPATAMTG,PMMIAMTG
          MOVE      TPATAMTH,PMMIAMTH
          MOVE      TPATAMTP,PMMIAMTP
          MOVE      TREBATE,PMMIRBAT
          MOVE      TDOCTA,PMMITDOC
          MOVE      TSERVS,PMMISERV
          MOVE      TDOCTO,PMMIODOC
          MOVE      TSESSION,PMMISESS
          MOVE      TMISGRP,PMMIMGRP
          MOVE      TBATCHN,PMMIBTCH
          MOVE      TINVPRT,PMMIINVN
          MOVE      PTDTDES2,PMMIDES2
          MOVE      PTDTGSTA,PMMIGSTA
          MOVE      PTDTGSTC,PMMIGSTC
          MOVE      PTDTGSTM,PMMIGSTM
          MOVE      TCHGDTE,PMMIDUPD
          MOVE      TCHGTIM,PMMITUPD
          MOVE      SP10,PMMIWUSR
.
          PACK      KEY14,PMMIVISN,PMMITRAN
          CALL      WRPMMTI1                      * Write to pending file
.
          PACK      KEY22,TADMNO,TREF,TTRANSN1
          CALL      DEDTRN1
.
          PRINT     *1,TADMNO,*20,TREF,*40,TTRANSN1
          IF        (RECNUM%1000) = 0
            DISPLAY   *P35:20,*V2LON,RECNUM
          ENDIF
          GOTO      DTRP1000                      * get next record
.
DTRP9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.**********************************************************************
ENDP0000  CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.*                               I/O                                  *
.**********************************************************************
WRPMMTI1  RESET     KEY14                                                       
          WRITE     PMSMTIA1,KEY14;PMMIVISN,PMMITRAN,PMMIURNO,PMMISYST,PMMIITEM:
                        PMMIDESC,PMMIDES2,PMMITDAT,PMMIRTYP,PMMIREFN:  
                        PMMIAMTT,PMMIAMTG,PMMIAMTH,PMMIAMTP,PMMIRBAT,PMMITDOC:  
                        PMMISERV,PMMIODOC,PMMISESS,PMMIMGRP,PMMIBTCH,PMMIINVN:  
                        PMMIGSTA,PMMIGSTC,PMMIGSTM,PMMIDUPD,PMMITUPD,PMMIWUSR:  
                        PMMIMVBR,PMMISPAR                                                
          RETURN
.
          INC       OUTDTRIO/INC
          INC       AAEDTRIO/INC
          INC       PATDTRIO/INC
.         INC       PMSMTIIO/INC
          INC       PATVISIO/INC
          INC       STD001IO
