. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS85                                             *
. * Desc      :   Budget Total Account Update                          *
. **********************************************************************
. * Date      :   10.10.1990                                           *
. * Author    :   B.G.Ackland (IBA)                                    *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
. * VF.00.01  11.01.2000 Charles Handaya                               *
. *           Recompiled for Audit File date                           *
. **********************************************************************
.$$$$$$
.Accounting Version 6
.--------------------
.Budgets
.-------
.6. Total Account Update
.-----------------------
.The total account update program for budgtes will consist of one passes
.through the budget file to be processed.
.
.The program is written using a procedure for the update section of the
.code. This procedure will be used in the automatic daily update program.
.
.Screen Layout
.-------------------------------------------------------------------------------
.IBAFMS85            Information Builders Australia              Date : 12/12/89
.V6.00               Budget Total Account Update
.
. 1. Budget Type          : ____
.
.Select Field, (P)ost, (C)ancel :
.-------------------------------------------------------------------------------
.Budget Type      : Use Std Keyin Routine.
.
.(C)ancel         : Exit Program
.
.(P)ost           : PROC   Update procedure
.
.NOTE - Add new index on the account master file FMSAMAFD
.FMSAMAA4  IFILE  SQL, FIXED=309, KEY="U53-53,1-2,3-14" 
.
.Update Procedure - DEFPROC
.----------------
.INCLUDE NAME - PRCFMSBU.PBL
.
.For the budget type passed to this procedure perform the following.
.
.1. OPEN Budget File           - fmsbtttt   where tttt = budget type
.   OPEN Account Master File   - fmsamaaf
.   OPEN Report Sequence File  - fmsarsaf
.
.2. Pass 1 Clear Totals
.   if program id = IBAFMS85 Display "Clearing : " on line 24
.   Loop through the account master file by index 4 U53-53,1-2,3-14
.   for all account of Type 6. ( ie TOTAL ACCOUNTS )
.   if program id = IBAFMS85 Display Each Account on line 24
.   Read budget file
.        If no record exists
.             Continue Loop
.        if record exists
.             Move Zero to Budget Period Values and Update Record
.             Continue Loop
.        
.
.3. Pass 2 Accumulate Total Accounts
.   if program id = IBAFMS85 Display "Accumulating : " on line 24
.   Loop through the account reporting sequence file by index 3
.   FMSARSA3  IFILE  SQL, FIXED=71, KEY="U3-4,27-29,15-26,1-2"  * Total Calc 
.   if program id = IBAFMS85 Display Each Account on line 24
.   Read Budget Record
.       If no record exists
.            Continue Loop
.       If record exists
.            Move period values to save values
.            Read budget record for Addto Ledger/Account
.                 If no record exists
.                    Write new record with saved period values
.                    Continue loop
.                 If record exists
.                    Add period values 
.                    Update Total Record
.                    Continue Loop
.
.
.
.$$$$$$
. Standard FMS Accounting System Include
.---------------------------------------
          INC       FMSSTDDF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
          INC       FMSBTYFD/INC
          INC       FMSAMAFD/INC
          INC       FMSLMAFD/INC
          INC       FMSTCFFD/INC
          INC       FMSBUDFD/INC
          INC       FMSSBUFD/INC
          INC       FMSLBUFD/INC
          INC       FMSCONFD/INC
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
FMSB      INIT      "fmsb"
FMSD      INIT      "fmsd"
FMSL      INIT      "fmsl"
FMSP      INIT      "fmsp"
FMSR      INIT      "fmsr"
FMSS      INIT      "fmss"
FMST      INIT      "fmst"
.
ACCOUNT   DIM       12
BUDTYPE   DIM       4
FILENAME  DIM       8
LEDGER    DIM       2
LINKONLY  FORM      1
.
PRGID     INIT      "IBAFMS85"
PRGNAM    INIT      "Budget Total Account Update"
VERSION   INIT      "V12.00.00"
.
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML0100    CALL      SCRA0000           * Display Screen Layout
          CALL      ACTA0000           * Accept budget type 
          BRANCH    EXIT,ML9999        * exit program Null Entry
.
          CALL      SELA0000           * Allow modification
          BRANCH    EXIT,ML9999        * exit program
.
          CALL      UPBUD000           * Perform Budget Update
          DISPLAY   *P1:24,*EL,"Update Complete - ";
          CALL      HITENTER
          GOTO      ML0100             * get next entry
.
ML9999    CHAIN     PGM                * chain back to menu
.
.******************************************************************************
.* INIT - Open Files                             Called by ML0000             *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening fmsbtyaf";  * 
          OPEN      FMSBTYA1,"fmsbtyaf"
.
          DISPLAY   *P64:24,"fmslmaaf";
          OPEN      FMSLMAA1,"fmslmaaf"
.
          DISPLAY   *P64:24,"fmsamaaf";
          OPEN      FMSAMAA1,"fmsamaaf"
          OPEN      FMSAMAA2,"fmsamaaf"
          OPEN      FMSAMAA3,"fmsamaaf"
          OPEN      FMSAMAA4,"fmsamaaf"
.
          DISPLAY   *P64:24,"fmstcfaf";
          OPEN      FMSTCFA1,"fmstcfaf"
          OPEN      FMSTCFA2,"fmstcfaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          CALL      RDFMCO51
          CALL      RDFMCO52
.
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.******************************************************************************
.* SCRA - Display Screen                         Called by ML0000             *
.******************************************************************************
RBTYFM00
SCRA0000  DISPLAY   *P1:4,*EF,*P2:4,*V2LON,"1",*HOFF:
                    ". Budget Type",*P26:4,": "
          RETURN
.******************************************************************************
.* ACTA - Accept initial value                   Called by ML0000             *
.******************************************************************************
ACTA0000  MOVE      "28",CCOL
          MOVE      "4",CVERT
          CALL      KBTYFM00           * keyin budget type code
          BRANCH    EXIT,ACTA9000,ACTA9000
          DISPLAY   *P28:4,*V2LON,FMBTCODE,*HOFF,*P41:4,FMBTDESC
          MOVE      FMBTCODE,BUDTYPE
.
          GOTO      ACTA9999
.
ACTA9000  MOVE      ONE,EXIT
.
ACTA9999  RETURN
.*************************************************************************
.* SELA - Select field to change Post or Cancel                          *
.*************************************************************************
SELA0000  CALL      MAINQST                * select item post cancel?
          COMPARE   "0",CCITEM             * post ?
          GOTO      SELA9500 IF EQUAL 
          COMPARE   "-1",CCITEM            * cancel ?
          GOTO      SELA9000 IF EQUAL 
.
          BRANCH    CCITEM,SELA1000
          BEEP
          GOTO      SELA0000
.
SELA1000  MOVE      "28",CCOL
          MOVE      "4",CVERT
          CALL      KBTYFM00           * keyin budget type code
          BRANCH    EXIT,SELA1000,SELA9000
          DISPLAY   *P28:4,*V2LON,FMBTCODE,*HOFF,*P41:4,FMBTDESC
          MOVE      FMBTCODE,BUDTYPE
          GOTO      SELA0000
.
SELA9000  MOVE      ONE,EXIT
          GOTO      SELA9999
.
SELA9500  MOVE      ZERO,EXIT
SELA9999  RETURN
.
.**********************************************************************
.  UPBU - Perform Budget Update                        Called By lots
.**********************************************************************
UPBUD000  PROC      FMSBU000           * up date budget data
.
          CLOSE     FMSBUDA1
          CLOSE     FMSSBUA1
          CLOSE     FMSLBUA2
.
          PACK      FILENAME,FMSL,BUDTYPE,SP70
          REP       " 0",FILENAME
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FMSBUDA1,FILENAME
          TRAPCLR   IO
          BRANCH    OVRCD,UPBU9999
.
          PACK      FILENAME,FMSL,BUDTYPE,SP70
          REP       " 0",FILENAME
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FMSSBUA1,FILENAME
          TRAPCLR   IO
          BRANCH    OVRCD,UPBU9999
.
          PACK      FILENAME,FMSL,BUDTYPE,SP70
          REP       " 0",FILENAME
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FMSLBUA2,FILENAME
          TRAPCLR   IO
          BRANCH    OVRCD,UPBU9999
.
          MOVE      "1",LINKONLY
          PROC      FMSBB000           * update for budget links
.
UPBU9999  RETURN
.******************************************************************************
.* INCLUDES                                                                   *
.******************************************************************************
.
          INC       PRCFMSBU           * Budget Total Account Update Procedure
          INC       FMSSTDCD           * FMS Acc. standard routines
          INC       FMSBTYKY
          INC       FMSBTYIO/INC
.
          INC       FMSAMAIO/INC
          INC       FMSLMAIO/INC
          INC       FMSTCFIO/INC
          INC       FMSBUDIO/INC
          INC       FMSSBUIO/INC
          INC       FMSLBUIO/INC
          INC       FMSCONIO/INC
.
          INC       PRCFMSBB
