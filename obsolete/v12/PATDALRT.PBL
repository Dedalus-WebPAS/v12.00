.
.     PATDALRT : Display Alerts
.

.     This include displays entries in the alert file for a given U/R Number 
.     This include is used in : IBAPAT32
.                               IBATRA21
.
.     V10.03.01     Jill Parkinson CAR 249362
.                   Changed alert key
.
PATDALRT  OPEN      PATALRT1,"patalrtf"
          MOVE      ONE,ALRFLG     * assume no alert details for u/r number
          CALL      PATDA400       * display screen layout
.
.         Loop over the enteries for the U/R Number 
.
          PACK      KEY16,PURNO,SP10
          CALL      RSPTALR1
PATDA100  CALL      RKPTALR1
          BRANCH    OVRCD,PATDA300
.
          MATCH     PURNO,PTALURNO
          GOTO      PATDA300 IF NOT EQUAL
.
          MOVE      ZERO,ALRFLG     * assume alert details exist for u/r number
.
.         Find alert code description
.
          MOVE      SP20,ALDESC
          PACK      KEY5,PTALCATG,PTALCODE
          CALL      RDCODE1
          BRANCH    OVRCD,PATDA200
          MOVE      TDESC,ALDESC
.
.         Display alert details
.
PATDA200  UNPACK    PTALCATG,DIM1,ALCATG
.
          MOVE      SP10,ALDATE
          UNPACK    PTALDATE,CCENT,CYEAR,CMON,CDAY
.
          MATCH     SP2,CDAY
          GOTO      PATDA250 IF EQUAL
.
          CALL      PACDATE
          MOVE      CPCDATE,ALDATE
.
PATDA250  DISPLAY   *PCCOL:CVERT,ALCATG,SP3,PTALCODE,SP2,ALDESC,SP1,ALDATE
          ADD       ONE,CVERT
.
          COMPARE   TWENTY3,CVERT
          GOTO      PATDA100 IF LESS
.
          MOVE      EIGHT,CVERT
          ADD       "40",CCOL
.
          COMPARE   "84",CCOL
          GOTO      PATDA100 IF LESS 
.
          KEYIN     *P1:23,*EL,*P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSE,ANS
          GOTO      PATDA300 IF EQUAL
.
          CMATCH    ANSF,ANS
          GOTO      PATDALRT IF EQUAL
          GOTO      PATDA450
.
.         Check to see if any alert details found
.
PATDA300  COMPARE   ZERO,ALRFLG
          GOTO      PATDA500 IF EQUAL
.
.         Check whether to display new or old standard error message
.
          BRANCH    NEWFLG,PATDA350
.
.         Old Standards
.
          DISPLAY   *P1:24,*EL,*P40:24,*V2LON,*B:
                    "** No Alert Details **",*W2;
          GOTO      PATDA999
.
.         New Standards
.
PATDA350  DISPLAY   *P1:23,*EF:
                    *P1:24,*B,"Alert Details not on file ":
                    "- Tap <",*V2LON,"ENTER",*HOFF,">";
          KEYIN     ANS;
          GOTO      PATDA999
.
.         Display Alert Heading
.
PATDA400  DISPLAY   *P1:7,*EF,*P1:7,*V2LON,*ULON:
                    "Cat Code Description         Date      ",*HOFF:
                    *P41:7,*V2LON,*ULON,"Cat Code Description":
                    "         Date      "
          MOVE      EIGHT,CVERT
          MOVE      ONE,CCOL
          RETURN
.
.         Next Screen
.
PATDA450  CALL      PATDA400       * display screen layout
          GOTO      PATDA100
.
.         Clear the screen
.
PATDA500  MOVE      ZERO,OVRCD
          CLOSE     PATALRT1
.
PATDA999  RETURN
