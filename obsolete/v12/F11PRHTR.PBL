.*****************************************************************************
.*    Program      : F11PRHTR                                                *
.*    Description  : Conversion prihtraf to new File layout                  *
.*                                                                           *
.*    Author       : Steve Armstrong                                         *
.*    Date         :                                                         *
.*    Modifications:                                                         *
.*        V10.11.01 28/08/2017  Steve Armstrong   TSK 0837619                *
.*                  Added new fields PRHTLVIS & PRHTROVR                     *
.*        V10.11.00 17/07/2017  Steve Armstrong   TSK 0837619                *
.*                  Created program.                                         *
.*****************************************************************************
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
LOGFILE   FILE      ASCII, FIXED=256
.
.         Old File Definition
.
OLDFILE1 IFILE SQL, FIXED=250, KEY="U1-8+9-14+15-20+21-23+24-29,115-122,30-31,32-40,41-43,44-46"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
DPRHTUNQ  DIM       8      8        1     Unique Identifier
PRHTPRAC  DIM       6      6        9     Medical Practice Code
PRHTDOCT  DIM       6      6        15    Service Doctor Code
PRHTPIND  DIM       3      3        21    Patient Indicator (Category GI)
PRHTREFD  DIM       6      6        24    Referring doctor code
DPRHTFLG  DIM       2      2        30    Item Flag
.                                              0 = MBS
.                                              1 = AMA
PRHTITMN  DIM       9      9        32    Item Number/Test Code (For pathology)
PRHTSUBN  DIM       3      3        41    Subitem Number
DPRHTNUM  DIM       3      3        44    Transaction Number
.
PRHTDATE  DIM       8      8        47    Service Date
PRHTTIME  DIM       5      5        55    Service Time
PRHTSERN  FORM      3      3        60    Number of Services
PRHTIDES  DIM       30     30       63    Item Description
PRHTIAMT  FORM     12.2    8        93    Item Amount
PRHTPFLG  FORM      1      2       101    Pathology Flag
.                                            0 = No
.                                            1 = Yes
PRHTS4B3  FORM      1      2       103    S4B3 Flag
.                                            0 = No
.                                            1 = Yes
PRHTMEDA  DIM       10     10      105    Medicare Assign Form Number
PRHTRDAT  DIM       8      8       115    Referral date
PRHTREFT  DIM       50     50      123    Referral Details
PRHTGSTA  FORM      1      2       173    Is GST Applicable to this item
PRHTGSTC  DIM       6      6       175    GST Payable Code
PRHTRFPD  DIM       3      3       181    Referral Period (Cat RF)
PRHTREFN  DIM       8      8       184    Referral/Encounter Number
PRHTENCN  DIM       8      8       192    Encounter Number
PRHTHICI  DIM       18     18      200    HIC Item Key
PRHTACOI  DIM       1      1       218    After Care Override Indicator
.                                          0 or Blank = No
.                                          1 = Yes
PRHTTDUR  DIM       3      3       219    Time Duration (mins)
OLDSPARE  DIM       28     28      222    Spare Variable
.
.End of Record                     250
.
.
.         New File Definition
.
PRIHTRA1 IFILE SQL, FIXED=399, KEY="U1-8+9-14+15-20+21-23+24-29,115-122,30-31,32-40,41-43,44-46"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.DPRHTUNQ DIM       8      8        1     Unique Identifier
.PRHTPRAC DIM       6      6        9     Medical Practice Code
.PRHTDOCT DIM       6      6        15    Service Doctor Code
.PRHTPIND DIM       3      3        21    Patient Indicator (Category GI)
.PRHTREFD DIM       6      6        24    Referring doctor code
.DPRHTFLG DIM       2      2        30    Item Flag
.                                              0 = MBS
.                                              1 = AMA
.                                              2 = IMC Details
.PRHTITMN DIM       9      9        32    Item Number/Test Code (For pathology)
.PRHTSUBN DIM       3      3        41    Subitem Number
.DPRHTNUM DIM       3      3        44    Transaction Number
.
.PRHTDATE DIM       8      8        47    Service Date
.PRHTTIME DIM       5      5        55    Service Time
.PRHTSERN FORM      3      3        60    Number of Services
.PRHTIDES DIM       30     30       63    Item Description
.PRHTIAMT FORM     12.2    8        93    Item Amount
.PRHTPFLG FORM      1      2       101    Pathology Flag
.                                            0 = No
.                                            1 = Yes
.PRHTS4B3 FORM      1      2       103    S4B3 Flag
.                                            0 = No
.                                            1 = Yes
.PRHTMEDA DIM       10     10      105    Medicare Assign Form Number
.PRHTRDAT DIM       8      8       115    Referral date
.PRHTREFT DIM       50     50      123    Referral Details
.PRHTGSTA FORM      1      2       173    Is GST Applicable to this item
.PRHTGSTC DIM       6      6       175    GST Payable Code
.PRHTRFPD DIM       3      3       181    Referral Period (Cat RF)
.PRHTREFN DIM       8      8       184    Referral/Encounter Number
.PRHTENCN DIM       8      8       192    Encounter Number
.PRHTHICI DIM       18     18      200    HIC Item Key
.PRHTACOI DIM       1      1       218    After Care Override Indicator
.                                          0 or Blank = No
.                                          1 = Yes
.PRHTTDUR DIM       3      3       219    Time Duration (mins)
PRHTMPOV  DIM       1      1       222    Multi Procedure Override
.                                          0 or Blank = No
.                                          1 = Yes
PRHTDSOV  DIM       1      1       223    Duplicate Service Override
.                                          0 or Blank = No
.                                          1 = Yes
PRHTSTXT  DIM       100    100     224    Service Text
PRHTASPF  DIM       1      1       324    Alternate Service Provider Flag
.                                          0 or Blank = No
.                                          1 = Yes
PRHTASPC  DIM       10     10      325    Alternate Service Provider HCP Code
.                                          (pmshcpaf)
PRHTAICF  DIM       1      1       335    Alternate IFC Issue Code Flag
.                                          0 or Blank = No
.                                          1 = Yes
PRHTAICV  DIM       3      3       336    Alternate IFC Issue Code Value
.                                          (Cat ic)
PRHTNMPT  DIM       2      2       339    Number of Patients
.                                          (R-Justified numeric)
PRHTLSPN  DIM       6      6       341    Location Specific Practice No. Value
PRHTSDCD  DIM       2      2       347    Self Deem Code (Cat Sd)
.                                          SD = Self Deemed
.                                          SS = Substituted Service
.                                          N  = Not Self Deemed
PRHTLVIS  DIM       8      8       349    Linked Visit Number
PRHTROVR  DIM       3      3       357    Referral Override (Cat ro)
PRHTSPAR  DIM       39     39      360    Spare Variable
.
.End of Record                     399
.
.
. Redefine FORM fields from key
. -----------------------------
PRHTUNIQ  FORM      8
PRHTFLAG  FORM      2
PRHTNUMB  FORM      3
.
.
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       200
.
DEFPATH   DIM       60
DIM60     DIM       60
DISNUM    FORM      4
.
ERORTEXT  DIM       80
.
INPFILE   DIM       8
.
MINUS3    FORM      "-3"
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
SAVEFLG   FORM      1
.
.
. PROGRAM CONSTANTS
. -----------------
CREATEXT  INIT    "Log file created "
DASHLINE  INIT      "--------------------------------------------------":
                    "------------------------------"
ENDGTEXT  INIT    "Ending   fixit (Option "
OPENTEXT  INIT    "Log file opened "
SP60      INIT    "                                                            "
STARTTXT  INIT    "Starting fixit (Option "
.
.
.
PRGNAM    INIT      "CONVERSION PRIHTRFD"
PRGID     INIT      "F11PRHTR"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
.
          CALL      OLOG0000                      * create/open log file
MAIN0500  CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000              * run c-isam fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9000
.
MAIN2000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN6000:               * yes
                          MAIN0500:               * no
                          MAIN9000                * cancel
.
.         Running c-isam fix
.
MAIN6000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9000
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/zip saved file
.
MAIN9000  CALL      CLOG0000                      * close log file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
          
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run c-isam fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
.
PREP0000  CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
.
          CALL      SLOG0000                      * write start to log file
.
          MOVE      "prihtraf",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat prihtraf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sprhtraf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat prihtraf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sprhtraf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "prihtraf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Unable to remove the original file.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "sprhtraf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            MOVE      "Unable to open saved original file.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "prihtraf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " 399 UC1-8+9-14+15-20+21-23+24-29,115-122,30-31,32-40,41-43,44-46",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.         Add Index 2
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " D30-31,32-40,41-43,47-54",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.         Add Index 3
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC1-8+9-14+15-20+21-23+24-29,115-122,44-46,30-31,32-40,41-43",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.         Add Index 4
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC1-8+9-14+15-20+21-23+24-29,115-122,30-31,32-40,41-43,47-54,44-46",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      PRIHTRA1,NEWFILE
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          GOTO      PREP9999
.
PREP5000  MOVE      "Old file not found.  ",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  MOVE      "Unable to copy original file.  (Error: ",ERORTEXT
          ENDSET    ERORTEXT
          APPEND    TASKID,ERORTEXT
          APPEND    ")  ",ERORTEXT
          RESET     ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
            MOVE      "File does not exist",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
              MOVE      "File has already been converted",ERORTEXT
              WRITE     LOGFILE,SEQ;ERORTEXT
            ELSE
              DISPLAY   "has caused an IO error - ";
              MOVE      "File has caused an IO Error",ERORTEXT
              WRITE     LOGFILE,SEQ;ERORTEXT
            ENDIF
          ENDIF
          MOVE      "File will be bypassed",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *+,ERORTEXT
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY54,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
.         Load new field variables
.
          MOVE      SP1,PRHTMPOV
          MOVE      SP1,PRHTDSOV
          PACK      PRHTSTXT,SP70,SP70
          MOVE      SP1,PRHTASPF
          MOVE      SP10,PRHTASPC
          MOVE      SP1,PRHTAICF
          MOVE      SP3,PRHTAICV
          MOVE      SP2,PRHTNMPT
          MOVE      SP6,PRHTLSPN
          MOVE      SP2,PRHTSDCD
          MOVE      SP8,PRHTLVIS
          MOVE      SP3,PRHTROVR
          MOVE      SP70,PRHTSPAR
.
          PACK      KEY54,PRHTUNIQ,PRHTPRAC,PRHTDOCT,PRHTPIND,PRHTREFD,PRHTRDAT,PRHTFLAG,PRHTITMN,PRHTSUBN,PRHTNUMB,SP70
          CALL      WRPRHT1                       * write to new file
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     PRIHTRA1                      * close new file
          CLOSE     OLDFILE1                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS:
                    *P1:24;
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
.
VALD0000  MOVE      ZERO,EXIT
.
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            MOVE      "Directory path must end with a slash (/) ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Directory does not exist ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or zip save file                                     *
.**********************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "sprhtraf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "gzip -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            MOVE      "Saved file not zipped or removed.  ",ERORTEXT
            WRITE     LOGFILE,SEQ;ERORTEXT
            DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
          DISPLAY   *P1:24,*EL,*B,"Finished processing  ",CDATE,SP1,CTIMEIS:
                    ".  ";
          CALL      HITENTER
.
          CALL      ELOG0000                     * write an end log entry
.
ENDP9999  RETURN
+
.**********************************************************************
.*        DEFT0000 -  Get the default directory                       *
.**********************************************************************
.
DEFT0000  EXECUTE   "ldat prihtraf.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  MOVE      "Error finding 'prihtraf'.  ",ERORTEXT
          WRITE     LOGFILE,SEQ;ERORTEXT
          DISPLAY   *P1:24,*EL,*B,*+,ERORTEXT;
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.*                            OLOG0000                                       *
.*                    Open the log file                                      *
.*****************************************************************************
.
OLOG0000  CALL      IBACLOCK
          REP       " 0",CDATE
          REP       " 0",CTIMEIS
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      LOGFILE,"f11prhtr.log"       * open log file
          TRAPCLR   IO
          IF        OVRCD = 1
            PREP      LOGFILE,"f11prhtr.log"     * create and open log file
            WRITE     LOGFILE,MINUS3;DASHLINE
            WRITE     LOGFILE,SEQ;CREATEXT,CDATE,SP1,CTIMEIS
          ELSE
            WRITE     LOGFILE,MINUS3;DASHLINE
            WRITE     LOGFILE,SEQ;OPENTEXT,CDATE,SP1,CTIMEIS
          ENDIF
.
OLOG9999  RETURN
+
.*****************************************************************************
.*                            CLOG0000                                       *
.*                    Close the log file                                     *
.*****************************************************************************
.
CLOG0000  CLOSE     LOGFILE
.
CLOG9999  RETURN
+
.*****************************************************************************
.*                            SLOG0000                                       *
.*                    Write a start log to the log file                      *
.*****************************************************************************
.
SLOG0000  WRITE     LOGFILE,SEQ;DASHLINE
          WRITE     LOGFILE,SEQ;STARTTXT,COPTION,RBRACK,SP1,CDATE,SP1:
                                   CTIMEIS
.
SLOG9999  RETURN
+
.*****************************************************************************
.*                            ELOG0000                                       *
.*                    Write an end log to the log file                       *
.*****************************************************************************
.
ELOG0000  WRITE     LOGFILE,SEQ;ENDGTEXT,COPTION,RBRACK,SP1,CDATE,SP1:
                                   CTIMEIS
ELOG9999  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
.         Old File I/O Routines
.
READSOLD  RESET     KEY54
          READ      OLDFILE1,KEY54;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;DPRHTUNQ,PRHTPRAC,PRHTDOCT,PRHTPIND,PRHTREFD:
                                  DPRHTFLG,PRHTITMN,PRHTSUBN,DPRHTNUM,PRHTDATE:
                                  PRHTTIME,PRHTSERN,PRHTIDES,PRHTIAMT,PRHTPFLG:
                                  PRHTS4B3,PRHTMEDA,PRHTRDAT,PRHTREFT,PRHTGSTA:
                                  PRHTGSTC,PRHTRFPD,PRHTREFN,PRHTENCN,PRHTHICI:
                                  PRHTACOI,PRHTTDUR,OLDSPARE
          GOTO      OVERCOND IF OVER
          MOVE      DPRHTUNQ,PRHTUNIQ
          MOVE      DPRHTFLG,PRHTFLAG
          MOVE      DPRHTNUM,PRHTNUMB
          RETURN
.
.         New File I/O Routines
.
RSPRHT1   RESET     KEY54
          READ      PRIHTRA1,KEY54;;
          RETURN
.
RKPRHT1   MOVE      ZERO,OVRCD
          READKS    PRIHTRA1;DPRHTUNQ,PRHTPRAC,PRHTDOCT,PRHTPIND,PRHTREFD:
                                  DPRHTFLG,PRHTITMN,PRHTSUBN,DPRHTNUM,PRHTDATE:
                                  PRHTTIME,PRHTSERN,PRHTIDES,PRHTIAMT,PRHTPFLG:
                                  PRHTS4B3,PRHTMEDA,PRHTRDAT,PRHTREFT,PRHTGSTA:
                                  PRHTGSTC,PRHTRFPD,PRHTREFN,PRHTENCN,PRHTHICI:
                                  PRHTACOI,PRHTTDUR,PRHTMPOV,PRHTDSOV,PRHTSTXT:
                                  PRHTASPF,PRHTASPC,PRHTAICF,PRHTAICV,PRHTNMPT:
                                  PRHTLSPN,PRHTSDCD,PRHTLVIS,PRHTROVR,PRHTSPAR
          GOTO      OVERCOND IF OVER
          MOVE      DPRHTUNQ,PRHTUNIQ
          MOVE      DPRHTFLG,PRHTFLAG
          MOVE      DPRHTNUM,PRHTNUMB
          RETURN
.
UPPRHT1   MOVE      PRHTUNIQ,DPRHTUNQ
          MOVE      PRHTFLAG,DPRHTFLG
          MOVE      PRHTNUMB,DPRHTNUM
          UPDATE    PRIHTRA1;DPRHTUNQ,PRHTPRAC,PRHTDOCT,PRHTPIND,PRHTREFD:
                             DPRHTFLG,PRHTITMN,PRHTSUBN,DPRHTNUM,PRHTDATE:
                             PRHTTIME,PRHTSERN,PRHTIDES,PRHTIAMT,PRHTPFLG:
                             PRHTS4B3,PRHTMEDA,PRHTRDAT,PRHTREFT,PRHTGSTA:
                             PRHTGSTC,PRHTRFPD,PRHTREFN,PRHTENCN,PRHTHICI:
                             PRHTACOI,PRHTTDUR,PRHTMPOV,PRHTDSOV,PRHTSTXT:
                             PRHTASPF,PRHTASPC,PRHTAICF,PRHTAICV,PRHTNMPT:
                             PRHTLSPN,PRHTSDCD,PRHTLVIS,PRHTROVR,PRHTSPAR
          RETURN
.
WRPRHT1   RESET     KEY54
          MOVE      PRHTUNIQ,DPRHTUNQ
          MOVE      PRHTFLAG,DPRHTFLG
          MOVE      PRHTNUMB,DPRHTNUM
          WRITE     PRIHTRA1,KEY54;DPRHTUNQ,PRHTPRAC,PRHTDOCT,PRHTPIND,PRHTREFD:
                                   DPRHTFLG,PRHTITMN,PRHTSUBN,DPRHTNUM,PRHTDATE:
                                   PRHTTIME,PRHTSERN,PRHTIDES,PRHTIAMT,PRHTPFLG:
                                   PRHTS4B3,PRHTMEDA,PRHTRDAT,PRHTREFT,PRHTGSTA:
                                   PRHTGSTC,PRHTRFPD,PRHTREFN,PRHTENCN,PRHTHICI:
                                   PRHTACOI,PRHTTDUR,PRHTMPOV,PRHTDSOV,PRHTSTXT:
                                   PRHTASPF,PRHTASPC,PRHTAICF,PRHTAICV,PRHTNMPT:
                                   PRHTLSPN,PRHTSDCD,PRHTLVIS,PRHTROVR,PRHTSPAR
          RETURN
.
          INC       STD001IO
