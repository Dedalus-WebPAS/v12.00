.***************************************************************************
.* System    :   Outpatients                                               *
.* Program   :   F98OTMA1                                                  *
.* Desc      :   Clear OTMACTRN and OTMASPAR                               *
.***************************************************************************
.* Date      :   06/10/2006                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will loop the entire outpatient master file. *
.*           :   There was garbage data in the spare variable. OTMACTRN    *
.*           :   was created from the spare. This fix will clear           *
.*           :   OTMACTRN and OTMASPAR                                     *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       OUTSITFD/INC
          INC       OUTMA1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
CFILEPRE  DIM       3
RECNUM    FORM      8
VSITE     DIM       6
.
PRGID     INIT      "F97OTMA1"
PRGNAM    INIT      "Fix clinic master spare"
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
          BRANCH    EXIT,ML9999                   * init failure
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          MOVE      ZERO,RECNUM
          MOVE      SP70,VSITE
          MOVE      "out",CFILEPRE
.
          MOVE      IDDD,VSITE          * Initialize the current site
          MATCH     SP6,VSITE           * Valid site ?
          GOTO      INIT9000 IF EQUAL   * No. Initialization failure
.
          DISPLAY   *P56:24,"Opening outsitaf";
          OPEN      OUTSITA1,"outsitaf" * Open the site file
.
          MOVE      VSITE,KEY6          * Key for the site file
          CALL      RDSITA1             * Read the site record
          BRANCH    OVRCD,INIT9000      * Invalid site.
.
          MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
.
          PACK      CFNAME,CFILEPRE,FILMA1A1
          CLOSE     OUTMA1A1
          CALL      OPOTMA11                * Open the file
.
          GOTO      INIT9999
.
.         Invalid call to program. Common not set up with correct site info
.
INIT9000  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "Incorrect call to program - Site information wrong.    ";
          CALL      HITENTER
          MOVE      ONE,EXIT                * Initialization failure
.
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  PACK      KEY18,SP70
          CALL      RDSMASA1
PROC1000  CALL      RDKMASA1
          BRANCH    OVRCD,PROC9000
.
          MATCH     "1",OTMACTRN
          IF        !@EQUAL
            MOVE      ZERO,OTMACTRN
          ENDIF
.
          MOVE      SP70,OTMASPAR
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          CALL      UPMASA1
.
          GOTO      PROC1000
.
PROC9000  CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER 
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       OUTSITIO/INC
          INC       OUTMA1IO/INC
