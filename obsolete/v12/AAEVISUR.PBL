.******************************************************************************
.* System         : Accident and Emergency                                    *
.*                                                                            *
.* Include        : AAEVISUR.PBL                                              *
.*                                                                            *
.* Author         : Justin 'The Pom' Coates      12/08/1993                   *
.*                                                                            *
.* Function       : To display all A&E visits for a U/R                       *
.*                                                                            *
.* Parameters     : PURNO         the U/R number                              *
.*                  VSURTYPE      type of patient to display                  *
.*                                0 = not discharged                          *
.*                                1 = discharged                              *
.*                                2 = any patients                            *
.*                                                                            *
.* Modifications  :                                                           *
.******************************************************************************
AAEVISUR  CALL      CLRAR000                * clear the array
          MOVE      ZERO,CNTITEM            * clear item counter
          MOVE      ONE,CPAGENO
          MOVE      SEVEN,CVERT
          DISPLAY   *P1:7,*EF,*V2LON,*ULON:
                    *P6:7,"   Date   ":
                    *P17:7,"Event No.":
                    *P27:7,"Complaint                             "
.
          IF        VSURTYPE <> ZERO
            DISPLAY   *P66:7,*V2LON,*ULON,"Discharged"
          ENDIF
          MOVE      SP20,TDESC
          REP       " z",TDESC
          PACK      KEY24,PURNO,TDESC
          CALL      RDSVISA2
.
. ******* loop over file *******
.
AEVSU100  CALL      RDPVISA2
          BRANCH    OVRCD,AEVSU500          * end of file
          MATCH     PURNO,PVIURNO
          GOTO      AEVSU500 IF NOT EQUAL   * diff. pat
          COMPARE   ONE,PVITYPE
          GOTO      AEVSU100 IF NOT EQUAL   * not an A&E visit
.
          MOVE      PVIBILL,KEY8
          CALL      RDDETA1
          BRANCH    OVRCD,AEVSU100          * invalid visit
.
          MOVE      SP8,ADSDATE             * clear discharge date
          MOVE      PVIBILL,KEY8
          CALL      RDDISA1                 * read discharge file
          COMPARE   THREE,VSURTYPE
          GOTO      AEVSU200 IF EQUAL       * display all patients
.
          IF        VSURTYPE = ZERO  & OVRCD = ZERO
            GOTO      AEVSU100                * already discharged
          ENDIF
          IF        VSURTYPE = ONE  & OVRCD = ONE
            GOTO      AEVSU100                * not discharged
          ENDIF
 
. ******* increment counters *******
.
AEVSU200  ADD       ONE,CVERT
          COMPARE   "23",CVERT
          GOTO      AEVSU400 IF NOT LESS    * need a new page
          ADD       ONE,CNTITEM             * add to item counter
.
. ******* display data and store key *******
.
AEVSU300  UNPACK    PVIDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      ADADIAG,KEY38
          DISPLAY   *P2:CVERT,*V2LON,CNTITEM,*HOFF,".  ":
                    *P6:CVERT,CPCDATE:
                    *P17:CVERT,PVIBILL:
                    *P27:CVERT,KEY38
          IF        VSURTYPE <> ZERO
            UNPACK    ADSDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            DISPLAY   *P66:CVERT,CPCDATE
          ENDIF
          PACK      KEY24,PVIURNO,PVIDATE,PVIBILL
          MOVE      KEY24,VISITS[CNTITEM]
          GOTO      AEVSU100
.
. ******* new screen needed *******
.
AEVSU400  BRANCH    CPAGENO,AEVSU450
.
.         Middle pages : (N)ext & (P)revious
.
AEVSU410  MOVE      ONE,CALLPOSN            * set position
          CALL      KVSUR0000
          BRANCH    EXIT,AEVSU700,AAEVISUR,AEVSU910
.                        Next     First    Exit
          GOTO      AEVSU600
.
.         first page : (N)ext
.
AEVSU450  MOVE      TWO,CALLPOSN
          CALL      KVSUR0000
          BRANCH    EXIT,AEVSU700,AEVSU590,AEVSU910
.                        Next     First    Exit
          GOTO      AEVSU600
.
. ******* no more data to display *******
.
AEVSU500  COMPARE   ZERO,CNTITEM
          GOTO      AEVSU890 IF EQUAL       * no visits
.
          BRANCH    CPAGENO,AEVSU550
.
.         last page : (P)revious
.
AEVSU510  MOVE      THREE,CALLPOSN
          CALL      KVSUR0000
          BRANCH    EXIT,AEVSU590,AAEVISUR,AEVSU910
.                        Next     First    Exit
          GOTO      AEVSU600
.
.         first page :
.
AEVSU550  MOVE      FOUR,CALLPOSN
          CALL      KVSUR0000
          BRANCH    EXIT,AEVSU590,AEVSU590,AEVSU910
.                        Next     First    Exit
          GOTO      AEVSU600
.
AEVSU590  BEEP   
AEVSU591  BRANCH    CALLPOSN,AEVSU410,AEVSU450,AEVSU510,AEVSU550
.
. ******* item on screen selected *******
.
AEVSU600  MOVE      VISITS[FORM2],KEY24
          CALL      RDVISA2
          BRANCH    OVRCD,AEVSU590          * invalid
.
          MOVE      PVIBILL,ADANUMB         * set number
          CALL      VALD0000                * validate the patient
          BRANCH    EXIT,AEVSU591           * invalid
.
          MOVE      ZERO,EXIT
          GOTO      AEVSU999
.
. ******* set up for a new page *******
.
AEVSU700  MOVE      VISITS[15],KEY24
          CALL      RDVISA2
          CALL      CLRAR000                * clear storage variables
          ADD       ONE,CPAGENO             * add to page counter
          MOVE      ZERO,CNTITEM             * reset item counter
          MOVE      "7",CVERT               * set row
          DISPLAY   *P1:8,*EF           * clear screen
          GOTO      AEVSU100
.
AEVSU890  DISPLAY   *P1:24,*B,"No visits for this patient.  ",*EL;
          CALL      HITENTER
.
. ******* set exit flags *******
.
AEVSU910  MOVE      ONE,EXIT                * exit selected
.
AEVSU999  RETURN
+
.*********************************************************************
.*                  KVSUR0000                    Called by :          *
.*        Keyin response for line 24 question                        *
.*        Para's  : CALLPOSN      which line 24 prompt               *
.*        Returns : EXIT = 0      item selected (FORM2)              *
.*                  EXIT = 1      Next screen selected               *
.*                  EXIT = 2      Previous screen selected           *
.*                  EXIT = 3      Nothing enterd                     *
.*********************************************************************
KVSUR000  DISPLAY   *P1:24,*EL,"Select Item, (":
                    *V2LON,ANSE,*HOFF,")xit";
          MOVE      "19",CCOL
          IF        CALLPOSN = ONE | CALLPOSN = TWO
            DISPLAY   ", (",*V2LON,"N",*HOFF,")ext";
            ADD       "8",CCOL
          ENDIF
          IF        CALLPOSN = ONE | CALLPOSN = THREE
            DISPLAY   ", (",*V2LON,"F",*HOFF,")irst";
            ADD       "9",CCOL
          ENDIF
          DISPLAY   " : ",*EL;
          ADD       "4",CCOL
.
KVSUR100  KEYIN     *PCCOL:24,*V2LON,*JR,DIM2:
                    *PCCOL:24,*DV,DIM2
.
          RESET     DIM2
          GOTO      KVSUR430 IF EOS         * nothing entered
.
          TYPE      DIM2
          GOTO      KVSUR500 IF EQUAL       * number entered
.
.         letter entered
.
          MATCH     SP1,DIM2
          GOTO      KVSUR150 IF NOT EQUAL   * invalid entry
.
          UNPACK    DIM2,ANS,ANS            * get required letter in ANS
          REP       UPPLOW,ANS              * get as uppercase
          REP       "N1F2E3",ANS            * turn valid letters into number
          MOVE      ZERO,EXIT               * clear exit flag
          MOVE      ANS,EXIT                * set exit flag
          BRANCH    EXIT,KVSUR999,KVSUR999,KVSUR999
.
KVSUR150  BEEP
          GOTO      KVSUR100
.
KVSUR430  MOVE      THREE,EXIT              * nothing entered
          GOTO      KVSUR999
.
.         number entered
.
KVSUR500  MOVE      ZERO,EXIT               * set exit flag
          MOVE      DIM2,FORM2              * get as a number
.
          COMPARE   FORM2,ZERO
          GOTO      KVSUR150 IF NOT LESS    * negative entered
.
          COMPARE   FORM2,CNTITEM
          GOTO      KVSUR150 IF LESS        * number too large
.
KVSUR999  RETURN
+
.*********************************************************************
.         Clear the storage variables
.*********************************************************************
CLRAR000  MOVE      TEN5,FORM2
          WHILE     FORM2
            MOVE      SP30,VISITS[FORM2]
            SUB       ONE,FORM2
          DO
CLRAR999  RETURN
+
