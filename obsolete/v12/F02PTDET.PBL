.*****************************************************************************
.*    Program      : F02PTDET                                                *
.*    Description  : Conversion patdetaf to new File layout                  *
.*                                                                           *
.*    Author       : Steve Armstrong                                         *
.*    Date         : 14/06/2012                                              *
.*    Modifications:                                                         *
.*****************************************************************************
.
          INC       STD001FD
.
          INC       PATDETFD/INC
          INC       PATINVFD/INC
.
.
. LOCAL VARIABLES
. ---------------
RECNUM    FORM      8
.
.
PRGNAM    INIT      "CONVERSION PATDETFD"
PRGID     INIT      "F02PTDET"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000              * run fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN0000:               * no
                          MAIN9999                * cancel
.
.         Running field fix
.
MAIN5000  CALL      FFIX0000                      * run field fix
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening ":
                    *P64:24,"patdetaf";
          OPEN      PATDETA1,"patdetaf"
.
          DISPLAY   *P64:24,"patinvrf";
          OPEN      PATINVR1,"patinvrf"
.         
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                            FFIX0000                                       *
.*            Fix the new field by getting the U/R from the invoice file     *
.*****************************************************************************
.
FFIX0000  DISPLAY   *P1:20,*EL,"Record No. :";
          MOVE      ZERO,RECNUM                   * initialise record count
.
          MOVE      SP10,KEY9
          CALL      RSPTDET1                      * position at start of file
FFIX0500  CALL      RKPTDET1                      * read next record
          BRANCH    OVRCD,FFIX9000                * eof - finished
.
          ADD       ONE,RECNUM                    * increment record counter
.         
          IF        (RECNUM%1000) = 0 | RECNUM = 1
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MOVE      PTDEINVN,KEY8
          CALL      RDPTINV1                     * invoice on file ?
          BRANCH    OVRCD,FFIX0500               * no - get next record
.
          MOVE      PINVUR,PTDEURNO              * load U/R number
.
          CALL      UPPTDET1                     * update record
          GOTO      FFIX0500                     * get next record
.         
FFIX9000  CLOSE     PATDETA1
          CLOSE     PATINVR1
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:24,*EL,"Update completed.  ";
          CALL      HITENTER
.
FFIX9999  RETURN
+
          INC       STD001IO
.
          INC       PATDETIO/INC
          INC       PATINVIO/INC
