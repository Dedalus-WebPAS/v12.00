.*****************************************************************************
.*    Program      : F04WTTR1                                                *
.*    Description  : Conversion of wattr1af to new file layout               *
.*                                                                           *
.*    Author       : Ania Pietrusewicz CAR 294376                            *
.*    Date         : 08/01/2014                                              *
.*    Modifications:                                                         *
.*                                                                           *
.*        V10.04.01 19/02/2014  Steve Armstrong  CAR 294167                  *
.*                  Updated fixit to extend WMREASON from DIM 40 to DIM 80.  *
.*                  Also removed reference to Oracle fix option as it is not *
.*                  required.                                                *
.*****************************************************************************
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
.
. Old File Definition
.
OLDFILE1  IFILE     SQL, FIXED=570, KEY="U1-8,9-17,18-19"
.
.NAME     TYPE      LENGTH  PHYS   START  DESCRIPTION
.----     ----      ------  ----   -----  -----------
.WMURNO    DIM       8       8         1   U/R Number
.WMCODE    DIM       9       9         9   Procedure Code
.DWMCNT    DIM       2       2        18   Procedure Count
.WMTIME    FORM      5       4        20   Procedure Time (Minutes)
.WMDESC    DIM       80     80        24   Procedure Description
OLDREASN   DIM       40     40       104   Reason/Diagnosis for Procedure
.DWMSTAT   DIM       1       1       144   Procedure Status
.WMDATE1   DIM       8       8       145   Date on List (ccyymmdd)
.WMDATE2   DIM       8       8       153   Date of Last Review (ccyymmdd) /
.WMDATE3   DIM       8       8       161   Scheduled Admit. Date (ccyymmdd)
.WMDATE4   DIM       8       8       169   Removal Date (ccyymmdd)
.WMREMOVE  DIM       3       3       177   Removal Reason (Category WR/BC)
.WMUNIT    DIM       3       3       180   Unit/Clinic (Category WU)
.WMDOCTOR  DIM       6       6       183   Attending Doctor
.WMSTAY    FORM      5       4       189   Estimated Stay (Days)
.WMPTY     DIM       3       3       193   Priority (Category TP)
.WMNOTICE  DIM       3       3       196   Short Notice (Category WS)
.WMPTYPE   DIM       3       3       199   Resident Status (Category T)
.WMPCAT    DIM       3       3       202   Admission Type (Cat A)
.WMUDEF1   DIM       3       3       205   User Defined 1 (Category W1)
.WMUDEF2   DIM       3       3       208   User Defined 2 (Category W2)
.WMUDEF3   DIM       3       3       211   User Defined 3 (Category W3)
.WMUDEF4   DIM       3       3       214   User Defined 4 (Category W4)
.WMBOOK    FORM      8       5       217   Booking Number
.WMKEYDT   DIM       8       8       222   Keyin Date For Procedure
.WTWMPORT  DIM       2       2       230   Locking Indicator
.WTWMREAD  DIM       8       8       232   Re-Assessment Date (ccyymmdd)
.WTWMDEDA  DIM       8       8       240   Deferred or Staged Adm Date
.WTWMUNIQ  FORM      5       4       248   Unique Identifier
.WTWMSENT  FORM      1       2       252   Procedure Status 4 & 5 records
.WTWMCLSS  DIM       3       3       254   Patient Cat/Adm Class (Cat P)
.WTWMDTAD  DIM       8       8       257   Decision to Admit Date 
.WTWMACAT  DIM       3       3       265   Administrative Category  "CL"
.WTWMGADD  DIM       8       8       268   Guaranteed Admission Date
.WTWMDABD  DIM       8       8       276   Do Not Admit Before Date/Proposed
.WTWMEATY  DIM       3       3       284   Elective Admission Type  "CC"
.WTWMADMC  DIM       3       3       287   Admission Category   "WA"
.WTWMECRA  DIM       20      20      290   ECR Approval Number
.WTWMGPFA  DIM       20      20      310   GPFH Approval Number
.WTWMRFGP  DIM       8       8       330   Referring GP
.WTWMGPPC  DIM       6       6       338   GP Practice code
.WTWMNAFR  DIM       8       8       344   Not available from date
.WTWMNATO  DIM       8       8       352   Not available to date
.WTWMGPPT  DIM       2       2       360   GP Practice count
.WTWMDOFR  DIM       3       3       362   District of Residence (Cat DA)
.WTWMOPER  DIM       4       4       365   Operator who added proc to W/L
.WTWMIHSP  DIM       3       3       369   Intended Hospital (pathspaf)  
.WTWMVLSF  DIM       1       1       372   Validation Letter Sent Flag (Y/N)
.WTWMPRO2  DIM       9       9       373   Procedure Code 2
.WTWMPRO3  DIM       9       9       382   Procedure Code 3
.WTWMTRPA  DIM       8       8       391   Pre-Anaesthetic Date (CCYYMMDD)
.WMUDEF5   DIM       3       3       399   User Defined 5 (Category X5)
.WMUDEF6   DIM       3       3       402   User Defined 6 (Category X6)
.WMUDEF7   DIM       3       3       405   User Defined 7 (Category X7)
.WMUDEF8   DIM       3       3       408   User Defined 8 (Category X8)
.WTWMRANK  DIM       6       6       411   Patient Rank
.WTWMSRCR  DIM       3       3       417   Source of Referral (S)
.WTWMDRSA  DIM       8       8       420   Date of ref for 1st spec. assessment
.WTWMDOSA  DIM       8       8       428   Date of 1st specialised assessment
.WTWMPHSP  DIM       3       3       436   Principal Health serv Purchaser Cat HP
.WTWMBSCD  DIM       2       2       439   Booking Status Code
.WTWMRSFL  DIM       1       1       441   Record sent flag (Y or N)
.WTWMECNT  DIM       9       9       442   ESIS unique count eg. CCYY?????
.WTWMCSST  DIM       1       1       451   Consent Obtained (N=NO,Y=YES)
.WTWMCSDT  DIM       8       8       452   Consent Changed Date (ccyymmdd)
.WTWMASSR  DIM       10      10      460   Assessor Code (pmshcpaf)         
.WTWMACPR  DIM       8       8       470   Actual Procedure Event Number
OLDSPARE  DIM       92      92      478   Spare
.End of Record                      570
.
. New File Definition
.
WATTR1A1  IFILE    SQL, FIXED=648, KEY="U1-8,9-17,18-19"
.
.NAME     TYPE      LENGTH  PHYS   START  DESCRIPTION
.----     ----      ------  ----   -----  -----------
WMURNO    DIM       8       8         1   U/R Number
WMCODE    DIM       9       9         9   Procedure Code
DWMCNT    DIM       2       2        18   Procedure Count
WMTIME    FORM      5       4        20   Procedure Time (Minutes)
WMDESC    DIM       80     80        24   Procedure Description
WMREASON  DIM       80     80       104   Reason/Diagnosis for Procedure
DWMSTAT   DIM       1       1       184   Procedure Status
.                                           1 - Unscheduled
.                                           2 - Scheduled
.                                           3 - Pre-Admitted
.                                           4 - Admitted
.                                           5 - Discharged
.                                           6 - Removed from Waiting List
.                                           7 - Performed
.                                           8 - Not Performed
.                                           9 - Cancelled Admission
WMDATE1   DIM       8       8       185   Date on List (ccyymmdd)
WMDATE2   DIM       8       8       193   Date of Last Review (ccyymmdd) /
.                                         Final Outcome of Offer
WMDATE3   DIM       8       8       201   Scheduled Admit. Date (ccyymmdd)
WMDATE4   DIM       8       8       209   Removal Date (ccyymmdd)
WMREMOVE  DIM       3       3       217   Removal Reason (Category WR/BC)
WMUNIT    DIM       3       3       220   Unit/Clinic (Category WU)
WMDOCTOR  DIM       6       6       223   Attending Doctor
WMSTAY    FORM      5       4       229   Estimated Stay (Days)
WMPTY     DIM       3       3       233   Priority (Category TP)
WMNOTICE  DIM       3       3       236   Short Notice (Category WS)
WMPTYPE   DIM       3       3       239   Resident Status (Category T)
WMPCAT    DIM       3       3       242   Admission Type (Cat A)
WMUDEF1   DIM       3       3       245   User Defined 1 (Category W1)
WMUDEF2   DIM       3       3       248   User Defined 2 (Category W2)
WMUDEF3   DIM       3       3       251   User Defined 3 (Category W3)
WMUDEF4   DIM       3       3       254   User Defined 4 (Category W4)
.
WMBOOK    FORM      8       5       257   Booking Number
WMKEYDT   DIM       8       8       262   Keyin Date For Procedure
.                                                 (ccyymmdd)
WTWMPORT  DIM       2       2       270   Locking Indicator
WTWMREAD  DIM       8       8       272   Re-Assessment Date (ccyymmdd)
WTWMDEDA  DIM       8       8       280   Deferred or Staged Adm Date
.                                               (ccyymmdd)
. The following two fields are used for Transmission to the SAHC
.
WTWMUNIQ  FORM      5       4       288   Unique Identifier
WTWMSENT  FORM      1       2       292   Procedure Status 4 & 5 records
.                                         already sent = 1, otherwise = 0
WTWMCLSS  DIM       3       3       294   Patient Cat/Adm Class (Cat P)
.
WTWMDTAD  DIM       8       8       297   Decision to Admit Date
WTWMACAT  DIM       3       3       305   Administrative Category  "CL"
WTWMGADD  DIM       8       8       308   Guaranteed Admission Date
WTWMDABD  DIM       8       8       316   Do Not Admit Before Date/Proposed
.                                         Operation Date
WTWMEATY  DIM       3       3       324   Elective Admission Type  "CC"
WTWMADMC  DIM       3       3       327   Admission Category   "WA"
WTWMECRA  DIM       20      20      330   ECR Approval Number
WTWMGPFA  DIM       20      20      350   GPFH Approval Number
WTWMRFGP  DIM       8       8       370   Referring GP
WTWMGPPC  DIM       6       6       378   GP Practice code
WTWMNAFR  DIM       8       8       384   Not available from date
WTWMNATO  DIM       8       8       392   Not available to date
WTWMGPPT  DIM       2       2       400   GP Practice count
WTWMDOFR  DIM       3       3       402   District of Residence (Cat DA)
WTWMOPER  DIM       4       4       405   Operator who added proc to W/L
WTWMIHSP  DIM       3       3       409   Intended Hospital (pathspaf)
WTWMVLSF  DIM       1       1       412   Validation Letter Sent Flag (Y/N)
WTWMPRO2  DIM       9       9       413   Procedure Code 2
WTWMPRO3  DIM       9       9       422   Procedure Code 3
WTWMTRPA  DIM       8       8       431   Pre-Anaesthetic Date (CCYYMMDD)
WMUDEF5   DIM       3       3       439   User Defined 5 (Category X5)
WMUDEF6   DIM       3       3       442   User Defined 6 (Category X6)
WMUDEF7   DIM       3       3       445   User Defined 7 (Category X7)
WMUDEF8   DIM       3       3       448   User Defined 8 (Category X8)
WTWMRANK  DIM       6       6       451   Patient Rank
WTWMSRCR  DIM       3       3       457   Source of Referral (S)
WTWMDRSA  DIM       8       8       460   Date of ref for 1st spec. assessment
WTWMDOSA  DIM       8       8       468   Date of 1st specialised assessment
WTWMPHSP  DIM       3       3       476   Principal Health serv Purchaser Cat HP
WTWMBSCD  DIM       2       2       479   Booking Status Code
WTWMRSFL  DIM       1       1       481   Record sent flag (Y or N)
WTWMECNT  DIM       9       9       482   ESIS unique count eg. CCYY?????
WTWMCSST  DIM       1       1       491   Consent Obtained (N=NO,Y=YES)
WTWMCSDT  DIM       8       8       492   Consent Changed Date (ccyymmdd)
WTWMASSR  DIM       10      10      500   Assessor Code (pmshcpaf)
WTWMACPR  DIM       8       8       510   Actual Procedure Event Number
WTWMPDSC  DIM       80      80      518   Procedure Description Comments
WTWMSPAR  DIM       50      50      598   Spare
.End of Record                      648
.
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       200
.
DEFPATH   DIM       60
DIM60     DIM       60

DISNUM    FORM      4
.
INPFILE   DIM       8
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
SAVEFLG   FORM      1
.
.
. PROGRAM CONSTANTS
. -----------------
SP60      INIT    "                                                            "
.
.
.
PRGNAM    INIT      "CONVERSION WATTR1FD"
PRGID     INIT      "F04WTTR1"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000              * run c-isam fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
MAIN2000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN0000:               * no
                          MAIN9999                * cancel
.
MAIN5000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/zip saved file
          GOTO      MAIN9999
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
          
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run c-isam fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
.
PREP0000  MOVE      "wattr1af",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat wattr1af.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "swttr1af.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat wattr1af.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "swttr1af.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "wattr1af",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "swttr1af",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "wattr1af",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
          APPEND    " 648 UC1-8,9-17,18-19",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC184-184,1-8,9-17,18-19",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC185-192,1-8,9-17,18-19",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC1-8,185-192,9-17,18-19",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC184-184,223-228,220-222,233-235,1-8,9-17,18-19",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC297-304,184-184,1-8,9-17,18-19",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC184-184,220-222,185-192,1-8,9-17,18-19",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC481-481,1-8,9-17,18-19",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      WATTR1A1,NEWFILE
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
            ELSE
              DISPLAY   "has caused an IO error - ";
            ENDIF
          ENDIF
          DISPLAY   "File will be bypassed"
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY19,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          PACK      WMREASON,OLDREASN,SP70,SP70
          CALL      WRTREA1                       * write to new file
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     WATTR1A1                      * close new file
          CLOSE     OLDFILE1                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS:
                    *P1:24;
          CALL      HITENTER
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
.
VALD0000  MOVE      ZERO,EXIT
.
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or zip save file                                     *
.**********************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "swttr1af*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "gzip ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not zipped or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:24,*EL,*B,"Finished processing  ",CDATE,SP1,CTIMEIS:
                    ".  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.*        DEFT0000 -  Get the default directory                       *
.**********************************************************************
.
DEFT0000  EXECUTE   "ldat wattr1af.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding 'wattr1af'.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
READSOLD  RESET     KEY19
          READ      OLDFILE1,KEY19;;
          RETURN  
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;WMURNO,WMCODE,DWMCNT,WMTIME,WMDESC:
                             OLDREASN,DWMSTAT,WMDATE1,WMDATE2,WMDATE3:
                             WMDATE4,WMREMOVE,WMUNIT,WMDOCTOR,WMSTAY:
                             WMPTY,WMNOTICE,WMPTYPE,WMPCAT:
                             WMUDEF1,WMUDEF2,WMUDEF3,WMUDEF4:
                             WMBOOK,WMKEYDT,WTWMPORT,WTWMREAD:
                             WTWMDEDA,WTWMUNIQ,WTWMSENT,WTWMCLSS:
                             WTWMDTAD,WTWMACAT,WTWMGADD,WTWMDABD:
                             WTWMEATY,WTWMADMC,WTWMECRA,WTWMGPFA:
                             WTWMRFGP,WTWMGPPC,WTWMNAFR,WTWMNATO:
                             WTWMGPPT,WTWMDOFR,WTWMOPER,WTWMIHSP:
                             WTWMVLSF,WTWMPRO2,WTWMPRO3,WTWMTRPA:
                             WMUDEF5,WMUDEF6,WMUDEF7,WMUDEF8:
                             WTWMRANK,WTWMSRCR,WTWMDRSA,WTWMDOSA:
                             WTWMPHSP,WTWMBSCD,WTWMRSFL,WTWMECNT:
                             WTWMCSST,WTWMCSDT,WTWMASSR,WTWMACPR:
                             OLDSPARE
          GOTO      OVERCOND IF OVER
          UNPACK    OLDSPARE,WTWMPDSC
          RETURN  
.
.
WRTREA1   RESET     KEY19
          WRITE     WATTR1A1,KEY19;WMURNO,WMCODE,DWMCNT,WMTIME,WMDESC:
                                   WMREASON,DWMSTAT,WMDATE1,WMDATE2,WMDATE3:
                                   WMDATE4,WMREMOVE,WMUNIT,WMDOCTOR,WMSTAY:
                                   WMPTY,WMNOTICE,WMPTYPE,WMPCAT:
                                   WMUDEF1,WMUDEF2,WMUDEF3,WMUDEF4:
                                   WMBOOK,WMKEYDT,WTWMPORT,WTWMREAD:
                                   WTWMDEDA,WTWMUNIQ,WTWMSENT,WTWMCLSS:
                                   WTWMDTAD,WTWMACAT,WTWMGADD,WTWMDABD:
                                   WTWMEATY,WTWMADMC,WTWMECRA,WTWMGPFA:
                                   WTWMRFGP,WTWMGPPC,WTWMNAFR,WTWMNATO:
                                   WTWMGPPT,WTWMDOFR,WTWMOPER,WTWMIHSP:
                                   WTWMVLSF,WTWMPRO2,WTWMPRO3,WTWMTRPA:
                                   WMUDEF5,WMUDEF6,WMUDEF7,WMUDEF8:
                                   WTWMRANK,WTWMSRCR,WTWMDRSA,WTWMDOSA:
                                   WTWMPHSP,WTWMBSCD,WTWMRSFL,WTWMECNT:
                                   WTWMCSST,WTWMCSDT,WTWMASSR,WTWMACPR:
                                   WTWMPDSC,WTWMSPAR
          RETURN
.
.
          INC       STD001IO
