.******************************************************************************
.*  Include Name  : PATINSKY.PBL                                              *
.*  Description   : Keyin of allowance codes                                  *
.*  Author        : Matt Surridge                                             *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF6 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMODE - is keyin mode      (1=Insert 0=other)          *
.*                                                                            *
.*  Returns       : ICODE    - Keyed in code                                  *
.*                  INAME    - Description                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.******************************************************************************
PATINSKY
PTISKY00  MATCH     SP6,CKYIDEF6                  * using a default ?
          GOTO      PTISKY10 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN6:      * no default used
                    *PECOL:EVERT,*V2LON,ICODE:
                    *PECOL:EVERT,*DV,ICODE
          GOTO      PTISKY20
.
PTISKY10  MOVE      CKYIDEF6,ICODE                 * default
          KEYIN     *PECOL:EVERT,*DV,ICODE:     * default used
                    *PECOL:EVERT,*V2LON,*RV,ICODE:
                    *PECOL:EVERT,*DV,ICODE
.
.         check what was entered
.
PTISKY20  CALL      INSCK000
          BRANCH    EXIT,PTISKY25,PTISKY31,PTISKY41,PTISKY95
.                        "?"      valid    nothing  exitchar
          GOTO      PTISKY00
.
.         a "?" was input
.
PTISKY25  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
PTISKY26  CALL      PATINSDS
          BRANCH    CNEWDS,PTISKY27 
.
          CALL      PUTSCR00                * redisplay screen
          GOTO      PTISKY00                * rekey code
.
.         now ask for code while leaving displayed codes on screen
.
PTISKY27  MATCH     SP6,CKYIDEF6                  * using a default
          GOTO      PTISKY28 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,"Insurance Code : ":
                    *DV,UNDLN6:      * no default used
                    *P18:24,*V2LON,ICODE:
                    *P18:24,*DV,ICODE
          GOTO      PTISKY29
.
PTISKY28  MOVE      CKYIDEF6,ICODE
          KEYIN     *P1:24,*EL,*DV,"Insurance Code : ":
                    *P18:24,*DV,ICODE:     * default used
                    *P18:24,*V2LON,*RV,ICODE:
                    *P18:24,*DV,ICODE
.
.         repeat the validation of the code
.         check what was entered
.
PTISKY29  CALL      INSCK000
          BRANCH    EXIT,PTISKY26,PTISKY30,PTISKY40,PTISKY90
.                        "?"      valid    nothing  exitchar
.
          GOTO      PTISKY27
.
.         a valid code was entered 
. 
PTISKY30  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,ICODE
.
PTISKY31  MOVE      FALSE,EXIT                    * valid input
          GOTO      PTISKY99 
.
.         nothing was input
.
PTISKY40  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,ICODE
.
PTISKY41  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      PTISKY99 
.
.         EXITCHAR input
.
PTISKY90  CALL      FRESCR00                * free the screen saved
.
PTISKY95  MOVE      TWO,EXIT                * EXITCHAR
.
PTISKY99  RETURN
+
.*********************************************************************
.*                  INSCK000                    Called by : PATCODKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
INSCK000  ENDSET    ICODE
          APPEND    SP6,ICODE
          RESET     ICODE
.
          MATCH     EXITCHAR,ICODE
          GOTO      INSCK600 IF EQUAL       * exit char
.
          MATCH     SP6,ICODE
          GOTO      INSCK700 IF EQUAL       * nothing
.
          MATCH     QUEST,ICODE
          GOTO      INSCK800 IF EQUAL       * ? mark
.
.         a code was entered so validate
. 
INSCK500  PACK      KEY6,ICODE,SP10
          CALL      RDINSR1
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,INSCK900          * invalid code
          ELSE
            COMPARE   ZERO,OVRCD
            GOTO      INSCK900 IF EQUAL
          ENDIF
.
          MOVE      TWO,EXIT                * valid code
          GOTO      INSCK999
.
.         exitchar entered
.
INSCK600  MOVE      FOUR,EXIT
          MOVE      SP6,ICODE
          MOVE      SP20,INAME              * clear desc field
          GOTO      INSCK999
.
.         nothing entered
.
INSCK700  BRANCH    CKYIMAND,INSCK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP6,ICODE
          MOVE      SP20,INAME              * clear desc field
          GOTO      INSCK999
.
.         "?" entered
.
INSCK800  MOVE      ONE,EXIT
          GOTO      INSCK999
.
.         invalid code entered
.
INSCK900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
.
          IF        CKYIMODE = 0
            DISPLAY   *P1:24,*EL,*B,"Invalid Insurance Code.  ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      INSCK999
.
.         field is mandatory
.
INSCK950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is mandatory.  ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
INSCK999  RETURN
+
          INC       PATINSDS
.
+
