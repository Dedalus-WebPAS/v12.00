.******************************************************************************
.*  Include Name  : PATMESKY.PBL                                              *
.*  Description   : Keyin of message code                                     *
.*  Author        : Ian Rutt                                                  *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF3 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMODE - is keyin mode                                  *
.*                                                                            *
.*  Returns       : PMCODE    - Keyed in code                                  *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.******************************************************************************
PATMESKY
PTMSKY00  MATCH     SP3,CKYIDEF3                  * using a default ?
          GOTO      PTMSKY10 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN3:      * no default used
                    *PECOL:EVERT,*V2LON,PMCODE:
                    *PECOL:EVERT,*DV,PMCODE
          GOTO      PTMSKY20
.
PTMSKY10  MOVE      CKYIDEF3,PMCODE                 * default
          KEYIN     *PECOL:EVERT,*DV,PMCODE:     * default used
                    *PECOL:EVERT,*V2LON,*RV,PMCODE:
                    *PECOL:EVERT,*DV,PMCODE
.
.         check what was entered
.
PTMSKY20  CALL      MESCK000
          BRANCH    EXIT,PTMSKY25,PTMSKY31,PTMSKY41,PTMSKY95
.                        "?"      valid    nothing  exitchar
          GOTO      PTMSKY00
.
.         a "?" was input
.
PTMSKY25  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
PTMSKY26  CALL      PATMESDS
          BRANCH    CNEWDS,PTMSKY27 
.
          CALL      PUTSCR00                * redisplay screen
          GOTO      PTMSKY00                * rekey code
.
.         now ask for code while leaving displayed codes on screen
.
PTMSKY27  MATCH     SP3,CKYIDEF3                  * using a default
          GOTO      PTMSKY28 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Message Code : ":
                    *P16:24,*DV,UNDLN3:      * no default used
                    *P16:24,*V2LON,PMCODE:
                    *P16:24,*DV,PMCODE
          GOTO      PTMSKY29
.
PTMSKY28  MOVE      CKYIDEF3,PMCODE
          KEYIN     *P1:24,*EL,*DV,"Message Code : ":
                    *P16:24,*DV,PMCODE:     * default used
                    *P16:24,*V2LON,*RV,PMCODE:
                    *P16:24,*DV,PMCODE
.
.         repeat the validation of the code
.         check what was entered
.
PTMSKY29  CALL      MESCK000
          BRANCH    EXIT,PTMSKY26,PTMSKY30,PTMSKY40,PTMSKY90
.                        "?"      valid    nothing  exitchar
.
          GOTO      PTMSKY27
.
.         a valid code was entered 
. 
PTMSKY30  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,PMCODE
.
PTMSKY31  MOVE      FALSE,EXIT                    * valid input
          GOTO      PTMSKY99 
.
.         nothing was input
.
PTMSKY40  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,PMCODE
.
PTMSKY41  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      PTMSKY99 
.
.         EXITCHAR input
.
PTMSKY90  CALL      FRESCR00                * free the screen saved
.
PTMSKY95  MOVE      TWO,EXIT                * EXITCHAR
.
PTMSKY99  RETURN
+
.*********************************************************************
.*                  MESCK000                    Called by : PATMESKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
MESCK000  ENDSET    PMCODE
          APPEND    SP3,PMCODE
          RESET     PMCODE
.
          MATCH     EXITCHAR,PMCODE
          GOTO      MESCK600 IF EQUAL       * exit char
.
          MATCH     SP3,PMCODE
          GOTO      MESCK700 IF EQUAL       * nothing
.
          MATCH     QUEST,PMCODE
          GOTO      MESCK800 IF EQUAL       * ? mark
.
.         a code was entered so validate
. 
MESCK500  PACK      KEY3,PMCODE,SP3
          CALL      RDMESG1
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,MESCK900          * invalid code
          ELSE
            COMPARE   ZERO,OVRCD   
            GOTO      MESCK900 IF EQUAL
          ENDIF  
.
          MOVE      TWO,EXIT                * valid code
          GOTO      MESCK999
.
.         exitchar entered
.
MESCK600  MOVE      FOUR,EXIT
          MOVE      SP3,PMCODE
          GOTO      MESCK999
.
.         nothing entered
.
MESCK700  BRANCH    CKYIMAND,MESCK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP3,PMCODE
          GOTO      MESCK999
.
.         "?" entered
.
MESCK800  MOVE      ONE,EXIT
          GOTO      MESCK999
.
.         invalid code entered
.
MESCK900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
.
          IF        CKYIMODE = 0
            DISPLAY   *P1:24,*EL,*B,"Invalid Message Code. ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      MESCK999
.
.         field is mandatory
.
MESCK950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is Mandatory. ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
MESCK999  RETURN
.
          INCLUDE   PATMESDS
+
