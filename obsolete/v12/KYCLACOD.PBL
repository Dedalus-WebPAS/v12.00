.******************************************************************************
.* System         : Clinical Audits                                           *
.*                                                                            *
.* Include Name   : KYCLACOD.PBL                                              *
.*                                                                            *
.* Description    : Keyin of a Codes file entry from CLACODFD                 *
.*                                                                            *
.* Author         : Justin Coates           06/05/1992                        *
.*                                                                            *
.* Parameters     : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF3 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CODE     - the category for the code                      *
.*                                                                            *
.* Returns        : CACDCODE - Keyed in code                                  *
.*                  CACDDESC - Description                                    *
.*                  EXIT = 0   Valid code entered                             *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.* On return      : You must display the description yourself.                *
.*                                                                            *
.******************************************************************************
KYCLACOD  MOVE      ONE,FORM1               * set ? as not entered
          MOVE      ECOL,CCOL
          MOVE      EVERT,CVERT
.
KCACD100  MATCH     SP3,CKYIDEF3
          GOTO      KCACD120 IF EQUAL       * no default
.
          MOVE      CKYIDEF3,CACDCODE
          KEYIN     *PCCOL:CVERT,*DV,CACDCODE:
                    *PCCOL:CVERT,*V2LON,*RV,CACDCODE:
                    *PCCOL:CVERT,*DV,CACDCODE
          GOTO      KCACD150
.
KCACD120  KEYIN     *PCCOL:CVERT,*DV,UNDLN3:
                    *PCCOL:CVERT,*V2LON,CACDCODE:
                    *PCCOL:CVERT,*DV,CACDCODE
.
KCACD150  PACK      CACDCODE,CACDCODE,SP3
          MATCH     SP3,CACDCODE
          GOTO      KCACD790 IF EQUAL       * nothing entered
          MATCH     EXITCHAR,CACDCODE
          GOTO      KCACD820 IF EQUAL       * exitchar entered
          MATCH     QUEST,CACDCODE
          GOTO      KCACD200 IF NOT EQUAL   * ? not entered
.
.         ? entered
.
          IF        FORM1 = ONE
            MOVE      "0",HLEF                * save all of the screen
            CALL      GETSCR00                * save screen
            MOVE      ZERO,FORM1              * set as ? performed
          ENDIF
.
          CALL      CLACDBDS                * display valid entries
.
          IF        CNEWDS = ZERO
            CALL      PUTSCR00                * redisplay screen
            MOVE      ONE,FORM1               * set as ? not performed
            MOVE      ECOL,CCOL
            MOVE      EVERT,CVERT
          ELSE
            DISPLAY   *P1:24,"Input the code : ",*EL;
            MOVE      "24",CVERT
            MOVE      "18",CCOL
            MOVE      ZERO,FORM1              * set ? as entered
          ENDIF
          GOTO      KCACD100
.
.         validate what was entered
.
KCACD200  PACK      KEY5,CODE,CACDCODE
          CALL      RDCACOD1
          BRANCH    OVRCD,KCACD400          * invalid code
.
          MOVE      ZERO,EXIT               * set as valid code
          GOTO      KCACD800
.
.         invalid code
.
KCACD400  DISPLAY   *P1:24,*B,"Invalid Code.  ",*EL;
          CALL      HITENTER
          BRANCH    FORM1,KCACD100
          DISPLAY   *P1:24,"Input the code : ",*EL;
          GOTO      KCACD100
.
.         nothing entered
.
KCACD790  MOVE      SP30,CACDDESC            * clear desc
          MOVE      ONE,EXIT                * set exit flag
          COMPARE   ZERO,CKYIMAND
          GOTO      KCACD800 IF EQUAL       * valid entry
.
.         field is mandatory
.
          DISPLAY   *P1:24,*B,"Field is mandatory.  ",*EL;
          CALL      HITENTER
          BRANCH    FORM1,KCACD100
          DISPLAY   *P1:24,"Input the code : ",*EL;
          GOTO      KCACD100
.
.         valid entry
.
KCACD800  IF        FORM1 = ZERO
            CALL      PUTSCR00                * redisplay screen
          ENDIF
          DISPLAY   *PECOL:EVERT,*V2LON,CACDCODE
          GOTO      KCACD999 
.
.         exitchar entered
.
KCACD820  MOVE      TWO,EXIT                * EXITCHAR entered
          MOVE      SP6,CACDCODE            * clear code
          IF        FORM1 = ZERO
            CALL      FRESCR00                * free the screen
          ENDIF
.
KCACD999  RETURN
+
