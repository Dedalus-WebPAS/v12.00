.******************************************************************************
.*    Program      : F93OTHED                                                 *
.*    Description  : Create outhedaf                                          *
.*                                                                            *
.*    Author       : Ebon Clements                                            *
.*    Date         : 09/09/2003                                               *
.*    Modifications: V9.03.00 09/09/2003 Ebon Clements CAR 22361              *
.*                            Created program to build outhedaf               *
.******************************************************************************
          INC       STD001FD
          INC       OUTMA1FD/INC
          INC       OUTHEDFD/INC
          INC       OUTSITFD/INC
.
FINDFILE  FILE      ASCII, FIXED=256
.
CMDLINE   DIM       100
DIM4      DIM       4
DIM4A     DIM       4
DIM6      DIM       6
DIM6A     DIM       6
DIM60     DIM       60
DDCENT    DIM       2
.
RECNUM    FORM      8
NEWFILE   DIM       60
NEWPATH   DIM       60
OLDPATH   DIM       60
DEFPATH   DIM       60
SAVEFLG   FORM      1
SP50      INIT    "                                                  "
SP60      INIT    "                                                            "
ORACLEFL  FORM      1
VSITE     DIM       6
CFILEPRE  DIM       3   * Current Open File Prefix for Outpatient Files
DEFTSHNO  DIM       2
DEFTSDAT  DIM       8
SAVKEY28  DIM       28
.
PRGNAM    INIT      "CREATE OUTHEDFD"
PRGID     INIT      "F93OTHED"
VERSION   INIT      "V12.00.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
MAIN0100  CALL      OPTN0000                      * select options
          BRANCH    EXIT,MAIN9999                 * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN0200,MAIN0300     * proceed with clean up
          GOTO      MAIN9999
.
MAIN0200  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
MAIN0300  CALL      KSHN0000                      * Keyin schedule number
          BRANCH    EXIT,MAIN9999
.
MAIN0400  CALL      KDAT0000                      * Keyin schedule date
          BRANCH    EXIT,MAIN9999
.
MAIN0500  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN1000,MAIN0000,MAIN9999 * Yes, no, cancel
.
MAIN1000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
. Loop thru old file and write records to new file
.
          CALL      READ0000                      * read master records 
.
          CALL      ENDP0000                      * save/compress saved file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      ZERO,ORACLEFL
          MOVE      SP70,VSITE
          MOVE      "out",CFILEPRE
.
          MOVE      IDDD,VSITE          * Initialize the current site
          MATCH     SP6,VSITE           * Valid site ?
          GOTO      INIT9000 IF EQUAL   * No. Initialization failure
.
          DISPLAY   *P56:24,"Opening outsitaf";
          OPEN      OUTSITA1,"outsitaf" * Open the site file
.
          MOVE      VSITE,KEY6          * Key for the site file
          CALL      RDSITA1             * Read the site record
          BRANCH    OVRCD,INIT9000      * Invalid site.
.
          MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
          GOTO      INIT9999
.
.         Invalid call to program. Common not set up with correct site info
.
INIT9000  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "Incorrect call to program - Site information wrong.    ";
          CALL      HITENTER
          MOVE      ONE,EXIT                * Initialization failure
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run fix program - CISAM ":
                    "- Build new files and populate":
                    *P1:6,*V2LON,TWO,*HOFF,". Run update program  - ORACLE ":
                    "- Populate new fields only";
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9100    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          MOVE      ZERO,ORACLEFL
          GOTO      OPTN9999
.
OPTN9100  MOVE      ZERO,EXIT
          MOVE      ONE,ORACLEFL
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
KDIR0000  CALL      DEFT0000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:12,*EL,"Keyin path for the new outhedaf file: ":
                    *P10:13,*EL,*DV,*RV,NEWPATH:
                    *P10:13,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          KSHN0000                                *
.*  Keyin the default schedule number                               *
.********************************************************************
KSHN0000  MOVE      SP70,DEFTSHNO
          KEYIN     *P1:15,*EL,"Keyin the default schedule number: ":
                    *P36:15,*EL,DEFTSHNO
.
          PACK      DEFTSHNO,DEFTSHNO,SP70
.
          MATCH     SP70,DEFTSHNO
          GOTO      KSHN9000 IF NOT EQUAL
.
          BEEP
          GOTO      KSHN0000
.
KSHN9000  MOVE      ZERO,EXIT
          GOTO      KSHN9999
.
KSHN9100  MOVE      ONE,EXIT
          GOTO      KSHN9999
.
KSHN9999  RETURN
+
.********************************************************************
.*                          KDAT0000                                *
.*  Keyin date                                                      *
.********************************************************************
KDAT0000  MOVE      SP70,DEFTSDAT
          DISPLAY   *P1:17,"Keyin the default clinic open date :";
          CALL      IBACLOCK
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
          MOVE      "38",CCOL
          MOVE      "17",CVERT
          CALL      KEYDATE
          BRANCH    OVRCD,KDAT9999
.
          PACK      DEFTSDAT WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",DEFTSDAT
.
KDAT9999  RETURN
+
.
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
PREP0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PACK      CFNAME,CFILEPRE,FILMA1A1
          OPEN      OUTMA1A1,CFNAME
          TRAPCLR   IO
          BRANCH    OVRCD,PREP7000       * Original file does not exist
.
          IF        ORACLEFL=1
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            PACK      CFNAME,CFILEPRE,FILHEDA1
            OPEN      OUTHEDA1,CFNAME 
            TRAPCLR   IO
            BRANCH    OVRCD,PREP6000       * Original file does not exist
            GOTO      PREP9999
          ENDIF
.
. open old file
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PACK      CFNAME,CFILEPRE,FILHEDA1
          OPEN      OUTHEDA1,CFNAME
          TRAPCLR   IO
          BRANCH    OVRCD,PREP0500       * Original file does exist
          CLOSE     OUTHEDA1
          GOTO      PREP5000
.
. Create the new file
.
PREP0500  DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    CFILEPRE,NEWFILE
          APPEND    "hedaf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.      eg    APPEND    " 195 U9-16",CMDLINE
.
          APPEND    " 425 UC1-6,7-12,13-13,14-18,19-20,21-28",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has
.  to be repeated with for each individual index
.
.          PACK      CMDLINE,SP60,SP60
.          CLEAR     CMDLINE
.          APPEND    "isadd ",CMDLINE
.          APPEND    NEWFILE,CMDLINE
.          APPEND    " UC1-6,13-18,7-12,19-19,20-24,446-447,310-317",CMDLINE
.          APPEND    SP60,CMDLINE
.          RESET     CMDLINE
.          EXECUTE   CMDLINE,TASKID
.
          OPEN      OUTHEDA1,NEWFILE
.
. set the flag to say we want to continue
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"outhedaf file already exists.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP6000  DISPLAY   *P1:24,*EL,*B,"outhedaf file does not exist.";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP7000  DISPLAY   *P1:24,*EL,*B,"outma1af file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
PREP9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and WRMASA1 each record             *
.**********************************************************************
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          PACK      KEY18,SP60
          CALL      RDSMASA1                      * position at start
READ1000  CALL      RDKMASA1                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
          MOVE      OMASITE,OTHESITE
          MOVE      OMACLIN,OTHECLIN
          MOVE      OMADAY,OTHEWDAY
          MOVE      OMASTART,OTHESTRT
          MOVE      DEFTSHNO,OTHESHNO
.
          MATCH     SP70,OTMADTCO
          IF        @EQUAL
            MOVE      DEFTSDAT,OTMADTCO          * Populate clinic open date
            CALL      UPMASA1                    * and Update if blank
          ENDIF
.
          MOVE      OTMADTCO,OTHESDAT
.
          MOVE      OTMADTCC,OTHEDTCC
          MOVE      OMACOM1,OTHECOM1
          MOVE      OMACOM2,OTHECOM2
          MOVE      OTMAINS1,OTHEINS1
          MOVE      OTMAINS2,OTHEINS2
          MOVE      OTMAINS3,OTHEINS3
          MOVE      OMADURN,OTHEDURN
          MOVE      OMAFIN,OTHEBFIN
          MOVE      OMAEND,OTHESEND
          MOVE      OTMALTYP,OTHELTYP
          MOVE      OTMACLSE,OTHECLSE
          MOVE      OTMAHOSP,OTHEHOSP
          MOVE      OTMALOCN,OTHELOCN
          MOVE      OMATYP,OTHECTYP
          MOVE      OMACIND,OTHECIND
          PACK      OTHESPAR,SP70,SP70,SP70
.
          PACK      KEY28,OTHESITE,OTHECLIN,OTHEWDAY,OTHESTRT,OTHESHNO:
                          OTHESDAT,SP70
.
          CALL      WROTHED1                      * WROTHED1 to new file
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     OUTMA1A1                      * close new file
          CLOSE     OUTHEDA1                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
VALD0000  MOVE      ZERO,EXIT
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.**********************************************************************
ENDP0000  CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.         Get the default directory
.**********************************************************************
DEFT0000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "ldat ",CMDLINE
          APPEND    CFILEPRE,CMDLINE
          APPEND    "ma1af.dat > temp.txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding outhedaf.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
DEFT9999  RETURN
.
          INC       OUTMA1IO/INC
          INC       OUTSITIO/INC
          INC       OUTHEDIO/INC
          INC       STD001IO
