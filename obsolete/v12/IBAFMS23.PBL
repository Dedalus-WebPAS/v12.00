. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS23                                             *
. * Desc      :   Batch Deletion                                       *
. **********************************************************************
. * Date      :   07.08.1991                                           *
. * Author    :   Neeriem Dye (IBA)                                    *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
. * VF.01.01  30.10.2000 Sandra Barcham                                *
. *           Recompiled for IBACODIO & IBAPRNIO                       *
. **********************************************************************
. * VF.00.02  17.11.1999 Sandra Barcham                                *
. *           Allow Cancelled Direct Debits                            *
. *           srf 635048                                               *
. * VF.00.01  01.09.1999 Sandra Barcham                                *
. *           Recompiled for change to index fix to FMSJPDA2           *
. *           srf 645667                                               *
. **********************************************************************
.$$$$$$
.$$$$$$
. Standard FMS Accounting System Include
.---------------------------------------
          INC       FMSSTDDF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
.
          INC       FMSAMAFD/INC
          INC       FMSBCFFD/INC       * Batch
          INC       FMSLMAFD/INC       * Ledger
          INC       FMSLMCFD/INC       * Ledger Calendar
          INC       FMSJMAFD/INC       * Journal Master
          INC       FMSJPDFD/INC       * Journal Posting
          INC       FMSCSAFD/INC
          INC       IBABATFD/INC
          INC       IBAPASFD/INC
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
.
BANKNAME  DIM       35
CREDNAME  DIM       35
DEBTNAME  DIM       35
DISCNAME  DIM       35
PAYMNAME  DIM       35
AGSTNAME  DIM       35
CGSTNAME  DIM       35
.
BCHST     INIT      "                        "
BCHST0    INIT      "Batch In Use"
BCHST1    INIT      "Batch Awaiting Mods"
BCHST2    INIT      "Batch Awaiting Printing"
BCHST3    INIT      "Batch Awaiting Update"
BCHST4    INIT      "Batch Processed"
BCHST5    INIT      "Batch Update In Progress"
BCHST6    INIT      "Batch Status Unknown"
.
FAMT      INIT      "(999,999,999.99 "
DAMT      DIM       16
.
ACCDESC   DIM       15
ACCEND    DIM       10
ACCEPT    FORM      1        * accept mode (1=on)
ACCYEAR   DIM       4 
BATDESC   DIM       18
BATFNAME  DIM       8
CASHDATE  DIM       10
CASDESC   DIM       15
CASEND    DIM       10
CASYEAR   DIM       4 
ENTRDATE  DIM       10
OPTIONA   FORM      2        * screen A selection keyin var
POSTDATE  DIM       10
SAVBATCH  DIM       5        * batch
SCRNO     FORM      2        * screen number
.
PRGID     INIT      "IBAFMS23"
PRGNAM    INIT      "Batch Deletion"
VERSION   INIT      "V12.00.00"
.
.******************************************************************************
.   MAINLINE - Controlling Logic
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML0100    CALL      PROC0000           * get batch
          BRANCH    EXIT,ML9999        * exit program ?
.
          CALL      SELA0000           * display/delete batch
          BRANCH    EXIT,ML0100,ML9999
          GOTO      ML0100             * get next selection
.
ML9999    CHAIN     PGM                * chain back to menu
.
.******************************************************************************
.  INIT - Open Files                             Called by ML
.******************************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CCENTRY
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
.
          DISPLAY   *P56:24,"fmsamaaf";  * Account
          OPEN      FMSAMAA1,"fmsamaaf"
.
          DISPLAY   *P64:24,"fmsbcfaf";  * Batch
          OPEN      FMSBCFA1,"fmsbcfaf"
.
          DISPLAY   *P64:24,"fmslmaaf";  * Ledger
          OPEN      FMSLMAA1,"fmslmaaf"
.
          DISPLAY   *P64:24,"fmslmcaf";  * Ledger Calendar
          OPEN      FMSLMCA1,"fmslmcaf"
.
          DISPLAY   *P64:24,"fmsjmaaf";  * Journal Master
          OPEN      FMSJMAA1,"fmsjmaaf"
.
          DISPLAY   *P64:24,"fmsjpdaf";  * Journal Posting
          OPEN      FMSJPDA1,"fmsjpdaf"
.
          DISPLAY   *P64:24,"ihapassf"           * passcode
          OPEN      IHAPASS1,"ihapassf"
          OPEN      IHAPASS2,"ihapassf"
.
INIT9999  DISPLAY   *P1:24,*EF;
          RETURN
.******************************************************************************
.  PROC - Get Key                                Called by ML
.         Returns : EXIT     (1=exit)
.******************************************************************************
PROC0000  UNPACK    SP70,SAVBATCH
          CALL      DHED0000                * display screen header
          MOVE      "25",CCOL               * set up keyin address
          MOVE      "4",CVERT
          MOVE      "0",SCRNO
.
PROC0100  CALL      KBCFFM00                * get batch
          BRANCH    EXIT,PROC9500,PROC9500  * entry ok ? (1=blank, 2=EXITCHAR)
.
          PACK      SAVBATCH,FMBCBAT,SP70
          DISPLAY   *PCCOL:CVERT,*V2LON,SAVBATCH;
.
PROC9000  MOVE      ZERO,EXIT               * continue
          GOTO      PROC9999
.
PROC9500  MOVE      ONE,EXIT                * quit
.
PROC9999  RETURN
.******************************************************************************
.  DHED - Display Screen header                  Called by PROC, redisps
.******************************************************************************
DHED0000  DISPLAY   *P1:4,"    Batch No          : ",*EF;
.
DHED9999  RETURN
.******************************************************************************
.  SELA - Handle display and delete of batch     Called by ML
.******************************************************************************
SELA0000  CALL      DATA0000           * display data
.
.---- check batch status ----
.
          MATCH     "0",FMBCSTA
          GOTO      SELA8100 IF EQUAL
          MATCH     "4",FMBCSTA
          GOTO      SELA8100 IF EQUAL
          MATCH     "5",FMBCSTA
          GOTO      SELA8100 IF EQUAL
          GOTO      SELA8500
.
SELA8100  DISPLAY   *P1:24,*B,*EF,*+,"Cannot Delete - ",BCHST," - ";
          CALL      HITENTER
          MATCH     "*",ANS
          GOTO      SELA8500 IF EQUAL       ***      back door entry        ***
          GOTO      SELA9999                *** sounds a bit rude doesnt it ***
.
.---- check batch transaction type ----
.
SELA8500  MATCH     "CS",FMBCTRT
          GOTO      SELA9250 IF EQUAL
          MATCH     "CJ",FMBCTRT
          GOTO      SELA9250 IF EQUAL
.
          DISPLAY   *P1:24,*B,*EF,*+:
                    "Only 'CS' And 'CJ' Type Batches May Be Deleted - ";
          CALL      HITENTER
          MATCH     "*",ANS
          GOTO      SELA9250 IF EQUAL       ***      back door entry        ***
          GOTO      SELA9999                *** sounds a bit rude doesnt it ***
.
.---- allow user to delete batch ----
.
SELA9250  CALL      DELQST
          SUB       ONE,CEXIT
          MOVE      CEXIT,EXIT
          BRANCH    CEXIT,SELA9999,SELA9999
          CALL      DELR0000        * delete record
.
SELA9999  RETURN
.**********************************************************************
.  DATA - Display selected record                      Called By SELA
.**********************************************************************
DATA0000  DISPLAY   *P1:4,"    Batch No          : ",*V2LON,SAVBATCH,*EF,*HOFF:
                    *P5:6,"Status            : ":
                    *P5:7,"Ledger Number     : ":
                    *P5:8,"Transaction Type  : ":
                    *P5:9,"Number of Entries : ":
                    *P5:10,"Batch Total       : ":
                    *P5:11,"Operator ID       : ":
                    *P5:12,"Date Entered      : ": 
                    *P5:13,"Post to Date      : "
.
          MOVE      "6",F1
          MOVE      FMBCSTA,F1
          ADD       ONE,F1
          MOVE      SP70,BCHST
          LOAD      BCHST,F1,BCHST0,BCHST1,BCHST2,BCHST3,BCHST4,BCHST5,BCHST6
.
DATA1000  MATCH     "CE",FMBCTRT
          GOTO      DATA1100 IF NOT EQUAL
          MOVE      "Committed Expend. ",BATDESC
          GOTO      DATA2000
.
DATA1100  MATCH     "CR",FMBCTRT
          GOTO      DATA1200 IF NOT EQUAL
          MOVE      "Credit Notes      ",BATDESC
          GOTO      DATA2000
.
DATA1200  MATCH     "CS",FMBCTRT
          GOTO      DATA1300 IF NOT EQUAL
          MOVE      "Cash Receipts     ",BATDESC
          GOTO      DATA2000
.
DATA1300  MATCH     "IN",FMBCTRT
          GOTO      DATA1400 IF NOT EQUAL
          MOVE      "Invoices          ",BATDESC
          GOTO      DATA2000
.
DATA1400  MATCH     "JA",FMBCTRT
          GOTO      DATA1450 IF NOT EQUAL
          MOVE      "Journals          ",BATDESC
          GOTO      DATA2000
.
DATA1450  MATCH     "JC",FMBCTRT
          GOTO      DATA1500 IF NOT EQUAL
          MOVE      "Journals          ",BATDESC
          GOTO      DATA2000
.
DATA1500  MATCH     "PY",FMBCTRT
          GOTO      DATA1600 IF NOT EQUAL
          MOVE      "Payments          ",BATDESC
          GOTO      DATA2000
.
DATA1600  MATCH     "RF",FMBCTRT
          GOTO      DATA1700 IF NOT EQUAL
          MOVE      "Refunds           ",BATDESC
          GOTO      DATA2000
.
DATA1700  MATCH     "SC",FMBCTRT
          GOTO      DATA1800 IF NOT EQUAL
          MOVE      "Supply Credits    ",BATDESC
          GOTO      DATA2000
.
DATA1800  MATCH     "RI",FMBCTRT
          GOTO      DATA1900 IF NOT EQUAL
          MOVE      "Reinstate Invoices",BATDESC
          GOTO      DATA2000
.
DATA1900  MATCH     "CJ",FMBCTRT
          GOTO      DATA1950 IF NOT EQUAL
          MOVE      "Cash Journals     ",BATDESC
          GOTO      DATA2000
.
DATA1950  MATCH     "CC",FMBCTRT
          GOTO      DATA1960 IF NOT EQUAL
          MOVE      "Cancelled Payments",BATDESC
          GOTO      DATA2000
.
DATA1960  MATCH     "PP",FMBCTRT
          GOTO      DATA2000 IF NOT EQUAL
          MOVE      "Prompt Payments   ",BATDESC
          GOTO      DATA2000
.
DATA2000  MOVE      SP70,FMLALEDG
          PACK      KEY2,FMBCLED
          CALL      RDFMLA1
.
          UNPACK    FMBCPDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,POSTDATE
.
          UNPACK    FMBCCDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,CASHDATE
.
          MOVE      FMBCPDAT,WORKDATE
          PACK      KEY2,FMBCLED
          CALL      RDFMLA1
.
          CALL      CFYR0000
          UNPACK    PEREDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,ACCEND
          MOVE      PERDESC,ACCDESC
          MOVE      CURYEAR,ACCYEAR
          MOVE      CURYEAR,CASYEAR
          MATCH     "CJ",FMBCTRT
          GOTO      DATA3000 IF NOT EQUAL
.
          MOVE      FMBCCDAT,WORKDATE
          CALL      CFYR0000
          UNPACK    PEREDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,CASEND
          MOVE      PERDESC,CASDESC
          MOVE      CURYEAR,CASYEAR
.
DATA3000  UNPACK    SP70,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FMBCDAT
          UNPACK    FMBCDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,ENTRDATE
.
          MOVE      SP70,SECUSER
          PACK      KEY4,FMBCUID
          CALL      RDPASS1
.
          MOVE      FAMT,DAMT
          FEDIT     FMBCTOT,DAMT
.
          MOVE      FMBCSEC,F5
          SUB       "2",F5
          DISPLAY   *P25:6,*V2LON,FMBCSTA,*HOFF,*P32:6,BCHST:
                    *P25:7,*V2LON,FMBCLED,*HOFF,*P32:7,FMLADESC:
                    *P25:8,*V2LON,FMBCTRT,*HOFF,*P32:8,BATDESC:
                    *P25:9,*V2LON,F5:
                    *P25:10,DAMT:
                    *P25:11,SECUSER:
                    *P25:12,ENTRDATE;
.
          MATCH     SP70,POSTDATE
          GOTO      DATA4000 IF EQUAL
.
          DISPLAY   *P25:13,*V2LON,POSTDATE,*HOFF,SP2,ACCDESC:
                    *P52:13,"Ending ",*V2LON,ACCEND 
.
DATA4000  MATCH     "CJ",FMBCTRT
          GOTO      DATA9000 IF NOT EQUAL
.
          DISPLAY   *P5:14,"Cash Post to Date : ",*V2LON:
                    *P25:14,CASHDATE,*HOFF,SP2,CASDESC:
                    *P52:14,"Ending ",*V2LON,CASEND 
          GOTO      DATA9000
.
DATA9000  
.
DATA9999  RETURN
.**********************************************************************
.  DELR - Delete selected record                       Called By SELA
.**********************************************************************
DELR0000  DISPLAY   *P1:24,*EL,"Deleting Batch : ",*V2LON,SAVBATCH;
.
          PACK      KEY5,SAVBATCH,SP70
          CALL      RDFMJP1
          CALL      DEFMJP1
          MOVE      SP70,BATFNAME
          APPEND    "bch",BATFNAME
          APPEND    SAVBATCH,BATFNAME
          RESET     BATFNAME
          REP       " 0",BATFNAME
.
          PACK      KEY7,FMJPJTY,FMJPJID,SP70
          CALL      DEFMJM1
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PREP      BATFILE,BATFNAME
          CLOSE     BATFILE
          TRAPCLR   IO
.
          PACK      KEY5,SAVBATCH,SP70
          CALL      DEFMBC1
.
DELR9999  RETURN
.******************************************************************************
.  Redisplays
.******************************************************************************
RBCFFM00  
RLMCFM00  
          BRANCH    SCRNO,RDISA000
          CALL      DHED0000
          GOTO      RZZZFM99
.
RDISA000  CALL      DHED0000
          CALL      DATA0000
          GOTO      RZZZFM99
.
RZZZFM99  RETURN
.******************************************************************************
.  INCLUDES FOR I/O'S
.******************************************************************************
.
          INC       FMSSTDCD           * FMS Acc. standard routines
          INC       FMSAMAIO/INC
          INC       FMSBCFIO/INC       * Batch
          INC       FMSLMAIO/INC       * Ledger
          INC       FMSLMCIO/INC       * Ledger Calendar
          INC       FMSJMAIO/INC       * Journal Master
          INC       FMSJPDIO/INC       * Journal Posting
          INC       IBABATIO/INC
          INC       IBAIHAIO/INC
          INC       FMSBCFKY           * Batch
          INC       FMSLMCKY           * Ledger Cal
