.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   F93PTDO1                                                  *
.* Desc      :   Fix program to populate the doctors start date            *
.***************************************************************************
.* Date      :   19/04/2004                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will prompt the user to enter a default      * 
.*           :   doctors start date. This date will then be allocated to   * 
.*           :   all doctor records.                                       * 
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATDO1FD/INC    
.
. LOCAL VARIABLE DEFINITION
. -------------------------
AUTOUPDT  FORM      1
RECNUM    FORM      8
STARTDAT  DIM       8
.
PRGID     INIT      "F93PTDO1"
PRGNAM    INIT      "Populate doctors start date"
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      SDAT0000               * Keyin start date
          BRANCH    EXIT,ML0100
.
ML1100    CALL      AUTO0000               * Keyin set auto update
          BRANCH    EXIT,ML0100
.
ML1200    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          MOVE      ZERO,RECNUM
          OPEN      PATDO1A1,"patdo1af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Populate doctors start date"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
.
.
.********************************************************************
.*                          SDAT0000                                *
.*  Keyin start date for all doctorts                               *
.********************************************************************
SDAT0000  MOVE      SP70,STARTDAT
          DISPLAY   *P1:9,"Default start date for all doctors :";
          CALL      IBACLOCK
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
          MOVE      "38",CCOL
          MOVE      "9",CVERT
          CALL      KEYDATE
          BRANCH    OVRCD,SDAT9000
.
          PACK      STARTDAT,CCENT,CYEAR,CMON,CDAY,SP70
          REP       " 0",STARTDAT
          MOVE      ZERO,EXIT
          GOTO      SDAT9999
.
SDAT9000  MOVE      ONE,EXIT
          GOTO      SDAT9999
.
SDAT9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  PACK      KEY6,SP70
          CALL      RDSDOCT1
PROC1000  CALL      RDKDOCT1
          BRANCH    OVRCD,PROC9999
.
          MATCH     SP70,PTDOSDAT
          GOTO      PROC1000 IF NOT EQUAL
.
          MOVE      STARTDAT,PTDOSDAT            * Start Date
          MOVE      AUTOUPDT,PTDOAUPD            * Auto Update
          CALL      UPDOCT1
.
          ADD       ONE,RECNUM                   * update record counter
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      PROC1000
.
PROC9999  RETURN
.
.
.------------------------------------------------------------
. Update records or print report only
.------------------------------------------------------------
AUTO0000  DISPLAY   *P1:10,*EF
          KEYIN     *P1:10,*EL,"Set Auto Update to Yes":
                    " (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          IF        @EQUAL
            MOVE      ONE,AUTOUPDT
            GOTO      AUTO9999
          ENDIF
.
          MATCH     ANSN,ANS
          IF        @EQUAL
            MOVE      ZERO,AUTOUPDT
            GOTO      AUTO9999
          ENDIF
.
          BEEP
          GOTO      AUTO0000
.
AUTO9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATDO1IO/INC
