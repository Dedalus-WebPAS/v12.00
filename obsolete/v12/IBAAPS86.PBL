. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAAPS86                                             *
. * Desc      :   Intercompany Code Maintenance                        *
. **********************************************************************
. * Date      :   20.06.2005                                           *
. * Author    :   Sandra Barcham                                       *
. * Mods      :                                                        *
. *                                                                    *
. **********************************************************************
.
. Standard FMS Accounting System Include
.---------------------------------------
          INC       FMSSTDDF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
.
          INC       FMSLMAFD/INC           * Ledger
          INC       FMSAMAFD/INC           * Account
          INC       FMSORAFD/INC           * Oracle Financial Mapping
          INC       FMSSBAFD/INC           * Subjective
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
KOPT      INIT      "      "   * vars for KOPT
KOPT1     INIT      "Insert"
KOPT2     INIT      "Change"
KOPT3     INIT      "Delete"
KOPT4     INIT      "List"
.
ACC1      DIM       12
ACC2      DIM       12
ACCDESC   DIM       35
ACCDESC1  DIM       35
ACCDESC2  DIM       35
BNKDESC   DIM       35
BNK2DESC  DIM       35
DISDESC   DIM       35
CRDDESC   DIM       35
DEBDESC   DIM       35
PAYDESC   DIM       35
AGSDESC   DIM       35
.
PRGID     INIT      "IBAAPS86"
PRGNAM    INIT      "Intercompany Code Maintenance"
VERSION   INIT      "V12.00.00"
.
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML0100    CALL      KOPT0000           * insert/modify/delete/exit/range ?
          COMPARE   ZERO,COPTION
          GOTO      ML9999 IF EQUAL    * exit program
.
          COMPARE   FOUR,COPTION
          GOTO      ML7000 IF EQUAL
.
ML0200    CALL      PROC0000           * get entry
          BRANCH    EXIT,ML0100,ML0100 * exit option
.
          BRANCH    COPTION,ML1000,ML1000,ML2000
.
.---- add/modify data ----
.
ML1000    CALL      SELA0000           * modify screen A
          BRANCH    EXIT,ML0200        * don't save data
.
          IF        COPTION=1
            CALL      POSTQST
            BRANCH    CEXIT,ML1100,ML0200,ML0200
          ENDIF
.
ML1100    CALL      POST0000           * post screen A data
          GOTO      ML0200             * get next entry
.
.---- delete data ----
.
ML2000    CALL      SCRA0000           * display screen A
          CALL      DELQST             * ok to delete (y/n/c) ?
          BRANCH    CEXIT,ML2100,ML0200,ML0100
.
ML2100    CALL      DELT0000           * delete screen A data
          GOTO      ML0200             * get next entry
.
ML7000    CLOCK     TIME,CTIMEIS
          MOVE      ZERO,CPAGENO
          MOVE      "55",CLNO
          PACK      KEY14,SP70
          CALL      RSFMOR1
.
ML7100    CALL      RKFMOR1
          BRANCH    OVRCD,ML9999
          COMPARE   "54",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          PRINT     *1,FMORLEDG,"/",FMORACCT,*17,FMAMDESC,*54,FMORINTR
          ADD       ONE,CLNO
          GOTO      ML7100
.
ML7900    CALL      UND80
.
ML9999    CHAIN     PGM
.
HEAD0000  CALL      HEAD80
          CALL      UND80
          PRINT     *1,"Ledger/Account",*17,"Description",*54,"Intercompany"
          ADD       ONE,CLNO
          CALL      UND80
          RETURN
.
.******************************************************************************
.* INIT - Open Files                             Called by ML0000             *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening fmsoraaf";
          OPEN      FMSORAA1,"fmsoraaf"
.
          DISPLAY   *P64:24,"fmsamaaf";
          OPEN      FMSAMAA1,"fmsamaaf"
          OPEN      FMSAMAA2,"fmsamaaf"
          OPEN      FMSAMAA3,"fmsamaaf"
.
          DISPLAY   *P64:24,"fmslmaaf";
          OPEN      FMSLMAA1,"fmslmaaf"
.
          DISPLAY   *P64:24,"fmssbaaf";
          OPEN      FMSSBAA1,"fmssbaaf"
.
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.******************************************************************************
.* KOPT - Get desired function                    Called by ML                *
.*       Displays option at *P50:2                                            *
.*       Display sub-option at *P65:2                                         *
.*       Requires : KOPT1, KOPT2, KOPT3 ....  (descriptions of options)       *
.*       Returns  : COPTION  (desired option)                                 *
.******************************************************************************
KOPT0000  DISPLAY   *P1:3,*EF:             * display screen
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = ",KOPT1:
                    *P1:6,*V2LON,"2",*HOFF," = ",KOPT2:
                    *P1:7,*V2LON,"3",*HOFF," = ",KOPT3:
                    *P1:8,*V2LON,"4",*HOFF," = ",KOPT4
.
KOPT1000  KEYIN     *P1:10,"Select Option : _",*EL,*P17:10,*V2LON,COPTION
          COMPARE   ZERO TO COPTION
          GOTO      KOPT9999 IF EQUAL       * exit ?
.
          MOVE      SP70,KOPT
          LOAD      KOPT,COPTION,KOPT1,KOPT2,KOPT3,KOPT4
          MATCH     SP70,KOPT
          GOTO      KOPT9000 IF NOT EQUAL   * valid option entered ?
          BEEP                              * illegal option, get another
          GOTO      KOPT1000
.
KOPT9000  DISPLAY   *P70:3,*V2LON,KOPT;     * display option
.
KOPT9999  RETURN
.******************************************************************************
.* SCRA - Display Screen A                       Called by SELA0000, redisps  *
.*       Requires : ACCEPT   (1=accept mode)                                  *
.******************************************************************************
SCRA0000  DISPLAY   *P1:4,"    Ledger               : ":
                           *V2LON,FMORLEDG,*HOFF,SP10,SP2,FMLADESC,*EL:
                    *P1:5,"    Account              : ":
                           *V2LON,FMORACCT,*HOFF,SP2,FMAMDESC,*EF:
                    *P1:7,*V2LON," 1",*HOFF,". Intercompany Code    : "
.
SCRA0030  BRANCH    COPTION,SCRA9999
.
          DISPLAY   *P28:7,*V2LON,FMORINTR
          GOTO      SCRA9999
.
SCRA9999  RETURN
.******************************************************************************
.* PROC - Enter Control Account                  Called by ML0000             *
.*        Returns : EXIT     (2=EXITCHAR entered, 1=nothing entered, 0=cont.) *
.*                  FMCALED,FMCAACC (variable entered)                        *
.******************************************************************************
PROC0000  DISPLAY   *P1:4,*EF,"    Ledger               : ":
                        *P1:5,"    Account              : ";
.
          MOVE      "28",CCOL          * set up screen address
          MOVE      "4",CVERT
          CALL      KLMAFM00
          BRANCH    EXIT,PROC9999,PROC9999
          DISPLAY   *P28:4,*EL,*V2LON,FMLALEDG,*HOFF,SP10,SP2,FMLADESC
          MOVE      FMLALEDG,FMORLEDG
.
PROC0100  MOVE      UNDLN70,FMORACCT
          KEYIN     *P28:5,*DV,FMORACCT:
                    *P28:5,*V2LON,FMORACCT:
                    *P28:5,*DV,FMORACCT
.
          ENDSET    FMORACCT
          APPEND    SP70,FMORACCT
          RESET     FMORACCT
.
          MATCH     SP70,FMORACCT
          GOTO      PROC0100 IF EQUAL
.
          CMATCH    EXITCHAR,FMORACCT
          GOTO      PROC0000 IF EQUAL
.
          PACK      KEY14,FMLALEDG,FMORACCT,SP70
          CALL      RDFMOR1
          BRANCH    OVRCD,PROC0200
          DISPLAY   *P28:5,*EL,*V2LON,FMORACCT
          GOTO      PROC9999
.
PROC0200  COMPARE   ONE,COPTION        * insert mode ?
          GOTO      PROC1000 IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Account Not on File - ";
          CALL      HITENTER
          GOTO      PROC0100
.
PROC1000  PACK      KEY14,FMLALEDG,FMORACCT,SP70
          CALL      RDFMOR1
          BRANCH    OVRCD,PROC1500
.  
          DISPLAY   *P1:24,*EL,"Account Already on File - ";
          CALL      HITENTER
          GOTO      PROC0100
.
PROC1500  CALL      RDFMAM1
          BRANCH    OVRCD,PROC8000
.
          MATCH     "3",FMAMTYPE
          GOTO      PROC1600 IF EQUAL
          MATCH     "4",FMAMTYPE
          GOTO      PROC1600 IF EQUAL
          MATCH     "5",FMAMTYPE
          GOTO      PROC1600 IF EQUAL
.
          MATCH     "6",FMAMTYPE
          GOTO      PROC1550 IF EQUAL
.
          DISPLAY   *P1:24,*EL,"Invalid Account Type - ";
          CALL      HITENTER
          GOTO      PROC0100
.
PROC1550  PACK      KEY14,FMLALEDG,FMORACCT,SP70
          CALL      RDFMSA1
          BRANCH    OVRCD,PROC8000
.
PROC1600  DISPLAY   *P28:5,*EL,*V2LON,FMORACCT,*HOFF,SP2,FMAMDESC
          GOTO      PROC9999
.
PROC8000  DISPLAY   *P1:24,*EL,*B,"Invalid Account Code - ";
          CALL      HITENTER
          GOTO      PROC0100
.
PROC9999  RETURN
.******************************************************************************
.* SELA - Modify screen A data                   Called by ML0000             *
.*        Returns : EXIT     (2=EXITCHAR entered, 1=nothing entered, 0=cont.) *
.******************************************************************************
SELA0000  CALL      SCRA0000
.
.---- determine option to execute ----
.
SELA0100  BRANCH    COPTION,SELA1100              * in accept mode ?
          CALL      MAINQST                      * select/post/cancel ?
.
          COMPARE   ZERO,CCITEM
          GOTO      SELA9000 IF EQUAL            * post ?
.
          COMPARE   SEQ,CCITEM
          GOTO      SELA9500 IF EQUAL            * cancel ?
.
SELA0500  BRANCH    CCITEM,SELA1100
          BEEP
          GOTO      SELA0100
.
.---- option 1 ---- Intercompany Code
.
SELA1100  MOVE      "1",CCONTIND
.
          MOVE      UNDLN70,FMORINTR
          KEYIN     *P28:7,*DV,FMORINTR:
                    *P28:7,*V2LON,FMORINTR:
                    *P28:7,*DV,FMORINTR
.
          ENDSET    FMORINTR
          APPEND    SP70,FMORINTR
          RESET     FMORINTR
.
          MATCH     SP70,FMORINTR
          GOTO      SELA1100 IF EQUAL
.
          CMATCH    EXITCHAR,FMORINTR
          GOTO      SELA9500 IF EQUAL
.
          COMPARE   TWO,COPTION
          GOTO      SELA0100 IF EQUAL
.
.---- exit points ----
.
SELA9000  MOVE      ZERO,EXIT                    * post
          GOTO      SELA9999
.
SELA9500  MOVE      ONE,EXIT                     * cancel
.
SELA9999  RETURN
.******************************************************************************
.* POST - Post data                              Called by ML, PRAN           *
.******************************************************************************
POST0000  PACK      KEY14,FMORLEDG,FMORACCT,SP70
          CALL      DEFMOR1                 * write new record
          CALL      WRFMOR1                 * write new record
.
POST9999  RETURN
.******************************************************************************
.* DELT - Delete data                            Called by ML0000             *
.******************************************************************************
DELT0000  PACK      KEY14,FMORLEDG,FMORACCT,SP70
          CALL      DEFMOR1                 * delete old record (if possible)
.
DELT9999  RETURN
.******************************************************************************
.* INCLUDES FOR I/O'S                                                         *
.******************************************************************************
.
          INC       FMSLMAIO/INC           * Ledger
          INC       FMSAMAIO/INC           * Account
          INC       FMSORAIO/INC           * Oracle Financial Mapping
          INC       FMSSBAIO/INC           * Subjective
.
          INC       FMSLMAKY           * Ledger
.
          INC       FMSSTDCD           * FMS Acc. standard routines
