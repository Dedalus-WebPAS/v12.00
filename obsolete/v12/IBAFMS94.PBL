. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS94                                             *
. * Desc      :   Closing Balance Transfer                             *
. **********************************************************************
. * Date      :   22.11.1990                                           *
. * Author    :   Paul Duncan (IBA)                                    *
. * Mods      :                                                        *
. **********************************************************************
. * VF.01.01  24.04.2001 Sandra Barcham                                *
. *           Fix financial year 01012000 to 31012000                  *
. **********************************************************************
.$$$$$$
.        PROGRAM IBAFMS94
.             open files and initialise variables
.             WHILE user wants to Produce report DO
.                  get desired parametres
.             END WHILE
.             IF user wants to post data
.                  THEN Produce printout
.             END IF
.        END
.$$$$$$
. Standard FMS Accounting System Include
.---------------------------------------
          INC       FMSSTDDF
          INC       FMSAMAFD/INC       * account master
          INC       FMSCSAFD/INC
          INC       FMSFPSFD/INC       * financial period summary
          INC       FMSLMAFD/INC       * ledger master
          INC       FMSLMCFD/INC       * financial year file
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
.
.
.==============================================================================
.LOCAL CONSTANT DEFINITION
.-------------------------
BANKNAME  DIM       35
CREDNAME  DIM       35
DEBTNAME  DIM       35
DISCNAME  DIM       35
PAYMNAME  DIM       35
AGSTNAME  DIM       35
CGSTNAME  DIM       35
ASSET     INIT      "Asset"
.
EQUITY    INIT      "Equity"
.
LIABILTY  INIT      "Liability"
.
EXPENDIT  INIT      "Expenditure"
INCOME    INIT      "Income"
UNKNOWN   INIT      "Unknown"
.
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
DD1       DIM       2
MM1       DIM       2
YY1       DIM       2
CC1       DIM       2
.
ABALANCE  FORM      10.2
ACRLBALC  FORM      10.2
ACRLTOTL  FORM      10.2
.
CBALANCE  FORM      10.2
CASHBALC  FORM      10.2
CASHTOTL  FORM      10.2
.
FIRSTDAY  FORM      3
FIRSTCC   FORM      2
FIRSTYY   FORM      2
FIRSTREC  FORM      1
FNAME     DIM       8
FROMYEAR  DIM       4
.
LASTDATE  DIM       8
LASTDAY   FORM      3
LASTCC    FORM      2
LASTYY    FORM      2
.
MODE      FORM      1
.
PREVTYPE  DIM       1
.
SDACCT    DIM       12
SDDESC    DIM       35
TOYEAR    DIM       4
TYPEDESC  DIM       11
.
FORMAT    INIT      "(999,999,999.99 "
CPRT      DIM       16 
APRT      DIM       16
.
ATOTINCF  DIM       16
ATOTEXPF  DIM       16
ATOTASSF  DIM       16
ATOTLIBF  DIM       16
ATOTEQTF  DIM       16
ATOTSURF  DIM       16
ATOTASDF  DIM       16
CTOTINCF  DIM       16
CTOTEXPF  DIM       16
CTOTASSF  DIM       16
CTOTLIBF  DIM       16
CTOTEQTF  DIM       16
CTOTSURF  DIM       16
CTOTASDF  DIM       16
.
ATOTINC   FORM      10.2
ATOTEXP   FORM      10.2
ATOTASS   FORM      10.2
ATOTLIB   FORM      10.2
ATOTEQT   FORM      10.2
ATOTSUR   FORM      10.2
ATOTASD   FORM      10.2
CTOTINC   FORM      10.2
CTOTEXP   FORM      10.2
CTOTASS   FORM      10.2
CTOTLIB   FORM      10.2
CTOTEQT   FORM      10.2
CTOTSUR   FORM      10.2
CTOTASD   FORM      10.2
.
PRGID     INIT      "IBAFMS94"
PRGNAM    INIT      "Closing Balance Transfer"
VERSION   INIT      "V12.00.00"
.
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML1000    CALL      CLR0000            * clear variables
          CALL      KPAR0000           * keyin parameters
          BRANCH    EXIT,ML9999,ML9999
          CALL      PRNT0000           * save data
          GOTO      ML1000
.
ML9999    CHAIN     PGM                * chain back to menu
.
.******************************************************************************
.* INIT - Open Files                             Called by ML0000             *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening fmsamaaf";  * ledger file
          OPEN      FMSAMAA1,"fmsamaaf"
          OPEN      FMSAMAA2,"fmsamaaf"
          OPEN      FMSAMAA3,"fmsamaaf"
.
          DISPLAY   *P64:24,"fmscsaaf"
          OPEN      FMSCSAA1,"fmscsaaf"
.
          DISPLAY   *P64:24,"fmslmaaf";  * ledger file
          OPEN      FMSLMAA1,"fmslmaaf"
.
          DISPLAY   *P64:24,"fmslmcaf";  * ledger file
          OPEN      FMSLMCA1,"fmslmcaf"
.
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.************************************************************************
.* CLR - Clear variables                                                *
.************************************************************************
CLR0000   UNPACK    SP70,FMLALEDG,FMLADESC,FMLCYEAR,FROMYEAR,TOYEAR,SDACCT
          UNPACK    SP70,SDDESC
.
CLR9999   RETURN
.************************************************************************
.* KPAR - Keyin the print parametres         Called by ML1000           *
.************************************************************************
KPAR0000  CALL      CLR0000            * clear variables
          CALL      RLMAFM00           * display screen
          MOVE      ZERO,MODE
.
KPAR1000  CALL      KLDG0000           * keyin ledger
          BRANCH    EXIT,KPAR9000,KPAR9000 * exit program
          MOVE      ZERO,MODE          * need to rekey rest of data
.
KPAR2000  CALL      KFYR0000           * from keyin Financial year
          LOAD      MODE,EXIT,ZERO,ZERO
          BRANCH    EXIT,KPAR1000,KPAR9000 * exit program
          BRANCH    MODE,KPAR3000
.
KPAR2500  CALL      KTYR0000           * keyin to Financial year
          LOAD      MODE,EXIT,ZERO,ZERO
          BRANCH    EXIT,KPAR2000,KPAR9000 * exit program
          BRANCH    MODE,KPAR3000
.
KPAR2700  CALL      KSDA0000           * keyin surplus/deficit Acct.
          LOAD      MODE,EXIT,ZERO,ZERO
          BRANCH    EXIT,KPAR2500,KPAR9000 * exit program
.
KPAR3000  CALL      MAINQST            * select item post cancel
          COMPARE   "0",CCITEM         * post
          GOTO      KPAR8000 IF EQUAL
.
          COMPARE   "-1",CCITEM        * cancel
          GOTO      KPAR9000 IF EQUAL
.
          MOVE      ONE,MODE           * set to change mode
          BRANCH    CCITEM,KPAR1000,KPAR2000,KPAR2500,KPAR2700
          BEEP
          GOTO      KPAR3000
.
KPAR8000  MOVE      ZERO,EXIT          * do printout
          GOTO      KPAR9999
.
KPAR9000  MOVE      ONE,EXIT           * exit prog
.
KPAR9999  RETURN 
.*************************************************************************
.* KLDG - Keyin ledger code                 Called by KPAR1000           *
.*        Returns EXIT = 0 - valid entry 1 -null 2 exitchar              *
.*************************************************************************
KLDG0000  MOVE      "4",CVERT           * set up variables for standard keyin
          MOVE      "28",CCOL
          CALL      KLMAFM00            * keyin ledger
          BRANCH    EXIT,KLDG9999,KLDG9999
.
          DISPLAY   *P28:4,*V2LON,FMLALEDG,*HOFF,SP10,SP2,FMLADESC
          GOTO      KLDG9999
.
KLDG9999  RETURN
.*************************************************************************
.* KFYR - Keyin Financial year              Called by KPAR2000           *
.*        Returns EXIT = 0 - valid entry 1 -null 2 exitchar              *
.*************************************************************************
KFYR0000  MOVE      "5",CVERT           * set up variables for standard keyin
          MOVE      "28",CCOL
          MOVE      SP10,FROMYEAR
          CALL      KLMCFM00            * keyin ledger
          BRANCH    EXIT,KLDG9999,KLDG9999
.
. get the last day in FROM year
.
          IF        FMLAPERS=0
            MOVE      FMLC12TO,LASTDATE      * monthly
          ELSE
            MOVE      FMLC13TO,LASTDATE      * fortnightly        
          ENDIF
. 
          UNPACK    LASTDATE,CC1,YY1,MM1,DD1
          MOVE      CC1,CC
          MOVE      YY1,YY
          MOVE      MM1,MM
          MOVE      DD1,DD
          CALL      DMYCON
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          CALL      DMYCON
          MOVE      JULDAY,LASTDAY
          MOVE      JULYR,LASTYY
          MOVE      JULCC,LASTCC
.
          DISPLAY   *P28:5,*V2LON,FMLCYEAR,*EL;
          MOVE      FMLCYEAR,FROMYEAR
          CLEAR     FNAME
          APPEND    "fmsp",FNAME
          APPEND    FMLCYEAR,FNAME
          RESET     FNAME
          REP       " 0",FNAME
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO     * can't do report if file doesn't exist
          OPEN      FMSFPSA1,FNAME
          TRAPCLR   IO
          BRANCH    OVRCD,KFYR8000
          GOTO      KLDG9999
.
KFYR8000  DISPLAY   *P1:24,*B,*EL,"Financial period summary file does not":
                                  " exist. ";                                 
          CALL      HITENTER
          GOTO      KFYR0000
.
KFYR9999  RETURN
.*************************************************************************
.* KTYR - Keyin TO Financial year           Called by KPAR2000           *
.*        Returns EXIT = 0 - valid entry 1 -null 2 exitchar              *
.*************************************************************************
KTYR0000  MOVE      "6",CVERT           * set up variables for standard keyin
          MOVE      "28",CCOL
          MOVE      SP10,TOYEAR
          CALL      KLMCFM00            * keyin ledger
          BRANCH    EXIT,KTYR9999,KTYR9999
.
. get the first day in TO year
.
          UNPACK    FMLC01FR,CC1,YY1,MM1,DD1
          MOVE      CC1,CC
          MOVE      YY1,YY
          MOVE      MM1,MM
          MOVE      DD1,DD
          CALL      DMYCON
          MOVE      JULDAY,FIRSTDAY
          MOVE      JULYR,FIRSTYY
          MOVE      JULCC,FIRSTCC
.
. is TO financial year after FROM financial year?
.
          COMPARE   LASTCC,FIRSTCC
          IF        @LESS
            GOTO      KTYR6000
          ENDIF
.
          COMPARE   LASTYY,FIRSTYY
          IF        @LESS
            GOTO      KTYR6000
          ENDIF
.
. check that the last day in the FROM financial year is the date before 
. the first day in the TO financial year
.
          COMPARE   FIRSTDAY,LASTDAY
          IF        !@EQUAL
            GOTO    KTYR6500
          ENDIF 
.
          DISPLAY   *P28:6,*V2LON,FMLCYEAR,*EL;
          MOVE      FMLCYEAR,TOYEAR
          COMPARE   ZERO,FMLCYLC
          GOTO      KTYR1000 IF EQUAL
          DISPLAY   *P1:24,"Balances Previously Transfered !!":
                           " Overwrite Balances (":
                           *V2LON,"Y",*HOFF,"/":
                           *V2LON,"N",*HOFF,") ?";
          MOVE      "62",CCOL
          MOVE      "24",CVERT
          CALL      YORN0000
          BRANCH    FYESNO,KTYR1000
          GOTO      KTYR0000
.
KTYR1000  CLEAR     FNAME
          APPEND    "fmsp",FNAME
          APPEND    FMLCYEAR,FNAME
          RESET     FNAME
          REP       " 0",FNAME
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO     * can't do report if file doesn't exist
          OPEN      FMSFPSA2,FNAME
          TRAPCLR   IO
          BRANCH    OVRCD,KTYR8000
          GOTO      KTYR9999
.
KTYR6000  DISPLAY   *P1:24,*B,*EL,"TO Financial Year Must Be After FROM ":
                    "Financial Year. ";                                 
          CALL      HITENTER
          DISPLAY   *P28:6,*EL;
          GOTO      KTYR9500
.
KTYR6500  DISPLAY   *P1:24,*B,*EL,"Financial Years Must ":
                    "Follow Each Other. ";                                 
          CALL      HITENTER
          DISPLAY   *P28:6,*EL;
          GOTO      KTYR9500
.
KTYR7000  DISPLAY   *P1:24,*B,*EL,"Ledger Financial Calender Control record ":
                    " does not exist. ";                                 
          CALL      HITENTER
          GOTO      KTYR9500
.
KTYR8000  DISPLAY   *P1:24,*B,*EL,"Financial period summary file does not":
                                  " exist. ";                                 
          CALL      HITENTER
          GOTO      KTYR0000
.
KTYR9500  MOVE      TRUE,EXIT
          GOTO      KTYR9999
.
KTYR9999  RETURN
.*************************************************************************
.* KSDA - Keyin Surplus/deficit account     Called by KPAR2700           *
.*        Returns EXIT = 0 - valid entry 1 -null 2 exitchar              *
.*************************************************************************
KSDA0000  MOVE      "7",CVERT           * set up variables for standard keyin
          MOVE      "28",CCOL
          MOVE      "11",CACCTIND
          CALL      KAMAFM00            * keyin ledger
          BRANCH    EXIT,KSDA9999,KSDA9999
.
          DISPLAY   *P28:7,*V2LON,FMAMACCT,*HOFF,*P42:7,FMAMDESC
          MOVE      FMAMACCT,SDACCT
          MOVE      FMAMDESC,SDDESC
.
KSDA9999  RETURN
.************************************************************************
.* REDISPLAY ROUTINES FOR STANDARD KEYINS                               *
.************************************************************************

RAMAFM00
RLMCFM00   DISPLAY   *P1:4,*EF,*V2LON," 1",*HOFF,". Ledger",*P26:4,": ":
                     *V2LON,FMLALEDG,*HOFF,SP10,SP2,FMLADESC
           GOTO      RLMAFM05
RLMAFM00   DISPLAY   *P1:4,*EF,*V2LON," 1",*HOFF,". Ledger",*P26:4,":"
RLMAFM05   DISPLAY   *P1:5,*V2LON," 2",*HOFF,". From Financial Year",*P26:5,":"
.
           PACK      FROMYEAR,FROMYEAR,SP4
           MATCH     FROMYEAR,SP4          * any year yet
           GOTO      RLMAFM10 IF EQUAL
.
           DISPLAY   *P28:5,*V2LON,FROMYEAR
RLMAFM10   DISPLAY   *P1:6,*V2LON," 3",*HOFF,". To Financial Year",*P26:6,":"
           PACK      TOYEAR,TOYEAR,SP4
           MATCH     TOYEAR,SP4          * any year yet
           GOTO      RLMAFM20 IF EQUAL
.
           DISPLAY   *P28:6,*V2LON,TOYEAR
RLMAFM20   DISPLAY   *P1:7,*V2LON," 4",*HOFF,". Deficit/Surplus Acct ":
                     *P26:7,":"
           PACK      SDACCT,SDACCT,SP30
           MATCH     SDACCT,SP30         * any year yet
           GOTO      RLMAFM99 IF EQUAL
.
           DISPLAY   *P28:7,*V2LON,SDACCT,*P42:7,*HOFF,SDDESC
.
RLMAFM99   RETURN                     
.************************************************************************
.* PRNT - Loop thru the account master printing required records        *
.*                   called by : M1000                                  *
.************************************************************************
PRNT0000  MOVE      ZERO,CPAGENO              * set head80 variables
          MOVE      ONE,FIRSTREC
          CLOCK     TIME,CTIMEIS
          MOVE      ONE,CNOUNDLN
          PACK      KEY15,FMLALEDG,ONE,SP20
          CALL      RSFMAM2                   * position account file
.
          DISPLAY   *P50:24,"Printing : "
.
PRNT1000  CALL      RKFMAM2                   * read next record
          BRANCH    EXIT,PRNT5000
.
          MATCH     FMLALEDG,FMAMLEDG         * right ledger
          GOTO      PRNT5000 IF NOT EQUAL
.
          DISPLAY   *P61:24,*V2LON,FMAMACCT
          MOVE      FIVE,DIM1
          MATCH     FMAMTYPE,DIM1             * valid account type
          GOTO      PRNT5000 IF LESS
.
          CALL      CCBL0000                  * calculate closing balances
.
          BRANCH    FIRSTREC,PRNT3000
          MATCH     FMAMTYPE,PREVTYPE         * change in account type?
          GOTO      PRNT2000 IF EQUAL
.
          CALL      TOTL0000                  * print total
          GOTO      PRNT3000
.
PRNT2000  COMPARE   "60",CLNO                 * full page
          GOTO      PRNT4000 IF LESS
.
PRNT3000  CALL      NEWP0000                  * new page thanks 
.
PRNT4000  CALL      PLIN0000                  * print a line of data
          MOVE      FMAMTYPE,F1
          BRANCH    F1,PRNT4100,PRNT4100
          CALL      UTYR0000                  * update to fin. year  
PRNT4100  MOVE      ZERO,FIRSTREC
          MOVE      FMAMTYPE,PREVTYPE
          GOTO      PRNT1000
.
PRNT5000  BRANCH    FIRSTREC,PRNT6000
          CALL      TOTL0000                  * print last total
          CALL      BALC0000                  * print balance
          GOTO      PRNT7000
PRNT6000  CALL      NEWP0000
.
PRNT7000  PRINT     *N,"*** End of Report ***"
.
PRNT9999  RETURN          
.************************************************************************
.* CCBL - Calculate the cloaing cash and accrual balances               *
.*                   called by : PRNT1000                               *
.************************************************************************
CCBL0000  MOVE      ZERO,CASHBALC
          MOVE      ZERO,ACRLBALC
          PACK      KEY14,FMLALEDG,FMAMACCT
          CALL      RDFMFP1
          BRANCH    OVRCD,CCBL9999
.
          MOVE      FMFPCBF,CASHBALC          * cash brought forward
          ADD       FMFPC01,CASHBALC
          ADD       FMFPC02,CASHBALC
          ADD       FMFPC03,CASHBALC
          ADD       FMFPC04,CASHBALC
          ADD       FMFPC05,CASHBALC
          ADD       FMFPC06,CASHBALC
          ADD       FMFPC07,CASHBALC
          ADD       FMFPC08,CASHBALC
          ADD       FMFPC09,CASHBALC
          ADD       FMFPC10,CASHBALC
          ADD       FMFPC11,CASHBALC
          ADD       FMFPC12,CASHBALC
          ADD       FMFPC13,CASHBALC
.
          MOVE      FMFPABF,ACRLBALC          * accrual brought forward
          ADD       FMFPA01,ACRLBALC
          ADD       FMFPA02,ACRLBALC
          ADD       FMFPA03,ACRLBALC
          ADD       FMFPA04,ACRLBALC
          ADD       FMFPA05,ACRLBALC
          ADD       FMFPA06,ACRLBALC
          ADD       FMFPA07,ACRLBALC
          ADD       FMFPA08,ACRLBALC
          ADD       FMFPA09,ACRLBALC
          ADD       FMFPA10,ACRLBALC
          ADD       FMFPA11,ACRLBALC
          ADD       FMFPA12,ACRLBALC
          ADD       FMFPA13,ACRLBALC
.
CCBL9999  RETURN 
.************************************************************************
.* TOTL - Print the total for this account type                         *
.*                   called by : PRNT4000                               *
.************************************************************************
TOTL0000  CALL      UND80 
          MOVE      FORMAT,CPRT
          MOVE      FORMAT,APRT
          FEDIT     CASHTOTL,CPRT
          FEDIT     ACRLTOTL,APRT
          PRINT     *16,"Total ",TYPEDESC,*49,CPRT,APRT:
                    *N,*49,"--------------------------------"
          MOVE      PREVTYPE,F1
          STORE     CASHTOTL,F1,CTOTINC,CTOTEXP,CTOTASS,CTOTLIB,CTOTEQT
          STORE     ACRLTOTL,F1,ATOTINC,ATOTEXP,ATOTASS,ATOTLIB,ATOTEQT
          ADD       CASHTOTL,CBALANCE
          ADD       ACRLTOTL,ABALANCE
          MOVE      ZERO,CASHTOTL
          MOVE      ZERO,ACRLTOTL
TOTL9999  RETURN 
.************************************************************************
.* BALC - Print the Balance of the report                               *
.*                   called by : PRNT4000                               *
.************************************************************************
BALC0000  
          MOVE      CTOTINC,CTOTSUR
          ADD       CTOTEXP,CTOTSUR
          MOVE      ATOTINC,ATOTSUR
          ADD       ATOTEXP,ATOTSUR
.
          MOVE      CTOTASS,CTOTASD
          ADD       CTOTLIB,CTOTASD
          MOVE      ATOTASS,ATOTASD
          ADD       ATOTLIB,ATOTASD
.
          MOVE      FORMAT,ATOTINCF
          MOVE      FORMAT,ATOTEXPF
          MOVE      FORMAT,ATOTASSF
          MOVE      FORMAT,ATOTLIBF
          MOVE      FORMAT,ATOTEQTF
          MOVE      FORMAT,ATOTSURF
          MOVE      FORMAT,ATOTASDF
          MOVE      FORMAT,CTOTINCF
          MOVE      FORMAT,CTOTEXPF
          MOVE      FORMAT,CTOTASSF
          MOVE      FORMAT,CTOTLIBF
          MOVE      FORMAT,CTOTEQTF
          MOVE      FORMAT,CTOTSURF
          MOVE      FORMAT,CTOTASDF
.
          FEDIT     ATOTINC,ATOTINCF
          FEDIT     ATOTEXP,ATOTEXPF
          FEDIT     ATOTASS,ATOTASSF
          FEDIT     ATOTLIB,ATOTLIBF
          FEDIT     ATOTEQT,ATOTEQTF
          FEDIT     ATOTSUR,ATOTSURF
          FEDIT     ATOTASD,ATOTASDF
          FEDIT     CTOTINC,CTOTINCF
          FEDIT     CTOTEXP,CTOTEXPF
          FEDIT     CTOTASS,CTOTASSF
          FEDIT     CTOTLIB,CTOTLIBF
          FEDIT     CTOTEQT,CTOTEQTF
          FEDIT     CTOTSUR,CTOTSURF
          FEDIT     CTOTASD,CTOTASDF
.
          PRINT     *N,*N,*20,"Closing Balance Report Summary",*N:
                    *20,"------------------------------",*N:
                    *46,"Cash          Accrual",*N:
                    *46,"----          -------",*N:
                 "Total Income                    : ",CTOTINCF,SP1,ATOTINCF,*N:
                 "Total Expenditure               : ",CTOTEXPF,SP1,ATOTEXPF,*N:
                    *36,"--------------- ----------------",*N:
                 "Surplus/Deficit Carried Forward : ",CTOTSURF,SP1,ATOTSURF:
                    *N,*N,*N:
                 "Total Assets                    : ",CTOTASSF,SP1,ATOTASSF,*N:
                 "Total Liabilities               : ",CTOTLIBF,SP1,ATOTLIBF,*N:
                    *36,"--------------- ----------------",*N:
                 "Accumulated Surplus/Deficit     : ",CTOTASDF,SP1,ATOTASDF,*N:
                    *36,"=============== ================",*N,*N,*N:
                 "Total Equity                    : ",CTOTEQTF,SP1,ATOTEQTF,*N:
                 "Plus Surplus/Deficit C/F        : ",CTOTSURF,SP1,ATOTSURF,*N:
                    *36,"--------------- ----------------"
          ADD       ATOTSUR,ATOTEQT
          ADD       CTOTSUR,CTOTEQT
          MOVE      FORMAT,ATOTEQTF
          MOVE      FORMAT,CTOTEQTF
          FEDIT     ATOTEQT,ATOTEQTF
          FEDIT     CTOTEQT,CTOTEQTF
          PRINT     "Accumulated Surplus/Deficit     : ",CTOTEQTF,SP1,ATOTEQTF:
                    *N,*36,"=============== ================"
.
          CALL      USDA0000
          PRINT     *N,*N,"Surplus/Deficit Carried Forward Posted to : ":
                    *N,FMLALEDG,"/",SDACCT,SP1,SDDESC
          CALL      UND80
          MOVE      ZERO,CBALANCE
          MOVE      ZERO,ABALANCE
          MOVE      ZERO,CTOTINC
          MOVE      ZERO,CTOTEXP
          MOVE      ZERO,CTOTASS
          MOVE      ZERO,CTOTLIB
          MOVE      ZERO,CTOTEQT
          MOVE      ZERO,ATOTINC
          MOVE      ZERO,ATOTEXP
          MOVE      ZERO,ATOTASS
          MOVE      ZERO,ATOTLIB
          MOVE      ZERO,ATOTEQT
.
BALC9999  RETURN 
.************************************************************************
.* NEWP - Print new page                          Called by PRNT2000    *       
.************************************************************************
NEWP0000  CALL     HEAD80
          PRINT    *N,*1,"Ledger : ",FMLALEDG,SP1,FMLADESC,SP3:
                   "Financial Year : ",FMLCYEAR
.
          MOVE     FMAMTYPE,FORM1
          LOAD     TYPEDESC,FORM1,INCOME,EXPENDIT,ASSET,LIABILTY,EQUITY
          PRINT    *22,"Account Type : ",TYPEDESC
          CALL     UND80
          PRINT    "Account      Description                                 ":
                   "  Cash         Accrual"
          CALL     UND80
          ADD      FOUR,CLNO
.
NEWP9999  RETURN 
.************************************************************************
.* PLIN - Print a line on the report           Called by PRNT5000       *
.************************************************************************
PLIN0000  
          COMPARE   ZERO,CASHBALC
          GOTO      PLIN1000 IF NOT EQUAL
          COMPARE   ZERO,ACRLBALC
          GOTO      PLIN9999 IF EQUAL
.
PLIN1000  MOVE      FORMAT,CPRT
          MOVE      FORMAT,APRT
          FEDIT     CASHBALC,CPRT
          FEDIT     ACRLBALC,APRT
          PRINT     FMAMACCT,SP1,FMAMDESC,CPRT,APRT
          ADD       ONE,CLNO
          ADD       CASHBALC,CASHTOTL
          ADD       ACRLBALC,ACRLTOTL
.
PLIN9999  RETURN 
.************************************************************************
.* UTYR - Update the to fin. year with the balance brought forward      *
.*                   called by : PRNT                                   *
.************************************************************************
UTYR0000  PACK      KEY14,FMLALEDG,FMAMACCT
          CALL      RDFMFP2
          BRANCH    OVRCD,UTYR1000
.
          MOVE      CASHBALC,FMFPCBF       * update brought forward feilds
          MOVE      ACRLBALC,FMFPABF
          MOVE      CASHBALC,FMFPCBFD      * update brought forward feilds
          MOVE      ACRLBALC,FMFPABFD
          MOVE      CASHBALC,FMFPCBFT      * update brought forward feilds
          MOVE      ACRLBALC,FMFPABFT
          CALL      UPFMFP2                * update file
          GOTO      UTYR9999
.
UTYR1000  CALL      MZER0000
          MOVE      CASHBALC,FMFPCBF       * update brought forward feilds
          MOVE      ACRLBALC,FMFPABF
          MOVE      CASHBALC,FMFPCBFD      * update brought forward feilds
          MOVE      ACRLBALC,FMFPABFD
          MOVE      CASHBALC,FMFPCBFT      * update brought forward feilds
          MOVE      ACRLBALC,FMFPABFT
          MOVE      ZERO,FORM2
          MOVE      FMLALEDG,FMFPCULD
          MOVE      FMAMACCT,FMFPCUAC
          CALL      WRFMFP2                * update file
.
UTYR9999  RETURN
MZER0000
          MOVE      ZERO,FMFPCBF 
          MOVE      ZERO,FMFPC01 
          MOVE      ZERO,FMFPC02 
          MOVE      ZERO,FMFPC03 
          MOVE      ZERO,FMFPC04 
          MOVE      ZERO,FMFPC05 
          MOVE      ZERO,FMFPC06 
          MOVE      ZERO,FMFPC07 
          MOVE      ZERO,FMFPC08 
          MOVE      ZERO,FMFPC09 
          MOVE      ZERO,FMFPC10 
          MOVE      ZERO,FMFPC11 
          MOVE      ZERO,FMFPC12 
          MOVE      ZERO,FMFPC13 
.
          MOVE      ZERO,FMFPABF 
          MOVE      ZERO,FMFPA01 
          MOVE      ZERO,FMFPA02 
          MOVE      ZERO,FMFPA03 
          MOVE      ZERO,FMFPA04 
          MOVE      ZERO,FMFPA05 
          MOVE      ZERO,FMFPA06 
          MOVE      ZERO,FMFPA07 
          MOVE      ZERO,FMFPA08 
          MOVE      ZERO,FMFPA09 
          MOVE      ZERO,FMFPA10 
          MOVE      ZERO,FMFPA11 
          MOVE      ZERO,FMFPA12 
          MOVE      ZERO,FMFPA13 
.
          MOVE      ZERO,FMFPCOM 
          MOVE      ZERO,FMFPCOM2
.
. Fields in Dollar only Scale
.
          MOVE      ZERO,FMFPCBFD
          MOVE      ZERO,FMFPC01D
          MOVE      ZERO,FMFPC02D
          MOVE      ZERO,FMFPC03D
          MOVE      ZERO,FMFPC04D
          MOVE      ZERO,FMFPC05D
          MOVE      ZERO,FMFPC06D
          MOVE      ZERO,FMFPC07D
          MOVE      ZERO,FMFPC08D
          MOVE      ZERO,FMFPC09D
          MOVE      ZERO,FMFPC10D
          MOVE      ZERO,FMFPC11D
          MOVE      ZERO,FMFPC12D
          MOVE      ZERO,FMFPC13D
.
          MOVE      ZERO,FMFPABFD
          MOVE      ZERO,FMFPA01D
          MOVE      ZERO,FMFPA02D
          MOVE      ZERO,FMFPA03D
          MOVE      ZERO,FMFPA04D
          MOVE      ZERO,FMFPA05D
          MOVE      ZERO,FMFPA06D
          MOVE      ZERO,FMFPA07D
          MOVE      ZERO,FMFPA08D
          MOVE      ZERO,FMFPA09D
          MOVE      ZERO,FMFPA10D
          MOVE      ZERO,FMFPA11D
          MOVE      ZERO,FMFPA12D
          MOVE      ZERO,FMFPA13D
.
          MOVE      ZERO,FMFPCOMD
.
. Fields in Thousands only Scale
.
          MOVE      ZERO,FMFPCBFT
          MOVE      ZERO,FMFPC01T
          MOVE      ZERO,FMFPC02T
          MOVE      ZERO,FMFPC03T
          MOVE      ZERO,FMFPC04T
          MOVE      ZERO,FMFPC05T
          MOVE      ZERO,FMFPC06T
          MOVE      ZERO,FMFPC07T
          MOVE      ZERO,FMFPC08T
          MOVE      ZERO,FMFPC09T
          MOVE      ZERO,FMFPC10T
          MOVE      ZERO,FMFPC11T
          MOVE      ZERO,FMFPC12T
          MOVE      ZERO,FMFPC13T
.
          MOVE      ZERO,FMFPABFT
          MOVE      ZERO,FMFPA01T
          MOVE      ZERO,FMFPA02T
          MOVE      ZERO,FMFPA03T
          MOVE      ZERO,FMFPA04T
          MOVE      ZERO,FMFPA05T
          MOVE      ZERO,FMFPA06T
          MOVE      ZERO,FMFPA07T
          MOVE      ZERO,FMFPA08T
          MOVE      ZERO,FMFPA09T
          MOVE      ZERO,FMFPA10T
          MOVE      ZERO,FMFPA11T
          MOVE      ZERO,FMFPA12T
          MOVE      ZERO,FMFPA13T
.
          MOVE      ZERO,FMFPCOMT
          RETURN
.************************************************************************
.* USDA - Update the surplus deficit account with the balance           *
.*                   called by : PRNT                                   *
.************************************************************************
USDA0000  PACK      KEY14,FMLALEDG,SDACCT
          CALL      RDFMFP2
          BRANCH    OVRCD,USDA1000
.
          ADD       ATOTSUR,FMFPABF 
          ADD       CTOTSUR,FMFPCBF
          MOVE      FMFPCBF,FMFPCBFD      * update brought forward feilds
          MOVE      FMFPABF,FMFPABFD
          MOVE      FMFPCBF,FMFPCBFT      * update brought forward feilds
          MOVE      FMFPABF,FMFPABFT
          CALL      UPFMFP2
          GOTO      USDA5000
.
USDA1000  CALL      MZER0000
          MOVE      FMLALEDG,FMFPCULD
          MOVE      SDACCT,FMFPCUAC
          MOVE      ATOTSUR,FMFPABF 
          MOVE      CTOTSUR,FMFPCBF
          MOVE      FMFPCBF,FMFPCBFD      * update brought forward feilds
          MOVE      FMFPABF,FMFPABFD
          MOVE      FMFPCBF,FMFPCBFT      * update brought forward feilds
          MOVE      FMFPABF,FMFPABFT
          CALL      WRFMFP2
.
USDA5000  PACK      KEY6,FMLALEDG,TOYEAR
          CALL      RDFMLC1
          BRANCH    OVRCD,USDA9999
          MOVE      ONE,FMLCYLC
          CALL      UPFMLC1
.
USDA9999  RETURN 
.******************************************************************************
.
          INC       FMSSTDCD           * FMS Acc. standard routines
          INC       FMSAMAIO/INC       * account master
          INC       FMSCSAIO/INC
          INC       FMSAMAKY
          INC       FMSFPSIO/INC       * financial period summary
          INC       FMSLMAIO/INC       * ledger master
          INC       FMSLMAKY
          INC       FMSLMCIO/INC       * financial year file
          INC       FMSLMCKY
