. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAAPS46                                             *
. * Desc      :   Input Bank Statement                                 *
. **********************************************************************
. * Date      :   13.09.90                                             *
. * Author    :   Neeriem Dye (IBA)                                    *
. * Mods      :                                                        *
. **********************************************************************
. * V9.05.B01 09.01.2006  Sandra Barcham   90333                       *
. *           The program will match the first receipt if finds only if*
. *           FMCOCSRL is set                                          *
. **********************************************************************
. * V9.03.01   08.01.2004  Mike Laming  CAR 45907                      *
. *            Add facility to Release individual Receipt Amounts when *
. *            switch FMCOCSRL is set (in RCOK0000 & UBRC0000)         *
. **********************************************************************
. * V9.02.00   03.05.2002 Glenn Saunders                               *
. *            Export to new release i.e. v9.02 from vf.09             *
. **********************************************************************
. * VF.00.01   05.01.1999 Sandra Barcham                               *
. *            Fix audit file existing message                         *
. *            srf 636869                                              *
. **********************************************************************
.
          INC       FMSSTDDF   
.
          INC       FMSBNKFD/INC
          INC       FMSCHQFD/INC 
          INC       FMSCONFD/INC                                       *I-f1101
.
BANKAUD1  FILE      ASCII, FIXED=38
BANKAUD2  FILE      ASCII, FIXED=38
.
AMOUNT    FORM      12.2
CHQTOTAL  FORM      12.2
F15       FORM      15
D15       DIM       15
REFER     DIM       15
CHQDATE   DIM       8
FINDDATE  DIM       8
PRESFLAG  FORM      1
.
BCHQ      DIM       15
BCHQNUM   DIM       15
BDATE     DIM       6
BKLED     DIM       2
.
CHEQUE    FORM      8
CURDATE   DIM       8
.
FDAY      FORM      2
FMTH      FORM      2
FYRR      FORM      2
FINAMT    FORM      1
FIRSTPG   FORM      "0"
FORMAT    INIT      "(999,999,999,999.99 "
FORMTOT   INIT      "(99,999,999,999,999.99 "
.
IOFLAG    FORM      1
.
N         INIT      "N"
NOPRINT   FORM      "1"
.
OPT       FORM      1
OUTFORM   DIM       20
OUTTOT    DIM       23
.
PAGENO    FORM      3
.
RDATE     DIM       6
.
SSIZE     FORM      "24"
.
TMPYRR    FORM      2
TOTCHEQ   FORM      14.2
.
VERT      FORM      2
.
Y         INIT      "Y"
.
PRGID     INIT      "IBAAPS46"
PRGNAM    INIT      "Input Bank Statement"
VERSION   INIT      "V12.00.00"
.
          CALL      DISPHEAD
          CALL      INIT0000
.
.
. ENTER THE CHEQUE ACCOUNT NUMBER TO GIVE A VALID CASH AT BANK CODE
.
MAIN0000  DISPLAY   *P1:4,*EF,"Cheque Account Code : "
          MOVE      "23",CCOL
          MOVE      "4",CVERT
          UNPACK    CURDATE,CCENT,CYEAR,CMON,CDAY
          CALL      KCHQFM00
          BRANCH    EXIT,END,END
          DISPLAY   *PCCOL:CVERT,*V2LON,FMCHCHQ,SP1,*HOFF,FMCHDES

          MOVE      FMCHCHQ,BCHQNUM
.
MAIN0200  DISPLAY   *P1:3,*EL,*P1:6,*EF:
                    *V2LON,*P1:6,"0",*P1:7,"1",*P1:8,"2",*HOFF:
                    *P2:6," = Exit":
                    *P2:7," = Receipts":
                    *P2:8," = Cheques"
.
MAIN0300  KEYIN     *P1:10,"Please Select : ",*V2LON,OPT
.
          COMPARE   ZERO TO OPT
          GOTO      MAIN0000 IF EQUAL
.
          BRANCH    OPT,RECP,CHEQ
          BEEP
          GOTO      MAIN0300
.
.      INPUT BANK CHEQUES
.
CHEQ      DISPLAY   *P70:3,*EL,*V2LON,"Cheques",*P1:6,*EF,*HOFF,*ULON:
                    *P4:6,"Date",*P12:6,"Cheque No.",*P34:6,"Amount"
          MOVE      "8" TO VERT
CHQNO     COMPARE   SSIZE TO VERT
          GOTO      GETDATE IF LESS
.
          DISPLAY   *P1:8,*EF,*V2LON,CPCDATE:
                    *P13:8,CHEQUE,*P25:8,AMOUNT,*P51:8,"* Presented *"
          MOVE      "9" TO VERT
.
GETDATE   MOVE      VERT,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:24,*EL,"Hit ",*V2LON,"ENTER",*HOFF," to accept":
                    " current date or type in required date."
          CALL      KEYDATE            * keyin date
          BRANCH    CQUEST,GETDATE    * no question mark
          BRANCH    OVRCD,GETDATE
          PACK      CHQDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
GETCHEQ   KEYIN     *P14:VERT,*EF,*V2LON,CHEQUE:
                    *P14:VERT,*EL,*DV,CHEQUE
          COMPARE   ZERO,CHEQUE
          GOTO      CHQ0 IF NOT EQUAL
.
CCHQ      KEYIN     *P1:24,*EL,"End of input (",*V2LON,"Y",*HOFF,"/",*V2LON:
                    "N",*HOFF,") ? ",*V2LON,ANS
          REP       UPPLOW,ANS
          RESET     ANS
          CMATCH    Y TO ANS
          GOTO      MAIN0200 IF EQUAL
          CMATCH    N TO ANS
          GOTO      CHQNO IF EQUAL
          BEEP
          GOTO      CCHQ
.
CHQ0      MOVE      CHEQUE,F15
          MOVE      F15,D15
          CALL      CHOK0000
          BRANCH    EXIT,GETCHEQ,GETDATE
.
AMNTC     KEYIN     *P25:VERT,*EF,*V2LON,AMOUNT
.
          COMPARE   ZERO TO AMOUNT
          GOTO      CHQNO IF EQUAL
.
          DISPLAY   *P25:VERT,*EL,*V2LON,AMOUNT
.
          COMPARE   AMOUNT TO CHQTOTAL
          GOTO      CHKOK IF EQUAL
.
          BEEP
          KEYIN     *P1:24,*V2LON,"** Cheque Amount Mismatch (",*DV,CHQTOTAL:
                    ") **",*HOFF:
                    "    Hit ",*V2LON,"RETURN",*HOFF:
                    " to Re-enter Amount ",ANS,*P1:24,*EL
          GOTO      AMNTC
.
CHKOK     MOVE      "Y" TO ANS
          KEYIN     *P51:VERT,*EL,"Ok (",*V2LON,"Y",*HOFF,"/",*V2LON:
                    "N",*HOFF,") ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
          RESET     ANS
          CMATCH    N TO ANS
          GOTO      AMNTC IF EQUAL
.
          CALL      UBNK0000
          DISPLAY   *P51:VERT,*EL,*V2LON,"* Presented *"
          ADD       ONE TO VERT
          GOTO      CHQNO
.**********************************************************************
. Update Bank Rec with Presented
.**********************************************************************
UBNK0000  PACK      KEY44,FMCHCHQ,ZERO,FINDDATE,D15,SP70
          CALL      RSFMBN3
UBNK1000  CALL      RKFMBN3
          BRANCH    OVRCD,UBNK9000
          MATCH     FMCHCHQ,FMBNBNK
          GOTO      UBNK9000 IF NOT EQUAL
          MATCH     "0",FMBNTYP
          GOTO      UBNK9000 IF NOT EQUAL
          MATCH     FMBNDAT,FINDDATE
          GOTO      UBNK9000 IF NOT EQUAL
          MATCH     D15,FMBNREF
          GOTO      UBNK9000 IF NOT EQUAL
          MOVE      "1",FMBNPRE
          PACK      FMBNPDT,CHQDATE,SP70
          CALL      UPFMBN3
          GOTO      UBNK1000
.
UBNK9000  WRITE     BANKAUD1,SEQ;D15,CHQTOTAL,FMCHCHQ,CPCDATE
          RETURN
.
.**********************************************************************
. Check date and cheque number
.**********************************************************************
CHOK0000  PACK      KEY36,FMCHCHQ,ZERO,D15,SP70
          MOVE      ZERO,PRESFLAG
          MOVE      ZERO,CHQTOTAL
          CALL      RSFMBN2
.
CHOK1000  CALL      RKFMBN2
          BRANCH    OVRCD,CHOK8000
          MATCH     FMCHCHQ,FMBNBNK
          GOTO      CHOK8000 IF NOT EQUAL
          MATCH     "0",FMBNTYP
          GOTO      CHOK8000 IF NOT EQUAL
          MATCH     D15,FMBNREF
          GOTO      CHOK8000 IF NOT EQUAL
.
          MOVE      FMBNPRE,F1
          BRANCH    F1,CHOK1000,CHOK1000
          BRANCH    PRESFLAG,CHOK2000
          MOVE      ONE,PRESFLAG
          MOVE      FMBNDAT,FINDDATE
          MOVE      FMBNAMT,CHQTOTAL
          GOTO      CHOK1000
.
CHOK2000  MATCH     FMBNDAT,FINDDATE
          GOTO      CHOK1000 IF NOT EQUAL
          ADD       FMBNAMT,CHQTOTAL
          GOTO      CHOK1000
.
CHOK8000  BRANCH    PRESFLAG,CHOK9000
          MOVE      ONE,EXIT
          BRANCH    F1,CHOK8100,CHOK8200
.
          DISPLAY   *P1:24,*B,*EL,"Cheque not on File - ";
          CALL      HITENTER
          GOTO      CHOK9999
.
CHOK8100  DISPLAY   *P1:24,*B,*EL,"Cheque Already Presented - ";
          CALL      HITENTER
          GOTO      CHOK9999
.
CHOK8200  DISPLAY   *P1:24,*B,*EL,"Cheque Cancelled - ";
          CALL      HITENTER
          GOTO      CHOK9999
.
CHOK9000  MATCH     FINDDATE,CHQDATE
          GOTO      CHOK9500 IF NOT LESS

          DISPLAY   *P1:24,*EL,*B,"Date Presented must be Greater than ":
                       "the Cheque Date - ";
          CALL      HITENTER
          MOVE      TWO,EXIT
          GOTO      CHOK9999
CHOK9500  MOVE      ZERO,EXIT
CHOK9999  RETURN
.**********************************************************************
.      INPUT BANK RECEIPTS
.**********************************************************************
RECP      DISPLAY   *P70:3,*V2LON,"Receipts ",*P1:6,*EF,*HOFF,*ULON:
                    *P3:6,"Date",*P12:6,"Reference",*P37:6,"Amount"
          MOVE      "8" TO VERT
.
RECNO     COMPARE   SSIZE TO VERT
          GOTO      RECKY IF LESS
.
          DISPLAY   *P1:8,*EF,*V2LON,*P1:8,CPCDATE:
                    *P12:8,REFER,*P28:8,AMOUNT,*P51:8,"* Presented *"
          MOVE      "9" TO VERT
.
RECKY     MOVE      VERT,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:24,*EL,"Hit ",*V2LON,"ENTER",*HOFF," to accept":
                    " current date or type in required date."
          DISPLAY   *P1:VERT,*EL
          CALL      KEYDATE            * keyin date
          BRANCH    CQUEST,GETDATE    * no question mark
          BRANCH    OVRCD,GETDATE
          PACK      CHQDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",CHQDATE
          CALL      PACDATE
.
GETREF    KEYIN     *P12:VERT,*EL,"_______________":
                    *P12:VERT,*V2LON,REFER:
                    *P12:VERT,*V2LON,*DV,REFER
          ENDSET    REFER
          APPEND    SP70,REFER
          RESET     REFER
          MATCH     SP70,REFER
          GOTO      CREP IF EQUAL
.
GETAMT    KEYIN     *P28:VERT,*EF,*V2LON,AMOUNT
          COMPARE   ZERO TO AMOUNT
          GOTO      AMTOK IF NOT EQUAL
.
CREP      KEYIN     *P1:24,*EL,"End of input (",*V2LON,"Y",*HOFF,"/",*V2LON:
                    "N",*HOFF,") ? ",*V2LON,ANS
          REP       UPPLOW,ANS
          RESET     ANS
          CMATCH    Y TO ANS
          GOTO      MAIN0200 IF EQUAL
          CMATCH    N TO ANS
          GOTO      RECKY IF EQUAL
          BEEP
          GOTO      CREP
.
AMTOK     DISPLAY   *P28:VERT,*EL,*V2LON,AMOUNT
.
          CALL      RCOK0000
          BRANCH    EXIT,GETREF,RECKY
          COMPARE   AMOUNT TO CHQTOTAL
          GOTO      VERFRC IF EQUAL
.
          BEEP
          KEYIN     *P1:24,*V2LON,"** Receipt Amount Mismatch (",*DV,CHQTOTAL:
                    ") **",*HOFF:
                    "    Hit ",*V2LON,"RETURN",*HOFF:
                    " to Re-enter Amount ",ANS,*P1:24,*EL
          GOTO      GETAMT
.
VERFRC    MOVE      "Y" TO ANS
          KEYIN     *P51:VERT,*EL,"Ok (",*V2LON,"Y",*HOFF,"/",*V2LON:
                    "N",*HOFF,") ? ",*V2LON,ANS
          REP       UPPLOW,ANS
          RESET     ANS
          CMATCH    N TO ANS
          GOTO      RECKY IF EQUAL
.
          CALL      UBRC0000
.
          DISPLAY   *P51:VERT,*EL,*V2LON,"* Presented *"
          ADD       ONE TO VERT
          GOTO      RECNO
.**********************************************************************
. Check date and Receipt number
.**********************************************************************
RCOK0000  PACK      KEY36,FMCHCHQ,ONE,REFER,SP70
          MOVE      ZERO,PRESFLAG
          MOVE      ZERO,CHQTOTAL
          CALL      RSFMBN2
RCOK1000  CALL      RKFMBN2
          BRANCH    OVRCD,RCOK8000
          MATCH     FMCHCHQ,FMBNBNK
          GOTO      RCOK8000 IF NOT EQUAL
          MATCH     "1",FMBNTYP
          GOTO      RCOK8000 IF NOT EQUAL
          MATCH     REFER,FMBNREF
          GOTO      RCOK8000 IF NOT EQUAL
.
          MOVE      FMBNPRE,F1
          BRANCH    F1,RCOK1000,RCOK1000
....      BRANCH    PRESFLAG,RCOK2000       * part of redundant code    D-f1101
.
          MATCH     CHQDATE,FMBNDAT         * check if reqd date
          GOTO      RCOK1000 IF NOT EQUAL
.
.* ****************************************** Start of Changes         *C-f1101
.....     MOVE      ONE,PRESFLAG
.....     MOVE      FMBNDAT,FINDDATE
.....     MOVE      FMBNAMT,CHQTOTAL
.....     GOTO      RCOK1000
.....
....2000  MATCH     FMBNDAT,FINDDATE
.....     GOTO      RCOK1000 IF NOT EQUAL
.....     ADD       FMBNAMT,CHQTOTAL
.....     GOTO      RCOK1000
.
          COMPARE   ONE,FMCOCSRL            * check if indiv.release
          IF        @EQUAL
            COMPARE   AMOUNT,FMBNAMT        * check if reqd. amount of receipt
            GOTO      RCOK1000 IF NOT EQUAL * bypass if not reqd release
          ENDIF
.
          MOVE      ONE,PRESFLAG            * set "found one" flag
          MOVE      FMBNDAT,FINDDATE
          ADD       FMBNAMT,CHQTOTAL
.
          COMPARE   ONE,FMCOCSRL
          GOTO      RCOK1000 IF NOT EQUAL
.
.* ******************************************   End of Changes         *C-f1101
.
RCOK8000  BRANCH    PRESFLAG,RCOK9000
          MOVE      ONE,EXIT
          BRANCH    F1,RCOK8100,RCOK8200
.
          DISPLAY   *P1:24,*B,*EL,"Receipt not on File - ";
          CALL      HITENTER
          GOTO      RCOK9999
.
RCOK8100  DISPLAY   *P1:24,*B,*EL,"Receipt Already Presented - ";
          CALL      HITENTER
          GOTO      RCOK9999
.
RCOK8200  DISPLAY   *P1:24,*B,*EL,"Receipt Cancelled - ";
          CALL      HITENTER
          GOTO      RCOK9999
.
RCOK9000  MATCH     FINDDATE,CHQDATE
          GOTO      RCOK9500 IF NOT LESS
          GOTO      RCOK9500 IF EQUAL

          DISPLAY   *P1:24,*EL,*B,"Date Presented must be Greater than ":
                       "the Receipt Date - ";
          CALL      HITENTER
          MOVE      TWO,EXIT
          GOTO      RCOK9999
RCOK9500  MOVE      ZERO,EXIT
RCOK9999  RETURN
.**********************************************************************
. Update Bank Rec with Presented
.**********************************************************************
UBRC0000  PACK      KEY44,FMCHCHQ,ONE,FINDDATE,REFER,SP70
          CALL      RSFMBN3
.
UBRC1000  CALL      RKFMBN3
          BRANCH    OVRCD,UBRC9000
          MATCH     FMCHCHQ,FMBNBNK
          GOTO      UBRC9000 IF NOT EQUAL
          MATCH     "1",FMBNTYP
          GOTO      UBRC9000 IF NOT EQUAL
          MATCH     FMBNDAT,FINDDATE
          GOTO      UBRC9000 IF NOT EQUAL
          MATCH     REFER,FMBNREF
          GOTO      UBRC9000 IF NOT EQUAL
.
.* ****************************************** Start of Addition        *I-f1101
          COMPARE   ONE,FMCOCSRL            * check if indiv.release
          GOTO      UBRC1500 IF NOT EQUAL
.
          MATCH     "1",FMBNPRE
          GOTO      UBRC1000 IF EQUAL
.
          COMPARE   CHQTOTAL,FMBNAMT      * check if reqd. amount of receipt
          GOTO      UBRC1000 IF NOT EQUAL * bypass if not reqd release
.
.* ******************************************   End of Addition        *I-f1101
.
UBRC1500  MOVE      "1",FMBNPRE
          PACK      FMBNPDT,CHQDATE,SP70
          CALL      UPFMBN3
.
          COMPARE   ONE,FMCOCSRL
          GOTO      UBRC1000 IF NOT EQUAL
.
UBRC9000  WRITE     BANKAUD2,SEQ;CPCDATE,CHQTOTAL,FMCHCHQ,REFER
          RETURN
.
.**********************************************************************
.            PRINT OUT THE AUDIT FILES
.**********************************************************************
END       COMPARE   ZERO TO VERT
          GOTO      THEEND IF EQUAL
.
          WEOF      BANKAUD1,SEQ
          WEOF      BANKAUD2,SEQ
.
          DISPLAY   *P56:24,*EL,"Closing bankaud1";
.
          DISPLAY   *P64:24,*EL,"bankaud2";
.
PRINTAUD
END1      KEYIN     *P1:4,*EF,"Would you like a printout (",*V2LON,"Y":
                    *HOFF,"/",*V2LON,"N",*HOFF,") ? ",*V2LON,ANS
          REP       UPPLOW,ANS
.
          MATCH     N,ANS
          GOTO      THEEND IF EQUAL
          MATCH     Y,ANS
          GOTO      END2 IF EQUAL
.
          BEEP
          GOTO      END1
.
END2      DISPLAY   *P1:6,*EL,"Processing Cheque No. : ":
                    *P56:24,*EL,"Opening bankaud1";
          OPEN      BANKAUD1,"bankaud1"
.
          DISPLAY   *P64:24,*EL,"bankaud2";
          OPEN      BANKAUD2,"bankaud2"
.
          DISPLAY   *P1:24,*EL;
          MOVE      "99" TO CLNO
          MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
          MOVE      ZERO TO TOTCHEQ
          MOVE      ONE TO NOPRINT
          MOVE      ONE TO FIRSTPG
.
NEXT1     
          READ      BANKAUD1,SEQ;D15,CHQTOTAL,FMCHCHQ,CPCDATE
          GOTO      REP2 IF OVER
.
          MOVE      ZERO TO NOPRINT
          BRANCH    FIRSTPG TO NEWPAGE1
          COMPARE   "63" TO CLNO
          GOTO      SAMEPG1 IF LESS
          PRINT     *6,"|",*19,"|",*37,"|",*62,"|",*80,"|",*N:
                    *6,"*----------------------------------------":
                       "---------------------------------*"
.
NEWPAGE1  CALL      HEADC
          MOVE      ZERO TO FIRSTPG
.
SAMEPG1   MOVE      FORMAT,OUTFORM
          FEDIT     CHQTOTAL,OUTFORM
          PRINT     *6,"| ",CPCDATE,*19,"| ",D15,*37,"|   ":
                    OUTFORM,*62,"| ",FMCHCHQ,*80,"|"
          ADD       ONE TO CLNO
          ADD       CHQTOTAL TO TOTCHEQ
          DISPLAY   *P25:6,*EL,*V2LON,D15;
          GOTO      NEXT1
.
REP2      BRANCH    NOPRINT TO REP2A
          MOVE      FORMTOT,OUTTOT
          FEDIT     TOTCHEQ,OUTTOT
          PRINT     *6,"|",*19,"|",*37,"|",*62,"|",*80,"|",*N:
                    *6,"*----------------------------------------":
                       "---------------------------------*",*N:
                    *25,"Total",*37,"|",OUTTOT,*62,"|",*N:
                    *37,"*------------------------*"
REP2A     MOVE      ONE TO NOPRINT
          MOVE      "99" TO CLNO
          MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
          MOVE      ZERO TO TOTCHEQ
          MOVE      ONE TO FIRSTPG
          DISPLAY   *P1:8,*EL,"Processing Receipt Amount : "
.
NEXT2     
          READ      BANKAUD2,SEQ;CPCDATE,CHQTOTAL,FMCHCHQ,REFER
          GOTO      ENDRECP IF OVER
.
          MOVE      ZERO TO NOPRINT
          BRANCH    FIRSTPG TO NEWPAGE2
          COMPARE   "63" TO CLNO
          GOTO      SAMEPG2 IF LESS
          PRINT     *6,"|",*19,"|",*37,"|",*62,"|",*80,"|",*N:
                    *6,"*-----------------------------------------------":
                       "------------------------*"
.
NEWPAGE2  CALL      HEADR
          MOVE      ZERO TO FIRSTPG
.
SAMEPG2   MOVE      FORMAT,OUTFORM
          FEDIT     CHQTOTAL,OUTFORM
          PRINT     *6,"| ",CPCDATE,*19,"| ",REFER,*37,"|   ",OUTFORM:
                    *62,"| ",FMCHCHQ,*80,"|"
          ADD       ONE TO CLNO
          ADD       CHQTOTAL TO TOTCHEQ
          DISPLAY   *P30:8,*EL,*V2LON,OUTFORM
          GOTO      NEXT2
.
ENDRECP   BRANCH    NOPRINT TO ENDIT
          MOVE      FORMTOT,OUTTOT
          FEDIT     TOTCHEQ,OUTTOT
          PRINT     *6,"|",*19,"|",*37,"|",*62,"|",*80,"|",*N:
                    *6,"*-------------------------------------------------":
                       "------------------------*",*N:
                    *25,"Total",*37,"|",OUTTOT,*62,"|",*N:
                    *37,"*------------------------*"
          GOTO      ENDIT
+
.
.            HEADINGS
.
.            HEADING FOR CHEQUES
.
HEADC     CALL      HEAD80
          PRINT     *25,"Presented Cheques",*N:
                    *6,"*-------------------------------------------------":
                       "------------------------*",*N:
                    *6,"|   Date     | Cheque Number   |                A":
                       "mount  | Cheque Ac. No.  |",*N:
                    *6,"*-------------------------------------------------":
                       "------------------------*",*N:
                    *6,"|            |                 |                  ":
                       "      |                 |"
          MOVE      "7" TO CLNO
          RETURN
.
.            HEADING FOR RECEIPTS
.
HEADR     CALL      HEAD80
          PRINT     *25,"Presented Receipts",*N,*N:
                    *6,"*------------------------------------------------":
                       "-------------------------*",*N:
                    *6,"|   Date     | Receipt Ref.    |                A":
                       "mount  | Cheque Ac. No.  |",*N:
                    *6,"*-------------------------------------------------":
                       "------------------------*",*N:
                    *6,"|            |                 |                 ":
                       "       |                 |"
          MOVE      "7" TO CLNO
          RETURN
+
ENDIT     PRINT     *N,*N,*41,"** END OF REPORT **"
.
THEEND    CLOSE     BANKAUD1
          PREP      BANKAUD1,"bankaud1"
          CLOSE     BANKAUD1
.
          CLOSE     BANKAUD2
          PREP      BANKAUD2,"bankaud2"
          CLOSE     BANKAUD2
.
EXIT      CHAIN     PGM
.
AUDERR    BEEP
          KEYIN     *P1:24,*EL,"Audit File Existing. Reprint and Continue (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,"). ",*V2LON,ANS
          REP       UPPLOW,ANS
          MATCH     "Y",ANS
          GOTO      PRINTAUD IF EQUAL
          GOTO      EXIT
.
NOFILE    MOVE      ONE,IOFLAG
          RETURN
.****************************************
.   Open Files And Initialize 
.****************************************
INIT0000  MOVE      ONE,CNOUNDLN
.
INIT1000  DISPLAY   *P56:24,"Opening fmsbnkaf";
          OPEN      FMSBNKA1,"fmsbnkaf"
          OPEN      FMSBNKA2,"fmsbnkaf"
          OPEN      FMSBNKA3,"fmsbnkaf"
          OPEN      CONTROLF,"controlf"                                *I-f1101
.
          CALL      RDFMCO66                     * Read Sector 66      *I-f1101
.
          PACK      CURDATE,CCC,CYY,CMM,CDD      * todays date
          REP       " 0",CURDATE
          MOVE      ONE,CCENTRY                  * use centries in keydate
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
.
          DISPLAY   *P64:24,*EL,"fmschqaf";
          OPEN      FMSCHQA1,"fmschqaf"
.
INIT5000  DISPLAY   *P64:24,*EL,"bankaud1";
          MOVE      ZERO TO VERT
          MOVE      ZERO TO PAGENO
          MOVE      ZERO,IOFLAG
          TRAP      NOFILE IF IO
          OPEN      BANKAUD1,"bankaud1"
          TRAPCLR   IO
          COMPARE   ZERO,IOFLAG
          GOTO      AUDERR IF EQUAL
          PREP      BANKAUD1,"bankaud1"
.
          DISPLAY   *P64:24,*EL,"bankaud2";
          MOVE      ZERO,IOFLAG
          TRAP      NOFILE IF IO
          OPEN      BANKAUD2,"bankaud2"
          TRAPCLR   IO
          COMPARE   ZERO,IOFLAG
          GOTO      AUDERR IF EQUAL
          PREP      BANKAUD2,"bankaud2"
.
          RETURN
.
RCHQFM00  DISPLAY   *P1:4,*EF,"Cheque Account Code : "
          RETURN
.
          INC       FMSBNKIO/INC
          INC       FMSCHQIO/INC
          INC       FMSCONIO/INC                                       *I-f1101
          INC       FMSCHQKY
          INC       FMSSTDCD     
.
