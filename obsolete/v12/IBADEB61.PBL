.-----------------------------------------------------------------------------
          INC       STDGENDF     * Required Include
.
VERSION   INIT      "V12.00.00"
.-----------------------------------------------------------------------------
. V9.03.01  05.08.2003 Henry Son CAR 41958
.           Oustanding Invoice report not matching Debt Trial balance.
.-------------------------------------------------------
. V9.02.00  03.05.2002  Glenn Saunders
.           Export to new release i.e. v9.02 from vf.09
.-----------------------------------------------------------------------------
. VF.09.01  28.02.2002  Glenn Saunders   srf 15126
.           Re-compile for changes in DEBDKIKY
.-----------------------------------------------------------------------------
. VF.01.00  07.03.2000 Charles Handaya
.           Change to Standard Export File  srf 638148
.-----------------------------------------------------------------------------
. VF.00.01  28.10.1999 Sandra Barcham
.           Fix Print of Outstanding Amount
.           srf 635427
.-----------------------------------------------------------------------------
EXPORTFL  FILE      ASCII, FIXED=400     * Export File (Adjust Length to Suit)
EXPORTFN  DIM       8                    * Export File Name
TABDELM   EQU       0x09                 * Tab Delimiter
.
ASATXXX   DIM       8        * Date Outstanding must be > .
.                            * Calculated from X Days Outstanding & As At Date
BOUT      FORM      12.2
CFDBSQLA  DIM       20        77  Enquiries SQL Form A
CFDBSQLB  DIM       20        97  Enquiries SQL Form B
CODEDESC  DIM       35
COMMAND   DIM       127
ENDDEB    DIM       8
ENDDEP    DIM       8
ENDPTY    DIM       8
F12P2     FORM      12.2
FALLDEP   FORM      1
FALLPTY   FORM      1
FILENAME  DIM       8
G30       FORM      12.2
G60       FORM      12.2
G90       FORM      12.2
GCUR      FORM      12.2
GOUT      FORM      12.2
IDATE     DIM       10
INVTOTAL  FORM      12.2
ISBUILD   INIT      "isbuild "
LASTDEB   DIM       8                       * Last Debtor              *I-F1101
LASTILN   DIM       3                       * Last Line Item           *I-F1101
LASTINV   DIM       12                      * Last Invoice             *I-F1101
LASTFLG   FORM      1
LASTSEQ   DIM       8
NEXTDEB   DIM       8
QUOTE     INIT      "#""
STRDEB    DIM       8
STRDEP    DIM       8
STRPTY    DIM       8
T30       DIM       13
T60       DIM       13
T90       DIM       13
TCUR      DIM       13
TOUT      DIM       13
TPAYMTS   FORM      12.2                    * Total Payements          *I-F1101
.
DEBTMPA1  IFILE    SQL, FIXED=41, KEY="U1-8,9-16,17-17,18-29,30-32"    *C-F1101
.
DBTMSEQ   DIM       8      8        1     Sequence
DBTMDEB   DIM       8      8        9     Debtor
DBTMDTY   DIM       1      1        17    Doc. Type 1=Inv, 2=Cr, 3=Pay, 4=Adj
DBTMDOC   DIM       12     12       18    Document Reference
DBTMDLN   DIM       3      3        30    Document Line Number
DBTOTAL   FORM      12.2   8        33    Total amount                 *I-F1101
.
.End                                41
.-----------------------------------------------------------------------------
.@@@@@ Start of Generated Code @@@@@
. NB - Code & Comments Included Before the above line 
.      will not be overwritten by the generation of a 
.      program. The STDGENDF file and VERSION must appear
.      before the line. 
.-----------------------------------------
.   Program       :   IBADEB61
.   Function      :   Outstanding Invoice Report/Export       
.   Generated  On :   13/12/2005
.-----------------------------------------
.=========================
.File Description Includes
.-------------------------
          INC       DEBCODFD/INC
          INC       DEBDBTFD/INC
          INC       DEBDEPFD/INC
          INC       DEBDKIFD/INC
          INC       DEBFDTFD/INC
          INC       DEBITMFD/INC
          INC       DEBOUTDF    
          INC       DEBPTYFD/INC
.=========================
.Local Variable Definition
.-------------------------
FROMDEB   DIM      40 * Scr 00 Fld     4
TODEB     DIM      40 * Scr 00 Fld     5
FROMDEP   DIM      40 * Scr 00 Fld     6
TODEP     DIM      40 * Scr 00 Fld     7
FROMPTY   DIM      40 * Scr 00 Fld     8
TOPTY     DIM      40 * Scr 00 Fld     9
REPTSEQ   FORM      1 * Scr 00 Fld    10
SEQDESC   DIM      15 * Scr 00 Fld    11
OUTDAYS   FORM      5 * Scr 00 Fld    12
OUTDOLL   FORM     12.2 * Scr 00 Fld    13
EXPORTYN  FORM      1 * Scr 00 Fld    15
.
ACCEPT    FORM      1
SACCEPT   FORM      1
.
PRGID     INIT      "IBADEB61"
PRGNAM    INIT      "Outstanding Invoice Report/Export       "
.
.--------------------------------
. MAINLINE - Controlling Logic
.--------------------------------
          TOPIC     PRGID
          MOVE      PORT,PARENTID
          CLOCK     PORT,DIM2
          MOVE      DIM2,PORT
ML0000
          CALL      INIT0000
          CALL      SC00L000
          CHAIN     PGM
.---------------------------------------
. INIT - Open Files & Display Heading
.---------------------------------------
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CCENTRY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          DISPLAY   *P56:24,"Opening scrsbga1";
          OPEN      SCRSBGA1,"scrsbgaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA1,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA2,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrslta1";
          OPEN      SCRSLTA1,"scrsltaf"
          MOVE      "   81",AUDTSECT
          MOVE      "  9",AUDTPOST
          READ      CONTROLF,AUDTSECT;*AUDTPOST,AUDTFLAG
          IF        AUDTFLAG=1
            DISPLAY   *P56:24,"Opening debaudcd";
            OPEN      DEBAUDCD,"debaudcd"
          ENDIF
          MOVE      "   81",AUDTSECT
          MOVE      " 10",AUDTPOST
          READ      CONTROLF,AUDTSECT;*AUDTPOST,AUDTFLAG
          IF        AUDTFLAG=1
            DISPLAY   *P56:24,"Opening debauddb";
            OPEN      DEBAUDDB,"debauddb"
          ENDIF
          MOVE      "   81",AUDTSECT
          MOVE      " 11",AUDTPOST
          READ      CONTROLF,AUDTSECT;*AUDTPOST,AUDTFLAG
          IF        AUDTFLAG=1
            DISPLAY   *P56:24,"Opening debaudde";
            OPEN      DEBAUDDE,"debaudde"
          ENDIF
          MOVE      "   81",AUDTSECT
          MOVE      " 12",AUDTPOST
          READ      CONTROLF,AUDTSECT;*AUDTPOST,AUDTFLAG
          IF        AUDTFLAG=1
            DISPLAY   *P56:24,"Opening debaudit";
            OPEN      DEBAUDIT,"debaudit"
          ENDIF
          MOVE      "   81",AUDTSECT
          MOVE      " 13",AUDTPOST
          READ      CONTROLF,AUDTSECT;*AUDTPOST,AUDTFLAG
          IF        AUDTFLAG=1
            DISPLAY   *P56:24,"Opening debaudpt";
            OPEN      DEBAUDPT,"debaudpt"
          ENDIF
          DISPLAY   *P56:24,"Opening debcodaf";
          OPEN      DEBCODA1,"debcodaf"
          DISPLAY   *P56:24,"Opening debcodaf";
          OPEN      DEBCODA2,"debcodaf"
          DISPLAY   *P56:24,"Opening debdbtaf";
          OPEN      DEBDBTA1,"debdbtaf"
          DISPLAY   *P56:24,"Opening debdbtaf";
          OPEN      DEBDBTA2,"debdbtaf"
          DISPLAY   *P56:24,"Opening debdbtaf";
          OPEN      DEBDBTA3,"debdbtaf"
          DISPLAY   *P56:24,"Opening debdepaf";
          OPEN      DEBDEPA1,"debdepaf"
          DISPLAY   *P56:24,"Opening debdepaf";
          OPEN      DEBDEPA2,"debdepaf"
          DISPLAY   *P56:24,"Opening debdkiaf";
          OPEN      DEBDKIA1,"debdkiaf"
          DISPLAY   *P56:24,"Opening debdkiaf";
          OPEN      DEBDKIA2,"debdkiaf"
          DISPLAY   *P56:24,"Opening debfdtaf";
          OPEN      DEBFDTA1,"debfdtaf"
          DISPLAY   *P56:24,"Opening debfdtaf";
          OPEN      DEBFDTA2,"debfdtaf"
          DISPLAY   *P56:24,"Opening debfdtaf";
          OPEN      DEBFDTA3,"debfdtaf"
          DISPLAY   *P56:24,"Opening debfdtaf";
          OPEN      DEBFDTA4,"debfdtaf"
          DISPLAY   *P56:24,"Opening debfdtaf";
          OPEN      DEBFDTA5,"debfdtaf"
          DISPLAY   *P56:24,"Opening debfdtaf";
          OPEN      DEBFDTA7,"debfdtaf"
          DISPLAY   *P56:24,"Opening debfdtaf";
          OPEN      DEBFDTA8,"debfdtaf"
          DISPLAY   *P56:24,"Opening debitmaf";
          OPEN      DEBITMA1,"debitmaf"
          DISPLAY   *P56:24,"Opening debitmaf";
          OPEN      DEBITMA2,"debitmaf"
          DISPLAY   *P56:24,"Opening debitmaf";
          OPEN      DEBITMA3,"debitmaf"
          DISPLAY   *P56:24,"Opening debptyaf";
          OPEN      DEBPTYA1,"debptyaf"
          DISPLAY   *P56:24,"Opening debptyaf";
          OPEN      DEBPTYA2,"debptyaf"
          DISPLAY   *P56:24,"Opening debptyaf";
          OPEN      DEBPTYA3,"debptyaf"
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.
.------------------------------------
. Called for Print Screen Routine 
.------------------------------------
PRTSVARS
          MATCH     "00",SCRID
          IF         @EQUAL
            CALL       PS00L000   * Set up Report Parameters           
          ENDIF
          RETURN
.--------------------------------
. Screen - Set up Report Parameters           
.--------------------------------
SC00L000
SC00L100
          MOVE      "00",SCRID
          MOVE      ZERO,PRCOL
          MOVE      ZERO,PRVERT
          MOVE      "    1",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SCPSCOL,PRCOL
            MOVE     SCPSROW,PRVERT
          ENDIF
          CALL      SBACK000 * Display Background
.
SC00L200
          CALL      CF00L000 * Clear Fields
          CALL      DEFPAR00
SC00L300
          CALL      KY00L000 * Enter Key Fields
          BRANCH    EXIT,SC00L999,SC00L999
SC00L400
          CALL      WP00L000 * Load any WP Details
SC00L500
          CALL      LM00L000 * Load any Batch Details
SC00L600
          CALL      DF00L000 * Display Fields
SC00L700
          CALL      SF00L000 * Select Fields
          BRANCH    EXIT,SC00L100,SC00L100,SC00L200
SC00L800
SC00L900
SC00L999  RETURN
.--------------------------------------------------------
. Clear Fields for Screen : Set up Report Parameters           
.-------------------------------------------------------
CF00L000
          MOVE      SP70,FROMDEB 
          MOVE      SP70,TODEB   
          MOVE      SP70,FROMDEP 
          MOVE      SP70,TODEP   
          MOVE      SP70,FROMPTY 
          MOVE      SP70,TOPTY   
          MOVE      ZERO,REPTSEQ 
          MOVE      SP70,SEQDESC 
          MOVE      ZERO,OUTDAYS 
          MOVE      ZERO,OUTDOLL 
          MOVE      SP70,ASATDATE
          MOVE      ZERO,EXPORTYN
CF00L999  RETURN
.--------------------------------------------------------
. Enter Key Fields for Set up Report Parameters           
.--------------------------------------------------------
KY00L000
          MOVE      ZERO,EXIT
          GOTO      KY00L999
.
KY00L900  MOVE      ONE,EXIT
KY00L999  RETURN
.---------------------------------------------------------
. Load WP files for : Set up Report Parameters           
.---------------------------------------------------------
WP00L000
          RETURN
.---------------------------------------------------------
. Load Batch Screen for : Set up Report Parameters           
.---------------------------------------------------------
LM00L000
          RETURN
.---------------------------------------------------------
. Display Fields for : Set up Report Parameters           
.---------------------------------------------------------
DF00L000
          BRANCH   ACCEPT,DF00L999
          PACK     KEY15,PRGID,SCRID,SP70
          CALL     RSSCPS1
DF00L010
          CALL     RKSCPS1
          BRANCH   OVRCD,DF00L900
          MATCH    PRGID,SCPSPID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SCRID,SCPSSID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SP70,SCPSMTY
          GOTO     DF00L010 IF NOT EQUAL
.
          CALL     PS00L000 * Display Field
          GOTO     DF00L010
DF00L900
DF00L999
          RETURN
.---------------------------------------------------------
. Display/Print Fields for : Set up Report Parameters           
.---------------------------------------------------------
PS00L000   PACK      VAR,SP70,SP70
          PACK     PRINTVAR,SP70,SP70
          MOVE     SCPSITM,FIELDNO
          BRANCH    FIELDNO,PS00L999,PS00L999,PS00L999,PS00L101:
                            PS00L102,PS00L103,PS00L104,PS00L105:
                            PS00L106,PS00L107,PS00L108,PS00L109:
                            PS00L110,PS00L111,PS00L112
          GOTO     PS00L999
.
PS00L101
          MOVE      FROMDEB,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L102
          MOVE      TODEB,VAR        
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L103
          MOVE      FROMDEP,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L104
          MOVE      TODEP,VAR        
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L105
          MOVE      FROMPTY,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L106
          MOVE      TOPTY,VAR        
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L107
          MOVE      REPTSEQ,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L108
          MOVE      SEQDESC,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L109
          MOVE      OUTDAYS,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L110
          MOVE      OUTDOLL,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L111
          UNPACK    ASATDATE,CCENT,CYEAR,CMON,CDAY          
          CALL      PACDATE
          MOVE      CPCDATE,VAR
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L112
          MOVE      NO,YESNO
          MOVE      ZERO,FORM2
          MOVE      EXPORTYN,FORM2   
          LOAD      YESNO,FORM2,YES
          MOVE      YESNO,VAR
          CALL      DISPITEM
          GOTO     PS00L999
PS00L999
          RETURN
.---------------------------------------------------------
. Selections for : Set up Report Parameters           
.---------------------------------------------------------
SF00L000  PACK     KEY13,PRGID,SCRID,SP70
          CALL     RSSCSL1
SF00L001  BRANCH   ACCEPT,SF00L020
.
SF00L005  CALL     MTSTQ000
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L006,SF00L101,SF00L102,SF00L103:
                            SF00L006,SF00L104,SF00L006,SF00L105:
                            SF00L006,SF00L106,SF00L006,SF00L107:
                            SF00L108,SF00L109,SF00L110
SF00L006  BEEP
          GOTO     SF00L005
SF00L020  CALL     RKSCSL1
          BRANCH   OVRCD,SF00L090
          MATCH    PRGID,SCSLPID
          GOTO     SF00L090 IF NOT EQUAL
          MATCH    SCRID,SCSLSID
          GOTO     SF00L090 IF NOT EQUAL
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L020,SF00L020,SF00L020,SF00L103:
                            SF00L020,SF00L104,SF00L020,SF00L105:
                            SF00L020,SF00L106,SF00L020,SF00L107:
                            SF00L108,SF00L109,SF00L110
          GOTO     SF00L020
SF00L090  MOVE     ZERO,ACCEPT
          GOTO     SF00L001
.
SF00L101
          MOVE      ZERO,EXIT
          GOTO      SF00L999
.
SF00L102
          CALL      REPT0000
          MOVE      ZERO,EXIT
          GOTO      SF00L999
.
SF00L103
.
.
          MOVE      "    5",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,TODEB   
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      RDEB0000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L103
            ENDIF
            BRANCH    EXIT,SF00L103,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      FROMDEB,VAR      
          CALL      DISPITEM
.
.
          MOVE      "    5",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      TODEB,VAR        
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L104
.
.
          MOVE      "    7",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,TODEP   
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      RDEP0000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L104
            ENDIF
            BRANCH    EXIT,SF00L104,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      FROMDEP,VAR      
          CALL      DISPITEM
.
.
          MOVE      "    7",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      TODEP,VAR        
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L105
.
.
          MOVE      "    9",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,TOPTY   
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      RPTY0000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L105
            ENDIF
            BRANCH    EXIT,SF00L105,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      FROMPTY,VAR      
          CALL      DISPITEM
.
.
          MOVE      "    9",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      TOPTY,VAR        
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L106
.
.
          MOVE      "   11",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,SEQDESC 
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      KRSEQ000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L106
            ENDIF
            BRANCH    EXIT,SF00L106,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      REPTSEQ,VAR      
          CALL      DISPITEM
.
.
          MOVE      "   11",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      SEQDESC,VAR      
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L107
          CALL     SELECTED
          MOVE      SP70,VAR
          CALL      DISPITEM
          KEYIN     *PCCOL:CVERT,*V2LON,*NOMASK,*RV,OUTDAYS 
          IF        SCPSMAN=1
            COMPARE   ZERO,OUTDAYS 
            IF        @EQUAL
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L107
            ENDIF
          ENDIF
          MOVE      OUTDAYS,VAR      
          CALL      DISPITEM
          GOTO     SF00L001
.
SF00L108
          CALL     SELECTED
          MOVE      SP70,VAR
          CALL      DISPITEM
          KEYIN     *PCCOL:CVERT,*V2LON,*NOMASK,*RV,OUTDOLL 
          IF        SCPSMAN=1
            COMPARE   ZERO,OUTDOLL 
            IF        @EQUAL
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L108
            ENDIF
          ENDIF
          MOVE      OUTDOLL,VAR      
          CALL      DISPITEM
          GOTO     SF00L001
.
SF00L109
          CALL      DEFDATE1
          CALL     SELECTED
          CALL      KEYDATE
          PACK      ASATDATE,CCENT,CYEAR,CMON,CDAY          
          UNPACK    ASATDATE,CCENT,CYEAR,CMON,CDAY          
          CALL      PACDATE
          MOVE      CPCDATE,VAR
          CALL      DISPITEM
          GOTO     SF00L001
.
SF00L110
          CALL     SELECTED
          CALL      YORN0000
          MOVE       FYESNO,EXPORTYN
          MOVE      NO,YESNO
          MOVE      ZERO,FORM2
          MOVE      EXPORTYN,FORM2   
          LOAD      YESNO,FORM2,YES
          MOVE      YESNO,VAR
          CALL      DISPITEM
          GOTO     SF00L001
SF00L990  MOVE      ONE,EXIT * Exit Char Restart
SF00L999  RETURN
.@@@@@ Function Restart Point @@@@@
.---------------------------------------
. Setup Defaults for Invoice Generation
.---------------------------------------
DEFPAR00  MOVE      "Start    ",FROMDEB
          MOVE      "Start    ",FROMDEP
          MOVE      "Start    ",FROMPTY
          MOVE      "Finish   ",TODEB
          MOVE      "Finish   ",TODEP
          MOVE      "Finish   ",TOPTY
          MOVE      SP70,STRDEB
          MOVE      SP70,STRDEP
          MOVE      SP70,STRPTY
          MOVE      Z70,ENDDEB
          MOVE      Z70,ENDDEP
          MOVE      Z70,ENDPTY
          PACK      ASATDATE,CCC,CYY,CMM,CDD
          REP       " 0",ASATDATE
          MOVE      ONE,REPTSEQ
          MOVE      ZERO,OUTDAYS
          MOVE      ZERO,OUTDOLL
          MOVE      "debtouts",EXPORTFN
          CALL      GRSEQ000
          RETURN
.
DEFDATE1  MOVE     CCC,CCENT
          MOVE     CYY,CYEAR
          MOVE     CMM,CMON
          MOVE     CDD,CDAY
          REP      " 0",CCENT
          REP      " 0",CYEAR
          REP      " 0",CMON
          REP      " 0",CDAY
          RETURN
.-----------------------------
. Print Trial Balance Report
.-----------------------------
REPT0000  CALL      OPNTMP00        * Make Temp File
          MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
          MOVE      "99",CLNO
.
          CALL      SETDAT00        * Set up Date for Outstanding > X Days
          CALL      CHKRAN00        * Check Ranges
.
          DISPLAY   *P1:24,*EL,"Extracting Transactions ..."
          MOVE      ZERO,SECTOR
          PACK      KEY8,STRDEB,SP70
          CALL      RDDBDB1
          IF        OVRCD=0
            CALL      RPDBDB1
          ENDIF
REPT1100  CALL      RKDBDB1
          BRANCH    OVRCD,REPT1900
          MATCH     DBDBDEB,ENDDEB
          GOTO      REPT1900 IF LESS
          ADD       ONE,SECTOR
          IF        SECTOR%100=0
            DISPLAY   *P40:24,SECTOR
          ENDIF
          CALL      EXTR0000    * Extract to Temp File for Sorting
          GOTO      REPT1100
.
. Print the report from the temp file.
.
REPT1900  CALL      MAKEXP00        * Create Export File
.
          MOVE      SP70,LASTSEQ
          PACK      KEY32,SP70
          CALL      RSDBTM1
REPT2000  CALL      RKDBTM1
          BRANCH    OVRCD,REPT9000
          MATCH     DBTMSEQ,LASTSEQ
          CALL      PTOT0000 IF NOT EQUAL
          CALL      PLIN0000
          GOTO      REPT2000
.
REPT9000  CALL      PTOT0000
          CALL      PEND0000
          CALL      CLSTMP00    * Delete Temp File
          CALL      TRAN0000
          MOVE      ZERO,EXIT
          GOTO      REPT9999
.
REPT9100  MOVE      FOUR,EXIT   * Restore Screen
          GOTO      REPT9999
.
REPT9999  RETURN
.------------------------------------------------------------
. Print End of Report
.------------------------------------------------------------
PEND0000  COMPARE   ZERO,CPAGENO
          CALL      PHED0000 IF EQUAL
          MOVE      "99,999,999.99",TOUT
          FEDIT     GOUT,TOUT
          PRINT     *103,"Report Total    ",TOUT
          CALL      UND132
          PRINT     *50,"*** End of Report ***"
          RETURN
.------------------------------------------------------------
. Print Summary Total
.------------------------------------------------------------
PTOT0000  MOVE      ZERO,LASTFLG
          MATCH     SP70,LASTSEQ
          GOTO      PTOT9000 IF EQUAL
.
          CALL      UND132
          MOVE      "99,999,999.99",TOUT
          FEDIT     BOUT,TOUT
          PRINT     CODEDESC,*110,"Total    ",TOUT
          CALL      UND132
.
PTOT9000  MOVE      DBTMSEQ,LASTSEQ
          MOVE      ZERO,BOUT
          BRANCH    REPTSEQ,PTOT9100,PTOT9200,PTOT9300,PTOT9400
.
PTOT9100  MOVE      DBTMSEQ,KEY8
          CALL      RDDBDB1
          MOVE      DBDBNA1,CODEDESC
          GOTO      PTOT9999
.
PTOT9200  MOVE      DBTMSEQ,KEY8
          CALL      RDDBDE1
          MOVE      DBDEDES,CODEDESC
          GOTO      PTOT9999
.
PTOT9300  MOVE      DBTMSEQ,KEY8
          CALL      RDDBPT1
          MOVE      DBPTDES,CODEDESC
          GOTO      PTOT9999
.
PTOT9400  MOVE      DBTMSEQ,KEY8
          CALL      RDDBIT1
          MOVE      DBITDES,CODEDESC
          GOTO      PTOT9999
.
PTOT9999  RETURN
.------------------------------------------------------------
. Print Line
.------------------------------------------------------------
PLIN0000  PACK      KEY24,DBTMDEB,DBTMDTY,DBTMDOC,DBTMDLN,SP70
          CALL      RDDBFD1
          BRANCH    OVRCD,PLIN9999
.
...       ASSIGN    DBFDTAX+DBFDTOT-DBFDPAID,CURRENT                   *D-F1101
.
          MOVE      DBTOTAL,CURRENT                                    *I-F1101
.
          MOVE      "99,999,999.99",TOUT
          FEDIT     CURRENT,TOUT
.
          ADD       CURRENT,BOUT     * Break Total
          ADD       CURRENT,GOUT     * Grand Total
.
          MOVE      DBFDDREF,KEY33
          MOVE      DBFDDEB,KEY8
          CALL      RDDBDB1
          MOVE      DBFDITM,KEY8
          CALL      RDDBIT1
          MOVE      DBITPTY,KEY8
          CALL      RDDBPT1
          MOVE      DBPTDEP,KEY8
          CALL      RDDBDE1
.
          IF        LASTFLG=0
            ADD       ONE,CLNO
            COMPARE   "50",CLNO
            CALL      PHED0000 IF NOT LESS
            MOVE      ONE,LASTFLG
            PRINT     CODEDESC,"(",DBTMSEQ,")"
          ENDIF
.
          ADD       ONE,CLNO
          COMPARE   "55",CLNO
          CALL      PHED0000 IF NOT LESS
.
          UNPACK    DBFDIDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          CALL      WRTEXP00
.
          BRANCH    REPTSEQ,PLIN1100,PLIN1200,PLIN1300,PLIN1400
.
PLIN1100  PRINT     *15,CPCDATE,SP1,DBFDINV,SP2,DBITDES,"(",DBFDITM,")":
                    SP1,KEY33,TOUT
          GOTO      PLIN1900
.
PLIN1200  MOVE      DBITDES,KEY33
          PRINT     *15,CPCDATE,SP1,DBFDINV,SP2,DBDBNA1,"(",DBDBDEB,")":
                    SP1,KEY33,TOUT
          GOTO      PLIN1900
.
PLIN1300  MOVE      DBITDES,KEY33
          PRINT     *15,CPCDATE,SP1,DBFDINV,SP2,DBDBNA1,"(",DBDBDEB,")":
                    SP1,KEY33,TOUT
          GOTO      PLIN1900
.
PLIN1400  PRINT     *15,CPCDATE,SP1,DBFDINV,SP2,DBDBNA1,"(",DBDBDEB,")":
                    SP1,KEY33,TOUT
          GOTO      PLIN1900
.
PLIN1900
PLIN9999  RETURN
.----------------------------------------------------------------------
. PHED0000 - Print Page Heading
.----------------------------------------------------------------------
PHED0000  CALL      HEAD132
          DISPLAY   *P1:24,*EL,"Printing Page ... ",CPAGENO
          UNPACK    ASATDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "Debtor Code From : ",FROMDEB," to ",TODEB:
                    "As At Date : ",CPCDATE
          PRINT     "Department From  : ",FROMDEP," to ",TODEP:
                    "Out. Days >: ",OUTDAYS
          PRINT     "Prod. Type From  : ",FROMPTY," to ",TOPTY:
                    "Out. Doll.>: ",OUTDOLL
          PRINT     "Report Sequence  : ",SEQDESC
          CALL      UND132
          BRANCH    REPTSEQ,PHED1100,PHED1200,PHED1300,PHED1400
.
PHED1100  PRINT     "Debtor        Inv. Date  Inv. Ref.   Sales Item         ":
                    "                           Debtors Reference          ":
                    "          Outstanding"
          GOTO      PHED1900
.
PHED1200  PRINT     "Department    Inv. Date  Inv. Ref.   Debtor             ":
                    "                           Sales Item                 ":
                    "          Outstanding"
          GOTO      PHED1900
.
PHED1300  PRINT     "Product Type  Inv. Date  Inv. Ref.   Debtor             ":
                    "                           Sales Item                 ":
                    "          Outstanding"
          GOTO      PHED1900
.
PHED1400  PRINT     "Sales Item    Inv. Date  Inv. Ref.   Debtor             ":
                    "                           Debtors Reference          ":
                    "          Outstanding"
          GOTO      PHED1900
.
PHED1900  CALL      UND132
          ADD       THREE,CLNO
          RETURN
.----------------------------------------------------------------------
. EXTR0000 - Extract Outstanding Transaction into Temp File
.----------------------------------------------------------------------
EXTR0000  PACK      KEY52,DBDBDEB,ASATDATE,Z70                         *C-F1101
          CALL      RSDBFD2            * Process all transactions paid after
EXTR1000  CALL      RKDBFD2            * the as at date.
          BRANCH    OVRCD,EXTR9999
          MATCH     DBFDDEB,DBDBDEB
          GOTO      EXTR9999 IF NOT EQUAL
.
          MATCH     DBFDIDAT,ASATDATE  * Ignore Invoices Dated After
          GOTO      EXTR1000 IF LESS   * as at Date
.
          CALL      RANGE000           * Check Department/Product Type
          BRANCH    EXIT,EXTR1000
.
          MOVE      DBFDDEB,LASTDEB    * Store Last Debtor number      *I-F1101
          MOVE      DBFDINV,LASTINV    * Store Last Invoice number     *I-F1101
          MOVE      DBFDILN,LASTILN    * Store Last Invoice Line       *I-F1101
          CALL      ADDP0000           * Calculate total amount        *I-F1101
.
...       MATCH     "1",DBFDDTY        * Process Invoices Only         *D-F1101
...       GOTO      EXTR1000 IF NOT EQUAL                              *D-F1101
...                                                                    *D-F1101
...       ASSIGN    DBFDTAX+DBFDTOT-DBFDPAID,CURRENT                   *D-F1101
.
          MOVE      TPAYMTS,CURRENT    * Total amt from ADDP0000       *I-F1101
.
          IF        OUTDOLL<>0
            IF        OUTDOLL>CURRENT
              GOTO      EXTR1000         * NOT Over X dollars Outstanding
            ENDIF
          ENDIF
          IF        OUTDAYS<>0
            MATCH     DBFDIDAT,ASATXXX   * Over X days Outstanding
            GOTO      EXTR1000 IF LESS
          ENDIF
.
          CALL      WRTMP000             * Passed all check write to Temp file
.
          GOTO      EXTR1000
.
EXTR9999  RETURN
.-------------------------------------------Start of changes vvvvvvvvvv*I-F1101
. ADDP0000 - Add up all Payements.
.----------------------------------------------------------------------
ADDP0000  MOVE      ZERO,TPAYMTS
          GOTO      ADDP1100           * On 1st entry already have rec.
.
ADDP1000  CALL      RKDBFD2
          BRANCH    OVRCD,ADDP9999
.
ADDP1100  MATCH     DBFDDEB,LASTDEB
          GOTO      ADDP9000 IF NOT EQUAL
.
          MATCH     DBFDIDAT,ASATDATE       * Ignore Invoices Dated After
          GOTO      ADDP1000 IF LESS        * as at Date
.
          MATCH     DBFDINV,LASTINV         * Different Invoice
          GOTO      ADDP9000 IF NOT EQUAL
.
          MATCH     DBFDILN,LASTILN         * Differnent Line Number
          GOTO      ADDP9000 IF NOT EQUAL
.
          MATCH     "1",DBFDDTY             * Process Invoice Type
          GOTO      ADDP3000 IF NOT EQUAL
.
ADDP2000  CALL      RANGE000           * Check Department/Product Type Range
          BRANCH    EXIT,ADDP1000      * failed selection, read next rec.
.
          ASSIGN    TPAYMTS+DBFDTAX+DBFDTOT-DBFDPAID,TPAYMTS
          GOTO      ADDP1000
.
ADDP3000  MOVE      ZERO,DBFDPAID
          MATCH     "4",DBFDDTY             * Debit Adjustment
          GOTO      ADDP2000 IF NOT EQUAL
          MULT      SEQ,DBFDTOT
          MULT      SEQ,DBFDTAX
          GOTO      ADDP2000
.
ADDP9000  CALL      RPDBFD2                 * Read Prev as one record ahead.
.
ADDP9999  RETURN
.---------------------------------------------End of changes ^^^^^^^^^^*I-F1101
. Write to Temp File
.--------------------------------------------------------
WRTMP000  IF        REPTSEQ=3
            PACK      KEY8,DBFDITM
            CALL      RDDBIT1
          ENDIF
          IF        REPTSEQ=2
            PACK      KEY8,DBFDITM
            CALL      RDDBIT1
            PACK      KEY8,DBITPTY
            CALL      RDDBPT1
          ENDIF
          LOAD      DBTMSEQ,REPTSEQ,DBFDDEB,DBPTDEP,DBITPTY,DBFDITM
          MOVE      DBFDDEB,DBTMDEB
          MOVE      DBFDDTY,DBTMDTY
          MOVE      DBFDDOC,DBTMDOC
          MOVE      DBFDDLN,DBTMDLN
          MOVE      CURRENT,DBTOTAL                                    *I-F1101
          PACK      KEY32,DBTMSEQ,DBFDDEB,DBFDDTY,DBFDDOC,DBFDDLN,SP70
          CALL      RADBTM1
          IF        OVRCD=1
            CALL      WRDBTM1
          ENDIF
WRTMP999  RETURN
.--------------------------------------------------------
. Check Ranges
.--------------------------------------------------------
RANGE000  BRANCH    FALLPTY,RANGE100
          PACK      KEY8,DBFDITM
          CALL      RDDBIT1
          BRANCH    OVRCD,RANGE810
.
          MATCH     STRPTY,DBITPTY    * Product Type Range
          GOTO      RANGE900 IF LESS
          MATCH     DBITPTY,ENDPTY
          GOTO      RANGE900 IF LESS
.
RANGE100  BRANCH    FALLDEP,RANGE200
          IF        FALLPTY=1
            PACK      KEY8,DBFDITM
            CALL      RDDBIT1
            BRANCH    OVRCD,RANGE810
          ENDIF
          PACK      KEY8,DBITPTY
          CALL      RDDBPT1
          BRANCH    OVRCD,RANGE820
.
          MATCH     STRDEP,DBPTDEP    * Department Range
          GOTO      RANGE900 IF LESS
          MATCH     DBPTDEP,ENDDEP
          GOTO      RANGE900 IF LESS
.
RANGE200  MOVE      ZERO,EXIT
          GOTO      RANGE999
.
RANGE810  CLEAR     DISPMSG
          APPEND    "ERROR : Sales Item Not On File - ",DISPMSG
          APPEND    KEY8,DISPMSG
          APPEND    " - ",DISPMSG
          RESET     DISPMSG
          BEEP
          CALL      MESSAGE1
          GOTO      RANGE900
.
RANGE820  CLEAR     DISPMSG
          APPEND    "ERROR : Product Type Not On File - ",DISPMSG
          APPEND    KEY8,DISPMSG
          APPEND    " - ",DISPMSG
          RESET     DISPMSG
          BEEP
          CALL      MESSAGE1
          GOTO      RANGE900
.
RANGE900  MOVE      ONE,EXIT
.
RANGE999  RETURN
.--------------------------------------------------
. Check Ranges and set "ALL" flag for each range
.--------------------------------------------------
CHKRAN00  MOVE      ZERO,FALLDEP
          MOVE      ZERO,FALLPTY
.
          MATCH     SP70,STRDEP
          IF        @EQUAL
            MATCH     Z70,ENDDEP
            IF        @EQUAL
              MOVE      ONE,FALLDEP
            ENDIF
          ENDIF
.
          MATCH     SP70,STRPTY
          IF        @EQUAL
            MATCH     Z70,ENDPTY
            IF        @EQUAL
              MOVE      ONE,FALLPTY
            ENDIF
          ENDIF
          RETURN
.
.----------------------------------------------------------------------
. SETDAT00 - Set up dates for Outstanding > X Days Old
.----------------------------------------------------------------------
SETDAT00  ENDSET    ASATDATE
          APPEND    SP70,ASATDATE
          RESET     ASATDATE
          MATCH     SP70,ASATDATE
          IF        @EQUAL
            PACK      ASATDATE,CCC,CYY,CMM,CDD
          ENDIF
          REP       " 0",ASATDATE
          UNPACK    ASATDATE,CCENT,CYEAR,CMON,CDAY
.
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON
          MOVE      JULDAY,F5
          SUB       OUTDAYS,F5
          IF        F5<1
            ADD       "365",F5
            ASSIGN    ((JULCC*100)+JULYR)-1,F4
            MOVE      F4,KEY4
            UNPACK    KEY4,JWKCC,JWKYER
            MOVE      JWKYER,JULYR
            MOVE      JWKCC,JULCC
          ENDIF
          MOVE      F5,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      ASATXXX,CC,YY,MM,DD
          REP       " 0",ASATXXX
.
          RETURN
.----------------------------------------------------------------
. Temp File Routines
.-------------------
.  OPNTMP00  -  Create Port Based Temp File for Extract
.  CLSTMP00  -  Close & Delete Temp File
.----------------------------------------------------------------
OPNTMP00  DISPLAY  *P1:10;
          CLEAR    FILENAME
          APPEND   "deb61A",FILENAME
          APPEND   PORT,FILENAME
          RESET    FILENAME
          REP      " 0",FILENAME
          MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     DEBTMPA1,FILENAME
          TRAPCLR  IO
          IF       OVRCD=0
            CLOSE    DEBTMPA1,DELETE
          ENDIF
.
          CLEAR    COMMAND
          APPEND   ISBUILD,COMMAND
          APPEND   FILENAME,COMMAND
          APPEND   " 41 UC1-8,9-16,17-17,18-29,30-32",COMMAND          *C-F1101
          RESET    COMMAND
          EXECUTE  COMMAND,TASKID
          OPEN     DEBTMPA1,FILENAME
.
          RETURN
.
CLSTMP00  DISPLAY  *P1:10;
          CLOSE    DEBTMPA1,DELETE
          RETURN
.
RSDBTM1   RESET     KEY32
          READ      DEBTMPA1,KEY32;;
          RETURN
.
RADBTM1   RESET     KEY32
          MOVE      ZERO,OVRCD
          READ      DEBTMPA1,KEY32;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RDDBTM1   RESET     KEY32
          MOVE      ZERO,OVRCD
          READ      DEBTMPA1,KEY32;DBTMSEQ,DBTMDEB,DBTMDTY,DBTMDOC:
                                   DBTMDLN,DBTOTAL                     *C-F1101
          GOTO      OVERCOND IF OVER
          RETURN
.
RKDBTM1   MOVE      ZERO,OVRCD
          READKS    DEBTMPA1;DBTMSEQ,DBTMDEB,DBTMDTY,DBTMDOC:
                                   DBTMDLN,DBTOTAL                     *C-F1101
          GOTO      OVERCOND IF OVER
          RETURN
.
RPDBTM1   MOVE      ZERO,OVRCD
          READKP    DEBTMPA1;DBTMSEQ,DBTMDEB,DBTMDTY,DBTMDOC:
                                   DBTMDLN,DBTOTAL                     *C-F1101
          GOTO      OVERCOND IF OVER
          RETURN
.
UPDBTM1   UPDATE    DEBTMPA1;DBTMSEQ,DBTMDEB,DBTMDTY,DBTMDOC:
                                   DBTMDLN,DBTOTAL                     *C-F1101
          RETURN
.
WRDBTM1   RESET     KEY32
          WRITE     DEBTMPA1,KEY32;DBTMSEQ,DBTMDEB,DBTMDTY,DBTMDOC:
                                   DBTMDLN,DBTOTAL                     *C-F1101
          RETURN
.
DEDBTM1   RESET     KEY32
          DELETE    DEBTMPA1,KEY32
          RETURN
.------------------------------------------------------------
. File       : casetran
.
. Functions  : This file contains standard routines for
.              creating and transfering TAB delimited
.              export files to a PC using the unix script
.              keatran.
.
. Routines   : MAKEXP00  - Create the Export File and Headings
.              WRTEXP00  - Write Export details Record to File
.              TRAN0000  - Transfer file to PC using script keatran
.
. Note       : The Variable EXPORTYN is used to determine if a
.              export file is to be created. 0=No, 1=Yes.
.
. Variables to be Declared
. ------------------------
. EXPORTFL  FILE      ASCII, FIXED=400     * Export File (Adjust Length to Suit)
. EXPORTFN  DIM       8                    * Export File Name
. TABDELM   EQU       0x09                 * Tab Delimiter
.------------------------------------------------------------
. Create Export File
.------------------------------------------------------------
MAKEXP00  COMPARE   ONE,EXPORTYN
          GOTO      MAKEXP99 IF NOT EQUAL
          MOVE      "EXPORT",KEY6
          PACK      EXPORTFN,KEY6,PORT
          REP       " 0",EXPORTFN
          PREP      EXPORTFL,EXPORTFN
.
          WRITE     EXPORTFL,SEQ;"Invoice Referrence",TABDELM:
                                 "Inovice Date",TABDELM:
                                 "Debtor Code",TABDELM:
                                 "Debtor Name",TABDELM:
                                 "Post Code",TABDELM:
                                 "Department Code",TABDELM:
                                 "Department Description",TABDELM:
                                 "Product Type",TABDELM:
                                 "Product Type Description",TABDELM:
                                 "Sales Item",TABDELM:
                                 "Sales Item Description",TABDELM:
                                 "Quantity",TABDELM:
                                 "Outstanding"
.
MAKEXP99  RETURN
.------------------------------------------------------------
. Write to Export File
.------------------------------------------------------------
WRTEXP00  COMPARE   ONE,EXPORTYN
          GOTO      WRTEXP99 IF NOT EQUAL
.
          WRITE     EXPORTFL,SEQ;DBFDINV,TABDELM:
                                 CPCDATE,TABDELM:
                                 DBDBDEB,TABDELM:
                                 DBDBNA1,TABDELM:
                                 DBDBPC,TABDELM:
                                 DBDEDEP,TABDELM:
                                 DBDEDES,TABDELM:
                                 DBPTPTY,TABDELM:
                                 DBPTDES,TABDELM:
                                 DBITITM,TABDELM:
                                 DBITDES,TABDELM:
                                 DBFDQTY,TABDELM:
                                 TOUT
.
WRTEXP99  RETURN
.------------------------------------------------------------
. Execute Transfer Script eg softerm
.------------------------------------------------------------
TRAN0000  COMPARE   ONE,EXPORTYN
          GOTO      TRAN9999 IF NOT EQUAL
          MOVE      YES,DEFYESNO
          CLOSE     EXPORTFL
          DISPLAY   *P1:24,*EL,"Execute Transfer Now (":
                               *V2LON,"Y",*HOFF,"/":
                               *V2LON,"N",*HOFF,") ? "
          MOVE      "24",CVERT
          MOVE      "30",CCOL
          CALL      YORN0000
          COMPARE   ZERO,FYESNO
          GOTO      TRAN9999 IF EQUAL   *  No Transfer
.
          MOVE      ZERO,HLEF
          CALL      GETSCR00
.
          CLEAR     KEY80
          APPEND    "keatran ",KEY80
          APPEND    EXPORTFN,KEY80
          APPEND    ".txt",KEY80
          RESET     KEY80
          EXECUTE   KEY80,TASKID
.
          CALL      PUTSCR00
.
          DISPLAY   *P1:24,*EL,"Transfer Completed Ok (":
                               *V2LON,"Y",*HOFF,"/":
                               *V2LON,"N",*HOFF,") ? "
          MOVE      "24",CVERT
          MOVE      "31",CCOL
          CALL      YORN0000
          COMPARE   ZERO,FYESNO
          GOTO      TRAN0000 IF EQUAL   *  Not ok
.
          PREP      EXPORTFL,EXPORTFN
          CLOSE     EXPORTFL
.
TRAN9999  RETURN
.
.--------------------------------------------------
. Enter Debtors Range
.--------------------------------------------------
RDEB0000   MOVE     "    4",FLDID
           CALL     GETPOSIT
           CALL     KDBDBA00
           BRANCH   EXIT,RDEB0100,RDEB0999
           MOVE     DBDBDEB,STRDEB
           PACK     FROMDEB,DBDBDEB,SP1,DBDBNA1
           GOTO     RDEB0140
RDEB0100   MOVE     "Start             ",FROMDEB
           MOVE     SP70,STRDEB
.
RDEB0140   MOVE     FROMDEB,VAR
           CALL     DISPITEM
.
RDEB0150   MOVE     "    5",FLDID
           CALL     GETPOSIT
           CALL     KDBDBA00
           BRANCH   EXIT,RDEB0200,RDEB0999
           MOVE     DBDBDEB,ENDDEB
           PACK     TODEB,DBDBDEB,SP1,DBDBNA1
           MATCH    STRDEB,ENDDEB
           GOTO     RDEB0999 IF EQUAL
           GOTO     RDEB0999 IF NOT LESS
           BEEP
           MOVE     "To Debtor Must Be > From Debtor - ",DISPMSG
           CALL     MESSAGE1
           GOTO     RDEB0000
.
RDEB0200   MOVE     "Finish             ",TODEB
           MOVE     Z70,ENDDEB
.
RDEB0999   MOVE     "    4",FLDID
           CALL     GETPOSIT
           RETURN
.--------------------------------------------------
. Enter Department Range
.--------------------------------------------------
RDEP0000   MOVE     "    6",FLDID
           CALL     GETPOSIT
           CALL     KDBDEA00
           BRANCH   EXIT,RDEP0100,RDEP0999
           MOVE     DBDEDEP,STRDEP
           PACK     FROMDEP,DBDEDEP,SP1,DBDEDES
           GOTO     RDEP0140
RDEP0100   MOVE     "Start             ",FROMDEP
           MOVE     SP70,STRDEP
.
RDEP0140   MOVE     FROMDEP,VAR
           CALL     DISPITEM
.
RDEP0150   MOVE     "    7",FLDID
           CALL     GETPOSIT
           CALL     KDBDEA00
           BRANCH   EXIT,RDEP0200,RDEP0999
           MOVE     DBDEDEP,ENDDEP
           PACK     TODEP,DBDEDEP,SP1,DBDEDES
           MATCH    STRDEP,ENDDEP
           GOTO     RDEP0999 IF EQUAL
           GOTO     RDEP0999 IF NOT LESS
           BEEP
           MOVE     "To Department Must Be > From Department - ",DISPMSG
           CALL     MESSAGE1
           GOTO     RDEP0000
.
RDEP0200   MOVE     "Finish             ",TODEP
           MOVE     Z70,ENDDEP
.
RDEP0999   MOVE     "    6",FLDID
           CALL     GETPOSIT
           RETURN
.--------------------------------------------------
. Enter PTYartment Range
.--------------------------------------------------
RPTY0000   MOVE     "    8",FLDID
           CALL     GETPOSIT
           CALL     KDBPTA00
           BRANCH   EXIT,RPTY0100,RPTY0999
           MOVE     DBPTPTY,STRPTY
           PACK     FROMPTY,DBPTPTY,SP1,DBPTDES
           GOTO     RPTY0140
RPTY0100   MOVE     "Start             ",FROMPTY
           MOVE     SP70,STRPTY
.
RPTY0140   MOVE     FROMPTY,VAR
           CALL     DISPITEM
.
RPTY0150   MOVE     "    9",FLDID
           CALL     GETPOSIT
           CALL     KDBPTA00
           BRANCH   EXIT,RPTY0200,RPTY0999
           MOVE     DBPTPTY,ENDPTY
           PACK     TOPTY,DBPTPTY,SP1,DBPTDES
           MATCH    STRPTY,ENDPTY
           GOTO     RPTY0999 IF EQUAL
           GOTO     RPTY0999 IF NOT LESS
           BEEP
           MOVE     "To Product Type Must Be > From Product Type - ",DISPMSG
           CALL     MESSAGE1
           GOTO     RPTY0000
.
RPTY0200   MOVE     "Finish             ",TOPTY
           MOVE     Z70,ENDPTY
.
RPTY0999   MOVE     "    8",FLDID
           CALL     GETPOSIT
           RETURN
.------------------------------------------------------------
. Input Report Layout
.------------------------------------------------------------
KRSEQ000   CALL      DRSEQ000
           MOVE      ONE,REPTSEQ
.
KRSEQ001   KEYIN     *PCCOL:CVERT,*V2LON,*RV,REPTSEQ
           BRANCH    REPTSEQ,KRSEQ010,KRSEQ010,KRSEQ010,KRSEQ010
           BEEP
           GOTO      KRSEQ001
.
KRSEQ010   CALL      GRSEQ000
KRSEQ999   CALL      PUTSCR00
           RETURN
.
DRSEQ000  MOVE      "17",HTOP
          MOVE      "55",HLEF
          MOVE      "24",HBOT
          MOVE      "80",HRIG
          CALL      GETSCR00
.
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          DISPLAY   *P60:18,*V2LON,*ULON,"Report Sequence",*HOFF:
                    *P57:19,*V2LON,"1. ",*HOFF,"Debtor":
                    *P57:20,*V2LON,"2. ",*HOFF,"Department":
                    *P57:21,*V2LON,"3. ",*HOFF,"Product Type":
                    *P57:22,*V2LON,"4. ",*HOFF,"Sales Item"
          RETURN
.
GRSEQ000   BRANCH    REPTSEQ,GRSEQ010,GRSEQ020,GRSEQ030,GRSEQ040
.
GRSEQ010   MOVE      "Debtor",SEQDESC
           GOTO      GRSEQ999
.
GRSEQ020   MOVE      "Department",SEQDESC
           GOTO      GRSEQ999
.
GRSEQ030   MOVE      "Product Type",SEQDESC
           GOTO      GRSEQ999
.
GRSEQ040   MOVE      "Sales Item",SEQDESC
           GOTO      GRSEQ999
.
GRSEQ999   RETURN
.-------------------------------
. Includes for I/O and Keyins
.-------------------------------
          INC       DEBCODIO/INC
          INC       DEBDBTIO/INC
          INC       DEBDBTKY    
          INC       DEBDEPIO/INC
          INC       DEBDEPKY    
          INC       DEBDKIIO/INC
          INC       DEBDKIKY    
          INC       DEBDKIPR    
          INC       DEBFDTIO/INC
          INC       DEBITMIO/INC
          INC       DEBPTYIO/INC
          INC       DEBPTYKY    
.
          INC       STDGENCD
.
