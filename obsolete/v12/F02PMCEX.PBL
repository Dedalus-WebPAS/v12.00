.*****************************************************************************
.*    Program      : F02PMCEX                                                *
.*    Description  : Conversion pmscexaf to new File layout                  *
.*                                                                           *
.*    Author       : J.Tan                                                   *
.*    Date         : 06/06/2011                                              *
.*    Modifications:                                                         *
.*        V10.02.02 08/06/2011  Ebon Clements     CAR 239530                 *
.*                  Added country of birth PMCECONT                          *
.*        V10.02.01 07/06/2011  Steve Armstrong   CAR 239592                 *
.*                  Modified to split given name into first and second       *
.*                  given name fields.                                       *
.*                  Also added Oracle option.                                *
.*        V10.02.00 06/06/2011  J.Tan         CAR 239592                     *
.*                  Program created                                          *
.*****************************************************************************
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
.
.         Old File Definition
.
OLDFILE1  IFILE    SQL, FIXED=536, KEY="U1-8,9-11"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.PMCEURNO  DIM       8      8        1     U/R Number
.PMCETYPE  DIM       3      3        9     Type of Contact (Cat tc)
.PMCETITL  DIM       4      4        12    Title
OLDSNAM    DIM       35     35       16    Surname
OLDGNAM    DIM       35     35       51    Given Name
.PMCEADD1  DIM       35     35       86    Address Line 1
.PMCEADD2  DIM       35     35       121   Address Line 2
.PMCEADD3  DIM       35     35       156   Address Line 3
.PMCEADD4  DIM       35     35       191   Address Line 4
.PMCEPOST  DIM       8      8        226   Post Code
.PMCETELP  DIM       20     20       234   Telephone Private
.PMCETELB  DIM       20     20       254   Telephone Business
.PMCETELM  DIM       20     20       274   Telephone Mobile
.PMCERELP  DIM       10     10       294   Relationship
.PMCEEMAI  DIM       80     80       304   Email Address
.PMCECDAT  DIM       8      8        384   Date Record Created (CCYYMMDD)
.PMCECTIM  DIM       8      8        392   Time Record Created (HH:MM:SS)
.PMCECUID  DIM       10     10       400   WEB User ID Created (websecaf)
.PMCEUDAT  DIM       8      8        410   Update Date (CCYYMMDD)
.PMCEUTIM  DIM       8      8        418   Update Time (HH:MM:SS)
.PMCEUUID  DIM       10     10       426   WEB User ID Updated (websecaf)
.PMCESLET  DIM       1      1        436   Send Letter (0=No,1=Yes)
.PMCEACTV  DIM       1      1        437   Active (0=Active 1=Inactive)
OLDSPAR    DIM       98     98       438   Spare
.
.End of Record                      536
.
.
.         New File Definition
.
PMSCEXA1  IFILE    SQL, FIXED=586, KEY="U1-8,9-11"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
PMCEURNO  DIM       8      8        1     U/R Number
PMCETYPE  DIM       3      3        9     Type of Contact (Cat tc)
PMCETITL  DIM       4      4        12    Title
PMCESNAM  DIM       40     40       16    Surname
PMCEGNAM  DIM       40     40       56    First Given Name
PMCEGNM2  DIM       40     40       96    Second Given Name
PMCEADD1  DIM       35     35       136   Address Line 1
PMCEADD2  DIM       35     35       171   Address Line 2
PMCEADD3  DIM       35     35       206   Address Line 3
PMCEADD4  DIM       35     35       241   Address Line 4
PMCEPOST  DIM       8      8        276   Post Code
PMCETELP  DIM       20     20       284   Telephone Private
PMCETELB  DIM       20     20       304   Telephone Business
PMCETELM  DIM       20     20       324   Telephone Mobile
PMCERELP  DIM       10     10       344   Relationship
PMCEEMAI  DIM       80     80       354   Email Address
PMCECDAT  DIM       8      8        434   Date Record Created (CCYYMMDD)
PMCECTIM  DIM       8      8        442   Time Record Created (HH:MM:SS)
PMCECUID  DIM       10     10       450   WEB User ID Created (websecaf)
PMCEUDAT  DIM       8      8        460   Update Date (CCYYMMDD)
PMCEUTIM  DIM       8      8        468   Update Time (HH:MM:SS)
PMCEUUID  DIM       10     10       476   WEB User ID Updated (websecaf)
PMCESLET  DIM       1      1        486   Send Letter (0=No,1=Yes)
PMCEACTV  DIM       1      1        487   Active (0=Active 1=Inactive)
PMCECONT  DIM       3      3        488   Country of Birth (Cat C)
PMCESPAR  DIM       95     95       491   Spare
.
.End of Record                      586
.
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       100
.
DEFPATH   DIM       60
DIM60     DIM       60
.
FORM3     FORM      3
FRSTGNAM  DIM       40
FULLGNAM  DIM       80
.
INPFILE   DIM       8
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
SAVEFLG   FORM      1
SCNDGNAM  DIM       40
.
.
. PROGRAM CONSTANTS
. -----------------
SP60      INIT    "                                                            "
.
.
PRGNAM    INIT      "CONVERSION PMSCEXFD"
PRGID     INIT      "F02PMCEX"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000:             * run c-isam fixit
                            MAIN2000              * run oracle fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
MAIN2000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN0000:               * no
                          MAIN9999                * cancel
.
MAIN5000  BRANCH    COPTION,MAIN6000:             * c-isam fixit
                            MAIN7000              * oracle fixit
.
.         Running c-isam fix
.
MAIN6000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/compress saved file
          GOTO      MAIN9999
.
.         Running oracle fix
.
MAIN7000  CALL      OFIX0000                      *
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit": 
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)":
                    *P1:6,*V2LON,TWO,*HOFF,". Run Fixit (Oracle only)"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * run c-isam fixit
                            OPTN9000             * run oracle fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
.
PREP0000  MOVE      "pmscexaf",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat pmscexaf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "spmcexaf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat pmscexaf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "spmcexaf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "pmscexaf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "spmcexaf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "pmscexaf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.      eg    APPEND    " 195 UC9-16",CMDLINE
.
          APPEND    " 586 UC1-8,9-11",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      PMSCEXA1,NEWFILE
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
            ELSE
              DISPLAY   "has caused an IO error - ";
            ENDIF
          ENDIF
          DISPLAY   "File will be bypassed"
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY11,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
.
          IF        (RECNUM%1000) = 0 | (RECNUM%1000) = 1
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          PACK      PMCESNAM,OLDSNAM,SP70        * load surname
          PACK      FULLGNAM,OLDGNAM,SP70,SP70
          CALL      SEPRNAME                     * separate given names
          PACK      PMCEGNAM,FRSTGNAM,SP70       * load first given name
          PACK      PMCEGNM2,SCNDGNAM,SP70       * load second given name
          MOVE      SP70,PMCESPAR                * load spare variable
          MOVE      SP70,PMCECONT                * country of birth
.
          CALL      WRPMCEX1                     * write to new file
.
          GOTO      READ1000                     * get next record
.
READ6000  CLOSE     PMSCEXA1                     * close new file
          CLOSE     OLDFILE1                     * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
.
VALD0000  MOVE      ZERO,EXIT
.
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or compress save file                                *
.**********************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "spmcexaf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "compress -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not compressed or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.*        DEFT0000 - Get the default directory                        *
.**********************************************************************
.
DEFT0000  EXECUTE   "ldat pmscexaf.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding 'pmscexaf'.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.*                            OFIX0000                                       *
.*            Oracle fix to.......                                           *
.*****************************************************************************
.
OFIX0000  DISPLAY   *P1:20,*EL,"Record No. :";
          OPEN      PMSCEXA1,"pmscexaf"
.
          MOVE      SP20,KEY11
          CALL      RSPMCEX1                     * position at start of file
OFIX0500  CALL      RKPMCEX1                     * read next record
          BRANCH    OVRCD,OFIX9000               * eof - finished
.
          ADD       ONE,RECNUM                   * increment record counter
.
          IF        (RECNUM%1000) = 0 | (RECNUM%1000) = 1
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          PACK      FULLGNAM,PMCEGNAM,SP70,SP70
          CALL      SEPRNAME                     * separate given names
          PACK      PMCEGNAM,FRSTGNAM,SP70       * load first given name
          PACK      PMCEGNM2,SCNDGNAM,SP70       * load second given name
          CALL      UPPMCEX1                     * update record
.
          GOTO      OFIX0500                     * get next record
.
OFIX9000  CLOSE     PMSCEXA1
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:24,*EL,"Update completed.  ";
          CALL      HITENTER
.
OFIX9999  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
.         Old I/O Routines
.
READSOLD  RESET     KEY11
          READ      OLDFILE1,KEY11;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;PMCEURNO,PMCETYPE,PMCETITL,OLDSNAM,OLDGNAM:
                        PMCEADD1,PMCEADD2,PMCEADD3,PMCEADD4,PMCEPOST,PMCETELP:
                        PMCETELB,PMCETELM,PMCERELP,PMCEEMAI,PMCECDAT,PMCECTIM:
                        PMCECUID,PMCEUDAT,PMCEUTIM,PMCEUUID,PMCESLET,PMCEACTV:
                        OLDSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
.         New I/O Routines
.
RSPMCEX1  RESET     KEY11
          READ      PMSCEXA1,KEY11;;
          RETURN
.
RKPMCEX1  MOVE      ZERO,OVRCD
          READKS    PMSCEXA1;PMCEURNO,PMCETYPE,PMCETITL,PMCESNAM,PMCEGNAM:
                        PMCEGNM2,PMCEADD1,PMCEADD2,PMCEADD3,PMCEADD4,PMCEPOST:
                        PMCETELP,PMCETELB,PMCETELM,PMCERELP,PMCEEMAI,PMCECDAT:
                        PMCECTIM,PMCECUID,PMCEUDAT,PMCEUTIM,PMCEUUID,PMCESLET:
                        PMCEACTV,PMCECONT,PMCESPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
WRPMCEX1  RESET     KEY11
          WRITE     PMSCEXA1,KEY11;PMCEURNO,PMCETYPE,PMCETITL,PMCESNAM,PMCEGNAM:
                        PMCEGNM2,PMCEADD1,PMCEADD2,PMCEADD3,PMCEADD4,PMCEPOST:
                        PMCETELP,PMCETELB,PMCETELM,PMCERELP,PMCEEMAI,PMCECDAT:
                        PMCECTIM,PMCECUID,PMCEUDAT,PMCEUTIM,PMCEUUID,PMCESLET:
                        PMCEACTV,PMCECONT,PMCESPAR
          RETURN
.         
UPPMCEX1  UPDATE    PMSCEXA1;PMCEURNO,PMCETYPE,PMCETITL,PMCESNAM,PMCEGNAM:
                        PMCEGNM2,PMCEADD1,PMCEADD2,PMCEADD3,PMCEADD4,PMCEPOST:
                        PMCETELP,PMCETELB,PMCETELM,PMCERELP,PMCEEMAI,PMCECDAT:
                        PMCECTIM,PMCECUID,PMCEUDAT,PMCEUTIM,PMCEUUID,PMCESLET:
                        PMCEACTV,PMCECONT,PMCESPAR
          RETURN
+
          INC       STD001IO
.
          INC       SEPRNAME
