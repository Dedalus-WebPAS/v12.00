. **********************************************************************
. * System    :   Web Finance System                                   *
. * Program   :   FMSWEB02                                             *
. * Desc      :   Financial Server - Dissection/Responsibility         *
. * Mods      :                                                        *
. **********************************************************************
. V10.03.01 16/07/2013  Jill Parkinson CAR 287923
.           Added include of PATCONFD
. * V9.03.01  21.10.2003 Ebon Clements  CAR 44461                      *
. *           Changed server to use the finance passcode WBSEFPCD      *
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
. * VF.00.01  23.12.1999 Sandra Barcham                                *
. *           Recompiled for change to FMSCCAFD.INC                    *
. *           srf 646145                                               *
. **********************************************************************
.
          INC       IBAWEBDF    
.
          INC       PATCONFD/INC
          INC       FMSAMAFD/INC       * account
          INC       FMSCONFD/INC       * controlf
          INC       FMSDSFFD/INC       * dissection accrual summary file
          INC       FMSLMAFD/INC       * ledger
          INC       FMSLMCFD/INC       * ledger financial calendar
          INC       FMSSQUFD/INC       * enquiry security
          INC       FMSDISFD/INC       * dissection
          INC       FMSRESFD/INC       * responsibility
          INC       WEBFEQDF
.
LINKREPT  DIM       1
EDITCUR   DIM       20
EDITYTD   DIM       20
DSETNUM   DIM       1
DSETAXIS  DIM       1
DSETTYPE  DIM       10
F1P2      FORM      1.2
NEXTLINK  DIM       127
LASTLINK  DIM       127
BUDARR    FORM      12.2[99]
ACTARR    FORM      12.2[99]
NAMARR    DIM       35[99]
COLARR    DIM       6[99]
LNKARR    DIM       80[99]
LASTARRY  FORM      2
LEDGCODE  DIM       2
ACCTCODE  DIM       12
INCOMEFL  FORM      2
LASTCC    DIM       12
NEXTCC    DIM       12
ACCOUNTN  DIM       35
BGRED     INIT      "#"##FF0000#""
BGGREEN   INIT      "#"##008000#""
YTDBGC    DIM       9
CURBGC    DIM       9
.
CHARTOPT  FORM      1
ACCTKEYS  DIM       14
FINCYEAR  DIM       4
FINCPERD  DIM       2
MAXVALUE  FORM      10
STEPVAL   FORM      10
INDEX2    FORM      2
JAVATMPN  DIM       30
JAVATMPF  FILE      ASCII, FIXED=250
PERNAME   DIM       12
PERNAME3  DIM       3 
.
ACTUALCP  FORM      12.2
BUDGETCP  FORM      12.2
VARIANCP  FORM      12.2
PERVARCP  FORM      6.1 
.
ACTUALYD  FORM      12.2
BUDGETYD  FORM      12.2
VARIANYD  FORM      12.2
PERVARYD  FORM      6.1 
.
.--------------------------------------------------
. Variables for Standard Code Routines
.--------------------------------------------------
ACCTYPE   INIT      "           "      * account type
ACCTYPE1  INIT      "Income"
ACCTYPE2  INIT      "Expenditure"
ACCTYPE3  INIT      "Asset"
ACCTYPE4  INIT      "Liability"
ACCTYPE5  INIT      "Equity"
ACCTYPE6  INIT      "Total"
ACCTYPE7  INIT      "Heading"
ACCTYPE8  INIT      "Stat -Post"
ACCTYPE9  INIT      "Stat -Total"
.
CACCTIND  FORM      " 0"     * FMSAMAKY ind.
.                 CACCTIND 0=all acc, 1=posting, 2=total/head, 3=stat, 4=total,
.                          5=post/total, 6=stats post, 7=asset,liab,equity
.                          8=all but heading, 9=stat total, 10=stat&norm post
.                          11=Equity Only
CACCOP1   DIM       1                           * valid option vars for FMSAMAKY
CACCOP2   DIM       1
CACCOP3   DIM       1
CACCOP4   DIM       1
CACCOP5   DIM       1
CACCOP6   DIM       1
CACCOP7   DIM       1
CACCOP8   DIM       1
CACCOP9   DIM       1
CCONTIND  FORM      "0"      * FMSCAFKY ind.
.                 CCONTIND 0=all accounts, 1=bank, 2=creditor, 3=discount
.                          4=debtors, 5=other
CDATASAV  DIM       20       * save var for keyins
CDIM4     DIM       4        * dim 4 for FMSLMCKY
CDIV1     FORM      12.5     * param for CDIV0000
CDIV2     FORM      12.5     * param for CDIV0000
CMDLINE   DIM       80       * Command Line
COLNO     FORM      2        * collumn number
CSTRING   DIM       35       * keyed in string (routine GSTR)
CSTRLEN   FORM      2        * string length   (routine GSTR)
CSUBJIND  FORM      "0"      * FMSSBAKY ind.
.                 CSUBJIND 0=all subj, 1=total, 2=non total, 3=non head
.                          4=stat-tot subj, 5=posting subj
CTYPE     DIM       1        * account type
.
DECPLACE  FORM      1        * decimal places
.
FMLCF4    FORM      4        * form 4 for FMSLMCKY
F12       FORM      12
F12P1     FORM      12.1
F12P2     FORM      12.2
F12P3     FORM      12.3
F12P4     FORM      12.4
F12P5     FORM      12.5
F12P6     FORM      12.6
.
HELPOPTN  FORM      1        * help search option
HELPSRCH  DIM       35       * help search string
.
KAMOUNT   FORM      12.6     * keyin amount
.
LINENO    FORM      2        * line number
.
TSTCCYY1  FORM      4        * Date Test Fields
TSTCCYY2  FORM      4
TSTMM1    FORM      2
TSTMM2    FORM      2
TSTDD1    FORM      2
TSTDD2    FORM      2
.--------------------------------------
. Variables for Period Validation
.--------------------------------------
CURYEAR   DIM       4
PERDESC   DIM       15
PEREDAT   DIM       8
PERSDAT   DIM       8
PERLOCK   FORM      1
ERRMSG    DIM       40
MAXPER    FORM      2
PERCNT    FORM      2
WORKDATE  DIM       8
.
PRGID     INIT      "FMSWEB02"
PRGNAM    INIT      "Financial Server - Dissection/Responsibility"
VERSION   INIT      "V12.00.00"
.
          CALL      WEBINT00       * Initialise Fifo Etc.
          CALL      INIT0000       * Open Files
.
MAIN1000  CALL      WEBRED00       * Read Fifo WEBPID and USERID
          COMPARE   "999",REPORTNO * End Server Process on Report Option 999
          GOTO      MAIN9999 IF EQUAL
.
          MATCH     SP70,WBSEFPCD       
          IF        !@EQUAL
            MOVE      WBSEFPCD,PASSCODE     * Web security finance passcode
          ENDIF
.
          BRANCH    REPORTNO,MAIN1100,MAIN1200
.
          MOVE      "INVALID OPTION",WEBTITLE
          CALL      WEBERR00   * Create Output File and Write HTML Page Header
          GOTO      MAIN1000
.
MAIN1100  CALL      SHWDIS00    * Dissection Details
          GOTO      MAIN1000
.
MAIN1200  CALL      SHWRES00    * Responsibility Details
          GOTO      MAIN1000
.
MAIN9999  MOVE      "SERVER SHUTDOWN COMPLETE",WEBTITLE
          CALL      WEBERR00
          STOP
PROFLD00  RETURN
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  OPEN      CONTROLF,"controlf"
          CALL      RDFMCO50
          CALL      RDFMCO51
          CALL      RDFMCO52
          CALL      RDFMCO65
          CALL      RDFMCO66
.
          OPEN      FMSAMAA1,"fmsamaaf"
          OPEN      FMSLMAA1,"fmslmaaf"
          OPEN      FMSLMCA1,"fmslmcaf"
          OPEN      FMSDISA1,"fmsdisaf"
          OPEN      FMSRESA1,"fmsresaf"
.
          UNPACK    SP70,ACTFILE,BUDFILE,TRANFILE,LACTFILE,LBUDFILE *clear files
.
          CALL      SETCOL00  * Set Array of Colors
.
          RETURN
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  MOVE      ZERO,REPORTNO
          MOVE      ZERO,CHARTOPT
          MOVE      SP70,ACCTKEYS
          MOVE      SP70,FINCYEAR
          MOVE      SP70,FINCPERD
          MOVE      "000",TEMPLATE
          RETURN
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "acctkeys=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ACCTKEYS
            REP       "a&",ACCTKEYS
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "chartopt=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,CHARTOPT
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fincyear=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FINCYEAR
            GOTO      SETPAR99
          ENDIF
.
          MATCH     "fincperd=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FINCPERD
            GOTO      SETPAR99
          ENDIF
.
SETPAR99  RETURN
.------------------------------------------------------------
. Account Account Details
.------------------------------------------------------------
SHWDIS00  PACK      KEY14,ACCTKEYS,SP70
          CALL      RDFMAM1
          BRANCH    OVRCD,SHWDIS80
.
          MOVE      ACCTKEYS,FMLALEDG
          CALL      PERD0000           * Set Period and Year
          MOVE      FINCPERD,PERIODNO
          MOVE      FINCPERD,F2
          LOAD      PERNAME,F2,FMLC01DE,FMLC02DE,FMLC03DE,FMLC04DE,FMLC05DE:
                               FMLC06DE,FMLC07DE,FMLC08DE,FMLC09DE,FMLC10DE:
                               FMLC11DE,FMLC12DE
.
          CALL      OPNDIS00           * Open Dissection File
          BRANCH    EXIT,SHWDIS99
.
          MOVE      FMAMTYPE,SAVTYPE   * save account type
          MOVE      ZERO,DECPLACE
          MOVE      FMAMDPLA,DECPLACE  * get no. of decimal places
          MOVE      ZERO,AVYTD
          MOVE      FMAMAYTD,AVYTD     * save ytd type
.
          MOVE      FMAMDESC,WEBTITLE
          CALL      WEBHED00            * Create Output File and Page Header
          BRANCH    EXIT,SHWDIS99
.
          MOVE      "1",LINKREPT
          CALL      NXTLST00           * Get Next & Last Period Links
          CALL      HEADAC00           * Output Account Details Heading
.
          WRITE     HTMLFILE;"<TABLE BORDER=1 WIDTH=#"100%#" >":
                             "<TR><TD><B>Dissection</B></TD>":
                             "<TD ALIGN=RIGHT><B>Current Period</B></TD>":
                             "<TD ALIGN=RIGHT><B>Year to Date</B></TD>":
                             "</TR>"
.
          MOVE      "0",TCURDIS
          MOVE      "0",TYTDDIS
          MOVE      "0",CURDIS
          MOVE      "0",YTDDIS
          MOVE      SP70,CURRDISP
.
          MOVE      ONE,INCOMEFL
          MOVE      FMAMTYPE,F1
          IF        F1=1|F1=4
            MOVE      SEQ,INCOMEFL
          ENDIF
          MOVE      ZERO,LASTARRY
          MOVE      SP70,CURRDISP
          PACK      KEY23,ACCTKEYS,SP70
          CALL      RSFMDF1                      * go to start of records
SHWDIS10  CALL      RKFMDF1                      * get next record
          BRANCH    OVRCD,SHWDIS90               * no more records ?
          PACK      KEY14,FMDFLEDG,FMDFACCT,SP70
          MATCH     KEY14,KEY23
          GOTO      SHWDIS90 IF NOT EQUAL        * no more records ?
          MATCH     SP70,CURRDISP
          IF        @EQUAL
            MOVE      FMDFDISS,CURRDISP
          ENDIF
          MATCH     FMDFDISS,CURRDISP
          IF        @EQUAL
            CALL      GDIS0000                     * Calculate Current & YTD
            GOTO      SHWDIS10
          ENDIF
.
          CALL      DISLIN00                       * Output Line Details
.
          GOTO      SHWDIS10
.
SHWDIS80  MOVE      "INVALID ACCOUNT",WEBTITLE
          CALL      WEBERR00
          GOTO      SHWDIS99
.                  
SHWDIS90  MATCH     SP70,CURRDISP
          IF        !@EQUAL
            CALL      DISLIN00
          ENDIF

          MOVE      "(999,999,999,999.99 ",EDITCUR
          FEDIT     TCURDIS,EDITCUR
          MOVE      "(999,999,999,999.99 ",EDITYTD 
          FEDIT     TYTDDIS,EDITYTD
          WRITE     HTMLFILE;"<TR><TD><B>Total</B></TD>":
                             "<TD ALIGN=RIGHT><B>",EDITCUR,"</B></TD>":
                             "<TD ALIGN=RIGHT><B>",EDITYTD,"</B></TD></TR>":
                             "</TABLE>"
          MOVE      "dissection_chart.html",JAVATMPN
          CALL      JCHRT000
          CALL      WEBEND00
.
SHWDIS99  RETURN
.------------------------------------------------------------
. Display Line Details
.------------------------------------------------------------
DISLIN00  MOVE      SP70,FMDSDESC
          PACK      KEY5,CURRDISP,SP70
          CALL      RDFMDS1
          MOVE      "(999,999,999,999.99 ",EDITCUR
          MULT      INCOMEFL,CURDIS
          FEDIT     CURDIS,EDITCUR
          MOVE      "(999,999,999,999.99 ",EDITYTD 
          MULT      INCOMEFL,YTDDIS
          FEDIT     YTDDIS,EDITYTD
          WRITE     HTMLFILE;"<TR><TD>",CURRDISP,SP1,FMDSDESC,"</TD>":
                                 "<TD ALIGN=RIGHT>",EDITCUR,"</TD>":
                                 "<TD ALIGN=RIGHT>",EDITYTD,"</TD></TR>"
          ADD       CURDIS,TCURDIS
          ADD       YTDDIS,TYTDDIS
          ADD       ONE,LASTARRY
          MOVE      FMDSDESC,NAMARR[LASTARRY]
          MOVE      CURDIS,ACTARR[LASTARRY]
          MOVE      YTDDIS,BUDARR[LASTARRY]
          MOVE      "0",CURDIS
          MOVE      "0",YTDDIS
          CALL      GDIS0000                     * Calculate Current & YTD
          MOVE      FMDFDISS,CURRDISP
          RETURN
.------------------------------------------------------------
. Account Heading with Next & Last Period Options
.------------------------------------------------------------
HEADAC00  LOAD      KEY8,PERIODNO,FMLC01TO,FMLC02TO,FMLC03TO,FMLC04TO:
                                  FMLC05TO,FMLC06TO,FMLC07TO,FMLC08TO:
                                  FMLC09TO,FMLC10TO,FMLC11TO,FMLC12TO,FMLC13TO
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          WRITE     HTMLFILE;"<table cellspacing=0 cellpadding=0":
                             " border=0 width=#"100%#" >":
                             "<tr bgcolor=#FFFF00>":
                             "<td align=center><a href=",LASTLINK,">":
                             "<img src=/images/last_period.gif border=0>":
                             "<BR>Last Period</a></td>":
                             "<td align=center><b>":
                             PERNAME,SP1,CCENT,CYEAR,"<BR>":
                             FMAMDESC,"</b></TD>":
                             "</b></td>":
                             "<td align=center>":
                             "<a href=",NEXTLINK,">":
                             "<img src=/images/next_period.gif border=0>":
                             "<BR>Next Period":
                             "</a></td></tr></table>"
          RETURN
.------------------------------------------------------------
. Open Dissection File
.------------------------------------------------------------
OPNDIS00  PACK      FILENAM,FMSD,CURYEAR
          REP       " 0",FILENAM
          MATCH     FILENAM,DISFILE
          GOTO      OPNDIS90 IF EQUAL            * file already open ?
.
          MOVE      FILENAM,DISFILE              * save current filename
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FMSDSFA1,FILENAM             * open current year file
          TRAP      OVERCOND IF IO
          OPEN      FMSDSFA2,FILENAM
          TRAPCLR   IO
          BRANCH    OVRCD,OPNDIS95               * data not found ?
.
OPNDIS90  MOVE      ZERO,EXIT
          GOTO      OPNDIS99
.
OPNDIS95  MOVE      "DISSECTION TABLE NOT AVAILABLE",WEBTITLE
          CALL      WEBERR00
          MOVE      SP70,DISFILE                 * clear file name
          MOVE      ONE,EXIT
.
OPNDIS99  RETURN
.
.------------------------------------------------------------
. GDIS - Calc dissection data                      
.------------------------------------------------------------
GDIS0000  ADD       FMDFBF,FMDF01                * add br fwd to period 1
          MOVE      ZERO,F2
          MOVE      ZERO,F12P2
.
GDIS1000  ADD       ONE,F2
          COMPARE   F2,PERIODNO
          GOTO      GDIS9000 IF LESS
          LOAD      F12P2,F2,FMDF01,FMDF02,FMDF03,FMDF04,FMDF05,FMDF06:
                              FMDF07,FMDF08,FMDF09,FMDF10,FMDF11,FMDF12,FMDF13
          ADD       F12P2,YTDDIS
          GOTO      GDIS1000
.
GDIS9000  ADD       F12P2,CURDIS
.
GDIS9999  RETURN
.------------------------------------------------------------
. Add JavaChart Graph
.------------------------------------------------------------
JCHRT000  PACK      FILNAM,TEMPLDIR,JAVATMPN,SP70
          SQUEEZE   FILNAM
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      JAVATMPF,FILNAM
          TRAPCLR   IO
          BRANCH    OVRCD,JCHRT999
.
JCHRT100  READ      JAVATMPF,SEQ;LINE127
          GOTO      JCHRT999 IF OVER
.
          MATCH     "%titleString",LINE127       * Insert Title String
          GOTO      JCHRT110 IF EQUAL
          MATCH     "%dataset",LINE127   * Insert Data Set 0 Actuals
          GOTO      JCHRT120 IF EQUAL
.
          WRITE     HTMLFILE;LINE127
          GOTO      JCHRT100
.
JCHRT110  WRITE     HTMLFILE;"  <param name=titleString ":
                                    "value=#"",FMAMDESC,"#">"
          GOTO      JCHRT100
.
JCHRT120  UNPACK    LINE127,KEY8,DSETNUM,DSETTYPE
          MATCH     "Labels",DSETTYPE
          GOTO      JCHRT130 IF EQUAL
          MATCH     "Links",DSETTYPE
          GOTO      JCHRT140 IF EQUAL
          MATCH     "Colors",DSETTYPE
          GOTO      JCHRT150 IF EQUAL
.
          UNPACK    LINE127,KEY8,DSETNUM,DSETAXIS,DSETTYPE
          MATCH     "ValuesA",DSETTYPE
          IF        @EQUAL
            CALL      ACTDAT00
          ENDIF
          MATCH     "ValuesB",DSETTYPE
          IF        @EQUAL
            CALL      BUDDAT00
          ENDIF
          GOTO      JCHRT100
.
JCHRT130  CALL      NAMDAT00
          GOTO      JCHRT100
.
JCHRT140  CALL      LNKDAT00
          GOTO      JCHRT100
.
JCHRT150  CALL      COLDAT00
          GOTO      JCHRT100
.
JCHRT999  RETURN
.------------------------------------------------------------
. Account Account Details
.------------------------------------------------------------
SHWRES00  PACK      KEY14,ACCTKEYS,SP70
          CALL      RDFMAM1
          BRANCH    OVRCD,SHWRES80
.
          MOVE      ACCTKEYS,FMLALEDG
          CALL      PERD0000           * Set Period and Year
          MOVE      FINCPERD,PERIODNO
          MOVE      FINCPERD,F2
          LOAD      PERNAME,F2,FMLC01DE,FMLC02DE,FMLC03DE,FMLC04DE,FMLC05DE:
                               FMLC06DE,FMLC07DE,FMLC08DE,FMLC09DE,FMLC10DE:
                               FMLC11DE,FMLC12DE
.
          CALL      OPNDIS00           * Open Dissection File
          BRANCH    EXIT,SHWRES99
.
          MOVE      FMAMTYPE,SAVTYPE   * save account type
          MOVE      ZERO,DECPLACE
          MOVE      FMAMDPLA,DECPLACE  * get no. of decimal places
          MOVE      ZERO,AVYTD
          MOVE      FMAMAYTD,AVYTD     * save ytd type
.
          MOVE      FMAMDESC,WEBTITLE
          CALL      WEBHED00            * Create Output File and Page Header
          BRANCH    EXIT,SHWRES99
.
          MOVE      "2",LINKREPT
          CALL      NXTLST00           * Get Next & Last Period Links
          CALL      HEADAC00           * Output Account Details Heading
.
          WRITE     HTMLFILE;"<TABLE BORDER=1 WIDTH=#"100%#" >":
                             "<TR><TD><B>Responsibility</B></TD>":
                             "<TD ALIGN=RIGHT><B>Current Period</B></TD>":
                             "<TD ALIGN=RIGHT><B>Year to Date</B></TD>":
                             "</TR>"
.
          MOVE      "0",TCURDIS
          MOVE      "0",TYTDDIS
          MOVE      "0",CURDIS
          MOVE      "0",YTDDIS
          MOVE      SP70,CURRDISP
.
          MOVE      ONE,INCOMEFL
          MOVE      FMAMTYPE,F1
          IF        F1=1|F1=4
            MOVE      SEQ,INCOMEFL
          ENDIF
.
          MOVE      ZERO,LASTARRY
          MOVE      SP70,CURRDISP
          PACK      KEY23,ACCTKEYS,SP70
          CALL      RSFMDF2                      * go to start of records
SHWRES10  CALL      RKFMDF2                      * get next record
          BRANCH    OVRCD,SHWRES90               * no more records ?
          PACK      KEY14,FMDFLEDG,FMDFACCT,SP70
          MATCH     KEY14,KEY23
          GOTO      SHWRES90 IF NOT EQUAL        * no more records ?
          MATCH     SP70,CURRDISP
          IF        @EQUAL
            MOVE      FMDFRESP,CURRDISP
          ENDIF
          MATCH     FMDFRESP,CURRDISP
          IF        @EQUAL
            CALL      GDIS0000                     * Calculate Current & YTD
            GOTO      SHWRES10
          ENDIF
.
          CALL      RESLIN00                     * Calculate Current & YTD
.
          GOTO      SHWRES10
.
SHWRES80  MOVE      "INVALID ACCOUNT",WEBTITLE
          CALL      WEBERR00
          GOTO      SHWRES99
.                  
SHWRES90  MATCH     SP70,CURRDISP
          IF        !@EQUAL
            CALL      RESLIN00
          ENDIF
          MOVE      "(999,999,999,999.99 ",EDITCUR
          FEDIT     TCURDIS,EDITCUR
          MOVE      "(999,999,999,999.99 ",EDITYTD 
          FEDIT     TYTDDIS,EDITYTD
          WRITE     HTMLFILE;"<TR><TD><B>Total</B></TD>":
                             "<TD ALIGN=RIGHT><B>",EDITCUR,"</B></TD>":
                             "<TD ALIGN=RIGHT><B>",EDITYTD,"</B></TD></TR>":
                             "</TABLE>"
          MOVE      "responsibility_chart.html",JAVATMPN
          CALL      JCHRT000
          CALL      WEBEND00
.
SHWRES99  RETURN
.------------------------------------------------------------
. Display Line Details
.------------------------------------------------------------
RESLIN00  MOVE      SP70,FMRSDESC
          PACK      KEY4,CURRDISP,SP70
          CALL      RDFMRS1
.
          MOVE      "(999,999,999,999.99 ",EDITCUR
          MULT      INCOMEFL,CURDIS
          FEDIT     CURDIS,EDITCUR
          MOVE      "(999,999,999,999.99 ",EDITYTD 
          MULT      INCOMEFL,YTDDIS
          FEDIT     YTDDIS,EDITYTD
          WRITE     HTMLFILE;"<TR><TD>",CURRDISP,SP1,FMRSDESC,"</TD>":
                                 "<TD ALIGN=RIGHT>",EDITCUR,"</TD>":
                                 "<TD ALIGN=RIGHT>",EDITYTD,"</TD></TR>"
          ADD       CURDIS,TCURDIS
          ADD       YTDDIS,TYTDDIS
          ADD       ONE,LASTARRY
          MOVE      FMRSDESC,NAMARR[LASTARRY]
          MOVE      CURDIS,ACTARR[LASTARRY]
          MOVE      YTDDIS,BUDARR[LASTARRY]
          MOVE      "0",CURDIS
          MOVE      "0",YTDDIS
          CALL      GDIS0000                     * Calculate Current & YTD
          MOVE      FMDFRESP,CURRDISP
          RETURN
.
          INC       FMSAMAIO/INC       * account
          INC       FMSCONIO/INC       * controlf
          INC       FMSDSFIO/INC       * dissection accrual summary file
          INC       FMSLMAIO/INC       * ledger
          INC       FMSLMCIO/INC       * ledger financial calendar
          INC       FMSSQUIO/INC       * enquiry security
          INC       FMSDISIO/INC       * dissection
          INC       FMSRESIO/INC       * responsibility
          INC       IBAWEBCD    
.------------------------------------------------------------
. Create Links to Next and Last Period
.------------------------------------------------------------
NXTLST00  MOVE      PERIODNO,F2
          MOVE      CURYEAR,F4
          SUB       ONE,F2
          IF        F2=0
            MOVE      "12",F2
            SUB       ONE,F4
          ENDIF
          MOVE      F2,KEY2
          REP       " 0",KEY2
          MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
          MOVE      F4,KEY4
          REP       " 0",KEY4
          CLEAR     LASTLINK
          APPEND    "fmsweb02.pbl?reportno=",LASTLINK
          APPEND    LINKREPT,LASTLINK
          APPEND    "&template=",NEXTLINK
          APPEND    KEY3,NEXTLINK
          APPEND    "&fincyear=",LASTLINK
          APPEND    KEY4,LASTLINK
          APPEND    "&fincperd=",LASTLINK
          APPEND    KEY2,LASTLINK
          APPEND    "&acctkeys=",LASTLINK
          STRIP     ACCTKEYS
          APPEND    ACCTKEYS,LASTLINK
          RESET     LASTLINK
.
          MOVE      PERIODNO,F2
          MOVE      CURYEAR,F4
          ADD       ONE,F2
          IF        F2=13
            MOVE      "1",F2
            ADD       ONE,F4
          ENDIF
          MOVE      F2,KEY2
          REP       " 0",KEY2
          MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
          MOVE      F4,KEY4
          REP       " 0",KEY4
          CLEAR     NEXTLINK
          APPEND    "fmsweb02.pbl?reportno=",NEXTLINK
          APPEND    LINKREPT,NEXTLINK
          APPEND    "&template=",NEXTLINK
          APPEND    KEY3,NEXTLINK
          APPEND    "&fincyear=",NEXTLINK
          APPEND    KEY4,NEXTLINK
          APPEND    "&fincperd=",NEXTLINK
          APPEND    KEY2,NEXTLINK
          MATCH     "1",LINKREPT
          APPEND    "&acctkeys=",NEXTLINK
          STRIP     ACCTKEYS
          APPEND    ACCTKEYS,NEXTLINK
          RESET     NEXTLINK
.
NXTLST99  RETURN
.------------------------------------------------------------
. Budget Dataset
.------------------------------------------------------------
BUDDAT00  MOVE      LASTARRY,F2
          COMPARE   ZERO,LASTARRY
          GOTO      BUDDAT99 IF EQUAL
          WRITE     HTMLFILE;"  <param name=dataset",DSETNUM,DSETAXIS,"Values ":
                                    "value=#"",BUDARR[F2];
BUDDAT10  SUB       ONE,F2
          COMPARE   ZERO,F2
          GOTO      BUDDAT90 IF EQUAL
          WRITE     HTMLFILE;",",BUDARR[F2];
          GOTO      BUDDAT10
BUDDAT90  WRITE     HTMLFILE;"#">"
BUDDAT99  RETURN
.------------------------------------------------------------
. Actuals Dataset
.------------------------------------------------------------
ACTDAT00  MOVE      LASTARRY,F2
          COMPARE   ZERO,LASTARRY
          GOTO      ACTDAT99 IF EQUAL
          WRITE     HTMLFILE;"  <param name=dataset",DSETNUM,DSETAXIS,"Values ":
                                    "value=#"",ACTARR[F2];
ACTDAT10  SUB       ONE,F2
          COMPARE   ZERO,F2
          GOTO      ACTDAT90 IF EQUAL
          WRITE     HTMLFILE;",",ACTARR[F2];
          GOTO      ACTDAT10
ACTDAT90  WRITE     HTMLFILE;"#">"
ACTDAT99  RETURN
.------------------------------------------------------------
. Colors Dataset
.------------------------------------------------------------
COLDAT00  MOVE      LASTARRY,F2
          COMPARE   ZERO,LASTARRY
          GOTO      COLDAT99 IF EQUAL
          WRITE     HTMLFILE;" <param name=dataset",DSETNUM,"Colors ":
                                    "value=#"",COLARR[F2];
COLDAT10  SUB       ONE,F2
          COMPARE   ZERO,F2
          GOTO      COLDAT90 IF EQUAL
          WRITE     HTMLFILE;",",COLARR[F2];
          GOTO      COLDAT10
COLDAT90  WRITE     HTMLFILE;"#">"
COLDAT99  RETURN
.------------------------------------------------------------
. Name Dataset
.------------------------------------------------------------
NAMDAT00  MOVE      LASTARRY,F2
          COMPARE   ZERO,LASTARRY
          GOTO      NAMDAT99 IF EQUAL
          STRIP     NAMARR[F2]
          MOVELPTR  NAMARR[F2],LENGTH
          SFORMAT   VAR,LENGTH
          MOVE      NAMARR[F2],VAR
          WRITE     HTMLFILE;"  <param name=dataset",DSETNUM,"Labels ":
                                    "value=#"",VAR;
          SFORMAT   VAR,127
NAMDAT10  SUB       ONE,F2
          COMPARE   ZERO,F2
          GOTO      NAMDAT90 IF EQUAL
          STRIP     NAMARR[F2]
          MOVELPTR  NAMARR[F2],LENGTH
          SFORMAT   VAR,LENGTH
          MOVE      NAMARR[F2],VAR
          WRITE     HTMLFILE;",",VAR;
          SFORMAT   VAR,127
          GOTO      NAMDAT10
NAMDAT90  WRITE     HTMLFILE;"#">"
NAMDAT99  RETURN
.------------------------------------------------------------
. Name Dataset
.------------------------------------------------------------
LNKDAT00  MOVE      LASTARRY,F2
          COMPARE   ZERO,LASTARRY
          GOTO      LNKDAT99 IF EQUAL
          STRIP     LNKARR[F2]
          MOVELPTR  LNKARR[F2],LENGTH
          SFORMAT   VAR,LENGTH
          MOVE      LNKARR[F2],VAR
          WRITE     HTMLFILE;"  <param name=dataset",DSETNUM,"Links ":
                                    "value=#"",VAR;
          SFORMAT   VAR,127
LNKDAT10  SUB       ONE,F2
          COMPARE   ZERO,F2
          GOTO      LNKDAT90 IF EQUAL
          STRIP     LNKARR[F2]
          MOVELPTR  LNKARR[F2],LENGTH
          SFORMAT   VAR,LENGTH
          MOVE      LNKARR[F2],VAR
          WRITE     HTMLFILE;",",VAR;
          SFORMAT   VAR,127
          GOTO      LNKDAT10
LNKDAT90  WRITE     HTMLFILE;"#">"
LNKDAT99  RETURN
.
SETCOL00  MOVE      "CC0000",COLARR[1]
          MOVE      "CC00CC",COLARR[2]
          MOVE      "0000CC",COLARR[3]
          MOVE      "00CCCC",COLARR[4]
          MOVE      "00CC00",COLARR[5]
          MOVE      "CCCC00",COLARR[6]
          MOVE      "000000",COLARR[7]
          MOVE      "CCCCCC",COLARR[8]
.
          MOVE      "FF0000",COLARR[9]
          MOVE      "FF00FF",COLARR[10]
          MOVE      "0000FF",COLARR[11]
          MOVE      "00FFFF",COLARR[12]
          MOVE      "00FF00",COLARR[13]
          MOVE      "FFFF00",COLARR[14]
          MOVE      "999999",COLARR[15]
          MOVE      "FFFFFF",COLARR[16]
.
          MOVE      "330000",COLARR[17]
          MOVE      "330033",COLARR[18]
          MOVE      "000033",COLARR[19]
          MOVE      "003333",COLARR[20]
          MOVE      "003300",COLARR[21]
          MOVE      "333300",COLARR[22]
          MOVE      "666666",COLARR[23]
          MOVE      "333333",COLARR[24]
.
          RETURN
.
.---------------------------------------------------------------------
. PERD - Set current period to last unlocked period   Called By ML
.        Requires - Control File sector 52 read
.                   FMLALEDG (ledger)
.        Returns  - EXIT     (1=quit)
.---------------------------------------------------------------------
PERD0000  PACK      KEY2,FMLALEDG,SP70
          CALL      RDFMLA1
          MATCH     SP70,FINCYEAR
          IF        @EQUAL
            PACK      KEY6,FMLALEDG,FMLACYRR,SP70
          ELSE
            PACK      KEY6,FMLALEDG,FINCYEAR,SP70
            UNPACK    KEY6,FMLALEDG,FMLACYRR
          ENDIF
          CALL      RDFMLC1
          MOVE      FMLACYRR,CURYEAR   * start at current year
          MATCH     SP70,FINCPERD
          IF        @EQUAL
            MOVE      FMLAPER,PERIODNO   * start at current period
          ELSE
            MOVE      FINCPERD,PERIODNO   * start at current period
          ENDIF
PERD0100  LOAD      F1,PERIODNO,FMLC01IN,FMLC02IN,FMLC03IN,FMLC04IN,FMLC05IN:
                                FMLC06IN,FMLC07IN,FMLC08IN,FMLC09IN,FMLC10IN:
                                FMLC11IN,FMLC12IN,FMLC13IN
          BRANCH    F1,PERD9000        * continue if period locked
          IF        PERIODNO=1
            CALL      RPFMLC1
            BRANCH    OVRCD,PERD9500          * no more data ?
            MATCH     FMLALEDG,FMLCLEDG
            GOTO      PERD9500 IF NOT EQUAL   * no more data
            MOVE      "12",PERIODNO
            ADD       FMLAPERS,PERIODNO
          ELSE
            SUB       ONE,PERIODNO
          ENDIF
          GOTO      PERD0100           * check current period
.
PERD9000  MOVE      ZERO,EXIT          * continue
          GOTO      PERD9999
.
PERD9500  MOVE      ONE,EXIT           * quit
.
PERD9999  RETURN
.
