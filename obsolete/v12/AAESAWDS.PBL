.******************************************************************************
.* System         : Accident and Emergency                                    *
.*                                                                            *
.* Include Name   : AAESAWDS.PBL                                              *
.*                                                                            *
.* Function       : Display the codes from the Employer/School file AAESAWFD  *
.*                                                                            *
.* Author         : Justin Coates      15/09/1993                             *
.*                                                                            *
.* Parameters     : AESWQTYP   the type of code to search for E/S             *
.*                                                                            *
.* Returns        : EXIT = TWO         no code selected but enter details     *
.*                  EXIT = ONE         exit                                   *
.*                  EXIT = ZERO        valid code selected                    *
.*                                                                            *
.* Modifications  :                                                           *
.******************************************************************************
AAESAWDS  DISPLAY   *P1:3,*EF:
                    *P1:4,"Name : ____________________"
.
          KEYIN     *P8:4,*V2LON,AESWQNAM:
                    *P8:4,*DV,AESWQNAM
.
HAESW050  DISPLAY   *P1:6,*EF:
                    *V2LON,*ULON,"Item",*P6:6,"Name                          ":
                    *P37:6,"Address                                 "
          MOVE      SIX,CVERT
          MOVE      ZERO,AESWQCNT
          MOVE      ONE,CPAGENO
          PACK      KEY37,AESWQTYP,AESWQNAM,SP20,SP20
          CALL      RSAESAW2                * positional read
.
HAESW100  CALL      RKAESAW2  
          BRANCH    OVRCD,HAESW500          * end of file
          MATCH     AESWTYPE,AESWQTYP
          GOTO      HAESW500 IF NOT EQUAL   * different type
.
          PACK      AESWQNAM,AESWQNAM,SP30,SP30
          MATCH     SP30,AESWQNAM
          GOTO      HAESW200 IF EQUAL       * display all
.
          STRIP     AESWQNAM
          MATCH     AESWQNAM,AESWNAME
          GOTO      HAESW500 IF NOT EQUAL   * different name
.
. ------- increment counters -------
.
HAESW200  ADD       ONE,CVERT               * inc line counter
          COMPARE   "23",CVERT
          GOTO      HAESW400 IF NOT LESS    * will fit on the page
.
          ADD       ONE,AESWQCNT            * add to count
.
. -------------------------------------------
. ------- display all the information -------
. -------------------------------------------
.
HAESW300  MOVE      AESWADD1,KEY40
          STRIP     KEY40
          ENDSET    KEY40
          APPEND    ", ",KEY40
          APPEND    AESWADD2,KEY40
          RESET     KEY40
          STRIP     KEY40
          ENDSET    KEY40
          APPEND    ", ",KEY40
          APPEND    AESWADD3,KEY40
          RESET     KEY40
          STRIP     KEY40
          ENDSET    KEY40
          APPEND    AESWADD4,KEY40
          APPEND    ", ",KEY40
          RESET     KEY40
          DISPLAY   *P2:CVERT,*V2LON,AESWQCNT,*HOFF,DOT,SP1,AESWNAME,SP1,KEY40
          PACK      AESWARRY[AESWQCNT],AESWTYPE,AESWNAME,AESWCODE
          GOTO      HAESW100
.
. ------- new page needed -------
.
HAESW400  BRANCH    CPAGENO,HAESW450        * first page
.
HAESW410  MOVE      ONE,AESWQLIN            * (N)ext, (F)irst
          CALL      HAESWK00
          BRANCH    EXIT,HAESW999,HAESW999,HAESW050,AAESAWDS,HAESW700
.                        exit     details  first    next     more
          GOTO      HAESW600
.
HAESW450  MOVE      TWO,AESWQLIN            * (N)ext only
          CALL      HAESWK00
          BRANCH    EXIT,HAESW999,HAESW999,HAESW590,AAESAWDS,HAESW700
.                        exit     details  first    next     more
          GOTO      HAESW600
.
. ------- end of data -------
.
HAESW500  BRANCH    CPAGENO,HAESW550        * first page
.
HAESW510  MOVE      THREE,AESWQLIN          * (F)irst
          CALL      HAESWK00
          BRANCH    EXIT,HAESW999,HAESW999,HAESW050,AAESAWDS,HAESW590
.                        exit     details  first    next     more
          GOTO      HAESW600
.
HAESW550  MOVE      FOUR,AESWQLIN           * no paging
          CALL      HAESWK00
          BRANCH    EXIT,HAESW999,HAESW999,HAESW590,AAESAWDS,HAESW590
.                        exit     details  first    next     more
          GOTO      HAESW600
.
HAESW590  BEEP
          BRANCH    AESWQLIN,HAESW410,HAESW450,HAESW510,HAESW550
.
. ------- item selected -------
.
HAESW600  MOVE      AESWARRY[FORM2],KEY37
          CALL      RDAESAW2
          BRANCH    OVRCD,HAESW590
.
          MOVE      ZERO,EXIT
          GOTO      HAESW999
.
. ------- (N)ext screen selected -------
.
HAESW700  DISPLAY   *P1:7,*EF;
          MOVE      SEVEN,CVERT             * first display row 
          MOVE      ONE,AESWQCNT            * reset counter
          ADD       ONE,CPAGENO             * add to page
          GOTO      HAESW300                * display the data
.
HAESW999  RETURN
+
.*********************************************************************
.         keyin 
.*********************************************************************
HAESWK00  DISPLAY   *P1:24,*EL,"Select item, (":
                    *V2LON,ANSE,*HOFF,")xit, (":
                    *V2LON,ANSN,*HOFF,")ext, (":
                    *V2LON,ANSD,*HOFF,")etails";
          MOVE      "38",CCOL
.
          IF        AESWQLIN = ONE | AESWQLIN = TWO
            DISPLAY   ", (",*V2LON,ANSM,*HOFF,")ore";
            ADD       EIGHT,CCOL
          ENDIF
          IF        AESWQLIN = ONE | AESWQLIN = THREE
            DISPLAY   ", (",*V2LON,ANSF,*HOFF,")irst";
            ADD       NINE,CCOL
          ENDIF
.
          DISPLAY   " : ";
          ADD       FOUR,CCOL
.
HAESWK10  KEYIN     *PCCOL:24,*V2LON,*JR,DIM2
          PACK      DIM2,DIM2,SP2
          TYPE      DIM2
          GOTO      HAESWK50 IF EQUAL       * number entered
.
          UNPACK    DIM2,ANS,ANS
          REP       UPPLOW,ANS
          MOVE      ZERO,EXIT
          REP       "E1D2F3N4M5",ANS
          MOVE      ANS,EXIT
          TYPE      ANS
          GOTO      HAESWK99 IF EQUAL       * valid number
.
HAESWK20  BEEP
          GOTO      HAESWK10
.
HAESWK50  MOVE      DIM2,FORM2
          MOVE      ZERO,EXIT
          COMPARE   FORM2,ZERO
          GOTO      HAESWK20 IF NOT LESS    * negative invalid
          COMPARE   FORM2,AESWQCNT
          GOTO      HAESWK20 IF LESS        * too high
.
HAESWK99  RETURN
+
