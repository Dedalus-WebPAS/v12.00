.******************************************************************************
.*                                                                            *
.* Include        : CCIDEPDS                                                  *
.*                                                                            *
.* Description    : Question mark routine for Costing Departments             *
.*                                                                            *
.* Author         : Peter Eddey   13/01/92                                    *
.*                                                                            *
.* Modifications  : 08/11/1993    Justin Coates  (Hawkes Bay mods)            *
.*                  add display of CCDTCSDP if want to                        *
.*                  10/07/1997  Steve Armstrong           WHL Mods            *
.*                  Mods for CCCNUFCD                                         *
.******************************************************************************
CCIDEPDS  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,SEVENTY1;*107,CCCNUFCD
          IF        CCCNUFCD > ZERO
            OPEN      CCIDPTA1,"ccidptaf"
          ENDIF
          PACK      KEY3,SP10
          CALL      RSCCDEP1                  * position on department file
          CALL      RKCCDEP1
          BRANCH    OVRCD,QDEPT200
.
.------ Display Heading, and then loop over entries ------
.
          DISPLAY   *P1:3,*EF,*P30:4,*V2LON,*ULON:
                    "Costing Departments"
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
.
          PACK      KEY3,SP10
          CALL      RSCCDEP1                  * position on department file
.
.------ Read through costing department file ------
.
QDEPT100  CALL      RKCCDEP1
          BRANCH    OVRCD,QDEPT400
.
.------ Display parameter variable and descriptions ------
.
          DISPLAY   *PCCOL:CVERT,*V2LON,CCDPDEPT,SP2,*HOFF,CCDPDESC;
          IF        CCCNUFCD > ZERO
            MOVE      CCDPDEPT,KEY3
            CALL      RDCCDPT1
            IF        OVRCD = ZERO
              IF        CCCNUFCD = 1
                MATCH     SP4,CCDTCSDP
                IF        !@EQUAL
                  ASSIGN    (CCOL+32),CCOL1
                  DISPLAY   *PCCOL1:CVERT," (",CCDTCSDP,")"
                ENDIF
              ENDIF
              IF        CCCNUFCD = 2
                MATCH     SP5,CCDTTRCS
                IF        !@EQUAL
                  ASSIGN    (CCOL+32),CCOL1
                  DISPLAY   *PCCOL1:CVERT," (",CCDTTRCS,")"
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      QDEPT100 IF LESS
.
.------ Set up second column display positions ------
.
          MOVE      FIVE,CVERT
          ADD       "40",CCOL
          COMPARE   "70",CCOL
          GOTO      QDEPT100 IF LESS
.
.------ Next screen or Continue ? ------
.
          KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
          REP       UPPLOW,ANS
.
          CMATCH    ANSC,ANS
          GOTO      QDEPT400 IF EQUAL
.
.------ Reset to first column display positions ------
.
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
          GOTO      QDEPT100
.
.------ No department codes on file ------
.
QDEPT200  DISPLAY  *P1:24,*EL,"No costing department codes on file.  ";
          CALL      HITENTER
          MOVE      ONE,OVRCD
          GOTO      QDEPT999
.
.------ End of codes ------
.
QDEPT400  MOVE      ZERO,OVRCD
          GOTO      QDEPT999
.
QDEPT999  RETURN
...............................................................................
