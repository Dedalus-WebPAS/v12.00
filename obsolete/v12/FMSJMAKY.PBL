.-------------------------------------------------------------------------------
.   System    :   FMS Accounting System  
.                           
.   Program   :   FMSJIDFM 
.                            
.   Routines  :   KJMAFM05 Keyin Jnl ID   - Returns a valid record
.   Regires   :   FMJMJTY to be set
.   
.   Return    :   EXIT = 1 null entry EXIT = 2 EXITCHAR entered
.
.   Internal  :   HJMAFM00 Help Display for Jnl ID  
.                            
.   Redisplay :   RJMAFM00 Screen Redisplay Called for Jnl ID  
.-------------------------------------------------------------------------------
.   Mods      :    27.06.91 Neeriem Dye
.                           Stopped Floaters useless code from changing CCOL
.                           and CVERT for KEYDATE
.
.                      23.12.91 Neeriem Dye
.                      Change Awaiting Auth to Awaiting Submission
.                      srf 112139
.
.-------------------------------------------------------------------------------
. Normal Code Keyin - Code Must Exist Exit = 1 if Null Entry
.-------------------------------------------------------------------------------
KJMAFM00  MOVE      FMJMJTY,DIM1
.
KJMAFM05  MOVE      UNDLN70,FMJMJID
          MOVE      DIM1,FMJMJTY
          KEYIN     *PCCOL:CVERT,*EL,*DV,FMJMJID:
                    *PCCOL:CVERT,*V2LON,*JR,FMJMJID: 
                    *PCCOL:CVERT,*EL,*DV,FMJMJID
.
          ENDSET    FMJMJID
          APPEND    SP70,FMJMJID
          RESET     FMJMJID
          MATCH     SP70,FMJMJID             * System Code Spaces so exit set
          GOTO      KJMAFM90 IF EQUAL
.
          PACK      CPCDATE,SP5,EXITCHAR
          MATCH     FMJMJID,CPCDATE         * Exit Selected
          GOTO      KJMAFM95 IF EQUAL
.
          MATCH     "     ?",FMJMJID            * Help Required  
          GOTO      KJMAFM10 IF NOT EQUAL
          CALL      HJMAFM00                  * Display List Of FMJMJIDs
          PACK      CDATASAV,FMJMJID,SP70    * save data entered
          CALL      RJMAFM00                  * Redisplay Routine
          PACK      FMJMJID,CDATASAV,SP70    * restore data entered
.
          ENDSET    FMJMJID
          APPEND    SP70,FMJMJID
          RESET     FMJMJID
          MATCH     SP70,FMJMJID             * System Code Spaces so exit set
          GOTO      KJMAFM05 IF EQUAL
.
KJMAFM10  PACK      KEY7,DIM1,FMJMJID
          CALL      RDFMJM1                                   
          BRANCH    OVRCD,KJMAFM80
          MOVE      ZERO,EXIT                 * no entry so exit is set
          GOTO      KJMAFM99
.
KJMAFM80  DISPLAY   *P1:24,*EL,*B,"Journal ID Does Not Exist - ";
          CALL      HITENTER
          GOTO      KJMAFM05
.
KJMAFM90  MOVE      ONE,EXIT                  * null entry so exit is set
          GOTO      KJMAFM99
.
KJMAFM95  MOVE      TWO,EXIT                  * Exit character entered    
.
KJMAFM99  RETURN
.------------------------------------------------------------------------------
. Display List of All Codes
.------------------------------------------------------------------------------
HJMAFM00  CALL      SJMAFM00             * get search option
          BRANCH    HELPOPTN,HJMAFM91
.
          DISPLAY   *P1:4,*EF,*P4:5,*V2LON,*ULON,"Item":
                    *P9:5,"Jnl ID":
                    *P16:5,"Description":
                    *P52:5,"Entered   ":
                    *P63:5,"Status"
          MOVE      ZERO,CPAGENO
.
HJMAFM05  MOVE      FIVE,STDROW
          MOVE      ZERO,XITEMNO
.
HJMAFM10  BRANCH    HELPOPTN,HJMAFM99,HJMAFM12,HJMAFM14,HJMAFM15
.
HJMAFM12  CALL      RKFMJM3
          BRANCH    OVRCD,HJMAFM20
          GOTO      HJMAFM16
.
HJMAFM14  CALL      RKFMJM1
          BRANCH    OVRCD,HJMAFM20
          GOTO      HJMAFM16
.
HJMAFM15  CALL      RKFMJM2
          BRANCH    OVRCD,HJMAFM20
          MATCH     "1",FMJMSTAT
          GOTO      HJMAFM15 IF NOT EQUAL
          GOTO      HJMAFM16
.
HJMAFM16  MATCH     FMJMJTY,DIM1         * right journal type
          GOTO      HJMAFM20 IF NOT EQUAL
.
          UNPACK    FMJMDENT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          CALL      GTSTFM00              * get journal status
.
HJMAFM18  ADD       ONE,XITEMNO
          ADD       ONE,STDROW
          STORE     FMJMJID,XITEMNO,ST1,ST2,ST3,ST4,ST5:
                                    ST6,ST7,ST8,ST9,ST10:
                                    ST11,ST12,ST13,ST14,ST15:
                                    ST16,ST17
          DISPLAY   *P5:STDROW,*V2LON,XITEMNO,DOT,*HOFF:
                    *P9:STDROW,FMJMJID,*P16:STDROW,FMJMDESC:    
                    *P52:STDROW,CPCDATE,*P63:STDROW,KEY13
          COMPARE   "17",XITEMNO
          GOTO      HJMAFM10 IF NOT EQUAL
.
HJMAFM20  MOVE      ZERO,CCITEM
          MOVE      SP2,CCITEMD
          DISPLAY   *P1:24,*EL,"Select Item, ";
          COMPARE   "17",XITEMNO
          GOTO      HJMAFM25 IF NOT EQUAL
          DISPLAY   "(",*V2LON,"N",*HOFF,")ext page, "; 
.
HJMAFM25  COMPARE   ZERO,CPAGENO
          GOTO      HJMAFM26 IF EQUAL
          DISPLAY   "(",*V2LON,"P",*HOFF,")revious page, ";
.
HJMAFM26  KEYIN     "e(",*V2LON,"X",*HOFF,")it ? ",*JR,*V2LON,CCITEMD;
.
          RESET     CCITEMD
          GOTO      HJMAFM30 IF EOS      * Next Page
          REP       UPPLOW,CCITEMD
          MATCH     "  N",CCITEMD        * Next Page
          GOTO      HJMAFM30 IF EQUAL
          MATCH     "  P",CCITEMD        * Previous Page
          GOTO      HJMAFM35 IF EQUAL
          MATCH     "  X",CCITEMD        * Exit
          GOTO      HJMAFM91 IF EQUAL
.
          TYPE      CCITEMD
          GOTO      HJMAFM90 IF NOT EQUAL  * Invalid Entry
.
          MOVE      CCITEMD,CCITEM
          COMPARE   ONE,CCITEM
          GOTO      HJMAFM90 IF LESS       * Invalid Entry
          COMPARE   CCITEM,XITEMNO
          GOTO      HJMAFM90 IF LESS       * Invalid Entry
          GOTO      HJMAFM92               * Item Selected
.
HJMAFM30  COMPARE   "17",XITEMNO            * Next Page Selected
          GOTO      HJMAFM90 IF NOT EQUAL  * No Next Page Invalid Selection
          ADD       ONE,CPAGENO
          MOVE      ZERO,XITEMNO            
          MOVE      FIVE,STDROW
          DISPLAY   *P1:6,*EF
          GOTO      HJMAFM10
.
HJMAFM35  COMPARE   ZERO,CPAGENO           * Previous Page Selected
          GOTO      HJMAFM90 IF EQUAL      * No Previous Page Invalid Selection
          DISPLAY   *P1:6,*EF
          SUB       ONE,CPAGENO
          BRANCH    HELPOPTN,HJMAFM99,HJMAFM50,HJMAFM40,HJMAFM60
.
HJMAFM40  COMPARE   "17",XITEMNO      
          GOTO      HJMAFM45 IF EQUAL 
          CALL      RPFMJM1                 * Initial reverse read
          COMPARE   ZERO,XITEMNO      
          GOTO      HJMAFM47 IF EQUAL       * No data on screen ?
HJMAFM45  CALL      RPFMJM1
          BRANCH    OVRCD,HJMAFM05
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HJMAFM45 IF NOT EQUAL
          COMPARE   FIVE,STDROW
          GOTO      HJMAFM10 IF EQUAL       * Display Down The Page
HJMAFM47  MOVE      FIVE,STDROW
          MOVE      "17",XITEMNO
          GOTO      HJMAFM45
.
HJMAFM50  COMPARE   "17",XITEMNO      
          GOTO      HJMAFM55 IF EQUAL 
          CALL      RPFMJM3            * Initial reverse read
          COMPARE   ZERO,XITEMNO      
          GOTO      HJMAFM57 IF EQUAL       * No data on screen ?
HJMAFM55  CALL      RPFMJM3
          BRANCH    OVRCD,HJMAFM05
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HJMAFM55 IF NOT EQUAL
          COMPARE   FIVE,STDROW
          GOTO      HJMAFM10 IF EQUAL   * Display Down The Page
HJMAFM57  MOVE      FIVE,STDROW
          MOVE      "17",XITEMNO
          GOTO      HJMAFM55
.
HJMAFM60  COMPARE   "17",XITEMNO      
          GOTO      HJMAFM65 IF EQUAL 
          CALL      RPFMJM1                 * Initial reverse read
          COMPARE   ZERO,XITEMNO      
          GOTO      HJMAFM67 IF EQUAL       * No data on screen ?
HJMAFM65  CALL      RPFMJM1
          BRANCH    OVRCD,HJMAFM05
          MATCH     "0",FMJMSTAT
          GOTO      HJMAFM65 IF NOT EQUAL
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HJMAFM65 IF NOT EQUAL
          COMPARE   FIVE,STDROW
          GOTO      HJMAFM10 IF EQUAL       * Display Down The Page
HJMAFM67  MOVE      FIVE,STDROW
          MOVE      "17",XITEMNO
          GOTO      HJMAFM65
.
.
HJMAFM90  BEEP                          * invalid entry
          GOTO      HJMAFM20
.
HJMAFM91  MOVE      SP70,FMJMJID
          GOTO      HJMAFM99
.
HJMAFM92  MOVE      SP70,FMJMJID
          LOAD      FMJMJID,CCITEM,ST1,ST2,ST3,ST4,ST5:
                                  ST6,ST7,ST8,ST9,ST10:
                                  ST11,ST12,ST13,ST14,ST15:
                                  ST16,ST17
.
HJMAFM99  RETURN
.
.-----------------------------------------------------------------------------
. SJMAFM - get search option
.         Returns : HELPOPTN (option selected 1=exit,2=name,3=code,4=acc typ)
.-----------------------------------------------------------------------------
SJMAFM00  MOVE      SP70,HELPSRCH
          DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON," 0",*HOFF,". Exit":
                    *P1:5,*V2LON," 1",*HOFF,". Search by Date Entered":
                    *P1:6,*V2LON," 2",*HOFF,". Search by Journal ID"
          MATCH     FMJOPRE3,DIM1
          GOTO      SJMAFM05 IF EQUAL
.
          DISPLAY   *P1:7,*V2LON," 3",*HOFF,". Journals Awaiting Submission"
.
SJMAFM05  KEYIN     *P5:9,"Select Option : _",*P21:9,*V2LON,HELPOPTN
          ADD       ONE,HELPOPTN
          MATCH     FMJOPRE3,DIM1
          IF        @EQUAL
            BRANCH    HELPOPTN,SJMAFM99,SJMAFM10,SJMAFM20
          ELSE
            BRANCH    HELPOPTN,SJMAFM99,SJMAFM10,SJMAFM20,SJMAFM30
          ENDIF
          BEEP                         * illegal search option
          GOTO      SJMAFM05
.
SJMAFM10  MOVE      UNDLN70,FMAMDESC
          DISPLAY   *P1:4,*EF,*P5:4,"From Date            : "
          MOVE      CVERT,F3
          MOVE      CCOL,F2
          MOVE      "4",CVERT
          MOVE      "28",CCOL
          UNPACK    SP10,CCENT,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          CALL      KEYDATE
          MOVE      F3,CVERT
          MOVE      F2,CCOL
          PACK      FMJMDENT,CCENT,CYEAR,CMON,CDAY
          MOVE      FMJMDENT,CPCDATE
          RESET     HELPSRCH
          PACK      KEY15,DIM1,FMJMDENT,SP20
          CALL      RSFMJM3
          CALL      RPFMJM3
          GOTO      SJMAFM99
.
SJMAFM20  MOVE      UNDLN70,FMJMJID
          KEYIN     *P1:4,*EF,*P5:4,"From Journal Id      : ":
                    *DV,FMJMJID,*P28:4,*JR,*V2LON,FMJMJID,*EL
          MOVE      FMJMJID,HELPSRCH
          RESET     HELPSRCH
          PACK      KEY7,DIM1,FMJMJID,SP10
          CALL      RSFMJM1
          CALL      RPFMJM1
          GOTO      SJMAFM99
.
SJMAFM30  PACK      KEY8,DIM1,ONE,SP70
          CALL      RSFMJM2
          CALL      RPFMJM2
          GOTO      SJMAFM99
.
SJMAFM99  RETURN
.
.************************************************************************
.                          GTSTFM00                                          
.      Set KEY13 to be the status of the Journal
.************************************************************************
GTSTFM00  MOVE      "Unknown",KEY13       * just in case it is currupt
          MATCH     FMJOPRE1,DIM1        * is it a general Journal
          GOTO      GTSTFM30 IF NOT EQUAL
.
          MOVE      FMJMSTAT,F1
          MOVE      STAT0,KEY13
          LOAD      KEY13,F1,STAT1,STAT2,STATE,STATE,STATE:
                             STATE,STATE,STATE,STAT9
          GOTO      GTSTFM99
.
GTSTFM30  MATCH     FMJOPRE2,DIM1         * is it a Standing Journal
          GOTO      GTSTFM60 IF NOT EQUAL
.
          MATCH     "0",FMJMSTAT          * processed
          GOTO      GTSTFM40 IF NOT EQUAL
.
          MOVE      "Normal",KEY13
          GOTO      GTSTFM99
.
GTSTFM40  MOVE      "Await. Auth.",KEY13
          GOTO      GTSTFM99
.
GTSTFM60  MATCH     FMJOPRE3,DIM1         * is it a Periodic Journal
          GOTO      GTSTFM99 IF NOT EQUAL
.
          MATCH     "0",FMJMSTAT          * not submitted?
          GOTO      GTSTFM70 IF NOT EQUAL
.
          MOVE      "Unsubmitted",KEY13
          GOTO      GTSTFM99
.
GTSTFM70  MATCH     "1",FMJMSTAT          * partially submitted
          GOTO      GTSTFM80 IF NOT EQUAL
.
          MOVE      "Part. Sub.",KEY13
          GOTO      GTSTFM99
.
GTSTFM80  MATCH     "2",FMJMSTAT          * partially submitted
          GOTO      GTSTFM99 IF NOT EQUAL
.
          MOVE      "Submitted",KEY13
          GOTO      GTSTFM99
.
GTSTFM99  RETURN
