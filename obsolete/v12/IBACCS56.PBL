.------------------------------------------------------------------------------
          INC       STDGENDF     * Required Include
.
VERSION   INIT      "V12.00.00"
.-------------------------------------------------------
. V9.02.00  04.07.2002  Glenn Saunders
.           Export to new release i.e. v9.02 from r5.09
.------------------------------------------------------------------------------
. V5.04.02  01.11.1993 B.G.Ackland
.           Change "Procedure" to Patient Service
.------------------------------------------------------------------------------
.@@@@@ Start of Generated Code @@@@@
. NB - Code & Comments Included Before the above line 
.      will not be overwritten by the generation of a 
.      program. The STDGENDF file and VERSION must appear
.      before the line. 
.-----------------------------------------
.   Program       :   IBACCS56
.   Function      :   Cost Model - Unit Cost Analysis         
.   Generated  On :   04/07/2002
.-----------------------------------------
.=========================
.File Description Includes
.-------------------------
          INC       CCS56TFD    
          INC       CCSCALFD/INC
          INC       CCSCMAFD/INC
          INC       CCSCMBFD/INC
          INC       CCSCMCFD/INC
          INC       CCSCMDFD/INC
          INC       CCSCMEFD/INC
          INC       CCSCMFFD/INC
          INC       CCSCTYFD/INC
          INC       CCSDPTFD/INC
          INC       CCSHCDFD/INC
          INC       CCSPDPFD/INC
          INC       CCSTMPDF    
.=========================
.Local Variable Definition
.-------------------------
FROMPERD  DIM      20 * Scr 00 Fld     6
TOPERD    DIM      20 * Scr 00 Fld     7
FROMDEPT  DIM      43 * Scr 00 Fld     8
TODEPT    DIM      43 * Scr 00 Fld     9
REPTSEQ   FORM      1 * Scr 00 Fld    10
SEQDESC   DIM      15 * Scr 00 Fld    11
.
ACCEPT    FORM      1
SACCEPT   FORM      1
.
PRGID     INIT      "IBACCS56"
PRGNAM    INIT      "Cost Model - Unit Cost Analysis         "
.
.--------------------------------
. MAINLINE - Controlling Logic
.--------------------------------
          TOPIC     PRGID
          CLOCK     PORT,DIM2
          MOVE      DIM2,PORT
ML0000
          CALL      INIT0000
          CALL      SC00L000
          CHAIN     PGM
.---------------------------------------
. INIT - Open Files & Display Heading
.---------------------------------------
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CCENTRY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          DISPLAY   *P56:24,"Opening scrsbga1";
          OPEN      SCRSBGA1,"scrsbgaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA1,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA2,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrslta1";
          OPEN      SCRSLTA1,"scrsltaf"
          DISPLAY   *P56:24,"Opening ccscalaf";
          OPEN      CCSCALA1,"ccscalaf"
          DISPLAY   *P56:24,"Opening ccscalaf";
          OPEN      CCSCALA2,"ccscalaf"
          DISPLAY   *P56:24,"Opening ccscalaf";
          OPEN      CCSCALA3,"ccscalaf"
          DISPLAY   *P56:24,"Opening ccscmaaf";
          OPEN      CCSCMAA1,"ccscmaaf"
          DISPLAY   *P56:24,"Opening ccsctyaf";
          OPEN      CCSCTYA1,"ccsctyaf"
          DISPLAY   *P56:24,"Opening ccsctyaf";
          OPEN      CCSCTYA2,"ccsctyaf"
          DISPLAY   *P56:24,"Opening ccsdptaf";
          OPEN      CCSDPTA1,"ccsdptaf"
          DISPLAY   *P56:24,"Opening ccsdptaf";
          OPEN      CCSDPTA2,"ccsdptaf"
          DISPLAY   *P56:24,"Opening ccsdptaf";
          OPEN      CCSDPTA3,"ccsdptaf"
          DISPLAY   *P56:24,"Opening ccsdptaf";
          OPEN      CCSDPTA4,"ccsdptaf"
          DISPLAY   *P56:24,"Opening ccshcdaf";
          OPEN      CCSHCDA1,"ccshcdaf"
          DISPLAY   *P56:24,"Opening ccshcdaf";
          OPEN      CCSHCDA2,"ccshcdaf"
          DISPLAY   *P56:24,"Opening ccspdpaf";
          OPEN      CCSPDPA1,"ccspdpaf"
          DISPLAY   *P56:24,"Opening ccspdpaf";
          OPEN      CCSPDPA2,"ccspdpaf"
          DISPLAY   *P56:24,"Opening ccspdpaf";
          OPEN      CCSPDPA3,"ccspdpaf"
          DISPLAY   *P56:24,"Opening ccspdpaf";
          OPEN      CCSPDPA4,"ccspdpaf"
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.
.------------------------------------
. Called for Print Screen Routine 
.------------------------------------
PRTSVARS
          MATCH     "00",SCRID
          IF         @EQUAL
            CALL       PS00L000   * Cost Model Report Options          
          ENDIF
          RETURN
.--------------------------------
. Screen - Cost Model Report Options          
.--------------------------------
SC00L000
SC00L100
          MOVE      "00",SCRID
          MOVE      ZERO,PRCOL
          MOVE      ZERO,PRVERT
          MOVE      "   12",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SCPSCOL,PRCOL
            MOVE     SCPSROW,PRVERT
          ENDIF
          CALL      SBACK000 * Display Background
.
SC00L200
          CALL      CF00L000 * Clear Fields
SC00L300
          CALL      KY00L000 * Enter Key Fields
          BRANCH    EXIT,SC00L999,SC00L999
SC00L400
          CALL      WP00L000 * Load any WP Details
SC00L500
          CALL      LM00L000 * Load any Batch Details
SC00L600
          CALL      DF00L000 * Display Fields
SC00L700
          CALL      SF00L000 * Select Fields
          BRANCH    EXIT,SC00L100,SC00L100,SC00L200
SC00L800
SC00L900
SC00L999  RETURN
.--------------------------------------------------------
. Clear Fields for Screen : Cost Model Report Options          
.-------------------------------------------------------
CF00L000
          MOVE      SP70,CCMACOM 
          MOVE      SP70,CCDPHCD 
          MOVE      SP70,CCCAYEAR
          MOVE      SP70,FROMPERD
          MOVE      SP70,TOPERD  
          MOVE      SP70,FROMDEPT
          MOVE      SP70,TODEPT  
          MOVE      ZERO,REPTSEQ 
          MOVE      SP70,SEQDESC 
          MOVE      SP70,CCMADES 
          MOVE      SP70,CCHCDES 
CF00L999  RETURN
.--------------------------------------------------------
. Enter Key Fields for Cost Model Report Options          
.--------------------------------------------------------
KY00L000
.
KY00L110
.
.
          MOVE      "   15",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,CCMADES 
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          MOVE      "    1",FLDID
          CALL      GETPOSIT
          IF        EXIT=1
            MOVE      "Mandatory Field Missing. Fatal - ",DISPMSG
            BEEP
            CALL      MESSAGE1
            GOTO      FATALERR
          ENDIF
          MOVE      "0",CKEYTYP
          CALL      ECMOD000
          BRANCH    EXIT,KY00L900,KY00L900
          MOVE      ZERO,ACCEPT
          MOVE      CCMACOM,VAR      
          CALL      DISPITEM
.
.
          MOVE      "   15",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      CCMADES,VAR      
            CALL      DISPITEM
          ENDIF
.
KY00L120
.
.
          MOVE      "   16",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,CCHCDES 
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          MOVE      "    2",FLDID
          CALL      GETPOSIT
          IF        EXIT=1
            MOVE      "Mandatory Field Missing. Fatal - ",DISPMSG
            BEEP
            CALL      MESSAGE1
            GOTO      FATALERR
          ENDIF
          MOVE      "0",CKEYTYP
          CALL      KCCHCA00
          MOVE      CCHCHCD,CCDPHCD
          BRANCH    EXIT,KY00L110,KY00L900
          MOVE      ZERO,ACCEPT
          MOVE      CCDPHCD,VAR      
          CALL      DISPITEM
.
.
          MOVE      "   16",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      CCHCDES,VAR      
            CALL      DISPITEM
          ENDIF
.
KY00L130
          MOVE      "    4",FLDID
          CALL      GETPOSIT
          IF        EXIT=1
            MOVE      "Mandatory Field Missing. Fatal - ",DISPMSG
            BEEP
            CALL      MESSAGE1
            GOTO      FATALERR
          ENDIF
          MOVE      "0",CKEYTYP
          CALL      KCCCAB00
          BRANCH    EXIT,KY00L120,KY00L900
          MOVE      ZERO,ACCEPT
          CALL      MVACCEPT
          MOVE      CCCAYEAR,VAR     
          CALL      DISPITEM
          MOVE      ZERO,EXIT
          GOTO      KY00L999
.
KY00L900  MOVE      ONE,EXIT
KY00L999  RETURN
.---------------------------------------------------------
. Load WP files for : Cost Model Report Options          
.---------------------------------------------------------
WP00L000
          RETURN
.---------------------------------------------------------
. Load Batch Screen for : Cost Model Report Options          
.---------------------------------------------------------
LM00L000
          RETURN
.---------------------------------------------------------
. Display Fields for : Cost Model Report Options          
.---------------------------------------------------------
DF00L000
          BRANCH   ACCEPT,DF00L999
          PACK     KEY15,PRGID,SCRID,SP70
          CALL     RSSCPS1
DF00L010
          CALL     RKSCPS1
          BRANCH   OVRCD,DF00L900
          MATCH    PRGID,SCPSPID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SCRID,SCPSSID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SP70,SCPSMTY
          GOTO     DF00L010 IF NOT EQUAL
.
          CALL     PS00L000 * Display Field
          GOTO     DF00L010
DF00L900
DF00L999
          RETURN
.---------------------------------------------------------
. Display/Print Fields for : Cost Model Report Options          
.---------------------------------------------------------
PS00L000   PACK      VAR,SP70,SP70
          PACK     PRINTVAR,SP70,SP70
          MOVE     SCPSITM,FIELDNO
          BRANCH    FIELDNO,PS00L999,PS00L999,PS00L999,PS00L999:
                            PS00L999,PS00L101,PS00L102,PS00L103:
                            PS00L104,PS00L105,PS00L106,PS00L999:
                            PS00L999,PS00L999,PS00L107,PS00L108
          GOTO     PS00L999
.
PS00L101
          MOVE      FROMPERD,VAR     
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L102
          MOVE      TOPERD,VAR       
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L103
          MOVE      FROMDEPT,VAR     
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L104
          MOVE      TODEPT,VAR       
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L105
          MOVE      REPTSEQ,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L106
          MOVE      SEQDESC,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L107
          MOVE      CCMADES,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L108
          MOVE      CCHCDES,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
PS00L999
          RETURN
.---------------------------------------------------------
. Selections for : Cost Model Report Options          
.---------------------------------------------------------
SF00L000  PACK     KEY13,PRGID,SCRID,SP70
          CALL     RSSCSL1
SF00L001  BRANCH   ACCEPT,SF00L020
.
SF00L005  CALL     MTSTQ000
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L006,SF00L006,SF00L006,SF00L006:
                            SF00L006,SF00L101,SF00L006,SF00L102:
                            SF00L006,SF00L103,SF00L006,SF00L006:
                            SF00L104,SF00L105,SF00L006,SF00L006
SF00L006  BEEP
          GOTO     SF00L005
SF00L020  CALL     RKSCSL1
          BRANCH   OVRCD,SF00L090
          MATCH    PRGID,SCSLPID
          GOTO     SF00L090 IF NOT EQUAL
          MATCH    SCRID,SCSLSID
          GOTO     SF00L090 IF NOT EQUAL
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L020,SF00L020,SF00L020,SF00L020:
                            SF00L020,SF00L101,SF00L020,SF00L102:
                            SF00L020,SF00L103,SF00L020,SF00L020:
                            SF00L020,SF00L020,SF00L020,SF00L020
          GOTO     SF00L020
SF00L090  MOVE     ZERO,ACCEPT
          GOTO     SF00L001
.
SF00L101
.
.
          MOVE      "    7",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,TOPERD  
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      EPERD000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L101
            ENDIF
            BRANCH    EXIT,SF00L101,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      FROMPERD,VAR     
          CALL      DISPITEM
.
.
          MOVE      "    7",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      TOPERD,VAR       
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L102
.
.
          MOVE      "    9",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,TODEPT  
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      EDEPT000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L102
            ENDIF
            BRANCH    EXIT,SF00L102,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      FROMDEPT,VAR     
          CALL      DISPITEM
.
.
          MOVE      "    9",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      TODEPT,VAR       
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L103
.
.
          MOVE      "   11",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,SEQDESC 
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      ESEQ0000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L103
            ENDIF
            BRANCH    EXIT,SF00L103,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      REPTSEQ,VAR      
          CALL      DISPITEM
.
.
          MOVE      "   11",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      SEQDESC,VAR      
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L104
          MOVE      ZERO,EXIT
          GOTO      SF00L999
.
SF00L105
          CALL      REPT0000
          MOVE      ZERO,EXIT
          GOTO      SF00L999
SF00L990  MOVE      ONE,EXIT * Exit Char Restart
SF00L999  RETURN
.@@@@@ Function Restart Point @@@@@
.----------------------------------------
. Move one to accept to for insert mode
.----------------------------------------
MVACCEPT  MOVE      ONE,ACCEPT
          RETURN
ECMOD000  MOVE      ZERO,CKEYTYP
          CALL      CCSMOPCL    * Close Model
          CALL      KCCMAA00
          BRANCH    EXIT,ECMOD999,ECMOD999
.
          CALL      CCSMOPOP    * Open Model
          IF        EXIT=1
            MOVE      "Unable to Open Cost Model Files. - ",DISPMSG
            BEEP
            CALL      MESSAGE1
            GOTO      ECMOD000
          ENDIF
ECMOD999  RETURN
EPERD000   MOVE     "    6",FLDID
           CALL     GETPOSIT
           CALL     KCCCAA00
           BRANCH   EXIT,EPERD100,EPERD999
           MOVE     CCCAPER,FPERIOD
           PACK     FROMPERD,CCCAPER,SP1,CCCADES
           GOTO     EPERD150
EPERD100   MOVE     "Start             ",FROMPERD
           MOVE     SP70,FPERIOD
.
EPERD150   MOVE     "    7",FLDID
           CALL     GETPOSIT
           CALL     KCCCAA00
           BRANCH   EXIT,EPERD200,EPERD999
           PACK     TOPERD,CCCAPER,SP1,CCCADES
           MOVE     CCCAPER,LPERIOD
           MATCH    FPERIOD,LPERIOD
           GOTO     EPERD999 IF EQUAL
           GOTO     EPERD999 IF NOT LESS
           BEEP
           MOVE     "To Period Must Be Greater Than From Period - ",DISPMSG
           CALL     MESSAGE1
           GOTO     EPERD000
EPERD200   MOVE     "Finish             ",TOPERD
           MOVE     "ZZ",LPERIOD
.
EPERD999   MOVE     "    6",FLDID
           CALL     GETPOSIT
           RETURN
EDEPT000   MOVE     "    8",FLDID
           CALL     GETPOSIT
           MOVE     CCHCHCD,CCDPHCD
           CALL     KCCDPA00
           BRANCH   EXIT,EDEPT100,EDEPT999
           MOVE     CCDPDPT,FDEPART
           PACK     FROMDEPT,CCDPDPT,SP1,CCDPDES
           GOTO     EDEPT150
EDEPT100   MOVE     "Start             ",FROMDEPT
           MOVE     SP70,FDEPART
.
EDEPT150   MOVE     "    9",FLDID
           CALL     GETPOSIT
           MOVE     CCHCHCD,CCDPHCD
           CALL     KCCDPA00
           BRANCH   EXIT,EDEPT200,EDEPT999
           PACK     TODEPT,CCDPDPT,SP1,CCDPDES
           MOVE     CCDPDPT,LDEPART
           MATCH    FDEPART,LDEPART
           GOTO     EDEPT999 IF EQUAL
           GOTO     EDEPT999 IF NOT LESS
           BEEP
           MOVE     "To Department Must Be > From Department - ",DISPMSG
           CALL     MESSAGE1
           GOTO     EDEPT000
EDEPT200   MOVE     "Finish             ",TODEPT
           MOVE     "ZZZZZZZZ",LDEPART
.
EDEPT999   MOVE     "    8",FLDID
           CALL     GETPOSIT
           RETURN
ESEQ0000  MOVE      SP70,DISPMSG
          CALL      MESSAGE2
ESEQ0010  DISPLAY   *P1:24,*EL,"Report Sequence : ":
                    *V2LON,"1. ",*HOFF,"Cost Type, ":
                    *V2LON,"2. ",*HOFF,"Patient Service"
          MOVE      ONE,REPTSEQ
          KEYIN     *PCCOL:CVERT,*V2LON,*RV,REPTSEQ
          BRANCH    REPTSEQ,ESEQ0100,ESEQ0200
          BEEP
          GOTO      ESEQ0010
.
ESEQ0100  MOVE      "Cost Type",SEQDESC
          GOTO      ESEQ0999
ESEQ0200  MOVE      "Patient Service",SEQDESC
          GOTO      ESEQ0999
ESEQ0999  CALL      MESSAGE3
          RETURN

.--------------------------------------------------
. Parient Service Cost Allocation Report
.--------------------------------------------------
REPT0000  DISPLAY   *P1:24,*EL,"Printing : "
          MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
          MOVE      "99",CLNO
          CLEAR     FILENAME
          APPEND    "CCST56",FILENAME
          APPEND    PORT,FILENAME
          RESET     FILENAME
          REP       " 0",FILENAME
          MOVE      " 72 U1-4",KEY10
          PACK      COMMAND,ISBUILD,FILENAME,KEY10
          EXECUTE   COMMAND,TASKID
          OPEN      CCSEXTA1,FILENAME
.
          MOVE      CCCAYEAR,KEY4
          PACK      KEY6,KEY4,FPERIOD
          CALL      RDCCCA1
          MATCH     "  ",FPERIOD
          IF        @EQUAL
            CALL      RKCCCA1
          ENDIF
          MOVE      CCCADES,FROMPNAM
          MOVE      KEY4,CCCAYEAR
.
          PACK      KEY6,KEY4,LPERIOD
          CALL      RDCCCA1
          MATCH     "ZZ",LPERIOD
          IF        @EQUAL
            CALL      RPCCCA1
          ENDIF
          MOVE      CCCADES,TOPNAM
          MOVE      KEY4,CCCAYEAR
.
          PACK      KEY10,CCHCHCD,FDEPART,SP70
          CALL      RDCCDP1
          IF        OVRCD=0
            CALL      RPCCDP1
          ENDIF
REPT1000  CALL      RKCCDP1
          BRANCH    OVRCD,REPT9999
          MATCH     CCDPHCD,CCHCHCD
          GOTO      REPT9999 IF NOT EQUAL
          MATCH     CCDPDPT,LDEPART
          GOTO      REPT9999 IF LESS
.
          MATCH     "1",CCDPDTY
          GOTO      REPT1000 IF EQUAL
.
          CALL      DPTCST00         * Calculate Department Costs
.
          DISPLAY   *P12:24,CCDPHCD,CCDPDPT
.
          CALL      HEAD0000         * Page Break on Department
.
          MOVE      ZERO,TOTALDPT
          IF        REPTSEQ=1
            CALL      PCTS0000         * Cost Type Sequence
            MOVE      SP70,FTOTAL1
            MOVE      SP70,FTOTAL2
            MOVE      SP70,FTOTAL3
            MOVE      CCFED1,FTOTAL4
            MOVE      TOTALDPT,F12P2
            FEDIT     F12P2,FTOTAL4
            ADD       ONE,CLNO
            PRINT     "| ",*20,"Department Total",*72,"|",FTOTAL1," |":
                      FTOTAL2," |",FTOTAL3," |",FTOTAL4," |"
          ELSE
            CALL      PPCS0000         * Parient Service Sequence
            MOVE      SP70,FTOTAL1
            MOVE      SP70,FTOTAL2
            MOVE      SP70,FTOTAL3
            MOVE      SP70,FTOTAL4
            MOVE      SP70,FTOTAL5
            MOVE      CCFED1,FTOTAL6
            MOVE      TOTALDPT,F12P2
            FEDIT     F12P2,FTOTAL6
            ADD       ONE,CLNO
            CALL      UND132
            PRINT     "|",*21,"Department Total",*46,"|",FTOTAL1," |":
                      FTOTAL2," |",FTOTAL3,FTOTAL4,FTOTAL5," |",FTOTAL6," |"
          ENDIF
.
          GOTO      REPT1000
.
REPT9999  CALL      UND132
          PRINT     *50,"*** End of Report ***"
          MOVE      "Report Completed - ",DISPMSG
          CALL      MESSAGE1
          CLOSE     CCSEXTA1,DELETE
          RETURN
.---------------------------------------------------------------------------
. Print Department Details by Cost Type
.---------------------------------------------------------------------------
PCTS0000  PACK      KEY4,SP70
          CALL      RSCCCT1
PCTS1000  CALL      RKCCCT1
          BRANCH    OVRCD,PCTS9999
.
          CALL      CTYTOT00           * Calculate Cost Type Total for Period
          COMPARE   ZERO,TOTALCTY      * If Zero for Cost Type Ignore
          GOTO      PCTS1000 IF EQUAL
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      SP70,FTOTAL1
          MOVE      SP70,FTOTAL2
          MOVE      SP70,FTOTAL3
          MOVE      CCFED1,FTOTAL4
          MOVE      TOTALCTY,F12P2
          FEDIT     F12P2,FTOTAL4
.
          ADD       ONE,CLNO
          PRINT     "| ",CCCTCTY,SP1,CCCTDES:
                     *72,"|",FTOTAL1," |",FTOTAL2," |",FTOTAL3," |",FTOTAL4," |"
          CALL      UND132
.
          MOVE      ZERO,TOTALCTY
          MOVE      ZERO,TOTALRVI
.
          CALL      PPCD0000      * Print Parient Service within this cost type
.
          ADD       TOTALCTY,TOTALDPT
          MOVE      "9999,999.9999",FTOTAL1
          MOVE      SP70,FTOTAL2
          MOVE      SP70,FTOTAL3
          MOVE      CCFED1,FTOTAL4
.
          MOVE      TOTALRVI,F12P4
          FEDIT     F12P4,FTOTAL1
.
          MOVE      TOTALCTY,F12P2
          FEDIT     F12P2,FTOTAL4
          CALL      UND132
          ADD       ONE,CLNO
          PRINT     "| ",*20,"Total":
                     *72,"|",FTOTAL1," |",FTOTAL2," |",FTOTAL3," |",FTOTAL4," |"
          CALL      UND132
          GOTO      PCTS1000
.
PCTS9999  RETURN
.--------------------------------------------------
. Print Parient Service Details
.--------------------------------------------------
PPCD0000  PACK      KEY21,CCDPHCD,CCDPDPT,SP70
          CALL      RSCCPD4
PPCD1000  CALL      RKCCPD4
          BRANCH    OVRCD,PPCD9999
.
          PACK      KEY21,CCPDHCD,CCPDDPT,CCPDPCD
          PACK      KEY10,CCDPHCD,CCDPDPT,SP70
          MATCH     KEY10,KEY21
          GOTO      PPCD9999 IF NOT EQUAL
.
          MOVE      ZERO,PERIODNO
          MOVE      ZERO,QTYTOTAL
          MOVE      ZERO,RVITOTAL
          MOVE      ZERO,CSTTOTAL
          MOVE      ZERO,CTYTOTAL
          PACK      KEY24,CCPDHCD,CCPDDPT,CCPDPCD,CCCAYEAR,SP70
          PACK      KEY22,CCPDHCD,CCPDDPT,CCPDPCD,CCCAYEAR,SP70
          CALL      RSCCME2
.
PPCD1100  CALL      RKCCME2
          BRANCH    OVRCD,PPCD2000
          PACK      KEY24,CCMEHCD,CCMEDPT,CCMEPCD,CCMEYEAR,CCMEPER,SP70
          MATCH     KEY22,KEY24
          GOTO      PPCD2000 IF NOT EQUAL
          MATCH     CCMEPER,LPERIOD
          GOTO      PPCD2000 IF LESS
          MATCH     FPERIOD,CCMEPER
          GOTO      PPCD1100 IF LESS
.
          ADD       CCMEQTY,QTYTOTAL
          ADD       ONE,PERIODNO
.
          PACK      KEY28,CCMEHCD,CCMEDPT,CCMEYEAR,CCMEPER,CCMEPCD,CCCTCTY
          CALL      RDCCMD1
          BRANCH    OVRCD,PPCD1100
.
          ADD       CCMDRVI,RVITOTAL
          ADD       CCMDCST,CSTTOTAL
          ASSIGN    CTYTOTAL+(CCMEQTY*CCMDCST),CTYTOTAL
          GOTO      PPCD1100
.
PPCD2000  COMPARE   ZERO,PERIODNO
          GOTO      PPCD1000 IF EQUAL
          COMPARE   ZERO,CTYTOTAL
          GOTO      PPCD1000 IF EQUAL
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          ADD       CTYTOTAL,TOTALCTY
          ASSIGN    TOTALRVI+(RVITOTAL/PERIODNO),TOTALRVI
.
          MOVE      "9999,999.9999",FTOTAL1
          MOVE      "99,999,999.99",FTOTAL2
          MOVE      "9999,999.9999",FTOTAL3
          MOVE      "99,999,999.99",FTOTAL4
.
          ASSIGN    (RVITOTAL/PERIODNO),F12P4    * Average Rvi
          FEDIT     F12P4,FTOTAL1
.
          MOVE      QTYTOTAL,F12P2
          FEDIT     F12P2,FTOTAL2
.
          ASSIGN    (CSTTOTAL/PERIODNO),F12P4    * Average Cost
          FEDIT     F12P4,FTOTAL3
.
          MOVE      CTYTOTAL,F12P2
          FEDIT     F12P2,FTOTAL4
.
          ADD       ONE,CLNO
          PRINT     "|",*20,CCPDSEQ,SP2,CCPDPCD,CCPDDES:
                     *72,"|",FTOTAL1," |",FTOTAL2," |",FTOTAL3," |",FTOTAL4," |"
          GOTO      PPCD1000
PPCD9999  RETURN
.
HEAD0000  CALL      HEAD132
          ASSIGN    DPTDIR+DPTIND,F12P2
          PRINT        "Cost Model    : ",CCMACOM,SP5,CCMADES:
                    "                Sequence      : ",REPTSEQ,SP1,SEQDESC:
                    *N,"Hospital      : ",CCHCHCD,SP7,CCHCDES:
                    "                Year          : ",CCCAYEAR:
                    *N,"Department    : ",CCDPDPT,SP1,CCDPDES:
                    "                Period        : ",FROMPNAM," to ",TOPNAM:
                    *N,"Direct Cost   : ",DPTDIR:
                       "       Indirect Cost : ",DPTIND:
                       "       Total Cost    : ",F12P2
          CALL      UND132
          IF        REPTSEQ=1
            PRINT     "| Cost Type                                   ":
                      "                         |              |     ":
                      "         |              |              |"
            PRINT     "|                  Seq. Patient Service       ":
                      "                         |       R.V.I. |     ":
                      "  Volume |    Unit Cost |   Total Cost |"
          ELSE
            PRINT     "| Seq. Patient Service                       |":
                      "              |              |                ":
                      " Unit Cost              |              |"
            PRINT     "|                   Cost Type                |":
                      "       Volume |  Avg. R.V.I. |        Fixed   ":
                      "  Variable        Total |   Total Cost |"
          ENDIF
          CALL      UND132
          MOVE      "11",CLNO
          RETURN
.--------------------------------------------------------
. Determine the department level costs for period range
.--------------------------------------------------------
DPTCST00  PACK      KEY12,CCDPHCD,CCDPDPT,CCCAYEAR,SP70
          PACK      KEY16,CCDPHCD,CCDPDPT,CCCAYEAR,SP70
          MOVE      ZERO,DPTDIR
          MOVE      ZERO,DPTIND
          CALL      RSCCMC1
DPTCST10  CALL      RKCCMC1
          BRANCH    OVRCD,DPTCST99
          PACK      KEY16,CCMCHCD,CCMCDPT,CCMCYEAR,SP70
          MATCH     KEY12,KEY16
          GOTO      DPTCST99 IF NOT EQUAL
.
          MATCH     CCMCPER,LPERIOD
          GOTO      DPTCST99 IF LESS
          MATCH     FPERIOD,CCMCPER
          GOTO      DPTCST10 IF LESS
.
          ADD       CCMCDIR,DPTDIR
          ADD       CCMCIND,DPTIND
          GOTO      DPTCST10
.
DPTCST99  RETURN
.--------------------------------------------------------
. Determine the Cost Type level costs for period range
.--------------------------------------------------------
CTYTOT00  MOVE      ZERO,TOTALCTY
          PACK      KEY20,CCDPHCD,CCDPDPT,CCCTCTY,CCCAYEAR,SP70
          PACK      KEY18,CCDPHCD,CCDPDPT,CCCTCTY,CCCAYEAR,SP70
          CALL      RSCCMB2
CTYTOT10  CALL      RKCCMB2          * Costs by Cost Type
          BRANCH    OVRCD,CTYTOT99
          PACK      KEY20,CCMBHCD,CCMBDPT,CCMBCTY,CCMBYEAR,CCMBPER,SP70
          MATCH     KEY18,KEY20
          GOTO      CTYTOT99 IF NOT EQUAL
          MATCH     CCMBPER,LPERIOD
          GOTO      CTYTOT99 IF LESS
          MATCH     FPERIOD,CCMBPER
          GOTO      CTYTOT10 IF LESS
.
          ASSIGN    TOTALCTY+CCMBDIR+CCMBIND,TOTALCTY
          GOTO      CTYTOT10
.
CTYTOT99  RETURN
.---------------------------------------------------------------------------
. Print Department Details by Parient Service
.---------------------------------------------------------------------------
PPCS0000  PACK      KEY21,CCDPHCD,CCDPDPT,SP70
          CALL      RSCCPD4
PPCS1000  CALL      RKCCPD4
          BRANCH    OVRCD,PPCS9999
          PACK      KEY21,CCPDHCD,CCPDDPT,SP70
          PACK      KEY10,CCDPHCD,CCDPDPT,SP70
          MATCH     KEY10,KEY21
          GOTO      PPCS9999 IF NOT EQUAL
.
          COMPARE   "50",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          CALL      PCDTOT00
.
          MOVE      CCFED1,FTOTAL1
          MOVE      SP70,FTOTAL2
          MOVE      SP70,FTOTAL3
          MOVE      SP70,FTOTAL4
          MOVE      "9999,999.9999",FTOTAL5
          MOVE      CCFED1,FTOTAL6
.
          MOVE      TOTALQTY,F12P2
          FEDIT     F12P2,FTOTAL1
.
          ASSIGN    TOTALCST/TOTALQTY,F12P4
          FEDIT     F12P4,FTOTAL5
          IF        TOTALQTY=0
            MOVE      SP70,FTOTAL5
          ENDIF
.
          MOVE      TOTALCST,F12P2
          FEDIT     F12P2,FTOTAL6
          ADD       F12P2,TOTALDPT
.
          ADD       ONE,CLNO
          PRINT     "| ",CCPDSEQ,SP2,CCPDPCD,SP1,CCPDDES:
                    *46,"|",FTOTAL1," |",FTOTAL2," |",FTOTAL3:
                    FTOTAL4,FTOTAL5," |",FTOTAL6," |"
          CALL      UND132
.
          CALL      PCTY0000      * Print Cost Types within this Parient Service
.
          MOVE      "9999,999.9999",FTOTAL2
          MOVE      "9999,999.9999",FTOTAL3
          MOVE      "9999,999.9999",FTOTAL4
          MOVE      "9999,999.9999",FTOTAL5
          MOVE      CCFED1,FTOTAL6
.
          MOVE      TOTALRVI,F12P4
          FEDIT     F12P4,FTOTAL2
.
          MOVE      TOTALFIX,F12P4
          FEDIT     F12P4,FTOTAL3
.
          MOVE      TOTALVAR,F12P4
          FEDIT     F12P4,FTOTAL4
.
          ASSIGN    TOTALVAR+TOTALFIX,F12P4
          FEDIT     F12P4,FTOTAL5
.
          ASSIGN    F12P4*TOTALQTY,F12P2
          FEDIT     F12P2,FTOTAL6
.
          CALL      UND132
          ADD       ONE,CLNO
          PRINT     "|",*21,"Total",*46,"|",FTOTAL1," |",FTOTAL2," |":
                    FTOTAL3,FTOTAL4,FTOTAL5," |",FTOTAL6," |"
          CALL      UND132
          GOTO      PPCS1000
.
PPCS9999  RETURN
.------------------------------------------------------------
. Print Cost Type Analysis
.------------------------------------------------------------
PCTY0000  MOVE      ZERO,TOTALRVI
          MOVE      ZERO,TOTALFIX
          MOVE      ZERO,TOTALVAR
          MOVE      SP70,KEY4
          CALL      RSCCEX1
.
PCTY1000  CALL      RKCCEX1
          BRANCH    OVRCD,PCTY9999
.
          COMPARE   ZERO,CCEXCST
          GOTO      PCTY1000 IF EQUAL
.
          COMPARE   "60",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          MOVE      "9999,999.9999",FTOTAL2
          MOVE      "9999,999.9999",FTOTAL3
          MOVE      "9999,999.9999",FTOTAL4
          MOVE      "9999,999.9999",FTOTAL5
          MOVE      CCFED1,FTOTAL6
.
          ASSIGN    CCEXRVI/CCEXNUM,F12P4
          ADD       F12P4,TOTALRVI
          FEDIT     F12P4,FTOTAL2
.
          MOVE      CCEXCTY,KEY4
          CALL      RDCCCT1
          IF        OVRCD=1
            MOVE      "Invalid Cost Type",CCCTDES
            MOVE      "1",CCCTFIX
          ENDIF
.
          MOVE      CCEXCST,F12P2
          FEDIT     F12P2,FTOTAL6
.
          ASSIGN    CCEXCST/TOTALQTY,F12P4
          FEDIT     F12P4,FTOTAL5
          MATCH     "1",CCCTFIX
          IF        @EQUAL
            FEDIT     F12P4,FTOTAL3
            MOVE      SP70,FTOTAL4
            ADD       F12P4,TOTALFIX
          ELSE
            MOVE      SP70,FTOTAL3
            FEDIT     F12P4,FTOTAL4
            ADD       F12P4,TOTALVAR
          ENDIF
          ADD       ONE,CLNO
          PRINT     "|",*21,CCEXCTY,SP1,CCCTDES,"|",FTOTAL2," |":
                    FTOTAL3,FTOTAL4,FTOTAL5," |",FTOTAL6," |"
          GOTO      PCTY1000
.
PCTY9999  RETURN
.--------------------------------------------------------
. Determine the Cost Type level costs for period range
.--------------------------------------------------------
PCDTOT00  CALL      CLEXT000
.
          MOVE      ZERO,TOTALQTY
          MOVE      ZERO,TOTALCST
          PACK      KEY24,CCDPHCD,CCDPDPT,CCPDPCD,CCCAYEAR,SP70
          PACK      KEY22,CCDPHCD,CCDPDPT,CCPDPCD,CCCAYEAR,SP70
          CALL      RSCCME2
.
PCDTOT10  CALL      RKCCME2          * Costs by Cost Type
          BRANCH    OVRCD,PCDTOT99
          PACK      KEY24,CCMEHCD,CCMEDPT,CCMEPCD,CCMEYEAR,CCMEPER,SP70
          MATCH     KEY22,KEY24
          GOTO      PCDTOT99 IF NOT EQUAL
          MATCH     CCMEPER,LPERIOD
          GOTO      PCDTOT99 IF LESS
          MATCH     FPERIOD,CCMEPER
          GOTO      PCDTOT10 IF LESS
          ASSIGN    TOTALCST+(CCMECST*CCMEQTY),TOTALCST
          ASSIGN    TOTALQTY+CCMEQTY,TOTALQTY
.
          PACK      KEY28,CCMEHCD,CCMEDPT,CCMEYEAR,CCMEPER,CCMEPCD,SP70
          PACK      KEY24,CCMEHCD,CCMEDPT,CCMEYEAR,CCMEPER,CCMEPCD,SP70
          CALL      RSCCMD1
PCDTOT20  CALL      RKCCMD1
          BRANCH    OVRCD,PCDTOT10
          PACK      KEY28,CCMDHCD,CCMDDPT,CCMDYEAR,CCMDPER,CCMDPCD,SP70
          MATCH     KEY24,KEY28
          GOTO      PCDTOT10 IF NOT EQUAL
          CALL      EXTRA000
          GOTO      PCDTOT20
.
PCDTOT99  RETURN
.------------------------------------------------------------
. Clear Extract
.------------------------------------------------------------
CLEXT000  PACK      KEY4,SP70  * Clear Extract
          CALL      RSCCEX1
          CALL      RKCCEX1
          BRANCH    OVRCD,CLEXT999
          PACK      KEY4,CCEXCTY
          CALL      DECCEX1
          GOTO      CLEXT000
CLEXT999  RETURN
.------------------------------------------------------------
. Extract Cost Type Analysis
.------------------------------------------------------------
. CCSEXTA1   IFILE   SQL, FIXED=72, KEY="U1-4"
.
. CCEXCTY    DIM     4     4     1
. CCEXNUM    FORM    2     2     40
. CCEXCST    FORM    12.6  10    42
. CCEXRVI    FORM    12.6  10    62
.------------------------------------------------------------
EXTRA000  COMPARE   ZERO,CCMDCST
          GOTO      EXTRA999 IF EQUAL
.
          PACK      KEY4,CCMDCTY
          CALL      RDCCEX1
          IF        OVRCD=0
            ADD       ONE,CCEXNUM
            ADD       CCMDRVI,CCEXRVI
            ASSIGN    CCEXCST+(CCMDCST*CCMEQTY),CCEXCST
            CALL      UPCCEX1
          ELSE
            UNPACK    KEY4,CCEXCTY
            MOVE      ONE,CCEXNUM
            MOVE      CCMDRVI,CCEXRVI
            ASSIGN    CCMDCST*CCMEQTY,CCEXCST
            CALL      WRCCEX1
          ENDIF
.
EXTRA999  RETURN
.-------------------------------
. Includes for I/O and Keyins
.-------------------------------
          INC       CCS56TIO    
          INC       CCSCALIO/INC
          INC       CCSCALKY    
          INC       CCSCMAIO/INC
          INC       CCSCMAKY    
          INC       CCSCMBIO/INC
          INC       CCSCMCIO/INC
          INC       CCSCMDIO/INC
          INC       CCSCMEIO/INC
          INC       CCSCMFIO/INC
          INC       CCSCTYIO/INC
          INC       CCSDPTIO/INC
          INC       CCSDPTKY    
          INC       CCSHCDIO/INC
          INC       CCSHCDKY    
          INC       CCSMOPCD    
          INC       CCSPDPIO/INC
.
          INC       STDGENCD
.
