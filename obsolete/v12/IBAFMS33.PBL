. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS33                                             *
. * Desc      :   Selected Control Maintenance                         *
. **********************************************************************
. * Date      :   23.01.1991                                           *
. * Author    :   B.G.Ackland (IBA)                                    *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
. * VF.01.01  12.05.2000 Charles Handaya                               *
. *           Fix Screen Description                                   *
. **********************************************************************
.$$$$$$
.        PROGRAM IBAFMS33
.             open files and initialise variables
.             WHILE user wants to add/modify/delete DO
.                  get desired option (add, modify or delete)
.                  CASE option OF
.                       ADD     : WHILE user wants to add records DO
.                                      accept initial values
.                                      WHILE user wants modify screen data DO
.                                           get number of item to be modified
.                                           allow user to modify option
.                                      END WHILE
.                                      IF user wants to post data
.                                           THEN write new data
.                                      END IF
.                                 END WHILE
.                       MODIFY  : WHILE user wants to modify records DO
.                                      get and display desired record
.                                      WHILE user wants modify screen data DO
.                                           get number of item to be modified
.                                           allow user to modify option
.                                      END WHILE
.                                      IF user wants to post data
.                                           THEN update data
.                                      END IF
.                                 END WHILE
.                       DELETE  : WHILE user wants to delete records DO
.                                      get and display desired record
.                                      IF user wants to delete data
.                                           THEN delete data
.                                      END IF
.                                 END WHILE
.                  END CASE
.             END WHILE
.        END
.$$$$$$
. Standard FMS Accounting System Include
.---------------------------------------
          INC       FMSSTDDF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
          INC       FMSCAFFD/INC
          INC       FMSCSAFD/INC
.
          INC       FMSLMAFD/INC           * Ledger
          INC       FMSAMAFD/INC           * Account
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
KOPT      INIT      "      "   * vars for KOPT
KOPT1     INIT      "Insert"
KOPT2     INIT      "Change"
KOPT3     INIT      "Delete"
KOPT4     INIT      "Range "
.
ACCEPT    FORM      1                  * accept mode (1=on)
ACC1      DIM       12
ACC2      DIM       12
ACCDESC   DIM       35
ACCDESC1  DIM       35
ACCDESC2  DIM       35
BNKDESC   DIM       35
BNK2DESC  DIM       35
DISDESC   DIM       35
CRDDESC   DIM       35
DEBDESC   DIM       35
PAYDESC   DIM       35
AGSDESC   DIM       35
LCCITEM   FORM      2
.
PRGID     INIT      "IBAFMS33"
PRGNAM    INIT      "Selected Control Maintenance"
VERSION   INIT      "V12.00.00"
.
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML0100    CALL      KOPT0000           * insert/modify/delete/exit/range ?
          COMPARE   ZERO,COPTION
          GOTO      ML9999 IF EQUAL    * exit program
          COMPARE   "4",COPTION
          GOTO      ML4000 IF EQUAL    * range input
.
ML0200    CALL      PROC0000           * get entry
          BRANCH    EXIT,ML0100,ML0100 * exit option
.
          LOAD      ACCEPT,COPTION,ONE,ZERO,ZERO * set up mode
.
          BRANCH    COPTION,ML1000,ML1000,ML2000
.
.---- add/modify data ----
.
ML1000    CALL      SELA0000           * modify screen A
          BRANCH    EXIT,ML0200        * don't save data
.
          CALL      POST0000           * post screen A data
          GOTO      ML0200             * get next entry
.
.---- delete data ----
.
ML2000    CALL      SCRA0000           * display screen A
          CALL      DELQST             * ok to delete (y/n/c) ?
          BRANCH    CEXIT,ML2100,ML0200,ML0100
.
ML2100    CALL      DELT0000           * delete screen A data
          GOTO      ML0200             * get next entry
.
.---- range input ----
.
ML4000    CALL      KRAN0000           * get range
          BRANCH    EXIT,ML0100        * don't execute range input
.
ML5000    MOVE      ONE,ACCEPT         * in accept mode
          CALL      SELA0000           * modify data
          BRANCH    EXIT,ML4000        * don't execute range input
.
          CALL      PRAN0000           * post screen D data
          GOTO      ML4000             * get next entry
.
.
ML9999    
          CHAIN     PGM
.
.******************************************************************************
.* INIT - Open Files                             Called by ML0000             *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening fmscafaf";  * Control Accountfile
          OPEN      FMSCAFA1,"fmscafaf"
.
          DISPLAY   *P64:24,"fmsamaaf";
          OPEN      FMSAMAA1,"fmsamaaf"
.
          DISPLAY   *P64:24,"fmsamaaf";
          OPEN      FMSAMAA2,"fmsamaaf"
.
          DISPLAY   *P64:24,"fmsamaaf";
          OPEN      FMSAMAA3,"fmsamaaf"
.
          DISPLAY   *P64:24,"fmslmaaf";
          OPEN      FMSLMAA1,"fmslmaaf"
.
          DISPLAY   *P64:24,"fmscsaaf";
          OPEN      FMSCSAA1,"fmscsaaf"
.
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.******************************************************************************
.* KOPT - Get desired function                    Called by ML                *
.*       Displays option at *P50:2                                            *
.*       Display sub-option at *P65:2                                         *
.*       Requires : KOPT1, KOPT2, KOPT3 ....  (descriptions of options)       *
.*       Returns  : COPTION  (desired option)                                 *
.******************************************************************************
KOPT0000  DISPLAY   *P1:3,*EF:             * display screen
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = ",KOPT1:
                    *P1:6,*V2LON,"2",*HOFF," = ",KOPT2:
                    *P1:7,*V2LON,"3",*HOFF," = ",KOPT3:
                    *P1:8,*V2LON,"4",*HOFF," = ",KOPT4
.
KOPT1000  KEYIN     *P1:10,"Select Option : _",*EL,*P17:10,*V2LON,COPTION
          COMPARE   ZERO TO COPTION
          GOTO      KOPT9999 IF EQUAL       * exit ?
.
          MOVE      SP70,KOPT
          LOAD      KOPT,COPTION,KOPT1,KOPT2,KOPT3,KOPT4
          MATCH     SP70,KOPT
          GOTO      KOPT9000 IF NOT EQUAL   * valid option entered ?
          BEEP                              * illegal option, get another
          GOTO      KOPT1000
.
KOPT9000  DISPLAY   *P70:3,*V2LON,KOPT;     * display option
.
KOPT9999  RETURN
.******************************************************************************
.* SCRA - Display Screen A                       Called by SELA0000, redisps  *
.*       Requires : ACCEPT   (1=accept mode)                                  *
.******************************************************************************
SCRA0000  BRANCH    COPTION,SCRA0010,SCRA0010,SCRA0010,SCRA0020
.
SCRA0010  DISPLAY   *P1:4,"    Ledger               : ":
                           *V2LON,FMLALEDG,*HOFF,SP10,SP2,FMLADESC,*EL:
                    *P1:5,"    Account              : ":
                           *V2LON,FMCSACC,*HOFF,SP2,ACCDESC,*EF:
                    *P1:7,*V2LON," 1",*HOFF,". Bank Account         : ":
                    *P1:8,*V2LON," 2",*HOFF,". Bank Account         : ":
                    *P1:9,*V2LON," 3",*HOFF,". Creditors Control    : ":
                   *P1:10,*V2LON," 4",*HOFF,". Discount Control     : ":
                   *P1:11,*V2LON," 5",*HOFF,". Debtors Control      : ":
                   *P1:12,*V2LON," 6",*HOFF,". Payments in Suspense : ":
                   *P1:13,*V2LON," 7",*HOFF,". G.S.T. Control       : ";
          GOTO      SCRA0030
.
SCRA0020  DISPLAY   *P1:4,"    Ledger               : ":
                           *V2LON,FMLALEDG,*HOFF,SP10,SP2,FMLADESC,*EL:
                    *P1:5,"    First Account        : ":
                           *V2LON,ACC1,*HOFF,SP2,ACCDESC1,*EL:
                    *P1:6,"    Last  Account        : ":
                           *V2LON,ACC2,*HOFF,SP2,ACCDESC2,*EF:
                    *P1:8,*V2LON," 1",*HOFF,". Bank Account         : ":
                    *P1:9,*V2LON," 2",*HOFF,". Bank Account 2       : ":
                   *P1:10,*V2LON," 3",*HOFF,". Creditors Control    : ":
                   *P1:11,*V2LON," 4",*HOFF,". Discount Control     : ":
                   *P1:12,*V2LON," 5",*HOFF,". Debtors Control      : ":
                   *P1:13,*V2LON," 6",*HOFF,". Payments in Suspense : ":
                   *P1:14,*V2LON," 7",*HOFF,". G.S.T. Control       : ";
.
SCRA0030  MOVE      SP70,BNKDESC
          MOVE      SP70,BNK2DESC
          MOVE      SP70,CRDDESC
          MOVE      SP70,DISDESC
          MOVE      SP70,DEBDESC
          MOVE      SP70,PAYDESC
          MOVE      SP70,AGSDESC
          BRANCH    ACCEPT,SCRA9999
.
          PACK      KEY14,FMLALEDG,FMCSBNK2,SP70
          CALL      RDFMAM1
          BRANCH    OVRCD,SCRA0900
          MOVE      FMAMDESC,BNK2DESC
.
SCRA0900  PACK      KEY14,FMLALEDG,FMCSBNK,SP70
          CALL      RDFMAM1
          BRANCH    OVRCD,SCRA1000
          MOVE      FMAMDESC,BNKDESC
.
SCRA1000  PACK      KEY14,FMLALEDG,FMCSCRD,SP70
          CALL      RDFMAM1
          BRANCH    OVRCD,SCRA2000
          MOVE      FMAMDESC,CRDDESC
.
SCRA2000  PACK      KEY14,FMLALEDG,FMCSDIS,SP70
          CALL      RDFMAM1
          BRANCH    OVRCD,SCRA3000
          MOVE      FMAMDESC,DISDESC
.
SCRA3000  PACK      KEY14,FMLALEDG,FMCSDEB,SP70
          CALL      RDFMAM1
          BRANCH    OVRCD,SCRA4000
          MOVE      FMAMDESC,DEBDESC
.
SCRA4000  PACK      KEY14,FMLALEDG,FMCSPAY,SP70
          CALL      RDFMAM1
          BRANCH    OVRCD,SCRA5000
          MOVE      FMAMDESC,PAYDESC
.
SCRA5000  PACK      KEY14,FMLALEDG,FMCSAGST,SP70
          CALL      RDFMAM1
          BRANCH    OVRCD,SCRA7000
          MOVE      FMAMDESC,AGSDESC
.
SCRA7000  BRANCH    COPTION,SCRA7010,SCRA7010,SCRA7010,SCRA7020
.
SCRA7010  DISPLAY   *P28:7,*V2LON,FMCSBNK,*HOFF,SP2,BNKDESC:
                    *P28:8,*V2LON,FMCSBNK2,*HOFF,SP2,BNK2DESC:
                    *P28:9,*V2LON,FMCSCRD,*HOFF,SP2,CRDDESC:
                   *P28:10,*V2LON,FMCSDIS,*HOFF,SP2,DISDESC:
                   *P28:11,*V2LON,FMCSDEB,*HOFF,SP2,DEBDESC:
                   *P28:12,*V2LON,FMCSPAY,*HOFF,SP2,PAYDESC:
                   *P28:13,*V2LON,FMCSAGST,*HOFF,SP2,AGSDESC
          GOTO      SCRA9999
.
SCRA7020  DISPLAY   *P28:8,*V2LON,FMCSBNK,*HOFF,SP2,BNKDESC:
                    *P28:9,*V2LON,FMCSBNK2,*HOFF,SP2,BNK2DESC:
                   *P28:10,*V2LON,FMCSCRD,*HOFF,SP2,CRDDESC:
                   *P28:11,*V2LON,FMCSDIS,*HOFF,SP2,DISDESC:
                   *P28:12,*V2LON,FMCSDEB,*HOFF,SP2,DEBDESC:
                   *P28:13,*V2LON,FMCSPAY,*HOFF,SP2,PAYDESC:
                   *P28:14,*V2LON,FMCSAGST,*HOFF,SP2,AGSDESC
.
SCRA9999  RETURN
.******************************************************************************
.* PROC - Enter Control Account                  Called by ML0000             *
.*        Returns : EXIT     (2=EXITCHAR entered, 1=nothing entered, 0=cont.) *
.*                  FMCALED,FMCAACC (variable entered)                        *
.******************************************************************************
PROC0000  DISPLAY   *P1:4,*EF,"    Ledger               : ":
                        *P1:5,"    Account              : ";
.
          MOVE      "28",CCOL          * set up screen address
          MOVE      "4",CVERT
          CALL      KLMAFM00
          BRANCH    EXIT,PROC9999,PROC9999
          DISPLAY   *P28:4,*EL,*V2LON,FMLALEDG,*HOFF,SP10,SP2,FMLADESC
.
PROC0100  MOVE      "5",CVERT
          MOVE      "28",CCOL          * set up screen address
          COMPARE   ONE,COPTION        * insert mode ?
          GOTO      PROC1000 IF EQUAL
          CALL      KCSAFM00
          BRANCH    EXIT,PROC0000,PROC9999
          MOVE      FMAMDESC,ACCDESC
          DISPLAY   *P28:5,*EL,*V2LON,FMCSACC,*HOFF,SP2,ACCDESC
          GOTO      PROC9999
.
PROC1000  MOVE      "1",CACCTIND       * posting accounts only
          CALL      KAMAFM00
          BRANCH    EXIT,PROC0000,PROC9999
.
          PACK      KEY14,FMLALEDG,FMAMACCT,SP70
          UNPACK    KEY14,FMCSLED,FMCSACC
          MOVE      FMAMDESC,ACCDESC
          DISPLAY   *P28:5,*EL,*V2LON,FMCSACC,*HOFF,SP2,ACCDESC
          CALL      RDFMCS1
          BRANCH    OVRCD,PROC9999
          DISPLAY   *P1:24,*EL,"Account Already on File - ";
          CALL      HITENTER
          GOTO      PROC0100
.
PROC9999  RETURN
.******************************************************************************
.* SELA - Modify screen A data                   Called by ML0000             *
.*        Returns : EXIT     (2=EXITCHAR entered, 1=nothing entered, 0=cont.) *
.******************************************************************************
SELA0000  LOAD      FMCSBNK,ACCEPT,SP70          * clear vars in accept mode
          LOAD      FMCSBNK2,ACCEPT,SP70
          LOAD      FMCSCRD,ACCEPT,SP70
          LOAD      FMCSDIS,ACCEPT,SP70
          LOAD      FMCSDEB,ACCEPT,SP70
          LOAD      FMCSPAY,ACCEPT,SP70
          LOAD      FMCSAGST,ACCEPT,SP70
.
          CALL      SCRA0000
          MOVE      ZERO,LCCITEM
.
.---- determine option to execute ----
.
SELA0100  ADD       ONE,LCCITEM                  * get next option
          BRANCH    ACCEPT,SELA0500              * in accept mode ?
          CALL      MAINQST                      * select/post/cancel ?
          MOVE      CCITEM,LCCITEM
.
          COMPARE   ZERO,LCCITEM
          GOTO      SELA9000 IF EQUAL            * post ?
.
          COMPARE   SEQ,LCCITEM
          GOTO      SELA9500 IF EQUAL            * cancel ?
.
SELA0500  MOVE      "28",CCOL                    * set up screen address
          LOAD      CVERT,COPTION,SIX,SIX,SIX,SEVEN
          ADD       LCCITEM,CVERT
          BRANCH    LCCITEM,SELA1100,SELA1150,SELA1200,SELA1300,SELA1400:
                            SELA1500,SELA1600
.
          BRANCH    ACCEPT,SELA0900              * finished accept mode ?
          BEEP                                   * illegal option
.
SELA0900  MOVE      ZERO,ACCEPT                  * clear accept mode
          GOTO      SELA0100                     * get next option
.
.---- option 1 ---- Bank
.
SELA1100  MOVE      "1",CCONTIND
          CALL      KCAFFM00
          BRANCH    EXIT,SELA1110,SELA9500
          MOVE      FMAMACCT,FMCSBNK
          MOVE      FMAMDESC,BNKDESC
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMACCT,*HOFF,SP2,FMAMDESC
          GOTO      SELA0100
.
SELA1110  MOVE      SP70,FMCSBNK
          MOVE      SP70,BNKDESC
          DISPLAY   *PCCOL:CVERT,*EL
          GOTO      SELA0100
.
.---- option 1 ---- Bank
.
SELA1150  MOVE      "1",CCONTIND
          CALL      KCAFFM00
          BRANCH    EXIT,SELA1160,SELA9500
          MOVE      FMAMACCT,FMCSBNK2
          MOVE      FMAMDESC,BNK2DESC
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMACCT,*HOFF,SP2,FMAMDESC
          GOTO      SELA0100
.
SELA1160  MOVE      SP70,FMCSBNK2
          MOVE      SP70,BNK2DESC
          DISPLAY   *PCCOL:CVERT,*EL
          GOTO      SELA0100
.
.---- option 2 ---- Creditors
.
SELA1200  MOVE      "2",CCONTIND
          CALL      KCAFFM00
          BRANCH    EXIT,SELA1210,SELA9500
          MOVE      FMAMACCT,FMCSCRD
          MOVE      FMAMDESC,CRDDESC
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMACCT,*HOFF,SP2,FMAMDESC
          GOTO      SELA0100
.
SELA1210  MOVE      SP70,FMCSCRD
          MOVE      SP70,CRDDESC
          DISPLAY   *PCCOL:CVERT,*EL
          GOTO      SELA0100
.
.---- option 3 ---- Discount
.
SELA1300  MOVE      "3",CCONTIND
          CALL      KCAFFM00
          BRANCH    EXIT,SELA1310,SELA9500
          MOVE      FMAMACCT,FMCSDIS
          MOVE      FMAMDESC,DISDESC
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMACCT,*HOFF,SP2,FMAMDESC
          GOTO      SELA0100
.
SELA1310  MOVE      SP70,FMCSDIS
          MOVE      SP70,DISDESC
          DISPLAY   *PCCOL:CVERT,*EL
          GOTO      SELA0100
.
.---- option 4 ---- Debtors
.
SELA1400  MOVE      "4",CCONTIND
          CALL      KCAFFM00
          BRANCH    EXIT,SELA1410,SELA9500
          MOVE      FMAMACCT,FMCSDEB
          MOVE      FMAMDESC,DEBDESC
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMACCT,*HOFF,SP2,FMAMDESC
          GOTO      SELA0100
.
SELA1410  MOVE      SP70,FMCSDEB
          MOVE      SP70,DEBDESC
          DISPLAY   *PCCOL:CVERT,*EL
          GOTO      SELA0100
.
.---- option 5 ---- Payments in Suspense
.
SELA1500  MOVE      "6",CCONTIND
          CALL      KCAFFM00
          BRANCH    EXIT,SELA1510,SELA9500
          MOVE      FMAMACCT,FMCSPAY
          MOVE      FMAMDESC,PAYDESC
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMACCT,*HOFF,SP1,FMAMDESC
          GOTO      SELA0100
.
SELA1510  MOVE      SP70,FMCSPAY
          MOVE      SP70,PAYDESC
          DISPLAY   *PCCOL:CVERT,*EL
          GOTO      SELA0100
.
.---- option 6 ---- GST
.
SELA1600  MOVE      "7",CCONTIND
          CALL      KCAFFM00
          BRANCH    EXIT,SELA1610,SELA9500
          MOVE      FMAMACCT,FMCSAGST
          MOVE      FMAMDESC,AGSDESC
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMACCT,*HOFF,SP1,FMAMDESC
          GOTO      SELA0100
.
SELA1610  MOVE      SP70,FMCSAGST
          MOVE      SP70,AGSDESC
          DISPLAY   *PCCOL:CVERT,*EL
          GOTO      SELA0100
.
.---- exit points ----
.
SELA9000  MOVE      ZERO,EXIT                    * post
          GOTO      SELA9999
.
SELA9500  MOVE      ONE,EXIT                     * cancel
.
SELA9999  RETURN
.**********************************************************************
.  KRAN - Keyin range                                  Called By ML
.        Returns  - FMAMLEDG   (ledger)
.                   ACC1, ACC2 (from and to accounts)
.                   EXIT       (1=quit option)
.**********************************************************************
KRAN0000  UNPACK    SP70,ACC1,ACCDESC1
          UNPACK    SP70,ACC2,ACCDESC2
          DISPLAY   *P1:4,*EF,"    Ledger               : ";
          MOVE      "28",CCOL
          MOVE      "4",CVERT
          CALL      KLMAFM00                * get ledger
          BRANCH    EXIT,KRAN9500,KRAN9500  * exit ?
          DISPLAY   *P1:4,"    Ledger               : ":
                           *V2LON,FMLALEDG,*HOFF,SP10,SP2,FMLADESC,*EF:
                    *P1:5,"    First Account        : ":
                           *V2LON,ACC1,*HOFF,SP2,ACCDESC1:
                    *P1:6,"    Last  Account        : ";
.
KRAN0100  MOVE      "28",CCOL
          MOVE      "5",CVERT
          DISPLAY   *P28:6,*EF;             * clear last account from screen
          MOVE      "1",CACCTIND            * posting accounts only
          CALL      KAMAFM00                * get account
          BRANCH    EXIT,KRAN0000,KRAN9500  * exit ?
          PACK      ACC1,FMAMACCT,SP70
          PACK      ACCDESC1,FMAMDESC,SP70
          DISPLAY   *PCCOL:CVERT,*V2LON,ACC1,*HOFF,SP2,ACCDESC1;
.
          MOVE      "28",CCOL
          MOVE      "6",CVERT
          CALL      KAMAFM00                * get account
          BRANCH    EXIT,KRAN0100,KRAN9500  * exit ?
          PACK      ACC2,FMAMACCT,SP70
          PACK      ACCDESC2,FMAMDESC,SP70
          DISPLAY   *PCCOL:CVERT,*V2LON,ACC2,*HOFF,SP2,ACCDESC2;
.
          MATCH     ACC1,ACC2
          GOTO      KRAN9000 IF NOT LESS    * valid range ?
          DISPLAY   *P1:24,*B,*EL:
                    "First Account Cannot Be After Last Account - ";
          CALL      HITENTER
          GOTO      KRAN0100
.
KRAN9000  MOVE      ZERO,EXIT          * continue
          GOTO      KRAN9999
.
KRAN9500  MOVE      ONE,EXIT           * quit
.
KRAN9999  RETURN
.******************************************************************************
.* PRAN - Post data for range input              Called by ML, PRAN           *
.******************************************************************************
PRAN0000  PACK      KEY14,FMLALEDG,ACC1,SP70
          CALL      RSFMAM1                 * go to start of records
          CALL      RPFMAM1                 * go back one record
.
PRAN1000  CALL      RKFMAM1                 * get next record
          BRANCH    OVRCD,PRAN9999          * no more records ?
          MATCH     FMLALEDG,FMAMLEDG
          GOTO      PRAN9999 IF NOT EQUAL   * out of range ?
          MATCH     FMAMACCT,ACC2
          GOTO      PRAN9999 IF LESS        * out of range ?
          MATCH     "6",FMAMTYPE
          GOTO      PRAN1000 IF EQUAL       * posting type ?
.
          MOVE      FMAMLEDG,FMCSLED
          MOVE      FMAMACCT,FMCSACC
          CALL      POST0000                * write record
          GOTO      PRAN1000
.
PRAN9999  RETURN
.******************************************************************************
.* POST - Post data                              Called by ML, PRAN           *
.******************************************************************************
POST0000  DISPLAY   *P1:24,"Posting : ",*V2LON,FMCSLED,SLASH,FMCSACC,*EL;
          PACK      KEY14,FMCSLED,FMCSACC,SP70
          CALL      DEFMCS1                 * delete old record (if possible)
          CALL      WRFMCS1                 * write new record
.
POST9999  RETURN
.******************************************************************************
.* DELT - Delete data                            Called by ML0000             *
.******************************************************************************
DELT0000  PACK      KEY14,FMCSLED,FMCSACC,SP70
          CALL      DEFMCS1                 * delete old record (if possible)
.
DELT9999  RETURN
.******************************************************************************
.* INCLUDES FOR I/O'S                                                         *
.******************************************************************************
.
          INC       FMSCAFIO/INC       * Control Account
          INC       FMSLMAIO/INC           * Ledger
          INC       FMSAMAIO/INC           * Account
          INC       FMSCSAIO/INC           * Selected Control File
.
          INC       FMSLMAKY           * Ledger
          INC       FMSAMAKY           * Account
          INC       FMSCSAKY           * Selected Control File
          INC       FMSCAFKY           * Control Account
.
          INC       FMSSTDCD           * FMS Acc. standard routines
