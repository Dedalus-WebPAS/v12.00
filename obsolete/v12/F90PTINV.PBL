.*****************************************************************************
.*    Program      : F90PTINV                                                *
.*    Description  : Conversion patinvrf to new File layout                  *
.*                                                                           *
.*    Author       : J.Tan          CAR 169149                               *
.*    Date         : 23/05/2008                                              *
.*    Modifications:                                                         *
.*                 :                                                         *
.*****************************************************************************
.
          INC       STD001FD
. New FD
.
.Data File Definition
.--------------------
PATINVR1   IFILE SQL, FIXED=370, KEY="U1-8"
PATINVR2   IFILE SQL, FIXED=370, KEY="U9-16,1-8"
PATINVR3   IFILE SQL, FIXED=370, KEY="U23-30,1-8"
PATINVR4   IFILE SQL, FIXED=370, KEY="U39-39,40-45,46-48,17-22,23-30,1-8"
PATINVR5   IFILE SQL, FIXED=370, KEY="U31-38,23-30,1-8,9-16"
PATINVR6   IFILE SQL, FIXED=370, KEY="U262-267,31-38,23-30,1-8,9-16"
.
. NAME     TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION
. ----     ----   ------   --------  ----------   -----------
PINVNO     DIM       8           8        1       INVOICE NUMBER
PINVDTE    DIM       8           8        9       INVOICE DATE  CCYYMMDD
PINALPHA   DIM       6           6       17       ALPHA SORT = PSNAME
DPINVADM   DIM       8           8       23       ADMISSION NUMBER
PINVUR     DIM       8           8       31       U.R. NUMBER
DPINVSYS   DIM       1           1       39       SYSTEM FOR INVOICE
.                                                 1 = A & E
.                                                 2 = OUT PATIENTS
.                                                 3 = IN PATIENTS
.                                                 4 = OTHER FINANCIAL
.                                                 5 = DAY CENTRE     
PINVSITE   DIM       6           6       40       OUTPATIENT SITE
PINVCLM    DIM       3           3       46       CLAIM TYPE
PINVLOCK   DIM       2           2       49       INVOICE LOCK INDICATOR
PINVAMT    FORM   12.2           8       51       INVOICE TOTAL 
PINVPATA   FORM   12.2           8       59       AMOUNT PAID BY PATIENT
PINVHFDA   FORM   12.2           8       67       AMOUNT PAID BY HEALTH FUND
PINVOTHA   FORM   12.2           8       75       OTHER AMOUNT PAID   
PINVJOUR   FORM   12.2           8       83       JOURNAL AMOUNT
PINVREB    FORM   12.2           8       91       FUND REBATE AMAOUNT
PINVCANC   DIM       8           8       99       DATE INVOICE CREDITED-CCYYMMDD
PINVPCSH   FORM   12.2           8      107       INVOICE CASH PENDING
PINVTYP    FORM      1           2      115       INVOICE TYPE
.                                                 0 = Normal Invoice
.                                                 1 = Insurance Inv of two invs
.                                                 2 = Pat Invoice of two invs
PINVBLCD   DIM       8           8      117       DATE INVOICE BALANCED CCYYMMDD
PINVCADM   DIM       8           8      125       ADM NO FOR CREDITED INVOICES
DPTINCMX   DIM       1           1      133       CASEMIX INVOICE ?
.                                                 1 = Yes, 2 = No (normal pats)
DPTINEDS   DIM       1           1      134       EDI sent flag
.                                                   0 - Not sent
.                                                   1 - Sent
.                                                   2 - Rejected claim
.                                                   3 - On hold
PTINGSTA   FORM   12.2           8      135       Total GST Amount
PTINGSTJ   FORM   12.2           8      143       GST Journal amount
PTINCMCD   DIM       9           9      151       Casemix Code (from pmscmtaf)
PTINPMD1   DIM      30          30      160       Patient Moeity description 1
PTINPMD2   DIM      35          35      190       Patient Moeity description 2
PTINPMAM   FORM   12.2           8      225       Patient Moeity amount
PTINCRST   DIM       1           1      233       Credit Note Status
.                                                   0 - No Credit Note
.                                                   1 - Fully Credited
.                                                   2 - Credit by Item
PTINCRTT   FORM   12.2           8      234       Credit Note total (inc.GST)
PTINMVDY   DIM       4           4      242       Mechanical Ventilation Days
PTINDISC   FORM   12.2           8      246       Discount Amount
PTINROUN   FORM   12.2           8      254       Rounding Amount
PTINHFND   DIM       6           6      262       Contract/Health Fund
PTINCDAT   DIM       8           8      268       Date Record Created (ccyymmdd)
PTINCTIM   DIM       8           8      276       Time Record Created (hh:mm:ss)
PTINCUID   DIM      10          10      284       WEB User ID Created (websecaf)
PTINUDAT   DIM       8           8      294       Date Record Updated (ccyymmdd)
PTINUTIM   DIM       8           8      302       Time Record Updated (hh:mm:ss)
PTINUUID   DIM      10          10      310       WEB User ID Updated (websecaf)
PINVSPAR   DIM      50          50      320       SPARE CHARACTERS
.End of Record                          370
.
.    SPARE VARIABLES
.
SPINVNO     DIM          8            1       INVOICE NUMBER
SPINVDTE    DIM          8            9       INVOICE DATE  CCYYMMDD
SPINALPH    DIM          6           15       ALPHA SORT = PSNAME
SPINVADM    FORM         6           23       ADMISSION NUMBER
SPINVUR     DIM          8           31       U.R. NUMBER
SPINVAMT    FORM      12.2           37       INVOICE TOTAL
SPINVPAT    FORM      12.2           48       AMOUNT PAID BY PATIENT
SPINVHFD    FORM      12.2           58       AMOUNT PAID BY HEALTH FUND
SPINVOTH    FORM      12.2           68       OTHER AMOUNT PAID
SPINVJOU    FORM      12.2           78       JOURNAL AMOUNT
SPINVCLM    DIM          3           90       CLAIM TYPE
SPINVREB    FORM      12.2           93       FUND REBATE AMAOUNT
SPINVCAN    DIM          8          103       CANCELATION DATE (CCYYMMDD)
SPINVSYS    FORM         1          109       SYSTEM FOR INVOICE
SPINVPCS    FORM      12.2          110       INVOICE CASH PENDING
SPINVTYP    FORM         1          120       INVOICE TYPE
SPINVLOC    DIM          2          121       INVOICE LOCKING INDICATOR
SPINVBLC    DIM          8           92       DATE INVOICE BALANCED
SPINVSIT    DIM          6          100       OUTPATIENT SITE
SPINVCAD    DIM          8          105       ADM NO FOR CANCELLED INVOICES
SPINVSPA    DIM          7          113       SPARE 7 CHARACTERS
.
.  redefine form fields from key
.  -----------------------------
. NAME     TYPE      LENGTH     START LOC.   DESCRIPTION
. ----     ----      ------     ----------   -----------
PINVADM    FORM         8           21       ADMISSION NUMBER
PINVSYS    FORM         1           34       SYSTEM FOR INVOICE
PTINCMIX   FORM         1                    CASEMIX INVOICE ?
PTINEDSF   FORM         1                    EDI sent flag
.
FINDFILE  FILE      ASCII, FIXED=256
.
. Enter the first or the shortest index from the original FD and rename
. the index OLDFILE
.   
OLDFILE   IFILE     SQL, FIXED=312, KEY="U1-8"
.
CMDLINE   DIM       100
DIM60     DIM       60
.
RECNUM    FORM      8
NEWFILE   DIM       60
NEWPATH   DIM       60
OLDPATH   DIM       60
DEFPATH   DIM       60
SAVEFLG   FORM      1
SP50      INIT    "                                                  "
SP60      INIT    "                                                            "
.
PRGNAM    INIT      "CONVERSION PATINVFD"
PRGID     INIT      "F90PTINV"
VERSION   INIT      "V12.00.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN1000,MAIN0000,MAIN9999 * Yes, no, cancel
.
MAIN1000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
. Loop thru old file and write records to new file
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/compress saved file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          
INIT9999  RETURN
.
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
PREP0000 
. open old file
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE,"patinvrf"
          TRAPCLR   IO
          BRANCH    OVRCD,PREP5000       * Original file does not exist
          CLOSE     OLDFILE
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
. Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat patinvrf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sptinvrf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat patinvrf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sptinvrf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
. remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "patinvrf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "sptinvrf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "patinvrf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.
          APPEND    " 370 UC1-8",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has
.  to be repeated with for each individual index
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " U9-16,1-8",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " U23-30,1-8",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " U39-39,40-45,46-48,17-22,23-30,1-8",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " U31-38,23-30,1-8,9-16",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " U262-267,31-38,23-30,1-8,9-16",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
          OPEN      PATINVR1,NEWFILE
.
. set the flag to say we want to continue
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
PREP9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and WRFUND1 each record               *
.**********************************************************************
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          PACK      KEY8,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
          PACK      PTINCDAT,SP70       * Date Record Created (ccyymmdd)
          PACK      PTINCTIM,SP70       * Time Record Created (hh:mm:ss)
          PACK      PTINCUID,SP70       * WEB User ID Created (websecaf)
          PACK      PTINUDAT,SP70       * Date Record Updated (ccyymmdd)
          PACK      PTINUTIM,SP70       * Time Record Updated (hh:mm:ss)
          PACK      PTINUUID,SP70       * WEB User ID Updated (websecaf)
          PACK      PINVSPAR,SP70       * SPARE CHARACTERS
.
          PACK      KEY8,PINVNO,SP70
          CALL      WRPTINV1                      * write to new file
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     PATINVR1                      * close new file
          CLOSE     OLDFILE                       * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
READ9999  RETURN
.
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
VALD0000  MOVE      ZERO,EXIT
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or compress save file                                *
.**********************************************************************
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "sptinvrf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "compress -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not compressed or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.         Get the default directory
.**********************************************************************
DEFT0000  EXECUTE   "ldat patinvrf.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding patinvrf.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
DEFT9999  RETURN
+
.**********************************************************************
. ---- OLD IO ROUTINES ----
.**********************************************************************
.  Copy and paste the Positional Read routine from the original
.  IO file, rename the routine READSOLD and change the index name
.  to OLDFILE
.
READSOLD  RESET     KEY8
          READ      OLDFILE,KEY8;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE;PINVNO,PINVDTE,PINALPHA,DPINVADM,PINVUR:
                                 DPINVSYS,PINVSITE,PINVCLM,PINVLOCK:
                                 PINVAMT,PINVPATA,PINVHFDA,PINVOTHA:
                                 PINVJOUR,PINVREB,PINVCANC,PINVPCSH:
                                 PINVTYP,PINVBLCD,PINVCADM,DPTINCMX:
                                 DPTINEDS,PTINGSTA,PTINGSTJ,PTINCMCD:
                                 PTINPMD1,PTINPMD2,PTINPMAM:
                                 PTINCRST,PTINCRTT,PTINMVDY:
                                 PTINDISC,PTINROUN,PTINHFND,PINVSPAR
          GOTO      OVERCOND IF OVER
          MOVE      DPINVSYS,PINVSYS
          MOVE      DPINVADM,PINVADM
          MOVE      PINVDTE,SPINVDTE
          MOVE      PINVADM,SPINVADM
          MOVE      PINVCLM,SPINVCLM
          MOVE      PINALPHA,SPINALPH
          MOVE      DPTINCMX,PTINCMIX
          MOVE      DPTINEDS,PTINEDSF
          RETURN
.
. New IO write routine
.
WRPTINV1  RESET     KEY8
          MOVE      PINVADM,DPINVADM
          MOVE      PINVSYS,DPINVSYS
          MOVE      PTINCMIX,DPTINCMX
          MOVE      PTINEDSF,DPTINEDS
          WRITE     PATINVR1,KEY8;KEY8,PINVDTE,PINALPHA:
                                  DPINVADM,PINVUR:
                                  DPINVSYS,PINVSITE,PINVCLM,PINVLOCK:
                                  PINVAMT,PINVPATA,PINVHFDA,PINVOTHA:
                                  PINVJOUR,PINVREB,PINVCANC,PINVPCSH:
                                  PINVTYP,PINVBLCD,PINVCADM,DPTINCMX:
                                  DPTINEDS,PTINGSTA,PTINGSTJ,PTINCMCD:
                                  PTINPMD1,PTINPMD2,PTINPMAM:
                                  PTINCRST,PTINCRTT,PTINMVDY:
                                  PTINDISC,PTINROUN,PTINHFND:
                                  PTINCDAT,PTINCTIM,PTINCUID:
                                  PTINUDAT,PTINUTIM,PTINUUID,PINVSPAR
.
          RETURN
.
          INC       STD001IO
