. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS65                                             *
. * Desc      :   HCS Cost Centre Conversion Maintenance               *
. **********************************************************************
. * Date      :   18.06.1991                                           *
. * Author    :   Neeriem Dye (IBA)                                    *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
. * VF.00.01  23.12.1999 Sandra Barcham                                *
. *           Recompiled for FMSCCAFD                                  *
. *           srf 646145                                               *
. **********************************************************************
.$$$$$$
.        PROGRAM IBAFMS65
.             open files and initialise variables
.             WHILE user wants to execute program DO
.                  user inputs key
.                  CASE user option OF
.                       INSERT : key in initial fields
.                                modify as desired
.                                save data if desired
.                       MODIFY : display existing data
.                                modify as desired
.                                save data if desired
.                       DELETE : display existing data
.                                delete data if desired
.                  END
.             END
.        END
.$$$$$$
. Standard FMS Accounting System Include
.---------------------------------------
          INC       FMSSTDDF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
.
          INC       FMSCCAFD/INC       * CC
          INC       FMSCONFD/INC       * Controlf
          INC       FMSHCCFD/INC       * HCA CC
          INC       FMSHCLFD/INC       * Institution
          INC       FMSLMAFD/INC       * Ledger
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
.
BUILDHCC  INIT      "isbuild fmshccaf 43 U1-3,4-8"
.
ACCEPT    FORM      1        * accept mode (1=on)
OPTIONA   FORM      2        * screen A selection keyin var
SCRNO     FORM      2        * screen number
SAVCOST   DIM       12       * ledger
SAVCOSTD  DIM       35       * ledger description
.
PRGID     INIT      "IBAFMS65"
PRGNAM    INIT      "HCS Cost Centre Conversion Maintenance"
VERSION   INIT      "V12.00.00"
.
.******************************************************************************
.   MAINLINE - Controlling Logic
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML0100    CALL      SELOPTN            * select option
          COMPARE   ZERO,COPTION
          GOTO      ML9999 IF EQUAL    * exit ?
.
ML0200    CALL      INST0000           * get inst
          BRANCH    EXIT,ML0100        * exit option
.
ML0300    CALL      PROC0000           * get key
          BRANCH    EXIT,ML0200        * exit option
          BRANCH    COPTION,ML1000,ML1000,ML2000
.
.---- add/modify ----
.
ML1000    CALL      SELA0000           * perform screen A
          BRANCH    EXIT,ML0300        * exit option
.
          CALL      POST0000           * save data
          GOTO      ML0300             * get next selection
.
.---- delete ----
.
ML2000    CALL      SCRA0000           * display screen A
          CALL      DELQST             * ok to delete (y/n/c) ?
          SUB       ONE,CEXIT
          BRANCH    CEXIT,ML0300,ML0100 * exit option
.
          CALL      DELT0000           * delete data
          GOTO      ML0300             * get next selection
.
ML9999    CHAIN     PGM                * chain back to menu
.
.******************************************************************************
.  INIT - Open Files                             Called by ML
.******************************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CCENTRY
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
.
          DISPLAY   *P56:24,"Opening controlf";  * Controlf
          OPEN      CONTROLF,"controlf"
          CALL      RDFMCO50
          CALL      RDFMCO51
          CALL      RDFMCO52
.
          DISPLAY   *P64:24,"fmsccaaf";  * CC
          OPEN      FMSCCAA1,"fmsccaaf"
          OPEN      FMSCCAA2,"fmsccaaf"
.
          DISPLAY   *P64:24,"fmslmaaf";  * Ledger
          OPEN      FMSLMAA1,"fmslmaaf"
.
          DISPLAY   *P64:24,"fmshclaf";  * Inst
          OPEN      FMSHCLA1,"fmshclaf"
.
          DISPLAY   *P64:24,"fmshccaf";  * HCA CC
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO               * look for file opening error
          OPEN      FMSHCCA1,"fmshccaf"          * open file (if possible)
          TRAPCLR   IO
          COMPARE   ONE,OVRCD                    * any problem with file ?
          GOTO      INIT9999 IF NOT EQUAL
          EXECUTE   BUILDHCC,TASKID
          OPEN      FMSHCCA1,"fmshccaf"          * open file (if possible)
.
INIT9999  DISPLAY   *P1:24,*EF;
          RETURN
.******************************************************************************
.  INST - Get Inst                               Called by ML
.         Returns : EXIT     (1=exit)
.******************************************************************************
INST0000  UNPACK    SP70,FMHLINST,FMHLDESC
          UNPACK    SP70,SAVCOST,SAVCOSTD
          UNPACK    SP70,FMHALEDG,FMHACOCE
          CALL      DHED0000                * display screen header
          MOVE      "28",CCOL               * set up keyin address
          MOVE      "4",CVERT
          MOVE      "0",SCRNO
.
INST0100  CALL      KHCLFM00                * get institution
          BRANCH    EXIT,INST9500,INST9500  * entry ok ? (1=blank, 2=EXITCHAR)
          DISPLAY   *PCCOL:CVERT,*V2LON,FMHLINST,*HOFF,SP10,SP1,FMHLDESC;
.
INST9000  MOVE      ZERO,EXIT               * continue
          GOTO      INST9999
.
INST9500  MOVE      ONE,EXIT                * quit
.
INST9999  RETURN
.******************************************************************************
.  PROC - Get Key                                Called by ML
.         Returns : EXIT     (1=exit)
.******************************************************************************
PROC0000  UNPACK    SP70,SAVCOST,SAVCOSTD
          UNPACK    SP70,FMHALEDG,FMHACOCE
          CALL      DHED0000                * display screen header
          MOVE      "28",CCOL               * set up keyin address
          MOVE      "5",CVERT
          MOVE      "0",SCRNO
.
PROC0100  COMPARE   ONE,COPTION             * insert ?
          CALL      AHCCFM00 IF EQUAL
          COMPARE   ONE,COPTION             * modify/delete ?
          CALL      KHCCFM00 IF NOT EQUAL
.
          BRANCH    EXIT,PROC9500,PROC9500  * entry ok ? (1=blank, 2=EXITCHAR)
.
          PACK      SAVCOST,FMHAHCCC,SP70
          LOAD      SAVCOSTD,COPTION,SP70,SP70,SP70
          DISPLAY   *PCCOL:CVERT,*V2LON,SAVCOST,*HOFF,SP2,SAVCOSTD;
          LOAD      ACCEPT,COPTION,ONE,ZERO,ZERO
.
PROC9000  MOVE      ZERO,EXIT               * continue
          GOTO      PROC9999
.
PROC9500  MOVE      ONE,EXIT           * quit
.
PROC9999  RETURN
.******************************************************************************
.  DHED - Display Screen header                  Called by PROC, redisps
.******************************************************************************
DHED0000  DISPLAY   *P1:3,*EL:
                    *P1:4,"   Institution Code      : ":
                          *V2LON,FMHLINST,*HOFF,SP10,SP1,FMHLDESC,*EL:
                    *P1:5,"   HCS Cost Centre       : ":
                          *V2LON,SAVCOST,*EF;
.
DHED9999  RETURN
.******************************************************************************
.  SCRA - Display Screen A                       Called by SELA, redisps
.******************************************************************************
SCRA0000  MOVE      SP70,FMLADESC
          PACK      KEY2,FMHALEDG,SP70
          CALL      RDFMLA1
          MOVE      SP70,FMCCDESC
          PACK      KEY14,FMHALEDG,FMHACOCE,SP70
          CALL      RDFMCC1
.
          DISPLAY   *P1:6,*EF,*V2LON:
                    *P1:7," 1",*HOFF,". Ledger               : ":
                          *V2LON,FMHALEDG,*HOFF,SP10,SP2,FMLADESC,*V2LON:
                    *P1:8," 2",*HOFF,". Cost Centre          : ":
                          *V2LON,FMHACOCE,*HOFF,SP2,FMCCDESC;
.
SCRA9999  RETURN
.******************************************************************************
.  SELA - Perform Screen A                       Called by ML
.         Returns : EXIT     (1=exit)
.******************************************************************************
SELA0000  UNPACK    SP70,ANS
          MOVE      ZERO,OPTIONA       * reset item desired
          CALL      SCRA0000           * display screen A
          MOVE      "1",SCRNO
.
SELA0100  ADD       ONE,OPTIONA        * get next item
          BRANCH    ACCEPT,SELA0110    * in accept mode ?
          CALL      MAINQST            * get option if not in accept mode
          MOVE      CCITEM,OPTIONA
.
SELA0110  MOVE      "28",CCOL          * get screen address
          MOVE      "6",CVERT
          ADD       OPTIONA,CVERT
          MOVE      OPTIONA,F2         * adjust selection for quit/continue
          ADD       TWO,F2
          BRANCH    F2,SELA9500,SELA9000:                       * quit, continue
                       SELA1100,SELA1200                        * execute option
          BRANCH    ACCEPT,SELA0500    * at end of accept mode ?
          BEEP                         * illegal option
          GOTO      SELA0100           * get another option
.
SELA0500  MOVE      ZERO,ACCEPT        * clear accept mode
          GOTO      SELA0100
.
.---- option 1 ----
.
SELA1100  CALL      KLMAFM00                * get data
          BRANCH    EXIT,SELA1190,SELA9500  * entry ok ? (1=blank, 2=EXITCHAR)
          DISPLAY   *PCCOL:CVERT,*V2LON,FMLALEDG,*HOFF,SP10,SP2,FMLADESC;
          PACK      FMHALEDG,FMLALEDG,SP70
          MOVE      ONE,ACCEPT              * go into accept mode
          GOTO      SELA0100
.
SELA1190  BRANCH    ACCEPT,SELA9500
          GOTO      SELA1100
.
.---- option 2 ----
.
SELA1200  CALL      KCCAFM00                * get data
          BRANCH    EXIT,SELA1200,SELA9500  * entry ok ? (1=blank, 2=EXITCHAR)
          DISPLAY   *PCCOL:CVERT,*V2LON,FMCCCOCE,*HOFF,SP2,FMCCDESC;
          PACK      FMHACOCE,FMCCCOCE,SP70
          GOTO      SELA0100
.
.
SELA9000  MOVE      ZERO,EXIT          * continue
          GOTO      SELA9999
.
SELA9500  MOVE      ONE,EXIT           * quit
.
SELA9999  RETURN
.**********************************************************************
.  POST - Post Data                                    Called By ML
.**********************************************************************
POST0000  PACK      KEY8,FMHLINST,SAVCOST,SP70
          UNPACK    KEY8,FMHAINST,FMHAHCCC
          CALL      DEFMHA1
          CALL      WRFMHA1
.
POST9999  RETURN
.**********************************************************************
.  DELT - Delete Data                                  Called By ML
.**********************************************************************
DELT0000  PACK      KEY8,FMHLINST,SAVCOST,SP70
          UNPACK    KEY8,FMHAINST,FMHAHCCC
          CALL      DEFMHA1
.
DELT9999  RETURN
.******************************************************************************
.  Redisplays
.******************************************************************************
RHCCFM00  
RHCLFM00  
RLMAFM00  
RCCAFM00  
          BRANCH    SCRNO,RDISA000
          CALL      DHED0000
          GOTO      RZZZFM99
.
RDISA000  CALL      DHED0000
          CALL      SCRA0000
          GOTO      RZZZFM99
.
RZZZFM99  RETURN
.******************************************************************************
.  INCLUDES FOR I/O'S
.******************************************************************************
.
          INC       FMSSTDCD           * FMS Acc. standard routines
.
          INC       FMSCCAIO/INC       * CC
          INC       FMSCONIO/INC       * Controlf
          INC       FMSHCCIO/INC       * HCA CC
          INC       FMSHCLIO/INC       * Institution
          INC       FMSLMAIO/INC       * Ledger
.
          INC       FMSCCAKY           * CC
          INC       FMSHCCKY           * HCA CC
          INC       FMSHCLKY           * Institution
          INC       FMSLMAKY           * Ledger
