.******************************************************************************
.* System        :  Operating Rooms                                           *
.*                                                                            *
.* Include Name  :  OPRDEBDS.PBL                                              *
.*                                                                            *
.* Function      :  Display the operating teams & surgeon for an operation    *
.*                  from the operating team file -> OPRDEBFD.INC              *
.*                                                                            *
.* Author        :  Justin Coates 12/11/1990                                  *
.*                                                                            *
.* Index used    :  OPRDETB1,"oprdetbf"     -> OPRDEBFD.INC                   *
.*               :  PATDO1A1,"patdo1af"     -> PATDO1FD.INC                   *
.*                                                                            *
.* Parameters    :  OPDAUNIQ  the unique identifier                           *
.*                                                                            *
.* Modifications :                                                            *
.******************************************************************************
.
OPRDEBDS  MOVE      FIVE,CVERT              * line counter
          PACK      KEY11,OPDAUNIQ,SP1      * key of blanks
          CALL      RSOPDEB1                * positional read
.
          DISPLAY   *P1:3,*EF,*V2LON,*ULON,*P32:4,"OPERATING TEAMS":
                    *P1:5,*ULON,"Team",*P8:5,"     Operating Surgeon        "
.
. -----------------------------------------------
. ------- next read on operating team file ------
. -----------------------------------------------
.
DEBDS100  CALL      RKOPDEB1  
          BRANCH    OVRCD,DEBDS995          * end of file
.
          MATCH     OPDBUNIQ,OPDAUNIQ
          GOTO      DEBDS995 IF NOT EQUAL   * different operation
.
. ----------------------------------
. ------- increment counters -------
. ----------------------------------
.
DEBDS200  ADD       ONE,CVERT               * inc line counter
.
          COMPARE   "23",CVERT
          GOTO      DEBDS400 IF NOT LESS    * need a new page
.
. -------------------------------------------
. ------- display all the information -------
. -------------------------------------------
.
DEBDS300  MOVE      "No Operating Surgeon",PACFNAME
          MOVE      ZERO,FORM1              * initialize counter
.
DEBDS310  ADD       ONE,FORM1               * get next doctor
          COMPARE   NINE,FORM1
          GOTO      DEBDS390 IF EQUAL       * no operating surgeon
.
          LOAD      KEY3,FORM1,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                               OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
.
          MATCH     ANSS,KEY3
          GOTO      DEBDS310 IF NOT EQUAL   * not a surgeon
.
          LOAD      KEY6,FORM1,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                               OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
.
          CALL      RDDOCT1
          BRANCH    OVRCD,DEBDS310          * invalid doctor code
.
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME                 * set up the doctors name
.
DEBDS390  DISPLAY   *P2:CVERT,*V2LON,OPDBTEAM,*HOFF,*P8:CVERT,PACFNAME
          GOTO      DEBDS100
.
. -------------------------------
. ------- new page needed -------
. -------------------------------
.
DEBDS400  DISPLAY   *P1:24,"( )ontinue, ( )irst, ( )ext ? ",*EL:
                    *V2LON,*P2:24,ANSC,*P14:24,ANSF,*P23:24,ANSN
.
          KEYIN     *P31:24,*DV,UNDLN1:
                    *P31:24,*V2LON,ANS
          REP       UPPLOW,ANS
.
          MATCH     ANSC,ANS
          GOTO      DEBDS995 IF EQUAL       * Continue selected
.
          MATCH     ANSF,ANS
          GOTO      OPRDEBDS IF EQUAL       * First selected
.
. --------------------------------------
. ------- (N)ext screen selected -------
. --------------------------------------
.
DEBDS700  MOVE      SIX,CVERT               * first display row 
          DISPLAY   *P1:CVERT,*EF;          * clear the screen
          GOTO      DEBDS390                * display the data
.
DEBDS995  MOVE      OPDAUNIQ,OPDBUNIQ       * get back unique identifier
.
DEBDS999  RETURN
+
