.*****************************************************************************
.*    Program      : F01PTTFE.PBL                                            *
.*    Description  : Conversion pattfeef to new File layout                  *
.*                                                                           *
.*    Author       : Peter Vela                                              *
.*    Date         : 14/12/2010                                              *
.*    Modifications:                                                         *
.*        V10.02.01 Peter Vela       CAR 233048                              *
.*                  Added Duplicate Record header display line.              * 
.*        V10.01.04 J.Tan            CAR 233048                              *
.*                  Added option 3 for oracle                                *
.*        V10.01.03 J.Tan            CAR 233048                              *
.*                  Mods to copy Theatre fee file to v10tfeef                *
.*        V10.01.02 Peter Vela       CAR 233048                              *
.*                  Fixed REPTLNNO count                                     *
.*        V10.01.01 Peter Vela       CAR 233048                              *
.*                  Removed save old file option from load contract ids      *
.*        V10.01.00 Peter Vela       CAR 233048                              *
.*                  Created program                                          *
.*****************************************************************************
.
.  Global change F10PTELG.PBL  eg F57PTDOC
.  Global change PATELGFD  eg PATDOCFD (new layout)
.  Global change patelgaf  eg patdoctf
.  Global change PATELGA1  eg PATDOCT1
.  Global change KEY8     eg KEY8
.  Global change WRPTELG1  eg WRDOCT1
.  Global change sptelgaf  eg sptdoctf
.
.  vi Global change command  :%s/FromString/ToString/g
.
.
          INC       STD001FD
.
          INC       PATFN1FD/INC
          INC       PATMTFFD/INC
          INC       PATCONFD/INC
.
FINDFILE  FILE      ASCII, FIXED=256
.
. Enter the first or the shortest index from the original FD and rename
. the index OLDPTTFE
.
.                                     ******** copy the old FD here *******
.                                     * comment out all continuing fields and
.                                     * prefix changing fields with "OLD" -
.                                     * remove old Indexes and Redefines     
.   
OLDPTTFE   IFILE    SQL, FIXED=133, KEY="U1-3,4-9,10-17,18-18,19-23" 
.
. NAME     TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION
. ----     ----   ------   --------  ----------   -----------
.TFCLM      DIM        3          3          1     CLM CODE (CODES FILE CAT. CL)
.TFHFUND    DIM        6          6          4     H.F. (PATFUNDH FILE)
TFHFTAB    DIM        8          8         10     H.F. TABLE (PATFUNDH)
.TFDAYC     DIM        1          1         18     DAY CASE RATE INDICATOR
.                                                 "D" = Day Case rate
.                                                 " " = Not a Day Case rate
.TFENDMBS   DIM        5          5         19     LAST MBS FEE IN RANGE or
.                                                 THEATRE CLASSIFICATION
.TFHF1      FORM      12.2        8         24    REBATE PORT IF 1st HIGHEST FEE
.TFPAT1     FORM      12.2        8         32     PATIENT PORTION
.TFHF2      FORM      12.2        8         40    REBATE PORT IF 2nd HIGHEST FEE
.TFPAT2     FORM      12.2        8         48     PATIENT PORTION
.TFHF3      FORM      12.2        8         56    REBATE PORT IF 3rd HIGHEST FEE
.TFPAT3     FORM      12.2        8         64     PATIENT PORTION
.TFHF4      FORM      12.2        8         72    REBATE PORT IF 4th HIGHEST FEE
.TFPAT4     FORM      12.2        8         80     PATIENT PORTION
.TFHF5      FORM      12.2        8         88    REBATE PORT IF 5th HIGHEST FEE
.TFPAT5     FORM      12.2        8         96     PATIENT PORTION
.TFHF6      FORM      12.2        8        104    REBATE PORT IF 6th HIGHEST FEE
.TFPAT6     FORM      12.2        8        112     PATIENT PORTION
.TFNINV     FORM       1          2        120     NUMBER OF INVS (1 OR 2 ONLY)
.TFSPARE    DIM       11         11        122     SPARE VARIABLES
.
.End of Record                            133
.
.  redefine form fields from key
.  -----------------------------
.
.  N/A
.
.                                     ******** copy the new FD here *******
.
PATTFEE1   IFILE   SQL, FIXED=133, KEY="U1-3,4-9,10-12,13-13,14-18,117-122"
.
. NAME     TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION
. ----     ----   ------   --------  ----------   -----------
TFCLM      DIM        3          3          1     CLM CODE (CODES FILE CAT. CL)
TFHFUND    DIM        6          6          4     H.F. (PATFUNDH FILE)
PTTFHTYP   DIM        3          3         10     Health Fund Table Type(Cat.HT)
TFDAYC     DIM        1          1         13     DAY CASE RATE INDICATOR
.                                                 "D" = Day Case rate
.                                                 " " = Not a Day Case rate
TFENDMBS   DIM        5          5         14     LAST MBS FEE IN RANGE or
.                                                 THEATRE CLASSIFICATION
TFHF1      FORM      12.2        8         19     REBATE PORT IF 1st HIGHEST FEE
TFPAT1     FORM      12.2        8         27     PATIENT PORTION
TFHF2      FORM      12.2        8         35     REBATE PORT IF 2nd HIGHEST FEE
TFPAT2     FORM      12.2        8         43     PATIENT PORTION
TFHF3      FORM      12.2        8         51     REBATE PORT IF 3rd HIGHEST FEE
TFPAT3     FORM      12.2        8         59     PATIENT PORTION
TFHF4      FORM      12.2        8         67     REBATE PORT IF 4th HIGHEST FEE
TFPAT4     FORM      12.2        8         75     PATIENT PORTION
TFHF5      FORM      12.2        8         83     REBATE PORT IF 5th HIGHEST FEE
TFPAT5     FORM      12.2        8         91     PATIENT PORTION
TFHF6      FORM      12.2        8         99     REBATE PORT IF 6th HIGHEST FEE
TFPAT6     FORM      12.2        8        107     PATIENT PORTION
TFNINV     FORM       1          2        115     NUMBER OF INVS (1 OR 2 ONLY)
PTTFCNID   DIM        6          6        117     Contract ID
TFSPARE    DIM       10         10        123     SPARE VARIABLES
.
.End of Record                            133
.
TEMPFILE   IFILE    SQL, FIXED=66, KEY="U1-3,4-9"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
TMPTCLAM  DIM       3      3        1     Claim code
TMPTHFND  DIM       6      6        4     Health Fund
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       100
.
DEFPATH   DIM       60
DIM60     DIM       60
.
FNAME1    INIT      "f01ptf"
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
REPTLNNO  FORM      2
.
TMPFILNM  DIM       8
. 
. PROGRAM CONSTANTS
. -----------------
SP60      INIT    "                                                            "
.
.
.
PRGNAM    INIT      "CONVERSION PATTFEFD"
PRGID     INIT      "F01PTTFE"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                         MAIN0000                                          *
.*                      Mainline code                                        *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      CHKMF000                * Check Mapping File parameter
          BRANCH    EXIT,MAIN9999
.
          CALL      OPTN0000                * Choose main option
          BRANCH    EXIT,MAIN9999           * EXIT
          BRANCH    OPTION,MAIN0500:
                           MAIN4000:
                           MAIN4000
.
MAIN0500  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN1000,MAIN0500,MAIN9999 * Yes, no, cancel
.
MAIN1000  CALL      CPFL0000                      * Copy pattfeef to v10tfeef
          BRANCH    EXIT,MAIN9999
.
          CALL      CTFE0000                      * Create new Theatre fees file
          GOTO      MAIN9999
.
MAIN4000  OPEN      PATMTFA1,"patmtfaf"
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000,MAIN4000,MAIN9999 * Yes, no, cancel
.
MAIN5000  CALL      CTMP0000           * create tempfile
          CALL      CLER0000                * empty it if Oracle
          CALL      READ0000           * read old records and write
          BRANCH    EXIT,MAIN9999
.
          CALL      REAC0000           * read new records and update contract id
          CALL      PRLR0000           * print error file
          CALL      ENDP0000           * end of processing
          CALL      KILL0000
.
          GOTO      MAIN9999
.
MAIN9999  CHAIN     PGM
+
.*****************************************************************************
.*                          INIT0000               Called by: MAIN0000       *
.*  Display heading and check that the files needed exist & open them        *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
.
          OPEN      PATFN1A1,"patfn1af"           * Open Health Fund File
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND18;*116,PTCNMFFL
.
INIT9999  RETURN
+
.*****************************************************************************
.*                          KDIR0000               Called by: MAIN0000       *
.*                       Keyin directories                                   *
.*****************************************************************************
KDIR0000  EXECUTE   "ldat pattfeef.dat > temp.txt",TASKID
          CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
.
KDIR1000  MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
.
          KEYIN     *P1:12,*EL,"Keyin path for saving v10tfeef file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
          ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.*****************************************************************************
.*                          CPFL0000               Called by: MAIN0000       *
.*                       Copy pattfeef to v10tfeef                           *
.*****************************************************************************
CPFL0000  CALL      CHEK0000             * old file found ok ?
          BRANCH    EXIT,CPFL9000        * no - finish
.
          CLOSE     OLDPTTFE
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat pattfeef.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "v10tfeef.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      CPFL8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat pattfeef.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "v10tfeef.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      CPFL8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "pattfeef",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      CPFL9000
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      CPFL9999
.
CPFL8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
CPFL9000  MOVE      ONE,EXIT
.
CPFL9999  RETURN
+
.*****************************************************************************
.         Create the new file
.*****************************************************************************
CTFE0000
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "pattfeef",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.      eg    APPEND    " 195 UC9-16",CMDLINE
.
          APPEND    " 133 UC1-3,4-9,10-12,13-13,14-18,117-122",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has 
.  to be repeated with for each individual index
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC117-122,1-3,4-9,10-12,13-13,14-18",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      PATTFEE1,NEWFILE
.
CTFE9999  RETURN
+
.*****************************************************************************
.         Opening v10tfeef file after being copied
.*****************************************************************************
OTFE0000  CLEAR     DIM60
          APPEND    "v10tfeef",DIM60
          APPEND    SP70,DIM60
          RESET     DIM60
.
          COMPARE   THREE,OPTION
          GOTO      OTFE2000 IF EQUAL       * Oracle
.
          EXECUTE   "ldat v10tfeef.dat > temp.txt",TASKID
          CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,OTFE9000
          MOVE      DEFPATH,OLDPATH
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "v10tfeef",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
OTFE2000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDPTTFE,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open v10tfeef file.  ";
            CALL      HITENTER
            GOTO      OTFE9999
          ENDIF
          GOTO      OTFE9999
.
OTFE9000  MOVE      ONE,OVRCD
OTFE9999  RETURN
.
.*****************************************************************************
.*                               READ0000          Called by: MAIN0000       *
.*             Loop thru old file (v10tfeef) and re-format to new file       *
.*****************************************************************************
READ0000  DISPLAY   *P1:24,*EL,"Opening V10tfeef";
.
          CALL      OTFE0000                      * Open v10tfeef file
          BRANCH    OVRCD,READ9000
.
          DISPLAY   *P1:24,*EL;
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATTFEE1,"pattfeef"
          TRAPCLR   IO
          BRANCH    OVRCD,READ8000       * new file does not exist
.
          MOVE      ZERO,CLNO
          CALL      PRHD0000
.
          DISPLAY   *P1:20,*EL,"Record No. : ";
          PACK      KEY23,SP70
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
.  All necessary changes to the files field lengths, date changes
.  or any other sort of modification must be made here
.
.    eg    PACK      DSPARE,SP50
.
          MOVE      SP70,PTTFHTYP
          MOVE      SP70,PTTFCNID
          MOVE      SP70,TFSPARE
          MOVE      SP70,KEY24
.
          PACK      KEY14,TFHFUND,TFHFTAB,SP70
          MATCH     SP70,KEY14
          GOTO      READ2000 IF EQUAL
.
          CALL      RDFUND1
          BRANCH    OVRCD,READ3500
.
          MOVE      PTHFBCAT,PTTFHTYP
.
READ2000  PACK      KEY24,TFCLM,TFHFUND,PTTFHTYP,TFDAYC,TFENDMBS,PTTFCNID,SP70
          CALL      RDATFEE1
          BRANCH    OVRCD,READ4000
.
READ3000  CALL      PRLN0000                   * Print duplicate record
          GOTO      READ1000
.
READ3500  CALL      PRLD0000                   * Print missing record
          GOTO      READ1000
.                                               * Pack key here
READ4000  CALL      WRTFEE1                     * write to new file
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     OLDPTTFE                       * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
          MOVE      ZERO,EXIT
          GOTO      READ9999
.
READ8000  DISPLAY   *P1:23,*EL,"New Theatre fees file does not exist"
          CALL      HITENTER
.
READ9000  DISPLAY   *P1:24,*EL,"Can not find v10tfeef ";
          CALL      HITENTER
          MOVE      ONE,EXIT
READ9999  RETURN
+
.**********************************************************************
.         Print duplicate record
.**********************************************************************
PRLN0000  COMPARE    "50",CLNO
          CALL       PRHD0000 IF NOT LESS
.
          PRINT      *1,TFCLM,*5,TFHFUND,"/",TFHFTAB,*20,PTTFHTYP,*20,TFDAYC:
                     *24,TFENDMBS,*30,TFHF1,*47,TFPAT1,*64,PTTFCNID
          ADD        ONE,CLNO
PRLN9999  RETURN
+
.**********************************************************************
.         Print missing record
.**********************************************************************
PRLD0000  COMPARE    "50",CLNO
          CALL       PRHD0000 IF NOT LESS
.
          PRINT      *1,"Health Fund/Table not found in patfn1af: ":
                     *50,TFHFUND,"/",TFHFTAB
          ADD        ONE,CLNO
PRLD9999  RETURN
+
.**********************************************************************
.         Print Heading
.**********************************************************************
PRHD0000  MOVE       ZERO,CLNO
.
          CALL       HEAD132
          PRINT      *1,"DUPLICATE RECORDS (SAME TABLE TYPE) DISPLAYED ON ":
                     "THIS REPORT WILL NOT BE WRITTEN TO THE THEATRE FEE FILE.":
                     *N
          PRINT      *1,"Clm",*5,"Hlth Fnd/Tab",*20,"Typ",*24,"Day":
                     *28,"MBS",*34,"Rebate",*51,"PatP",*68,"Contract"
          CALL       UND132
          ADD        THREE,CLNO
PRHD9999  RETURN
+
.*****************************************************************************
.*                               VALD0000          Called by: KDIR0000       *
.*                   Check if it a valid directory                           *
.*****************************************************************************
.
VALD0000  SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9100
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9100
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      VALD9999
.
VALD9100  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.*****************************************************************************
.*                               ENDP0000          Called by: MAIN0000       *
.*                    End of processing                                      *
.*****************************************************************************
ENDP0000  COMPARE   THREE,OPTION
          GOTO      ENDP8000 IF EQUAL    * Oracle
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "v10tfeef*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "compress -f ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          DISPLAY   *P1:24,*EL,"Compressing v10tfeef file.. ":
                    "Please wait !!  ";
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"v10tfeef file not compressed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
ENDP8000  DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.*****************************************************************************
.*                               DEFT0000          Called by: KDIR0000       *
.*                      Get the default directory                            *
.*****************************************************************************
.
DEFT0000  PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding pattfeef.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDPTTFE,"pattfeef"
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDPTTFE
          GOTO      CHEK9000
.
CHEK2000  DISPLAY   *P1:23,*EF,"File 'pattfeef' ";
          SCAN      "I * I",DIM60
          IF        @EQUAL
            DISPLAY   "does not exist - ";
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
            ELSE
              DISPLAY   "has caused an IO error - ";
            ENDIF
          ENDIF
          DISPLAY   "File will be bypassed":
                    *P1:24;
          CALL      HITENTER
.
          MOVE      ONE,EXIT
          GOTO      CHEK9999
.
CHEK9000  MOVE      ZERO,EXIT
.
CHEK9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: MAIN0000 *
.*                                Select Option                               *
.******************************************************************************
.
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          HLINE     *G37,2,57,80
          DISPLAY   *P40:4,*EF:
                    *P1:3,*V2LON,"0",*HOFF," = Exit":
                    *P1:4,*V2LON,"1",*HOFF:
                    " = Copy Current Theatre fees to v10tfeef (CISAM only)":
                    *P1:5,*V2LON,"2",*HOFF:
                    " = Load the New Theatre Fees file with Contract ID ":
                    "from Mapping file (CISAM)":
                    *P1:6,*V2LON,"3",*HOFF:
                    " = Load the New Theatre Fees file with Contract ID ":
                    "from Mapping file (ORACLE)":
                    *P1:8,"Select Option : ";
.
OPTN1000  KEYIN     *P17:8,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000:
                           OPTN9000:
                           OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               REAC0000          Called by: MAIN0000       *
.*             Loop thru new file and load Contract ID from Mapping file     *
.*****************************************************************************
REAC0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          PACK      KEY24,SP70
          CALL      RDSTFEE1                      * position at start
REAC1000  CALL      RDKTFEE1                      * read next record
          BRANCH    OVRCD,REAC6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
          PACK      KEY24,TFCLM,TFHFUND,PTTFHTYP,TFDAYC,TFENDMBS,PTTFCNID,SP70
.
          PACK      TFCLM,TFCLM,SP70
          PACK      TFHFUND,TFHFUND,SP70
.
          PACK      KEY9,TFCLM,TFHFUND,SP70
          CALL      RDPTMTF1
          BRANCH    OVRCD,REAC4000
.
          PACK      PTMTCNTR,PTMTCNTR,SP70
          MATCH     SP70,PTMTCNTR
          GOTO      REAC5000 IF NOT EQUAL
.
REAC4000  PACK      KEY9,TFCLM,TFHFUND,SP70
          CALL      RATEMP1
          IF        OVRCD=1
            MOVE      TFCLM,TMPTCLAM
            MOVE      TFHFUND,TMPTHFND
            CALL      WRTEMP1
          ENDIF
          GOTO      REAC1000
.
REAC5000  MOVE      PTMTCNTR,PTTFCNID
.
          CALL      UPTFEE1
          CALL      RDSTFEE1
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      REAC1000                      * get next record
.
REAC6000  CLOSE     PATTFEE1                      * close new file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
REAC9999  RETURN
+
.******************************************************************************
.*                             CTMP0000                 Called by:            *
.*                        create temporary file                               *
.******************************************************************************
CTMP0000  PACK      TMPFILNM,FNAME1,PORT
          REP       " 0",TMPFILNM
.
          CALL      KILL0000
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TMPFILNM,CMDLINE
          APPEND    " 66 U1-3,4-9",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,TMPFILNM
.
CTMP9999  RETURN
+
.******************************************************************************
.*                             KILL0000                  Called by:           *
.*                   erase temporary file                                     *
.******************************************************************************
KILL0000  CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TMPFILNM,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.******************************************************************************
.*                             CLER0000                 Called by:            *
.*               close and erase the temporary file                           *
.******************************************************************************
.
CLER0000  PACK      KEY9,SP70
          CALL      RSTEMP1
          CALL      RKTEMP1
          BRANCH    OVRCD,CLER9999
.
          PACK      KEY9,TMPTCLAM,TMPTHFND,SP70
          CALL      DETEMP1
.
          GOTO      CLER0000
.
CLER9999  RETURN
+
.******************************************************************************
.*                             PRLR0000                 Called by:            *
.******************************************************************************
PRLR0000  MOVE      ZERO,REPTLNNO
          CALL      PRH20000
.
          PACK      KEY9,SP70
          CALL      RSTEMP1
PRLR1000  CALL      RKTEMP1
          BRANCH    OVRCD,PRLR9999
.
          CALL      PRLT0000
.
          GOTO      PRLR1000
.
PRLR9999  RETURN
.**********************************************************************
.         Print duplicate record
.**********************************************************************
PRLT0000  COMPARE    "50",REPTLNNO
          CALL       PRH20000 IF NOT LESS
.
          PRINT      *1,TMPTCLAM,*15,TMPTHFND
          ADD        ONE,REPTLNNO
.
PRLT9999  RETURN
+
.**********************************************************************
.         Print Heading
.**********************************************************************
PRH20000  MOVE       ZERO,REPTLNNO
.
          CALL       HEAD132
          PRINT      *1,"THEATRE FEE MAPPING RECORDS NOT FOUND:",*N
          PRINT      *1,"Claim Code",*15,"Health Fund"
.
          CALL       UND132
          ADD        THREE,REPTLNNO
PRH29999  RETURN
+
.**********************************************************************
.         Check Mapping File Parameter before proceeding
.**********************************************************************
CHKMF000  MATCH      "1",PTCNMFFL
          GOTO       CHKMF500 IF NOT EQUAL
          GOTO       CHKMF900
.
CHKMF500  DISPLAY   *P1:24,*EL,"Error: Records not found or missing contract ids in Theatre fee mapping file.";
          CALL      HITENTER
.
          MOVE       ONE,EXIT
          GOTO       CHKMF999
.
CHKMF900  MOVE       ZERO,EXIT
CHKMF999  RETURN
+

.==============================================================================
.                Tempfile I/O
.==============================================================================
RSTEMP1   RESET     KEY9
          READ      TEMPFILE,KEY9;;
          RETURN
.
RATEMP1   RESET     KEY9
          MOVE      ZERO,OVRCD
          READ      TEMPFILE,KEY9;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;TMPTCLAM,TMPTHFND
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTEMP1   MOVE      ZERO,OVRCD
          READKP    TEMPFILE;TMPTCLAM,TMPTHFND
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          RESET     KEY9
          READ      TEMPFILE,KEY9;TMPTCLAM,TMPTHFND
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY9
          WRITE     TEMPFILE,KEY9;TMPTCLAM,TMPTHFND
          RETURN
.
DETEMP1   RESET     KEY9
          DELETE    TEMPFILE,KEY9
          RETURN
.
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
.                  ******************************    OLD IO ROUTINES
.                  Copy the Positional Read (RS or RDS) from original I/O file,
.                  - change Label to READSOLD and the index name to OLDPTTFE
.
READSOLD  RESET     KEY23
          READ      OLDPTTFE,KEY23;;
          RETURN    
.  
.                  Copy the Read Next (RK or RDK) from the original I/O file
.                  - change Label to READKOLD and the index name to OLDPTTFE
.                  - change any fields that are changing to a prefix of OLD,
.                    eg. PTEDSPAR becomes OLDDSPAR
.
READKOLD
         MOVE      ZERO,OVRCD
         READKS    OLDPTTFE;TFCLM,TFHFUND,TFHFTAB,TFDAYC,TFENDMBS:
                            TFHF1,TFPAT1,TFHF2,TFPAT2,TFHF3,TFPAT3:
                            TFHF4,TFPAT4,TFHF5,TFPAT5,TFHF6,TFPAT6:
                            TFNINV,TFSPARE
         GOTO      OVERCOND IF OVER
         RETURN
.
.                  ******************************    NEW IO ROUTINES
.                  Copy the Write statement from the new I/O file           
.                  (no changes are needed to this routine)               
.
RDTFEE1  MOVE      ZERO,OVRCD
         RESET     KEY24
         READ      PATTFEE1,KEY24;TFCLM,TFHFUND,PTTFHTYP,TFDAYC,TFENDMBS:
                                  TFHF1,TFPAT1,TFHF2,TFPAT2,TFHF3,TFPAT3:
                                  TFHF4,TFPAT4,TFHF5,TFPAT5,TFHF6,TFPAT6:
                                  TFNINV,PTTFCNID,TFSPARE
         GOTO      OVERCOND IF OVER
         RETURN

.
RDATFEE1 RESET     KEY24
         MOVE      ZERO,OVRCD
         READ      PATTFEE1,KEY24;ANS
         GOTO      OVERCOND IF OVER
         RETURN
.
WRTFEE1  RESET     KEY24
         WRITE     PATTFEE1,KEY24;TFCLM,TFHFUND,PTTFHTYP,TFDAYC,TFENDMBS:
                                  TFHF1,TFPAT1,TFHF2,TFPAT2,TFHF3,TFPAT3:
                                  TFHF4,TFPAT4,TFHF5,TFPAT5,TFHF6,TFPAT6:
                                  TFNINV,PTTFCNID,TFSPARE
         GOTO      OVERCOND IF OVER
         RETURN
.
UPTFEE1  UPDATE    PATTFEE1;TFCLM,TFHFUND,PTTFHTYP,TFDAYC,TFENDMBS:
                            TFHF1,TFPAT1,TFHF2,TFPAT2,TFHF3,TFPAT3:
                            TFHF4,TFPAT4,TFHF5,TFPAT5,TFHF6,TFPAT6:
                            TFNINV,PTTFCNID,TFSPARE
         RETURN
.
RDSTFEE1 RESET     KEY24
         READ      PATTFEE1,KEY24;;
         RETURN
.
RDKTFEE1 MOVE      ZERO,OVRCD
         READKS    PATTFEE1;TFCLM,TFHFUND,PTTFHTYP,TFDAYC,TFENDMBS:
                            TFHF1,TFPAT1,TFHF2,TFPAT2,TFHF3,TFPAT3:
                            TFHF4,TFPAT4,TFHF5,TFPAT5,TFHF6,TFPAT6:
                            TFNINV,PTTFCNID,TFSPARE
         GOTO      OVERCOND IF OVER
         RETURN
.
         INC       PATFN1IO/INC
         INC       PATMTFIO/INC
         INC       STD001IO
