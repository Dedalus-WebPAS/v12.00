.******************************************************************************
.* System         : Clinical Audits                                           *
.* Program        : IBACLA02.PBL                                              *
.* Name           : Clinical Audit Site Maintenance                           *
.******************************************************************************
.* Date           : 26/03/1992                                                *
.* Author         : Justin Coates                                             *
.* Function       : To maintain the site file for clinical audits             *
.* Modifications  :                                                           *
.******************************************************************************
.
          INC       STD001FD/PBL
          INC       CLACATFD/INC            * site category file
          INC       CLASITFD/INC            * site file
          INC       SCRPSTFD/INC            * screen position file
          INC       SCRSBGFD/INC            * background file
          INC       SCRSLTFD/INC            * selections file
.
.         program variables
.
CODESITE  DIM       6         * site code
SAVESCRN  DIM       2         * screen id entered
SITOVRCD  FORM      1         * save ovrcd
.
.         program constants
.
CLAAUD    INIT      "CLAAUD"
CLAHOC    INIT      "CLAHOC"
IP        INIT      "Inpatients          "
OP        INIT      "Outpatients         "
AE        INIT      "Accident & Emergency"
TH        INIT      "Theatre             "
.
PRGID     INIT      "IBACLA02"
PRGNAM    INIT      "Site Maintenance"
VERSION   INIT      "V12.00.00"
.
.*********************************************************************
.*                  MAINLINE                                         *
.*********************************************************************
ML0000    CALL      INIT0000                * open files
          CALL      PROC0000                * process the site
ML9999    CHAIN     PGM
+
.*********************************************************************
.*                  INIT0000                    Called by : ML0000   *
.*        initialization and open files                              *
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"opening "
          DISPLAY   *P58:24,"clasitaf"
          OPEN      CLASITA1,"clasitaf"
          DISPLAY   *P58:24,"clacataf"
          OPEN      CLACATA1,"clacataf"
          MOVE      ONE,CNEWDS
INIT9999  RETURN
+
.*********************************************************************
.*                  PROC0000                    Called by : ML0000   *
.*        Enter the site code and process the data                   *
.*********************************************************************
PROC0000  CALL      KSIT0000                * enter the site code
          BRANCH    EXIT,PROC9999           * exit the program
          COMPARE   ZERO,SITOVRCD
          GOTO      PROC4900 IF EQUAL       * change mode
.
. ------- insert mode -------
.
          CALL      DSCR0000                * display the screen
          CALL      KDES0000                * enter description
          CALL      PSWD0000                * enter password
          CALL      SCID0000                * enter screen id
          CALL      ATYP0000                * enter audit type
          CALL      NSCR0000                * enter number of screens
          GOTO      PROC5000
.
. ------- change mode -------
.
PROC4900  CALL      DSCR0000                * display the screen
          CALL      DDAT0000                * display the data
.
.         Select item, (P)ost, (C)ancel, (D)elete ?
.
PROC5000  CALL      MQST0000
          BRANCH    EXIT,PROC8000,PROC0000,PROC9000
.                        Post     Cancel   Delete
          BRANCH    CCITEM,PROC5100,PROC5200,PROC5300,PROC5400,PROC5500
.
          BEEP
          GOTO      PROC5000
.
PROC5100  CALL      KDES0000                * enter description
          GOTO      PROC5000
.
PROC5200  CALL      PSWD0000                * enter password
          GOTO      PROC5000
.
PROC5300  CALL      SCID0000                * enter screen id
          GOTO      PROC5000
.
PROC5400  CALL      ATYP0000                * enter audit type
          GOTO      PROC5000
.
PROC5500  CALL      NSCR0000                * enter number of screens
          GOTO      PROC5000
.
.         Post the details
.
PROC8000  DISPLAY   *P1:24,*EL,"POSTING"
          MOVE      CODESITE,CASTSITE
          MOVE      CASTSITE,KEY6
          IF        SITOVRCD = ONE
            CALL      WRCASIT1
          ELSE 
            CALL      UPCASIT1
          ENDIF
          GOTO      PROC0000
.
.         Delete the details
.
PROC9000  CALL      DSIT0000
          GOTO      PROC0000
.
PROC9999  RETURN
+
.*********************************************************************
.*                  KSIT0000                    Called by : PROC0000 *
.*        Enter the site code                                        *
.*        Returns : EXIT = 1      nothing entered so exit            *
.*                  OVRCD = 1     insert mode                        *
.*                  OVRCD = 0     change mode                        *
.*********************************************************************
KSIT0000  DISPLAY   *P1:3,*EF:
                    *P5:4,"Site           : "
          MOVE      ONE,FORM1
          MOVE      "22",CCOL
          MOVE      FOUR,CVERT
.
KSIT1000  KEYIN     *PCCOL:CVERT,*DV,UNDLN6:
                    *PCCOL:CVERT,*V2LON,*FILL,CODESITE:
                    *PCCOL:CVERT,*DV,CODESITE
.
          RESET     CODESITE
          GOTO      KSIT9100 IF EOS         * nothing entered
          MATCH     SP6,CODESITE
          GOTO      KSIT9100 IF EQUAL       * nothing entered
          MATCH     QUEST,CODESITE
          GOTO      KSIT2000 IF NOT EQUAL   * ? not entered
.
.         ? entered
.
          CALL      CLASITDS
          DISPLAY   *P1:24,"Site : ",*EL;
          MOVE      "8",CCOL
          MOVE      "24",CVERT
          MOVE      ZERO,FORM1
          GOTO      KSIT1000
.
KSIT2000  MOVE      SP30,CASTDESC 
          UNPACK    SP30,CASTPASS,CASTFILE
          MOVE      "9",CASTTYPE
          MOVE      ZERO,CASTSCRN
.
          MOVE      CODESITE,KEY6
          CALL      RDCASIT1
          MOVE      OVRCD,SITOVRCD          * save ovrcd
.       
          IF        FORM1=0
            DISPLAY   *P1:3,*EF:
                      *P5:4,"Site           : ",*V2LON,CODESITE
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      KSIT9999
.
KSIT9100  MOVE      ONE,EXIT
.
KSIT9999  RETURN
+
.*********************************************************************
.*                  DSCR0000                    Called by : PROC0000 *
.*        Display the input screen fields                            *
.*********************************************************************
DSCR0000  DISPLAY   *P1:5,*EF,*V2LON:
                    *P1:6," 1",*P1:7," 2",*P1:8," 3",*P1:9," 4",*P1:10," 5":
                    *HOFF:
                    *P3:06,". Description    : ":
                    *P3:07,". Password       : ":
                    *P3:08,". Screen Id      : ":
                    *P3:09,". Audit Type     : ":
                    *P3:10,". No. of Screens : "
DSCR9999  RETURN
+
.*********************************************************************
.*                  DDAT0000                    Called by : PROC4900 *
.*        Display the current data                                   *
.*********************************************************************
DDAT0000  MOVE      CASTTYPE,FORM1
          MOVE      CASTFILE,SAVESCRN
          MOVE      IP,KEY20
          LOAD      KEY20,FORM1,OP,AE,TH
          DISPLAY   *V2LON,*P22:06,CASTDESC,*P22:07,CASTPASS,*P22:08,CASTFILE:
                    *P22:09,CASTTYPE,*P22:10,CASTSCRN,*HOFF,*P30:09,KEY20
DDAT9999  RETURN
+
.*********************************************************************
.*                  KDES0000                    Called by : PROC0000 *
.*        Enter the description                                      *
.*********************************************************************
KDES0000  MATCH     SP30,CASTDESC
          GOTO      KDES5000 IF EQUAL       * no default
.
          KEYIN     *P22:06,*DV,CASTDESC:
                    *P22:06,*V2LON,*FILL,*RV,CASTDESC:
                    *P22:06,*DV,CASTDESC
          GOTO      KDES9999
.
KDES5000  KEYIN     *P22:06,*DV,UNDLN30:
                    *P22:06,*V2LON,*FILL,CASTDESC:
                    *P22:06,*DV,CASTDESC
.
KDES9999  RETURN
+
.*********************************************************************
.*                  PSWD0000                    Called by : xxxx0000 *
.*        Enter the password for the site                            *
.*********************************************************************
PSWD0000  KEYIN     *P22:07,*DV,UNDLN6:
                    *P22:07,*V2LON,*FILL,CASTPASS:
                    *P22:07,*DV,CASTPASS
.
PSWD9999  RETURN
+
.*********************************************************************
.*                  SCID0000                    Called by : xxxx0000 *
.*        Enter the Screen ID                                        *
.*********************************************************************
SCID0000  KEYIN     *P22:08,*DV,UNDLN2:
                    *P22:08,*V2LON,CASTFILE:
                    *P22:08,*DV,CASTFILE
.
          PACK      CASTFILE,CASTFILE,SP2
.    
          MATCH     SP2,CASTFILE
          GOTO      SCID1000 IF NOT EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Field is mandatory.  ";
          CALL      HITENTER
          GOTO      SCID0000
.
.         check if screen id already used
.
SCID1000  MOVE      CASTFILE,SAVESCRN       * save screen id
          PROC      CKSCRNID                * check id
          BRANCH    EXIT,SCID0000           * already used
.
SCID9999  RETURN
+
.*********************************************************************
.*                  ATYP0000                    Called by : xxxx0000 *
.*        Enter the Audit type                                       *
.*********************************************************************
ATYP0000  DISPLAY   *P1:24,*EL:
                    *V2LON,"0",*HOFF,"=Inpatients, ":
                    *V2LON,"1",*HOFF,"=Outpatients, ":
                    *V2LON,"2",*HOFF,"=Accident & Emergency, ":
                    *V2LON,"3",*HOFF,"=Theatre"
.
          KEYIN     *P22:09,*DV,UNDLN1:
                    *P22:09,*V2LON,FORM1:
                    *P22:09,*DV,FORM1
.
          COMPARE   ZERO,FORM1
          GOTO      ATYP1000 IF EQUAL       * inpatients
          BRANCH    FORM1,ATYP1000,ATYP1000,ATYP1000
.
          BEEP
          GOTO      ATYP0000
.
ATYP1000  MOVE      IP,KEY20
          LOAD      KEY20,FORM1,OP,AE,TH
          MOVE      FORM1,CASTTYPE          * set audit type
          DISPLAY   *P22:09,*V2LON,CASTTYPE,*HOFF,*P30:09,KEY20:
                    *P1:24,*EL
.
ATYP9999  RETURN
+
.*********************************************************************
.*                  NSCR0000                    Called by : xxxx0000 *
.*        Enter the Number of screens                                *
.*********************************************************************
NSCR0000  DISPLAY   *P1:24,*EL,"Enter '",*V2LON,ONE,*HOFF:
                    "' or '",*V2LON,TWO,*HOFF,"'"
          KEYIN     *P22:10,*DV,UNDLN1:
                    *P22:10,*V2LON,CASTSCRN:
                    *P22:10,*DV,CASTSCRN
.
          BRANCH    CASTSCRN,NSCR9999,NSCR9999
.
          BEEP
          GOTO      NSCR0000
.
NSCR9999  RETURN
+
.*********************************************************************
.                   DSIT0000                    Called by : PROC9000 
.         delete the site
.*********************************************************************
DSIT0000  DISPLAY   *P1:24,"Are you sure you want to Delete (",*V2LON,"Y",*HOFF:
                    "/",*V2LON,"N",*HOFF,") ? ",*EL;
          KEYIN     *P41:24,"_":
                    *P41:24,*V2LON,ANS
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
          MATCH     ANSN,ANS
          GOTO      DSIT9999 IF EQUAL       * dont delete
          MATCH     ANSY,ANS
          GOTO      DSIT0500 IF EQUAL       * delete
          BEEP
          GOTO      DSIT0000
.
DSIT0500  MOVE      CODESITE,KEY6
          DISPLAY   *P1:24,*EL,"DELETING"
          CALL      DECASIT1
.
.         delete all categories relating to this site
.
DSIT1000  PACK      KEY8,CODESITE,SP2
          CALL      RSCACAT1
          CALL      RKCACAT1
          BRANCH    OVRCD,DSIT2000
.
          MATCH     CODESITE,CACTSITE
          GOTO      DSIT2000 IF NOT EQUAL   * different site
.
          PACK      KEY8,CACTSITE,CACTUNIQ
          CALL      DECACAT1
          GOTO      DSIT1000
.
. ******* delete all screen painter file details for this site *******
.
DSIT2000  OPEN      SCRPSTA1,"scrpstaf"
          OPEN      SCRSBGA1,"scrsbgaf"
          OPEN      SCRSLTA1,"scrsltaf"
.
.         delete all background data - SCRSBGFD
.
DSIT2100  PACK      CFNAME,CLAAUD,SAVESCRN  * INPUT SCREEN details
          PACK      KEY12,CFNAME,SP4
          CALL      RSSCSB1
          CALL      RKSCSB1
          BRANCH    OVRCD,DSIT2500          * end of file
.
          MATCH     CFNAME,SCSBPID
          GOTO      DSIT2500 IF NOT EQUAL   * different program id
.
          PACK      KEY12,SCSBPID,SCSBSID,SCSBLIN
          CALL      DESCSB1
          GOTO      DSIT2100
.
DSIT2500  PACK      CFNAME,CLAHOC,SAVESCRN  * AD-HOC enquiry details
          PACK      KEY12,CFNAME,SP4
          CALL      RSSCSB1
          CALL      RKSCSB1
          BRANCH    OVRCD,DSIT3000          * end of file
.
          MATCH     CFNAME,SCSBPID
          GOTO      DSIT3000 IF NOT EQUAL   * different program id
.
          PACK      KEY12,SCSBPID,SCSBSID,SCSBLIN
          CALL      DESCSB1
          GOTO      DSIT2500
.
.         delete all selections data - SCRSLTFD
.
DSIT3000  PACK      CFNAME,CLAAUD,SAVESCRN
          PACK      KEY13,CFNAME,SP5
          CALL      RSSCSL1
          CALL      RKSCSL1
          BRANCH    OVRCD,DSIT3500          * end of file
.
          MATCH     CFNAME,SCSLPID
          GOTO      DSIT3500 IF NOT EQUAL   * different program id
.
          PACK      KEY13,SCSLPID,SCSLSID,SCSLSEL
          CALL      DESCSL1
          GOTO      DSIT3000
.
DSIT3500  PACK      CFNAME,CLAHOC,SAVESCRN
          PACK      KEY13,CFNAME,SP5
          CALL      RSSCSL1
          CALL      RKSCSL1
          BRANCH    OVRCD,DSIT4000          * end of file
.
          MATCH     CFNAME,SCSLPID
          GOTO      DSIT4000 IF NOT EQUAL   * different program id
.
          PACK      KEY13,SCSLPID,SCSLSID,SCSLSEL
          CALL      DESCSL1
          GOTO      DSIT3500
.
.         delete all screen position data - SCRPSTFD
.
DSIT4000  PACK      CFNAME,CLAAUD,SAVESCRN
          PACK      KEY15,CFNAME,SP5
          CALL      RSSCPS1
          CALL      RKSCPS1
          BRANCH    OVRCD,DSIT4500          * end of file
.
          MATCH     CFNAME,SCPSPID
          GOTO      DSIT4500 IF NOT EQUAL   * different program id
.
          PACK      KEY15,SCPSPID,SCPSSID,SCPSROW,SCPSCOL
          CALL      DESCPS1
          GOTO      DSIT4000
.
DSIT4500  PACK      CFNAME,CLAHOC,SAVESCRN
          PACK      KEY15,CFNAME,SP5
          CALL      RSSCPS1
          CALL      RKSCPS1
          BRANCH    OVRCD,DSIT9999          * end of file
.
          MATCH     CFNAME,SCPSPID
          GOTO      DSIT9999 IF NOT EQUAL   * different program id
.
          PACK      KEY15,SCPSPID,SCPSSID,SCPSROW,SCPSCOL
          CALL      DESCPS1
          GOTO      DSIT4500
.
DSIT9999  RETURN
+
.*********************************************************************
.*                  MQST0000                    Called by : PROC5000 *
.*        Select item, (P)ost, (C)ancel, (D)elete ? ___              *
.*        Returns : EXIT = 1      2         3                        *
.*                  CCITEM        item numbe to check                *
.*********************************************************************
MQST0000  DISPLAY   *P1:24,"Select item, (P)ost, (C)ancel, (D)elete ? ",*EL:
                    *V2LON,*P15:24,"P",*P23:24,"C",*P33:24,"D"
          KEYIN     *P43:24,*DV,UNDLN2:
                    *P43:24,*V2LON,*JR,CCITEMD:
                    *P43:24,*DV,CCITEMD
.
          TYPE      CCITEMD
          GOTO      MQST5000 IF EQUAL       * number entered
.
          UNPACK    CCITEMD,ANS,ANS,ANS
          REP       UPPLOW,ANS 
          REP       "P1C2D3",ANS
          MOVE      ZERO,EXIT
          MOVE      ANS,EXIT
          BRANCH    EXIT,MQST9999,MQST9999,MQST9999
.
          BEEP
          GOTO      MQST0000
.
.         number entered
.
MQST5000  MOVE      CCITEMD,CCITEM
          MOVE      ZERO,EXIT
.
MQST9999  RETURN
+
.*********************************************************************
.                   CKSCRNID                    Called by : SCID1000 
.         check if the screen id has already been entered
.*********************************************************************
          DEFPROC   CKSCRNID
.
          INC       CLASITFD/INC
.
CKID0000  OPEN      CLASITA1,"clasitaf"
          MOVE      SP6,KEY6
          CALL      RSCASIT1
.
CKID1000  CALL      RKCASIT1
          BRANCH    OVRCD,CKID8000          * valid id
.
          MATCH     CODESITE,CASTSITE
          GOTO      CKID1000 IF EQUAL       * dont check for same site
.
          MATCH     SAVESCRN,CASTFILE
          GOTO      CKID1000 IF NOT EQUAL   * different screen id
.
          DISPLAY   *P1:24,*B,"Screen Id already exists.  ",*EL;
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      CKID9999
.
CKID8000  MOVE      ZERO,EXIT
          GOTO      CKID9999
.
          INC       CLASITIO/INC
          INC       IBAOVRIO/INC
.
CKID9999  ENDPROC
+
          INC       STD001IO/PBL
          INC       CLACATIO/INC            * site category file
          INC       CLASITIO/INC            * site file
          INC       CLASITDS/PBL            * ? for site file
          INC       SCRPSTIO/INC            * screen position file
          INC       SCRSBGIO/INC            * background file
          INC       SCRSLTIO/INC            * selections file
...............................................................................
