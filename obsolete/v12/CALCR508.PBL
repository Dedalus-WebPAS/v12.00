
.****************************************************************************
.* Routine    :  PATCALCR  (CALCR508.PBL)                                   *
.* Desc       :  Calculate Revenue routine.                                 *
.****************************************************************************
.* Date       :  06/07/1993                                                 *
.* Author     :  Glenn Berry                                                *
.* Function   :  Calculate Revenue for all Invoices for a given             *
.*               Admission Number.                                          *
.*                                                                          *
.* Modified   :                                                             *
.*    V9.12.01   12/03/2010  J.Tan        CAR 194862                        *
.*               Fixed calculate income                                     *
.*    V9.04.01   04/08/2005  J.Tan        CAR 62750                         *
.*               Mods not to use the redefined amount variables             *
.*    V9.03.01   09/08/2004   J.Tan      CAR 52388                          *
.*               Mods to check for credit notes                             *
.*    V9.03.00   03/03/2004   Ebon Clements    CAR 46515                    *
.*               Added ALCL0000 for allied heath revenue                    *
.*    V9.02.00   18.06.2002   Glenn Saunders   V9.02 port                   *
.*               Export to new release i.e. v9.02 from vf.09                *
.*               Remove PSN/OSN prefixed PAS cloned includes                *
.*--------------------------------------------------------------------------*
.*               15/07/1999   Steve Armstrong   srf 633231                  *
.*               Fixed setting of TODATE for NHOSPDAY where discharge record*
.*               not on file.                                               *
.*               16.08.93     B.G.Ackland                                   *
.*               Modified to Branch on PVITYPE not PINVTYPE                 *
.*    V6.00.01   10.01.1993   B.G.Ackland                                   *
.*               Added New Income & Bed Time Component Calculation          *
.*--------------------------------------------------------------------------*
.*                                                                          *
.*                                                                          *
.* Parameters :  PVIBILL   FORM 8    -  Admission Number                    *
.*               PVITYPE   FORM 1    -  Visit Type                          *
.*                                                                          *
.* Returns    :  EXIT      = 1       -  If PVIBILL record not found on      *
.*                                      PATINVFD or PATMISFD                * 
.*               OT1REVNE  FORM 9.2  -  Misc. Revenue Type 1                *
.*               OT2REVNE  FORM 9.2  -  Misc. Revenue Type 2                *
.*               OT3REVNE  FORM 9.2  -  Misc. Revenue Type 3                *
.*               OTOREVNE  FORM 9.2  -  Misc. Revenue Other                 *
.*               AC1REVNE  FORM 9.2  -  Accom. Revenue Type 1               *
.*               AC2REVNE  FORM 9.2  -  Accom. Revenue Type 2               *
.*               AC3REVNE  FORM 9.2  -  Accom. Revenue Type 3               *
.*               ACOREVNE  FORM 9.2  -  Accom. Revenue Other                *
.*               THTREVNE  FORM 9.2  -  Theatre Revenue                     *
.*               TOTPBDAY  FORM 6.2  -  Total Patient Bed Days              *
.*               TO1PBDAY  FORM 6.2  -  Patient Bed Days Acc. Type 1        *
.*               TO2PBDAY  FORM 6.2  -  Patient Bed Days Acc. Type 2        *
.*               TO3PBDAY  FORM 6.2  -  Patient Bed Days Acc. Type 3        *
.* Requirements                                                             * 
.* ------------                                                             *
.* Includes   :  PATINVFD, PATIOINV                                         *
.*               PATMISFD, PATIOMIS                                         *
.*               PATDSCFD, PATIODSC                                         *
.*               PATCODFD, PATIOCOD                                         *
.*               PATDTRFD, PATIODTR                                         *
.*               CCSIMCFD, CCSIMCIO                                         *
.*               NHOSPDAY                                                   *
.*                                                                          *
.* Open files :  PATINVR3                                                   *
.*               PATMISS1                                                   *
.*               PATDSCH1                                                   *
.*               PATCODE1                                                   *
.*               PATDTRA1                                                   *
.*               CCSIMCA1                                                   *
.*                                                                          *
.****************************************************************************
PATCALCR  MOVE      PVIBILL,SAVEBILL
          MOVE      ZERO,EXIT                * initialize
          MOVE      ZERO,OT1REVNE            * initialize
          MOVE      ZERO,OT2REVNE            * initialize
          MOVE      ZERO,OT3REVNE            * initialize
          MOVE      ZERO,OTOREVNE            * initialize
          MOVE      ZERO,AC1REVNE            * initialize
          MOVE      ZERO,AC2REVNE            * initialize
          MOVE      ZERO,AC3REVNE            * initialize
          MOVE      ZERO,ACOREVNE            * initialize
          MOVE      ZERO,THTREVNE            * initialize
          MOVE      ZERO,TOTPBDAY            * initialize
          MOVE      ZERO,TO1PBDAY            * initialize
          MOVE      ZERO,TO2PBDAY            * initialize
          MOVE      ZERO,TO3PBDAY            * initialize
.
.---- get patient type
.
          BRANCH    PVITYPE,PTCL1000:        * A&E
                            PTCL1000:        * Outpatient
                            PTCL2000         * Inpatient
          GOTO      PTCL1200                 * Any other patient type
.
.---- A&E Patients & Outpatients
PTCL1000  MOVE      ONE,TOTPBDAY
          MOVE      ZERO,AC1REVNE
          MOVE      ZERO,AC2REVNE
          MOVE      ZERO,AC3REVNE
          MOVE      ZERO,ACOREVNE
          MOVE      ZERO,THTREVNE
          GOTO      PTCL1500
.
.---- All other patients 
PTCL1200  MOVE      ZERO,TOTPBDAY
          MOVE      ZERO,TO1PBDAY
          MOVE      ZERO,TO2PBDAY
          MOVE      ZERO,TO3PBDAY
          MOVE      ZERO,AC1REVNE
          MOVE      ZERO,AC2REVNE
          MOVE      ZERO,AC3REVNE
          MOVE      ZERO,ACOREVNE
          MOVE      ZERO,THTREVNE
          GOTO      PTCL1500
.
.-------- calculate OTHREVNE
PTCL1500  PACK      KEY16,SAVEBILL,SP8          
          CALL      RDSINV3                  * pos read on first invoice V90200
PTCL1510  CALL      RDKINV3                  * Read Next invoice         V90200
          BRANCH    OVRCD,PTCL9999           * EOF
.          
          MATCH     SAVEBILL,DPINVADM        * same admission no.?       V90200
          GOTO      PTCL9999 IF NOT EQUAL    * no, end
.
          ADD       PINVAMT,OTOREVNE                                  * V90200
          ADD       PINVJOUR,OTOREVNE                                  * V90200
          SUB       PTINCRTT,OTOREVNE        * Exclude credit note
          GOTO      PTCL1510                 * loop
.
.---- Inpatients 
PTCL2000
.
.-- calc TOTPBDAY
          MOVE      SAVEBILL,DAADMNO                                   * V90200
          PACK      KEY8,DAADMNO                                       * V90200
          CALL      RDMISS1                                            * V90200
          BRANCH    OVRCD,PTCL9900
          MOVE      ADATE,FROMDATE           * set FROMDATE as admn dte  V90200
.
          MOVE      SAVEBILL,DDADMNO                                   * V90200
          PACK      KEY8,DDADMNO                                       * V90200
          CALL      RDDSCH1                  * patient discharged?       V90200
          IF        OVRCD=1
            PACK      TODATE,CCC,CYY,CMM,CDD * set TODATE as current date
            REP       " 0",TODATE
          ELSE
            MOVE      DDATE,TODATE           * set TODATE as dischg dte  V90200
          ENDIF
.
          CALL       NHOSPDAY
          MOVE       NBRDAYS,TOTPBDAY
.-------------------------
.  Calc Income
.-------------------------
          PACK       KEY22,SAVEBILL,SP20
          CALL       RDSDTRN1                                          * V90200
PTCL2500  CALL       RDKDTRN1                * Read next PATDTRFD record V90200
          BRANCH     OVRCD,PTCL9999          * EOF
          MATCH      SAVEBILL,DTADMNO        * same admission no.?       V90200
          GOTO       PTCL9999 IF NOT EQUAL
.
          MATCH      "1",PTDTCRST            * Fully credited
          GOTO       PTCL2500 IF EQUAL
.
          MOVE       DTRECTYP,F1                                       * V90200
          BRANCH     F1,PTCL2510:      * Accom
                        PTCL2520:      * Theatre
                        PTCL2530       * Misc.
          COMPARE    F1,SIX          
          GOTO       PTCL2600 IF EQUAL       * Journal
          GOTO       PTCL2500              
.
PTCL2510  MOVE       "5",INCTYPE             * Acc. Other
          PACK       KEY10,FOUR,TDWARD,SP70                            * V90200
          CALL       RDCCIM1             * Check for Ward Mapping
          IF         OVRCD=0
            MOVE       CCIMITY,INCTYPE
            COMPARE    ZERO,INCTYPE 
            GOTO       PTCL2500 IF EQUAL
            GOTO       PTCL3000
          ENDIF
.
          PACK       KEY10,FIVE,TADMTYP,SP70                           * V90200
          CALL       RDCCIM1             * Check for Admission Type Mapping
          IF         OVRCD=0
            MOVE       CCIMITY,INCTYPE
            COMPARE    ZERO,INCTYPE 
            GOTO       PTCL2500 IF EQUAL
            GOTO       PTCL3000
          ENDIF
.
          PACK       KEY8,DTFCENT,DTFYEAR,DTFMONTH,DTFDAY              * V90200
          REP        " 0",KEY8
          PACK       KEY30,DTADMNO,KEY8,Z70                            * V90200
          CALL       RDSTRAN2                                          * V90200
          CALL       RDKTRAN2                                          * V90200
          BRANCH     OVRCD,PTCL3000
          MATCH      DTADMNO,DTADMN           * Check for Admission No   V90200
          GOTO       PTCL3000 IF NOT EQUAL
          MATCH      TDWARD,TWARD             * Check for the Ward       V90200
          GOTO       PTCL3000 IF NOT EQUAL
.
          PACK       KEY10,SIX,TRBTYP,SP70
          CALL       RDCCIM1             * Check for Bed Type Mapping
          IF         OVRCD=0
            MOVE       CCIMITY,INCTYPE
          ENDIF
          COMPARE    ZERO,INCTYPE 
          GOTO       PTCL2500 IF EQUAL
          GOTO       PTCL3000
.
PTCL2520  MATCH      "2",PTDTCRST        * Credit by item
          GOTO       PTCL2500 IF EQUAL
          MOVE       "1",INCTYPE         * Theatre
          GOTO       PTCL2535
.
PTCL2530  MATCH      "2",PTDTCRST        * Credit by item
          GOTO       PTCL2500 IF EQUAL
          MOVE       "9",INCTYPE         * Misc Other
.
PTCL2535  PACK       KEY10,TWO,TITEMNO,SP70                            * V90200
          CALL       RDCCIM1             * Check for Item Code Mapping
          IF         OVRCD=0
            MOVE       CCIMITY,INCTYPE   * Use Specific Mapping
          ELSE
            PACK       KEY10,ONE,TMISGRP,SP70                          * V90200
            CALL       RDCCIM1           * Check for Group Mapping
            IF         OVRCD=0
              MOVE       CCIMITY,INCTYPE * Use Specified Mapping
            ENDIF
          ENDIF
          COMPARE    ZERO,INCTYPE 
          GOTO       PTCL2500 IF EQUAL
          GOTO       PTCL3000
.
PTCL2600  MOVE       "9",INCTYPE         * Misc. Other
          PACK       KEY10,THREE,TTYPE,SP70                            * V90200
          CALL       RDCCIM1             * Check for Journal Code Mapping
          IF         OVRCD=0
            MOVE       CCIMITY,INCTYPE * Use Specified Mapping
          ENDIF
          COMPARE    ZERO,INCTYPE 
          GOTO       PTCL2500 IF EQUAL
.           
PTCL3000  ADD        PTDTLSPT,TPATAMTT                                 * V90200
          ADD        PTDTLSRB,TPATAMTT                                 * V90200
          CALL       GCCR0000               * Check for any credit notes
          BRANCH     INCTYPE,PTCL3100,PTCL3200,PTCL3300,PTCL3400,PTCL3500:
                             PTCL3600,PTCL3700,PTCL3800,PTCL3900
PTCL3100  ADD        TPATAMTT,THTREVNE                                  * V90200
          GOTO       PTCL2500
PTCL3200  ADD        TPATAMTT,AC1REVNE                                  * V90200
          ADD        TNODAYS,TO1PBDAY                                  * V90200
          GOTO       PTCL2500
PTCL3300  ADD        TPATAMTT,AC2REVNE                                  * V90200
          ADD        TNODAYS,TO2PBDAY                                  * V90200
          GOTO       PTCL2500
PTCL3400  ADD        TPATAMTT,AC3REVNE                                  * V90200
          ADD        TNODAYS,TO3PBDAY                                  * V90200
          GOTO       PTCL2500
PTCL3500  ADD        TPATAMTT,ACOREVNE                                  * V90200
          GOTO       PTCL2500
PTCL3600  ADD        TPATAMTT,OT1REVNE                                  * V90200
          GOTO       PTCL2500
PTCL3700  ADD        TPATAMTT,OT2REVNE                                  * V90200
          GOTO       PTCL2500
PTCL3800  ADD        TPATAMTT,OT3REVNE                                  * V90200
          GOTO       PTCL2500
PTCL3900  ADD        TPATAMTT,OTOREVNE                                  * V90200
          GOTO       PTCL2500
.
PTCL9900  MOVE      ONE,EXIT
.
PTCL9999  RETURN
+
.
.------------------------------------------------------------
. Calculate Revenue routine for allied health
.*               OT1REVNE  FORM 9.2  -  Misc. Revenue Type 1                *
.*               OT2REVNE  FORM 9.2  -  Misc. Revenue Type 2                *
.*               OT3REVNE  FORM 9.2  -  Misc. Revenue Type 3                *
.*               OTOREVNE  FORM 9.2  -  Misc. Revenue Other                 *
.*               AC1REVNE  FORM 9.2  -  Accom. Revenue Type 1               *
.*               AC2REVNE  FORM 9.2  -  Accom. Revenue Type 2               *
.*               AC3REVNE  FORM 9.2  -  Accom. Revenue Type 3               *
.*               ACOREVNE  FORM 9.2  -  Accom. Revenue Other                *
.*               THTREVNE  FORM 9.2  -  Theatre Revenue                     *
.*               TOTPBDAY  FORM 6.2  -  Total Patient Bed Days              *
.*               TO1PBDAY  FORM 6.2  -  Patient Bed Days Acc. Type 1        *
.*               TO2PBDAY  FORM 6.2  -  Patient Bed Days Acc. Type 2        *
.*               TO3PBDAY  FORM 6.2  -  Patient Bed Days Acc. Type 3        *
.* Requirements                                                             *
.------------------------------------------------------------
ALCL0000  MOVE      PVIBILL,SAVEBILL
          MOVE      ZERO,EXIT                * initialize
          MOVE      ZERO,OT1REVNE            * initialize
          MOVE      ZERO,OT2REVNE            * initialize
          MOVE      ZERO,OT3REVNE            * initialize
          MOVE      ZERO,OTOREVNE            * initialize
          MOVE      ZERO,AC1REVNE            * initialize
          MOVE      ZERO,AC2REVNE            * initialize
          MOVE      ZERO,AC3REVNE            * initialize
          MOVE      ZERO,ACOREVNE            * initialize
          MOVE      ZERO,THTREVNE            * initialize
          MOVE      ZERO,TOTPBDAY            * initialize
          MOVE      ZERO,TO1PBDAY            * initialize
          MOVE      ZERO,TO2PBDAY            * initialize
          MOVE      ZERO,TO3PBDAY            * initialize
.
          PACK      KEY22,ALENUNIQ,SP70
          CALL      RSPRDT1
ALCL1000  CALL      RKPRDT1
          BRANCH    OVRCD,ALCL9999
.
          MATCH     ALENUNIQ,DPRDTUNQ
          GOTO      ALCL9999 IF NOT EQUAL
.
          BRANCH    PRDTRTYP,ALCL1100,ALCL1000,ALCL1100
.                            Invoice  Receipt  Journal
          GOTO      ALCL1000
.
ALCL1100  MATCH     ALENVISN,PRDTREFN
          GOTO      ALCL1000 IF NOT EQUAL
.
          MATCH     ALENENCT,PRDTENCN
          GOTO      ALCL1000 IF NOT EQUAL
.
          ADD       PRDTAMNT,OTOREVNE       * Add up invoices and journals
.
ALCL9999  RETURN
+
.------------------------------------------------------------
.         Get credit notes for the transaction
.------------------------------------------------------------
GCCR0000  OPEN      PMSFCIA1,"pmsfciaf"
          PACK      KEY22,DTADMNO,TREF,TTRANSN1
          PACK      KEY30,DTADMNO,TREF,TTRANSN1,SP70
          CALL      RSPMFCI1
GCCR1000  CALL      RKPMFCI1
          BRANCH    OVRCD,GCCR9999
          PACK      KEY22A,PMFCVISN,PMFCINVN,PMFCTRAN
          MATCH     KEY22,KEY22A
          GOTO      GCCR9999 IF NOT EQUAL
          ADD       PMFCCAMT,TPATAMTT
          ADD       PMFCCGST,TPATAMTT
          GOTO      GCCR1000
GCCR9999  RETURN
+
