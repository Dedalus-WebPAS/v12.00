.******************************************************************************
.* System         : Accident and Emergency                                    *
.* Program        : IBAAAE64.PBL                                              *
.* Name           : Diagnosis Codes Master Listing                            *
.******************************************************************************
.* Date           : 29/09/1993                                                *
.* Author         : Justin Coates                                             *
.* Function       : Produce a master listing for All diagnosis details        *
.* Modifications  :                                                           *
.*        V5.05.01  17/12/1997  Steve Armstrong       EMR Mods                *
.*                  Mods for new field AERCFLAG                               *
.******************************************************************************
.
          INC       STD001FD                * standard variables
          INC       AAEGDCFD/INC
          INC       AAEGDLFD/INC
          INC       AAEGICFD/INC
          INC       AAEGILFD/INC
          INC       AAEGRCFD/INC
          INC       AAEGTCFD/INC
          INC       AAEGTLFD/INC
.
.         program variables
.
PRTCODE   DIM       6
PRTDESC   DIM       20
.
.         program constants
.
OPTA      INIT      "Diagnosis    "
OPTB      INIT      "Investigation"
OPTC      INIT      "Treatment    "
OPTD      INIT      "Region       "
.
PRGID     INIT      "IBAAAE64"
PRGNAM    INIT      "Diagnosis Codes Master Listing"
VERSION   INIT      "V12.00.00"
+
.*********************************************************************
.         Mainline Code / Controlling Logic
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000
          BRANCH    EXIT,ML9999             * exit selected
.
ML2000    CALL      REPT0000
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.********************************************************************* 
.         Initialization
.*********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CNEWDS
          DISPLAY   *P50:24,"Opening"
          DISPLAY   *P58:24,"aaegdcaf"
          OPEN      AAEGDCA1,"aaegdcaf"
          DISPLAY   *P58:24,"aaegdlaf"
          OPEN      AAEGDLA1,"aaegdlaf"
          DISPLAY   *P58:24,"aaegicaf"
          OPEN      AAEGICA1,"aaegicaf"
          DISPLAY   *P58:24,"aaegilaf"
          OPEN      AAEGILA1,"aaegilaf"
          DISPLAY   *P58:24,"aaegtcaf"
          OPEN      AAEGTCA1,"aaegtcaf"
          DISPLAY   *P58:24,"aaegtlaf"
          OPEN      AAEGTLA1,"aaegtlaf"
          DISPLAY   *P58:24,"aaegrcaf"
          OPEN      AAEGRCA1,"aaegrcaf"
.
INIT9999  RETURN
+
.*********************************************************************
.         See if to produce listing or not
.*********************************************************************
OPTN0000  HLINE     *G37,2,55,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Diagnosis codes ":
                    *P1:6,*V2LON,TWO,*HOFF," = Investigation codes ":
                    *P1:7,*V2LON,THREE,*HOFF," = Treatment codes ":
                    *P1:8,*V2LON,FOUR,*HOFF," = Region codes ":
                    *P1:10,"Select option : "
.
OPTN1000  KEYIN     *P17:10,*V2LON,MOPTION
.
          MOVE      ONE,EXIT
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9999 IF EQUAL       * exit
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,OPTN9000,OPTN9000,OPTN9000,OPTN9000
          BEEP
          GOTO      OPTN1000
OPTN9000  LOAD      CPHDROPT,MOPTION,OPTA,OPTB,OPTC,OPTD
          DISPLAY   *P55:2,*V2LON,"- ",*+,CPHDROPT,SP1
.
OPTN9999  RETURN
+
.*********************************************************************
.         Produce the report
.*********************************************************************
REPT0000  DISPLAY   *P1:24,*EL,"Printing..."
          CALL      IBACLOCK
          MOVE      ZERO,CPAGENO            * clear page number
          CALL      HEAD0000                * print header
.
          MOVE      SP2,KEY2
          MOVE      SP4,KEY4
          PERFORM   MOPTION,RSAEGDC1,RSAEGIC1,RSAEGTC1,RSAEGRC1
.
REPT1000  CALL      RDKF0000
          CALL      PEND0000                * print end of report
.
REPT9999  RETURN
+
.*********************************************************************
.         Print the page header
.*********************************************************************
HEAD0000  CALL      HEAD80
          IF        MOPTION = 4
            PRINT     *1,"*------------------------------------------*"
            PRINT     *1,"| Code   Description          Foreign Body |"
            PRINT     *1,"*------------------------------------------*"
          ELSE
            PRINT     *1,"*-----------------------------*"
            PRINT     *1,"| Code   Description          |"
            PRINT     *1,"*-----------------------------*"
          ENDIF
          MOVE      "4",CLNO
HEAD9999  RETURN
+
.*********************************************************************
.         Print a line of the report
.*********************************************************************
PLIN0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          ADD       ONE,CLNO
          IF        MOPTION = 4
            MATCH     ANSF,AERCFLAG
            IF        @EQUAL
              PRINT     *1,"| ",PRTCODE,SP1,PRTDESC,SP1,"Yes          |"
            ELSE
              MATCH     ANSN,AERCFLAG
              IF        @EQUAL
                PRINT     *1,"| ",PRTCODE,SP1,PRTDESC,SP1,"No           |"
              ELSE
                PRINT     *1,"| ",PRTCODE,SP1,PRTDESC,SP1,"             |"
              ENDIF
            ENDIF
          ELSE
            PRINT     *1,"| ",PRTCODE,SP1,PRTDESC," |"
          ENDIF
PLIN9999  RETURN
+
.*********************************************************************
.         Print the ending of the report
.*********************************************************************
PEND0000  IF        MOPTION = 4
            PRINT     *1,"*------------------------------------------*":
                      *N,*N,"*** End of Report ***",*N
          ELSE
            PRINT     *1,"*-----------------------------*":
                      *N,*N,"*** End of Report ***",*N
          ENDIF
PEND9999  RETURN
+
.*********************************************************************
.         do a next read on the files
.*********************************************************************
RDKF0000  BRANCH    MOPTION,RDKF1000,RDKF2000,RDKF3000,RDKF4000
.
. diagnosis codes - AAEGDCFD and AAEGDLFD
.
RDKF1000  CALL      RKAEGDC1
          BRANCH    OVRCD,RDKF9999
          PACK      PRTCODE,AEDCCODE,SP6
          PACK      PRTDESC,AEDCDESC,SP20
          CALL      PLIN0000
.
          PACK      KEY3,AEDCCODE,SP1
          CALL      RSAEGDL1
.
RDKF1100  CALL      RKAEGDL1
          BRANCH    OVRCD,RDKF1000
          MATCH     AEDLMAIN,AEDCCODE
          GOTO      RDKF1000 IF NOT EQUAL
          PACK      PRTCODE,AEDCCODE,AEDLCODE,SP6
          PACK      PRTDESC,AEDLDESC,SP20
          CALL      PLIN0000
          GOTO      RDKF1100
.
. investigation codes - AAEGICFD and AAEGILFD
.
RDKF2000  CALL      RKAEGIC1
          BRANCH    OVRCD,RDKF9999
          PACK      PRTCODE,AEICCODE,SP6
          PACK      PRTDESC,AEICDESC,SP20
          CALL      PLIN0000
.
          PACK      KEY6,AEICCODE,SP2
          CALL      RSAEGIL1
.
RDKF2100  CALL      RKAEGIL1
          BRANCH    OVRCD,RDKF2000
          MATCH     AEILMAIN,AEICCODE
          GOTO      RDKF2000 IF NOT EQUAL
          PACK      PRTCODE,AEICCODE,AEILCODE,SP6
          PACK      PRTDESC,AEILDESC,SP20
          CALL      PLIN0000
          GOTO      RDKF2100
.
. treatment codes - AAEGTCFD and AAEGTLFD
.
RDKF3000  CALL      RKAEGTC1
          BRANCH    OVRCD,RDKF9999
          PACK      PRTCODE,AETCCODE,SP6
          PACK      PRTDESC,AETCDESC,SP20
          CALL      PLIN0000
.
          PACK      KEY6,AETCCODE,SP2
          CALL      RSAEGTL1
.
RDKF3100  CALL      RKAEGTL1
          BRANCH    OVRCD,RDKF3000
          MATCH     AETLMAIN,AETCCODE
          GOTO      RDKF3000 IF NOT EQUAL
          PACK      PRTCODE,AETCCODE,AETLCODE,SP6
          PACK      PRTDESC,AETLDESC,SP20
          CALL      PLIN0000
          GOTO      RDKF3100
.
. region codes - AAEGRCFD
.
RDKF4000  CALL      RKAEGRC1
          BRANCH    OVRCD,RDKF9999
          PACK      PRTCODE,AERCCODE,SP6
          PACK      PRTDESC,AERCDESC,SP20
          CALL      PLIN0000
          GOTO      RDKF4000
.
RDKF9999  RETURN
+
.
.         I/O includes
.
          INC       STD001IO              * standard routine
          INC       AAEGDCIO/INC
          INC       AAEGDLIO/INC
          INC       AAEGICIO/INC
          INC       AAEGILIO/INC
          INC       AAEGRCIO/INC
          INC       AAEGTCIO/INC
          INC       AAEGTLIO/INC
................................................................................
