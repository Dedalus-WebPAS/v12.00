.*****************************************************************************
.*    Program      : F00PTEX1                                                *
.*    Description  : Conversion patex1xx to new File layout                  *
.*                                                                           *
.*    Author       : Mike Laming                                             *
.*    Date         : 30/03/2010                                              *
.*    Modifications:                                                         *
.*        V10.00.01  29/03/2010  Mike Laming   CAR 157391                    *
.*                   Program created.                                        *
.*****************************************************************************
.
.  Global change F00PTEX1  eg F57PTDOC
.  Global change PATEX1FD  eg PATDOCFD (new layout)
.  Global change patex1xx  eg patdoctf
.  Global change PATEX1A1  eg PATDOCT1
.  Global change KEY8   eg KEY8
.  Global change KEY8  eg KEY9
.  Global change WRPTEXA1  eg WRDOCT1
.  Global change sptex1xx  eg sptdoctf
.
.  vi Global change command  :%s/FromString/ToString/g
.
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
.
. Enter the first or the shortest index from the original FD and rename
. the index OLDFILE1
.
.                                     ******** copy the old FD here *******
.                                     * comment out all continuing fields and
.                                     * prefix changing fields with "OLD" -
.                                     * remove old Indexes and Redefines     
.
..OLDFILE1  IFILE     SQL, FIXED=xxx, KEY="Uxx-xx"
.Data File Definition
.--------------------
OLDFILE1  IFILE     SQL, FIXED=354, KEY="U1-8"
.
.FILEXAA1  INIT      "patexa"
.FILEX1A1  INIT      "patex1"
.
. NAME     TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION
. ----     ----   ------   --------  ----------   -----------
.DPTXAADM   DIM       8        8           1       Admission number
.PTXAADAT   DIM       8        8           9       Admission date (CCYYMMDD)
.PTXADDAT   DIM       8        8          17       Discharge date (CCYYMMDD)
.PTXADAYC   FORM      1        2          25       Day case indicator
.                                                   1 = Daycase
.                                                   0 = Other
.PTXAPLOS   FORM      4        3          27       Length of stay during period
.                                                   - Zero for daycases
.PTXAALOS   FORM      4        3          30       Length of stay since admiss.
.                                                   - Zero for daycases
.PTXAPLVE   FORM      4        3          33       On-leave days during period
.PTXAALVE   FORM      4        3          36       On-leave days since admission
.PTXAAWRD   DIM       3        3          39       Ward at admission
.PTXAABED   DIM       3        3          42       Bed at admission
.PTXASWRD   DIM       3        3          45       Ward at start of period
.PTXASBED   DIM       3        3          48       Bed at start of period
.PTXADWRD   DIM       3        3          51       Ward at end of period
.PTXADBED   DIM       3        3          54       Bed at end of period
.PTXAATYP   DIM       3        3          57       Admission type at admission
.                                                   - (Cat A)
.PTXASTYP   DIM       3        3          60       Admission type at start of
.                                                 period - (Cat A)
.PTXADTYP   DIM       3        3          63       Admission type at end of
.                                                 period - (Cat A)
.PTXASEX    DIM       1        1          66       Patients sex  (M or F)
.PTXAPOST   DIM       8        8          67       Postcode
.PTXAMLGA   DIM       4        4          75       L.G.A code from PMI
.PTXAPLGA   DIM       4        4          79       L.G.A code from postcode
.PTXARELG   DIM       3        3          83       Religion - (Cat R)
.PTXACONT   DIM       3        3          86       Country of birth - (Cat C)
.PTXARESI   DIM       3        3          89       Resident status - (Cat T)
.PTXAMSTT   DIM       3        3          92       Marital status - (Cat M)
.PTXAAAGD   FORM      5        4          95       Patients age at admission
.                                                   - in days
.PTXAAAGM   FORM      4        3          99       Patients age at admission
.                                                   - in months
.PTXAAAGY   FORM      3        3         102       Patients age at admission
.                                                   - in years
.PTXASAGD   FORM      5        4         105       Patients age at start of
.                                                 period - in days
.PTXASAGM   FORM      4        3         109       Patients age at start of
.                                                 period - in months
.PTXASAGY   FORM      3        3         112       Patients age at start of
.                                                 period - in years
.PTXADAGD   FORM      5        4         115       Patients age at end of period
.                                                   - in days
.PTXADAGM   FORM      4        3         119       Patients age at end of period
.                                                   - in months
.PTXADAGY   FORM      3        3         122       Patients age at end of period
.                                                   - in years
.PTXAPUS1   DIM       3        3         125       PMI user defined field 1
.                                                   - (Cat P1)
.PTXAPUS2   DIM       3        3         128       PMI user defined field 2
.                                                   - (Cat P2)
.PTXAPUS3   DIM       3        3         131       PMI user defined field 3
.                                                   - (Cat P3)
.PTXAPUS4   DIM       3        3         134       PMI user defined field 4
.                                                   - (Cat P4)
.PTXAPUS5   DIM       3        3         137       PMI user defined field 5
.                                                   - (Cat P5)
.PTXAPUS6   DIM       3        3         140       PMI user defined field 6
.                                                   - (Cat P6)
.PTXAPYN1   DIM       1        1         143       Using PMI user defined field
.                                                 1 - (Y or N)
.PTXAPYN2   DIM       1        1         144       Using PMI user defined field
.                                                 2 - (Y or N)
.PTXAPYN3   DIM       1        1         145       Using PMI user defined field
.                                                 3 - (Y or N)
.PTXADCSE   DIM       1        1         146       Deceased during period
.                                                   - (Y or N)
.PTXADDTH   DIM       8        8         147       Date of death (CCYYMMDD)
.PTXABADD   DIM       1        1         155       Bad Debt ? (Y or N)
.PTXAAADC   DIM       6        6         156       Attending doctor at admission
.PTXAAADT   DIM       3        3         162       Attending doc. type at
.                                                 admission - (Cat DT)
.PTXASADC   DIM       6        6         165       Attending doctor at start of
.                                                 period
.PTXASADT   DIM       3        3         171       Attending doc. type at start
.                                                 of period - (Cat DT)
.PTXADADC   DIM       6        6         174       Attending doctor at end of
.                                                 period
.PTXADADT   DIM       3        3         180       Attending doc. type at end
.                                                 of period - (Cat DT)
.PTXAARDC   DIM       6        6         183       Referring doctor
.PTXAARDT   DIM       3        3         189       Referring doc. type (Cat DT)
.PTXAATDC   DIM       6        6         192       Associated doctor
.PTXAATDT   DIM       6        6         198       Associated doc. type (Cat DT)
.PTXASOUR   DIM       3        3         204       Source of referral - (Cat S)
.PTXAAHOS   DIM       5        5         207       Admission transfer source
.PTXACLMC   DIM       3        3         212       Claim code - (Cat CL)
.PTXADIET   DIM       3        3         215       Diet code - (Cat DC)
.PTXAACLS   DIM       3        3         218       Admission class - (Cat P)
.PTXAACRE   DIM       3        3         221       Care class at admission
.                                                   - (Cat CC)
.PTXASCRE   DIM       3        3         224       Care class at start of the
.                                                 period - (Cat CC)
.PTXADCRE   DIM       3        3         227       Care class at end of the
.                                                 period - (Cat CC)
.PTXAADPT   DIM       3        3         230       Account classification at
.                                                 time of admission - (Cat AC)
.PTXASDPT   DIM       3        3         233       Account classification at
.                                                 start of period - (Cat AC)
.PTXADDPT   DIM       3        3         236       Account classification at end
.                                                 of period - (Cat AC)
.PTXAFUND   DIM       6        6         239       Health fund code
.PTXAFTBL   DIM       8        8         245       Health fund table code
.PTXAAUS1   DIM       3        3         253       Admission user defined field
.                                                 1 - (Cat U1)
.PTXAAUS2   DIM       3        3         256       Admission user defined field
.                                                 2 - (Cat U2)
.PTXAAUS3   DIM       3        3         259       Admission user defined field
.                                                 3 - (Cat U3)
.PTXAAUS4   DIM       3        3         262       Admission user defined field
.                                                 4 - (Cat U4)
.PTXAAUS5   DIM       3        3         265       Admission user defined field
.                                                 5 - (Cat U5)
.PTXAAUS6   DIM       3        3         268       Admission user defined field
.                                                 6 - (Cat U6)
.PTXAAUS7   DIM       3        3         271       Admission user defined field
.                                                 7 - (Cat U7)
.PTXAADIA   DIM       9        9         274       Admitting diagnosis code
.PTXADDIA   DIM       9        9         283       Discharge diagnosis code
.PTXADSTT   DIM       3        3         292       Discharge status - (Cat D)
.PTXADDST   DIM       3        3         295       Discharge dest. - (Cat DD)
.PTXADHOS   DIM       5        5         299       Discharge transfer source
.PTXADUS1   DIM       3        3         303       Discharge user defined field
.                                                 1 - (Cat D1)
.PTXADUS2   DIM       3        3         306       Discharge user defined field
.                                                 2 - (Cat D2)
.PTXADUS3   DIM       3        3         309       Discharge user defined field
.                                                 3 - (Cat D3)
.PTXADUS4   DIM       3        3         312       Discharge user defined field
.                                                 4 - (Cat D4)
.PTXADUS5   DIM       3        3         315       Discharge user defined field
.                                                 5 - (Cat D5)
.PTXADRGC   DIM       6        6         319       DRG Code
.PTXAMDCC   DIM       5        5         324       MDC Code
OLDASPRE   DIM      25       25         329       Spare variable
.
.End of Record                          354
.
.
.                                     ******** copy the new FD here *******
.Data File Definition
.--------------------
PATEX1A1  IFILE     SQL, FIXED=400, KEY="U1-8"
.
FILEXAA1  INIT      "patexa"
FILEX1A1  INIT      "patex1"
.
. NAME     TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION
. ----     ----   ------   --------  ----------   -----------
DPTXAADM   DIM       8        8           1       Admission number
PTXAADAT   DIM       8        8           9       Admission date (CCYYMMDD)
PTXADDAT   DIM       8        8          17       Discharge date (CCYYMMDD)
PTXADAYC   FORM      1        2          25       Day case indicator
.                                                   1 = Daycase
.                                                   0 = Other
PTXAPLOS   FORM      4        3          27       Length of stay during period
.                                                   - Zero for daycases
PTXAALOS   FORM      4        3          30       Length of stay since admiss.
.                                                   - Zero for daycases
PTXAPLVE   FORM      4        3          33       On-leave days during period
PTXAALVE   FORM      4        3          36       On-leave days since admission
PTXAAWRD   DIM       3        3          39       Ward at admission
PTXAABED   DIM       3        3          42       Bed at admission
PTXASWRD   DIM       3        3          45       Ward at start of period
PTXASBED   DIM       3        3          48       Bed at start of period
PTXADWRD   DIM       3        3          51       Ward at end of period
PTXADBED   DIM       3        3          54       Bed at end of period
PTXAATYP   DIM       3        3          57       Admission type at admission
.                                                   - (Cat A)
PTXASTYP   DIM       3        3          60       Admission type at start of
.                                                 period - (Cat A)
PTXADTYP   DIM       3        3          63       Admission type at end of
.                                                 period - (Cat A)
PTXASEX    DIM       1        1          66       Patients sex  (M or F)
PTXAPOST   DIM       8        8          67       Postcode
PTXAMLGA   DIM       4        4          75       L.G.A code from PMI
PTXAPLGA   DIM       4        4          79       L.G.A code from postcode
PTXARELG   DIM       3        3          83       Religion - (Cat R)
PTXACONT   DIM       3        3          86       Country of birth - (Cat C)
PTXARESI   DIM       3        3          89       Resident status - (Cat T)
PTXAMSTT   DIM       3        3          92       Marital status - (Cat M)
PTXAAAGD   FORM      5        4          95       Patients age at admission
.                                                   - in days
PTXAAAGM   FORM      4        3          99       Patients age at admission
.                                                   - in months
PTXAAAGY   FORM      3        3         102       Patients age at admission
.                                                   - in years
PTXASAGD   FORM      5        4         105       Patients age at start of
.                                                 period - in days
PTXASAGM   FORM      4        3         109       Patients age at start of
.                                                 period - in months
PTXASAGY   FORM      3        3         112       Patients age at start of
.                                                 period - in years
PTXADAGD   FORM      5        4         115       Patients age at end of period
.                                                   - in days
PTXADAGM   FORM      4        3         119       Patients age at end of period
.                                                   - in months
PTXADAGY   FORM      3        3         122       Patients age at end of period
.                                                   - in years
PTXAPUS1   DIM       3        3         125       PMI user defined field 1
.                                                   - (Cat P1)
PTXAPUS2   DIM       3        3         128       PMI user defined field 2
.                                                   - (Cat P2)
PTXAPUS3   DIM       3        3         131       PMI user defined field 3
.                                                   - (Cat P3)
PTXAPUS4   DIM       3        3         134       PMI user defined field 4
.                                                   - (Cat P4)
PTXAPUS5   DIM       3        3         137       PMI user defined field 5
.                                                   - (Cat P5)
PTXAPUS6   DIM       3        3         140       PMI user defined field 6
.                                                   - (Cat P6)
PTXAPYN1   DIM       1        1         143       Using PMI user defined field
.                                                 1 - (Y or N)
PTXAPYN2   DIM       1        1         144       Using PMI user defined field
.                                                 2 - (Y or N)
PTXAPYN3   DIM       1        1         145       Using PMI user defined field
.                                                 3 - (Y or N)
PTXADCSE   DIM       1        1         146       Deceased during period
.                                                   - (Y or N)
PTXADDTH   DIM       8        8         147       Date of death (CCYYMMDD)
PTXABADD   DIM       1        1         155       Bad Debt ? (Y or N)
PTXAAADC   DIM       6        6         156       Attending doctor at admission
PTXAAADT   DIM       3        3         162       Attending doc. type at
.                                                 admission - (Cat DT)
PTXASADC   DIM       6        6         165       Attending doctor at start of
.                                                 period
PTXASADT   DIM       3        3         171       Attending doc. type at start
.                                                 of period - (Cat DT)
PTXADADC   DIM       6        6         174       Attending doctor at end of
.                                                 period
PTXADADT   DIM       3        3         180       Attending doc. type at end
.                                                 of period - (Cat DT)
PTXAARDC   DIM       6        6         183       Referring doctor
PTXAARDT   DIM       3        3         189       Referring doc. type (Cat DT)
PTXAATDC   DIM       6        6         192       Associated doctor
PTXAATDT   DIM       6        6         198       Associated doc. type (Cat DT)
PTXASOUR   DIM       3        3         204       Source of referral - (Cat S)
PTXAAHOS   DIM       5        5         207       Admission transfer source
PTXACLMC   DIM       3        3         212       Claim code - (Cat CL)
PTXADIET   DIM       3        3         215       Diet code - (Cat DC)
PTXAACLS   DIM       3        3         218       Admission class - (Cat P)
PTXAACRE   DIM       3        3         221       Care class at admission
.                                                   - (Cat CC)
PTXASCRE   DIM       3        3         224       Care class at start of the
.                                                 period - (Cat CC)
PTXADCRE   DIM       3        3         227       Care class at end of the
.                                                 period - (Cat CC)
PTXAADPT   DIM       3        3         230       Account classification at
.                                                 time of admission - (Cat AC)
PTXASDPT   DIM       3        3         233       Account classification at
.                                                 start of period - (Cat AC)
PTXADDPT   DIM       3        3         236       Account classification at end
.                                                 of period - (Cat AC)
PTXAFUND   DIM       6        6         239       Health fund code
PTXAFTBL   DIM       8        8         245       Health fund table code
PTXAAUS1   DIM       3        3         253       Admission user defined field
.                                                 1 - (Cat U1)
PTXAAUS2   DIM       3        3         256       Admission user defined field
.                                                 2 - (Cat U2)
PTXAAUS3   DIM       3        3         259       Admission user defined field
.                                                 3 - (Cat U3)
PTXAAUS4   DIM       3        3         262       Admission user defined field
.                                                 4 - (Cat U4)
PTXAAUS5   DIM       3        3         265       Admission user defined field
.                                                 5 - (Cat U5)
PTXAAUS6   DIM       3        3         268       Admission user defined field
.                                                 6 - (Cat U6)
PTXAAUS7   DIM       3        3         271       Admission user defined field
.                                                 7 - (Cat U7)
PTXAADIA   DIM       9        9         274       Admitting diagnosis code
PTXADDIA   DIM       9        9         283       Discharge diagnosis code
PTXADSTT   DIM       3        3         292       Discharge status - (Cat D)
PTXADDST   DIM       3        3         295       Discharge dest. - (Cat DD)
PTXADHOS   DIM       5        5         299       Discharge transfer source
PTXADUS1   DIM       3        3         303       Discharge user defined field
.                                                 1 - (Cat D1)
PTXADUS2   DIM       3        3         306       Discharge user defined field
.                                                 2 - (Cat D2)
PTXADUS3   DIM       3        3         309       Discharge user defined field
.                                                 3 - (Cat D3)
PTXADUS4   DIM       3        3         312       Discharge user defined field
.                                                 4 - (Cat D4)
PTXADUS5   DIM       3        3         315       Discharge user defined field
.                                                 5 - (Cat D5)
PTXADRGC   DIM       6        6         319       DRG Code
PTXAMDCC   DIM       5        5         324       MDC Code
.                                                 *** added in V10.00 *I-157391
PTXAOPID   DIM      10       10         329       Operator
PTXARLOC   DIM       3        3         339       Record Location     ? (Cat MV)
PTXAABRG   DIM       3        3         341       Aboriginality PRS/2   (Cat VA)
PTXACADM   DIM       3        3         344       Admissn. Criterion    (Cat VB)
PTXAIRAD   DIM       3        3         347       Intent to Readmit     (Cat VR)
PTXAIDUS   DIM       3        3         351       Intend Stay Duration  (Cat VI)
PTXAMHLS   DIM       3        3         354       Mental Health L.Stat  (Cat LS)
PTXAASUT   DIM       3        3         357       Admiss.Serv. Unit Type(Cat KA)
PTXAELPS   DIM       3        3         360       Elective Patient Statu(Cat KC)
PTXAEMPL   DIM       3        3         363       Employment Status     (Cat KD)
PTXAREAD   DIM       3        3         366       Re-Admissn in 28 Days (Cat KM)
PTXAUDIN   DIM       3        3         369       Discharge Intention   (Cat KN)
PTXAUREA   DIM       3        3         372       Unplanned Readmission (Cat KO)
PTXAUVTH   DIM       3        3         375       Unplan.Visit to Theatr(Cat KB)
PTXASPRE   DIM       22       22        378
.
.End of Record                          400
.
.  redefine form fields from key
.  -----------------------------
.NAME       TYPE     LENGTH     START     DESCRIPTION
.----       ----     ------     -----     -----------
PTXAADMN    FORM       8          1       Admission number
.
.
CMDLINE   DIM       100
DIM4      DIM       4
DIM4A     DIM       4
DIM6      DIM       6
DIM6A     DIM       6
DIM60     DIM       60
DDCENT    DIM       2
DISCOUNT  FORM      2
.
FORM5     FORM      5
FILCOUNT  FORM      2
.
INPFILE   DIM       8
.
RECNUM    FORM      8
NEWFILE   DIM       60
NEWPATH   DIM       60
OLDPATH   DIM       60
DEFPATH   DIM       60
SAVEFLG   FORM      1
SP50      INIT    "                                                  "
SP60      INIT    "                                                            "
.
PRGNAM    INIT      "CONVERSION PATEX1FD"
PRGID     INIT      "F00PTEX1"
VERSION   INIT      "V12.00.00"
.
.********************************************************************
. Start of Program
.********************************************************************
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
MAIN1000  CALL      MENU0000
          BRANCH    EXIT,MAIN9999
.
          CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN2000,MAIN0000,MAIN9999 * Yes, no, cancel
.
MAIN2000  BRANCH    OPTION,MAIN5000
.                                           * Option 2
          DISPLAY   *P1:18,*EF,"Processing -";
          MOVE      "2",DISCOUNT
          MOVE      ZERO,FILCOUNT           * loop through all files
MAIN4000  ADD       ONE,FILCOUNT
          IF        FILCOUNT > 16
            DISPLAY   *P1:23,*EF,"File conversion complete"
            CALL      HITENTER
            GOTO      MAIN1000
          ENDIF
          MOVE      FILCOUNT,KEY2
          REP       " 0",KEY2
.
          CALL      CHEK0000                * check file validity
          BRANCH    EXIT,MAIN4000
.
          DISPLAY   *PDISCOUNT:19,KEY2;
          ADD       THREE,DISCOUNT
.
.
MAIN5000  MOVE      ZERO,EXIT
          CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
. Loop thru old file/s and write records to new file
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/compress saved file
          BRANCH    OPTION,MAIN1000,MAIN4000
          GOTO      MAIN1000
.
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,EXIT
          MOVE      ZERO,RECNUM
          PACK      DIM60,SP70
.
          MOVE      "02",KEY2               * initialize INPFILE for Option 2
          PACK      INPFILE,FILEX1A1,KEY2,SP10
.
INIT9999  RETURN
.
.********************************************************************
.*                          MENU0000                                *
.*  Select OPTION                                                   *
.********************************************************************
MENU0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:4,*EF," 0. Exit":
                    *P1:5," 1. Convert a single files":
                    *P1:6," 2. Convert all files":
                    *P1:8," Select Option : ";
          KEYIN     *P18:8,OPTION;
          DISPLAY   *P18:8,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      MENU9000 IF EQUAL
          COMPARE   TWO,OPTION
          GOTO      MENU9999 IF EQUAL
          COMPARE   ONE,OPTION
          GOTO      MENU2000 IF EQUAL
.
          BEEP
          GOTO      MENU0000
.
MENU2000  MOVE      "  ",KEY2
          KEYIN     *P1:10,*EF,"Keyin the required File number : ",KEY2;
          MATCH     "  ",KEY2
          IF        @EQUAL | @EOS
            GOTO      MENU0000
          ENDIF
          SQUEEZE   KEY2
          MOVE      ZERO,FORM2
          MOVE      KEY2,FORM2
          IF        FORM2 < 1 | FORM2 > 20
            DISPLAY   *P1:24,"Invalid file number - should be '01' to '20' - ";
            CALL      HITENTER
            GOTO      MENU2000
          ENDIF
.
          MOVE      FORM2,KEY2
          REP       " 0",KEY2
          CALL      CHEK0000                * validate the file 
          BRANCH    EXIT,MENU2000
          GOTO      MENU9999
.
.
MENU9000  MOVE      ONE,EXIT
.
MENU9999  RETURN
.
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
CHEK0000  MOVE      ZERO,EXIT  
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          PACK      INPFILE,FILEX1A1,KEY2,SP10
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1 
          GOTO      CHEK9999
.         
CHEK2000  SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File number '",KEY2,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
          ELSE      
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
            ELSE
              DISPLAY   "has caused an IO error - ";
            ENDIF
          ENDIF
          DISPLAY   "File will be bypassed"
.
          IF        FORM2 < 17
            CALL      HITENTER
          ENDIF
          DISPLAY   *P1:23,*EF
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
.
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
PREP0000 
. open old file
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PACK      INPFILE,FILEX1A1,KEY2,SP10
          OPEN      OLDFILE1,INPFILE            * "patex1" + "nn"
          TRAPCLR   IO
          BRANCH    OVRCD,PREP5000       * Original file does not exist
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
. Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat ",CMDLINE
          APPEND    INPFILE,CMDLINE
          APPEND    ".dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sptex1",CMDLINE
          APPEND    KEY2,CMDLINE
          APPEND    ".dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat ",CMDLINE
          APPEND    INPFILE,CMDLINE
          APPEND    ".idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sptex1",CMDLINE
          APPEND    KEY2,CMDLINE
          APPEND    ".idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
. remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    INPFILE,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Opening old file after being copied
.
PREP0800  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "sptex1",DIM60
          APPEND    KEY2,DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    INPFILE,NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.      eg    APPEND    " 195 UC9-16",CMDLINE
.
          APPEND    " 400 UC1-8",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has 
.  to be repeated with for each individual index
.
..         PACK      CMDLINE,SP60,SP60
..         CLEAR     CMDLINE
..         APPEND    "isadd ",CMDLINE
..         APPEND    NEWFILE,CMDLINE
..         APPEND    " UCxx-xx,x-xx",CMDLINE
..         APPEND    SP60,CMDLINE
..         RESET     CMDLINE
..         EXECUTE   CMDLINE,TASKID
.
          OPEN      PATEX1A1,NEWFILE
.
. set the flag to say we want to continue
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
...       DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
...                 *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
PREP9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
READ0000  DISPLAY   *P1:21,*EL,"Record No. : ";
.
          UNPACK    SP70,PTXAOPID,PTXARLOC,PTXAABRG,PTXACADM,PTXAIRAD
          UNPACK    SP70,PTXAIDUS,PTXAMHLS,PTXAASUT,PTXAELPS,PTXAREAD
          UNPACK    SP70,PTXAUDIN,PTXAUREA,PTXAUVTH,PTXASPRE
.
          PACK      KEY8,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
.  All necessary changes to the files field lengths, date changes
.  or any other sort of modification must be made here
.
.    eg    PACK      DSPARE,SP50
.          PACK      PTDSDMDC,SP10
.          PACK      PTDSDDRG,SP10
.                                               * Pack key here
          PACK      KEY8,PTXAADMN,SP70
          CALL      WRPTEXA1                    * write to new file
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:22,*V2LON,RECNUM
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     PATEX1A1                      * close new file
          CLOSE     OLDFILE1                      * close old file
.
...       CALL      IBACLOCK
...       REP       " 0",CDATE
...       DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
READ9999  RETURN
.
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
VALD0000  MOVE      ZERO,EXIT
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or compress save file                                *
.**********************************************************************
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "sptex1",DIM60
          APPEND    KEY2,DIM60
          APPEND    "*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "compress -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not compressed or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
...       CALL      IBACLOCK
...       DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
...       CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.         Get the default directory
.**********************************************************************
DEFT0000  CLEAR     CMDLINE
          APPEND    "ldat ",CMDLINE
          APPEND    INPFILE,CMDLINE
          APPEND    ".dat > temp.txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
........  EXECUTE   "ldat patex1xx.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding 'patex1--'.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
DEFT9999  RETURN
+
.
.                  ******************************    OLD IO ROUTINES
.                  Copy the Positional Read (RS or RDS) from original I/O file,
.                  - change Label to READSOLD and the index name to OLDFILE1
.
.READSOLD  MOVE      ZERO,OVRCD
.          RESET     KEY6
.          PACK      KEY8,SP2,KEY6
.          READ      OLDFILE1,KEY8;;
.          RETURN
READSOLD  MOVE      ZERO,OVRCD
          RESET     KEY8
          READ      OLDFILE1,KEY8;;
          RETURN
.
.                  Copy the Read Next (RK or RDK) from the original I/O file
.                  - change Label to READKOLD and the index name to OLDFILE1
.                  - change any fields that are changing to a prefix of OLD,
.                    eg. PTEDSPAR becomes OLDDSPAR
.
.READKOLD  MOVE      ZERO,OVRCD
.          READKS    OLDFILE1;DDURNO,DDADMNO:
.                             DIM6,DTIME,DSTAT,DDEST:
.                             PTDSVOGU,DSPARE
.          GOTO      OVERCOND IF OVER
.          MOVE      QDURNO,DURNO
.          RETURN
READKOLD  MOVE      ZERO,OVRCD
          RESET     KEY8
          READKS    OLDFILE1;DPTXAADM,PTXAADAT,PTXADDAT,PTXADAYC,PTXAPLOS:
                             PTXAALOS,PTXAPLVE,PTXAALVE,PTXAAWRD,PTXAABED:
                             PTXASWRD,PTXASBED,PTXADWRD,PTXADBED,PTXAATYP:
                             PTXASTYP,PTXADTYP,PTXASEX,PTXAPOST,PTXAMLGA:
                             PTXAPLGA,PTXARELG,PTXACONT,PTXARESI,PTXAMSTT:
                             PTXAAAGD,PTXAAAGM,PTXAAAGY,PTXASAGD,PTXASAGM:
                             PTXASAGY,PTXADAGD,PTXADAGM,PTXADAGY,PTXAPUS1:
                             PTXAPUS2,PTXAPUS3,PTXAPUS4,PTXAPUS5,PTXAPUS6:
                             PTXAPYN1,PTXAPYN2,PTXAPYN3,PTXADCSE,PTXADDTH:
                             PTXABADD,PTXAAADC,PTXAAADT,PTXASADC,PTXASADT:
                             PTXADADC,PTXADADT,PTXAARDC,PTXAARDT,PTXAATDC:
                             PTXAATDT,PTXASOUR,PTXAAHOS,PTXACLMC,PTXADIET:
                             PTXAACLS,PTXAACRE,PTXASCRE,PTXADCRE,PTXAADPT:
                             PTXASDPT,PTXADDPT,PTXAFUND,PTXAFTBL,PTXAAUS1:
                             PTXAAUS2,PTXAAUS3,PTXAAUS4,PTXAAUS5,PTXAAUS6:
                             PTXAAUS7,PTXAADIA,PTXADDIA,PTXADSTT,PTXADDST:
                             PTXADHOS,PTXADUS1,PTXADUS2,PTXADUS3,PTXADUS4:
                             PTXADUS5,PTXADRGC,PTXAMDCC,OLDASPRE
          GOTO      OVERCOND IF OVER
          MOVE      DPTXAADM,PTXAADMN
          RETURN
.
.                  ******************************    NEW IO ROUTINES
.                  Copy the Write statement from the new I/O file           
.                  (no changes are needed to this routine)               
WRPTEXA1  MOVE      ZERO,OVRCD
          RESET     KEY8
          WRITE     PATEX1A1,KEY8;KEY8,PTXAADAT,PTXADDAT,PTXADAYC,PTXAPLOS:
                                  PTXAALOS,PTXAPLVE,PTXAALVE,PTXAAWRD,PTXAABED:
                                  PTXASWRD,PTXASBED,PTXADWRD,PTXADBED,PTXAATYP:
                                  PTXASTYP,PTXADTYP,PTXASEX,PTXAPOST,PTXAMLGA:
                                  PTXAPLGA,PTXARELG,PTXACONT,PTXARESI,PTXAMSTT:
                                  PTXAAAGD,PTXAAAGM,PTXAAAGY,PTXASAGD,PTXASAGM:
                                  PTXASAGY,PTXADAGD,PTXADAGM,PTXADAGY,PTXAPUS1:
                                  PTXAPUS2,PTXAPUS3,PTXAPUS4,PTXAPUS5,PTXAPUS6:
                                  PTXAPYN1,PTXAPYN2,PTXAPYN3,PTXADCSE,PTXADDTH:
                                  PTXABADD,PTXAAADC,PTXAAADT,PTXASADC,PTXASADT:
                                  PTXADADC,PTXADADT,PTXAARDC,PTXAARDT,PTXAATDC:
                                  PTXAATDT,PTXASOUR,PTXAAHOS,PTXACLMC,PTXADIET:
                                  PTXAACLS,PTXAACRE,PTXASCRE,PTXADCRE,PTXAADPT:
                                  PTXASDPT,PTXADDPT,PTXAFUND,PTXAFTBL,PTXAAUS1:
                                  PTXAAUS2,PTXAAUS3,PTXAAUS4,PTXAAUS5,PTXAAUS6:
                                  PTXAAUS7,PTXAADIA,PTXADDIA,PTXADSTT,PTXADDST:
                                  PTXADHOS,PTXADUS1,PTXADUS2,PTXADUS3,PTXADUS4:
                                  PTXADUS5,PTXADRGC,PTXAMDCC,PTXAOPID,PTXARLOC:
                                  PTXAABRG,PTXACADM,PTXAIRAD,PTXAIDUS,PTXAMHLS:
                                  PTXAASUT,PTXAELPS,PTXAEMPL,PTXAREAD,PTXAUDIN:
                                  PTXAUREA,PTXAUVTH,PTXASPRE
          RETURN
.
          INC       STD001IO
