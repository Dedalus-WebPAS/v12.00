KCAFFM00  MOVE      ZERO,CKEYTYP
          MOVE      FMLALEDG,FMCALED
          CALL      KFMCAA00
          RETURN
.---------------------------------------------------------------------------
.   System    :   Financial Management System        
.   Include   :   FMSCAFKY
.   Routine   :   HFMCAA00 Control Account     
.   Return    :           
.-------------------------------------------------------------------------------
HFMCAA00
          MOVE      ZERO,CSCRSIZ
          IF        HTOP<3
            MOVE      "14",HTOP
            MOVE      "24",HBOT
            MOVE      "10",HLEF
            MOVE      "80",HRIG
          ENDIF
HFMCAA04
          CALL      GSCRN000
          BRANCH    EXIT,HFMCAA99
          MOVE      STDCOL00,STDCOL01
          ADD       " 5",STDCOL01
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON,"Account"            
.
          MOVE      STDCOL01,STDCOL02
          ADD       "13",STDCOL02
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"Description"        
.
          MOVE      STDCOL02,STDCOL03
          ADD       "36",STDCOL03
          DISPLAY   *PSTDCOL03:STDROW,*V2LON,*ULON,"Control Type"       
.
          MOVE      STDCOL03,STDCOL04
          ADD       "13",STDCOL04
HFMCAA08
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          MOVE      SP70,FMCAACC 
          PACK      KEY14,FMCALED,FMCAACC,SP70                                  
          PACK      SAVKEY80,FMCALED,FMCAACC,SP70                               
          SETLPTR   SAVKEY80,2                                                  
          CALL      RSFMCA1 
HFMCAA10
          CALL      RKFMCA1 
          BRANCH    OVRCD,HFMCAA15
          CALL      GCAFFM00 
          CALL      CKCAF000
          BRANCH    OVRCD,HFMCAA10
          PACK      KEY14,FMCALED,FMCAACC,SP70                                  
          CALL      RDFMAM1
          MATCH     SAVKEY80,KEY14                                              
          GOTO      HFMCAA15 IF NOT EQUAL                                       
          BRANCH    CSEARCH,HFMCAA12
          CALL      HFMCAA60
          BRANCH    OVRCD,HFMCAA10
HFMCAA12
          ADD       ONE,XITEMNO
          ADD       ONE,STDROW
          STORE     KEY14,XITEMNO,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,XITEMNO,DOT
          DISPLAY   *PSTDCOL01:STDROW,FMCAACC 
          DISPLAY   *PSTDCOL02:STDROW,FMAMDESC
          DISPLAY   *PSTDCOL03:STDROW,KEY20   
          COMPARE   MAXITEM,XITEMNO
          GOTO      HFMCAA10 IF NOT EQUAL
HFMCAA15
          UNPACK    SAVKEY80,FMCALED,FMCAACC                                    
          MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
HFMCAA20
          CALL      HSCRI000
          BRANCH    EXIT,HFMCAA30,HFMCAA35,HFMCAA40,HFMCAA45,HFMCAA04:
                              HFMCAA91,HFMCAA50
          GOTO      HFMCAA92
HFMCAA30
          CALL      RKFMCA1 
          BRANCH    OVRCD,HFMCAA85
          PACK      KEY14,FMCALED,FMCAACC,SP70                                  
          MATCH     SAVKEY80,KEY14                                              
          GOTO      HFMCAA85 IF NOT EQUAL                                       
          CALL      CKCAF000
          BRANCH    OVRCD,HFMCAA35
          CALL      RPFMCA1 
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HFMCAA10
HFMCAA35
          MOVE      ST1,KEY14
          CALL      RDFMCA1 
          CALL      RPFMCA1 
          BRANCH    OVRCD,HFMCAA85
          PACK      KEY14,FMCALED,FMCAACC,SP70                                  
          MATCH     SAVKEY80,KEY14                                              
          GOTO      HFMCAA85 IF NOT EQUAL                                       
          MATCH     SP70,FMCAACC                                                
          GOTO      HFMCAA85 IF EQUAL                                           
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
HFMCAA36
          CALL      RPFMCA1 
          BRANCH    OVRCD,HFMCAA37
          PACK      KEY14,FMCALED,FMCAACC,SP70                                  
          MATCH     SAVKEY80,KEY14                                              
          GOTO      HFMCAA37 IF NOT EQUAL                                       
          CALL      CKCAF000
          BRANCH    OVRCD,HFMCAA36
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HFMCAA36 IF NOT EQUAL
HFMCAA37
          MOVE      ZERO,XITEMNO
          GOTO      HFMCAA10
HFMCAA40
          MOVE      ST1,KEY14
          CALL      RDFMCA1 
          CALL      RPFMCA1 
          BRANCH    OVRCD,HFMCAA85
          PACK      KEY14,FMCALED,FMCAACC,SP70                                  
          MATCH     SAVKEY80,KEY14                                              
          GOTO      HFMCAA85 IF NOT EQUAL                                       
          CALL      CKCAF000
          BRANCH    OVRCD,HFMCAA40
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HFMCAA08
HFMCAA45
          CALL      RKFMCA1 
          BRANCH    OVRCD,HFMCAA85
          PACK      KEY14,FMCALED,FMCAACC,SP70                                  
          MATCH     SAVKEY80,KEY14                                              
          GOTO      HFMCAA85 IF NOT EQUAL                                       
          SUSPEND
          CLIP      1,1,CRIG,24
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,XITEMNO
          PACK      KEY14,SAVKEY80,Z70                                          
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
          CALL      RSFMCA1 
HFMCAA46
          CALL      RPFMCA1 
          BRANCH    OVRCD,HFMCAA47
          PACK      KEY14,FMCALED,FMCAACC,SP70                                  
          MATCH     SAVKEY80,KEY14                                              
          GOTO      HFMCAA47 IF NOT EQUAL                                       
          CALL      CKCAF000
          BRANCH    OVRCD,HFMCAA46
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HFMCAA46 IF NOT EQUAL
HFMCAA47
          MOVE      ZERO,XITEMNO
          GOTO      HFMCAA10
HFMCAA50  COMPARE   ZERO,CSEARCH
          GOTO      HFMCAA55 IF EQUAL
          MOVE      ZERO,CSEARCH
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          UNPACK    SP70,SCHFLD01,SCHFLD02,SCHFLD03,SCHFLD04,SCHFLD05
          UNPACK    SP70,SCHFLD06,SCHFLD07,SCHFLD08,SCHFLD09,SCHFLD10
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          DISPLAY   *PSTDCOL00:STDROW,*V2LON,*HON,"Sch"
          KEYIN     *PSTDCOL01:STDROW,*V2LON,SCHFLD01
          RESET     SCHFLD01
          KEYIN     *PSTDCOL02:STDROW,*V2LON,SCHFLD02
          RESET     SCHFLD02
          KEYIN     *PSTDCOL03:STDROW,*V2LON,SCHFLD03
          RESET     SCHFLD03
          PACK      KEY8,SP70
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HFMCAA08
HFMCAA55  MOVE      ONE,CSEARCH
          PACK      KEY8,SP70
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HFMCAA08
HFMCAA60  MOVE      ZERO,OVRCD
          MATCH     SP70,SCHFLD01
          GOTO      HFMCAA61 IF EQUAL
          MATCH     SCHFLD01,FMCAACC 
          GOTO      HFMCAA72 IF NOT EQUAL
HFMCAA61
          MATCH     SP70,SCHFLD02
          GOTO      HFMCAA62 IF EQUAL
          MATCH     SCHFLD02,FMAMDESC
          GOTO      HFMCAA72 IF NOT EQUAL
HFMCAA62
          MATCH     SP70,SCHFLD03
          GOTO      HFMCAA63 IF EQUAL
          MATCH     SCHFLD03,KEY20   
          GOTO      HFMCAA72 IF NOT EQUAL
HFMCAA63
HFMCAA64
HFMCAA65
HFMCAA66
HFMCAA67
HFMCAA68
HFMCAA69
HFMCAA70
          GOTO      HFMCAA75
HFMCAA72  MOVE      ONE,OVRCD
HFMCAA75  RETURN
HFMCAA85
          LOAD      KEY14,XITEMNO,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY14,FMCALED,FMCAACC                                       
          CALL      RDFMCA1 
HFMCAA90
          UNPACK    SAVKEY80,FMCALED,FMCAACC                                    
          BEEP
          GOTO      HFMCAA20
HFMCAA91
          MOVE      ZERO,FMCATYP 
          MOVE      ZERO,FMCAJNL 
          MOVE      SP70,FMCACHQ 
          MOVE      SP70,FMCASPA 
          MOVE      SP70,KEY14
          MOVE      SP70,FMCAACC 
          GOTO      HFMCAA96
HFMCAA92
          COMPARE   ZERO,XITEMNO
          GOTO      HFMCAA91 IF EQUAL
          MOVE      SP70,KEY14
          MOVE      SP70,FMCAACC 
          LOAD      KEY14,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY14,FMCALED,FMCAACC                                       
HFMCAA96
          PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
HFMCAA99
          RETURN
.---------------------------------------------------------------------------
.   System    :   Financial Management System        
.   Include   :   FMSCAFKY
.   Routine   :   KFMCAA00 Keyin Control Account     
.   Parameter :   CKEYTYP 0 = Keyin , 1 = Accept, 2 = Either
.   Return    :   EXIT = 1 null entry, 2 EXITCHAR entered
.-------------------------------------------------------------------------------
KFMCAA00 TOPIC     "FMCAACC "
          MOVE      UNDLN70,FMCAACC 
          KEYIN     *PCCOL:CVERT,*DV,FMCAACC:                                   
                    *PCCOL:CVERT,*V2LON,FMCAACC:                                
                    *PCCOL:CVERT,*DV,FMCAACC 
.
          ENDSET    FMCAACC 
          APPEND    SP70,FMCAACC 
          RESET     FMCAACC 
.
          MATCH     SP70,FMCAACC 
          GOTO      KFMCAA90 IF EQUAL
.
          CMATCH    EXITCHAR,FMCAACC 
          GOTO      KFMCAA95 IF EQUAL
.
          CMATCH    QUEST,FMCAACC 
          GOTO      KFMCAA10 IF NOT EQUAL
.
          MOVE      CCITEM,SCCITEM
          CALL      HFMCAA00
          MOVE      SCCITEM,CCITEM
.
          ENDSET    FMCAACC 
          APPEND    SP70,FMCAACC 
          RESET     FMCAACC 
.
          MATCH     SP70,FMCAACC 
          GOTO      KFMCAA00 IF EQUAL
.
          DISPLAY   *PCCOL:CVERT,*V2LON,FMCAACC                                 
KFMCAA10
          PACK      KEY14,FMCALED,FMCAACC                                       
          CALL      RDFMCA1 
          BRANCH    OVRCD,KFMCAA80
          CALL      CKCAF000
          BRANCH    OVRCD,KFMCAA80
          CALL      RDFMAM1 
          BRANCH    OVRCD,KFMCAA85
          BRANCH    CKEYTYP,KFMCAA75
          CALL      RDFMCA1 
KFMCAA70
          MOVE      ZERO,EXIT
          GOTO      KFMCAA99
KFMCAA75
          MOVE      "Control Account Already Exist - ",DISPMSG                                       
          CALL      MESSAGE1
          GOTO      KFMCAA00
KFMCAA80
          MOVE      ZERO,EXIT
          BRANCH    CKEYTYP,KFMCAA99,KFMCAA99
          MOVE      "Not a Valid Control Account - ",DISPMSG       
          CALL      MESSAGE1
          GOTO      KFMCAA00
KFMCAA85
          MOVE      "Account Does Not Exist on Account Masterfile - ",DISPMSG
          CALL      MESSAGE1
          GOTO      KFMCAA00
KFMCAA90
          MOVE      ONE,EXIT
          GOTO      KFMCAA99
KFMCAA95
          MOVE      TWO,EXIT
KFMCAA99 TOPIC    PRGID
          RETURN
.**********************************************************************
. GCAFFM get type description
.        Requires - FMCATYP  (account type of record to be tested)
.        Returns  - KEY20    (description)
.**********************************************************************
GCAFFM00  MOVE      SP70,KEY20
          BRANCH    FMCATYP,GCAFFM10,GCAFFM20,GCAFFM30,GCAFFM40,GCAFFM50:
                            GCAFFM60,GCAFFM70,GCAFFM80
          GOTO      GCAFFM99                     * exit
GCAFFM10  MOVE      "Bank Control        ",KEY20
          GOTO      GCAFFM99
GCAFFM20  MOVE      "Creditors Control   ",KEY20
          GOTO      GCAFFM99
GCAFFM30  MOVE      "Discount Control    ",KEY20
          GOTO      GCAFFM99
GCAFFM40  MOVE      "Debtors Control     ",KEY20
          GOTO      GCAFFM99
GCAFFM50  MOVE      "Other Control       ",KEY20
          GOTO      GCAFFM99
GCAFFM60  MOVE      "Payments in Suspense",KEY20
          GOTO      GCAFFM99
GCAFFM70  MOVE      "Cash G.S.T Control  ",KEY20
          GOTO      GCAFFM99
GCAFFM80  MOVE      "Accrual G.S.T. Cont.",KEY20
          GOTO      GCAFFM99
GCAFFM99  RETURN
.**********************************************************************
. CKCAF - Check if correct subjective indicator
.        Requires - CCONTIND 0=all accounts, 1=bank, 2=creditor, 3=discount
.                          4=debtors, 5=other, 6=Payments in Suspense, 
.                          7=Cash GST, 8=Accrual GST
.                   FMCATYP  (account type of record to be tested)
.        Returns  - OVRCD    (1=invalid record)
.**********************************************************************
CKCAF000  MOVE      ZERO,OVRCD              * valid record
          COMPARE   ZERO,CCONTIND
          GOTO      CKCAF999 IF EQUAL       * All
          COMPARE   FMCATYP,CCONTIND
          GOTO      CKCAF999 IF EQUAL       * Selected
          MOVE      ONE,OVRCD               * invalid record
          GOTO      CKCAF999
CKCAF999  RETURN
.
