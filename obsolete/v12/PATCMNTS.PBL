.******************************************************************************
. PROC    PATCMNTS.PBL: Admission Comments Maintenance and Enquires
.
. PROC    URCMNTS.PBL : U/R level Comments Maintenance and Enquires
.
.         This include contains the code for enquiries on the comments file.
.         One of the options for the enquiry is to Add new comments, Change
.         the comments, or Delete the comments.
.
.         The following includes are assumed to be in the main program
.
.         PATMI1FD
.         PATIOMIS
.         PATCMNFD
.         PATIOCMN
.
.         This include may be called either by:
.
.         PROC       PATCMNTS    for the enquiry and maintenance
.         PROC       URCMNTS 
.
.         or by:
.
.         CALL       PRTCMNTS    for printing the data
.
. MODIFICATIONS: 
.         V10.04.01 15/04/2014  J.Tan          CAR 261630
.                   Removed port number from temp file name
.         V9.02.01  13/05/2002  Pat Dirito     
.                   Fix to stop blank records written to comment files.
.         V5.03.01  03/11/1995    Justin Coates
.                   added the passing of FORM1 to URCMNTS 1=enquiry mode
.******************************************************************************
.         V5.01.01  28/06/89 K.Peak
.                   Changed Adm & U/R to 8 characters
.         V5.01.02  11/01/90 K.Peak
.                   Added No Admission Details SRF 103475
.         V5.01.03  05/08/92 ROD   
.                   Added U/R level comments
.         V5.01.04  10/08/92 ROD   
.                   Fixed check if no comments
.         V5.01.05  21/01/1994    Justin Coates  (Hawkes Bay)
.                   added security on U/R comments
.         V5.01.06  24/03/1994  Rob Leonard  QUOTE 600357
.                   changed keyins to case insensitive etc.
.         V5.01.07  05/04/1994  Steve Armstrong
.                   Fixed U/R comments to use new WP (same as admis. comments)
.******************************************************************************
.
.         Subroutine to print the comments file. Used by IBAPAT32.
.
PRTCMNTS  PROC       HASCMNTS
          BRANCH     OVRCD OF NOCMNTS
.
          COMPARE   ONE,PTCNURCO                 * U/R level comments on?
          GOTO      PRTCM700 IF NOT EQUAL        * no
.
. Print U/R level comments
.
          PRINT     *N,*1,"--------------------------------":
                    " Comments Details  (U/R) -----------------------":
                    "-------------------------------------------------":
                    *N
          ADD       THREE,LNO
.
          PROC      PRURCMNT                      * print U/R comments
.
PRTCM700  PRINT     *N,*N,*1,"--------------------------------":
                    " Comments Details  (Admission) -----------------":
                    "-------------------------------------------------":
                    *N
          ADD       THREE,LNO
.
          CALL      PRPTCMNT
          GOTO      ENDPCMNT
.
NOCMNTS   PRINT      *N,*1,"---------------------------*****":
                     " No Comments Details *****----------------------":
                     "-------------------------------------------------"
          ADD        TWO,LNO
.
ENDPCMNT  RETURN
+
.*************************************************************************
.* PATCMNTS  :  Admission level comments                                 *
.*************************************************************************
          DEFPROC   PATCMNTS
.
.
COMMCNT   FORM      3         * counter for current comments
WORDFLAG  FORM      1
WPFILE1   FILE      ASCII, FIXED=70
.
.         invoke the word processor to do the comments
.
PATCMNTS  MOVE      ZERO,COMMFLAG      * comments found flag
          MOVE      AADMNO,KEY8
          REP       " 0",KEY8
          MATCH     "00000000",KEY8
          GOTO      PTCMN950 IF EQUAL
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,CFNAME
          PREP      WPFILE1,CFNAME
          DISPLAY   *P1:9,*EF;
.
.         copy in any existing comments
.
          PACK      KEY10,AADMNO,SP10
          CALL      RDSCMNT1
.
PTCMN220  CALL      RDKCMNT1
          BRANCH    OVRCD,PTCMN300          * no more comments to copy
.
          COMPARE   AADMNO,PCADMN           * same admission number?
          GOTO      PTCMN300 IF NOT EQUAL   * no
.
          WRITE     WPFILE1,SEQ;PCLINE
          GOTO      PTCMN220
.
PTCMN300  MOVE      "0",WORDFLAG            * set status flag
          CLOSE     WPFILE1                 * close the wp file
          WORD      CFNAME,99,5,9,76,22,WORDFLAG
.
          COMPARE   ONE,WORDFLAG
          GOTO      PTCMN999 IF NOT EQUAL   * dont post changes
.
.         copy the comments back to file
.         first delete existing comments
.
          PACK      KEY10,AADMNO,SP10
          CALL      RDSCMNT1
.
PTCMN400  CALL      RDKCMNT1
          BRANCH    OVRCD,PTCMN500          * end of file
          COMPARE   AADMNO,PCADMN           * same U/R Number?
          GOTO      PTCMN500 IF NOT EQUAL
.
          PACK      KEY10,PCADMN,PCLNO
          CALL      DECMNT1
          GOTO      PTCMN400
.
.         now write the new comments back
.
PTCMN500  OPEN      WPFILE1,CFNAME          * get back to start of file
          MOVE      AADMNO,PCADMN           * set admission number
          MOVE      ZERO,PCLNO              * set line number
.
PTCMN510  READ      WPFILE1,SEQ;PCLINE,ANS
          GOTO      PTCMN900 IF OVER
.
          BRANCH    COMMFLAG,PTCMN520       * Comments Found?
          RESET     PCLINE 
          MATCH     SP70,PCLINE             * Check for blank line
          IF        !@EQUAL
            MOVE      ONE,COMMFLAG      * Data found set flag
            GOTO      PTCMN500 
          ENDIF
          GOTO      PTCMN510                * Read next line 
.
PTCMN520  REP       " 0",ANS
          ADD       ONE,PCLNO               * add to line number
.
          PACK      KEY10,PCADMN,PCLNO,SP70
          CALL      WRCMNT1
          GOTO      PTCMN510
.
PTCMN900  CLOSE     WPFILE1,DELETE          * delete temp file
          GOTO      PTCMN999
.
PTCMN950  DISPLAY   *P1:24,*EL,*B,"No Admission Details for Comments. ";
          CALL      HITENTER
+
PTCMN999  ENDPROC
+
.************************************************************************
.*  HASCMNTS  :  Determine if there are admission or U/R comments       *
.*               Returns : OVRCD=1 No admission or U/R comments         *
.************************************************************************
          DEFPROC   HASCMNTS
.
          INC       PATCOMFD/INC
.
HASCMNTS  MOVE      ONE,OVRCD               * set flag for no comments
.
.         See if there are any admission based comments on file
.
          PACK      KEY10,AADMNO,SP10
          CALL      RDSCMNT1                * position on admission #
          CALL      RDKCMNT1                * read next record
          BRANCH    OVRCD,HASCM500          * end of file
.
          COMPARE   AADMNO,PCADMN           * same admission # ?
          GOTO      HASCM950 IF EQUAL       * yes - comments found so finish
.
.         There were no admission based comments so
.         see if there are any U/R based comments on file
.
HASCM500  MOVE      ONE,OVRCD
          COMPARE   ONE,PTCNURCO            * using U/R based comments ?
          GOTO      HASCM950 IF NOT EQUAL   * no - finish
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*248,PTCNSOUC
          OPEN      PATCOMA1,"patcomaf"
          PACK      KEY11,PURNO,SP3
          CALL      RSPTCOM1                * position on U/R #
HASCM600  CALL      RKPTCOM1                * read next record
          BRANCH    OVRCD,HASCM900          * end of file
.
          MATCH     PURNO,PTCOURNO          * same U/R # ?
          IF        @EQUAL
.
.         If there is security on the U/R comments, check that the operator
.         has access to comments
.
            IF        PTCNSOUC = ONE
              MATCH     SP4,PTCOOPER        * is there an operator code ??
              IF        !@EQUAL
                MATCH     PASSCODE,PTCOOPER      * valid operator ?
                GOTO      HASCM600 IF NOT EQUAL  * no
              ENDIF
            ENDIF
            GOTO      HASCM900
          ENDIF
.
          MOVE      ONE,OVRCD
.
HASCM900  CLOSE     PATCOMA1
HASCM950  GOTO      HASCM999
.
          INC       PATCOMIO/INC
          INC       IBAOVRIO/INC
.
HASCM999  ENDPROC
+
.***********************************************************************
.*  PRPTCMNT  :  Print admission comments                              *
.***********************************************************************
.
PRPTCMNT  PACK      KEY10,AADMNO,SP10
          CALL      RDSCMNT1                * position on admission #
PRTCM600  CALL      RDKCMNT1                * read next record
          BRANCH    OVRCD,PRTCM999          * end of file
.
          COMPARE   AADMNO,PCADMN           * same admission # ?
          GOTO      PRTCM999 IF NOT EQUAL   * no
.
          COMPARE   FIFTY7,LNO              * page full ?
          CALL      HEAD IF NOT LESS        * yes
.
          PRINT     *5,PCLINE
          ADD       ONE,LNO                 * increment line count
          GOTO      PRTCM600
.
PRTCM999  RETURN
+
.*************************************************************************
.         URCMNTS  :  U/R level comments
.         Para's  : FORM1 = 1     enquiry mode 
.*************************************************************************
          DEFPROC   URCMNTS
.
          INC       PATCOMFD/INC
.
ENQUIRY   FORM      1
COMMCNT   FORM      3         * counter for current comments
WORDFLAG  FORM      2
WPFILE1   FILE      ASCII, FIXED=70
.
.         invoke the word processor to do the comments
.
URCMNTS   MOVE      ZERO,COMMFLAG      * comments found flag
          MOVE      FORM1,ENQUIRY
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,CFNAME
          PREP      WPFILE1,CFNAME
          DISPLAY   *P1:9,*EF;
.
.         copy in any existing comments
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*248,PTCNSOUC
          OPEN      PATCOMA1,"patcomaf"
          PACK      KEY11,PURNO,SP3
          CALL      RSPTCOM1
.
URCMN220  CALL      RKPTCOM1
          BRANCH    OVRCD,URCMN300          * no more comments to copy
.
          MATCH     PURNO,PTCOURNO          * same U/R Number?
          GOTO      URCMN300 IF NOT EQUAL   * different person
.
          IF        PTCNSOUC = ONE
            MATCH     PASSCODE,PTCOOPER
            GOTO      URCMN220 IF NOT EQUAL   * not the user who entered comms
          ENDIF
.
          WRITE     WPFILE1,SEQ;PTCOLINE
          GOTO      URCMN220
.
URCMN300  MOVE      "0",WORDFLAG            * set status flag
          LOAD      WORDFLAG,ENQUIRY,TEN2   * set to readonly/show more
          CLOSE     WPFILE1                 * close the wp file
          WORD      CFNAME,999,5,9,76,22,WORDFLAG
.
          COMPARE   ONE,WORDFLAG
          GOTO      URCMN999 IF NOT EQUAL   * dont post changes
.
.         copy the comments back to file
.         first delete existing comments
.
          PACK      KEY11,PURNO,SP3
          CALL      RSPTCOM1
.
URCMN400  CALL      RKPTCOM1
          BRANCH    OVRCD,URCMN500          * end of file
          MATCH     PURNO,PTCOURNO          * same U/R Number?
          GOTO      URCMN500 IF NOT EQUAL
.
          IF        PTCNSOUC = ONE
            MATCH     PASSCODE,PTCOOPER
            GOTO      URCMN400 IF NOT EQUAL   * not the user who entered comms
          ENDIF
.
          PACK      KEY11,PTCOURNO,PTCOCLNO
          CALL      DEPTCOM1
          GOTO      URCMN400
.
.         now write the new comments back
.
URCMN500  CALL      ROCC0000                * re-order the comments count
          OPEN      WPFILE1,CFNAME          * get back to start of file
          MOVE      PURNO,PTCOURNO          * set U/R Number
          MOVE      COMMCNT,PTCOCLNO        * set latest line number
.
URCMN510  READ      WPFILE1,SEQ;PTCOLINE,ANS
          GOTO      URCMN900 IF OVER
.
          BRANCH    COMMFLAG,URCMN520       * Comments Found?
          RESET     PTCOLINE
          MATCH     SP70,PTCOLINE           * Check for blank line
          IF        !@EQUAL
            MOVE      ONE,COMMFLAG      * Data found set flag
            GOTO      URCMN500
          ENDIF
          GOTO      URCMN510                * Read next line
.
URCMN520  REP       " 0",ANS
          ADD       ONE,PTCOCLNO            * add to line number
          MOVE      SP4,PTCOOPER
          LOAD      PTCOOPER,PTCNSOUC,PASSCODE
.
          PACK      KEY11,PTCOURNO,PTCOCLNO
          CALL      WRPTCOM1
          GOTO      URCMN510
.
URCMN900  CLOSE     PATCOMA1
          CLOSE     WPFILE1,DELETE          * delete temp file
          GOTO      URCMN999
.
.*********************************************************************
.         re-order the comments counter if using security on comments
.*********************************************************************
ROCC0000  MOVE      ZERO,COMMCNT
          COMPARE   ONE,PTCNSOUC
          GOTO      ROCC9999 IF NOT EQUAL   * not using security so dont do
.
          PACK      KEY11,PURNO,SP3
          CALL      RSPTCOM1
.
ROCC1000  CALL      RKPTCOM1
          BRANCH    OVRCD,ROCC9999
          MATCH     PURNO,PTCOURNO
          GOTO      ROCC9999 IF NOT EQUAL   * different patient
.
          ADD       ONE,COMMCNT
          MOVE      COMMCNT,PTCOCLNO
          CALL      UPPTCOM1
          GOTO      ROCC1000
.
ROCC9999  RETURN
+
          INC       PATCOMIO/INC
          INC       IBAOVRIO/INC
.
URCMN999  ENDPROC
+
.***********************************************************************
.*  PRURCMNT  :  Print U/R comments                                    *
.***********************************************************************
          DEFPROC   PRURCMNT
.
          INC       PATCOMFD/INC
.
PRURCMNT  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*248,PTCNSOUC
          OPEN      PATCOMA1,"patcomaf"
          PACK      KEY11,PURNO,SP3
          CALL      RSPTCOM1
PRTCM600  CALL      RKPTCOM1
          BRANCH    OVRCD,PRTCM650
          MATCH     PURNO,PTCOURNO
          GOTO      PRTCM650 IF NOT EQUAL
.
          IF        PTCNSOUC = ONE
            MATCH     SP4,PTCOOPER            * is there an operator code ??
            IF        !@EQUAL
              MATCH     PASSCODE,PTCOOPER
              GOTO      PRTCM600 IF NOT EQUAL   * not the user who entered comms
            ENDIF
          ENDIF
.
          COMPARE   FIFTY7,LNO
          CALL      HEAD IF NOT LESS
.
          PRINT     *5,PTCOLINE
          ADD       ONE,LNO
          GOTO      PRTCM600
.
PRTCM650  CLOSE     PATCOMA1
          GOTO      PRTCM999
.
          INC       PATCOMIO/INC
          INC       IBAOVRIO/INC
.
PRTCM999  ENDPROC
..............................................................................
