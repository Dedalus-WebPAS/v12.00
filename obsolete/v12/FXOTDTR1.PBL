.*****************************************************************************
.*    Program      : FXOTDTR1                                                 *
.*    Description  : Conversion outdtrof to new File layout                   *
.*                                                                           *
.*    Author       : Mike Laming                                             *
.*    Date         : 22 Jan 2007                                             *
.*    Modifications:                                                         *
.*                 : V9.10.01  18/06/08 Mike Laming   CAR 167401             *
.*                             Add logic to handle multiple Outpat. Sites    *
.*****************************************************************************
.
.  Global change FXOTDTR1   eg F57PTDOC
.  Global change OUTDTRFD   eg PATDOCFD
.  Global change OUTDTRIO   eg PATIODOC
.  Global change outdtrof   eg patdoctf
.  Global change OUTDTRO1   eg PATDOCT1
.  Global change KEY22      eg KEY8
.  Global change WRDTRO1    eg WRDOCT1
.  Global change savdtrof   eg sptdoctf
.
.  vi Global change command  :%s/FromString/ToString/g
.
.
          INC       STD001FD
          INC       OUTSITFD/INC
.
FINDFILE  FILE      ASCII, FIXED=256
.
. ******************************************* Original FD ********************
.                               1 * use 1st or shortest key and rename OLDFILE
.                               2 * comment out all fields that are in new FD
.                               3 * change prefix to XXX for fields that change
.
OLDFILE    IFILE  SQL, FIXED=180, KEY="U1-8,9-16,17-22"
.
..FILDTRO1    INIT     "dtrof"
.
. NAME     TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION
. ----     ----   ------   --------  ----------   -----------
..DOTNUMB     DIM       8          8         1      OUTPATIENT NUMBER
..OTINVNO     DIM       8          8         9      (INVOICE #)
..DOTTRANS    DIM       6          6        17      TRANSACTION NUMBER
.
..OTDDESC     DIM      30         30        23      DESCRIPTION
..OTPATAMT    FORM   12.2          8        53      TOTAL AMOUNT
..OTDDATE     DIM       8          8        61      DATE CCYYMMDD
.
..OTITEMNO    DIM       9          9        69      ITEM NUMBER
.
..OTTYPE      DIM       2          2        78     TRANSACTION TYPE  PY=PAYMENTS
.                                                                   DB=CHARGES
.                                                                   xx=JOURNALS
.
..OTPAYTYP    FORM      1           2       80      PAYMENT METHOD
.                                                0 = NOT APPLICABLE
.                                                1 = CASH
.                                                2 = CHEQUE
.                                                3 = CREDIT CARD
.                                                4 = MERCHANT CARDS EG AMEX
.                                                5 = MONEY ORDER
.
..OTRECEPT   DIM      12          12       82      RECEIPT NUMBER
.
..OTINVPRT   FORM      1           2       94      0=INVOICE NOT YET PRINTED
.                                                1=INVOICE PRINTED
.
..DOTRECTY   DIM       1           1       96      RECORD TYPE 1 - MISC ITEM
.                                                            2 - CASH RECEIPT
.                                                            3 - JOURNALS
.                                                            4 - DOCTOR FEES
.                                                            5 - DISCOUNT
.
.          These next two variables are updated automatically by the
.          I/O include for each write or update
.
..OTCHGDTE   DIM       8           8       97      DATE THIS RECORD LAST CHANGED
..OTCHGTIM   DIM       8           8      105      TIME THIS RECORD LAST CHANGED
.
..OTMISGRP   DIM       3           3      113      MISCELLANEOUS GROUP (Cat FI)
.                                                JH-Misc.Item Group (Cat FN)
..OTDEPTYP   DIM       3           3      116    DEPOSIT TYPE(BLANK IF NOT DEP)
..OTBATCHN   DIM       8           8      119    BTCH NO FOR MISC.ITMS,CASH,JOUR
.                                                JH-Misc.Item Group (Cat FI)
..OTPATPOR   FORM   12.2           8      127      PATIENT PORTION OF CHARGE
..OTREBPOR   FORM   12.2           8      135      REBATE PORTION OF CHARGE
..DOTNINVS   DIM       1           1      143      No. of invoices to be printed
XXXSERVS    FORM      2           2      144      Number of services
..OTDTGSTA   FORM      1           2      146     Is GST Applicable to this item
.                                                 0-No GST, 1-Yes, 2-Unknown GST
..OTDTGSTC   DIM       6           6      148      GST Payable code
..OTDTGSTM   FORM    12.2          8      154      GST Amount
..OTDTEFFD   DIM       8           8      162    Date item assigned to Fees Inv.
..OTDTCRST   DIM       1           1      170      Credit Note status
.                                                 0 - No Credit Note
.                                                 1 - Fully Credited
.                                                 2 - Credit by Item
XXXSPARE    DIM       9           9      171      SPARE
.
.End of record                          180
.
.******************************************** New FD goes here ****************
.
OUTDTRO1   IFILE  SQL, FIXED=229, KEY="U1-8,9-16,17-22"
OUTDTRO2   IFILE  SQL, FIXED=229, KEY="U96-96,97-104,1-8,9-16,17-22"
OUTDTRO3   IFILE  SQL, FIXED=229, KEY="U1-8,9-16,96-96,61-68,17-22"
OUTDTRO4   IFILE  SQL, FIXED=229, KEY="U9-16,96-96,1-8,17-22"
OUTDTRO5   IFILE  SQL, FIXED=229, KEY="U1-8,9-16,96-96,113-115,17-22"
OUTDTRO6   IFILE  SQL, FIXED=229, KEY="U173-188,9-16,1-8,17-22"
.
FILDTRO1    INIT     "dtrof"
FILDTRO2    INIT     "dtrof"
FILDTRO3    INIT     "dtrof"
FILDTRO4    INIT     "dtrof"
FILDTRO5    INIT     "dtrof"
FILDTRO6    INIT     "dtrof"
.
. NAME     TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION
. ----     ----   ------   --------  ----------   -----------
DOTNUMB     DIM       8          8         1      OUTPATIENT NUMBER
OTINVNO     DIM       8          8         9      (INVOICE #)
DOTTRANS    DIM       6          6        17      TRANSACTION NUMBER
.
OTDDESC     DIM      30         30        23      DESCRIPTION
OTPATAMT    FORM   12.2          8        53      TOTAL AMOUNT
OTDDATE     DIM       8          8        61      DATE CCYYMMDD
.
OTITEMNO    DIM       9          9        69      ITEM NUMBER
.
OTTYPE      DIM       2          2        78      TRANSACTION TYPE  PY=PAYMENTS
.                                                                   DB=CHARGES
.                                                                   xx=JOURNALS
.
OTPAYTYP    FORM      1           2       80      PAYMENT METHOD
.                                                0 = NOT APPLICABLE
.                                                1 = CASH
.                                                2 = CHEQUE
.                                                3 = CREDIT CARD
.                                                4 = MERCHANT CARDS EG AMEX
.                                                5 = MONEY ORDER
.
OTRECEPT   DIM      12          12       82      RECEIPT NUMBER
.
OTINVPRT   FORM      1           2       94      0=INVOICE NOT YET PRINTED
.                                                1=INVOICE PRINTED
.
DOTRECTY   DIM       1           1       96      RECORD TYPE 1 - MISC ITEM
.                                                            2 - CASH RECEIPT
.                                                            3 - JOURNALS
.                                                            4 - DOCTOR FEES
.                                                            5 - DISCOUNT
.
.          These next two variables are updated automatically by the
.          I/O include for each write or update
.
OTCHGDTE   DIM       8           8       97      DATE THIS RECORD LAST CHANGED
OTCHGTIM   DIM       8           8      105      TIME THIS RECORD LAST CHANGED
.
OTMISGRP   DIM       3           3      113      MISCELLANEOUS GROUP (Cat FI)
.                                                JH-Misc.Item Group (Cat FN)
OTDEPTYP   DIM       3           3      116      DEPOSIT TYPE(BLANK IF NOT DEP)
OTBATCHN   DIM       8           8      119      BTCH NO FOR MISC.ITMS,CASH,JOUR
.                                                JH-Misc.Item Group (Cat FI)
OTPATPOR   FORM   12.2           8      127      PATIENT PORTION OF CHARGE
OTREBPOR   FORM   12.2           8      135      REBATE PORTION OF CHARGE
DOTNINVS   DIM       1           1      143      No. of invoices to be printed
OTSERVS    FORM      5           4      144      Number of services
OTDTGSTA   FORM      1           2      148      Is GST Applicable to this item
.                                                 0-No GST, 1-Yes, 2-Unknown GST
OTDTGSTC   DIM       6           6      150      GST Payable code
OTDTGSTM   FORM    12.2          8      156      GST Amount
OTDTEFFD   DIM       8           8      164      Date item assigned to Fees Inv.
OTDTCRST   DIM       1           1      172      Credit Note status
.                                                 0 - No Credit Note
.                                                 1 - Fully Credited
.                                                 2 - Credit by Item
OTDTBTCH   DIM       16         16      173      Batch Details (NZ Priv Exract)
.                                                format (99999XXXCCYYMMDD)
.                                                where 99999 = G/L Batch No.
.                                                      XXX   = Hospital Code
.                                                      CCYYMMDD = Extract.date
OTSPARE    DIM      40          40      189      SPARE
.
.End of record                          229
.
.  redefine form fields from key 
.  ----------------------------- 
. NAME     TYPE      LENGTH     START LOC.   DESCRIPTION
. ----     ----      ------     ----------   -----------
OTNUMB      FORM         8             1      OUTPATIENT NUMBER
OTTRANS     FORM         6            17      TRANSACTION NUMBER
OTRECTYP    FORM         1            88      TRANSACTION TYPE
OTNINVS     FORM         1            129     No. of invoices to be printed 
.
. ********************************** other program variables ******************
. the index OLDFILE
.   
.
CROW      FORM      2
CMDLINE   DIM       100
DIM4      DIM       4
DIM4A     DIM       4
DIM6      DIM       6
DIM6A     DIM       6
DIM12     DIM       12
DIM60     DIM       60
DDCENT    DIM       2
DEFPATH   DIM       60
FORM12D2  FORM      12.2
FILENAMA  DIM       8
FILENAMB  DIM       8
FILESAVE  DIM       8
.
NEWFILE   DIM       60
NEWPATH   DIM       60
OLDPATH   DIM       60
FOUND     DIM       1[20]
PREFIX    DIM       3[20]
PREVSITE  DIM       3
RECNUM    FORM      8
SAVEFLG   FORM      1
SITCOUNT  FORM      2
SP50      INIT    "                                                  "
SP60      INIT    "                                                            "
.
PRGNAM    INIT      "CONVERSION OUTDTRFD"
PRGID     INIT      "FXOTDTR1"
VERSION   INIT      "V12.00.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN0000
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN1000,MAIN0000,MAIN9999 * Yes, no, cancel
.
MAIN1000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN0000
.
. Loop thru old file and WRDTRO1 records to new file
.
          CALL      READ0000                      * read old records and WRDTRO1
          CALL      ENDP0000                      * save/compress saved file
          GOTO      MAIN0000
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,EXIT
          MOVE      ZERO,RECNUM
          MOVE      FIVE,CROW
          MOVE      SP8,FILENAMB
.
          OPEN      OUTSITA3,"outsitaf"
          MOVE      ZERO,SITCOUNT
          MOVE      SP3,PREVSITE
          DISPLAY   *P1:4,*EF,"Select Site file to be converted : ":
                    *P3:5,"0. Exit";
.
          PACK      KEY9,SP10
          CALL      RDSSITA3
INIT1000  CALL      RDKSITA3
          BRANCH    OVRCD,INIT2000
          MATCH     SP3,PREVSITE
          GOTO      INIT1200 IF EQUAL
          MATCH     PREVSITE,OSTFILE
          GOTO      INIT1000 IF EQUAL
.
INIT1200  MOVE      OSTFILE,PREVSITE
          ADD       ONE,SITCOUNT
          PACK      FILENAMA,OSTFILE,FILDTRO1
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OUTDTRO1,FILENAMA  
          TRAPCLR   IO          
          IF        OVRCD = 0
            MOVE      "- converted ",DIM12
            MOVE      "Y",FOUND[SITCOUNT]
          ELSE 
            MOVE      SP20,DIM12
            MOVE      SP1,FOUND[SITCOUNT]
          ENDIF
          MOVE      OSTFILE,PREFIX[SITCOUNT]
.
          ADD       ONE,CROW
          DISPLAY   *P2:CROW,*EL,SITCOUNT,". ",OSTDESC," - ",OSTFILE,SP2,DIM12
          GOTO      INIT1000
.
INIT2000  ADD       TWO,CROW
.
INIT2200  MOVE      ZERO,F2
          KEYIN     *P1:CROW,"Selection : ",F2;
.
          COMPARE   ZERO,F2
          GOTO      INIT9500 IF EQUAL  
          IF        F2 > SITCOUNT
            GOTO      INIT2200
          ENDIF
.
          MATCH     SP1,FOUND[F2]
          GOTO      INIT2200 IF NOT EQUAL
.
          PACK      FILENAMB,PREFIX[F2],FILDTRO1
          UNPACK    PREFIX[F2],KEY1,KEY2
          MOVE      "s",ANS
          PACK      FILESAVE,ANS,KEY2,FILDTRO1
          DISPLAY   *P30:CROW,"Table '",FILENAMB,"' selected";
          GOTO      INIT9999
.
INIT9500  MOVE      ONE,EXIT
.
INIT9999  RETURN
.
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          ADD       TWO,CROW
.
          IF        CROW > 13
            CALL      DISPHEAD
            DISPLAY   *P1:10,"Selection : ",F2:
                      *P30:10,"Table '",FILENAMB,"' selected";
            MOVE      "12",CROW
          ENDIF
            
KDIR0500  KEYIN     *P1:CROW,*EL,"Do you want to save ":
                           "the original file (Y/N) ? ",ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0500
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          ADD       TWO,CROW
          MOVE      CROW,F2
          ADD       ONE,F2
          KEYIN     *P1:CROW,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:F2,*EL,*DV,*RV,OLDPATH:
                    *P10:F2,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  ADD       TWO,CROW
          MOVE      CROW,F2
          ADD       ONE,F2
          KEYIN     *P1:CROW,*EL,"Keyin path for saving original file: ":
                    *P10:F2,*EL,*DV,*RV,OLDPATH:
                    *P10:F2,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:F2,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          ADD       THREE,CROW
          MOVE      CROW,F2
          ADD       ONE,F2
          KEYIN     *P1:CROW,*EL,"Keyin path for the new file: ":
                    *P10:F2,*EL,*DV,*RV,NEWPATH:
                    *P10:F2,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:F2,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
PREP0000 
. open old file
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE,FILENAMB
          TRAPCLR   IO
          BRANCH    OVRCD,PREP5000       * Original file does not exist
          CLOSE     OLDFILE
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
. Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat ",CMDLINE
          APPEND    FILENAMB,CMDLINE
          APPEND    ".dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    FILESAVE,CMDLINE
          APPEND    ".dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat ",CMDLINE
          APPEND    FILENAMB,CMDLINE
          APPEND    ".idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    FILESAVE,CMDLINE
          APPEND    ".idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
. remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    FILENAMB,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    FILESAVE,DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    FILENAMB,NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.      eg    APPEND    " 195 UC9-16",CMDLINE
.
          APPEND    " 229 UC1-8,9-16,17-22",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has 
.  to be repeated with for each individual index
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC96-96,97-104,1-8,9-16,17-22",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC1-8,9-16,96-96,61-68,17-22",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC9-16,96-96,1-8,17-22",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC1-8,9-16,96-96,113-115,17-22",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " UC173-188,9-16,1-8,17-22",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
          OPEN      OUTDTRO1,NEWFILE
.
. set the flag to say we want to continue
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
PREP9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and WRDTRO1 each record               *
.**********************************************************************
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          PACK      KEY22,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
.  All necessary changes to the files field lengths, date changes
.  or any other sort of modification must be made here
.
.    eg    PACK      DSPARE,SP50
.          PACK      PTDSDMDC,SP10
.          PACK      PTDSDDRG,SP10
.
          MOVE      XXXSERVS,OTSERVS
          IF        OTRECTYP = 1
            MOVE      OTPATPOR,FORM12D2
            ADD       OTREBPOR,FORM12D2
            ASSIGN    (OTPATAMT/FORM12D2),OTSERVS
            IF        OTSERVS = 0
              MOVE      ONE,OTSERVS
            ENDIF
          ENDIF
          PACK      OTDTBTCH,SP70
          PACK      OTSPARE,SP70,SP70
.                                                 * Pack key here
          PACK      KEY22,DOTNUMB,OTINVNO,DOTTRANS
          CALL      WRDTRO1                       * WRDTRO1 to new file
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     OUTDTRO1                      * close new file
          CLOSE     OLDFILE                       * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
READ9999  RETURN
.
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
VALD0000  MOVE      ZERO,EXIT
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or compress save file                                *
.**********************************************************************
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    FILESAVE,DIM60
          APPEND    "*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "compress -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not compressed or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.         Get the default directory
.**********************************************************************
DEFT0000  EXECUTE   "ldat outdtrof.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding outdtrof.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
DEFT9999  RETURN
+
.**********************************************************************
. ---- OLD IO ROUTINES ----
.**********************************************************************
.  Copy and paste the Positional Read routine from the original
.  IO file, rename the routine READSOLD and change the index name
.  to OLDFILE
.
READSOLD RESET     KEY22
         READ      OLDFILE,KEY22;;
         RETURN
.
.  Copy and paste the Read Next routine from the original
.  IO file, rename the routine READKOLD and change the index name 
.  to OLDFILE
.
READKOLD MOVE      ZERO,OVRCD
         READKS    OLDFILE;DOTNUMB,OTINVNO,DOTTRANS,OTDDESC,OTPATAMT:
                            OTDDATE,OTITEMNO,OTTYPE,OTPAYTYP,OTRECEPT:
                            OTINVPRT,DOTRECTY,OTCHGDTE,OTCHGTIM:
                            OTMISGRP,OTDEPTYP,OTBATCHN:
                            OTPATPOR,OTREBPOR,DOTNINVS,XXXSERVS,OTDTGSTA:
                            OTDTGSTC,OTDTGSTM,OTDTEFFD:
                            OTDTCRST,XXXSPARE
.
         GOTO      OVERCOND IF OVER
         MOVE      DOTNUMB,OTNUMB
         MOVE      DOTTRANS,OTTRANS
         MOVE      DOTRECTY,OTRECTYP
         MOVE      ONE,OTNINVS
         MOVE      DOTNINVS,OTNINVS
         RETURN
.
.
WRDTRO1  RESET     KEY22
         MOVE      OTRECTYP,DOTRECTY
         MOVE      OTNINVS,DOTNINVS
.
         CLOCK     TIME,OTCHGTIM
         PACK      OTCHGDTE,CCC,CYY,CMM,CDD
         REP       " 0",OTCHGDTE
.                           
         WRITE     OUTDTRO1,KEY22;KEY22,OTDDESC,OTPATAMT:
                                  OTDDATE,OTITEMNO,OTTYPE,OTPAYTYP,OTRECEPT:
                                  OTINVPRT,DOTRECTY,OTCHGDTE,OTCHGTIM:
                                  OTMISGRP,OTDEPTYP,OTBATCHN:
                                  OTPATPOR,OTREBPOR,DOTNINVS,OTSERVS,OTDTGSTA:
                                  OTDTGSTC,OTDTGSTM,OTDTEFFD:
                                  OTDTCRST,OTDTBTCH,OTSPARE
         RETURN
.
.
          INC       OUTSITIO/INC
          INC       STD001IO
