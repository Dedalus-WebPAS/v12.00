.******************************************************************************
.* System  : CCI                                                              *
.* Program : IBACCI10                                                         *
.* Desc    : CCI TRANSITION EXTRACT 7 DATA TRANSMISSION                       *
.******************************************************************************
.* Date    : 29/02/2002                                                       *
.* Author  : Tonii    (Program is written for St Vincents Hospital only)      *
.* Function:                                                                  *
.*           - OPTION 1 will allow the user to run the extract, hence creating*
.*             an ASCII file CCIDAT.txt in the assigned cd path. However if   *
.*             records with a status of 'E' (extracted) is found, user will be*
.*             prompted with a choice to delete all previously extracted      *
.*             records before continuing.                                     *
.*                                                                            *
.*           - OPTION 2 will reset all extracted records from status 'E' to   *
.*             'T', user will then need to reselect option 1 for extraction   *
.*                                                                            *
.* Mods.   :                                                                  *
.******************************************************************************
.*        V11.03.01 20.03.2023  DA Sarkies    TSK 0909393                     *
.*                  Changed RSOPDEA1 & RKOPDEA1 to 2                          *
.*        V11.02.01 10/02/2022  Thanh T       TSK 0889899                     *
.*                  Recompiled as WATTR1FD changes                            *
.*        V10.04.01 20/12/2013  Ania P        CAR 294308                      *
.*                  Recompiled for PATRDEFD                                   *
.*        V10.02.02 31/10/2011  Mike Laming   CAR 250267                      *
.*                  Correct lengths for vars in PATICUFD at EXIP2500          *
.*        V10.02.01 28/03/2011  Mike Laming   CAR 240107                      *
.*                  Mods to PATECDaf & PATECOaf variables & Keys - file change*
.*                  Removed PATMDSFD/IO - obsolete                            *
.*        V10.00.01 31/03/2010  Mike Laming   CAR 217575                      *
.*                  Recompiled for changes to MEHDIAFD                        *
.******************************************************************************
.*      V9.03.01  04/06/2003  Jill Habasque SRF 31802                         *
.*                Changed pmpxintr to match to one instead of zero            *
.******************************************************************************
.*      V9.02.19  29/11/2002  Davin  SRF 22668                                *
.*                - encounter number now zero filled & RJ (e.g. IP00012345)   *
.*                - admit type now category P                                 *
.*                - patient type now category CL + category A                 *
.*                - LOS, preop days & postop days now zero filled & RJ        *
.*      V9.02.18  01/08/2002  Tonii  SRF 20343                                *
.*                 - Fixed U * O - stack overflow                             *
.*                 - added delete of extract file after prep                  *
.*      V9.02.17  16/04/2002  Tonii                                           *
.*                - Recompiled                                                *
.*      V9.02.16  16/04/2002  Tonii                                           *
.*                - Changed to use 'Arrival Date' not 'Discharge Date' when   *
.*                  validating extraction date                                *
.*                - Fixed validating of EMR/Outpatient Event records          *
.*      V9.02.15  11/04/2002  Tonii                                           *
.*                - write E record if EMR patient has been discharged then    *
.*                  addmitted as an inpatient                                 *
.*      V9.02.14  11/04/2002  Tonii                                           *
.*                - Added error message if no outpatient visit is found       *
.*                - Changed to make outbokaf use the 6th index not 3rd        *
.*      V9.02.13  10/04/2002  Tonii                                           *
.*                - Mods to read the EMRUNKAF table if patient has no UR No.  *
.*      V9.02.12  10/04/2002  Tonii                                           *
.*                - Added VALD0000 to validate tables and print error report  *
.*                - Fixed EMR extract problems                                *
.*      V9.02.11  08/04/2002  Tonii                                           *
.*                - Fixed discharge fields                                    *
.*      V9.02.10  05/04/2002  Tonii                                           *
.*                - Fixed extract of Admission Time and reading of transfer   *
.*                  details                                                   *
.*                - Added new Visit Type '9' for OP events                    *
.*                - New routine EXEV0000 added for OP events                  *
.*      V9.02.09  04/04/2002  Tonii                                           *
.*                - Added prompt before extracting data                       *
.*                - Fixed fiscal year and period in O/P                       *
.*                - Added new error messages                                  *
.*                - Change record to be of status 'E' if it doesnt fall within*
.*                  the specified start date                                  *
.*      V9.02.08  03/04/2002  Tonii                                           *
.*                - Changed to use PMEVADTE and not PVIDATE when calculating  *
.*                  fiscal year and period in non-booked attending O/P        *
.*      V9.02.07  03/04/2002  Tonii                                           *
.*                - Fixed field lengths for 'Pastoral Care Indicator' and     *
.*                  'Duration Cont mechanical ventilation'                    *
.*                - Left justified 'Homeless person indicator','ICU hours' and*
.*                  'MR Number' for inpatients                                *
.*                - Removed ":" at the end of time format                     *
.*      V9.02.06  03/04/2002  Tonii                                           *
.*                - Fixed extract of 'Patient Type'                           *
.*                - Fixed extract of 'Country of Birth', code only not desc.  *
.*      V9.02.05  28/03/2002  Tonii                                           *
.*                - Fixed layout of extract fields                            *
.*      V9.02.04  28/03/2002  Tonii                                           *
.*                - Added new O/P record format for non-booked attendees      *
.*                - Change 'Patient type' from char(5) to char(6)             *
.*                - Change 'Referral Source' from char(5) to char(4)          *
.*      V9.02.03  28/03/2002  Tonii                                           *
.*                - Changed Company code to be 5 char field, was only writing *
.*                  4 characters                                              *
.*                - Left justified Encounter Number and MR number             *
.*      V9.02.02  28/03/2002  Tonii                                           *
.*                - Removed comma in extract                                  *
.*                - Added confirmation before reset                           *
.*                - Added in printing of error messages                       *
.******************************************************************************
.
          INC       STD001FD
          INC       STDKWSDF
          INC       STDHLPDF
          INC       PATCOMM/INC
.
          INC       CCICONFD/INC
          INC       CCICTYFD/INC
          INC       CCIDPTFD/INC             * cci clinical dept's
          INC       CCIERRFD/INC             * cci error record file
          INC       CCIEX7FD/INC
          INC       CCIUTLFD/INC
          INC       EMRVISFD/INC
          INC       EMRUNKFD/INC
          INC       EOCLNKFD/INC             * EOC link file
          INC       EOCREFFD/INC             * EOC Referral file
          INC       MEHCONFD/INC
          INC       MEHDIAFD/INC             * mental health diagnosis file
          INC       MEHLEGFD/INC             * MH legal status history file
          INC       NHIMASFD/INC             * NHI master file
          INC       OUTBOAFD/INC             * clinic session booking file a
          INC       OUTBB1FD/INC             * clinic session booking file b
          INC       OUTCLIFD/INC             * outpatients clinic file
          INC       OUTRF1FD/INC             * O/P referral file (for eoc)
          INC       OUTSITFD/INC             * outpatients site code file
          INC       OUTCONFD/INC             * outpatient control filea
          INC       OPRDEAFD/INC
          INC       OPRTSMFD/INC
          INC       PATCALAG/INC
          INC       PATDHEAD/INC
          INC       PATASFFD/INC
          INC       PATCT1FD/INC             * Catchment file
          INC       PATCMPFD/INC
          INC       PATCODFD/INC             * codes file
          INC       PATCONFD/INC             * controlf file
          INC       PATDADFD/INC             * adm/disc transfer dest. file
          INC       PATDO1FD/INC             * doctor file
          INC       PATDRGFD/INC             * period file
          INC       PATDSCFD/INC             * discharge file
          INC       PATECOFD/INC
          INC       PATECDFD/INC
          INC       PATHSPFD/INC             * hospital file
          INC       PATICDFD/INC             * icd-9cm diseases file
          INC       PATICOFD/INC
          INC       PATICUFD/INC             * ICU file
          INC       PATMA1FD/INC             * master file
          INC       PATMI1FD/INC             * admission file
          INC       PATNIDFD/INC             * national id file
          INC       PATODCFD/INC
          INC       PATRDEFD/INC
          INC       PATTRNFD/INC             * transfer file
          INC       PATVISFD/INC             * visit file
          INC       PATWR1FD/INC             * ward file
          INC       PMSEVTFD/INC             * patient event file
          INC       PMSPX2FD/INC
          INC       PMSVX1FD/INC
.
          INC       WATTR1FD/INC
+
.**********************************************************************
.*                           VARIABLES                                *
.**********************************************************************
BJDAY     FORM      3            * julian birth day
CALDAYS   FORM      4            * number of days
CDYSDAYS  FORM      6            * required for CALCDAYS
CDYSFDTE  DIM       8
CDYSTDTE  DIM       8
CDYSYEAR  FORM      2
CJDAY     FORM      3            * julian discharge day
CMDLINE   DIM       80
COUNT     FORM      2            * counter
DIM1A     DIM       1
DIM3      DIM       3
DIM4      DIM       4
DIM4A     DIM       4
DIM4B     DIM       4
DIM5      DIM       5
DIM6      DIM       6
DIM8      DIM       8 
DIM8A     DIM       8 
DIM10     DIM       10
DIM30     DIM       30
DIM40     DIM       40
OPERROR   FORM      1
ERRFLG    FORM      5 
ERRMSG    DIM       50
.
FORM3     FORM      3
FORM3A    FORM      3
FORM3B    FORM      3
FORM3C    FORM      3
FROMDATE  DIM       8
NBRDAYS   FORM      4
RECCNT    FORM      5
SAVDATE   DIM       8
SAVURNO   DIM       8
SAVCODE   DIM       9
SAVOUTNO  FORM      8
STATUS    DIM       2
SKEY11    DIM       11
TEMPADM   FORM      8
TMPTIME   DIM       5            * hh:mm
TMPTIME2  DIM       5            * hh:mm
TODATE    DIM       8            * ccyymmdd
TODAY     DIM       8
.
CCIDAT    FILE      ASCII, FIXED=1126  * Extract file
.
CATA      INIT      "A "
CATAC     INIT      "AC"
CATC      INIT      "C "
CATCC     INIT      "CC"
CATCL     INIT      "CL"
CATCU     INIT      "CU"
CATD      INIT      "D "
CATDD     INIT      "DD"
CATM      INIT      "M " 
CATR      INIT      "R "
CATS      INIT      "S "
CATTP     INIT      "TP"
CATVA     INIT      "VA"
CATVB     INIT      "VB"
CATVI     INIT      "VI"
CATVM     INIT      "VM"
CATVR     INIT      "VR"
.
EMR       INIT      "EMR Depmnt"
OP        INIT      "Outpatient"
IP        INIT      "Inpatient "
FNAME     INIT      "CCIDAT.txt"
Z20       INIT      "ZZZZZZZZZZZZZZZZZZZZ"
ZEDS      INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
+
.**********************************************************************
.*                 EXTRACT   VARIABLES                                *
.**********************************************************************
.
.Name     Type    Length      Pos.      Description
.-------  ----    ------      ----      -----------
X7RECTYP  DIM       3         1         Record Type "MR "
X7ENCNUM  DIM       20        4         Encounter number
X7SERENO  DIM       20        24        Series Encounter number
X7COMPCD  DIM       5         44        Company Code "B45  "
X7INOUT   DIM       1         49        "I" or "O" or "E"
X7FSYEAR  DIM       4         50        Fiscal Year	
X7FSPERD  DIM       2         54        Fiscal Period
X7MRNUMB  DIM       20        56        Medical Record Number
X7SSECNO  DIM       9         76        Social Security Number
X7ADMDTE  DIM       8         85        Admission Date (CCYYMMDD)
X7ADMTIM  DIM       6         93        Admission Time (HHMM)
X7ADMTYP  DIM       5         99        Admission Type
X7TRNSPT  DIM       5         104       Transport
X7BLANK   DIM       5         109       BLANK
X7ADMDEP  DIM       4         114       Admission Department
X7CLNTYP  DIM       6         118       Clinic Type
X7PATTYP  DIM       6         124       Patient Type
X7REFSRC  DIM       4         130       Referral Source
X7READ07  DIM       1         134       Readmit within 7 Days
X7READ30  DIM       1         135       Readmit within 30 Days
X7DISDTE  DIM       8         136       Discharge Date (CCYYMMDD)
X7DISTIM  DIM       6         144       Discharge Time (HHMM)
X7LOS     DIM       4         150       Length of Stay
X7PREOPD  DIM       3         154       PRE OP Days
X7PROOPD  DIM       3         157       PRO OP Days
X7SPCARE  DIM       4         160       Special Care Hours 
X7ICUHRS  DIM       4         164       ICU Hours
X7CCUHRS  DIM       4         168       CCU Hours
X7CHRGES  DIM       9         172       Charges
X7BILLNG  DIM       1         181       Billing Status
X7PPAYOR  DIM       5         182       Primary Payor
X7ADMBAR  DIM       5         187       Admit Barthel
X7CLNSUB  DIM       5         192       Clinical Sub Program
X7FPAYOR  DIM       5         197       Fourth Payor
X7CMMPYR  DIM       2         202       CMM Payor
X7DRG     DIM       4         204       DRG Code
X7MDC     DIM       2         208       MDC Code
X7ADDIAG  DIM       6         210       Admit Diagnosis
X7PDIAG   DIM       10        216       Principal Diagnosis
X7SDIAGA  DIM       10        226       Secondary Diagnosis 1
X7SDIAGB  DIM       10        236       Secondary Diagnosis 2
X7SDIAGC  DIM       10        246       Secondary Diagnosis 3
X7SDIAGD  DIM       10        256       Secondary Diagnosis 4
X7SDIAGE  DIM       10        266       Secondary Diagnosis 5
X7SDIAGF  DIM       10        276       Secondary Diagnosis 6
X7SDIAGG  DIM       10        286       Secondary Diagnosis 7
X7SDIAGH  DIM       10        296       Secondary Diagnosis 8
X7SDIAGI  DIM       10        306       Secondary Diagnosis 9
X7SDIAGJ  DIM       10        316       Secondary Diagnosis 10
X7PPROC   DIM       10        326       Principal Procedure
X7SPROCA  DIM       10        336       Secondary Procedure 1
X7SPROCB  DIM       10        346       Secondary Procedure 2
X7SPROCC  DIM       10        356       Secondary Procedure 3
X7SPROCD  DIM       10        366       Secondary Procedure 4
X7SPROCE  DIM       10        376       Secondary Procedure 5
X7SPROCF  DIM       10        386       Secondary Procedure 6
X7SPROCG  DIM       10        396       Secondary Procedure 7
X7SPROCH  DIM       10        406       Secondary Procedure 8
X7SPROCI  DIM       10        416       Secondary Procedure 9
X7SPROCJ  DIM       10        426       Secondary Procedure 10
X7ADMTMD  DIM       10        436       Admit MD (Medical Department)
X7DATEWL  DIM       8         446       Date on Waiting List
X7WLCAT   DIM       5         454       Waiting List Category
X7PREAD   DIM       50        459       Pre-ad Clinic (Re-assessment Date)
X7REFMD   DIM       10        509       Referring MD
X7DSCMD   DIM       12        519       Discharge MD
X7VCDRG4  DIM       19        531       VICDRG4
X7PSURGN  DIM       12        550       Principal Surgeon
X7PANEST  DIM       12        562       Principal Anethesiologist
X7THEATR  DIM       5         574       Theatre (Operating Room Code)
X7OPDTE1  DIM       8         579       Operation Date 1
X7OPDTE2  DIM       8         587       Operation Date 2
X7OPDTE3  DIM       8         595       Operation Date 3
X7OPDTE4  DIM       8         603       Operation Date 4
X7OPDTE5  DIM       8         611       Operation Date 5
X7TRNFLG  DIM       3         619       Transfer Flag
X7DSCTYP  DIM       3         622       Discharge Type
X7DSCDIS  DIM       5         625       Discharge Disposition
X7DSCSRV  DIM       5         630       Discharge Service
X7DSCDEP  DIM       5         635       Discharge Department
X7EXPAUT  DIM       1         640       Expired Autopsy
X7EXPCOR  DIM       1         641	      Expired Coroners Case
X7EXPOPR  DIM       1         642       Expired Operative Death
X7EXPANS  DIM       1         643       Expired Under Anethesia
X7EXPPST  DIM       1         644       Expired Post OP
X7EXP48H  DIM       1         645       Expired Under 48 hours
X7DOB     DIM       8         646       Birthdate (ccyymmdd)
X7NBNFLG  DIM       1         654       Newborn Flag
X7BTHWGT  DIM       4         655       Birthweight (grams)
X7NAME    DIM       30        659       Name (Surname,Given Name)
X7POSTCD  DIM       9         689       Post Code
X7COUNTY  DIM       5         698       County
X7PHONEX  DIM       3         703       Phone Exchange
X7SEX     DIM       1         706       Patient Sex (M, F, U or I)
X7RACE    DIM       5         707       Patient Race (Cat VA)
X7MSTAT   DIM       5         712       Marital Status (Cat M)
X7RELIGN  DIM       5         717       Religion (Cat R)
X7EMPLST  DIM       5         722       Employment Status
X7EMPLYR  DIM       5         727       Employer 
X7GEMPLY  DIM       5         732       Guarantor Employer
X7OCCUPN  DIM       5         737       Occupation
X7SUBURB  DIM       30        742       Suburb
X7COB     DIM       20        772       Country of Birth
X7ARDRG4  DIM       4         792       ARDRG4
X7ARMDC4  DIM       4         796       ARMDC4
X7SPDYCR  DIM       11        800       Special Day Care
X7HDUDAY  DIM       21        811       HDU Days
X7MEDI    DIM       20        832       Medicare number
X7READMI  DIM       20        852       Readmission Intention
X7CARETY  DIM       20        872       Care type
X7CRIADM  DIM       20        892       Criteria for Admission
X7ILOS    DIM       20        912       Intended length of stay
X7HMELSS  DIM       20        932       Homeless Person Indicator (Y/N)
X7PASTOR  DIM       20        952       Pastoral Care Indicator (Y/N)
X7INTERP  DIM       20        972       Interpreter Requirements (Y/N)
X7LVEDAY  DIM       20        992       Leave Days
X7PATCAT  DIM       20       1012       Patient Category (Y/N)
X7DCMV    DIM       30       1032       Duration Cont mechanical ventilation
X7SPFNDA  DIM       30       1062       Special Funding Arrangement
X7VCDRG3  DIM       4        1092       VICDRG3
X7IPEPS   DIM       30       1096       IP Episode
. End of record              1126
.
.-------------------------------------------------------------------------------
.
PRGID     INIT      "IBACCI10"
PRGNAM    INIT      "Extract 7 Data Transmission"
VERSION   INIT      "V12.00.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.**********************************************************************
.
ML0000    CALL      INIT0000            * initialization
          BRANCH    EXIT,ML9999         * no access
.
ML1000    CALL      SCRN0000            * display screen
          BRANCH    OPTION,ML2000:      * run Extract 7
                           ML3000       * Reset Processed Records
          GOTO      ML9999              * Exit
.
ML2000    CALL      CONTQST
          BRANCH    CEXIT,ML2500,ML1000,ML1000
.
ML2500    CALL      EXTR0000            * run Extract 7
          GOTO      ML1000              
.
ML3000    CALL      RSET0000            * Reset Processed Records
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.**********************************************************************
.*                            INIT0000                                *
.*                   Open files & initialize variables                *
.*                   Called : ML0000                                  *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P57:24,"Opening controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P65:24,"outsitaf"
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P65:24,"outbokaf"
          OPEN      OUTBOKA3,"outbokaf"
          OPEN      OUTBOKA6,"outbokaf"
.
          DISPLAY   *P65:24,"outbb1af"
          OPEN      OUTBB1A1,"outbb1af"
.
          DISPLAY   *P65:24,"oprdetaf"
          OPEN      OPRDETA1,"oprdetaf"
          OPEN      OPRDETA2,"oprdetaf"
.
          DISPLAY   *P65:24,"oprtsmaf"
          OPEN      OPRTSMA1,"oprtsmaf"
.
          DISPLAY   *P65:24,"patct1cf"
          OPEN      PATCT1C1,"patct1cf"
.
          DISPLAY   *P65:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P65:24,"patasfaf"
          OPEN      PATASFA1,"patasfaf"
.
          DISPLAY   *P65:24,"patdadaf"
          OPEN      PATDADA1,"patdadaf"
.
          DISPLAY   *P65:24,"patdo1af"
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P65:24,"patdschf"
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATDSCH2,"patdschf"
.
          DISPLAY   *P65:24,"patecdaf"
          OPEN      PATECDA1,"patecdaf"
.
          DISPLAY   *P65:24,"patecoaf"
          OPEN      PATECOA2,"patecoaf"
.
          DISPLAY   *P65:24,"paticddf"
          OPEN      PATICDD1,"paticddf"
.
          DISPLAY   *P65:24,"paticdof"
          OPEN      PATICDO1,"paticdof"
.
          DISPLAY   *P65:24,"pati10df"
          OPEN      PATI10D1,"pati10df"
.
          DISPLAY   *P65:24,"pati10of"
          OPEN      PATI10O1,"pati10of"
.
          DISPLAY   *P65:24,"paticuaf"
          OPEN      PATICUA1,"paticuaf"
.
          DISPLAY   *P65:24,"patma1af"
          OPEN      PATMA1A1,"patma1af"
          DISPLAY   *P65:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P65:24,"patmi1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATMI1A2,"patmi1af"
.
          DISPLAY   *P65:24,"pmsevtaf"
          OPEN      PMSEVTA1,"pmsevtaf"
.
          DISPLAY   *P65:24,"pmspx2af"
          OPEN      PMSPX2A1,"pmspx2af"
.
          DISPLAY   *P65:24,"pmsvx1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P65:24,"patodcaf"
          OPEN      PATODCA2,"patodcaf"
.
          DISPLAY   *P65:24,"patwr1af"
          OPEN      PATWR1A1,"patwr1af"
.
          DISPLAY   *P65:24,"pathspaf"
          OPEN      PATHSPA1,"pathspaf"
.
          DISPLAY   *P65:24,"pattranf"
          OPEN      PATTRAN1,"pattranf"
          OPEN      PATTRAN2,"pattranf"
.
          DISPLAY   *P65:24,"patrdeaf"
          OPEN      PATRDEA1,"patrdeaf"
.
          DISPLAY   *P65:24,"patvisaf"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATVISA2,"patvisaf"
.
          DISPLAY   *P65:24,"emrvisaf"
          OPEN      EMRVISA1,"emrvisaf"
.
          DISPLAY   *P65:24,"emrunkaf"
          OPEN      EMRUNKA1,"emrunkaf"
.
          DISPLAY   *P65:24,"ccidptaf"
          OPEN      CCIDPTA1,"ccidptaf"
.
          DISPLAY   *P65:24,"ccictyaf"
          OPEN      CCICTYA1,"ccictyaf"
.
          DISPLAY   *P65:24,"cciutlaf"
          OPEN      CCIUTLL3,"cciutlaf"
.
          DISPLAY   *P65:24,"ccierraf"
          OPEN      CCIERRA1,"ccierraf"
.
          DISPLAY   *P65:24,"cciex7af"
          OPEN      CCIEX7A1,"cciex7af"
.
          DISPLAY   *P65:24,"wattr1af"
          OPEN      WATTR1A1,"wattr1af"
.
          READ      CONTROLF,TEN;*54,CFILENO,*235,CMDISP,CMDISS,CMDISC,CMDISA:
                                 *244,CHOSPNUM
          READ      CONTROLF,TWENTY;*87,PTCNBRD
          READ      CONTROLF,TWENTY1;*138,PTCNNHII
          READ      CONTROLF,THIRTY1;*200,OTCNUDDC
          READ      CONTROLF,FIFTY9;*234,PTCNNMPI      
          READ      CONTROLF,SIXTY9;*69,MHCNUSE
          READ      CONTROLF,SEVENTY1;*48,CCCNAECD,*51,CCCNBRDR,*195,CCCNAEDP:
                                      *200,CCCNSVHM,*201,CCCNX7SD,*209,CCCNX7FP
          IF        CCCNSVHM <> 1
            DISPLAY  *P1:24,*EL,*B,"Not using CCI for SVHM.  ";
            CALL     HITENTER
            GOTO     INIT9100
          ENDIF
.
          READ      CONTROLF,SEVENTY9;*120,PTCNUEOC,*121,PTCNUACC
          READ      CONTROLF,EIGHTY8;*43,PTCNACCF,*59,PTCNI10D
          STRIP     CCCNX7FP
          CLOSE     CONTROLF
.
          IF        PTCNNHII=1
            DISPLAY   *P65:24,"nhimasaf";
            OPEN      NHIMASA2,"nhimasaf"
          ELSE
            DISPLAY   *P65:24,"patnidaf";
            OPEN      PATNIDA1,"patnidaf"
          ENDIF
.
          PACK      TODAY,CCC,CYY,CMM,CDD     * todays date
          REP       " 0",TODAY
.
          MOVE      ZERO,ERRFLG
          MOVE      ZERO,EXIT
          GOTO      INIT9999
.
INIT9100  MOVE      ONE,EXIT
.
INIT9999  RETURN
+
.**********************************************************************
.*                             SCRN0000                               *
.*                        Main Option Screen                          *
.*                        Called : ML0000                             *
.**********************************************************************
.
SCRN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Run Extract 7 ":        
                    *P1:6,*V2LON,TWO,*HOFF," = Reset Processed Records"
.
SCRN1000  KEYIN     *P1:8,"Select Option : ",*EF,*DV,UNDLN1:
                    *P17:8,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION                  * exit selected ?
          GOTO      SCRN9999 IF EQUAL            * yes - finished
.
          BRANCH    OPTION,SCRN9999:             * run Extract 7  
                           SCRN9999              * reset processed records
.
          BEEP                                   * invalid option
          GOTO      SCRN1000
.
SCRN9999  RETURN
+
.**********************************************************************
.*        Option 1 selected - Run Extract 7           called by ML2000*
.**********************************************************************
EXTR0000  MOVE      ZERO,EXIT
          MOVE      ZERO,ERRFLG
          MOVE      ZERO,RECCNT
          PACK      KEY11,ANSE,SP20     * check for previously extracted records
          CALL      RSCLEX71
          CALL      RKCLEX71
.    
          MATCH     ANSE,CLEXSTAT
          GOTO      EXTR2000 IF NOT EQUAL
.
EXTR0500  DISPLAY   *P1:24,*EL,*V2LON:
                    "Previously extracted records will be deleted. ",*HOFF:
                    "Ok to Continue (",*V2LON,ANSY,*HOFF,SLASH:
                    *V2LON,ANSN,*HOFF,SLASH,*V2LON,ANSC,*HOFF,") ?";
.
          MOVE      SP1,ANS
          KEYIN     *P72:24,*DV,SP3,*P72:24,*V2LON,ANS:
                    *P72:24,*DV,ANS;
          TYPE      ANS
          GOTO      EXTR0500 IF EQUAL
.
          REP       "Y1N2C3y1n2c3",ANS     
          MOVE      ANS,FORM1
          BRANCH    FORM1,EXTR1000,EXTR9999,EXTR9999
.                           Yes   /  No    /  Cancel
          GOTO      EXTR0500
.
EXTR1000  PACK      KEY11,ANSE,SP20
          CALL      RSCLEX71             * Loop through and remove all records
EXTR1500  CALL      RKCLEX71             * with status = 'E'
          BRANCH    OVRCD,EXTR2000 
.
          MATCH     ANSE,CLEXSTAT
          GOTO      EXTR2000 IF NOT EQUAL
.
          PACK      KEY11,CLEXSTAT,CLEXTYPE,CLEXVISN
          CALL      DECLEX71
.
          GOTO      EXTR1500
.
EXTR2000  CALL      CREA0000
.
          CALL      EXED0000            * Extract E/D records
.
          CALL      EXOP0000            * Extract O/P records
.
          CALL      EXEV0000            * Extract O/P patient event records
.
          CALL      EXIP0000            * Extract I/P records
.
          DISPLAY   *P1:24,*V2LON,RECCNT," records extracted, ":
                           ERRFLG," errors found. ";
          CALL      HITENTER
.
          IF        ERRFLG <> 0
            PRINT     *1,"------------------------------------------":
                         "--------------------------------------" 
            PRINT     *N,*N,"Number of errors extracted : ",ERRFLG
          ENDIF
.
EXTR9999  RETURN
+
.**********************************************************************
.*                         RSET0000              Called by ML3000     *
.*                Reset Processed Records                             *
.**********************************************************************
RSET0000  MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL,*B,"Reset all processed records? (":
                                  *V2LON,ANSY,*HOFF,"/",*V2LON,ANSN,*HOFF,") ";
          KEYIN     *P36:24,*V2LON,ANS 
          REP       UPPLOW,ANS
.
          MATCH     ANSY,ANS
          GOTO      RSET0500 IF EQUAL
.
          MATCH     ANSN,ANS
          GOTO      RSET9999 IF EQUAL
.
          GOTO      RSET0000
.
RSET0500  PACK      KEY11,ANSE,SP20     * check for previously extracted records
          CALL      RSCLEX71
RSET1000  CALL      RKCLEX71
          BRANCH    OVRCD,RSET9000
.
          MATCH     ANSE,CLEXSTAT
          GOTO      RSET9000 IF NOT EQUAL
.
          PACK      KEY11,ANST,CLEXTYPE,CLEXVISN
          CALL      RDCLEX71
          IF        OVRCD = 0
            PACK      KEY11,ANSE,CLEXTYPE,CLEXVISN
            CALL      DECLEX71
            GOTO      RSET1000
          ENDIF
.
          PACK      KEY11,ANSE,CLEXTYPE,CLEXVISN
          CALL      RDCLEX71
          IF        OVRCD = 0
            MOVE      ANST,CLEXSTAT
            CALL      UPCLEX71
          ENDIF 
.
          PACK      KEY11,ANSE,CLEXTYPE,CLEXVISN
          CALL      RSCLEX71
          GOTO      RSET1000
.
RSET9000  DISPLAY   *P1:24,*EL,"Reset process complete. ";
          CALL      HITENTER
.
RSET9999  RETURN
+
.**********************************************************************
.*                         EXED0000              Called by EXTR2000   *
.*               Extract Emergency visit records                      *
.**********************************************************************
EXED0000  MOVE      " 1",STATUS 
          PACK      KEY11,ANST,STATUS,SP20
          CALL      RSCLEX71
EXED1000  CALL      RKCLEX71
          BRANCH    OVRCD,EXED9999
.
          COMPARE   ONE,CLEXTYPE
          GOTO      EXED9999 IF NOT EQUAL
.
          MOVE      CLEXVISN,KEY8
          CALL      RDEMVIS1
          IF        OVRCD=1
            MOVE      "Patient EMR visit details missing. ",ERRMSG
            CALL      PRTERR00
            GOTO      EXED1000
          ENDIF
.
          MATCH     SP8,EMVIPADM            * has patient been admitted?
          IF        !@EQUAL              
            PACK      KEY11,ANST,SP1,ONE,EMVIADMN
            MOVE      KEY11,SKEY11
            CALL      RDCLEX71
.
            MOVE      ANSE,CLEXSTAT
            CALL      UPCLEX71
            MOVE      SKEY11,KEY11
            CALL      RSCLEX71
            GOTO      EXED1000
          ENDIF
.
          MOVE      EMVIURNO,SAVURNO
.
          MATCH     CCCNX7SD,EMVIDATE
          IF        @LESS            
            PACK      KEY11,ANST,SP1,ONE,EMVIADMN
            MOVE      KEY11,SKEY11
            CALL      RDCLEX71
.
            MOVE      ANSE,CLEXSTAT
            CALL      UPCLEX71
            MOVE      SKEY11,KEY11
            CALL      RSCLEX71
            GOTO      EXED1000
          ENDIF
.
          DISPLAY   *P1:24,*EL:
                    *P32:24,"Extracting... [",*V2LON,EMVIADMN,*HOFF,"]"
          MOVE      EMVIADMN,TEMPADM
.
          CALL      CLEA0000      * Clear extract 7 variables
.
          MOVE      "MR ",X7RECTYP
          MOVE      EMVIADMN,DIM8
          REP       " 0",DIM8
          PACK      X7ENCNUM,ANSE,ANSM,DIM8,SP20
          MOVE      "B45  ",X7COMPCD
          MOVE      ANSE,X7INOUT
.
          MOVE      EMVIDATE,CPTDATE
          CALL      GPERD
          IF        EXIT = 0
            PACK      X7FSYEAR,DRGYR
            PACK      X7FSPERD,DRGNUM
          ENDIF
.
          MOVE      EMVIURNO,DIM8A
          LJUSTIFY  DIM8A
          PACK      X7MRNUMB,DIM8A,SP20
          RESET     X7MRNUMB
.
          PACK      X7ADMDTE,EMVIDATE,SP8
          UNPACK    EMVITIME,TMPTIME,DIM1
          PACK      X7ADMTIM,TMPTIME,SP6
          PACK      X7ADMDEP,EMVIUNIT,SP4
          PACK      X7REFSRC,EMVISRCE,SP4
          PACK      X7DISDTE,EMVIDDAT,SP8
          UNPACK    EMVIDTIM,TMPTIME2,DIM1
          PACK      X7DISTIM,TMPTIME2,SP6
          MOVE      "0001",X7LOS
          MOVE      ANSF,X7BILLNG
          PACK      X7PPAYOR,EMVICOMP,SP5
          PACK	     X7ADMTMD,EMVIDOCT,SP10
          PACK      X7REFMD,EMVIREFD,SP10
.
          MOVE      EMVIURNO,KEY8
          CALL      RDMAST1
          IF        OVRCD = 1
            MOVE      EMVIADMN,KEY8
            CALL      RDEMUNK1
            IF        OVRCD = 0
              PACK      X7DOB,EMUNBDAT,SP20
              STRIP     EMUNDET1
              PACK      DIM40,COMMA,EMUNDET2,SP30
              PACK      X7NAME,EMUNDET1,DIM40
              PACK      X7SEX,EMUNSEX,SP20
            ENDIF
          ELSE
            PACK      X7DOB,PBDATE,SP20
            STRIP     PSNAME
            PACK      DIM40,COMMA,PGNAME,SP30
            PACK      X7NAME,PSNAME,DIM40
            PACK      X7POSTCD,PPOST,SP9
            PACK      X7SEX,PSEX,SP20
            PACK      X7MSTAT,PMSTAT,SP5
            PACK      X7RELIGN,PREG,SP5
            PACK      X7SUBURB,PSUBRB,SP30
. 
            PACK      X7COB,PCONT,SP20
            PACK      X7MEDI,PMEDI,SP20
.
            CALL      RDPMPX21
            IF        OVRCD = 1
            ELSE
              PACK      X7RACE,PMPXABRG,SP20
.
              MATCH     "0",PMPXHOML
              IF        @EQUAL
                MOVE      ANSN,DIM1
              ELSE
                MOVE      ANSY,DIM1
              ENDIF
.
              MATCH     "1",PMPXINTR
              IF        !@EQUAL
                MOVE      ANSN,DIM1A
              ELSE
                MOVE      ANSY,DIM1A
              ENDIF
.
              PACK      X7HMELSS,DIM1,SP20
              PACK      X7INTERP,DIM1A,SP20
            ENDIF
          ENDIF
.
          CALL      WRITE000
          GOTO      EXED1000                  * srf 20343
.
EXED9999  RETURN
+
.**********************************************************************
.*                         EXOP0000              Called by EXTR2000   *
.*               Extract Outpatient visit records                     *
.**********************************************************************
EXOP0000  MOVE      " 2",STATUS
          PACK      KEY11,ANST,STATUS,SP20
          CALL      RSCLEX71
EXOP1000  CALL      RKCLEX71     
          BRANCH    OVRCD,EXOP9999
.
          COMPARE   TWO,CLEXTYPE
          GOTO      EXOP9999 IF NOT EQUAL
.
          MOVE      CLEXVISN,KEY8
          CALL      RDVISA1
          IF        OVRCD=1
            MOVE      "Patient visit details missing. ",ERRMSG
            CALL      PRTERR00
            GOTO      EXOP1000
          ENDIF
.
          MOVE      PVIURNO,SAVURNO
          MOVE      PVIBILL,SAVOUTNO
          MOVE      PVIDATE,SAVDATE
.
          MATCH     CCCNX7SD,PVIDATE
          IF        @LESS            
            PACK      KEY11,ANST,SP1,TWO,PVIBILL
            MOVE      KEY11,SKEY11
            CALL      RDCLEX71
.
            MOVE      ANSE,CLEXSTAT
            CALL      UPCLEX71
            MOVE      SKEY11,KEY11
            CALL      RSCLEX71
            GOTO      EXOP1000
          ENDIF
.
          DISPLAY   *P1:24,*EL:
                    *P32:24,"Extracting... [",*V2LON,PVIBILL,*HOFF,"]"
          MOVE      PVIBILL,TEMPADM
.
          CALL      CLEA0000    * Clear extract 7 variables
.
          MOVE      "MR ",X7RECTYP
          MOVE      "B45  ",X7COMPCD
          MOVE      ANSO,X7INOUT
.
          PACK      KEY36,PVIBILL,PVIDATE,SP70
          CALL      RDSBOKA6
EXOP2000  CALL      RDKBOKA6
          BRANCH    OVRCD,EXOP3000
.
          COMPARE   OBAOUTNO,CLEXVISN
          GOTO      EXOP3000 IF NOT EQUAL
.
          MATCH     PVIURNO,OBAURNO
          GOTO      EXOP3000 IF NOT EQUAL
.     
          MOVE      OBAOUTNO,DIM8
          REP       " 0",DIM8    
          PACK      X7ENCNUM,ANSO,ANSP,DIM8,SP20
.
          MOVE      OBADATE,CPTDATE
          CALL      GPERD
          IF        EXIT = 0
            PACK      X7FSYEAR,DRGYR
            PACK      X7FSPERD,DRGNUM
          ENDIF
.
          MOVE      OBAURNO,DIM8A
          LJUSTIFY  DIM8A  
          PACK      X7MRNUMB,DIM8A,SP20
.
          PACK      X7ADMDTE,OBADATE,SP8
          UNPACK    OBATIME,TMPTIME,DIM1
          PACK      X7ADMTIM,TMPTIME,SP6
.
          PACK      X7CLNTYP,OBACLIN,SP6
          PACK      X7DISDTE,OBADATE,SP8
.
          MOVE      OBAOUTNO,KEY8
          CALL      RDBOKB1
          BRANCH    OVRCD,EXOP3000
.
          PACK      X7ADMDEP,OTBBDEPT,SP4
          MOVE      "0001",X7LOS
          MOVE      ANSF,X7BILLNG
          PACK      X7PPAYOR,OBACOMP,SP5
          PACK      X7ADMTMD,OBADOCT,SP10
          PACK      X7REFMD,OTBBRFGP,SP10
.
          PACK      X7REFSRC,OBASRCR,SP4
.
EXOP3000  MOVE      PVIURNO,KEY8
          CALL      RDMAST1
          IF        OVRCD = 1
.            MOVE      "Patient Master details missing. ",ERRMSG
.            CALL      PRTERR00
.            GOTO      EXOP1000
          ELSE
            PACK      X7DOB,PBDATE,SP20
            STRIP     PSNAME
            PACK      DIM40,COMMA,PGNAME,SP30
            PACK      X7NAME,PSNAME,DIM40
            PACK      X7POSTCD,PPOST,SP9
            PACK      X7SEX,PSEX,SP20
            PACK      X7MSTAT,PMSTAT,SP5
            PACK      X7RELIGN,PREG,SP5
            PACK      X7SUBURB,PSUBRB,SP30
.
            PACK      X7COB,PCONT,SP20
            PACK      X7MEDI,PMEDI,SP20
          ENDIF
.
          CALL      RDPMPX21
          IF        OVRCD = 1
.            MOVE      "Patient Master Ext. details missing.	",ERRMSG
.            CALL      PRTERR00
.            GOTO      EXOP1000
          ELSE
            PACK      X7RACE,PMPXABRG,SP20
.
            MATCH     "0",PMPXHOML
            IF        @EQUAL
              MOVE      ANSN,DIM1
            ELSE
              MOVE      ANSY,DIM1
            ENDIF
.
            MATCH     "1",PMPXINTR
            IF        !@EQUAL
              MOVE      ANSN,DIM1A
            ELSE
              MOVE      ANSY,DIM1A
            ENDIF
.
            PACK      X7HMELSS,DIM1,SP20
            PACK      X7INTERP,DIM1A,SP20
          ENDIF
.
EXOP8000  CALL      WRITE000
          GOTO      EXOP1000
.
EXOP9999  RETURN
.
.**********************************************************************
.*                         EXEV0000              Called by EXTR2000   *
.*               Extract Outpatient event records                     *
.**********************************************************************
EXEV0000  MOVE      " 9",STATUS
          PACK      KEY11,ANST,STATUS,SP20
          CALL      RSCLEX71
EXEV1000  CALL      RKCLEX71     
          BRANCH    OVRCD,EXEV9999
.
          COMPARE   NINE,CLEXTYPE
          GOTO      EXEV9999 IF NOT EQUAL
.
          MOVE      CLEXVISN,KEY8
          CALL      RDVISA1
          IF        OVRCD=1
            MOVE      "Patient visit details missing. ",ERRMSG
            CALL      PRTERR00
            GOTO      EXEV1000
          ENDIF
.
          MATCH     CCCNX7SD,PVIDATE
          IF        @LESS            
            PACK      KEY11,ANST,SP1,NINE,PVIBILL
            MOVE      KEY11,SKEY11
            CALL      RDCLEX71
.
            MOVE      ANSE,CLEXSTAT
            CALL      UPCLEX71
            MOVE      SKEY11,KEY11
            CALL      RSCLEX71
            GOTO      EXEV1000
          ENDIF
.
          COMPARE   NINE,PVITYPE
          GOTO      EXEV1000 IF NOT EQUAL
.
          MATCH     " 1",PVISYST
          GOTO      EXEV1000 IF NOT EQUAL
.
.....  Patient Event Details
          MOVE      PVIBILL,KEY8
          CALL      RDPMEVT1
          IF        OVRCD = 1
            MOVE      "Patient Event details missing. ",ERRMSG
            CALL      PRTERR00
            GOTO      EXEV1000
          ENDIF
.
          DISPLAY   *P1:24,*EL:
                    *P32:24,"Extracting... [",*V2LON,PVIBILL,*HOFF,"]"
          MOVE      PVIBILL,TEMPADM
.
          MOVE      PVIURNO,SAVURNO
.
          CALL      CLEA0000    * Clear extract 7 variables
.
          MOVE      "MR ",X7RECTYP
          MOVE      "B45  ",X7COMPCD
          MOVE      ANSO,X7INOUT
.
          MOVE      PVIBILL,DIM8
          REP       " 0",DIM8    
          PACK      X7ENCNUM,ANSO,ANSP,DIM8,SP20
.
          MOVE      PVIURNO,DIM8A
          LJUSTIFY  DIM8A
          PACK      X7MRNUMB,DIM8A,SP20
.
          PACK      X7ADMDTE,PMEVADTE,SP8
          UNPACK    PMEVATIM,TMPTIME,DIM1
          PACK      X7ADMTIM,TMPTIME,SP6
          PACK      X7ADMDEP,PMEVUNIT,SP4
          PACK      X7CLNTYP,PMEVCLID,SP6
          PACK      X7REFSRC,PMEVBTYP,Sp4
          PACK      X7DISDTE,PMEVDDTE,SP8
          UNPACK    PMEVDTIM,TMPTIME2,DIM1
          PACK      X7DISTIM,TMPTIME2,SP6
.
          MOVE      PMEVADTE,CPTDATE
          CALL      GPERD
          IF        EXIT = 0
            PACK      X7FSYEAR,DRGYR
            PACK      X7FSPERD,DRGNUM
          ENDIF
.
          MOVE      "0001",X7LOS
.
          MOVE      ANSF,X7BILLNG
          PACK      X7PPAYOR,PMEVCCOD,SP5
          PACK      X7ADMTMD,PMEVACON,SP10
          PACK      X7REFMD,PMEVREFB,SP10
.
          MOVE      PVIURNO,KEY8
          CALL      RDMAST1
          IF        OVRCD = 1
.            MOVE      "Patient Master details missing. ",ERRMSG
.            CALL      PRTERR00
.            GOTO      EXEV1000
          ELSE
            PACK      X7DOB,PBDATE,SP20
            STRIP     PSNAME
            PACK      DIM40,COMMA,PGNAME,SP30
            PACK      X7NAME,PSNAME,DIM40
            PACK      X7POSTCD,PPOST,SP9
            PACK      X7SEX,PSEX,SP20
            PACK      X7MSTAT,PMSTAT,SP5
            PACK      X7RELIGN,PREG,SP5
            PACK      X7SUBURB,PSUBRB,SP30
.
            PACK      X7COB,PCONT,SP20
            PACK      X7MEDI,PMEDI,SP20
          ENDIF
.
          CALL      RDPMPX21
          IF        OVRCD = 1
.            MOVE      "Patient Master Ext. details missing.	",ERRMSG
.            CALL      PRTERR00
.            GOTO      EXEV1000
          ELSE
            PACK      X7RACE,PMPXABRG,SP20
.
            MATCH     "0",PMPXHOML
            IF        @EQUAL
              MOVE      ANSN,DIM1
            ELSE
              MOVE      ANSY,DIM1
            ENDIF
.
            MATCH     "1",PMPXINTR
            IF        !@EQUAL
              MOVE      ANSN,DIM1A
            ELSE
              MOVE      ANSY,DIM1A
            ENDIF
.
            PACK      X7HMELSS,DIM1,SP20
            PACK      X7INTERP,DIM1A,SP20
          ENDIF
.
EXEV8000  CALL      WRITE000
          GOTO      EXEV1000
.
EXEV9999  RETURN
+
.**********************************************************************
.*                         EXIP0000              Called by EXTR2000   *
.*               Extract Inpatient visit records                      *
.**********************************************************************
EXIP0000  MOVE      " 3",STATUS
          PACK      KEY11,ANST,STATUS,SP20
          CALL      RSCLEX71
EXIP1000  CALL      RKCLEX71     * then read forward to reposition at start
          BRANCH    OVRCD,EXIP9999
.
          COMPARE   THREE,CLEXTYPE
          GOTO      EXIP9999 IF NOT EQUAL
.
          MOVE      CLEXVISN,KEY8
          CALL      RDVISA1
          IF        OVRCD=1
            MOVE      "Patient visit details missing. ",ERRMSG
            CALL      PRTERR00
            GOTO      EXIP1000
          ENDIF
.
          MOVE      PVIURNO,SAVURNO
.
          MATCH     CCCNX7SD,PVIDATE
          IF        @LESS            
            PACK      KEY11,ANST,SP1,THREE,PVIBILL
            MOVE      KEY11,SKEY11
            CALL      RDCLEX71
.
            MOVE      ANSE,CLEXSTAT
            CALL      UPCLEX71
            MOVE      SKEY11,KEY11
            CALL      RSCLEX71
            GOTO      EXIP1000
          ENDIF
.
          DISPLAY   *P1:24,*EL:
                    *P32:24,"Extracting... [",*V2LON,PVIBILL,*HOFF,"]"
          MOVE      PVIBILL,TEMPADM
.
          CALL      CLEA0000      * Clear extract 7 variables
.
          MOVE      "MR ",X7RECTYP
          MOVE      "B45  ",X7COMPCD
          MOVE      ANSI,X7INOUT
.
.... Admission Variables
          MOVE      CLEXVISN,KEY8
          CALL      RDMISS1 
          BRANCH    OVRCD,EXIP1000
.
          PACK      X7ADMDTE,ADATE,SP8
          UNPACK    ATIME,TMPTIME,DIM1
          PACK      X7ADMTIM,TMPTIME,SP6
          PACK      X7REFSRC,ASRCE,SP4
          PACK      X7PPAYOR,ACLAIM,SP5
          PACK      X7REFMD,PMVXRHC1,SP10
          PACK      X7BTHWGT,ABWGHT,SP4
.>>>>     PACK      X7PATTYP,ACLSS,ACLAIM
          PACK      X7ADMTYP,ACLSS,SP5
.
          PACK      KEY5,CATCC,ACARE
          CALL      RDCODE1
          IF        OVRCD = 0
            PACK      X7CARETY,TDESC,SP20
          ENDIF
.
          IF        AVISIT = 1
            PACK      X7PASTOR,ANSY,SP20
          ELSE
            IF        AVISIT = 2
              PACK      X7PASTOR,ANSN,SP20
            ENDIF
          ENDIF
.
          MOVE      AADMNO,KEY8
          CALL      RDPMVX11
          IF        OVRCD = 1
.            MOVE      "Patient Visit Extn. details missing. ",ERRMSG
.            CALL      PRTERR00
.            GOTO      EXIP1000
          ELSE
            PACK      X7TRNSPT,PMVXMDAV,SP20
            PACK      X7READ30,PMVXIRAD,SP20
.
            PACK      KEY5,CATVB,PMVXCADM
            CALL      RDCODE1
            IF        OVRCD = 0
              PACK      X7CRIADM,TDESC,SP20
            ENDIF
.
            PACK      KEY5,CATVI,PMVXIDUS
            CALL      RDCODE1
            IF        OVRCD = 0
              PACK      X7ILOS,TDESC,SP20
            ENDIF
          ENDIF
.
.... Discharge Variables
          MOVE      AADMNO,KEY8
          CALL      RDDSCH1
          IF        OVRCD = 0 
. 
            MOVE      DADMNO,DIM8
            REP       " 0",DIM8
            PACK      X7ENCNUM,ANSI,ANSP,DIM8,SP20
.
.... Calculate Fiscal year and period
            MOVE      DDATE,CPTDATE
            CALL      GPERD
            IF        EXIT = 0
              PACK      X7FSYEAR,DRGYR
              PACK      X7FSPERD,DRGNUM
            ENDIF
.
            MOVE      DURNO,DIM8A
            LJUSTIFY  DIM8A
            PACK      X7MRNUMB,DIM8A,SP20
.
            PACK      X7DISDTE,DDATE,SP20
            UNPACK    DTIME,TMPTIME2,DIM1
            PACK      X7DISTIM,TMPTIME2,SP20
            PACK      X7DRG,PTDSDDRG,SP20
            PACK      X7MDC,PTDSDMDC,SP20
            PACK      X7DSCDIS,DDEST,SP20
          ENDIF
.
.... Patient master variables
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          IF        OVRCD = 1
.            MOVE      "Patient Master details missing. ",ERRMSG
.            CALL      PRTERR00
.            GOTO      EXIP1000
          ELSE
            PACK      X7DOB,PBDATE,SP20
            PACK      DIM40,COMMA,PGNAME,SP30
            STRIP     PSNAME
            PACK      DIM40,COMMA,PGNAME,SP30
            PACK      X7NAME,PSNAME,DIM40
            PACK      X7POSTCD,PPOST,SP9
            PACK      X7SEX,PSEX,SP20
            PACK      X7MSTAT,PMSTAT,SP5
            PACK      X7RELIGN,PREG,SP5
            PACK      X7SUBURB,PSUBRB,SP30
.
            PACK      X7COB,PCONT,SP20
            PACK      X7MEDI,PMEDI,SP20
          ENDIF
.
.... Patient Master extention 2 variables
          MOVE      PURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD = 1
.            MOVE      "Patient Master Ext. details missing.	",ERRMSG
.            CALL      PRTERR00
.            GOTO      EXIP1000
          ELSE
            PACK      X7RACE,PMPXABRG,SP20
.
            MATCH     "0",PMPXHOML
            IF        @EQUAL
              MOVE      ANSN,DIM1
            ELSE
              MOVE      ANSY,DIM1
            ENDIF
.
            MATCH     "1",PMPXINTR
            IF        !@EQUAL
              MOVE      ANSN,DIM1A
            ELSE
              MOVE      ANSY,DIM1A
            ENDIF
.
            PACK      X7HMELSS,DIM1,SP20
            PACK      X7INTERP,DIM1A,SP20
          ENDIF
.
          CALL      LEAV0000
.
.... Transfer Variables
          PACK      KEY30,CLEXVISN,SP70
          CALL      RDSTRAN2
EXIP2000  CALL      RDKTRAN2
          BRANCH    OVRCD,EXIP2500
.
          COMPARE   TADMN,AADMNO
          GOTO      EXIP2500 IF NOT EQUAL
.
          MATCH     ANSA,TMOVE
          IF        @EQUAL
            PACK      X7PATTYP,ACLAIM,TATYPE
.>>>>       PACK      X7ADMTYP,TATYPE,SP20
            PACK      X7ADMDEP,TDEPT,SP20
            PACK      X7ADMTMD,TADOCT,SP20
          ENDIF
.
          MATCH     ANSD,TMOVE
          IF        @EQUAL
            PACK      X7DSCMD,TADOCT,SP20
            PACK      X7DSCSRV,TWARD,SP20
            PACK      X7DSCDEP,TDEPT,SP20
          ENDIF
.
          GOTO      EXIP2000
.
.
.... Patient Intensive Care Unit
EXIP2500  MOVE      AADMNO,KEY8
          CALL      RDPTICU1
          IF        OVRCD = 0
            MOVE      PTICUINT,FORM4        * (form 5 to form 4)      *C-250267
            MOVE      FORM4,DIM4
            LJUSTIFY  DIM4
            PACK      X7ICUHRS,DIM4,SP20
.
            MOVE      PTICUCCU,FORM4        * (form 5 to form 4)      *C-250267
            MOVE      FORM4,DIM4A
            LJUSTIFY  DIM4A
            PACK      X7CCUHRS,DIM4A,SP20
.
            MOVE      PTICUMEC,FORM4        * (form 5 to form 4)      *C-250267
            MOVE      FORM4,DIM4B
            LJUSTIFY  DIM4B
            PACK      X7DCMV,DIM4B,SP20,SP20
          ENDIF
.
.... Patient Episode and Disease Code file
          MOVE      ZERO,COUNT
          PACK      KEY13,AADMNO,SP70
          CALL      RSPTECD1
EXIP3000  CALL      RKPTECD1
          BRANCH    OVRCD,EXIP4000
.
          COMPARE   AADMNO,PTEDADMN
          GOTO      EXIP4000 IF NOT EQUAL
.
          ADD       ONE,COUNT
.
          IF        PTEDEPNO = 1 
            IF        PTEDCNT = 1
              PACK      X7PDIAG,PTEDCODE,SP20
              MOVE      PTEDCODE,SAVCODE
              GOTO      EXIP3000
            ENDIF
.
            IF        PTEDCNT = 2 
              PACK      X7SDIAGA,PTEDCODE,SP20
              GOTO      EXIP3000
            ENDIF
.
            IF        PTEDCNT = 3
              PACK      X7SDIAGB,PTEDCODE,SP20
              GOTO      EXIP3000
            ENDIF
.
            IF        PTEDCNT = 4
              PACK      X7SDIAGC,PTEDCODE,SP20
              GOTO      EXIP3000
            ENDIF
.
            IF        PTEDCNT = 5
              PACK      X7SDIAGD,PTEDCODE,SP20
              GOTO      EXIP3000
            ENDIF
.
            IF        PTEDCNT = 6
              PACK      X7SDIAGE,PTEDCODE,SP20
              GOTO      EXIP3000
            ENDIF
.
            IF        PTEDCNT = 7
              PACK      X7SDIAGF,PTEDCODE,SP20
              GOTO      EXIP3000
            ENDIF
.
            IF        PTEDCNT = 8
              PACK      X7SDIAGG,PTEDCODE,SP20
              GOTO      EXIP3000
            ENDIF
.
            IF        PTEDCNT = 9
              PACK      X7SDIAGH,PTEDCODE,SP20
              GOTO      EXIP3000
            ENDIF
.
            IF        PTEDCNT = 10
              PACK      X7SDIAGI,PTEDCODE,SP20
              GOTO      EXIP3000
            ENDIF
.
            IF        PTEDCNT = 11
              PACK      X7SDIAGJ,PTEDCODE,SP20
              GOTO      EXIP3000
            ENDIF
          ENDIF
.
          GOTO      EXIP3000
.
EXIP4000  PACK      KEY13,AADMNO,SP20
          CALL      RSPTECD1
EXIP4500  CALL      RKPTECD1
          BRANCH    OVRCD,EXIP4800 
.
          COMPARE   AADMNO,PTEOADMN
          GOTO      EXIP4800 IF NOT EQUAL
.
          MOVE      ADATE,CDYSFDTE
          MOVE      PTEODATE,CDYSTDTE
          CALL      CALCDAYS
.          SUB       ONE,CDYSDAYS
          SUB       FORM3,CDYSDAYS
          MOVE      CDYSDAYS,FORM3
          MOVE      FORM3,X7PREOPD
          REP       " 0",X7PREOPD
.

          IF        PTEOEPNO = 1 
            IF        PTEOCNT = 1
              PACK      X7PPROC,PTEOCODE,SP20
              GOTO      EXIP4500
            ENDIF
.
            IF        PTEOCNT = 2 
              PACK      X7SPROCA,PTEOCODE,SP20
              GOTO      EXIP4500
            ENDIF
.
            IF        PTEOCNT = 3
              PACK      X7SPROCB,PTEOCODE,SP20
              GOTO      EXIP4500
            ENDIF
.
            IF        PTEOCNT = 4
              PACK      X7SPROCC,PTEOCODE,SP20
              GOTO      EXIP4500
            ENDIF
.
            IF        PTEOCNT = 5
              PACK      X7SPROCD,PTEOCODE,SP20
              GOTO      EXIP4500
            ENDIF
.
            IF        PTEOCNT = 6
              PACK      X7SPROCE,PTEOCODE,SP20
              GOTO      EXIP4500
            ENDIF
.
            IF        PTEOCNT = 7
              PACK      X7SPROCF,PTEOCODE,SP20
              GOTO      EXIP4500
            ENDIF
.
            IF        PTEOCNT = 8
              PACK      X7SPROCG,PTEOCODE,SP20
              GOTO      EXIP4500
            ENDIF
.
            IF        PTEOCNT = 9
              PACK      X7SPROCH,PTEOCODE,SP20
              GOTO      EXIP4500
            ENDIF
.
            IF        PTEOCNT = 10
              PACK      X7SPROCI,PTEOCODE,SP20
              GOTO      EXIP4500
            ENDIF
.
            IF        PTEOCNT = 11
              PACK      X7SPROCJ,PTEOCODE,SP20
              GOTO      EXIP4500
            ENDIF
          ENDIF
.
          GOTO      EXIP4500
.
EXIP4800  CALL      RPPTECD1      * Read back one record for last OP on file
          COMPARE   AADMNO,PTEOADMN
          GOTO      EXIP5000 IF NOT EQUAL
.
          MOVE      ADATE,CDYSFDTE
          MOVE      PTEODATE,CDYSTDTE 
          CALL      CALCDAYS
.          SUB       ONE,CDYSDAYS
          SUB       FORM3,CDYSDAYS
          MOVE      CDYSDAYS,FORM3
          MOVE      FORM3,X7PROOPD
          REP       " 0",X7PROOPD
.
EXIP5000  PACK      KEY19,PURNO,X7PPROC,SP1,ONE
          CALL      RDTREA1
          BRANCH    OVRCD,EXIP5500
.
          PACK      X7DATEWL,WMDATE1,SP20
          PACK      X7WLCAT,WMPTY,SP20
          PACK      X7PREAD,WTWMREAD,SP20
.
EXIP5500  MOVE      ZERO,COUNT 
          PACK      KEY31,AADMNO,SP70
          CALL      RSOPDEA2
EXIP6000  CALL      RKOPDEA2
          BRANCH    OVRCD,EXIP6500
.
          MOVE      AADMNO,DIM8
          MATCH     DIM8,OPDAADMN
          GOTO      EXIP6500 IF NOT EQUAL
.
          ADD       ONE,COUNT
.
          IF        COUNT = 1
            MOVE      OPDAUNIQ,DIM10
            PACK      X7PSURGN,OPDACLIN,SP20
            PACK      X7THEATR,OPDATHET,SP20
            PACK      X7OPDTE1,OPDADATE,SP20
          ENDIF
.
          IF        COUNT = 2
            PACK      X7OPDTE2,OPDADATE,SP20
          ENDIF
.
          IF        COUNT = 3
            PACK      X7OPDTE3,OPDADATE,SP20
          ENDIF
.
          IF        COUNT = 4
            PACK      X7OPDTE4,OPDADATE,SP20
          ENDIF
.
          IF        COUNT = 5
            PACK      X7OPDTE5,OPDADATE,SP20
          ENDIF
.
          GOTO      EXIP6000
.
EXIP6500  PACK      KEY35,DIM10,SP70
          CALL      RSOPTSM1
          CALL      RKOPTSM1
          BRANCH    OVRCD,EXIP7000
.
          MATCH     DIM10,OPTSUNID
          GOTO      EXIP7000 IF NOT EQUAL
.
          MATCH     "1",OPTSTMNO
          IF        @EQUAL
            COMPARE   TWO,OPTSSIND
            IF        @EQUAL
              PACK      X7PANEST,OPTSSCDE,SP20
            ENDIF
          ENDIF
.
EXIP7000  PACK      KEY10,AADMNO,SP10
          CALL      RSPTRDE1
          CALL      RKPTRDE1
          BRANCH    OVRCD,EXIP7500
.
          COMPARE   AADMNO,PTRDADMN
          GOTO      EXIP7500 IF NOT EQUAL
.
          PACK      X7ADMBAR,PTRDBISA,SP20
          PACK      X7ADMBAR,PTRDCLSP,SP20
.
EXIP7500  PACK      KEY25,AADMNO,SP20
          CALL      RSPTASF1
          CALL      RKPTASF1
          BRANCH    OVRCD,EXIP8000
.
          COMPARE   AADMNO,PTAFADMN
          GOTO      EXIP8000 IF NOT EQUAL
.
          PACK      X7SPFNDA,PTAFCODE,SP10
.
EXIP8000  MATCH     SP8,DDATE
          IF        @EQUAL
            MOVE      "1",X7BILLNG
          ELSE
            MATCH     SP8,SAVCODE 
            IF        @EQUAL
              MOVE      ANST,X7BILLNG
            ELSE
              MOVE      ANSF,X7BILLNG
            ENDIF
          ENDIF
.
          CALL      WRITE000
          GOTO      EXIP1000
.
EXIP9999  RETURN
+
.**********************************************************************
.*                         WRITE000                                   *
.*               Write to extract file                                *
.**********************************************************************
WRITE000  CALL      VALD0000
          BRANCH    EXIT,WRITE999
. 
          WRITE     CCIDAT,SEQ;*+,X7RECTYP,X7ENCNUM,X7SERENO,X7COMPCD,X7INOUT:
                                  X7FSYEAR,X7FSPERD,X7MRNUMB,X7SSECNO,X7ADMDTE:
                                  X7ADMTIM,X7ADMTYP,X7TRNSPT,X7BLANK,X7ADMDEP:
                                  X7CLNTYP,X7PATTYP,X7REFSRC,X7READ07,X7READ30:
                                  X7DISDTE,X7DISTIM,X7LOS,X7PREOPD,X7PROOPD:
                                  X7SPCARE,X7ICUHRS,X7CCUHRS,X7CHRGES,X7BILLNG:
                                  X7PPAYOR,X7ADMBAR,X7CLNSUB,X7FPAYOR,X7CMMPYR:
                                  X7DRG,X7MDC,X7ADDIAG,X7PDIAG,X7SDIAGA:
                                  X7SDIAGB,X7SDIAGC,X7SDIAGD,X7SDIAGE,X7SDIAGF:
                                  X7SDIAGG,X7SDIAGH,X7SDIAGI,X7SDIAGJ,X7PPROC:
                                  X7SPROCA,X7SPROCB,X7SPROCC,X7SPROCD,X7SPROCE:
                                  X7SPROCF,X7SPROCG,X7SPROCH,X7SPROCI,X7SPROCJ:
                                  X7ADMTMD,X7DATEWL,X7WLCAT,X7PREAD,X7REFMD:
                                  X7DSCMD,X7VCDRG4,X7PSURGN,X7PANEST,X7THEATR:
                                  X7OPDTE1,X7OPDTE2,X7OPDTE3,X7OPDTE4,X7OPDTE5:
                                  X7TRNFLG,X7DSCTYP,X7DSCDIS,X7DSCSRV,X7DSCDEP:
                                  X7EXPAUT,X7EXPCOR,X7EXPOPR,X7EXPANS,X7EXPPST:
                                  X7EXP48H,X7DOB,X7NBNFLG,X7BTHWGT,X7NAME:
                                  X7POSTCD,X7COUNTY,X7PHONEX,X7SEX,X7RACE:
                                  X7MSTAT,X7RELIGN,X7EMPLST,X7EMPLYR,X7GEMPLY:
                                  X7OCCUPN,X7SUBURB,X7COB,X7ARDRG4,X7ARMDC4:
                                  X7SPDYCR,X7HDUDAY,X7MEDI,X7READMI,X7CARETY:
                                  X7CRIADM,X7ILOS,X7HMELSS,X7PASTOR,X7INTERP:
                                  X7LVEDAY,X7PATCAT,X7DCMV,X7SPFNDA,X7VCDRG3:
                                  X7IPEPS
.
          PACK      KEY11,ANST,STATUS,TEMPADM
          MOVE      KEY11,SKEY11
          CALL      RDCLEX71
          BRANCH    OVRCD,WRITE999
.
          MOVE      ANSE,CLEXSTAT
          CALL      UPCLEX71
          MOVE      SKEY11,KEY11
          CALL      RSCLEX71
.
          ADD       ONE,RECCNT
.
WRITE999  RETURN
+
.**********************************************************************
.*                         VALD0000              Called by WRITE000   *
.*                    Validate tables                                 *
.**********************************************************************
VALD0000  MOVE      ZERO,EXIT
.
          MOVE      SAVURNO,KEY8
          CALL      RDMAST1
          IF        OVRCD = 1
            IF        CLEXTYPE = 1 
              MATCH     "       0",SAVURNO
              GOTO      VALD0100 IF NOT EQUAL
.
              MOVE      CLEXVISN,KEY8
              CALL      RDEMUNK1
              IF        OVRCD=1
                MOVE      "Emergency unknown patient details missing. ",ERRMSG
                CALL      PRTERR00
              ENDIF
            ELSE
              GOTO    VALD0100
            ENDIF
          ELSE
            GOTO      VALD0500          
          ENDIF
.
          GOTO      VALD0600
.
VALD0100  MOVE      "Patient Master details missing. ",ERRMSG
          CALL      PRTERR00
          GOTO      VALD0500 
.
VALD0500  CALL      RDPMPX21
          IF        OVRCD = 1
            MOVE      "Patient Master Ext. details missing. ",ERRMSG
            CALL      PRTERR00
          ENDIF
.  
VALD0600  IF        CLEXTYPE = 3
            MOVE      CLEXVISN,KEY8
            CALL      RDPMVX11
            IF        OVRCD = 1
              MOVE      "Patient Visit Extn. details missing. ",ERRMSG
              CALL      PRTERR00
            ENDIF
          ENDIF
.
          IF        CLEXTYPE = 2
            MOVE      ZERO,OPERROR
            PACK      KEY36,SAVOUTNO,SAVDATE,SP70
            CALL      RDSBOKA6
VALD1000    CALL      RDKBOKA6 
            BRANCH    OVRCD,VALD2000
.             
            COMPARE   OBAOUTNO,SAVOUTNO
            GOTO      VALD2000 IF NOT EQUAL
.           
            MATCH     OBAURNO,SAVURNO
            GOTO      VALD2000 IF NOT EQUAL
.
            MOVE      ONE,OPERROR
.
VALD2000    BRANCH    OPERROR,VALD3000
.
            MOVE      "Outpatient Table A details missing. ",ERRMSG
            CALL      PRTERR00
.
VALD3000    MOVE      SAVOUTNO,KEY8
            CALL      RDBOKB1
            IF        OVRCD = 1
              MOVE      "Outpatient Table B details missing. ",ERRMSG
              CALL      PRTERR00
            ENDIF
          ENDIF
.
VALD9999  RETURN
+
.**********************************************************************
.*                         CREA0000              Called by EXTR1000   *
.*               Create the ASCII file CCIDAT.txt                     *
.**********************************************************************
CREA0000  PACK      KEY40,CCCNX7FP,FNAME,SP30,SP30
          STRIP     KEY40
          PREP      CCIDAT,KEY40
          CLOSE     CCIDAT,DELETE           * srf 20343
          PREP      CCIDAT,KEY40

CREA9999  RETURN
+
.**********************************************************************
.*                         CLEA0000              Called by ML0000     *
.*        Clear all extract 7 variables                               *
.**********************************************************************
CLEA0000  UNPACK    SP70,X7RECTYP,X7ENCNUM,X7SERENO
          UNPACK    SP70,X7COMPCD,X7INOUT
          UNPACK    SP70,X7FSYEAR,X7FSPERD
          UNPACK    SP70,X7MRNUMB,X7SSECNO,X7ADMDTE
          UNPACK    SP70,X7ADMTIM,X7ADMTYP
          UNPACK    SP70,X7TRNSPT,X7BLANK,X7ADMDEP
          UNPACK    SP70,X7CLNTYP,X7PATTYP
          UNPACK    SP70,X7REFSRC,X7READ07,X7READ30
          UNPACK    SP70,X7DISDTE,X7DISTIM
          MOVE      "0000",X7LOS
          MOVE      "000",X7PREOPD
          MOVE      "000",X7PROOPD
          UNPACK    SP70,X7SPCARE,X7ICUHRS
          UNPACK    SP70,X7CCUHRS,X7CHRGES,X7BILLNG
          UNPACK    SP70,X7PPAYOR,X7ADMBAR
          UNPACK    SP70,X7CLNSUB,X7FPAYOR,X7CMMPYR
          UNPACK    SP70,X7DRG,X7MDC,X7ADDIAG
          UNPACK    SP70,X7PDIAG,X7SDIAGA,X7SDIAGB
          UNPACK    SP70,X7SDIAGC,X7SDIAGD
          UNPACK    SP70,X7SDIAGE,X7SDIAGF,X7SDIAGG
          UNPACK    SP70,X7SDIAGH,X7SDIAGI
          UNPACK    SP70,X7SDIAGJ,X7PPROC,X7SPROCA
          UNPACK    SP70,X7SPROCB,X7SPROCC
          UNPACK    SP70,X7SPROCD,X7SPROCE,X7SPROCF
          UNPACK    SP70,X7SPROCG,X7SPROCH
          UNPACK    SP70,X7SPROCI,X7SPROCJ,X7ADMTMD
          UNPACK    SP70,X7DATEWL,X7WLCAT
          UNPACK    SP70,X7PREAD
          UNPACK    SP70,X7REFMD,X7DSCMD
          UNPACK    SP70,X7VCDRG4,X7PSURGN
          UNPACK    SP70,X7PANEST,X7THEATR,X7OPDTE1
          UNPACK    SP70,X7OPDTE2,X7OPDTE3,X7OPDTE4
          UNPACK    SP70,X7OPDTE5,X7TRNFLG
          UNPACK    SP70,X7DSCTYP,X7DSCDIS,X7DSCSRV
          UNPACK    SP70,X7DSCDEP,X7EXPAUT
          UNPACK    SP70,X7EXPCOR,X7EXPOPR
          UNPACK    SP70,X7EXPANS,X7EXPPST,X7EXP48H
          UNPACK    SP70,X7DOB,X7NBNFLG,X7BTHWGT
          UNPACK    SP70,X7NAME,X7POSTCD,X7COUNTY
          UNPACK    SP70,X7PHONEX,X7SEX,X7RACE
          UNPACK    SP70,X7MSTAT,X7RELIGN
          UNPACK    SP70,X7EMPLST,X7EMPLYR
          UNPACK    SP70,X7GEMPLY,X7OCCUPN,X7SUBURB
          UNPACK    SP70,X7COB,X7ARDRG4,X7ARMDC4
          UNPACK    SP70,X7SPDYCR,X7HDUDAY
          UNPACK    SP70,X7MEDI,X7READMI,X7CARETY
          UNPACK    SP70,X7CRIADM,X7ILOS
          UNPACK    SP70,X7HMELSS,X7PASTOR,X7INTERP
          UNPACK    SP70,X7LVEDAY,X7PATCAT
          UNPACK    SP70,X7DCMV,X7SPFNDA
          UNPACK    SP70,X7VCDRG3,X7IPEPS
          UNPACK    SP70,DIM1,DIM1A,DIM8,DIM8A,TMPTIME,TMPTIME2
.
CLEA9999  RETURN
+
.**********************************************************************
.*                           LEAV0000                                 *
.*         Calulate the number of days a patient was on leave         *
.**********************************************************************
.         
LEAV0000  MOVE      ZERO,FORM3
          PACK      KEY30,AADMNO,SP30
          CALL      RDSTRAN2
LEAV1000  CALL      RDKTRAN2 
          BRANCH    OVRCD,LEAV4000
.
          COMPARE   AADMNO,TADMN
          GOTO      LEAV4000 IF NOT EQUAL
.
          MATCH     ANSD,TMOVE
          GOTO      LEAV4000 IF EQUAL
.
          MATCH     ANSL,TMOVE
          GOTO      LEAV1000 IF NOT EQUAL
.
          PACK      CDYSFDTE,TDATE
          REP       " 0",CDYSFDTE
.
LEAV2000  CALL      RDKTRAN2
          BRANCH    OVRCD,LEAV4000          * If over use todays date
.
          COMPARE   AADMNO,TADMN
          GOTO      LEAV4000 IF NOT EQUAL
.
          MATCH     ANSR,TMOVE
          GOTO      LEAV2000 IF NOT EQUAL
.
          PACK      CDYSTDTE,TDATE
          REP       " 0",CDYSTDTE
.
          CALL      CALCDAYS
          IF        CDYSDAYS > 0
            SUB       ONE,CDYSDAYS
          ENDIF
          ADD       CDYSDAYS,FORM3
          GOTO      LEAV1000
.
.-----  Only set up the leave days value if it is not zero ------
.
LEAV4000  IF        FORM3 > 0
            MOVE      FORM3,DIM3
            LJUSTIFY  DIM3
            PACK      X7LVEDAY,DIM3,SP20
          ENDIF
.
LEAV5000  MOVE      ADATE,CDYSFDTE
          MOVE      DDATE,CDYSTDTE
          CALL      CALCDAYS
          SUB       ONE,CDYSDAYS
          SUB       FORM3,CDYSDAYS
          IF        CDYSDAYS < 0
            MOVE      "0000",X7LOS
          ELSE
            MOVE      CDYSDAYS,FORM4
            MOVE      FORM4,X7LOS
            REP       " 0",X7LOS
          ENDIF
.
LEAV9999  RETURN
+
.**********************************************************************
.*                        PRTERR00                 called by ALL      *
.*                  Prints error messages                             *
.**********************************************************************
PRTERR00  IF        ERRFLG=0
            CALL      IBACLOCK
            CLOCK     TIME,CTIMEIS
            CALL      HEAD80
            PRINT     *N,"------------------------------------------":
                         "--------------------------------------":
                      *N,"| Visit No. | Visit Type | Description",*80,"|":
                      *N,"------------------------------------------":
                         "--------------------------------------"
          ENDIF
.
          LOAD      DIM10,CLEXTYPE,EMR,OP,IP
.
          PRINT     *1,"| ",CLEXVISN,*13,"| ",DIM10," | ",ERRMSG,*80,"|"
          ADD       ONE,ERRFLG
          MOVE      ONE,EXIT   
          MOVE      SP70,ERRMSG
.
PRTERR99  RETURN
.
.**********************************************************************
.*        I/O includes for files and general routines                 *
.**********************************************************************
.
          INC       STD001IO
          INC       STDKWSCD
          INC       STDHLPCD
          INC       PATM10T9
          INC       PATCOMN3
          INC       NHOSPDAY                 * bed day calculating routine
          INC       CALCAGE
          INC       CALCDAYS
.
          INC       CCICTYIO/INC
          INC       CCIDPTIO/INC
          INC       CCIERRIO/INC             * cci error record file
          INC       CCIEX7IO/INC
          INC       CCIUTLIO/INC
          INC       EOCLNKIO/INC             * EOC Link file
          INC       EOCREFIO/INC             * EOC referral file
          INC       EMRVISIO/INC
          INC       EMRUNKIO/INC
          INC       MEHDIAIO/INC
          INC       MEHLEGIO/INC
          INC       NHIMASIO/INC             * NHI master file
          INC       OUTBOAIO/INC             * clinic session booking file a
          INC       OUTBB1IO/INC             * clinic session booking file b
          INC       OUTCLIIO/INC             * outpatients clinic file
          INC       OUTRF1IO/INC             * O/P referral file (for eoc)
          INC       OUTSITIO/INC             * outpatients site code file
          INC       OPRDEAIO/INC
          INC       OPRTSMIO/INC
          INC       PATASFIO/INC
          INC       PATICUIO/INC             * icu file
          INC       PATCT1IO/INC
          INC       PATCMPIO/INC
          INC       PATCODIO/INC             * codes file
          INC       PATDADIO/INC             * adm/disc transfer dest. file
          INC       PATDO1IO/INC             * doctors file
          INC       PATDRGIO/INC             * period file
          INC       PATDSCIO/INC             * discharge file
          INC       PATECDIO/INC
          INC       PATECOIO/INC
          INC       PATHSPIO/INC             * hospital file
          INC       PATICDIO/INC             * icd-9cm diseases file
          INC       PATICOIO/INC
          INC       PATMA1IO/INC             * master file
          INC       PATMI1IO/INC             * admission file
          INC       PATNIDIO/INC             * national id file
          INC       PATODCIO/INC
          INC       PATRDEIO/INC
          INC       PATTRNIO/INC             * transfer file
          INC       PATVISIO/INC             * visit file
          INC       PATWR1IO/INC             * ward file
          INC       PMSEVTIO/INC             * patient event file
          INC       PMSPX2IO/INC
          INC       PMSVX1IO/INC
.
          INC       WATTR1IO/INC
.
