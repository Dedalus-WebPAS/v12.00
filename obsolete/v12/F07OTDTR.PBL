.*****************************************************************************
.*    Program      : F07OTDTR                                                *
.*    Description  : Conversion outdtrof to new File layout                  *
.*                                                                           *
.*    Author       : J.Tan                                                   *
.*    Date         : 27/10/2105                                              *
.*    Modifications:                                                         *
.*                   V10.08.01 23/05/2015 J.Tan    CAR 303942                *
.*                   Mods Checking for payment type                          *
.*                   V10.07.00 27/10/2015 J.Tan    CAR 303942                *
.*                   Created fixit for outdtrof                              *
.*                                                                           *
.*****************************************************************************
.
.  using vi Global change command      :%s/FromString/ToString/g
.
.  Global change F07OTDTR  eg F57PTDOC
.  Global change OUTDTRFD  eg PATDOCFD (new layout)
.  Global change outdtrof  eg patdoctf
.  Global change OUTDTRO1  eg PATDOCT1
.  Global change KEY22     eg KEY6 
.  Global change WROTDTR   eg WRDOCT1
.  Global change sotdtrof  eg sptdoctf
.
.  Global changes if Oracle Option is required -
.
.  Global Change Yes  to Yes
.  Global change RDSDTRO   eg RSPTDOC1
.  Global change RDKDTRO   eg RKPTDOC1
.  Global change UPDTRO    eg UPPTDOC1
.
.  Global changes if Oracle Option is NOT required -

.  Global Change No  to No
.  Global change RDSDTRO   to OFDUMYIO
.  Global change RDKDTRO   to OFDUMYIO
.  Global change UPDTRO    to OFDUMYIO
.
          INC       STD001FD
          INC       PATCODFD/INC
.
FINDFILE  FILE      ASCII, FIXED=256
.
OLDFILE1   IFILE  SQL, FIXED=229, KEY="U1-8,9-16,17-22"
.
. NAME     TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION
. ----     ----   ------   --------  ----------   -----------
.DOTNUMB     DIM       8          8         1    OUTPATIENT NUMBER
.OTINVNO     DIM       8          8         9    (INVOICE #)
.DOTTRANS    DIM       6          6        17    TRANSACTION NUMBER
.
.OTDDESC     DIM      30         30        23    DESCRIPTION
.OTPATAMT    FORM   12.2          8        53    TOTAL AMOUNT
.OTDDATE     DIM       8          8        61    DATE CCYYMMDD
.
.OTITEMNO    DIM       9          9        69    ITEM NUMBER
.
.OTTYPE      DIM       2          2        78    TRANSACTION TYPE  PY=PAYMENTS
.                                                                   DB=CHARGES
.                                                                   xx=JOURNALS
.
.OTPAYTYP    FORM      1           2       80    PAYMENT METHOD
.                                                0 = NOT APPLICABLE
.                                                1 = CASH
.                                                2 = CHEQUE
.                                                3 = CREDIT CARD
.                                                4 = MERCHANT CARDS EG AMEX
.                                                5 = MONEY ORDER
.
.OTRECEPT   DIM      12          12       82     RECEIPT NUMBER
.
.OTINVPRT   FORM      1           2       94     0=INVOICE NOT YET PRINTED
.                                                1=INVOICE PRINTED
.
.DOTRECTY   DIM       1           1       96     RECORD TYPE 1 - MISC ITEM
.                                                            2 - CASH RECEIPT
.                                                            3 - JOURNALS
.                                                            4 - DOCTOR FEES
.                                                            5 - DISCOUNT
.
.          These next two variables are updated automatically by the
.          I/O include for each write or update
.
.OTCHGDTE   DIM       8           8       97     DATE THIS RECORD LAST CHANGED
.OTCHGTIM   DIM       8           8      105     TIME THIS RECORD LAST CHANGED
.
.OTMISGRP   DIM       3           3      113     MISCELLANEOUS GROUP (Cat FI)
.                                                JH-Misc.Item Group (Cat FN)
.OTDEPTYP   DIM       3           3      116     DEPOSIT TYPE(BLANK IF NOT DEP)
.OTBATCHN   DIM       8           8      119     BTCH NO FOR MISC.ITMS,CASH,JOUR
.                                                JH-Misc.Item Group (Cat FI)
.OTPATPOR   FORM   12.2           8      127     PATIENT PORTION OF CHARGE
.OTREBPOR   FORM   12.2           8      135     REBATE PORTION OF CHARGE
.DOTNINVS   DIM       1           1      143     No. of invoices to be printed
.OTSERVS    FORM      5           4      144     Number of services
.OTDTGSTA   FORM      1           2      148     Is GST Applicable to this item
.                                                 0-No GST, 1-Yes, 2-Unknown GST
.OTDTGSTC   DIM       6           6      150     GST Payable code
.OTDTGSTM   FORM    12.2          8      156     GST Amount
.OTDTEFFD   DIM       8           8      164     Date item assigned to Fees Inv.
.OTDTCRST   DIM       1           1      172     Credit Note status
.                                                 0 - No Credit Note
.                                                 1 - Fully Credited
.                                                 2 - Credit by Item
.OTDTBTCH   DIM       16         16      173     Batch Details (NZ Priv Exract)
.                                                format (99999XXXCCYYMMDD)
.                                                where 99999 = G/L Batch No.
.                                                      XXX   = Hospital Code
.                                                      CCYYMMDD = Extract.date
.OTSPARE    DIM      40          40      189     SPARE
.
.End of record                          229
.

.                                     ******** the new FD here *******
.Data File Definition
.--------------------
.
OUTDTRO1   IFILE  SQL, FIXED=229, KEY="U1-8,9-16,17-22"
OUTDTRO2   IFILE  SQL, FIXED=229, KEY="U96-96,97-104,1-8,9-16,17-22"
OUTDTRO3   IFILE  SQL, FIXED=229, KEY="U1-8,9-16,96-96,61-68,17-22"
OUTDTRO4   IFILE  SQL, FIXED=229, KEY="U9-16,96-96,1-8,17-22"
OUTDTRO5   IFILE  SQL, FIXED=229, KEY="U1-8,9-16,96-96,113-115,17-22"
OUTDTRO6   IFILE  SQL, FIXED=229, KEY="U173-188,9-16,1-8,17-22"
.
FILDTRO1    INIT     "dtrof"
FILDTRO2    INIT     "dtrof"
FILDTRO3    INIT     "dtrof"
FILDTRO4    INIT     "dtrof"
FILDTRO5    INIT     "dtrof"
FILDTRO6    INIT     "dtrof"
.
. NAME     TYPE   LENGTH   PHYSICAL  START LOC.   DESCRIPTION
. ----     ----   ------   --------  ----------   -----------
DOTNUMB     DIM       8          8         1      OUTPATIENT NUMBER
OTINVNO     DIM       8          8         9      (INVOICE #)
DOTTRANS    DIM       6          6        17      TRANSACTION NUMBER
.
OTDDESC     DIM      30         30        23      DESCRIPTION
OTPATAMT    FORM   12.2          8        53      TOTAL AMOUNT
OTDDATE     DIM       8          8        61      DATE CCYYMMDD
.
OTITEMNO    DIM       9          9        69      ITEM NUMBER
.
OTTYPE      DIM       2          2        78      TRANSACTION TYPE  PY=PAYMENTS
.                                                                   DB=CHARGES
.                                                                   xx=JOURNALS
.
OTPAYTYP    FORM      1           2       80      PAYMENT METHOD
.                                                0 = NOT APPLICABLE
.                                                1 = CASH
.                                                2 = CHEQUE
.                                                3 = CREDIT CARD
.                                                4 = MERCHANT CARDS EG AMEX
.                                                5 = MONEY ORDER
.
OTRECEPT   DIM      12          12       82      RECEIPT NUMBER
.
OTINVPRT   FORM      1           2       94      0=INVOICE NOT YET PRINTED
.                                                1=INVOICE PRINTED
.
DOTRECTY   DIM       1           1       96      RECORD TYPE 1 - MISC ITEM
.                                                            2 - CASH RECEIPT
.                                                            3 - JOURNALS
.                                                            4 - DOCTOR FEES
.                                                            5 - DISCOUNT
.
.          These next two variables are updated automatically by the
.          I/O include for each write or update
.
OTCHGDTE   DIM       8           8       97      DATE THIS RECORD LAST CHANGED
OTCHGTIM   DIM       8           8      105      TIME THIS RECORD LAST CHANGED
.
OTMISGRP   DIM       3           3      113      MISCELLANEOUS GROUP (Cat FI)
.                                                JH-Misc.Item Group (Cat FN)
OTDEPTYP   DIM       3           3      116      DEPOSIT TYPE(BLANK IF NOT DEP)
OTBATCHN   DIM       8           8      119      BTCH NO FOR MISC.ITMS,CASH,JOUR
.                                                JH-Misc.Item Group (Cat FI)
OTPATPOR   FORM   12.2           8      127      PATIENT PORTION OF CHARGE
OTREBPOR   FORM   12.2           8      135      REBATE PORTION OF CHARGE
DOTNINVS   DIM       1           1      143      No. of invoices to be printed
OTSERVS    FORM      5           4      144      Number of services
OTDTGSTA   FORM      1           2      148      Is GST Applicable to this item
.                                                 0-No GST, 1-Yes, 2-Unknown GST
OTDTGSTC   DIM       6           6      150      GST Payable code
OTDTGSTM   FORM    12.2          8      156      GST Amount
OTDTEFFD   DIM       8           8      164      Date item assigned to Fees Inv.
OTDTCRST   DIM       1           1      172      Credit Note status
.                                                 0 - No Credit Note
.                                                 1 - Fully Credited
.                                                 2 - Credit by Item
OTDTBTCH   DIM       16         16      173      Batch Details (NZ Priv Exract)
.                                                format (99999XXXCCYYMMDD)
.                                                where 99999 = G/L Batch No.
.                                                      XXX   = Hospital Code
.                                                      CCYYMMDD = Extract.date
OTDTPCOD   DIM       3           3      189      Payment Code (Cat Py)
OTSPARE    DIM      37          37      192      SPARE
.
.End of record                          229
.
.  redefine form fields from key
.  -----------------------------
. NAME     TYPE      LENGTH     START LOC.   DESCRIPTION
. ----     ----      ------     ----------   -----------
OTNUMB      FORM         8             1      OUTPATIENT NUMBER
OTTRANS     FORM         6            17      TRANSACTION NUMBER
OTRECTYP    FORM         1            88      TRANSACTION TYPE
OTNINVS     FORM         1            129     No. of invoices to be printed
.
. LOCAL VARIABLES
. ---------------
PAYMARR   DIM       3[6]
CMDLINE   DIM       200
.
DEFPATH   DIM       60
DIM60     DIM       60
DISNUM    FORM      4
.
ERRFLAG   FORM      1
INPFILE   DIM       8
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
PAYMDESC  DIM       11
SAVEFLG   FORM      1
.
USINGORC  DIM       1
.
.
. PROGRAM CONSTANTS
. -----------------
CASH      INIT    "Cash       "
CHEQUE    INIT    "Cheque     "
CRDCARD   INIT    "Credit Card"
DIRDEP    INIT    "Direct Dep."
MONORD    INIT    "Money Order"
EFTPOS    INIT    "EFTPOS     "
SP60      INIT    "                                                            "
.
.
.
PRGNAM    INIT      "CONVERSION OUTDTRFD"
PRGID     INIT      "F07OTDTR"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000:             * run c-isam fixit
                            MAIN2000              * run oracle fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
MAIN2000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN0000:               * no
                          MAIN9999                * cancel
.
MAIN5000  CALL      PIND0000                      * Check payment type setup
          IF        ERRFLAG=1
            DISPLAY   *P1:24,*EL,"All Payment types must be Setup. ";
            CALL      HITENTER
            GOTO      MAIN9999
          ENDIF
.
          BRANCH    COPTION,MAIN6000:             * c-isam fixit
                            MAIN7000              * oracle fixit
.
.         Running c-isam fix
.
MAIN6000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/zip saved file
.
          GOTO      MAIN9999
.
.         Running oracle fix
.
MAIN7000  CALL      OFIX0000                      * run oracle fix
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
          OPEN      PATCODE1,"patcodes"
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  MOVE      "Yes",USINGORC
          REP       "yYnN",USINGORC
          MOVE      USINGORC,KEY1
          REP       "Y-N-",KEY1
          MATCH     "-",KEY1
          GOTO      OPTN9200 IF NOT EQUAL
.
          DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)"
          MATCH     "Y",USINGORC
          IF        @EQUAL
            DISPLAY   *P1:6,*V2LON,TWO,*HOFF,". Run Fixit (Oracle only)"
          ENDIF
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * run c-isam fixit
                            OPTN9000             * run oracle fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9200  DISPLAY   *P1:3,*EF,*P1:4," Oracle Option has not been set up ":
                          "correctly":
                    *P1:6," Program will terminate    - ";
          CALL      HITENTER
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
.
PREP0000  MOVE      "outdtrof",INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat outdtrof.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sotdtrof.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat outdtrof.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sotdtrof.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "outdtrof",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "sotdtrof",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "outdtrof",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.
          APPEND    " 229 UC1-8,9-16,17-22",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC96-96,97-104,1-8,9-16,17-22",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC1-8,9-16,96-96,61-68,17-22",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC9-16,96-96,1-8,17-22",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC1-8,9-16,96-96,113-115,17-22",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC173-188,9-16,1-8,17-22",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      OUTDTRO1,NEWFILE
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
            ELSE
              DISPLAY   "has caused an IO error - ";
            ENDIF
          ENDIF
          DISPLAY   "File will be bypassed"
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
+
.**********************************************************************
.         Check if payment type setup in codes file
.         Make sure there is a code record for each payment type
.**********************************************************************
PIND0000  MOVE      ZERO,ERRFLAG
          MOVE      ZERO,F1
          REPEAT
            ADD       ONE,F1
            PACK      PAYMARR[F1],SP70
          UNTIL     F1=6
.
          MOVE      ZERO,F1
PIND1000  ADD       ONE,F1
          MOVE      "Py",KEY2
          PACK      KEY5,KEY2,SP70
          CALL      RDSCODE1
PIND2000  CALL      RDKCODE1
          BRANCH    OVRCD,PIND4000
.
          MOVE      CASH,PAYMDESC
          LOAD      PAYMDESC,F1,CASH,CHEQUE,CRDCARD,DIRDEP,MONORD,EFTPOS
.
          MATCH     KEY2,TCODE
          GOTO      PIND4000 IF NOT EQUAL     * Different category
.
.         1=Cash, 2=cheque, 3=Credit care, 4=Direct Dep, 5=MoneyOrder 6=EFTPOS
.
          MOVE      F1,DIM1
.
.         PTCDNHCP = Blank or 1 are for Cash
.
          MATCH     SP70,PTCDNHCP
          IF        @EQUAL
            MOVE       "1",PTCDNHCP
          ENDIF
.
          SQUEEZE   PTCDNHCP
          MOVE      ZERO,F2
          MOVE      PTCDNHCP,F2
          COMPARE   SEVEN,F2
          GOTO      PIND2000 IF NOT LESS      * skip if greater than 6
.
          MOVE      SP70,KEY1
          MOVE      PTCDNHCP,KEY1
          MATCH     DIM1,KEY1                 * HCP Equivalent
          GOTO      PIND2000 IF NOT EQUAL
          GOTO      PIND6000
.
PIND4000  PRINT     *1,"Payment type : ",PAYMDESC," has Not been set up yet! "
          DISPLAY  *P1:20,"Payment type : ",PAYMDESC," has Not been set up yet!"
          CALL      HITENTER
          MOVE      ONE,ERRFLAG
.
PIND6000  MOVE      ACODE,PAYMARR[F1]
          COMPARE   SIX,F1
          GOTO      PIND1000 IF LESS
.
PIND9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY22,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MOVE      SP70,OTDTPCOD                 * Payment code
.
          COMPARE   TWO,OTRECTYP
          GOTO      READ2000 IF NOT EQUAL         * Not a cash record
.
          MOVE      ZERO,F1
          MOVE      OTPAYTYP,F1
.
          IF        F1=0
            MOVE      ONE,F1
          ELSE
            IF        F1=4
              MOVE      "3",F1                  * Merchant Cards as Credit Card
            ENDIF
          ENDIF
          MOVE      PAYMARR[F1],OTDTPCOD        * Payment code
.
READ2000  PACK      KEY22,DOTNUMB,OTINVNO,DOTTRANS,SP70
          CALL      WRDTRO1                     * write to new file
          GOTO      READ1000                    * get next record
.
READ6000  CLOSE     OUTDTRO1                      * close new file
          CLOSE     OLDFILE1                      * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS:
                    *P1:24;
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
.
VALD0000  MOVE      ZERO,EXIT
.
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or zip save file                                     *
.**********************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "sotdtrof*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "gzip -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not zipped or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:24,*EL,*B,"Finished processing  ",CDATE,SP1,CTIMEIS:
                    ".  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.*        DEFT0000 -  Get the default directory                       *
.**********************************************************************
.
DEFT0000  EXECUTE   "ldat outdtrof.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding 'outdtrof'.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.*                            OFIX0000                                       *
.*                    Oracle fix to "OUTDTRFD"                               *
.*****************************************************************************
.
OFIX0000  DISPLAY   *P1:24,*EL;
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,*EL,"Record No. :";
          OPEN      OUTDTRO1,"outdtrof"
.
          PACK      KEY22,SP70
          CALL      RDSDTRO1                      * position at start of file
OFIX0500  CALL      RDKDTRO1                      * read next record
          BRANCH    OVRCD,OFIX9000                * eof - finished
.
          ADD       ONE,RECNUM                    * increment record counter
          ADD       ONE,DISNUM                    * Display counter
.         
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM 
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MOVE      SP70,OTDTPCOD                * Payment code
          COMPARE   TWO,OTRECTYP
          GOTO      OFIX2000 IF NOT EQUAL         * Not a cash record
.
          MOVE      ZERO,F1
          MOVE      OTPAYTYP,F1
.
          IF        F1=0
            MOVE      ONE,F1
          ELSE
            IF        F1=4
              MOVE      "3",F1                  * Merchant Cards as Credit Card
            ENDIF
          ENDIF
          MOVE      PAYMARR[F1],OTDTPCOD        * Payment code
.
OFIX2000  CALL      UPDTRO1
          GOTO      OFIX0500                     * get next record
.
OFIX9000  CLOSE     OUTDTRO1
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Update completed ",CDATE,SP1,CTIMEIS:
                    *P1:24;
          CALL      HITENTER
.
OFIX9999  RETURN
.
OFDUMYIO  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
READSOLD MOVE      ZERO,OVRCD
         RESET     KEY22
         READ      OLDFILE1,KEY22;;
         RETURN
.
READKOLD MOVE      ZERO,OVRCD
         READKS    OLDFILE1;DOTNUMB,OTINVNO,DOTTRANS,OTDDESC,OTPATAMT:
                            OTDDATE,OTITEMNO,OTTYPE,OTPAYTYP,OTRECEPT:
                            OTINVPRT,DOTRECTY,OTCHGDTE,OTCHGTIM:
                            OTMISGRP,OTDEPTYP,OTBATCHN:
                            OTPATPOR,OTREBPOR,DOTNINVS,OTSERVS,OTDTGSTA:
                            OTDTGSTC,OTDTGSTM,OTDTEFFD:
                            OTDTCRST,OTDTBTCH,OTSPARE
.
         GOTO      OVERCOND IF OVER
         MOVE      DOTNUMB,OTNUMB
         MOVE      DOTTRANS,OTTRANS
         MOVE      DOTRECTY,OTRECTYP
         MOVE      ONE,OTNINVS
         MOVE      DOTNINVS,OTNINVS
         RETURN
.
.                  ******************************    NEW IO ROUTINES
.
RDSDTRO1 RESET     KEY22
         READ      OUTDTRO1,KEY22;;
         RETURN
.
RDKDTRO1 MOVE      ZERO,OVRCD
         READKS    OUTDTRO1;DOTNUMB,OTINVNO,DOTTRANS,OTDDESC,OTPATAMT:
                            OTDDATE,OTITEMNO,OTTYPE,OTPAYTYP,OTRECEPT:
                            OTINVPRT,DOTRECTY,OTCHGDTE,OTCHGTIM:
                            OTMISGRP,OTDEPTYP,OTBATCHN:
                            OTPATPOR,OTREBPOR,DOTNINVS,OTSERVS,OTDTGSTA:
                            OTDTGSTC,OTDTGSTM,OTDTEFFD:
                            OTDTCRST,OTDTBTCH,OTDTPCOD,OTSPARE
.
         GOTO      OVERCOND IF OVER
         MOVE      DOTNUMB,OTNUMB
         MOVE      DOTTRANS,OTTRANS
         MOVE      DOTRECTY,OTRECTYP
         MOVE      ONE,OTNINVS
         MOVE      DOTNINVS,OTNINVS
         RETURN
.
WRDTRO1  RESET     KEY22
         MOVE      OTRECTYP,DOTRECTY
         MOVE      OTNINVS,DOTNINVS
.
         WRITE     OUTDTRO1,KEY22;KEY22,OTDDESC,OTPATAMT:
                                  OTDDATE,OTITEMNO,OTTYPE,OTPAYTYP,OTRECEPT:
                                  OTINVPRT,DOTRECTY,OTCHGDTE,OTCHGTIM:
                                  OTMISGRP,OTDEPTYP,OTBATCHN:
                                  OTPATPOR,OTREBPOR,DOTNINVS,OTSERVS,OTDTGSTA:
                                  OTDTGSTC,OTDTGSTM,OTDTEFFD:
                                  OTDTCRST,OTDTBTCH,OTDTPCOD,OTSPARE
         RETURN
.
UPDTRO1  MOVE      OTNUMB,DOTNUMB
         MOVE      OTTRANS,DOTTRANS
         MOVE      OTRECTYP,DOTRECTY
         MOVE      OTNINVS,DOTNINVS
.
         UPDATE    OUTDTRO1;DOTNUMB,OTINVNO,DOTTRANS,OTDDESC,OTPATAMT:
                            OTDDATE,OTITEMNO,OTTYPE,OTPAYTYP,OTRECEPT:
                            OTINVPRT,DOTRECTY,OTCHGDTE,OTCHGTIM:
                            OTMISGRP,OTDEPTYP,OTBATCHN:
                            OTPATPOR,OTREBPOR,DOTNINVS,OTSERVS,OTDTGSTA:
                            OTDTGSTC,OTDTGSTM,OTDTEFFD:
                            OTDTCRST,OTDTBTCH,OTDTPCOD,OTSPARE
         RETURN
.
+
          INC       PATCODIO/INC
          INC       STD001IO
