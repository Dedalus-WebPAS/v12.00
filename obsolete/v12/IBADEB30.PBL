.-------------------------------------------------------
          INC       STDGENDF     * Required Include
.
VERSION   INIT      "V12.00.00"
.-------------------------------------------------------
. V9.02.00  03.05.2002  Glenn Saunders
.           Export to new release i.e. v9.02 from vf.09
.-------------------------------------------------------
. VF.01.00  09.03.2000 Charles Handaya
.           Add in Standard Export File  srf 638148
.-------------------------------------------------------
EXPORTFL  FILE      ASCII, FIXED=400     * Export File (Adjust Length to Suit)
EXPORTFN  DIM       8                    * Export File Name
TABDELM   EQU       0x09                 * Tab Delimiter
.
DSPTIME   DIM       8
.
TRANTYPE  DIM       8
TRANINV   INIT      "Invoice"
TRANCRD   INIT      "Cr. Not"
TRANCSH   INIT      "Payment"
TRANDRA   INIT      "Dr Adj."
TRANCRA   INIT      "Cr Adj."
.
SUBEXT    FORM      12.2
SUBTAX    FORM      12.2
TOTEXT    FORM      12.2
TOTTAX    FORM      12.2
.
F12P2     FORM      12.2
FORQTY    DIM       9
FORPRI    DIM       13
FOREXT    DIM       13
FORTAX    DIM       13
FORTOT    DIM       13
.
STRDAT    DIM       8
ENDDAT    DIM       8
LASTDAT   DIM       8
DATRANGE  DIM       30
STRUSR    DIM       4
ENDUSR    DIM       4
USRRANGE  DIM       60
.-------------------------------------------------------
.@@@@@ Start of Generated Code @@@@@
. NB - Code & Comments Included Before the above line 
.      will not be overwritten by the generation of a 
.      program. The STDGENDF file and VERSION must appear
.      before the line. 
.-----------------------------------------
.   Program       :   IBADEB30
.   Function      :   Input Audit Report                      
.   Generated  On :   13/12/2005
.-----------------------------------------
.=========================
.File Description Includes
.-------------------------
          INC       DEBDBTFD/INC
          INC       DEBFDTFD/INC
          INC       DEBFTHFD/INC
          INC       DEBITMFD/INC
          INC       IBAPASFD/INC
.=========================
.Local Variable Definition
.-------------------------
FROMDAT   DIM      10 * Scr 00 Fld     4
TODAT     DIM      10 * Scr 00 Fld     5
FROMUSR   DIM      40 * Scr 00 Fld     6
TOUSR     DIM      40 * Scr 00 Fld     7
REPTTYP   FORM      1 * Scr 00 Fld     8
TYPDESC   DIM      30 * Scr 00 Fld     9
EXPORTYN  FORM      1 * Scr 00 Fld    10
.
ACCEPT    FORM      1
SACCEPT   FORM      1
.
PRGID     INIT      "IBADEB30"
PRGNAM    INIT      "Input Audit Report                      "
.
.--------------------------------
. MAINLINE - Controlling Logic
.--------------------------------
          TOPIC     PRGID
          MOVE      PORT,PARENTID
          CLOCK     PORT,DIM2
          MOVE      DIM2,PORT
ML0000
          CALL      INIT0000
          CALL      SC00L000
          CHAIN     PGM
.---------------------------------------
. INIT - Open Files & Display Heading
.---------------------------------------
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CCENTRY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          DISPLAY   *P56:24,"Opening scrsbga1";
          OPEN      SCRSBGA1,"scrsbgaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA1,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA2,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrslta1";
          OPEN      SCRSLTA1,"scrsltaf"
          MOVE      "   81",AUDTSECT
          MOVE      " 10",AUDTPOST
          READ      CONTROLF,AUDTSECT;*AUDTPOST,AUDTFLAG
          IF        AUDTFLAG=1
            DISPLAY   *P56:24,"Opening debauddb";
            OPEN      DEBAUDDB,"debauddb"
          ENDIF
          MOVE      "   81",AUDTSECT
          MOVE      " 12",AUDTPOST
          READ      CONTROLF,AUDTSECT;*AUDTPOST,AUDTFLAG
          IF        AUDTFLAG=1
            DISPLAY   *P56:24,"Opening debaudit";
            OPEN      DEBAUDIT,"debaudit"
          ENDIF
          DISPLAY   *P56:24,"Opening debdbtaf";
          OPEN      DEBDBTA1,"debdbtaf"
          DISPLAY   *P56:24,"Opening debdbtaf";
          OPEN      DEBDBTA2,"debdbtaf"
          DISPLAY   *P56:24,"Opening debdbtaf";
          OPEN      DEBDBTA3,"debdbtaf"
          DISPLAY   *P56:24,"Opening debdbtaf";
          OPEN      DEBDBTA4,"debdbtaf"
          DISPLAY   *P56:24,"Opening debfdtaf";
          OPEN      DEBFDTA1,"debfdtaf"
          DISPLAY   *P56:24,"Opening debfdtaf";
          OPEN      DEBFDTA2,"debfdtaf"
          DISPLAY   *P56:24,"Opening debfdtaf";
          OPEN      DEBFDTA3,"debfdtaf"
          DISPLAY   *P56:24,"Opening debfdtaf";
          OPEN      DEBFDTA4,"debfdtaf"
          DISPLAY   *P56:24,"Opening debfdtaf";
          OPEN      DEBFDTA5,"debfdtaf"
          DISPLAY   *P56:24,"Opening debfdtaf";
          OPEN      DEBFDTA6,"debfdtaf"
          DISPLAY   *P56:24,"Opening debfdtaf";
          OPEN      DEBFDTA7,"debfdtaf"
          DISPLAY   *P56:24,"Opening debfdtaf";
          OPEN      DEBFDTA8,"debfdtaf"
          DISPLAY   *P56:24,"Opening debfthaf";
          OPEN      DEBFTHA1,"debfthaf"
          DISPLAY   *P56:24,"Opening debfthaf";
          OPEN      DEBFTHA2,"debfthaf"
          DISPLAY   *P56:24,"Opening debfthaf";
          OPEN      DEBFTHA3,"debfthaf"
          DISPLAY   *P56:24,"Opening debfthaf";
          OPEN      DEBFTHA4,"debfthaf"
          DISPLAY   *P56:24,"Opening debfthaf";
          OPEN      DEBFTHA5,"debfthaf"
          DISPLAY   *P56:24,"Opening debfthaf";
          OPEN      DEBFTHA6,"debfthaf"
          DISPLAY   *P56:24,"Opening debfthaf";
          OPEN      DEBFTHA7,"debfthaf"
          DISPLAY   *P56:24,"Opening debitmaf";
          OPEN      DEBITMA1,"debitmaf"
          DISPLAY   *P56:24,"Opening debitmaf";
          OPEN      DEBITMA2,"debitmaf"
          DISPLAY   *P56:24,"Opening debitmaf";
          OPEN      DEBITMA3,"debitmaf"
          DISPLAY   *P56:24,"Opening  ihapassf           ";
          OPEN      IHAPASS1,"ihapassf"          
          DISPLAY   *P56:24,"Opening  ihapassf           ";
          OPEN      IHAPASS2,"ihapassf"          
          DISPLAY   *P56:24,"Opening  ihapassf           ";
          OPEN      IHAPASS3,"ihapassf"          
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.
.------------------------------------
. Called for Print Screen Routine 
.------------------------------------
PRTSVARS
          MATCH     "00",SCRID
          IF         @EQUAL
            CALL       PS00L000   * Menu Options                       
          ENDIF
          RETURN
.--------------------------------
. Screen - Menu Options                       
.--------------------------------
SC00L000
SC00L100
          MOVE      "00",SCRID
          MOVE      ZERO,PRCOL
          MOVE      ZERO,PRVERT
          MOVE      "    1",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SCPSCOL,PRCOL
            MOVE     SCPSROW,PRVERT
          ENDIF
          CALL      SBACK000 * Display Background
.
SC00L200
          CALL      CF00L000 * Clear Fields
          CALL      DEFPAR00
SC00L300
          CALL      KY00L000 * Enter Key Fields
          BRANCH    EXIT,SC00L999,SC00L999
SC00L400
          CALL      WP00L000 * Load any WP Details
SC00L500
          CALL      LM00L000 * Load any Batch Details
SC00L600
          CALL      DF00L000 * Display Fields
SC00L700
          CALL      SF00L000 * Select Fields
          BRANCH    EXIT,SC00L100,SC00L100,SC00L200
          IF        EXIT=4
            MOVE      ONE,EXIT
          ENDIF
SC00L800
SC00L900
SC00L999  RETURN
.--------------------------------------------------------
. Clear Fields for Screen : Menu Options                       
.-------------------------------------------------------
CF00L000
          MOVE      SP70,FROMDAT 
          MOVE      SP70,TODAT   
          MOVE      SP70,FROMUSR 
          MOVE      SP70,TOUSR   
          MOVE      ZERO,REPTTYP 
          MOVE      SP70,TYPDESC 
          MOVE      ZERO,EXPORTYN
CF00L999  RETURN
.--------------------------------------------------------
. Enter Key Fields for Menu Options                       
.--------------------------------------------------------
KY00L000
          MOVE      ZERO,EXIT
          GOTO      KY00L999
.
KY00L900  MOVE      ONE,EXIT
KY00L999  RETURN
.---------------------------------------------------------
. Load WP files for : Menu Options                       
.---------------------------------------------------------
WP00L000
          RETURN
.---------------------------------------------------------
. Load Batch Screen for : Menu Options                       
.---------------------------------------------------------
LM00L000
          RETURN
.---------------------------------------------------------
. Display Fields for : Menu Options                       
.---------------------------------------------------------
DF00L000
          BRANCH   ACCEPT,DF00L999
          PACK     KEY15,PRGID,SCRID,SP70
          CALL     RSSCPS1
DF00L010
          CALL     RKSCPS1
          BRANCH   OVRCD,DF00L900
          MATCH    PRGID,SCPSPID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SCRID,SCPSSID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SP70,SCPSMTY
          GOTO     DF00L010 IF NOT EQUAL
.
          CALL     PS00L000 * Display Field
          GOTO     DF00L010
DF00L900
DF00L999
          RETURN
.---------------------------------------------------------
. Display/Print Fields for : Menu Options                       
.---------------------------------------------------------
PS00L000   PACK      VAR,SP70,SP70
          PACK     PRINTVAR,SP70,SP70
          MOVE     SCPSITM,FIELDNO
          BRANCH    FIELDNO,PS00L999,PS00L999,PS00L999,PS00L101:
                            PS00L102,PS00L103,PS00L104,PS00L105:
                            PS00L106,PS00L107
          GOTO     PS00L999
.
PS00L101
          MOVE      FROMDAT,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L102
          MOVE      TODAT,VAR        
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L103
          MOVE      FROMUSR,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L104
          MOVE      TOUSR,VAR        
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L105
          MOVE      REPTTYP,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L106
          MOVE      TYPDESC,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L107
          MOVE      NO,YESNO
          MOVE      ZERO,FORM2
          MOVE      EXPORTYN,FORM2   
          LOAD      YESNO,FORM2,YES
          MOVE      YESNO,VAR
          CALL      DISPITEM
          GOTO     PS00L999
PS00L999
          RETURN
.---------------------------------------------------------
. Selections for : Menu Options                       
.---------------------------------------------------------
SF00L000  PACK     KEY13,PRGID,SCRID,SP70
          CALL     RSSCSL1
SF00L001  BRANCH   ACCEPT,SF00L020
.
SF00L005  CALL     MTSTQ000
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L006,SF00L101,SF00L102,SF00L103:
                            SF00L006,SF00L104,SF00L006,SF00L105:
                            SF00L006,SF00L106
SF00L006  BEEP
          GOTO     SF00L005
SF00L020  CALL     RKSCSL1
          BRANCH   OVRCD,SF00L090
          MATCH    PRGID,SCSLPID
          GOTO     SF00L090 IF NOT EQUAL
          MATCH    SCRID,SCSLSID
          GOTO     SF00L090 IF NOT EQUAL
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L020,SF00L020,SF00L020,SF00L103:
                            SF00L020,SF00L104,SF00L020,SF00L105:
                            SF00L020,SF00L106
          GOTO     SF00L020
SF00L090  MOVE     ZERO,ACCEPT
          GOTO     SF00L001
.
SF00L101
          MOVE      ZERO,EXIT
          GOTO      SF00L999
.
SF00L102
          CALL      REPT0000
          MOVE      ZERO,EXIT
          GOTO      SF00L999
.
SF00L103
.
.
          MOVE      "    5",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,TODAT   
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      RDAT0000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L103
            ENDIF
            BRANCH    EXIT,SF00L103,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      FROMDAT,VAR      
          CALL      DISPITEM
.
.
          MOVE      "    5",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      TODAT,VAR        
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L104
.
.
          MOVE      "    7",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,TOUSR   
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      RUSR0000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L104
            ENDIF
            BRANCH    EXIT,SF00L104,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      FROMUSR,VAR      
          CALL      DISPITEM
.
.
          MOVE      "    7",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      TOUSR,VAR        
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L105
.
.
          MOVE      "    9",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,TYPDESC 
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      KRTYP000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L105
            ENDIF
            BRANCH    EXIT,SF00L105,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      REPTTYP,VAR      
          CALL      DISPITEM
.
.
          MOVE      "    9",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      TYPDESC,VAR      
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L106
          CALL     SELECTED
          CALL      YORN0000
          MOVE       FYESNO,EXPORTYN
          MOVE      NO,YESNO
          MOVE      ZERO,FORM2
          MOVE      EXPORTYN,FORM2   
          LOAD      YESNO,FORM2,YES
          MOVE      YESNO,VAR
          CALL      DISPITEM
          GOTO     SF00L001
SF00L990  MOVE      FOUR,EXIT * Exit Char Exit
SF00L999  RETURN
.@@@@@ Function Restart Point @@@@@
.---------------------------------------
. Setup Defaults for Invoice Generation
.---------------------------------------
DEFPAR00  PACK      STRDAT,CCC,CYY,CMM,CDD
          REP       " 0",STRDAT
          UNPACK    STRDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,FROMDAT
          MOVE      CPCDATE,TODAT
          PACK      ENDDAT,CCC,CYY,CMM,CDD
          REP       " 0",ENDDAT
          MOVE      PASSCODE,STRUSR
          MOVE      PASSCODE,ENDUSR
          MOVE      PASSCODE,KEY4
          CALL      RDPASS1
          MOVE      SECUSER,FROMUSR
          MOVE      SECUSER,TOUSR
          MOVE      ZERO,REPTTYP
          CALL      GRTYP000
          RETURN
.
.---------------------------------------
. Print Report
.---------------------------------------
REPT0000  MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
          MOVE      "99",CLNO
          CALL      MAKEXP00        * Create export file
.
          MOVE      ZERO,TOTTAX
          MOVE      ZERO,TOTEXT
          STRIP     FROMUSR
          STRIP     TOUSR
          STRIP     FROMDAT
          STRIP     TODAT
          MOVE      SP70,LASTDAT
          MOVE      " to ",KEY4
          PACK      USRRANGE,FROMUSR,KEY4,TOUSR
          PACK      DATRANGE,FROMDAT,KEY4,TODAT
          PACK      KEY35,STRDAT,SP70
          CALL      RSDBFH5
REPT1000  CALL      RKDBFH5
          BRANCH    OVRCD,REPT9000
          MATCH     DBFHCDAT,ENDDAT
          GOTO      REPT9000 IF LESS
.
          MATCH     STRUSR,DBFHCUSR     * Check User Range
          GOTO      REPT1000 IF LESS
          MATCH     DBFHCUSR,ENDUSR
          GOTO      REPT1000 IF LESS
          IF        REPTTYP<>0
            MOVE      REPTTYP,KEY1      * Report Type 0 for All
            MATCH     DBFHDTY,KEY1      * or a Selected Transaction Type
            GOTO      REPT1000 IF NOT EQUAL
          ENDIF
.
          MATCH     DBFHCDAT,LASTDAT
          GOTO      REPT1100 IF EQUAL
          CALL      PSUB0000
          MOVE      DBFHCDAT,LASTDAT
          MOVE      "99",CLNO
.
REPT1100  CALL      PRTDOC00            * Print Document Header
          CALL      PRTDET00            * Print Document Details
          CALL      PRTTOT00            * Print Doocument Total
          GOTO      REPT1000
.
REPT9000  CALL      PSUB0000            * Print Last Daily Sub Total
          CALL      PEND0000            * Print End of Report
          CALL      TRAN0000
.
          RETURN
.--------------------------------------------------
. Print Document
.--------------------------------------------------
PRTDOC00  IF        CLNO>50
            CALL      PHED0000
          ELSE
            CALL      UND132
          ENDIF
.
          MOVE      DBFHDEB,KEY8
          CALL      RDDBDB1
          UNPACK    DBFHCTIM,CHOUR,CMIN,KEY2
          MOVE      ":",KEY1
          PACK      DSPTIME,CHOUR,KEY1,CMIN,KEY1,KEY2
          MOVE      DBFHCUSR,KEY4
          CALL      RDPASS1
          MOVE      DBFHDTY,F1
          LOAD      TRANTYPE,F1,TRANINV,TRANCRD,TRANCSH,TRANDRA,TRANCRA
          PRINT     TRANTYPE,SP1,DBFHDOC,SP1:
                    DBDBNA1,"(",DBFHDEB,") ",SECUSER,SP3,DSPTIME
          ADD       ONE,CLNO
          RETURN
.--------------------------------------------------
. Print Document Details
.--------------------------------------------------
PRTDET00  PACK      KEY24,DBFHDEB,DBFHDTY,DBFHDOC,SP70
          CALL      RSDBFD1
PRTDET10  CALL      RKDBFD1
          BRANCH    OVRCD,PRTDET99
          PACK      KEY21,DBFHDEB,DBFHDTY,DBFHDOC,SP70
          PACK      KEY24,DBFDDEB,DBFDDTY,DBFDDOC,DBFDDLN,SP70
          MATCH     KEY21,KEY24
          GOTO      PRTDET99 IF NOT EQUAL
          CALL      PRTLIN00
          GOTO      PRTDET10
PRTDET99  RETURN
.--------------------------------------------------
. Print Detail Line
.--------------------------------------------------
PRTLIN00  COMPARE   "55",CLNO
          CALL      PHED0000 IF NOT LESS
          PACK      KEY8,DBFDITM,SP70
          CALL      RDDBIT1
          IF        OVRCD=1
            MOVE      "ERROR",DBITDES
          ENDIF
.
          MOVE      DBFDDTY,F1
          BRANCH    F1,PRTLIN20,PRTLIN10,PRTLIN10,PRTLIN20,PRTLIN10
.
PRTLIN10  MULT      SEQ,DBFDTOT
          MULT      SEQ,DBFDTAX
.
PRTLIN20  MOVE      "999999.99",FORQTY
          FEDIT     DBFDQTY,FORQTY
          MOVE      "9999,999.9999",FORPRI
          FEDIT     DBFDPRI,FORPRI
          MOVE      "999999,999.99",FOREXT
          FEDIT     DBFDTOT,FOREXT
          MOVE      "999999,999.99",FORTAX
          FEDIT     DBFDTAX,FORTAX
          MOVE      "999999,999.99",FORTOT
          ASSIGN    DBFDTOT+DBFDTAX,F12P2
          FEDIT     F12P2,FORTOT
          UNPACK    DBFDSDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          CALL      WRTEXP00
.
          PRINT     *10,CPCDATE,SP1,DBITDES,"(",DBFDITM,")":
                    *71,FORQTY,FORPRI,FOREXT,FORTAX,FORTOT
          ADD       ONE,CLNO
          RETURN
.------------------------------------------------------------
. Print Document Total
.------------------------------------------------------------
PRTTOT00  MOVE      SP70,FORQTY
          MOVE      SP70,FORPRI
.
          MOVE      DBFHDTY,F1
          BRANCH    F1,PRTTOT20,PRTTOT10,PRTTOT10,PRTTOT20,PRTTOT10
.
PRTTOT10  MULT      SEQ,DBFHTOT
          MULT      SEQ,DBFHTAX
.
PRTTOT20  MOVE      "999999,999.99",FOREXT
          FEDIT     DBFHTOT,FOREXT
          MOVE      "999999,999.99",FORTAX
          FEDIT     DBFHTAX,FORTAX
          MOVE      "999999,999.99",FORTOT
          ASSIGN    DBFHTOT+DBFHTAX,F12P2
          FEDIT     F12P2,FORTOT
          CALL      UND132
          PRINT     "Total",*71,FORQTY,FORPRI,FOREXT,FORTAX,FORTOT
          ADD       ONE,CLNO
          ADD       DBFHTAX,TOTTAX
          ADD       DBFHTOT,TOTEXT
          ADD       DBFHTAX,SUBTAX
          ADD       DBFHTOT,SUBEXT
          RETURN
.------------------------------------------------------------
. Print Daily Sub Total
.------------------------------------------------------------
PSUB0000  MATCH     SP70,LASTDAT
          GOTO      PSUB9000 IF EQUAL
          MOVE      SP70,FORQTY
          MOVE      SP70,FORPRI
          MOVE      "999999,999.99",FOREXT
          FEDIT     SUBEXT,FOREXT
          MOVE      "999999,999.99",FORTAX
          FEDIT     SUBTAX,FORTAX
          MOVE      "999999,999.99",FORTOT
          ASSIGN    SUBEXT+SUBTAX,F12P2
          FEDIT     F12P2,FORTOT
          UNPACK    LASTDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          CALL      UND132
          PRINT     "Total for ",CPCDATE,*71,FORQTY,FORPRI,FOREXT,FORTAX,FORTOT
          ADD       ONE,CLNO
PSUB9000  MOVE      ZERO,SUBTAX
          MOVE      ZERO,SUBEXT
          RETURN
.------------------------------------------------------------
. Print Report Total
.------------------------------------------------------------
PEND0000  COMPARE   ZERO,CPAGENO
          CALL      PHED0000 IF EQUAL
          MOVE      SP70,FORQTY
          MOVE      SP70,FORPRI
          MOVE      "999999,999.99",FOREXT
          FEDIT     TOTEXT,FOREXT
          MOVE      "999999,999.99",FORTAX
          FEDIT     TOTTAX,FORTAX
          MOVE      "999999,999.99",FORTOT
          ASSIGN    TOTEXT+TOTTAX,F12P2
          FEDIT     F12P2,FORTOT
          CALL      UND132
          PRINT     "Report Total",*71,FORQTY,FORPRI,FOREXT,FORTAX,FORTOT
          ADD       ONE,CLNO
          CALL      UND132
          PRINT     *50,"**** End of Report ****"
          RETURN
.------------------------------------------------------------
. Print Page Heading
.------------------------------------------------------------
PHED0000  CALL      HEAD132
          DISPLAY   *P1:24,*EL,"Printing Page : ",CPAGENO
          PRINT     "Input Date From : ",DATRANGE,*60,"Report Type : ",TYPDESC
          PRINT     "Operator   From : ",USRRANGE
          UNPACK    LASTDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     "Input Date      : ",CPCDATE
          CALL      UND132
          PRINT     "Type     Reference  Debtor                            ":
                    "            Operator                                Time"
          PRINT     "         Ser. Date  Sales Item                       ":
                    "                  Quantity        Price       Ex Tax ":
                    "         Tax        Total"
          CALL      UND132
          ADD       FOUR,CLNO
          RETURN
.------------------------------------------------------------
. File       : casetran
.
. Functions  : This file contains standard routines for
.              creating and transfering TAB delimited
.              export files to a PC using the unix script
.              keatran.
.
. Routines   : MAKEXP00  - Create the Export File and Headings
.              WRTEXP00  - Write Export details Record to File
.              TRAN0000  - Transfer file to PC using script keatran
.
. Note       : The Variable EXPORTYN is used to determine if a
.              export file is to be created. 0=No, 1=Yes.
.
. Variables to be Declared
. ------------------------
. EXPORTFL  FILE      ASCII, FIXED=400     * Export File (Adjust Length to Suit)
. EXPORTFN  DIM       8                    * Export File Name
. TABDELM   EQU       0x09                 * Tab Delimiter
.------------------------------------------------------------
. Create Export File
.------------------------------------------------------------
MAKEXP00  COMPARE   ONE,EXPORTYN
          GOTO      MAKEXP99 IF NOT EQUAL
          MOVE      "EXPORT",KEY6
          PACK      EXPORTFN,KEY6,PORT
          REP       " 0",EXPORTFN
          PREP      EXPORTFL,EXPORTFN
.
          WRITE     EXPORTFL,SEQ;"Transaction Type",TABDELM:
                                 "Debtor Code",TABDELM:
                                 "Debtor Name",TABDELM:
                                 "Document Reference",TABDELM:
                                 "User ID",TABDELM:
                                 "User Name",TABDELM:
                                 "Creation Time",TABDELM:
                                 "Service Date",TABDELM:
                                 "Sales Item",TABDELM:
                                 "Sales Item Description",TABDELM:
                                 "Quantity",TABDELM:
                                 "Price",TABDELM:
                                 "Excluded Tax",TABDELM:
                                 "Tax",TABDELM:
                                 "Total"
.
MAKEXP99  RETURN
.------------------------------------------------------------
. Write to Export File
.------------------------------------------------------------
WRTEXP00  COMPARE   ONE,EXPORTYN
          GOTO      WRTEXP99 IF NOT EQUAL
.
          WRITE     EXPORTFL,SEQ;TRANTYPE,TABDELM:
                                 DBFHDEB,TABDELM:
                                 DBDBNA1,SP1,DBDBNA2,TABDELM:
                                 DBFHDOC,TABDELM:
                                 SECODE,TABDELM:
                                 SECUSER,TABDELM:
                                 DSPTIME,TABDELM:
                                 CPCDATE,TABDELM:
                                 DBFDITM,TABDELM:
                                 DBITDES,TABDELM:
                                 DBFDQTY,TABDELM:
                                 DBFDPRI,TABDELM:
                                 DBFDTOT,TABDELM:
                                 DBFDTAX,TABDELM:
                                 F12P2
.
WRTEXP99  RETURN
.------------------------------------------------------------
. Execute Transfer Script eg softerm
.------------------------------------------------------------
TRAN0000  COMPARE   ONE,EXPORTYN
          GOTO      TRAN9999 IF NOT EQUAL
          MOVE      YES,DEFYESNO
          CLOSE     EXPORTFL
          DISPLAY   *P1:24,*EL,"Execute Transfer Now (":
                               *V2LON,"Y",*HOFF,"/":
                               *V2LON,"N",*HOFF,") ? "
          MOVE      "24",CVERT
          MOVE      "30",CCOL
          CALL      YORN0000
          COMPARE   ZERO,FYESNO
          GOTO      TRAN9999 IF EQUAL   *  No Transfer
.
          MOVE      ZERO,HLEF
          CALL      GETSCR00
.
          CLEAR     KEY80
          APPEND    "keatran ",KEY80
          APPEND    EXPORTFN,KEY80
          APPEND    ".txt",KEY80
          RESET     KEY80
          EXECUTE   KEY80,TASKID
.
          CALL      PUTSCR00
.
          DISPLAY   *P1:24,*EL,"Transfer Completed Ok (":
                               *V2LON,"Y",*HOFF,"/":
                               *V2LON,"N",*HOFF,") ? "
          MOVE      "24",CVERT
          MOVE      "31",CCOL
          CALL      YORN0000
          COMPARE   ZERO,FYESNO
          GOTO      TRAN0000 IF EQUAL   *  Not ok
.
          PREP      EXPORTFL,EXPORTFN
          CLOSE     EXPORTFL
.
TRAN9999  RETURN
.
.----------------------------------------
. Enter Date Range
.----------------------------------------
RDAT0000   MOVE     "    4",FLDID
           CALL     GETPOSIT
           MOVE     SP70,CDAY
           MOVE     CMM,CMON
           MOVE     CYY,CYEAR
           MOVE     CCC,CCENT
           CALL     KEYDATE
           BRANCH   OVRCD,RDAT0100
           MATCH    SP2,CDAY
           GOTO     RDAT0100 IF EQUAL
           PACK     STRDAT,CCENT,CYEAR,CMON,CDAY
           REP      " 0",STRDAT
           CALL     PACDATE
           MOVE     CPCDATE,FROMDAT
           GOTO     RDAT0140
RDAT0100   MOVE     "Start             ",FROMDAT
           MOVE     SP70,STRDAT
.
RDAT0140   MOVE     FROMDAT,VAR
           CALL     DISPITEM
.
RDAT0150   MOVE     "    5",FLDID
           CALL     GETPOSIT
           MOVE     SP70,CDAY
           MOVE     CMM,CMON
           MOVE     CYY,CYEAR
           MOVE     CCC,CCENT
           CALL     KEYDATE
           BRANCH   OVRCD,RDAT0200
           MATCH    SP2,CDAY
           GOTO     RDAT0200 IF EQUAL
           PACK     ENDDAT,CCENT,CYEAR,CMON,CDAY
           REP      " 0",ENDDAT
           CALL     PACDATE
           MOVE     CPCDATE,TODAT
           MATCH    STRDAT,ENDDAT
           GOTO     RDAT0999 IF EQUAL
           GOTO     RDAT0999 IF NOT LESS
           BEEP
           MOVE     "To Date Must Be > From Date - ",DISPMSG
           CALL     MESSAGE1
           GOTO     RDAT0000
.
RDAT0200   MOVE     "Finish             ",TODAT
           MOVE     "99999999",ENDDAT
.
RDAT0999   MOVE     "    4",FLDID
           CALL     GETPOSIT
           RETURN
.--------------------------------------------------
. Enter USRtors Range
.--------------------------------------------------
RUSR0000   MOVE     SP70,DISPMSG
           CALL     MESSAGE2
.
RUSR0010   MOVE     "    6",FLDID
           CALL     GETPOSIT
           MOVE     SP70,VAR
           CALL     DISPITEM
           CALL     KIBPSA00
           BRANCH   EXIT,RUSR0100,RUSR0999
           MOVE     SECODE,STRUSR
           PACK     FROMUSR,SECUSER,SP70
           GOTO     RUSR0140
RUSR0100   MOVE     "Start             ",FROMUSR
           MOVE     SP70,STRUSR
.
RUSR0140   MOVE     FROMUSR,VAR
           CALL     DISPITEM
.
RUSR0150   MOVE     "    7",FLDID
           CALL     GETPOSIT
           CALL     KIBPSA00
           BRANCH   EXIT,RUSR0200,RUSR0999
           MOVE     SECODE,ENDUSR
           PACK     TOUSR,SECUSER,SP70
           MATCH    STRUSR,ENDUSR
           GOTO     RUSR0999 IF EQUAL
           GOTO     RUSR0999 IF NOT LESS
           BEEP
           MOVE     "To User Must Be > From User - ",DISPMSG
           CALL     MESSAGE1
           GOTO     RUSR0010
.
RUSR0200   MOVE     "Finish             ",TOUSR
           MOVE     Z70,ENDUSR
.
RUSR0999   MOVE     "    6",FLDID
           CALL     GETPOSIT
           CALL     MESSAGE3
           RETURN
.
.------------------------------------------------------------
. Input Report Layout
.------------------------------------------------------------
KRTYP000   CALL      DRTYP000
           MOVE      ZERO,REPTTYP
.
KRTYP001   KEYIN     *PCCOL:CVERT,*V2LON,*RV,REPTTYP
           COMPARE   ZERO,REPTTYP
           GOTO      KRTYP010 IF EQUAL
           BRANCH    REPTTYP,KRTYP010,KRTYP010,KRTYP010,KRTYP010,KRTYP010
           BEEP
           GOTO      KRTYP001
.
KRTYP010   CALL      GRTYP000
KRTYP999   CALL      PUTSCR00
           RETURN
.
DRTYP000  MOVE      "15",HTOP
          MOVE      "55",HLEF
          MOVE      "24",HBOT
          MOVE      "80",HRIG
          CALL      GETSCR00
.
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          DISPLAY   *P60:16,*V2LON,*ULON,"Report Type",*HOFF:
                    *P57:17,*V2LON,"0. ",*HOFF,"All Transactions":
                    *P57:18,*V2LON,"1. ",*HOFF,"Invoices Only":
                    *P57:19,*V2LON,"2. ",*HOFF,"Credit Notes Only":
                    *P57:20,*V2LON,"3. ",*HOFF,"Payments Only":
                    *P57:21,*V2LON,"4. ",*HOFF,"Dr Adj. Only":
                    *P57:22,*V2LON,"5. ",*HOFF,"Cr Adj. Only"
          RETURN
.
GRTYP000   BRANCH    REPTTYP,GRTYP010,GRTYP020,GRTYP030,GRTYP040,GRTYP050
.
           MOVE      "All Transactions",TYPDESC
           GOTO      GRTYP999
.
GRTYP010   MOVE      "Invoices Only",TYPDESC
           GOTO      GRTYP999
.
GRTYP020   MOVE      "Credit Notes Only",TYPDESC
           GOTO      GRTYP999
.
GRTYP030   MOVE      "Payments Only",TYPDESC
           GOTO      GRTYP999
.
GRTYP040   MOVE      "Dr Adj. Only",TYPDESC
           GOTO      GRTYP999
.
GRTYP050   MOVE      "Cr Adj. Only",TYPDESC
           GOTO      GRTYP999
.
GRTYP999   RETURN
.
.-------------------------------
. Includes for I/O and Keyins
.-------------------------------
          INC       DEBDBTIO/INC
          INC       DEBFDTIO/INC
          INC       DEBFTHIO/INC
          INC       DEBITMIO/INC
          INC       IBAPASIO/INC
          INC       IBAPASKY    
.
          INC       STDGENCD
.
