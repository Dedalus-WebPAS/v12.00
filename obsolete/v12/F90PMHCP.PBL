.*****************************************************************************
.*    Program      : F90PMHCP                                                *
.*    Description  : Conversion pmshcpaf to new File layout                  *
.*                                                                           *
.*    Author       : J.Tan          CAR 166690                               *
.*    Date         : 07/05/2008                                              *
.*    Modifications:                                                         *
.*                 :                                                         *
.*****************************************************************************
.
          INC       STD001FD
. New FD
.
.Data File Definition
.--------------------
PMSHCPA1   IFILE    SQL, FIXED=1791, KEY="U1-10"
PMSHCPA2   IFILE    SQL, FIXED=1791, KEY="U58-92,23-57,1-10"
PMSHCPA3   IFILE    SQL, FIXED=1791, KEY="U824-833,1-10"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
PMHCHCPC  DIM       10     10       1     HCP Code
PMHCHCPT  DIM       2      2        11    Type of HCP D=Dentist G=GP
PMHCTITL  DIM       10     10       13    HCP Title
PMHCGNAM  DIM       35     35       23    HCP Given Names
PMHCSNAM  DIM       35     35       58    HCP Surname
PMHCINIT  DIM       5      5        93    HCP Initials
PMHCGNDR  DIM       1      1        98    HCP Gender (M,F,I,U)
PMHCDTOB  DIM       8      8        99    HCP Date of Birth (ccyymmdd)
PMHCSPC1  DIM       3      3        107   HCP Speciality 1 (Cat DT)
PMHCSPD1  DIM       8      8        110   HCP Speciality Date 1 (ccyymmdd)
PMHCSPC2  DIM       3      3        118   HCP Speciality 2 (Cat DT)
PMHCSPD2  DIM       8      8        121   HCP Speciality Date 2 (ccyymmdd)
PMHCSPC3  DIM       3      3        129   HCP Speciality 3 (Cat DT)
PMHCSPD3  DIM       8      8        132   HCP Speciality Date 3 (ccyymmdd)
PMHCSPC4  DIM       3      3        140   HCP Speciality 4 (Cat DT)
PMHCSPD4  DIM       8      8        143   HCP Speciality Date 4 (ccyymmdd)
PMHCSPC5  DIM       3      3        151   HCP Speciality 5 (Cat DT)
PMHCSPD5  DIM       8      8        154   HCP Speciality Date 5 (ccyymmdd)
PMHCRCPT  DIM       25     25       162   HCP Receptionist's Name
PMHCADR1  DIM       60     60       187   HCP Surgery Address Line 1
PMHCADR2  DIM       60     60       247   HCP Surgery Address Line 2
PMHCADR3  DIM       60     60       307   HCP Surgery Address Line 3
PMHCADR4  DIM       60     60       367   HCP Surgery Address Line 4
PMHCADR5  DIM       60     60       427   HCP Surgery Address Line 5
PMHCADR6  DIM       60     60       487   HCP Surgery Address Line 6
PMHCPOST  DIM       8      8        547   HCP Surgery PostCode
PMHCSTAT  DIM       4      4        555   HCP State
PMHCSEML  DIM       80     80       559   HCP Surgery Email Address
PMHCSTEL  DIM       20     20       639   Surgery Telephone
PMHCPAGR  DIM       20     20       659   HCP Pager Telephone
PMHCPAGN  DIM       20     20       679   HCP Pager Number
PMHCAHP1  DIM       25     25       699   Associated HCP 1
PMHCAHP2  DIM       25     25       724   Associated HCP 2
PMHCAHP3  DIM       25     25       749   Associated HCP 3
PMHCAHP4  DIM       25     25       774   Associated HCP 4
PMHCAHP5  DIM       25     25       799   Associated HCP 5
PMHCPRV1  DIM       10     10       824   Provider Number 1 - Reg. No. NZ
PMHCPRV2  DIM       10     10       834   Provider Number 2 - Prev Reg. No NZ
PMHCPRV3  DIM       10     10       844   Provider Number 3
PMHCPRV4  DIM       10     10       854   Provider Number 4
PMHCPRV5  DIM       10     10       864   Provider Number 5
PMHCHTEL  DIM       20     20       874   HCP Home Telephone
PMHCHAD1  DIM       60     60       894   HCP Home Address Line 1
PMHCHAD2  DIM       60     60       954   HCP Home Address Line 2
PMHCHAD3  DIM       60     60       1014  HCP Home Address Line 3
PMHCHAD4  DIM       60     60       1074  HCP Home Address Line 4
PMHCHAD5  DIM       60     60       1134  HCP Home Address Line 5
PMHCHAD6  DIM       60     60       1194  HCP Home Address Line 6
PMHCHPCD  DIM       8      8        1254  HCP Home Post Code
PMHCHEML  DIM       80     80       1262  HCP Home Email Address
.                                         NZ - Healthlink Mailbox
PMHCDFAC  DIM       8      8        1342  Date of First Accred. (ccyymmdd)
PMHCDLAC  DIM       8      8        1350  Date of Last Accred. (ccyymmdd)
PMHCYACC  FORM      2      2        1358  Years Of Accreditation
PMHCATYP  DIM       3      3        1360  Accreditation Type (Cat AT)
PMHCHHCP  FORM      3      3        1363  H.C.S. HCP Code
PMHCOSLV  DIM       3      3        1366  Operating Supervision Level (cat DL
PMHCHCST  FORM      2      2        1369  HCP Status
.                                         0 - Attending Doctor
.                                         1 - Referring Doctor
.                                         2 - Attending & Referring Doctor
.                                         3 - GP
.                                         4 - Attending Doctor & GP
.                                         5 - Referring Doctor & GP
.                                         6 - Attending, Referring & GP
.                                         7 - Other
.                                         8 - Other Attending
.                                         9 - Other Referring
.                                        10 - Other Attending & Referring
PMHCHCSD  DIM       8      8        1371  HCP Status Date (ccyymmdd)
PMHCFAXN  DIM       20     20       1379  HCP's Fax Number
PMHCHFXN  DIM       20     20       1399  HCP's Home Fax Number
PMHCMOBN  DIM       20     20       1419  HCP's Mobile Phone Number
PMHCCRDC  DIM       5      5        1439  Creditor Code (apsmasaf)
PMHCWAHC  DIM       10     10       1444  WA HDP HCP Code
PMHCMRBN  DIM       10     10       1454  Medical Registration Board Number
PMHCNHSN  DIM       10     10       1464  N.H.S. Proscriber Number
PMHCUDF1  DIM       3      3        1474  User Defined Field 1 (Cat R5)
PMHCUDF2  DIM       3      3        1477  User Defined Field 2 (Cat R6)
PMHCUDF3  DIM       3      3        1480  User Defined Field 3 (Cat R7)
PMHCUDF4  DIM       3      3        1483  User Defined Field 4 (Cat R8)
PMHCUDF5  DIM       3      3        1486  User Defined Field 5 (Cat R9)
PMHCUYN1  DIM       1      1        1489  User def. Y/N field 1 (0=NO,1=YES)
PMHCUYN2  DIM       1      1        1490  User def. Y/N field 2 (0=NO,1=YES)
PMHCUYN3  DIM       1      1        1491  User def. Y/N field 3 (0=NO,1=YES)
PMHCUYN4  DIM       1      1        1492  User def. Y/N field 4 (0=NO,1=YES)
PMHCUYN5  DIM       1      1        1493  User def. Y/N field 5 (0=NO,1=YES)
PMHCINSC  DIM       6      6        1494  Insurance Code (patin1af)
PMHCIPLN  DIM       20     20       1500  Insurance Policy Number
PMHCIDTF  DIM       8      8        1520  Insurance Date From (ccyymmdd)
PMHCIDTT  DIM       8      8        1528  Insurance Date To (ccyymmdd)
PMHCMPGN  DIM       20     20       1536  MPG Certificate Number
PMHCAPCI  DIM       1      1        1556  APC (0=NO,1=YES) - NZ
PMHCREGT  DIM       3      3        1557  Registration Type (Cat HR) - NZ
PMHCRGDF  DIM       8      8        1560  Reg. Date From - Gen Reg Date (NZ)
PMHCRGDT  DIM       8      8        1568  Reg. Date To - Gen Reg Date (NZ)
PMHCPRDF  DIM       8      8        1576  Probation. Reg. Date From ccyymmdd
PMHCPRDT  DIM       8      8        1584  Probation. Reg. Date To ccyymmdd
PMHCTSDF  DIM       8      8        1592  Temp. Start Date From (ccyymmdd)
PMHCTFDT  DIM       8      8        1600  Temp. Finish Date To (ccyymmdd)
PMHCVISD  DIM       8      8        1608  Visa Date (ccyymmdd)
PMHCPRFC  DIM       3      3        1616  HCP's Preferd Communication (Cat CZ
PMHCPRFN  DIM       30     30       1619  HCP's Preferred Name
PMHCABNN  DIM       11     11       1649  ABN Number
PMHCUPDF  DIM       1      1        1660  Update Flag M=manual, A=Automatic
PMHCPRMK  DIM       1      1        1661  Processed Record Marker 1=Processed
PMHCSTTS  DIM       1      1        1662  HCP rec status (0=Active/1=Inactive
PMHCDLMI  DIM       8      8        1663  Date Last Made Inactive (ccyymmdd)
PMHCDLMA  DIM       8      8        1671  Date Last Made Active (ccyymmdd)
PMHCCDTE  DIM       8      8        1679  Date Record Created (ccyymmdd)
PMHCCTIM  DIM       8      8        1687  Time Record Created (hh:mm:ss)
PMHCWEBC  DIM       10     10       1695  WEB User Id creator (websecaf)
PMHCLUPD  DIM       8      8        1705  Last Update Date (ccyymmdd)
PMHCLUPT  DIM       8      8        1713  Last Update Time (hh:mm:ss)
PMHCWEBU  DIM       10     10       1721  WEB User Id updated (websecaf)
PMHCSLGP  DIM       1      1        1731  Show Local GP on PMI (plocdoc)
.                                         0=No 1=Yes
PMHCHOSS  DIM       1      1        1732  Hospital Specific HCP
.                                         (0=NO 1=YES) mlthcpaf
PMHCPTYP  DIM       3      3        1733  Provider Type (nzpptyaf Cat pt)
PMHCEXML  DIM       1      1        1736  Exclude from Mailing List 0=N 1=Y
PMHCDEAC  DIM       8      8        1737  Accred. Expiry Date (ccyymmdd)
PMHCCPER  DIM       6      6        1745  Common Person Number or HPN CPN
PMHCSPAR  DIM       40     40       1751  Spare Variable
.
.End of Record                      1791
.
FINDFILE  FILE      ASCII, FIXED=256
.
. Enter the first or the shortest index from the original FD and rename
. the index OLDFILE
.   
OLDFILE   IFILE     SQL, FIXED=1751, KEY="U1-10"
.
CMDLINE   DIM       100
DIM60     DIM       60
.
RECNUM    FORM      8
NEWFILE   DIM       60
NEWPATH   DIM       60
OLDPATH   DIM       60
DEFPATH   DIM       60
SAVEFLG   FORM      1
SP50      INIT    "                                                  "
SP60      INIT    "                                                            "
.
PRGNAM    INIT      "CONVERSION PMSHCPFD"
PRGID     INIT      "F90PMHCP"
VERSION   INIT      "V12.00.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN1000,MAIN0000,MAIN9999 * Yes, no, cancel
.
MAIN1000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
. Loop thru old file and write records to new file
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/compress saved file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          
INIT9999  RETURN
.
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
PREP0000 
. open old file
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE,"pmshcpaf"
          TRAPCLR   IO
          BRANCH    OVRCD,PREP5000       * Original file does not exist
          CLOSE     OLDFILE
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
. Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat pmshcpaf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "spmhcpaf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat pmshcpaf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "spmhcpaf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
. remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "pmshcpaf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "spmhcpaf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "pmshcpaf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.
          APPEND    " 1791 UC1-10",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has
.  to be repeated with for each individual index
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " U58-92,23-57,1-10",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " U824-833,1-10",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
          OPEN      PMSHCPA1,NEWFILE
.
. set the flag to say we want to continue
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
PREP9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and WRFUND1 each record               *
.**********************************************************************
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          PACK      KEY10,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
          MOVE      SP70,PMHCCPER                 * Common person
          MOVE      SP70,PMHCSPAR
.
          PACK      KEY10,PMHCHCPC,SP70
          CALL      WRPMHCP1                      * write to new file
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     PMSHCPA1                      * close new file
          CLOSE     OLDFILE                       * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
READ9999  RETURN
.
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
VALD0000  MOVE      ZERO,EXIT
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or compress save file                                *
.**********************************************************************
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "spmhcpaf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "compress -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not compressed or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.         Get the default directory
.**********************************************************************
DEFT0000  EXECUTE   "ldat pmshcpaf.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding pmshcpaf.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
DEFT9999  RETURN
+
.**********************************************************************
. ---- OLD IO ROUTINES ----
.**********************************************************************
.  Copy and paste the Positional Read routine from the original
.  IO file, rename the routine READSOLD and change the index name
.  to OLDFILE
.
READSOLD  RESET     KEY10
          READ      OLDFILE,KEY10;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE;PMHCHCPC,PMHCHCPT,PMHCTITL,PMHCGNAM,PMHCSNAM:
                        PMHCINIT,PMHCGNDR,PMHCDTOB,PMHCSPC1,PMHCSPD1,PMHCSPC2:
                        PMHCSPD2,PMHCSPC3,PMHCSPD3,PMHCSPC4,PMHCSPD4,PMHCSPC5:
                        PMHCSPD5,PMHCRCPT,PMHCADR1,PMHCADR2,PMHCADR3,PMHCADR4:
                        PMHCADR5,PMHCADR6,PMHCPOST,PMHCSTAT,PMHCSEML,PMHCSTEL:
                        PMHCPAGR,PMHCPAGN,PMHCAHP1,PMHCAHP2,PMHCAHP3,PMHCAHP4:
                        PMHCAHP5,PMHCPRV1,PMHCPRV2,PMHCPRV3,PMHCPRV4,PMHCPRV5:
                        PMHCHTEL,PMHCHAD1,PMHCHAD2,PMHCHAD3,PMHCHAD4,PMHCHAD5:
                        PMHCHAD6,PMHCHPCD,PMHCHEML,PMHCDFAC,PMHCDLAC,PMHCYACC:
                        PMHCATYP,PMHCHHCP,PMHCOSLV,PMHCHCST,PMHCHCSD,PMHCFAXN:
                        PMHCHFXN,PMHCMOBN,PMHCCRDC,PMHCWAHC,PMHCMRBN,PMHCNHSN:
                        PMHCUDF1,PMHCUDF2,PMHCUDF3,PMHCUDF4,PMHCUDF5,PMHCUYN1:
                        PMHCUYN2,PMHCUYN3,PMHCUYN4,PMHCUYN5,PMHCINSC,PMHCIPLN:
                        PMHCIDTF,PMHCIDTT,PMHCMPGN,PMHCAPCI,PMHCREGT,PMHCRGDF:
                        PMHCRGDT,PMHCPRDF,PMHCPRDT,PMHCTSDF,PMHCTFDT,PMHCVISD:
                        PMHCPRFC,PMHCPRFN,PMHCABNN,PMHCUPDF,PMHCPRMK,PMHCSTTS:
                        PMHCDLMI,PMHCDLMA,PMHCCDTE,PMHCCTIM,PMHCWEBC,PMHCLUPD:
                        PMHCLUPT,PMHCWEBU,PMHCSLGP,PMHCHOSS,PMHCPTYP,PMHCEXML:
                        PMHCDEAC,PMHCSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
. New IO write routine
WRPMHCP1  RESET     KEY10
          WRITE     PMSHCPA1,KEY10;PMHCHCPC,PMHCHCPT,PMHCTITL,PMHCGNAM,PMHCSNAM:
                        PMHCINIT,PMHCGNDR,PMHCDTOB,PMHCSPC1,PMHCSPD1,PMHCSPC2:
                        PMHCSPD2,PMHCSPC3,PMHCSPD3,PMHCSPC4,PMHCSPD4,PMHCSPC5:
                        PMHCSPD5,PMHCRCPT,PMHCADR1,PMHCADR2,PMHCADR3,PMHCADR4:
                        PMHCADR5,PMHCADR6,PMHCPOST,PMHCSTAT,PMHCSEML,PMHCSTEL:
                        PMHCPAGR,PMHCPAGN,PMHCAHP1,PMHCAHP2,PMHCAHP3,PMHCAHP4:
                        PMHCAHP5,PMHCPRV1,PMHCPRV2,PMHCPRV3,PMHCPRV4,PMHCPRV5:
                        PMHCHTEL,PMHCHAD1,PMHCHAD2,PMHCHAD3,PMHCHAD4,PMHCHAD5:
                        PMHCHAD6,PMHCHPCD,PMHCHEML,PMHCDFAC,PMHCDLAC,PMHCYACC:
                        PMHCATYP,PMHCHHCP,PMHCOSLV,PMHCHCST,PMHCHCSD,PMHCFAXN:
                        PMHCHFXN,PMHCMOBN,PMHCCRDC,PMHCWAHC,PMHCMRBN,PMHCNHSN:
                        PMHCUDF1,PMHCUDF2,PMHCUDF3,PMHCUDF4,PMHCUDF5,PMHCUYN1:
                        PMHCUYN2,PMHCUYN3,PMHCUYN4,PMHCUYN5,PMHCINSC,PMHCIPLN:
                        PMHCIDTF,PMHCIDTT,PMHCMPGN,PMHCAPCI,PMHCREGT,PMHCRGDF:
                        PMHCRGDT,PMHCPRDF,PMHCPRDT,PMHCTSDF,PMHCTFDT,PMHCVISD:
                        PMHCPRFC,PMHCPRFN,PMHCABNN,PMHCUPDF,PMHCPRMK,PMHCSTTS:
                        PMHCDLMI,PMHCDLMA,PMHCCDTE,PMHCCTIM,PMHCWEBC,PMHCLUPD:
                        PMHCLUPT,PMHCWEBU,PMHCSLGP,PMHCHOSS,PMHCPTYP,PMHCEXML:
                        PMHCDEAC,PMHCCPER,PMHCSPAR
          RETURN
.
          INC       STD001IO
