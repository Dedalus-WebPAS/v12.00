. *****************************************************************************
. * Procedure Name: OPHISENQ                                                  *
. *                                                                           *
. * Function      : Display Theatre History.                                  *
. *                                                                           *
.         08/12/1998  Glenn Berry        (OPHISENQ) 
.                     Fixed (E)xit bug.
.******************************************************************************
          DEFPROC   OPHISENQ
.
          INC       OPRHISFD/INC
          INC       PATCODFD/INC
.
DIM4      DIM       4
DIM8      DIM       8
DIM14     DIM       14
DIM15     DIM       15
DIM16     DIM       16
DIM20     DIM       20
DIM79     DIM       79
DDAT1     DIM       10
DDAT2     DIM       10
NUMITEM   FORM      2
ARRYITEM  DIM       38[24]
FIRSTREC  FORM      1
CMDLINE   DIM       120
SCREEN    FORM      2
PREVITEM  FORM      2[99]
COUNTER   FORM      5
FORM2     FORM      2
.
CHGDESC   DIM       15 
CHGDES1   INIT      "Booked         "
CHGDES2   INIT      "Rescheduled    "
CHGDES3   INIT      "Cancelled      "
.
Z10       INIT      "zzzzzzzzzz"
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
CODESC    INIT      "SC"
.
OPHISENQ  OPEN       OPRHISA1,"oprhisaf"
          OPEN       PATCODE1,"patcodes"
.
          CALL      DHIS0000                      * batch screen processing
.
          MOVE      ZERO,EXIT
.
OPHIS999  GOTO      ENDOPHIS
.*************************************************************************
.*  DHIS0000  :  Display the Theatre History                             *
.*************************************************************************
DHIS0000  DISPLAY   *P1:9,*EF;
          MOVE      ONE,SCREEN                    * init screen no.
          MOVE      ONE,NUMITEM                   * init Item no.
          MOVE      "10",CVERT
          DISPLAY   *P1:9,*V2LON,*ULON,"Date Changed":
                    *P14:9,"Sess. Date":
                    *P25:9,"Type of Change          ":
                    *P50:9,"Reason for Change       "
.
          PACK      KEY38,PURNO,OPDAADMN,OPDAUNIQ,Z20        * position at end  
          CALL      RSOPHIS1
DHIS1000  CALL      RPOPHIS1
          BRANCH    OVRCD,DHIS5000                * no more records
.
          MATCH     PURNO,OPHIURNO
          GOTO      DHIS5000 IF NOT EQUAL
.
          MATCH     OPDAADMN,OPHIEVNT 
          GOTO      DHIS5000 IF NOT EQUAL
.
          MATCH     OPDAUNIQ,OPHIUNIQ 
          GOTO      DHIS5000 IF NOT EQUAL
.
. check if enough room for display
.
DHIS1100  COMPARE   TEN3,NUMITEM  
          GOTO      DHIS3000 IF LESS
.
. we need a new screen
.
DHIS1200  CALL      QNXT0000                     * ask question with Next scrn
          BRANCH    EXIT,DHIS2500:               * Page Forward selected
                         DHIS2000:               * Page Back selected
                         DHIS6500                * Exit
.
. previous screen was selected
.
DHIS2000  PACK      KEY38,ARRYITEM[1]
          CALL      RSOPHIS1                      * pos on 1st key on screen
.
. read back 13 records until 1st key on previous screen is reached
.
          MOVE      ZERO,COUNTER                  * init counter
DHIS2200  CALL      RKOPHIS1                      * read back 1 record
          ADD       ONE,COUNTER                   * increment counter
          COMPARE   TEN2,COUNTER    * check if read far enough
          GOTO      DHIS2200 IF NOT EQUAL
.
. we have read back far enough
.
          DISPLAY   *P1:10,*EF                     * erase screen
          MOVE      "10",CVERT                     * init screen row
          MOVE      ONE,NUMITEM                   * init screen disp counter
          SUB       ONE,SCREEN                    * update screen no.
          GOTO      DHIS3000
.
. Next screen was selected
.
DHIS2500  ADD       ONE,SCREEN                    * increment screen no.
          DISPLAY   *P1:10,*EF                     * erase screen
          MOVE      "10",CVERT                     * init screen row
          MOVE      ONE,NUMITEM                   * init screen disp counter
.
. display record
.
DHIS3000  UNPACK    OPHIEDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DDAT1
.
          UNPACK    OPHIDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DDAT2
.
          MOVE      SP20,TDESC
          MATCH     SP3,OPHICHGD
          IF        !@EQUAL
            PACK      KEY5,CODESC,OPHICHGD,SP10
            CALL      RDCODE1
          ENDIF
.
.
          MOVE      SP30,CHGDESC
          LOAD      CHGDESC,OPHITCHG,CHGDES1,CHGDES2,CHGDES3
.
          DISPLAY   *P1:CVERT,DDAT1:
                    *P14:CVERT,DDAT2:
                    *P25:CVERT,CHGDESC:
                    *P50:CVERT,OPHICHGD,SP1,TDESC
.
.    save each record as it is displayed (for selection purposes)
.
          PACK      ARRYITEM[NUMITEM],OPHIURNO,OPHIEVNT,OPHIUNIQ,OPHIEDAT:
                                      OPHIUCNT,SP20
.
          ADD       ONE,CVERT                     * update row pos
          ADD       ONE,NUMITEM                   * increment counter
          GOTO      DHIS1000                      * get next record
.
. we have no more records to display
.
DHIS5000  CALL      QLST0000
          BRANCH    EXIT,DHIS2500:                * shouldn't get here
                         DHIS2000:                * Page Back selected
                         DHIS6500                 * Exit
.
. Exit Selected
.
DHIS6500  MOVE      ZERO,EXIT
.
DHIS9999  RETURN              
+
.**************************************************************************
.*  QNXT0000  :  ask line 24 question with (N)ext                         *
.**************************************************************************
QNXT0000  
. check which screen we are on. If 1st dont' display (P)revious
.
          BRANCH    SCREEN,QNXT0500
.
. screen is not 1st screen so display "(P)revious"
.
          DISPLAY   *P1:24,*EL:
                    "(",*V2LON,"F",*HOFF,")orward, ":
                    "(",*V2LON,"B",*HOFF,")ack, ":
                    "(",*V2LON,"E",*HOFF,")xit ? "
          MOVE      "29",CCOL
          GOTO      QNXT1000                      * get keyin
.
. screen is 1st screen so don't display "(P)revious"
. 
QNXT0500  DISPLAY   *P1:24,*EL:
                    "(",*V2LON,"F",*HOFF,")orward, ":
                    "(",*V2LON,"E",*HOFF,")xit ? "
          MOVE      "21",CCOL
.
. now get keyin
.
QNXT1000  KEYIN     *PCCOL:24,*DV,UNDLN1:
                    *PCCOL:24,*V2LON,DIM2:
                    *PCCOL:24,*DV,DIM2
.
          TYPE      DIM2
          GOTO      QNXT1500 IF EQUAL
.
          ENDSET    DIM2
          APPEND    SP2,DIM2
          RESET     DIM2
          RESET     ANS
.
          CMOVE     DIM2,ANS
          REP       UPPLOW,ANS                  * Change to lower case
          REP       "F1B2E3",ANS
.
          TYPE      ANS                          * valid letter entered?
          GOTO      QNXT1500 IF NOT EQUAL        * No.
.
          MOVE      ANS,FORM1
          BRANCH    FORM1,QNXT2000,QNXT3000,QNXT4000
QNXT1500  BEEP 
          GOTO      QNXT1000
.
QNXT2000  MOVE      ONE,EXIT                      * Add selected
          GOTO      QNXT9999
.
. Previous was selected so check if it was asked
.
QNXT3000  BRANCH    SCREEN,QNXT5200
          MOVE      TWO,EXIT                     * page Back selected
          GOTO      QNXT9999
.
QNXT4000  MOVE      THREE,EXIT                    * page Forward selected
          GOTO      QNXT9999
.
. Previous was selected but wasn't asked so error
.
QNXT5200  BEEP
          GOTO      QNXT1000
.
QNXT9999  RETURN
+
.**************************************************************************
.*  QLST0000  :  ask line 24 question without (N)ext                      *
.**************************************************************************
QLST0000  
. check which screen we are on. If 1st dont' display (P)revious
.
          BRANCH    SCREEN,QLST0500
.
. screen is not 1st screen so display "(P)revious"
.
          DISPLAY   *P1:24,*EL:
                    "(",*V2LON,"B",*HOFF,")ack, ":
                    "(",*V2LON,"E",*HOFF,")xit ? "
            MOVE      "18",CCOL                     * keyin position
          GOTO      QLST1000                      * get keyin
.
. screen is 1st screen so don't display "(P)revious"
. 
QLST0500  DISPLAY   *P1:24,*EL:
                    "(",*V2LON,"E",*HOFF,")xit ? "
          MOVE      "10",CCOL                     * keyin position
.
. now get keyin
.
QLST1000  KEYIN     *PCCOL:24,*DV,UNDLN1:
                    *PCCOL:24,*V2LON,DIM2:
                    *PCCOL:24,*DV,DIM2
.
          TYPE      DIM2
          GOTO      QLST1500 IF EQUAL
.
          ENDSET    DIM2
          APPEND    SP2,DIM2
          RESET     DIM2
          RESET     ANS
.
          CMOVE     DIM2,ANS
          REP       UPPLOW,ANS              
          REP       "B1E2",ANS
.
          TYPE      ANS                          * valid letter entered?
          GOTO      QLST1500 IF NOT EQUAL        * No.
.
          MOVE      ANS,FORM1
          BRANCH    FORM1,QLST2000,QLST3000
.
QLST1500  BEEP 
          GOTO      QLST1000
.
. Previous was selected so check if it was asked
.
QLST2000  BRANCH    SCREEN,QLST5200
          MOVE      TWO,EXIT                     * page Back selected
          GOTO      QLST9999
.
QLST3000  MOVE      THREE,EXIT                      * Delete selected
          GOTO      QLST9999
.
. Previous was selected but wasn't asked so error
.
QLST5200  BEEP
          GOTO      QLST1000
.
QLST9999  RETURN
.
.
          INC       OPRHISIO/INC
          INC       IBAOVRIO/INC
          INC       PATCODIO/INC
.
ENDOPHIS  ENDPROC
