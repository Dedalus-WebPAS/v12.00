.*****************************************************************************
.*    Program      : F04PTYTD                                                *
.*    Description  : Conversion of patytdaf to new file layout               *
.*                                                                           *
.*    Author       : Don Nguyen                                              *
.*    Date         : 09/01/2014                                              *
.*    Modifications:                                                         *
.*        V10.04.01  25/03/2014  Steve Armstrong      CAR 294451             *
.*                   Mods to have two options for financial years 12 & 13.   *
.*                   Removed Oracle Fix code as it is redundant.             *
.*        V10.04.00  09/01/2014  Don Nguyen           CAR 294451             *
.*                   C-ISAM table fix program created                        *
.*                                                                           *
.*****************************************************************************
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
.
. Old File Definition
.
OLDFILE1  IFILE     SQL, FIXED=250, KEY="U1-8"
.
. NAME    TYPE   LENGTH  PHYSICAL START LOC.  DESCRIPTION
. ----    ----   ------  -------- ----------  -----------
.DPTYDADM DIM       8        8        1       Admission Number
.
.PTYDYADM FORM      5        4        9       YTD Admissions (inc statistics)
.PTYDYSEP FORM      5        4       13       YTD Separations (inc statistics)
.PTYDYSTS FORM      5        4       17       YTD Statistical Separations
.PTYDYBED FORM      6        4       21       YTD Bed Days
.PTYDYHAS FORM      5        4       25       YTD Public Acute Separations
.PTYDYHAB FORM      6        4       29       YTD Public Acute Bed Days
.PTYDYHAT FORM      6        4       33       Public Acute Bed Days-YTD Seps
.PTYDYPAS FORM      5        4       37       YTD Private Acute Separations
.PTYDYPAB FORM      6        4       41       YTD Private Acute Bed Days
.PTYDYPAT FORM      6        4       45       Privat Acute Bed Days-YTD Seps
.PTYDYCOS FORM      5        4       49       YTD Compensable Separations
.PTYDYCOB FORM      6        4       53       YTD Compensable Bed Days
.PTYDYCOT FORM      6        4       57       Comp. Acute Bed Days-YTD Seps
.PTYDYIES FORM      5        4       61       YTD Ineligible Separations
.PTYDYIEB FORM      6        4       65       YTD Ineligible Bed Days
.PTYDYIET FORM      6        4       69       Inelig. Acute Bed Days-YTD Seps
.PTYDYHNS FORM      5        4       73       YTD Public NHT Separations
.PTYDYHNB FORM      6        4       77       YTD Public NHT Bed Days
.PTYDYHNT FORM      6        4       81       Public NHT Bed Days-YTD Seps
.PTYDYPNS FORM      5        4       85       YTD Private NHT Separations
.PTYDYPNB FORM      6        4       89       YTD Private NHT Bed Days
.PTYDYPNT FORM      6        4       93       Private NHT Bed Days-YTD Seps
.PTYDYH5S FORM      5        4       97       YTD Public NHT/NH5 Separations
.PTYDYH5B FORM      6        4      101       YTD Public NHT/NH5 Bed Days
.PTYDYH5T FORM      6        4      105       Public NHT/NH5 Bed Days-YTD Seps
.PTYDYP5S FORM      5        4      109       YTD Private NHT/NH5 Separations
.PTYDYP5B FORM      6        4      113       YTD Private NHT/NH5 Bed Days
.PTYDYP5T FORM      6        4      117       Private NHT/NH5 Bed Days-YTD Seps
.PTYDYHCS FORM      5        4      121       YTD Public Contract Separations
.PTYDYHCB FORM      6        4      125       YTD Public Contract Bed Days
.PTYDYHCT FORM      6        4      129       Public Contract Bed Days-YTD Seps
.PTYDYPCS FORM      5        4      133       YTD Private Contract Separations
.PTYDYPCB FORM      6        4      137       YTD Private Contract Bed Days
.PTYDYPCT FORM      6        4      141       Private Contract Bed Day-YTD Seps
.PTYDYCNS FORM      5        4      145       YTD Compens. Non-Acute Separations
.PTYDYCNB FORM      6        4      149       YTD Compens. Non-Acute Bed Days
.PTYDYCNT FORM      6        4      153       Comp. Non-Acute Bed Days-YTD Seps
.PTYDYINS FORM      5        4      157       YTD Inelig. Non-Acute Separatiopns
.PTYDYINB FORM      6        4      161       YTD Inelig. Non-Acute Bed Days
.PTYDYINT FORM      6        4      165       Inelig Non-Acute Bed Days-YTD Seps
.PTYDYHSD FORM      5        4      169       YTD Public Same Day
.PTYDYPSD FORM      5        4      173       YTD Private Same Day
.PTYDYCSD FORM      5        4      177       YTD Compensable Same Day
.PTYDYISD FORM      5        4      181       YTD Ineligible Same Day
.PTYDYHCD FORM      5        4      185       YTD Public Contract Same Day
.PTYDYPCD FORM      5        4      189       YTD Private Contract Same Day
.PTYDFORM FORM      1        2      193       Unique Key Format for PRS/2
.                                         1 = Base 36
.                                         2 = Episode & Admission number type 1
.                                         3 = Episode & Admission number type 2
.PTYDYUEP FORM      4        3      195       Number of entirely unqualified
.                                             episodes
.PTYDYUBD FORM      5        4      198       Number of unqualified bed days
.PTYDDTDL DIM       8        8      202       Date deleted (ccyymmdd)
.PTYDHOSP DIM       3        3      210       Hospital code (pathspaf)
OLDDSPAR  DIM       37       37     213       Spare Variable
.
.End of Record                      250
.
.
. New File Definition
.
.Data File Definition
.--------------------
PATYTDA1  IFILE     SQL, FIXED=274, KEY="U1-8"
.
.
. NAME    TYPE   LENGTH  PHYSICAL START LOC.  DESCRIPTION
. ----    ----   ------  -------- ----------  -----------
DPTYDADM  DIM       8        8        1       Admission Number
.
PTYDYADM  FORM      5        4        9       YTD Admissions (inc statistics)
PTYDYSEP  FORM      5        4       13       YTD Separations (inc statistics)
PTYDYSTS  FORM      5        4       17       YTD Statistical Separations
PTYDYBED  FORM      6        4       21       YTD Bed Days
PTYDYHAS  FORM      5        4       25       YTD Public Acute Separations
PTYDYHAB  FORM      6        4       29       YTD Public Acute Bed Days
PTYDYHAT  FORM      6        4       33       Public Acute Bed Days-YTD Seps
PTYDYPAS  FORM      5        4       37       YTD Private Acute Separations
PTYDYPAB  FORM      6        4       41       YTD Private Acute Bed Days
PTYDYPAT  FORM      6        4       45       Privat Acute Bed Days-YTD Seps
PTYDYCOS  FORM      5        4       49       YTD Compensable Separations
PTYDYCOB  FORM      6        4       53       YTD Compensable Bed Days
PTYDYCOT  FORM      6        4       57       Comp. Acute Bed Days-YTD Seps
PTYDYIES  FORM      5        4       61       YTD Ineligible Separations
PTYDYIEB  FORM      6        4       65       YTD Ineligible Bed Days
PTYDYIET  FORM      6        4       69       Inelig. Acute Bed Days-YTD Seps
PTYDYHNS  FORM      5        4       73       YTD Public NHT Separations
PTYDYHNB  FORM      6        4       77       YTD Public NHT Bed Days
PTYDYHNT  FORM      6        4       81       Public NHT Bed Days-YTD Seps
PTYDYPNS  FORM      5        4       85       YTD Private NHT Separations
PTYDYPNB  FORM      6        4       89       YTD Private NHT Bed Days
PTYDYPNT  FORM      6        4       93       Private NHT Bed Days-YTD Seps
PTYDYH5S  FORM      5        4       97       YTD Public NHT/NH5 Separations
PTYDYH5B  FORM      6        4      101       YTD Public NHT/NH5 Bed Days
PTYDYH5T  FORM      6        4      105       Public NHT/NH5 Bed Days-YTD Seps
PTYDYP5S  FORM      5        4      109       YTD Private NHT/NH5 Separations
PTYDYP5B  FORM      6        4      113       YTD Private NHT/NH5 Bed Days
PTYDYP5T  FORM      6        4      117       Private NHT/NH5 Bed Days-YTD Seps
PTYDYHCS  FORM      5        4      121       YTD Public Contract Separations
PTYDYHCB  FORM      6        4      125       YTD Public Contract Bed Days
PTYDYHCT  FORM      6        4      129       Public Contract Bed Days-YTD Seps
PTYDYPCS  FORM      5        4      133       YTD Private Contract Separations
PTYDYPCB  FORM      6        4      137       YTD Private Contract Bed Days
PTYDYPCT  FORM      6        4      141       Private Contract Bed Day-YTD Seps
PTYDYCNS  FORM      5        4      145       YTD Compens. Non-Acute Separations
PTYDYCNB  FORM      6        4      149       YTD Compens. Non-Acute Bed Days
PTYDYCNT  FORM      6        4      153       Comp. Non-Acute Bed Days-YTD Seps
PTYDYINS  FORM      5        4      157       YTD Inelig. Non-Acute Separatiopns
PTYDYINB  FORM      6        4      161       YTD Inelig. Non-Acute Bed Days
PTYDYINT  FORM      6        4      165       Inelig Non-Acute Bed Days-YTD Seps
PTYDYHSD  FORM      5        4      169       YTD Public Same Day
PTYDYPSD  FORM      5        4      173       YTD Private Same Day
PTYDYCSD  FORM      5        4      177       YTD Compensable Same Day
PTYDYISD  FORM      5        4      181       YTD Ineligible Same Day
PTYDYHCD  FORM      5        4      185       YTD Public Contract Same Day
PTYDYPCD  FORM      5        4      189       YTD Private Contract Same Day
.
PTYDFORM  FORM      1        2      193       Unique Key Format for PRS/2
.                                         1 = Base 36
.                                         2 = Episode & Admission number type 1
.                                         3 = Episode & Admission number type 2
.
PTYDYUEP  FORM      4        3      195       Number of entirely unqualified
.                                             episodes
PTYDYUBD  FORM      5        4      198       Number of unqualified bed days
PTYDDTDL  DIM       8        8      202       Date deleted (ccyymmdd)
PTYDHOSP  DIM       3        3      210       Hospital code (pathspaf)
PTYDYOPS  FORM      4        3      213       YTD Posthumous Organ Procurement
.                                             separations
PTYDYOPB  FORM      5        4      216       YTD Posthumous Organ Procurement
.                                             separation bed days
PTYDYOPT  FORM      5        4      220       Post Org Procure Bed Days-YTD Seps
PTYDSPAR  DIM      50       50      224       Spare Variable
.
.End of Record                      274
.
. Extras
. ------
PTYDADMN  FORM      8
.
.
. LOCAL VARIABLES
. ---------------
CMDLINE   DIM       200
.
DEFPATH   DIM       60
DIM5      DIM       5  
DIM60     DIM       60
DISNUM    FORM      4
.
FILARRAY  DIM       8[2]
.
INPFILE   DIM       8
.
NEWFILE   DIM       60
NEWPATH   DIM       60
.
OLDPATH   DIM       60
.
RECNUM    FORM      8
.
SAVARRAY  DIM       8[2]
SAVEFLG   FORM      1
.
.
. PROGRAM CONSTANTS
. -----------------
SP60      INIT    "                                                            "
.
.
.
PRGNAM    INIT      "CONVERSION PATYTDFD"
PRGID     INIT      "F04PTYTD"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
MAIN0500  CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000:             * run c-isam fixit patytd12
                            MAIN1000              * run c-isam fixit patytd13
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
MAIN2000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN0000:               * no
                          MAIN9999                * cancel
.
.         Running c-isam fix
.
MAIN5000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/zip saved file
          GOTO      MAIN0500
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
.
.         Load file name array
.
          MOVE      "patytd12",FILARRAY[1]
          MOVE      "patytd13",FILARRAY[2]
.
.         Load saved file name array
.
          MOVE      "sptytd12",SAVARRAY[1]
          MOVE      "sptytd13",SAVARRAY[2]
          
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  HLINE     *G37,2,53,80
          DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit patytd12 (c-isam only)":
                    *P1:6,*V2LON,TWO,*HOFF,". Run Fixit patytd13 (c-isam only)"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * run c-isam fixit patytd12
                            OPTN9000             * run c-isam fixit patytd12
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  DISPLAY   *P53:2,*V2LON,SP1,FILARRAY[COPTION],SP1:
                    *P1:3,*EF;
.
          MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
.
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
.
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
.
PREP0000  MOVE      FILARRAY[COPTION],INPFILE
          CALL      CHEK0000             * check file exists or has been changed
          BRANCH    EXIT,PREP9000
.
          CLOSE     OLDFILE1
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
.         Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat ",CMDLINE
          APPEND    FILARRAY[COPTION],CMDLINE
          APPEND    ".dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    SAVARRAY[COPTION],CMDLINE
          APPEND    ".dat",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat ",CMDLINE
          APPEND    FILARRAY[COPTION],CMDLINE
          APPEND    ".idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    SAVARRAY[COPTION],CMDLINE
          APPEND    ".idx",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
.         Remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    FILARRAY[COPTION],CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    SAVARRAY[COPTION],DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
.         Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    FILARRAY[COPTION],NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " 274 UC1-8",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      PATYTDA1,NEWFILE
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started    : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record No. : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
.
PREP9999  RETURN
+
.********************************************************************
.*                          CHEK0000                                *
.*  Check if file exists, or has already been converted             *
.********************************************************************
.
CHEK0000  MOVE      ZERO,EXIT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND GIVING DIM60 IF IO
          OPEN      OLDFILE1,INPFILE
          TRAPCLR   IO
          BRANCH    OVRCD,CHEK2000       * Original file does not exist
          CLOSE     OLDFILE1
          GOTO      CHEK9999
.
CHEK2000  RESET     DIM60
          SCAN      "I * I",DIM60
          DISPLAY   *P1:23,*EF,"File '",INPFILE,"' ";
          IF        @EQUAL
            DISPLAY   "does not exist - ";
          ELSE
            RESET     DIM60
            SCAN      "I * L",DIM60
            IF        @EQUAL
              DISPLAY   "has already been converted - ";
            ELSE
              DISPLAY   "has caused an IO error - ";
            ENDIF
          ENDIF
          DISPLAY   "File will be bypassed"
.
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
.
CHEK9000  MOVE      ONE,EXIT
.
CHEK9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
.
          PACK      KEY8,SP60
          CALL      READSOLD                     * position at start
READ1000  CALL      READKOLD                     * read next record
          BRANCH    OVRCD,READ6000               * no more records
.
          ADD       ONE,RECNUM                   * update record counter
          ADD       ONE,DISNUM                   * Display counter
.
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          PACK      PTYDSPAR,SP60
          CALL      WRPTYTD1                     * write to new file
.
          GOTO      READ1000                     * get next record
.
READ6000  CLOSE     PATYTDA1                     * close new file
          CLOSE     OLDFILE1                     * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended      : ",CDATE,SP1,CTIMEIS:
                    *P1:24;
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
.
VALD0000  MOVE      ZERO,EXIT
.
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
.
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or zip save file                                     *
.**********************************************************************
.
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    SAVARRAY[COPTION],DIM60
          APPEND    "*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "gzip ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not zipped or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:24,*EL,*B,"Finished processing  ",CDATE,SP1,CTIMEIS:
                    ".  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.*        DEFT0000 -  Get the default directory                       *
.**********************************************************************
.
DEFT0000  PACK      CMDLINE,SP70,SP70,SP70
          CLEAR     CMDLINE
          APPEND    "ldat ",CMDLINE
          APPEND    FILARRAY[COPTION],CMDLINE
          APPEND    ".dat > temp.txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding '",FILARRAY[COPTION],"'.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
.
DEFT9999  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
.         Old I/O Routines
.
READSOLD  RESET     KEY8
          READ      OLDFILE1,KEY8;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE1;DPTYDADM,PTYDYADM,PTYDYSEP,PTYDYSTS,PTYDYBED:
                             PTYDYHAS,PTYDYHAB,PTYDYHAT,PTYDYPAS,PTYDYPAB:
                             PTYDYPAT,PTYDYPNS,PTYDYPNB,PTYDYPNT,PTYDYCOS:
                             PTYDYCOB,PTYDYCOT,PTYDYIES,PTYDYIEB,PTYDYIET:
                             PTYDYHNS,PTYDYHNB,PTYDYHNT,PTYDYH5S,PTYDYH5B:
                             PTYDYH5T,PTYDYP5S,PTYDYP5B,PTYDYP5T,PTYDYHCS:
                             PTYDYHCB,PTYDYHCT,PTYDYPCS,PTYDYPCB,PTYDYPCT:
                             PTYDYCNS,PTYDYCNB,PTYDYCNT,PTYDYINS,PTYDYINB:
                             PTYDYINT,PTYDYHSD,PTYDYPSD,PTYDYCSD,PTYDYISD:
                             PTYDYHCD,PTYDYPCD,PTYDFORM,PTYDYUEP,PTYDYUBD:
                             PTYDDTDL,PTYDHOSP,OLDDSPAR
          GOTO      OVERCOND IF OVER
          MOVE      DPTYDADM,PTYDADMN
          UNPACK    OLDDSPAR,D4,DIM5,D5
          MOVE      D4,PTYDYOPS
          MOVE      DIM5,PTYDYOPB
          MOVE      D5,PTYDYOPT
          RETURN
.
.         New I/O Routines
.
WRPTYTD1  RESET     KEY8
          MOVE      PTYDADMN,DPTYDADM
          WRITE     PATYTDA1,KEY8;DPTYDADM,PTYDYADM,PTYDYSEP,PTYDYSTS,PTYDYBED:
                                  PTYDYHAS,PTYDYHAB,PTYDYHAT,PTYDYPAS,PTYDYPAB:
                                  PTYDYPAT,PTYDYPNS,PTYDYPNB,PTYDYPNT,PTYDYCOS:
                                  PTYDYCOB,PTYDYCOT,PTYDYIES,PTYDYIEB,PTYDYIET:
                                  PTYDYHNS,PTYDYHNB,PTYDYHNT,PTYDYH5S,PTYDYH5B:
                                  PTYDYH5T,PTYDYP5S,PTYDYP5B,PTYDYP5T,PTYDYHCS:
                                  PTYDYHCB,PTYDYHCT,PTYDYPCS,PTYDYPCB,PTYDYPCT:
                                  PTYDYCNS,PTYDYCNB,PTYDYCNT,PTYDYINS,PTYDYINB:
                                  PTYDYINT,PTYDYHSD,PTYDYPSD,PTYDYCSD,PTYDYISD:
                                  PTYDYHCD,PTYDYPCD,PTYDFORM,PTYDYUEP,PTYDYUBD:
                                  PTYDDTDL,PTYDHOSP,PTYDYOPS,PTYDYOPB,PTYDYOPT:
                                  PTYDSPAR
          MOVE      DPTYDADM,PTYDADMN
          RETURN
.
          INC       STD001IO
