.******************************************************************************
.* System         : Clinical Audits                                           *
.* Program        : IBACLA08.PBL                                              *
.* Name           : Codes file Master Listing                                 *
.******************************************************************************
.* Date           : 11/05/1992                                                *
.* Author         : Justin Coates                                             *
.* Function       : To print all categories/codes on CLACODFD                 *
.* Modifications  :                                                           *
.******************************************************************************
.
          INC       STD001FD/PBL
          INC       CLACODFD/INC            * codes file
.
.         program variables
.
STRTCAT   DIM       2             * start category 
STOPCAT   DIM       2             * end   category
STRTCATD  DIM       20            * start category desc
STOPCATD  DIM       20            * end   category desc
.
PRGID     INIT      "IBACLA08"
PRGNAM    INIT      "Codes File Master Listing"
VERSION   INIT      "V12.00.00"
.
.*********************************************************************
.                   ML0000
.         MAINLINE CODE / CONTROLLING LOGIC
.*********************************************************************
ML0000    CALL      INIT0000
.
ML1000    CALL      OPTN0000                * print report ??
          BRANCH    EXIT,ML9999             * exit
.
ML2000    CALL      KRGE0000                * enter range
          CALL      CONTQST
          BRANCH    CEXIT,ML3000,ML2000,ML1000
.
ML3000    CALL      PRGE0000                * print codes
          GOTO      ML1000
.
ML9999    CHAIN     PGM
+
.*********************************************************************
.                   INIT0000                    Called by : ML0000
.*********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P50:24,"Opening"
          DISPLAY   *P58:24,"clacodaf"
          OPEN      CLACODA1,"clacodaf"
INIT9999  RETURN
+
.*********************************************************************
.                   OPTN0000                    Called by : ML1000
.         enter option
.*********************************************************************
OPTN0000  DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Category Sequence":
                    *P1:7,"Select option :"
.
OPTN1000  KEYIN     *P17:7,*DV,UNDLN1:
                    *P17:7,*V2LON,MOPTION
.
          MOVE      ZERO,EXIT
          BRANCH    MOPTION,OPTN9999
.
          MOVE      ONE,EXIT
          COMPARE   ZERO,MOPTION
          GOTO      OPTN9999 IF EQUAL       * exit
.
          BEEP
          GOTO      OPTN1000
.
OPTN9999  RETURN
+
.*********************************************************************
.                   KRGE0000
.         Get the range of categories to print
.*********************************************************************
KRGE0000  DISPLAY   *P1:9,*EF:
                    *P1:09,"Category From : ":
                    *P1:10,"Category To   : "
.
. ******* enter starting category *******
.
KRGE1000  KEYIN     *P17:9,*DV,UNDLN2:
                    *P17:9,*V2LON,STRTCAT:
                    *P17:9,*DV,STRTCAT
.
          PACK      STRTCAT,STRTCAT,SP3 
          MATCH     SP3,STRTCAT
          GOTO      KRGE4500 IF EQUAL       * start
.
          PACK      KEY5,STRTCAT,SP3
          CALL      RDCACOD1
          COMPARE   ZERO,OVRCD
          GOTO      KRGE4900 IF EQUAL       * valid category
.
          DISPLAY   *P1:24,*B,"Invalid category.  ",*EL;
          CALL      HITENTER
          GOTO      KRGE1000
.
.         nothing entered - START
.
KRGE4500  DISPLAY   *P17:9,*V2LON,"Start",*EL;
          MOVE      SP3,STRTCAT
          MOVE      SP20,CACDDESC
.
.         have a valid start code
.
KRGE4900  MOVE      CACDDESC,STRTCATD
          DISPLAY   *P30:09,STRTCATD
.
. ******* enter ending category *******
.
KRGE5000  KEYIN     *P17:10,*DV,UNDLN2:
                    *P17:10,*V2LON,STOPCAT:
                    *P17:10,*DV,STOPCAT
.
          PACK      STOPCAT,STOPCAT,SP3 
          MATCH     SP3,STOPCAT
          GOTO      KRGE5900 IF EQUAL       * start
.
          PACK      KEY5,STOPCAT,SP3
          CALL      RDCACOD1
          COMPARE   ZERO,OVRCD
          GOTO      KRGE6000 IF EQUAL       * valid category
.
          DISPLAY   *P1:24,*B,"Invalid category.  ",*EL;
          CALL      HITENTER
          GOTO      KRGE5000
.
.         nothing entered - FINISH
.
KRGE5900  DISPLAY   *P17:10,*V2LON,"Finish",*EL;
          MOVE      "zzz",STOPCAT
          MOVE      SP20,CACDDESC
.
.         have a valid ending code
.
KRGE6000  MOVE      CACDDESC,STOPCATD
          DISPLAY   *P30:10,STOPCATD
.
          MATCH     STRTCAT,STOPCAT
          GOTO      KRGE9999 IF NOT LESS  * valid range
.
          DISPLAY   *P1:24,*B,"End value must be after Start value.  ",*EL;
          CALL      HITENTER
          GOTO      KRGE5000 
.
KRGE9999  RETURN
+
.*********************************************************************
.*                  PRGE0000                    Called by : DCOD6000 *
.*        Print items from the CLACODFD file                         *
.*********************************************************************
PRGE0000  MOVE      ZERO,CPAGENO            * set page
          CALL      IBACLOCK
          MOVE      "70",CLNO
          PACK      KEY5,STRTCAT,SP3
          CALL      RDCACOD1
          COMPARE   ZERO,OVRCD
          GOTO      PRGE2000 IF EQUAL       * valid cat
.
. ******* loop over file *******
.
PRGE1000  CALL      RKCACOD1
          BRANCH    OVRCD,PRGE5000          * end of file
.
PRGE2000  MATCH     CACDCATG,STOPCAT
          GOTO      PRGE5000 IF LESS        * end of range
.
. ******* increment counters *******
.
          IF        CLNO > 55
            CALL      HEAD80
            MATCH     SP2,STRTCAT
            IF        @EQUAL
              PRINT     *5,"Category From : Start"
            ELSE
              PRINT     *5,"Category From : ",STRTCAT,SP2,STRTCATD
            ENDIF
            MATCH     "zz",STOPCAT
            IF        @EQUAL
              PRINT     *5,"Category To   : Finish"
            ELSE
              PRINT     *5,"Category To   : ",STOPCAT,SP2,STOPCATD
            ENDIF
            MOVE      "8",CLNO
          ENDIF
.
          MATCH     SP3,CACDCODE
          IF        @EQUAL
            PRINT     *N,"Category : ",CACDCATG," - ",CACDDESC
            PRINT     *1,"------------------------------------"
            ADD       TWO,CLNO
          ELSE
            PRINT     *1,CACDCODE,SP2,CACDDESC
            ADD       ONE,CLNO
          ENDIF
          GOTO      PRGE1000
.
. ******* no more data to display *******
.
PRGE5000  PRINT     *N,*N,"*** End of Report ***",*N
.
PRGE9999  RETURN
+
          INC       STD001IO/PBL
          INC       CLACODIO/INC            * codes file
...............................................................................
