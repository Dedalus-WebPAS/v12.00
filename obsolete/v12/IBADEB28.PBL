.-------------------------------------------------------
          INC       STDGENDF     * Required Include
.
VERSION   INIT      "V12.00.00"
.-------------------------------------------------------
. V9.02.00  03.05.2002  Glenn Saunders
.           Export to new release i.e. v9.02 from vf.09
.-------------------------------------------------------
. VF.09.B03 01.02.2001 Charles Handaya
.           Fix Modification of BAS Statement
. VF.09.B02 18.01.2001 Charles Handaya
.           Modification of BAS Statement
. VF.09.B01 17.11.2000 Sandra Barcham
.           Replaced FMSGINFD & IO with IBAGINFD & IO
.-------------------------------------------------------
. VF.01.01  12.05.2000 Charles Handaya
.           Fix proper Document Type description
.           related to call 1783
. VF.01.00  06.03.2000 Charles Handaya
.           Add in Standard Export File  srf 638148
.-------------------------------------------------------
EXPORTFL  FILE      ASCII, FIXED=400     * Export File (Adjust Length to Suit)
EXPORTFB  DIM       8                    * Export File Name
TABDELM   EQU       0x09                 * Tab Delimiter
.
CFDBSQLA  DIM           20        77  Enquiries SQL Form A
CFDBSQLB  DIM           20        97  Enquiries SQL Form B
DIM30     DIM       30
DTOTAMT   FORM      12.2
DTOTTAX   FORM      12.2
ENDDAT    DIM       8
ERRMSG    DIM       30
ERRORS    FORM      6
F12P2     FORM      12.2
FALL      DIM       14
FGTOTAL   DIM       18
FLAGDEB   FORM      1
FLGIBAGL  FORM      1
FTAX      DIM       14
FTOTAL    DIM       14
LASTDEB   DIM       8
LASTDN1   DIM       35
RTOTAMT   FORM      12.2
RTOTTAX   FORM      12.2
STRDAT    DIM       8
.-------------------------------------------------------
.@@@@@ Start of Generated Code @@@@@
. NB - Code & Comments Included Before the above line 
.      will not be overwritten by the generation of a 
.      program. The STDGENDF file and VERSION must appear
.      before the line. 
.-----------------------------------------
.   Program       :   IBADEB28
.   Function      :   Export General Ledger Details           
.   Generated  On :   13/12/2005
.-----------------------------------------
.=========================
.File Description Includes
.-------------------------
          INC       DEBDBTFD/INC
          INC       DEBFDTFD/INC
          INC       DEBFTHFD/INC
          INC       DEBITMFD/INC
          INC       FMSAMADF    
          INC       FMSAMAFD/INC
          INC       FMSLMAFD/INC
          INC       IBAGINFD/INC
          INC       IBAGSTFD/INC
.=========================
.Local Variable Definition
.-------------------------
FROMDAT   DIM      10 * Scr 00 Fld     4
TODAT     DIM      10 * Scr 00 Fld     5
EXPORTFN  DIM      40 * Scr 00 Fld     6
SELLED    DIM       2 * Scr 00 Fld     7
EXPORTYN  FORM      1 * Scr 00 Fld     9
.
ACCEPT    FORM      1
SACCEPT   FORM      1
.
PRGID     INIT      "IBADEB28"
PRGNAM    INIT      "Export General Ledger Details           "
.
.--------------------------------
. MAINLINE - Controlling Logic
.--------------------------------
          TOPIC     PRGID
          MOVE      PORT,PARENTID
          CLOCK     PORT,DIM2
          MOVE      DIM2,PORT
ML0000
          CALL      INIT0000
          CALL      SC00L000
          CHAIN     PGM
.---------------------------------------
. INIT - Open Files & Display Heading
.---------------------------------------
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CCENTRY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
          DISPLAY   *P56:24,"Opening scrsbga1";
          OPEN      SCRSBGA1,"scrsbgaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA1,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrpsta1";
          OPEN      SCRPSTA2,"scrpstaf"
          DISPLAY   *P56:24,"Opening scrslta1";
          OPEN      SCRSLTA1,"scrsltaf"
          DISPLAY   *P56:24,"Opening debdbtaf";
          OPEN      DEBDBTA1,"debdbtaf"
          DISPLAY   *P56:24,"Opening debdbtaf";
          OPEN      DEBDBTA3,"debdbtaf"
          DISPLAY   *P56:24,"Opening debdbtaf";
          OPEN      DEBDBTA4,"debdbtaf"
          DISPLAY   *P56:24,"Opening debfdtaf";
          OPEN      DEBFDTA1,"debfdtaf"
          DISPLAY   *P56:24,"Opening debfdtaf";
          OPEN      DEBFDTA5,"debfdtaf"
          DISPLAY   *P56:24,"Opening debfdtaf";
          OPEN      DEBFDTA6,"debfdtaf"
          DISPLAY   *P56:24,"Opening debfdtaf";
          OPEN      DEBFDTA7,"debfdtaf"
          DISPLAY   *P56:24,"Opening debfdtaf";
          OPEN      DEBFDTA8,"debfdtaf"
          DISPLAY   *P56:24,"Opening debfthaf";
          OPEN      DEBFTHA1,"debfthaf"
          DISPLAY   *P56:24,"Opening debfthaf";
          OPEN      DEBFTHA6,"debfthaf"
          DISPLAY   *P56:24,"Opening debfthaf";
          OPEN      DEBFTHA7,"debfthaf"
          DISPLAY   *P56:24,"Opening debitmaf";
          OPEN      DEBITMA1,"debitmaf"
          DISPLAY   *P56:24,"Opening ibagstaf";
          OPEN      IBAGSTA1,"ibagstaf"
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.
.------------------------------------
. Called for Print Screen Routine 
.------------------------------------
PRTSVARS
          MATCH     "00",SCRID
          IF         @EQUAL
            CALL       PS00L000   * Options                            
          ENDIF
          RETURN
.--------------------------------
. Screen - Options                            
.--------------------------------
SC00L000
SC00L100
          MOVE      "00",SCRID
          MOVE      ZERO,PRCOL
          MOVE      ZERO,PRVERT
          MOVE      "    1",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SCPSCOL,PRCOL
            MOVE     SCPSROW,PRVERT
          ENDIF
          CALL      SBACK000 * Display Background
.
SC00L200
          CALL      CF00L000 * Clear Fields
          CALL      DEFPAR00
SC00L300
          CALL      KY00L000 * Enter Key Fields
          BRANCH    EXIT,SC00L999,SC00L999
SC00L400
          CALL      WP00L000 * Load any WP Details
SC00L500
          CALL      LM00L000 * Load any Batch Details
SC00L600
          CALL      DF00L000 * Display Fields
SC00L700
          CALL      SF00L000 * Select Fields
          BRANCH    EXIT,SC00L100,SC00L100,SC00L200
SC00L800
SC00L900
SC00L999  RETURN
.--------------------------------------------------------
. Clear Fields for Screen : Options                            
.-------------------------------------------------------
CF00L000
          MOVE      SP70,FROMDAT 
          MOVE      SP70,TODAT   
          MOVE      SP70,EXPORTFN
          MOVE      SP70,SELLED  
          MOVE      SP70,FMLADESC
          MOVE      ZERO,EXPORTYN
CF00L999  RETURN
.--------------------------------------------------------
. Enter Key Fields for Options                            
.--------------------------------------------------------
KY00L000
          MOVE      ZERO,EXIT
          GOTO      KY00L999
.
KY00L900  MOVE      ONE,EXIT
KY00L999  RETURN
.---------------------------------------------------------
. Load WP files for : Options                            
.---------------------------------------------------------
WP00L000
          RETURN
.---------------------------------------------------------
. Load Batch Screen for : Options                            
.---------------------------------------------------------
LM00L000
          RETURN
.---------------------------------------------------------
. Display Fields for : Options                            
.---------------------------------------------------------
DF00L000
          BRANCH   ACCEPT,DF00L999
          PACK     KEY15,PRGID,SCRID,SP70
          CALL     RSSCPS1
DF00L010
          CALL     RKSCPS1
          BRANCH   OVRCD,DF00L900
          MATCH    PRGID,SCPSPID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SCRID,SCPSSID
          GOTO     DF00L900 IF NOT EQUAL
          MATCH    SP70,SCPSMTY
          GOTO     DF00L010 IF NOT EQUAL
.
          CALL     PS00L000 * Display Field
          GOTO     DF00L010
DF00L900
DF00L999
          RETURN
.---------------------------------------------------------
. Display/Print Fields for : Options                            
.---------------------------------------------------------
PS00L000   PACK      VAR,SP70,SP70
          PACK     PRINTVAR,SP70,SP70
          MOVE     SCPSITM,FIELDNO
          BRANCH    FIELDNO,PS00L999,PS00L999,PS00L999,PS00L101:
                            PS00L102,PS00L103,PS00L104,PS00L105:
                            PS00L106
          GOTO     PS00L999
.
PS00L101
          MOVE      FROMDAT,VAR      
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L102
          MOVE      TODAT,VAR        
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L103
          MOVE      EXPORTFN,VAR     
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L104
          MOVE      SELLED,VAR       
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L105
          MOVE      FMLADESC,VAR     
          CALL      DISPITEM
          GOTO     PS00L999
.
PS00L106
          MOVE      NO,YESNO
          MOVE      ZERO,FORM2
          MOVE      EXPORTYN,FORM2   
          LOAD      YESNO,FORM2,YES
          MOVE      YESNO,VAR
          CALL      DISPITEM
          GOTO     PS00L999
PS00L999
          RETURN
.---------------------------------------------------------
. Selections for : Options                            
.---------------------------------------------------------
SF00L000  PACK     KEY13,PRGID,SCRID,SP70
          CALL     RSSCSL1
SF00L001  BRANCH   ACCEPT,SF00L020
.
SF00L005  CALL     MTSTQ000
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L006,SF00L101,SF00L102,SF00L103:
                            SF00L006,SF00L104,SF00L105,SF00L006:
                            SF00L106
SF00L006  BEEP
          GOTO     SF00L005
SF00L020  CALL     RKSCSL1
          BRANCH   OVRCD,SF00L090
          MATCH    PRGID,SCSLPID
          GOTO     SF00L090 IF NOT EQUAL
          MATCH    SCRID,SCSLSID
          GOTO     SF00L090 IF NOT EQUAL
          MOVE     SCSLITM,FIELDNO
          BRANCH    FIELDNO,SF00L020,SF00L020,SF00L020,SF00L103:
                            SF00L020,SF00L104,SF00L105,SF00L020:
                            SF00L106
          GOTO     SF00L020
SF00L090  MOVE     ZERO,ACCEPT
          GOTO     SF00L001
.
SF00L101
          MOVE      ZERO,EXIT
          GOTO      SF00L999
.
SF00L102
            MOVE      ZERO,HLEF
            CALL      GETSCR00
          CALL      PROC0000
          MOVE      EXIT,SECTOR
          IF        SECTOR=0
            CALL      FRESCR00
            GOTO      SF00L999
          ENDIF
          IF        SECTOR=1
            CALL      FRESCR00
            MOVE      ZERO,EXIT
            GOTO      SF00L001
          ENDIF
          IF        SECTOR=2
            CALL      FRESCR00
            MOVE      ONE,EXIT
            GOTO      SF00L999
          ENDIF
          MOVE      "00",SCRID
          MOVE      ZERO,PRCOL
          MOVE      ZERO,PRVERT
          MOVE      "    1",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SCPSCOL,PRCOL
            MOVE     SCPSROW,PRVERT
          ENDIF
          CALL      PUTSCR00
          IF        SECTOR=4
            CALL      DF00L000
          ENDIF
          GOTO      SF00L001
.
SF00L103
.
.
          MOVE      "    5",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,TODAT   
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      RDAT0000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L103
            ENDIF
            BRANCH    EXIT,SF00L103,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      FROMDAT,VAR      
          CALL      DISPITEM
.
.
          MOVE      "    5",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      TODAT,VAR        
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L104
          CALL     SELECTED
          STRIP     EXPORTFN
          SFORMAT   VAR,SCPSLEN
          MOVE      EXPORTFN,VAR
          KEYIN     *PCCOL:CVERT,*V2LON,*MASK,*RV,VAR
          MOVE      VAR,EXPORTFN
          SFORMAT   VAR,127
          ENDSET    EXPORTFN
          APPEND    SP70,EXPORTFN
          RESET     EXPORTFN
          CMATCH    EXITCHAR,EXPORTFN
          GOTO      SF00L990 IF EQUAL
          IF        SCPSMAN=1
            MATCH     SP70,EXPORTFN
            IF        @EQUAL
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L104
            ENDIF
          ENDIF
          MOVE      EXPORTFN,VAR     
          CALL      DISPITEM
          GOTO     SF00L001
.
SF00L105
.
.
          MOVE      "    8",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE     SP70,FMLADESC
            PACK      VAR,SP70,SP70
            CALL      DISPITEM
          ENDIF
          CALL     SELECTED
          MOVE      "0",CKEYTYP
          CALL      ELEDG000
          IF        SCPSMAN=1
            IF        EXIT=1
              MOVE      "Mandatory Field - ",DISPMSG
              BEEP
              CALL      MESSAGE1
              GOTO      SF00L105
            ENDIF
            BRANCH    EXIT,SF00L105,SF00L990
          ELSE
            COMPARE   TWO,EXIT
            GOTO      SF00L990 IF EQUAL
          ENDIF
          MOVE      SELLED,VAR       
          CALL      DISPITEM
.
.
          MOVE      "    8",FLDID
          CALL      GETPOSIT
          IF        EXIT=0
            MOVE      FMLADESC,VAR     
            CALL      DISPITEM
          ENDIF
          GOTO     SF00L001
.
SF00L106
          CALL     SELECTED
          CALL      YORN0000
          MOVE       FYESNO,EXPORTYN
          MOVE      NO,YESNO
          MOVE      ZERO,FORM2
          MOVE      EXPORTYN,FORM2   
          LOAD      YESNO,FORM2,YES
          MOVE      YESNO,VAR
          CALL      DISPITEM
          GOTO     SF00L001
SF00L990  MOVE      ONE,EXIT * Exit Char Restart
SF00L999  RETURN
.@@@@@ Function Restart Point @@@@@
.---------------------------------------
. Setup Defaults
.---------------------------------------
DEFPAR00  MOVE      "Start    ",FROMDAT
          MOVE      "Finish   ",TODAT
          MOVE      SP70,STRDAT
          MOVE      Z70,ENDDAT
          PACK      KEY4,CDD,CMM
          REP       " 0",KEY4
          CLEAR     EXPORTFN
          MATCH     "00",SCRID
          IF        @EQUAL
            APPEND    "DACC",EXPORTFN
          ELSE
            APPEND    "DCSH",EXPORTFN
          ENDIF
          APPEND    KEY4,EXPORTFN
          APPEND    SP70,EXPORTFN
          RESET     EXPORTFN
          MOVE      SP70,SELLED
          MOVE      "All Ledgers",FMLADESC
          CALL      OPNGL000
          RETURN
.-------------------------------------------------------------
. Open General Ledger Files
.-------------------------------------------------------------
OPNGL000  MOVE      ONE,FLGIBAGL       * Can't Open IBA GL Files
          MOVE      ZERO,OVRCD
.
          TRAP      OVERCOND IF IO
          OPEN      FMSLMAA1,"fmslmaaf"
          TRAPCLR   IO
          BRANCH    OVRCD,OPNGL999
.
          TRAP      OVERCOND IF IO
          OPEN      FMSAMAA1,"fmsamaaf"
          TRAPCLR   IO
          BRANCH    OVRCD,OPNGL999
          OPEN      FMSAMAA2,"fmsamaaf"
          OPEN      FMSAMAA3,"fmsamaaf"
.
          MOVE      ZERO,FLGIBAGL        * IBA GL Files Open
.
OPNGL999  RETURN
.
.------------------------------------------------------------
. Process General Ledger Transfer
.------------------------------------------------------------
PROC0000  DISPLAY   *P1:24,*EL,"Extracting Details ...."
          CALL      OPEN0000           * Create Export File
          BRANCH    EXIT,PROC9100
.
          CALL      MAKEXP00
          CALL      EXTR0000           * Extract Details & Print Error Report
.
          CALL      CHKERR00           * Check Errors Found and Enable Abort
          BRANCH    EXIT,PROC9000
.
          CALL      REPT0000           * Print Transfer Report & Update Flags
          CALL      TRAN0000
.
PROC9000  MOVE      ZERO,EXIT          * Exit Program
          GOTO      PROC9999
.
PROC9100  MOVE      FOUR,EXIT          * Redisplay Screen & Enable Changes
          GOTO      PROC9999
.
PROC9999  RETURN
.------------------------------------------------------------
. Extract Details to Export File & Print Any Errors
.------------------------------------------------------------
EXTR0000  MOVE      ZERO,ERRORS        * Number of Errors Printed
          MOVE      ZERO,CPAGENO       * Setup Report Details
          CLOCK     TIME,CTIMEIS
          MOVE      "99",CLNO
          CALL      HDEXPB00
.
          MOVE      ZERO,SECTOR
          MOVE      SP70,LASTDEB
          MOVE      ZERO,FLAGDEB
          PACK      KEY25,ZERO,SP70
          CALL      RSDBFD5
EXTR1000  CALL      RKDBFD5
          BRANCH    OVRCD,EXTR9000
          MATCH     "0",DBFDGLS
          GOTO      EXTR9000 IF NOT EQUAL
.
          MATCH     "3",DBFDDTY        * NO Cash Transfer.. Done by Rec System
          IF        @EQUAL
            MOVE      ONE,DBFDGLS      * Flag to Avoid Read Next Time.
            CALL      UPDBFD5
            PACK      KEY25,ZERO,DBFDDEB,DBFDDTY,DBFDDOC,SP70
            CALL      RDDBFD5
            GOTO      EXTR1000
          ENDIF
.
          MATCH     SP70,SELLED
          IF        !@EQUAL
            MATCH     DBFDLED,SELLED
            GOTO      EXTR1000 IF NOT EQUAL
          ENDIF
.
EXTR1100  ADD       ONE,SECTOR
          IF        SECTOR%100=0
            DISPLAY   *P40:24,*V2LON,SECTOR
          ENDIF
          PACK      KEY21,DBFDDEB,DBFDDTY,DBFDDOC
          CALL      RDDBFH1
          BRANCH    OVRCD,EXTR1000
          MATCH     STRDAT,DBFHDDAT    * Doc. Date Range
          GOTO      EXTR1000 IF LESS
          MATCH     DBFHDDAT,ENDDAT
          GOTO      EXTR1000 IF LESS
.
          CALL      WRLINE00           * Write to export file
.
          GOTO      EXTR1000
.
EXTR9000  CALL      ERREND00           * Print End of Errors
          RETURN
.------------------------------------------------------------
. Check on Error Printed and Enable Abort
.------------------------------------------------------------
CHKERR00  MOVE      ZERO,EXIT
          COMPARE   ZERO,ERRORS
          GOTO      CHKERR99 IF EQUAL
          BEEP
          KEYIN     *P1:24,*EL,*V2LON,*DV,ERRORS,*HOFF:
                    " ERRORS Printed do you want to ":
                    "Continue with the Export (Y/N) ? ",*V2LON,ANS
          REP       UPPLOW,ANS
          MATCH     "Y",ANS
          GOTO      CHKERR99 IF EQUAL
          MATCH     "N",ANS
          GOTO      CHKERR00 IF NOT EQUAL
          CLOSE     IBAGINA1,DELETE
          MOVE      ONE,EXIT
CHKERR99  RETURN
.------------------------------------------------------------
. Print Transfer Report & Update GL Flag
.------------------------------------------------------------
REPT0000  DISPLAY   *P1:24,*EL,"Processing Export File ....";
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      IBAGINA1,EXPORTFN  * Reset to Start of File
          TRAPCLR   IO
.
          IF        OVRCD=1
            DISPLAY   *P1:24,*EL,*B,"No records found. ";
            CALL      HITENTER
            GOTO      REPT9999
          ENDIF
.
          MOVE      ZERO,CPAGENO       * Setup Report Details for Audit
          CLOCK     TIME,CTIMEIS
          MOVE      "99",CLNO
          MOVE      SP70,LASTDEB
          MOVE      ZERO,RTOTAMT
          MOVE      ZERO,DTOTAMT
          MOVE      ZERO,SECTOR
          CALL      HDEXPA00
.
REPT1000  CALL      RKIBGI1
          BRANCH    OVRCD,REPT9000
          ADD       ONE,SECTOR
          IF        SECTOR%100=0
            DISPLAY   *P40:24,*V2LON,SECTOR
          ENDIF
          UNPACK    IBGIDESC,DBFDDTY,KEY1,DBFDDEB
          UNPACK    IBGIREF,DBFDDOC,DBFDDLN
          PACK      KEY24,DBFDDEB,DBFDDTY,DBFDDOC,DBFDDLN,SP70
          CALL      RDDBFD1
          IF        OVRCD=0
            MOVE      ONE,DBFDGLS
            CALL      UPDBFD1
          ENDIF
.
          PACK      KEY21,DBFDDEB,DBFDDTY,DBFDDOC,SP70
          CALL      RDDBFH1
.
          MATCH     DBFDDEB,LASTDEB
          GOTO      REPT1100 IF NOT EQUAL
          CALL      PRLINE00           * Print Report Line
.
          GOTO      REPT1000
.
. New Debtor
.
REPT1100  COMPARE   ZERO,CPAGENO       * First Page So No Totals to Print
          GOTO      REPT1200 IF EQUAL
          CALL      DEBTOT00
.
REPT1200  COMPARE   "55",CLNO
          CALL      PRHEAD00 IF NOT LESS
.
          CALL      PRDEBT00
          CALL      PRLINE00
          GOTO      REPT1000
.
REPT9000  IF        CPAGENO<>0
            CALL      DEBTOT00
          ENDIF
          CALL      PEND0000
          MOVE      "Process Complete - ",DISPMSG
          CALL      MESSAGE1
.
REPT9999  RETURN
.------------------------------------------------------------
. Write to Transfer File
.------------------------------------------------------------
WRLINE00  BRANCH    FLGIBAGL,WRLINE50
          CALL      CHKGL000             * Check IBA GL Details
          BRANCH    EXIT,WRLINE99
.
WRLINE50  MOVE      DBFDLED,IBGILEDG
          MOVE      DBFDIAC,IBGIACCT
          MOVE      DBFHDDAT,IBGIDATE
          MATCH     "1",DBFDDTY
          IF        @EQUAL
            MOVE      "-",IBGISIGN       * Invoice Cr Income Dr Control
          ELSE
            MATCH     "4",DBFDDTY
            IF        @EQUAL
              MOVE      "-",IBGISIGN       * Dr Adj. Cr Income Dr Control
            ELSE
              MOVE      " ",IBGISIGN       * Credit  Dr Income Cr Control
            ENDIF
          ENDIF
          MOVE      DBFDTOT,IBGIVALU     * Value of Transaction
.
          UNPACK    SP70,IBGIBASC        * default blank BAS Code
          PACK      KEY6,DBFDGST,SP70    * GST code
          CALL      RDIBGS1
          IF        OVRCD=0 & !(IBGSACTV=1)
            MOVE      IBGSBASC,IBGIBASC
          ENDIF
.
. Key to Debtors Details File (debfdtaf) Stored in Description & Reference
. to be used for update of flag after file is complete.  This way if we abort
. or crash during the first phase it is not a problem and the export may be
. simply run again.
.
          MOVE      DBFDDEB,KEY8
          CALL      RDDBDB1
          PACK      IBGIDESC,DBFDDTY,SP1,DBFDDEB,SP1,DBDBNA1,SP70
          PACK      IBGIREF,DBFDDOC,DBFDDLN,SP70
          MOVE      SP70,IBGIDISS
          MOVE      SP70,IBGIRESP
          MOVE      SP70,IBGICRAC
          MOVE      DBFDCAC,IBGICRAA
          MOVE      DBFDCAC,IBGICRAC
          CALL      WRIBGI1              * Write Transaction Amount
.
WRLINE60  COMPARE   ZERO,DBFDTAX
          GOTO      WRLINE99 IF EQUAL
          MOVE      DBFDTAX,IBGIVALU     * Value of Transaction
          MOVE      DBFDTAC,IBGIACCT
          MOVE      DBFDTCA,IBGICRAA
          MOVE      DBFDCAC,IBGICRAC
          MOVE      SP70,IBGIBASC
          CALL      WRIBGI1              * Write GST Transaction
.
WRLINE99  RETURN
.------------------------------------------------------------
. Check IBA General Ledger Details
.------------------------------------------------------------
CHKGL000  MOVE      ZERO,EXIT
          MOVE      SP70,ERRMSG
.
CHKGL010  PACK      KEY14,DBFDLED,DBFDIAC
          CALL      RDFMAM1
          BRANCH    OVRCD,CHKGL100
          PACK      KEY14,DBFDLED,DBFDCAC
          CALL      RDFMAM1
          BRANCH    OVRCD,CHKGL100
          COMPARE   ZERO,DBFDTAX
          GOTO      CHKGL990 IF EQUAL
          PACK      KEY14,DBFDLED,DBFDTAC
          CALL      RDFMAM1
          BRANCH    OVRCD,CHKGL100
          PACK      KEY14,DBFDLED,DBFDTCA
          CALL      RDFMAM1
          BRANCH    OVRCD,CHKGL100
          GOTO      CHKGL990
.
CHKGL100  BRANCH    EXIT,CHKGL920
          PACK      KEY8,DBFDITM
          CALL      RDDBIT1
          BRANCH    OVRCD,CHKGL910
          PACK      KEY24,DBFDDEB,DBFDDTY,DBFDDOC,DBFDDLN,SP70
          CALL      RDDBFD1
          IF        OVRCD=0
            MOVE      DBITLED,DBFDLED
            MOVE      DBITIAC,DBFDIAC
            MOVE      DBITCAC,DBFDCAC
            MOVE      DBITTAC,DBFDTAC
            MOVE      DBITTCA,DBFDTCA
            CALL      UPDBFD1
          ENDIF
          MOVE      ONE,EXIT
          GOTO      CHKGL010
.
CHKGL910  MOVE      "Invalid Sales Item",ERRMSG
          CALL      PRTERR00
          MOVE      ONE,EXIT
          GOTO      CHKGL999
.
CHKGL920  MOVE      "Invalid Account Code",ERRMSG
          CALL      PRTERR00
          MOVE      ONE,EXIT
          GOTO      CHKGL999
.
CHKGL990  MOVE      ZERO,EXIT
.
CHKGL999  RETURN
.------------------------------------------------------------
. Print Error
.------------------------------------------------------------
PRTERR00  COMPARE   "55",CLNO
          CALL      ERRHED00 IF NOT LESS
          ADD       ONE,ERRORS
.
          MOVE      "Invoice   ",DIM10
          MATCH     "2",DBFDDTY
          IF        @EQUAL
            MOVE      "Cr. Note  ",DIM10
          ENDIF
          MATCH     "3",DBFDDTY
          IF        @EQUAL
            MOVE      "Cash Rec. ",DIM10
          ENDIF
          MATCH     "4",DBFDDTY
          IF        @EQUAL
            MOVE      "Dr.Adjust.",DIM10
          ENDIF
          MATCH     "5",DBFDDTY
          IF        @EQUAL
            MOVE      "Cr.Adjust.",DIM10
          ENDIF
.
          PACK      KEY8,DBFDDEB,SP70
          CALL      RDDBDB1
.
          MOVE      DBFDITM,KEY8
          CALL      RDDBIT1
.
          PRINT     DBFDDEB,SP1,DIM10,SP1,DBFDDOC,"/",DBFDDLN,SP1,DBFDITM,SP1:
                    DBITDES,SP1,ERRMSG
          ADD       ONE,CLNO
          CALL      WRTEXB00
.
PRTERR99  RETURN
.------------------------------------------------------------
. Print Error Page Header
.------------------------------------------------------------
ERRHED00  CALL      HEAD132
          PRINT     *50,"Export Error Report"
          CALL      UND132
          PRINT     "Debtor   Tr. Ty.    Doc Ref.         Sales Item        ":
                    "                           Error Message"
          CALL      UND132
          ADD       "2",CLNO
          RETURN
.------------------------------------------------------------
. Print Error End of Report
.------------------------------------------------------------
ERREND00  COMPARE   ZERO,CPAGENO
          GOTO      ERREND99 IF EQUAL
          CALL      UND132
          PRINT     *50,"*** End of Error Report ***"
ERREND99  RETURN
.------------------------------------------------------------
. Print Debtor Name & Code Heading & Clear Totals
.------------------------------------------------------------
PRDEBT00  MOVE      DBFDDEB,KEY8
          CALL      RDDBDB1
          PRINT     DBDBNA1,"(",DBFDDEB,")"
          ADD       ONE,CLNO
          MOVE      DBFDDEB,LASTDEB
          MOVE      DBDBNA1,LASTDN1
          MOVE      ZERO,DTOTAMT
          MOVE      ZERO,DTOTTAX
          RETURN
.------------------------------------------------------------
. Print Report Detail Line
.------------------------------------------------------------
PRLINE00  COMPARE   "55",CLNO
          CALL      PRHEAD00 IF NOT LESS
          UNPACK    DBFHDDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      "Invoice   ",DIM10
          MATCH     "2",DBFDDTY
          IF        @EQUAL
            MOVE      "Cr. Note  ",DIM10
          ENDIF
          MATCH     "3",DBFDDTY
          IF        @EQUAL
            MOVE      "Cash Rec. ",DIM10
          ENDIF
          MATCH     "4",DBFDDTY
          IF        @EQUAL
            MOVE      "Dr.Adjust.",DIM10
          ENDIF
          MATCH     "5",DBFDDTY
          IF        @EQUAL
            MOVE      "Cr.Adjust.",DIM10
          ENDIF
.
          MATCH     "-",IBGISIGN
          IF        !@EQUAL
            MULT      SEQ,IBGIVALU
          ENDIF
          MOVE      "999,999,999.99",FTOTAL
          FEDIT     IBGIVALU,FTOTAL
          MOVE      DBFDITM,KEY8
          CALL      RDDBIT1
.
          PACK      KEY2,DBFDLED,SP70
          CALL      RDFMLA1
          PACK      DIM30,DBITDES,SP70
.
          PRINT     *5,DIM10,SP1,DBFDDOC,"/",DBFDDLN,SP1,CPCDATE,SP1:
                    DBFDGST,SP1,DIM30,"(",DBFDITM,")":
                    *91,DBFDLED,"/",IBGICRAA,SP1,IBGIACCT,FTOTAL
          ADD       ONE,CLNO
          CALL      WRTEXA00
.
PRLINE20  ADD         IBGIVALU,DTOTAMT
          ADD         IBGIVALU,RTOTAMT
PRLINE99  RETURN
.------------------------------------------------------------
. Print Debtors Total
.------------------------------------------------------------
DEBTOT00  MOVE        "999,999,999.99",FTOTAL
          FEDIT       DTOTAMT,FTOTAL
          CALL        UND132
          PRINT       "Total for ",DBDBNA1,"(",LASTDEB,")",*119,FTOTAL
          ADD         ONE,CLNO
          MOVE        ZERO,DTOTAMT
          CALL        UND132
          RETURN
.------------------------------------------------------------
. Print Report Total
.------------------------------------------------------------
PEND0000  COMPARE     ZERO,CPAGENO
          CALL        PRHEAD00 IF EQUAL
.
          MOVE        "999,999,999,999.99",FGTOTAL
          FEDIT       RTOTAMT,FGTOTAL
          CALL        UND132
          PRINT       "Report Total  ",*115,FGTOTAL
          CALL        UND132
          RETURN
.------------------------------------------------------------
. Print Report Header
.------------------------------------------------------------
PRHEAD00  CALL       HEAD132
          PRINT      "Export File          : ",EXPORTFN
          PRINT      "Documents Dates From : ",FROMDAT," to ",TODAT
          CALL       UND132
          PRINT      "Debtor Doc.Ty. Doc. Ref.        Doc.Date   GST    ":
                     "Sales  Item",*91,"Ledger/Dr Ac":
                     "ct. Cr. Acct.           Amount"
          CALL       UND132
          RETURN
.------------------------------------------------------------
. Open/Create Downlaod File
.------------------------------------------------------------
OPEN0000  STRIP     EXPORTFN
          ENDSET    EXPORTFN
          APPEND    SP70,EXPORTFN
          RESET     EXPORTFN
          MATCH     SP70,EXPORTFN
          GOTO      OPEN9000 IF EQUAL
.
          STRIP     EXPORTFN
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      IBAGINA1,EXPORTFN
          TRAPCLR   IO
          BRANCH    OVRCD,OPEN1000
.
OPEN0100  BEEP
          KEYIN     *P1:24,*EL,"Export File Already Exists. Overwrite (":
                    *V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS
          REP       UPPLOW,ANS
          MOVE      ONE,EXIT
          MATCH     "N",ANS
          GOTO      OPEN9999 IF EQUAL
          MATCH     "Y",ANS
          GOTO      OPEN0100 IF NOT EQUAL
          CLOSE     IBAGINA1,DELETE
.
OPEN1000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          PREP      IBAGINA1,EXPORTFN
          TRAPCLR   IO
          BRANCH    OVRCD,OPEN9100
          MOVE      ZERO,EXIT
          GOTO      OPEN9999
.
OPEN9000  BEEP
          MOVE      "Export Filename MUST be Specified - ",DISPMSG
          CALL      MESSAGE1
          MOVE      ONE,EXIT
          GOTO      OPEN9999
.
OPEN9100  BEEP
          MOVE      "Unable to Create Export File - ",DISPMSG
          CALL      MESSAGE1
          MOVE      ONE,EXIT
          GOTO      OPEN9999
.
OPEN9999  RETURN
.------------------------------------------------------------
. File       : casetran
.
. Functions  : This file contains standard routines for
.              creating and transfering TAB delimited
.              export files to a PC using the unix script
.              keatran.
.
. Routines   : MAKEXP00  - Create the Export File and Headings
.              HDEXPA00  - Export File Normal Header
.              HDEXPB00  - Export File Error Header
.              WRTEXA00  - Write Export Normal details Record to File
.              WRTEXB00  - Write Export Error details Record to File
.              TRAN0000  - Transfer file to PC using script keatran
.
. Note       : The Variable EXPORTYN is used to determine if a
.              export file is to be created. 0=No, 1=Yes.
.
. Variables to be Declared
. ------------------------
. EXPORTFL  FILE      ASCII, FIXED=400     * Export File (Adjust Length to Suit)
. EXPORTFB  DIM       8                    * Export File Name
. TABDELM   EQU       0x09                 * Tab Delimiter
.------------------------------------------------------------
. Create Export File
.------------------------------------------------------------
MAKEXP00  COMPARE   ONE,EXPORTYN
          GOTO      MAKEXP99 IF NOT EQUAL
          MOVE      "EXPORT",KEY6
          PACK      EXPORTFB,KEY6,PORT
          REP       " 0",EXPORTFB
          PREP      EXPORTFL,EXPORTFB
.
MAKEXP99  RETURN
.------------------------------------------------------------
. Export File Normal Header
.------------------------------------------------------------
HDEXPA00  COMPARE   ONE,EXPORTYN
          GOTO      HDEXPA99 IF NOT EQUAL
.
          WRITE     EXPORTFL,SEQ;SP1,TABDELM
          WRITE     EXPORTFL,SEQ;SP1,TABDELM
          WRITE     EXPORTFL,SEQ;SP1,TABDELM
.
          WRITE     EXPORTFL,SEQ;"Debtor",TABDELM:
                                 "Debtor Name",TABDELM:
                                 "Document Type",TABDELM:
                                 "Document Reference",TABDELM:
                                 "Document Date",TABDELM:
                                 "GST Code",TABDELM:
                                 "Sales Item",TABDELM:
                                 "Sales Item Description",TABDELM:
                                 "Ledger",TABDELM:
                                 "Ledger Description",TABDELM:
                                 "Debit Account",TABDELM:
                                 "Credit Account",TABDELM:
                                 "Amount"
HDEXPA99  RETURN
.------------------------------------------------------------
. Export File Error Header
.------------------------------------------------------------
HDEXPB00  COMPARE   ONE,EXPORTYN
          GOTO      HDEXPB99 IF NOT EQUAL
          WRITE     EXPORTFL,SEQ;"Debtor",TABDELM:
                                 "Debtor Name",TABDELM:
                                 "Document Type",TABDELM:
                                 "Document Reference",TABDELM:
                                 "Sales Item",TABDELM:
                                 "Sales Item Description",TABDELM:
                                 "Error Message"
HDEXPB99  RETURN
.------------------------------------------------------------
. Write Normal Detail to Export File
.------------------------------------------------------------
WRTEXA00  COMPARE   ONE,EXPORTYN
          GOTO      WRTEXA99 IF NOT EQUAL
.
          WRITE     EXPORTFL,SEQ;LASTDEB,TABDELM:
                                 LASTDN1,TABDELM:
                                 DIM10,TABDELM:
                                 DBFDDOC,SLASH,DBFDDLN,TABDELM:
                                 CPCDATE,TABDELM:
                                 DBFDGST,TABDELM:
                                 DBFDITM,TABDELM:
                                 DBITDES,TABDELM:
                                 DBFDLED,TABDELM:
                                 FMLADESC,TABDELM:
                                 IBGICRAA,TABDELM:
                                 IBGIACCT,TABDELM:
                                 IBGIVALU
.
WRTEXA99  RETURN
.------------------------------------------------------------
. Write Error Detail to Export File
.------------------------------------------------------------
WRTEXB00  COMPARE   ONE,EXPORTYN
          GOTO      WRTEXB99 IF NOT EQUAL
.
          WRITE     EXPORTFL,SEQ;DBFDDEB,TABDELM:
                                 DBDBNA1,TABDELM:
                                 DIM10,TABDELM:
                                 DBFDDOC,SLASH,DBFDDLN,TABDELM:
                                 DBFDITM,TABDELM:
                                 DBITDES,TABDELM:
                                 ERRMSG
.
WRTEXB99  RETURN
.------------------------------------------------------------
. Execute Transfer Script eg softerm
.------------------------------------------------------------
TRAN0000  COMPARE   ONE,EXPORTYN
          GOTO      TRAN9999 IF NOT EQUAL
          MOVE      YES,DEFYESNO
          CLOSE     EXPORTFL
          DISPLAY   *P1:24,*EL,"Execute Transfer Now (":
                               *V2LON,"Y",*HOFF,"/":
                               *V2LON,"N",*HOFF,") ? "
          MOVE      "24",CVERT
          MOVE      "30",CCOL
          CALL      YORN0000
          COMPARE   ZERO,FYESNO
          GOTO      TRAN9999 IF EQUAL   *  No Transfer
.
          MOVE      ZERO,HLEF
          CALL      GETSCR00
.
          CLEAR     KEY80
          APPEND    "keatran ",KEY80
          APPEND    EXPORTFB,KEY80
          APPEND    ".txt",KEY80
          RESET     KEY80
          EXECUTE   KEY80,TASKID
.
          CALL      PUTSCR00
.
          DISPLAY   *P1:24,*EL,"Transfer Completed Ok (":
                               *V2LON,"Y",*HOFF,"/":
                               *V2LON,"N",*HOFF,") ? "
          MOVE      "24",CVERT
          MOVE      "31",CCOL
          CALL      YORN0000
          COMPARE   ZERO,FYESNO
          GOTO      TRAN0000 IF EQUAL   *  Not ok
.
          PREP      EXPORTFL,EXPORTFB
          CLOSE     EXPORTFL
.
TRAN9999  RETURN
.
.----------------------------------------
. Enter Service Date Range
.----------------------------------------
RDAT0000   MOVE     "    4",FLDID
           CALL     GETPOSIT
           MOVE     SP70,CDAY
           MOVE     CMM,CMON
           MOVE     CYY,CYEAR
           MOVE     CCC,CCENT
           CALL     KEYDATE
           BRANCH   OVRCD,RDAT0100
           MATCH    SP2,CDAY
           GOTO     RDAT0100 IF EQUAL
           PACK     STRDAT,CCENT,CYEAR,CMON,CDAY
           REP      " 0",STRDAT
           CALL     PACDATE
           MOVE     CPCDATE,FROMDAT
           GOTO     RDAT0140
RDAT0100   MOVE     "Start             ",FROMDAT
           MOVE     SP70,STRDAT
.
RDAT0140   MOVE     FROMDAT,VAR
           CALL     DISPITEM
.
RDAT0150   MOVE     "    5",FLDID
           CALL     GETPOSIT
           MOVE     SP70,CDAY
           MOVE     CMM,CMON
           MOVE     CYY,CYEAR
           MOVE     CCC,CCENT
           CALL     KEYDATE
           BRANCH   OVRCD,RDAT0200
           MATCH    SP2,CDAY
           GOTO     RDAT0200 IF EQUAL
           PACK     ENDDAT,CCENT,CYEAR,CMON,CDAY
           REP      " 0",ENDDAT
           CALL     PACDATE
           MOVE     CPCDATE,TODAT
           MATCH    STRDAT,ENDDAT
           GOTO     RDAT0999 IF EQUAL
           GOTO     RDAT0999 IF NOT LESS
           BEEP
           MOVE     "To Date Must Be > From Date - ",DISPMSG
           CALL     MESSAGE1
           GOTO     RDAT0000
.
RDAT0200   MOVE     "Finish             ",TODAT
           MOVE     "99999999",ENDDAT
.
RDAT0999   MOVE     "    4",FLDID
           CALL     GETPOSIT
           RETURN
.
.------------------------------------------------------------
. Enter Ledger
.------------------------------------------------------------
ELEDG000  BRANCH   FLGIBAGL,ELEDG900
          MOVE     ZERO,CKEYTYP
          MOVE     SP70,SELLED
          CALL     KFMLAA00
          BRANCH   EXIT,ELEDG100,ELEDG999
          MOVE     FMLALEDG,SELLED
          GOTO     ELEDG999
.
ELEDG100  MOVE     SP70,SELLED
          MOVE     "All Ledgers ",FMLADESC
          GOTO     ELEDG999
.
ELEDG900  KEYIN    *PCCOL:CVERT,*V2LON,*RV,SELLED
          MOVE     SP70,FMLADESC
.
ELEDG999  RETURN
.
.
.-------------------------------
. Includes for I/O and Keyins
.-------------------------------
          INC       DEBDBTIO/INC
          INC       DEBFDTIO/INC
          INC       DEBFTHIO/INC
          INC       DEBITMIO/INC
          INC       FMSAMAIO/INC
          INC       FMSLMAIO/INC
          INC       FMSLMAKY    
          INC       IBAGINIO/INC
          INC       IBAGSTIO/INC
.
          INC       STDGENCD
.
