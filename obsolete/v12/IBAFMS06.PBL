. ********************************************************************** 
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS06                                             *
. * Desc      :   Posting Account Maintenance                          *
. **********************************************************************
. * Date      :   03.10.1990                                           *
. * Author    :   Neeriem Dye                                          *
. * Mods      :                                                        *
. *                                                                    *
. **********************************************************************
. * V10.13.01 05/12/2018  Don Nguyen        TSK 0838558                *
. *           Deleted temp file (FMSTMP01) on program exit.            *
. *           Removed PORT from temp filenames (FILENAM1 & FILENAM2).  *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
. * VF.00.03  30.12.1999 Charles Handaya                               *
. *           Fixed write to audit files                               *
. * VF.00.02  23.12.1999 Sandra Barcham                                *
. *           Recompiled for FMSCCAFD                                  *
. *           srf 646145                                               *
. * VF.00.01  09.11.1999 Charles Handaya                               *
. *           Fix Y2K of Audit Files Date (CCYYMMDD)                   *
. **********************************************************************
.$$$$$$
.        PROGRAM IBAFMS06
.             open files and initialise variables
.             WHILE user wants to add/modify/delete DO
.                  get desired option (add, modify or delete)
.                  CASE option OF
.                       ADD     : WHILE user wants to add records DO
.                                      accept initial values for screens A,B
.                                      IF user wants to post data
.                                           THEN write new data
.                                      END IF
.                                 END WHILE
.                       MODIFY  : WHILE user wants to modify records DO
.                                      get used to select screen (A,B)
.                                      get and display desired record
.                                      WHILE user wants modify screen data DO
.                                           get number of item to be modified
.                                           allow user to modify option
.                                      END WHILE
.                                      IF user wants to post data
.                                           THEN update data
.                                      END IF
.                                 END WHILE
.                       DELETE  : WHILE user wants to delete records DO
.                                      get and display desired record's screen A
.                                      IF user wants to delete data
.                                           THEN delete data
.                                      END IF
.                                 END WHILE
.                  END CASE
.             END WHILE
.        END
.$$$$$$
. Standard FMS Accounting System Include
.---------------------------------------
          INC       FMSSTDDF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
          INC       FMSAMAFD/INC       * account master file
          INC       FMSCAFFD/INC       * control account file
          INC       FMSCCAFD/INC       * cost centre file
          INC       FMSCCCFD/INC       * consol cost centre file
          INC       FMSCODFD/INC       * codes file
          INC       FMSCONFD/INC       * controlf
          INC       FMSCTRFD/INC       * normal tran file
          INC       FMSLMAFD/INC       * ledger
          INC       FMSLMBFD/INC       * ledger report desc.
          INC       FMSONCFD/INC       * oncost
          INC       FMSRSFFD/INC       * report seq file
          INC       FMSSBAFD/INC       * subjective file
          INC       FMSSSCFD/INC
          INC       FMSSTRFD/INC       * stat tran file
          INC       FMSTCFFD/INC       * total calc file
          INC       FMSTRNFD/INC       * normal tran file
          INC       FMSUWPFD/INC       * user defined wp. file
          INC       FMSXCSFD/INC       * xref file
.
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
FMSTMP01  FILE      ASCII,FIXED=70     * WP Holding File Def'n 1
FILENAM1  DIM       8
NAME1     INIT      "fmstm1"
.
FMSTMP02  FILE      ASCII,FIXED=70     * WP Holding File Def'n 2
FILENAM2  DIM       8
NAME2     INIT      "fmstm2"
.
CP        INIT      "cp "              * word processor variables
TXT       INIT      ".txt "
WP        INIT      "wp "
WPDAT     INIT      ".txt 999 7 -1 70 14"
WPSTAT    FORM      2
.
STATUS    INIT      "                      "  * status
STATUS0   INIT      "Open"
STATUS1   INIT      "Open for Journals Only"
STATUS2   INIT      "Closed"
.
SP12      INIT      "            "
.
ACCEPT    FORM      1                  * accept mode (1=on)
ACCOUNT   DIM       12
AMOUNTNO  FORM      1                  * amount number (param for DAMT, GAMT)
COSTCODE  DIM       12
CURPRTP   DIM       5                  * current print position
LEDGER    DIM       2                  * ledger
OPTNO     FORM      2                  * current option number
OPTIONA   FORM      1                  * screen selection keyin var
SAVACCT   DIM       12                 * key save for account
SAVDACCT  DIM       35                 * key save for account description
SAVDLEDG  DIM       35                 * key save for ledger description
SAVLEDG   DIM       2                  * key save for ledger
SAVPERS   FORM      1                  * save for no of periods (1=13,0=12)
SAVRPNO   DIM       2                  * key save for report number
SAVKEY19  DIM       19                 * key save for audits
SCRNO     FORM      1                  * screen number
SELECT    FORM      2                  * selection
SUBJCODE  DIM       12
WAUDTYPE  FORM      1                  * write audit parameter
FILENAME  DIM       8                  * file
NORMFIL1  DIM       8                  * norm tran file
NORMFIL2  DIM       8                  * norm tran file
STATFILE  DIM       8                  * stat tran file
ACCSTAT   FORM      1                  * 1=cc/subj file
F4YEAR    FORM      4
FMST      INIT      "fmst"
FMSA      INIT      "fmsa"
.
PRGID     INIT      "IBAFMS06"
PRGNAM    INIT      "Posting Account Maintenance"
VERSION   INIT      "V12.00.00"
.
.******************************************************************************
.*  MAINLINE - Controlling Logic                                              *
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML0100    CALL      SELOPTN            * insert/modify/delete/exit ?
          COMPARE   ZERO,COPTION
          GOTO      ML9999 IF EQUAL    * exit program
.
ML0150    CALL      LEDG0000           * get ledger
          BRANCH    EXIT,ML0100,ML0100 * exit option
.
ML0200    CALL      PROC0000           * get second index
          BRANCH    EXIT,ML0150,ML0100 * exit option
.
          BRANCH    COPTION,ML1000,ML2000,ML3000
.
.---- add data ----
.
ML1000    CALL      DEFV0000           * set up default values
          MOVE      ONE,ACCEPT         * accept mode
          CALL      SELA0000           * modify screen A
          BRANCH    EXIT,ML0200        * don't save data
.
          CALL      POST0000           * post data
          GOTO      ML0200             * get next entry
.
.---- modify data ----
.
ML2000    MOVE      ZERO,ACCEPT        * modify mode
          MOVE      "2",WAUDTYPE
          CALL      WAUD0000           * write audit record data
.
          COMPARE   ONE,ACCSTAT        * modify data
          CALL      SELB0000 IF EQUAL
          COMPARE   ONE,ACCSTAT
          CALL      SELA0000 IF NOT EQUAL
          BRANCH    EXIT,ML2190        * quit
.
          CALL      POST0000           * post data
          MOVE      "3",WAUDTYPE
          CALL      WAUD0000           * write audit record data
          GOTO      ML0200
.
ML2190    CALL      DAUD0000           * delete before change audit record
          GOTO      ML0200
.
.---- delete data ----
.
ML3000    MOVE      ZERO,ACCEPT        * modify mode
          CALL      SCRA0000           * display basic details screen
          CALL      DISA0000           * display basic details
          CALL      DELQST             * ok to delete (y/n/c) ?
          SUB       ONE,CEXIT
          BRANCH    CEXIT,ML0200,ML0100
.
          CALL      DELT0000           * delete data
          MOVE      "4",WAUDTYPE
          CALL      WAUD0000           * write audit record data
          GOTO      ML0200             * get next entry
.
.
ML9999    PREP      FMSTMP01,FILENAM1
          CLOSE     FMSTMP01,DELETE
          CHAIN     PGM                * chain back to menu
.
.******************************************************************************
.* INIT - Open Files                             Called by ML0000             *
.******************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";  * control file
          OPEN      CONTROLF,"controlf"
          CALL      RDFMCO51
          CALL      RDFMCO52
.
          DISPLAY   *P64:24,"fmslmaaf";  * ledger file
          OPEN      FMSLMAA1,"fmslmaaf"
.
          DISPLAY   *P64:24,"fmsccaaf";  * cost centre file
          OPEN      FMSCCAA1,"fmsccaaf"
.
          DISPLAY   *P64:24,"fmscccaf";  * consolidated cost centre file
          OPEN      FMSCCCA1,"fmscccaf"
          OPEN      FMSCCCA2,"fmscccaf"
.
          DISPLAY   *P64:24,"fmssbaaf";  * subjective file
          OPEN      FMSSBAA1,"fmssbaaf"
.
          DISPLAY   *P64:24,"fmsamaaf";  * account master file
          OPEN      FMSAMAA1,"fmsamaaf"
          OPEN      FMSAMAA2,"fmsamaaf"
          OPEN      FMSAMAA3,"fmsamaaf"
.
          DISPLAY   *P64:24,"fmsaudam";  * account audit file
          OPEN      FMSAUDAM,"fmsaudam"
.
          DISPLAY   *P64:24,"fmscodaf";  * codes file
          OPEN      FMSCODA1,"fmscodaf"
          OPEN      FMSCODA2,"fmscodaf"
.
          DISPLAY   *P64:24,"fmslmbaf";  * ledger report desc. file
          OPEN      FMSLMBA1,"fmslmbaf"
.
          DISPLAY   *P64:24,"fmsoncaf";  * oncost code
          OPEN      FMSONCA1,"fmsoncaf"
.
          DISPLAY   *P64:24,"fmscafaf";  * control account file
          OPEN      FMSCAFA1,"fmscafaf"
.
          DISPLAY   *P64:24,"fmsuwpaf";  * user defined wp file
          OPEN      FMSUWPA1,"fmsuwpaf"
.
          DISPLAY   *P64:24,"fmsrsfaf";  * report seq file
          OPEN      FMSRSFA2,"fmsrsfaf"
.
          DISPLAY   *P64:24,"fmstcfaf";  * total calc file
          OPEN      FMSTCFA1,"fmstcfaf"
          OPEN      FMSTCFA2,"fmstcfaf"
.
          DISPLAY   *P64:24,"fmssscaf"   * stat subj calc file
          OPEN      FMSSSCA1,"fmssscaf"
          OPEN      FMSSSCA2,"fmssscaf"
.
          DISPLAY   *P64:24,"fmsxcsaf"   * xref file
          OPEN      FMSXCSA1,"fmsxcsaf"
.
.         PACK      FILENAM1,NAME1,PORT
.         REP       " 0",FILENAM1
.
          CALL      TFILENAM
          MOVE      TFILNAME,FILENAM1
.
.         PACK      FILENAM2,NAME2,PORT
.         REP       " 0",FILENAM2
.
          CALL      TFILENAM
          MOVE      TFILNAME,FILENAM2
.
          UNPACK    SP70,NORMFIL1,NORMFIL2,STATFILE
.
          MOVE      ONE,CCENTRY
          MOVE      ONE,CCANLDTE
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
          MOVE      "1",CACCTIND       * set for posting accounts only
.
INIT9999  DISPLAY   *P1:24,*EL;
          RETURN
.******************************************************************************
.* SCRA - Display Screen A                       Called by SELA0000, redisps  *
.******************************************************************************
SCRA0000  MOVE      "1",SCRNO                    * screen number
          DISPLAY   *P1:4,"    Ledger               : ":
                    *V2LON,FMLALEDG,*HOFF,SP12,FMLADESC,*EL:
                    *P1:5,"    Posting Account      : ":
                    *V2LON,FMAMACCT,*EF:
                    *P1:7,*V2LON," 1",*HOFF,". Description          : ":
                    *P1:8,*V2LON," 2",*HOFF,". Account Type         : ":
                    *P1:9,*V2LON," 3",*HOFF,". Status               : ":
                   *P1:10,*V2LON," 4",*HOFF,". Asset Reg. Ind.(Y/N) : ";
.
SCRA1000  MOVE      "10",LINENO                  * starting line number
          MOVE      " 1",COLNO                   * fix collumn number
          MOVE      "4",OPTNO                    * starting option number
          COMPARE   ONE,FMACR1ON
          GOTO      SCRA1100 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          ADD       ONE,OPTNO                    * increment option number
          DISPLAY   *PCOLNO:LINENO,*V2LON,OPTNO,*HOFF,". ",FMACR1NA," : ";
.
SCRA1100  COMPARE   ONE,FMACR2ON
          GOTO      SCRA1200 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          ADD       ONE,OPTNO                    * increment option number
          DISPLAY   *PCOLNO:LINENO,*V2LON,OPTNO,*HOFF,". ",FMACR2NA," : ";
.
SCRA1200  COMPARE   ONE,FMACR3ON
          GOTO      SCRA1300 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          ADD       ONE,OPTNO                    * increment option number
          DISPLAY   *PCOLNO:LINENO,*V2LON,OPTNO,*HOFF,". ",FMACR3NA," : ";
.
SCRA1300  COMPARE   ONE,FMACR4ON
          GOTO      SCRA2000 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          ADD       ONE,OPTNO                    * increment option number
          DISPLAY   *PCOLNO:LINENO,*V2LON,OPTNO,*HOFF,". ",FMACR4NA," : ";
.
SCRA2000  COMPARE   ONE,FMACD1ON
          GOTO      SCRA2100 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          ADD       ONE,OPTNO                    * increment option number
          DISPLAY   *PCOLNO:LINENO,*V2LON,OPTNO,*HOFF,". ",FMACD1NA," : ";
.
SCRA2100  COMPARE   ONE,FMACD2ON
          GOTO      SCRA2200 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          ADD       ONE,OPTNO                    * increment option number
          DISPLAY   *PCOLNO:LINENO,*V2LON,OPTNO,*HOFF,". ",FMACD2NA," : ";
.
SCRA2200  COMPARE   ONE,FMACD3ON
          GOTO      SCRA2300 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          ADD       ONE,OPTNO                    * increment option number
          DISPLAY   *PCOLNO:LINENO,*V2LON,OPTNO,*HOFF,". ",FMACD3NA," : ";
.
SCRA2300  COMPARE   ONE,FMACD4ON
          GOTO      SCRA3000 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          ADD       ONE,OPTNO                    * increment option number
          DISPLAY   *PCOLNO:LINENO,*V2LON,OPTNO,*HOFF,". ",FMACD4NA," : ";
.
SCRA3000  COMPARE   ONE,FMACA1ON
          GOTO      SCRA3100 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          ADD       ONE,OPTNO                    * increment option number
          DISPLAY   *PCOLNO:LINENO,*V2LON,OPTNO,*HOFF,". ",FMACA1NA," : ";
.
SCRA3100  COMPARE   ONE,FMACA2ON
          GOTO      SCRA3200 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          ADD       ONE,OPTNO                    * increment option number
          DISPLAY   *PCOLNO:LINENO,*V2LON,OPTNO,*HOFF,". ",FMACA2NA," : ";
.
SCRA3200  COMPARE   ONE,FMACA3ON
          GOTO      SCRA3300 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          ADD       ONE,OPTNO                    * increment option number
          DISPLAY   *PCOLNO:LINENO,*V2LON,OPTNO,*HOFF,". ",FMACA3NA," : ";
.
SCRA3300  COMPARE   ONE,FMACA4ON
          GOTO      SCRA4000 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          ADD       ONE,OPTNO                    * increment option number
          DISPLAY   *PCOLNO:LINENO,*V2LON,OPTNO,*HOFF,". ",FMACA4NA," : ";
.
SCRA4000  COMPARE   ONE,FMACC1ON
          GOTO      SCRA4100 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          ADD       ONE,OPTNO                    * increment option number
          PACK      FMCDDESC,SP70
          PACK      KEY5,FMACC1NA,SP70
          CALL      RDFMCD1
          DISPLAY   *PCOLNO:LINENO,*V2LON,OPTNO,*HOFF,". ",FMCDDESC," : ";
.
SCRA4100  COMPARE   ONE,FMACC2ON
          GOTO      SCRA4200 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          ADD       ONE,OPTNO                    * increment option number
          PACK      FMCDDESC,SP70
          PACK      KEY5,FMACC2NA,SP70
          CALL      RDFMCD1
          DISPLAY   *PCOLNO:LINENO,*V2LON,OPTNO,*HOFF,". ",FMCDDESC," : ";
.
SCRA4200  COMPARE   ONE,FMACC3ON
          GOTO      SCRA4300 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          ADD       ONE,OPTNO                    * increment option number
          PACK      FMCDDESC,SP70
          PACK      KEY5,FMACC3NA,SP70
          CALL      RDFMCD1
          DISPLAY   *PCOLNO:LINENO,*V2LON,OPTNO,*HOFF,". ",FMCDDESC," : ";
.
SCRA4300  COMPARE   ONE,FMACC4ON
          GOTO      SCRA5000 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          ADD       ONE,OPTNO                    * increment option number
          PACK      FMCDDESC,SP70
          PACK      KEY5,FMACC4NA,SP70
          CALL      RDFMCD1
          DISPLAY   *PCOLNO:LINENO,*V2LON,OPTNO,*HOFF,". ",FMCDDESC," : ";
.
SCRA5000  
.
SCRA9999  RETURN
.******************************************************************************
.* DISA - Display Screen A                       Called by SELA0000, redisps  *
.******************************************************************************
DISA0000  COMPARE   ONE,ACCEPT
          GOTO      DISA0100 IF NOT EQUAL        * in accept mode ?
          COMPARE   "5",SELECT   
          GOTO      DISA9999 IF LESS             * display more ?
.
DISA0100  MOVE      "Invalid Type",ACCTYPE
          MOVE      FMAMTYPE,F1
          LOAD      ACCTYPE,F1,ACCTYPE1,ACCTYPE2,ACCTYPE3:  * get acc type
                               ACCTYPE4,ACCTYPE5,ACCTYPE6,ACCTYPE7:
                               ACCTYPE8,ACCTYPE9
          MOVE      STATUS0,STATUS
          LOAD      STATUS,FMAMSTAT,STATUS1,STATUS2   * get status
          DISPLAY   *P28:7,*V2LON,FMAMDESC:
                    *P28:8,ACCTYPE:
                    *P28:9,STATUS
          COMPARE   ONE,ACCEPT
          GOTO      DISA0200 IF NOT EQUAL        * in accept mode ?
          COMPARE   "5",SELECT   
          GOTO      DISA9999 IF LESS             * display more ?
.
DISA0200  
DISA0300  MOVE      NO,YESNO
          LOAD      YESNO,FMAMASST,YES           * get yes/no
          DISPLAY   *P28:10,*V2LON,YESNO;
.
DISA1000  MOVE      "10",LINENO                  * starting line number
          MOVE      "28",COLNO                   * fix collumn number
          COMPARE   ONE,FMACR1ON
          GOTO      DISA1100 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          DISPLAY   *PCOLNO:LINENO,*V2LON,FMAMREF1;
.
DISA1100  COMPARE   ONE,FMACR2ON
          GOTO      DISA1200 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          DISPLAY   *PCOLNO:LINENO,*V2LON,FMAMREF2;
.
DISA1200  COMPARE   ONE,FMACR3ON
          GOTO      DISA1300 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          DISPLAY   *PCOLNO:LINENO,*V2LON,FMAMREF3;
.
DISA1300  COMPARE   ONE,FMACR4ON
          GOTO      DISA2000 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          DISPLAY   *PCOLNO:LINENO,*V2LON,FMAMREF4;
.
DISA2000  COMPARE   ONE,FMACD1ON
          GOTO      DISA2100 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          UNPACK    FMAMDAT1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *PCOLNO:LINENO,*V2LON,CPCDATE;
.
DISA2100  COMPARE   ONE,FMACD2ON
          GOTO      DISA2200 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          UNPACK    FMAMDAT2,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *PCOLNO:LINENO,*V2LON,CPCDATE;
.
DISA2200  COMPARE   ONE,FMACD3ON
          GOTO      DISA2300 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          UNPACK    FMAMDAT3,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *PCOLNO:LINENO,*V2LON,CPCDATE;
.
DISA2300  COMPARE   ONE,FMACD4ON
          GOTO      DISA3000 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          UNPACK    FMAMDAT4,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *PCOLNO:LINENO,*V2LON,CPCDATE;
.
DISA3000  COMPARE   ONE,FMACA1ON
          GOTO      DISA3100 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          MOVE      "1",AMOUNTNO
          CALL      DAMT0000
.
DISA3100  COMPARE   ONE,FMACA2ON
          GOTO      DISA3200 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          MOVE      "2",AMOUNTNO
          CALL      DAMT0000
.
DISA3200  COMPARE   ONE,FMACA3ON
          GOTO      DISA3300 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          MOVE      "3",AMOUNTNO
          CALL      DAMT0000
.
DISA3300  COMPARE   ONE,FMACA4ON
          GOTO      DISA4000 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          MOVE      "4",AMOUNTNO
          CALL      DAMT0000
.
DISA4000  COMPARE   ONE,FMACC1ON
          GOTO      DISA4100 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          MATCH     SP70,FMAMCOD1
          GOTO      DISA4100 IF EQUAL
.
          MOVE      SP70,FMCDDESC
          PACK      KEY5,FMACC1NA,FMAMCOD1,SP70
          CALL      RDFMCD1
          DISPLAY   *PCOLNO:LINENO,*V2LON,FMAMCOD1,*HOFF,*P44:LINENO,FMCDDESC;
.
DISA4100  COMPARE   ONE,FMACC2ON
          GOTO      DISA4200 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          MATCH     SP70,FMAMCOD2
          GOTO      DISA4200 IF EQUAL
.
          MOVE      SP70,FMCDDESC
          PACK      KEY5,FMACC2NA,FMAMCOD2,SP70
          CALL      RDFMCD1
          DISPLAY   *PCOLNO:LINENO,*V2LON,FMAMCOD2,*HOFF,*P44:LINENO,FMCDDESC;
.
DISA4200  COMPARE   ONE,FMACC3ON
          GOTO      DISA4300 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          MATCH     SP70,FMAMCOD3
          GOTO      DISA4300 IF EQUAL
.
          MOVE      SP70,FMCDDESC
          PACK      KEY5,FMACC3NA,FMAMCOD3,SP70
          CALL      RDFMCD1
          DISPLAY   *PCOLNO:LINENO,*V2LON,FMAMCOD3,*HOFF,*P44:LINENO,FMCDDESC;
.
DISA4300  COMPARE   ONE,FMACC4ON
          GOTO      DISA5000 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,LINENO                   * increment line number
          MATCH     SP70,FMAMCOD4
          GOTO      DISA5000 IF EQUAL
.
          MOVE      SP70,FMCDDESC
          PACK      KEY5,FMACC4NA,FMAMCOD4,SP70
          CALL      RDFMCD1
          DISPLAY   *PCOLNO:LINENO,*V2LON,FMAMCOD4,*HOFF,*P44:LINENO,FMCDDESC;
.
DISA5000  
.
DISA9999  RETURN
.******************************************************************************
.* SCRB - Display Screen B                       Called by SELB0000, redisps  *
.******************************************************************************
SCRB0000  MOVE      "2",SCRNO                    * screen number
          DISPLAY   *P1:4,"    Ledger               : ":
                    *V2LON,FMLALEDG,*HOFF,SP12,FMLADESC,*EL:
                    *P1:5,"    Posting Account      : ":
                    *V2LON,FMAMACCT,*EF:
                    *P1:7,*V2LON," 1",*HOFF,". Status               : ";
.
SCRB9999  RETURN
.******************************************************************************
.* DISB - Display Screen B                       Called by SELB0000, redisps  *
.******************************************************************************
DISB0000  
          MOVE      STATUS0,STATUS
          LOAD      STATUS,FMAMSTAT,STATUS1,STATUS2   * get status
          DISPLAY   *P28:7,*V2LON,STATUS
.
DISB9999  RETURN
.******************************************************************************
.* USRD - User defined options                   Called by SELA               *
.*       Requires : SELECT   (option selected)                                *
.*       Note : clears accept mode when completed last available option       *
.*       Returns  : EXIT = 2 if exitchar entered
.******************************************************************************
USRD0000  MOVE      " 4",OPTNO                   * starting option number
.
          COMPARE   ONE,FMACR1ON
          GOTO      USRD1100 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,OPTNO                    * increment option
          COMPARE   SELECT,OPTNO
          GOTO      USRD1100 IF NOT EQUAL        * desired option ?
.
          MOVE      FMACR1LN,CSTRLEN
          CALL      GSTR0000           * get string
          PACK      FMAMREF1,CSTRING,SP70
          GOTO      USRD9999
.
USRD1100  COMPARE   ONE,FMACR2ON
          GOTO      USRD1200 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,OPTNO                    * increment option
          COMPARE   SELECT,OPTNO
          GOTO      USRD1200 IF NOT EQUAL        * desired option ?
.
          MOVE      FMACR2LN,CSTRLEN
          CALL      GSTR0000           * get string
          PACK      FMAMREF2,CSTRING,SP70
          GOTO      USRD9999
.
USRD1200  COMPARE   ONE,FMACR3ON
          GOTO      USRD1300 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,OPTNO                    * increment option
          COMPARE   SELECT,OPTNO
          GOTO      USRD1300 IF NOT EQUAL        * desired option ?
.
          MOVE      FMACR3LN,CSTRLEN
          CALL      GSTR0000           * get string
          PACK      FMAMREF3,CSTRING,SP70
          GOTO      USRD9999
.
USRD1300  COMPARE   ONE,FMACR4ON
          GOTO      USRD2000 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,OPTNO                    * increment option
          COMPARE   SELECT,OPTNO
          GOTO      USRD2000 IF NOT EQUAL        * desired option ?
.
          MOVE      FMACR4LN,CSTRLEN
          CALL      GSTR0000           * get string
          PACK      FMAMREF4,CSTRING,SP70
          GOTO      USRD9999
.
USRD2000  COMPARE   ONE,FMACD1ON
          GOTO      USRD2100 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,OPTNO                    * increment option
          COMPARE   SELECT,OPTNO
          GOTO      USRD2100 IF NOT EQUAL        * desired option ?
.
          UNPACK    FMAMDAT1,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE
          PACK      FMAMDAT1,CCENT,CYEAR,CMON,CDAY
.
          GOTO      USRD9999
.
USRD2100  COMPARE   ONE,FMACD2ON
          GOTO      USRD2200 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,OPTNO                    * increment option
          COMPARE   SELECT,OPTNO
          GOTO      USRD2200 IF NOT EQUAL        * desired option ?
.
          UNPACK    FMAMDAT2,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE
          PACK      FMAMDAT2,CCENT,CYEAR,CMON,CDAY
.
          GOTO      USRD9999
.
USRD2200  COMPARE   ONE,FMACD3ON
          GOTO      USRD2300 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,OPTNO                    * increment option
          COMPARE   SELECT,OPTNO
          GOTO      USRD2300 IF NOT EQUAL        * desired option ?
.
          UNPACK    FMAMDAT3,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE
          PACK      FMAMDAT3,CCENT,CYEAR,CMON,CDAY
.
          GOTO      USRD9999
.
USRD2300  COMPARE   ONE,FMACD4ON
          GOTO      USRD3000 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,OPTNO                    * increment option
          COMPARE   SELECT,OPTNO
          GOTO      USRD3000 IF NOT EQUAL        * desired option ?
.
          UNPACK    FMAMDAT4,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE
          PACK      FMAMDAT4,CCENT,CYEAR,CMON,CDAY
.
          GOTO      USRD9999
.
USRD3000  COMPARE   ONE,FMACA1ON
          GOTO      USRD3100 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,OPTNO                    * increment option
          COMPARE   SELECT,OPTNO
          GOTO      USRD3100 IF NOT EQUAL        * desired option ?
.
          MOVE      "1",AMOUNTNO
          CALL      GAMT0000
          GOTO      USRD9999
.
USRD3100  COMPARE   ONE,FMACA2ON
          GOTO      USRD3200 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,OPTNO                    * increment option
          COMPARE   SELECT,OPTNO
          GOTO      USRD3200 IF NOT EQUAL        * desired option ?
.
          MOVE      "2",AMOUNTNO
          CALL      GAMT0000
          GOTO      USRD9999
.
USRD3200  COMPARE   ONE,FMACA3ON
          GOTO      USRD3300 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,OPTNO                    * increment option
          COMPARE   SELECT,OPTNO
          GOTO      USRD3300 IF NOT EQUAL        * desired option ?
.
          MOVE      "3",AMOUNTNO
          CALL      GAMT0000
          GOTO      USRD9999
.
USRD3300  COMPARE   ONE,FMACA4ON
          GOTO      USRD4000 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,OPTNO                    * increment option
          COMPARE   SELECT,OPTNO
          GOTO      USRD4000 IF NOT EQUAL        * desired option ?
.
          MOVE      "4",AMOUNTNO
          CALL      GAMT0000
          GOTO      USRD9999
.
USRD4000  COMPARE   ONE,FMACC1ON
          GOTO      USRD4100 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,OPTNO                    * increment option
          COMPARE   SELECT,OPTNO
          GOTO      USRD4100 IF NOT EQUAL        * desired option ?
.
          MOVE      FMACC1NA,CATEGORY
          CALL      KCODFM00                     * get code
          MOVE      FMCDCODE,FMAMCOD1
          MATCH     SP70,FMAMCOD1
          GOTO      USRD9999 IF EQUAL
.
          MOVE      SP70,FMCDDESC
          PACK      KEY5,FMACC1NA,FMAMCOD1,SP70
          CALL      RDFMCD1
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMCOD1,*HOFF,*P44:CVERT,FMCDDESC;
          GOTO      USRD9999
.
USRD4100  COMPARE   ONE,FMACC2ON
          GOTO      USRD4200 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,OPTNO                    * increment option
          COMPARE   SELECT,OPTNO
          GOTO      USRD4200 IF NOT EQUAL        * desired option ?
.
          MOVE      FMACC2NA,CATEGORY
          CALL      KCODFM00                     * get code
          MOVE      FMCDCODE,FMAMCOD2
          MATCH     SP70,FMAMCOD2
          GOTO      USRD9999 IF EQUAL
.
          MOVE      SP70,FMCDDESC
          PACK      KEY5,FMACC2NA,FMAMCOD2,SP70
          CALL      RDFMCD1
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMCOD2,*HOFF,*P44:CVERT,FMCDDESC;
          GOTO      USRD9999
.
USRD4200  COMPARE   ONE,FMACC3ON
          GOTO      USRD4300 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,OPTNO                    * increment option
          COMPARE   SELECT,OPTNO
          GOTO      USRD4300 IF NOT EQUAL        * desired option ?
.
          MOVE      FMACC3NA,CATEGORY
          CALL      KCODFM00                     * get code
          MOVE      FMCDCODE,FMAMCOD3
          MATCH     SP70,FMAMCOD3
          GOTO      USRD9999 IF EQUAL
.
          MOVE      SP70,FMCDDESC
          PACK      KEY5,FMACC3NA,FMAMCOD3,SP70
          CALL      RDFMCD1
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMCOD3,*HOFF,*P44:CVERT,FMCDDESC;
          GOTO      USRD9999
.
USRD4300  COMPARE   ONE,FMACC4ON
          GOTO      USRD5000 IF NOT EQUAL        * is this field activated ?
          ADD       ONE,OPTNO                    * increment option
          COMPARE   SELECT,OPTNO
          GOTO      USRD5000 IF NOT EQUAL        * desired option ?
.
          MOVE      FMACC4NA,CATEGORY
          CALL      KCODFM00                     * get code
          MOVE      FMCDCODE,FMAMCOD4
          MATCH     SP70,FMAMCOD4
          GOTO      USRD9999 IF EQUAL
.
          MOVE      SP70,FMCDDESC
          PACK      KEY5,FMACC4NA,FMAMCOD4,SP70
          CALL      RDFMCD1
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMCOD4,*HOFF,*P44:CVERT,FMCDDESC;
          GOTO      USRD9000                     * make sure accept mode cleared
.
.
USRD5000  BRANCH    ACCEPT,USRD9000              * in accept mode ?
          BEEP                                   * illegal option
.
USRD9000  MOVE      ZERO,ACCEPT                  * clear accept mode
.
USRD9999  RETURN
.******************************************************************************
.* DAMT - Display amount                         Called by lots               *
.*       Requires : AMOUNTNO      (1=amount number)                           *
.*                  LINENO,COLNO  (screen address)                            *
.******************************************************************************
DAMT0000  LOAD      F1,AMOUNTNO,FMACA1DP,FMACA2DP,FMACA3DP,FMACA4DP * dec places
          BRANCH    F1,DAMT0100,DAMT0200,DAMT0300,DAMT0400,DAMT0500,DAMT0600
.
.---- no decimal places ----
.
          LOAD      F12,AMOUNTNO,FMAMAMT1,FMAMAMT2,FMAMAMT3,FMAMAMT4
          DISPLAY   *PCOLNO:LINENO,*V2LON,F12;
          GOTO      DAMT9999
.
.---- 1 decimal places ----
.
DAMT0100  LOAD      F12P1,AMOUNTNO,FMAMAMT1,FMAMAMT2,FMAMAMT3,FMAMAMT4
          DISPLAY   *PCOLNO:LINENO,*V2LON,F12P1;
          GOTO      DAMT9999
.
.---- 2 decimal places ----
.
DAMT0200  LOAD      F12P2,AMOUNTNO,FMAMAMT1,FMAMAMT2,FMAMAMT3,FMAMAMT4
          DISPLAY   *PCOLNO:LINENO,*V2LON,F12P2;
          GOTO      DAMT9999
.
.---- 3 decimal places ----
.
DAMT0300  LOAD      F12P3,AMOUNTNO,FMAMAMT1,FMAMAMT2,FMAMAMT3,FMAMAMT4
          DISPLAY   *PCOLNO:LINENO,*V2LON,F12P3;
          GOTO      DAMT9999
.
.---- 4 decimal places ----
.
DAMT0400  LOAD      F12P4,AMOUNTNO,FMAMAMT1,FMAMAMT2,FMAMAMT3,FMAMAMT4
          DISPLAY   *PCOLNO:LINENO,*V2LON,F12P4;
          GOTO      DAMT9999
.
.---- 5 decimal places ----
.
DAMT0500  LOAD      F12P5,AMOUNTNO,FMAMAMT1,FMAMAMT2,FMAMAMT3,FMAMAMT4
          DISPLAY   *PCOLNO:LINENO,*V2LON,F12P5;
          GOTO      DAMT9999
.
.---- 6 decimal places ----
.
DAMT0600  LOAD      F12P6,AMOUNTNO,FMAMAMT1,FMAMAMT2,FMAMAMT3,FMAMAMT4
          DISPLAY   *PCOLNO:LINENO,*V2LON,F12P6;
          GOTO      DAMT9999
.
DAMT9999  RETURN
.******************************************************************************
.* GAMT - Get amount                             Called by lots               *
.*       Requires : AMOUNTNO (1=amount number)                                *
.*                  CVERT,CCOL    (screen address)                            *
.******************************************************************************
GAMT0000  LOAD      F1,AMOUNTNO,FMACA1DP,FMACA2DP,FMACA3DP,FMACA4DP * dec places
          BRANCH    F1,GAMT0100,GAMT0200,GAMT0300,GAMT0400,GAMT0500,GAMT0600
.
.---- no decimal places ----
.
          KEYIN     *PCCOL:CVERT,"____________":
                    *PCCOL:CVERT,*V2LON,F12:
                    *PCCOL:CVERT,*DV,F12;
          STORE     F12,AMOUNTNO,FMAMAMT1,FMAMAMT2,FMAMAMT3,FMAMAMT4
          GOTO      GAMT9999
.
.---- 1 decimal places ----
.
GAMT0100  KEYIN     *PCCOL:CVERT,"____________._":
                    *PCCOL:CVERT,*V2LON,F12P1:
                    *PCCOL:CVERT,*DV,F12P1;
          STORE     F12P1,AMOUNTNO,FMAMAMT1,FMAMAMT2,FMAMAMT3,FMAMAMT4
          GOTO      GAMT9999
.
.---- 2 decimal places ----
.
GAMT0200  KEYIN     *PCCOL:CVERT,"____________.__":
                    *PCCOL:CVERT,*V2LON,F12P2:
                    *PCCOL:CVERT,*DV,F12P2;
          STORE     F12P2,AMOUNTNO,FMAMAMT1,FMAMAMT2,FMAMAMT3,FMAMAMT4
          GOTO      GAMT9999
.
.---- 3 decimal places ----
.
GAMT0300  KEYIN     *PCCOL:CVERT,"____________.___":
                    *PCCOL:CVERT,*V2LON,F12P3:
                    *PCCOL:CVERT,*DV,F12P3;
          STORE     F12P3,AMOUNTNO,FMAMAMT1,FMAMAMT2,FMAMAMT3,FMAMAMT4
          GOTO      GAMT9999
.
.---- 4 decimal places ----
.
GAMT0400  KEYIN     *PCCOL:CVERT,"____________.____":
                    *PCCOL:CVERT,*V2LON,F12P4:
                    *PCCOL:CVERT,*DV,F12P4;
          STORE     F12P4,AMOUNTNO,FMAMAMT1,FMAMAMT2,FMAMAMT3,FMAMAMT4
          GOTO      GAMT9999
.
.---- 5 decimal places ----
.
GAMT0500  KEYIN     *PCCOL:CVERT,"____________._____":
                    *PCCOL:CVERT,*V2LON,F12P5:
                    *PCCOL:CVERT,*DV,F12P5;
          STORE     F12P5,AMOUNTNO,FMAMAMT1,FMAMAMT2,FMAMAMT3,FMAMAMT4
          GOTO      GAMT9999
.
.---- 6 decimal places ----
.
GAMT0600  KEYIN     *PCCOL:CVERT,"____________.______":
                    *PCCOL:CVERT,*V2LON,F12P6:
                    *PCCOL:CVERT,*DV,F12P6;
          STORE     F12P6,AMOUNTNO,FMAMAMT1,FMAMAMT2,FMAMAMT3,FMAMAMT4
          GOTO      GAMT9999
.
GAMT9999  RETURN
.******************************************************************************
.* DEFV - set up default variables               Called by ML0000             *
.******************************************************************************
DEFV0000  MOVE      SP70,FMAMDESC                * set description
          MOVE      SP70,FMAMSEC                 * set security code
          MOVE      ZERO,FMAMTYPE                * set account type
          MOVE      ZERO,FMAMSTAT                * set status
          MOVE      ZERO,FMAMCTRL                * set bank account
          MOVE      ZERO,FMAMASST                * set Asset Reg. Acc.
          MOVE      ZERO,FMAMPPT                 * set Pres. Pay. Tax
          MOVE      ZERO,FMAMONC                 * set oncost indic
          MOVE      SP70,FMAMONCC                * set oncost code
          MOVE      ZERO,FMAMOBAL                * set opening bal
          MOVE      SP70,FMAMCHEQ                * set cheque no.
          PACK      FMAMBDAT,CCC,SP70            * date of opening balance
.
          UNPACK    SP70,FMAMREF1,FMAMREF2       * set user defined fields
          UNPACK    SP70,FMAMREF3,FMAMREF4
.
          PACK      FMAMDAT1,CCC,SP70
          PACK      FMAMDAT2,CCC,SP70
          PACK      FMAMDAT3,CCC,SP70
          PACK      FMAMDAT4,CCC,SP70
.
          UNPACK    SP70,FMAMCOD1,FMAMCOD2,FMAMCOD3,FMAMCOD4
.
          MOVE      ZERO,FMAMAMT1
          MOVE      ZERO,FMAMAMT2
          MOVE      ZERO,FMAMAMT3
          MOVE      ZERO,FMAMAMT4
.
          MOVE      ONE,FMAMMULT
          MOVE      ONE,FMAMDIV
          MOVE      ZERO,FMAMAYTD
          UNPACK    SP70,FMAMDPLA,FMAMUNIT
.
DEFV9999  RETURN
.******************************************************************************
.* LEDG - Enter Ledger Number                    Called by ML0000             *
.*        Returns : EXIT     (2=EXITCHAR entered, 1=nothing entered, 0=cont.) *
.*                  FMLALEDG (Ledger)                                         *
.******************************************************************************
LEDG0000  MOVE      ZERO,SCRNO         * clear screen number
          DISPLAY   *P1:4,"    Ledger               : ",*EF;
.
          MOVE      "28",CCOL          * set up screen address
          MOVE      "4",CVERT
.
          CALL      KLMAFM00           * get ledger
          MOVE      FMLALEDG,SAVLEDG   * save ledger
          MOVE      FMLADESC,SAVDLEDG  * save ledger description
.
LEDG9999  RETURN
.******************************************************************************
.* PROC - Enter second index                     Called by ML0000             *
.*       Requires : FMLALEDG (Ledger)                                         *
.*       Returns : EXIT     (2=EXITCHAR entered, 1=nothing entered, 0=cont.)  *
.*                 FMAMACCT (Account)                                         *
.******************************************************************************
PROC0000  MOVE      ZERO,SCRNO         * clear screen number
          DISPLAY   *P1:4,"    Ledger               : ":
                    *V2LON,SAVLEDG,*HOFF,SP12,SAVDLEDG,*EL:
                    *P1:5,"    Posting Account      : ",*EF;
          MOVE      "28",CCOL          * set up screen address
          MOVE      "5",CVERT
.
          MOVE      ZERO,FMAMTYPE
          MOVE      "1",CACCTIND       * set for posting accounts only
          COMPARE   ONE,COPTION        * insert mode ?
          CALL      AAMAFM00 IF EQUAL
.
          MOVE      ZERO,FMAMTYPE
          COMPARE   ONE,COPTION        * not insert mode ?
          CALL      KAMAFM00 IF NOT EQUAL
          BRANCH    EXIT,PROC9999,PROC9999       * quit ?
.
          MATCH     "6",FMAMTYPE
          GOTO      PROC5000 IF LESS
.
          DISPLAY   *P1:24,*EL,*B,"This Is Not A Posting Account - ";
          CALL      HITENTER
          GOTO      PROC0000
.
PROC5000  MOVE      FMLALEDG,LEDGER
          MOVE      FMAMACCT,ACCOUNT
          CALL      CKCS0000           * check account not subj/cc related
          COMPARE   TWO,ACCSTAT
          GOTO      PROC0000 IF EQUAL
.
          MOVE      FMAMACCT,SAVACCT   * save account
          MOVE      FMAMDESC,SAVDACCT  * save account desc
.
PROC9999  RETURN
.******************************************************************************
.* SELA - Modify screen A data                   Called by ML0000             *
.*        Returns : EXIT     (1=quit, 0=post)                                 *
.******************************************************************************
SELA0000  MOVE      ZERO,SELECT   
          CALL      GETW0000                     * read in wp data (if req)
          CALL      SCRA0000                     * display screen A
          CALL      DISA0000                     * display screen A data
.
.---- determine option to execute ----
.
SELA0100  ADD       ONE,SELECT                   * get next option
          BRANCH    ACCEPT,SELA0500              * in accept mode ?
          CALL      MQST0000                     * select/post/cancel/notes ?
.
          COMPARE   ZERO,SELECT
          GOTO      SELA9000 IF EQUAL            * post ?
.
          COMPARE   SEQ,SELECT
          GOTO      SELA9500 IF EQUAL            * cancel ?
.
SELA0500  MOVE      "28",CCOL                    * set up screen address
          MOVE      "6",CVERT
          ADD       SELECT,CVERT
          BRANCH    SELECT,SELA1100,SELA1300:    * execute option
                           SELA1400,SELA1500
          GOTO      SELA2000
.
.---- option 1 ----
.
SELA1100  MATCH     FMAMDESC,SP70
          GOTO      SELA1150 IF NOT EQUAL        * description spaces ?
          PACK      FMAMDESC,UNDLN70
.
SELA1150  KEYIN     *PCCOL:CVERT,*DV,FMAMDESC:   * read in description
                    *PCCOL:CVERT,*V2LON,*RV,FMAMDESC;
          PACK      FMAMDESC,FMAMDESC,SP70
.
          MATCH     FMAMDESC,SP70
          GOTO      SELA1100 IF EQUAL            * spaces entered ?
          MATCH     FMAMDESC,UNDLN70
          GOTO      SELA9500 IF EQUAL            * no entry ?
          MATCH     FMAMDESC,EXITCHAR
          GOTO      SELA9500 IF EQUAL            * EXITCHAR entered ?
.
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMDESC;
          GOTO      SELA0100
.
.---- option 3 ----
.
SELA1300  DISPLAY   *P1:24,*V2LON,"1",*HOFF," = ",ACCTYPE1,", ":
                           *V2LON,"2",*HOFF," = ",ACCTYPE2,", ":
                           *V2LON,"3",*HOFF," = ",ACCTYPE3,", ":
                           *V2LON,"4",*HOFF," = ",ACCTYPE4,", ":
                           *V2LON,"5",*HOFF," = ",ACCTYPE5,*EL;
.
SELA1310  KEYIN     *PCCOL:CVERT,"_",*EL:             * read in description
                    *PCCOL:CVERT,*V2LON,F1;
          MOVE      SP70,ACCTYPE
          LOAD      ACCTYPE,F1,ACCTYPE1,ACCTYPE2:     * get description
                               ACCTYPE3,ACCTYPE4,ACCTYPE5
          MATCH     SP70,ACCTYPE
          GOTO      SELA1390 IF NOT EQUAL             * legal option ?
.
          BEEP
          GOTO      SELA1310
.
SELA1390  DISPLAY   *PCCOL:CVERT,*V2LON,ACCTYPE:
                    *P1:24,*EL;
          MOVE      F1,FMAMTYPE
          GOTO      SELA0100
.
.---- option 4 ----
.
SELA1400  DISPLAY   *P1:24,*V2LON,"0",*HOFF," = ",STATUS0,", ":
                           *V2LON,"1",*HOFF," = ",STATUS1,", ":
                           *V2LON,"2",*HOFF," = ",STATUS2,*EL;
.
SELA1410  MOVE      FMAMSTAT,F1
          KEYIN     *PCCOL:CVERT,*DV,F1,*EL:          * read in description
                    *PCCOL:CVERT,*V2LON,*RV,F1;
          MOVE      F1,FMAMSTAT
          MOVE      SP70,STATUS
          LOAD      STATUS,FMAMSTAT,STATUS1,STATUS2   * get description
          MATCH     SP70,STATUS
          GOTO      SELA1490 IF NOT EQUAL             * legal option ?
.
          MOVE      STATUS0,STATUS
          COMPARE   ZERO,FMAMSTAT
          GOTO      SELA1490 IF EQUAL                 * legal option ?
.
          BEEP
          GOTO      SELA1410
.
SELA1490  DISPLAY   *PCCOL:CVERT,*V2LON,STATUS:
                    *P1:24,*EL;
          GOTO      SELA0100
.
.---- option 5 ----
.
SELA1500  MOVE      "N",DEFYESNO                 * default to no
          CALL      YORN0000                     * get yes or no
          MOVE      FYESNO,FMAMASST
          GOTO      SELA0100
.
.---- user defined fields ----
.
SELA2000  CALL      USRD0000                     * execute user defined option
          COMPARE   "2",EXIT
          GOTO      SELA9500 IF EQUAL            * exitchar entered ?
          GOTO      SELA0100                     * get next option
.
.---- exit points ----
.
SELA9000  MOVE      ZERO,EXIT                    * post
          GOTO      SELA9999
.
SELA9500  MOVE      ONE,EXIT                     * cancel
.
SELA9999  RETURN
.******************************************************************************
.* SELB - Modify screen B data                   Called by ML0000             *
.*        Returns : EXIT     (1=quit, 0=post)                                 *
.******************************************************************************
SELB0000  MOVE      ZERO,SELECT   
          CALL      GETW0000                     * read in wp data (if req)
          CALL      SCRB0000                     * display screen A
          CALL      DISB0000                     * display screen A data
.
.---- determine option to execute ----
.
SELB0100  ADD       ONE,SELECT                   * get next option
          BRANCH    ACCEPT,SELB0500              * in accept mode ?
          CALL      MQST0000                     * select/post/cancel/notes ?
.
          COMPARE   ZERO,SELECT
          GOTO      SELB9000 IF EQUAL            * post ?
.
          COMPARE   SEQ,SELECT
          GOTO      SELB9500 IF EQUAL            * cancel ?
.
SELB0500  MOVE      "28",CCOL                    * set up screen address
          MOVE      "6",CVERT
          ADD       SELECT,CVERT
          BRANCH    SELECT,SELB1400              * execute option
          BEEP
          GOTO      SELB0100
.
.---- option 1 ----
.
SELB1400  DISPLAY   *P1:24,*V2LON,"0",*HOFF," = ",STATUS0,", ":
                           *V2LON,"1",*HOFF," = ",STATUS1,", ":
                           *V2LON,"2",*HOFF," = ",STATUS2,*EL;
.
SELB1410  MOVE      FMAMSTAT,F1
          KEYIN     *PCCOL:CVERT,*DV,F1,*EL:          * read in description
                    *PCCOL:CVERT,*V2LON,*RV,F1;
          MOVE      F1,FMAMSTAT
          MOVE      SP70,STATUS
          LOAD      STATUS,FMAMSTAT,STATUS1,STATUS2   * get description
          MATCH     SP70,STATUS
          GOTO      SELB1490 IF NOT EQUAL             * legal option ?
.
          MOVE      STATUS0,STATUS
          COMPARE   ZERO,FMAMSTAT
          GOTO      SELB1490 IF EQUAL                 * legal option ?
.
          BEEP
          GOTO      SELB1410
.
SELB1490  DISPLAY   *PCCOL:CVERT,*V2LON,STATUS:
                    *P1:24,*EL;
          GOTO      SELB0100
.
.---- exit points ----
.
SELB9000  MOVE      ZERO,EXIT                    * post
          GOTO      SELB9999
.
SELB9500  MOVE      ONE,EXIT                     * cancel
.
SELB9999  RETURN
.******************************************************************************
.* MQST - question select/post/cancel/notes ?    Called by SELA, SELB         *
.*        Returns : Selection (-1 = Cancel, 0 = Post, other = selection)      *
.******************************************************************************
MQST0000  DISPLAY   *P1:24,*EL,"Select, (",*V2LON,"P":
                    *HOFF,")ost, (",*V2LON,"C",*HOFF,")ancel";
          MOVE      "24",F2
          COMPARE   ONE,FMACW1ON
          GOTO      MQST0500 IF NOT EQUAL        * wp activated ?
.
          DISPLAY   ", (",*V2LON,"N",*HOFF,")otes";
          ADD       "9",F2
.
MQST0500  DISPLAY   " : ";
          ADD       "4",F2
.
MQST1000  KEYIN     *PF2:24,"__":
                    *PF2:24,*V2LON,*JR,DIM2,*EL;
          PACK      DIM2,DIM2,SP70
          REP       UPPLOW,DIM2
          MOVE      SEQ,SELECT
          MATCH     " C",DIM2
          GOTO      MQST9999 IF EQUAL            * cancel ?
.
          MOVE      ZERO,SELECT
          MATCH     " P",DIM2
          GOTO      MQST9999 IF EQUAL            * post ?
.
          COMPARE   ONE,FMACW1ON
          GOTO      MQST2000 IF NOT EQUAL        * notes active ?
          MATCH     " N",DIM2
          GOTO      MQST2000 IF NOT EQUAL        * notes ?
.
          CALL      NOTE0000                     * execute wp
          CALL      RCODFM00                     * redisplay screen
          GOTO      MQST0000
.
MQST2000  MOVE      DIM2,SELECT                  * selection made
          TYPE      DIM2
          GOTO      MQST9999 IF EQUAL            * valid numeric ?
          BEEP
          GOTO      MQST1000
.
MQST9999  RETURN
.******************************************************************************
.* GETW - read in wp data                        Called by SELA               *
.******************************************************************************
GETW0000  COMPARE   ONE,FMACW1ON
          GOTO      GETW9999 IF NOT EQUAL        * notes active ?
.
          PREP      FMSTMP01,FILENAM1            * clear and open temp file
          PACK      KEY17,SAVLEDG,SAVACCT,SP70
          CALL      RSFMUW1                      * go to start of records
.
GETW1000  CALL      RKFMUW1                      * get next record
          BRANCH    OVRCD,GETW2000               * no more records ?
          PACK      KEY14,FMUWLEDG,FMUWACCT,SP70
          MATCH     KEY14,KEY17
          GOTO      GETW2000 IF NOT EQUAL        * no more records ?
.
          WRITE     FMSTMP01,SEQ;FMUWLINE
          GOTO      GETW1000
.
GETW2000  WEOF      FMSTMP01,SEQ
          CLOSE     FMSTMP01
.
GETW9999  RETURN
.******************************************************************************
.* NOTE - Execute word processor                 Called by MQST               *
.******************************************************************************
NOTE0000  
.         DISPLAY   *P1:24,"Initialising the Wordprocessor - ":
.                    *BLINKON,*V2LON,"Please Wait",*EL;
.
.          PREP      FMSTMP02,FILENAM2
.          WRITE     FMSTMP02,SEQ;PRGID,SP10,SP2,CNAME,SP10,SP5,CDATE
.          WRITE     FMSTMP02,SEQ;VERSION,SP10,SP5,PRGNAM
.          WRITE     FMSTMP02,SEQ;SP1
.          WRITE     FMSTMP02,SEQ;"    Ledger               : ":
.                    FMLALEDG,SP10,SP4,FMLADESC
.          WRITE     FMSTMP02,SEQ;"    Posting Account      : ":
.                    FMAMACCT,SP4,FMAMDESC
.          WRITE     FMSTMP02,SEQ;"    Notes"
.          WRITE     FMSTMP02,SEQ;SP1
.
.          OPEN      FMSTMP01,FILENAM1            * go to start of records
.
.NOTE1000  READ      FMSTMP01,SEQ;FMUWLINE        * read in wp data
.          GOTO      NOTE2000 IF OVER
.
.          WRITE     FMSTMP02,SEQ;FMUWLINE
.          GOTO      NOTE1000
.
NOTE2000  CLOSE     FMSTMP01
          CLEAR     CMDLINE
          APPEND    FILENAM1,CMDLINE
          APPEND    TXT,CMDLINE
          RESET     CMDLINE
          MOVE      ONE,WPSTAT
          WORD      CMDLINE,999,2,7,76,22,WPSTAT
.         PACK      CMDLINE,WP,FILENAM2,WPDAT
.         EXECUTE   CMDLINE,TASKID               * initiate word processor
.
.         CALL      WEOF0000                     * make sure something in file
.
.         CLOSE     FMSTMP02
.         CLOSE     FMSTMP01
.
.         CLEAR     CMDLINE                      * copy file to original
.         PACK      CMDLINE,CP,FILENAM2,TXT,FILENAM1,TXT 
.         EXECUTE   CMDLINE,TASKID
.
NOTE9999  RETURN
.**********************************************************************
.  WEOF - Make sure wp file not empty                  Called By NOTE
.**********************************************************************
.WEOF0000  CLOSE     FMSTMP02
.          MOVE      ZERO,OVRCD
.          TRAP      OVERCOND IF IO
.          OPEN      FMSTMP02,FILENAM2
.          TRAPCLR   IO
.          COMPARE   ONE,OVRCD
.          GOTO      WEOF9999 IF NOT EQUAL
..
.          PREP      FMSTMP02,FILENAM2
.          WEOF      FMSTMP02,SEQ
..
.WEOF9999  RETURN
.******************************************************************************
.* POST - Post data                              Called by ML0000             *
.******************************************************************************
POST0000  PACK      KEY14,FMLALEDG,FMAMACCT,SP70
          UNPACK    KEY14,FMAMLEDG,FMAMACCT
          MOVE      ONE,FMAMMULT
          MOVE      ONE,FMAMDIV
          MOVE      ZERO,FMAMAYTD
          MOVE      ZERO,FMAMDPLA
          UNPACK    SP70,FMAMUNIT
          MOVE      "0000000000",FMAMCLEV
.
          COMPARE   ONE,COPTION
          GOTO      POST1000 IF NOT EQUAL
          CALL      RAFMAM1                 * record exists ?
          BRANCH    OVRCD,POST1000
          DISPLAY   *P1:24,*B,*EF,"Account Code Has Just Been Entered - ";
          CALL      HITENTER
          GOTO      POST9999
.
POST1000  CALL      DEFMAM1                 * delete old record (if possible)
          CALL      WRFMAM1                 * write new record
.
          IF        COPTION=1
            MOVE      "1",WAUDTYPE
            CALL      WAUD0000              * write audit record data
          ENDIF 
.
          COMPARE   ONE,FMACW1ON
          GOTO      POST9999 IF NOT EQUAL   * notes active ?
.
          OPEN      FMSTMP01,FILENAM1
          PACK      KEY17,FMLALEDG,FMAMACCT,SP70
          PACK      KEY14,FMLALEDG,FMAMACCT,SP70
          MOVE      ZERO,F3
.
POST3000  READ      FMSTMP01,SEQ;FMUWLINE        * save wp data
          GOTO      POST4000 IF OVER
.
          ADD       ONE,F3
          PACK      KEY17,FMLALEDG,FMAMACCT,F3,SP70
          UNPACK    KEY17,FMUWLEDG,FMUWACCT,FMUWLNO
          CALL      DEFMUW1                      * delete old record
          CALL      WRFMUW1                      * write new record
          GOTO      POST3000
.                                                * delete excess records
POST4000  CALL      RSFMUW1                      * position file pointer
          CALL      RKFMUW1                      * get next record
.
          BRANCH    OVRCD,POST9999               * no more records ?
          PACK      KEY17,FMUWLEDG,FMUWACCT,FMUWLNO,SP70
          MATCH     KEY14,KEY17
          GOTO      POST9999 IF NOT EQUAL        * no more records ?
.
          CALL      DEFMUW1                      * delete old record
          GOTO      POST4000
.
POST9999  RETURN
.******************************************************************************
.* CKTS - Check whether there are any Calc Details   Called by lots           *
.*        Returns : EXIT     (0=ok, 1=calc det)                               *
.******************************************************************************
CKTS0000  PACK      KEY31,SAVLEDG,SAVACCT,SP70
          CALL      RSFMTS1                      * position file pointer
          CALL      RKFMTS1                      * get next record
          BRANCH    OVRCD,CKTS5000               * no more records ?
.
          MATCH     FMTSTLED,SAVLEDG
          GOTO      CKTS5000 IF NOT EQUAL        * no more records ?
.
          MATCH     FMTSTACC,SAVACCT
          GOTO      CKTS5000 IF NOT EQUAL        * no more records ?
.
          DISPLAY   *P1:24,*B,*EF:
                    "Calculation Details Exist for this Subjective - ";
          CALL      HITENTER
          MATCH     "*",ANS
          GOTO      CKTS5000 IF EQUAL
          GOTO      CKTS9500
.
CKTS5000  PACK      KEY31,SAVLEDG,SAVACCT,SP70
          CALL      RSFMTS2                      * position file pointer
          CALL      RKFMTS2                      * get next record
          BRANCH    OVRCD,CKTS9000               * no more records ?
.
          MATCH     FMTSTLED,SAVLEDG
          GOTO      CKTS9000 IF NOT EQUAL        * no more records ?
.
          MATCH     FMTSSACC,SAVACCT
          GOTO      CKTS9000 IF NOT EQUAL        * no more records ?
.
          DISPLAY   *P1:24,*B,*EF:
                    "Account Used In ",FMTSTLED,SLASH,FMTSTACC:
                    " Calculation Details - ";
          CALL      HITENTER
          MATCH     "*",ANS
          GOTO      CKTS9000 IF EQUAL
          GOTO      CKTS9500
.
CKTS9000  MOVE      ZERO,EXIT
          GOTO      CKTS9999
.
CKTS9500  MOVE      ONE,EXIT
.
CKTS9999  RETURN
.**********************************************************************
.  SUBA - check if sub or add to accounts exist        Called By lots
.        Requires - LEDGER,ACCOUNT     (ledger/account)
.        Returns  - EXIT               (1=quit)
.**********************************************************************
SUBA0000  PACK      KEY33,LEDGER,ACCOUNT,SP70
          CALL      RSFMTC1
          CALL      RKFMTC1
          BRANCH    OVRCD,SUBA1000     * no more records ?
          PACK      KEY14,FMTCTLED,FMTCTACC,SP70
          MATCH     KEY33,KEY14
          GOTO      SUBA1000 IF NOT EQUAL
.
          DISPLAY   *P1:24,*B,*EF,"Sub Accounts Exist For This Account - ";
          CALL      HITENTER
          GOTO      SUBA9500
.
SUBA1000  PACK      KEY33,LEDGER,ACCOUNT,SP70
          CALL      RSFMTC2
          CALL      RKFMTC2
          BRANCH    OVRCD,SUBA9000     * no more records ?
          PACK      KEY14,FMTCSLED,FMTCSACC,SP70
          MATCH     KEY33,KEY14
          GOTO      SUBA9000 IF NOT EQUAL
.
          DISPLAY   *P1:24,*B,*EF,"This Account Adds To Account ":
                    FMTCTLED,SLASH,FMTCTACC," - ";
          CALL      HITENTER
          GOTO      SUBA9500
.
SUBA9000  MOVE      ZERO,EXIT          * continue
          GOTO      SUBA9999
.
SUBA9500  MOVE      ONE,EXIT           * quit
.
SUBA9999  RETURN
.******************************************************************************
.* DELT - Delete data                            Called by ML0000             *
.******************************************************************************
DELT0000  CALL      CKTS0000             * check not in stat subj calc file
          BRANCH    EXIT,DELT9999
.
          CALL      SUBA0000             * check sub accounts
          BRANCH    EXIT,DELT9999
.
          PACK      KEY14,FMAMLEDG,FMAMACCT,SP70
          CALL      CDAT0000             * check for data
          BRANCH    EXIT,DELT9999
.
          PACK      KEY17,FMAMLEDG,FMAMACCT,SP70 * delete user def wp data
          PACK      KEY14,FMAMLEDG,FMAMACCT,SP70
.
DELT1000  CALL      RSFMUW1                      * position file pointer
          CALL      RKFMUW1                      * get next record
.
          BRANCH    OVRCD,DELT2000               * no more records ?
          PACK      KEY17,FMUWLEDG,FMUWACCT,FMUWLNO,SP70
          MATCH     KEY14,KEY17
          GOTO      DELT2000 IF NOT EQUAL        * no more records ?
.
          CALL      DEFMUW1                      * delete old record
          GOTO      DELT1000
.
DELT2000  PACK      KEY26,FMAMLEDG,FMAMACCT,SP70
.
DELT3000  CALL      RSFMRF2                      * position file pointer
          CALL      RKFMRF2                      * get next record
.
          BRANCH    OVRCD,DELT4000               * no more records ?
          PACK      KEY26,FMRFLED,FMRFACC,FMRFREP,FMRFPOS,FMRFSEQ,SP70
          MATCH     KEY14,KEY26
          GOTO      DELT4000 IF NOT EQUAL        * no more records ?
.
          CALL      DEFMRF2                      * delete old record
          GOTO      DELT3000
.
DELT4000  PACK      KEY33,FMAMLEDG,FMAMACCT,SP70
.
DELT5000  CALL      RSFMTC2                      * position file pointer
          CALL      RKFMTC2                      * get next record
.
          BRANCH    OVRCD,DELT8000               * no more records ?
          PACK      KEY33,FMTCSLED,FMTCSACC,FMTCTLED,FMTCTACC,FMTCASEQ,SP70
          MATCH     KEY14,KEY33
          GOTO      DELT8000 IF NOT EQUAL        * no more records ?
.
          CALL      DEFMTC2                      * delete old record
          GOTO      DELT5000
.
DELT8000  PACK      KEY14,FMAMLEDG,FMAMACCT,SP70
          CALL      DEFMAM1                 * delete old record (if possible)
          CALL      DEFMCA1                 * delete old control account record
.
DELT9999  RETURN
.**********************************************************************
.  CKCS - check if account cost centre/subj related    Called By lots
.         including consolidated cost centres
.        Requires - LEDGER, ACCOUNT
.        Returns  - ACCSTAT  (0=normal, 1=cc/subj in modify mode, 2=illegal acc)
.**********************************************************************
CKCS0000  PACK      KEY14,LEDGER,ACCOUNT,SP70
          CALL      RDFMSA1
          COMPARE   ONE,OVRCD                    * check for subj
          GOTO      CKCS9500 IF NOT EQUAL
.
          CALL      RDFMCC1
          COMPARE   ONE,OVRCD                    * check for cc
          GOTO      CKCS9500 IF NOT EQUAL
.
          CALL      GCOD0000                     * check for subj/cc combinat'n
          PACK      KEY26,LEDGER,SUBJCODE,COSTCODE,SP70
          CALL      RDFMXC1
          COMPARE   ONE,OVRCD
          GOTO      CKCS9500 IF NOT EQUAL
.
          PACK      KEY26,LEDGER,COSTCODE,SP70
          CALL      RDFMCF1                      * check if ccc related
          COMPARE   ONE,OVRCD
          GOTO      CKCS9700 IF NOT EQUAL
.
.
CKCS9000  MOVE      ZERO,ACCSTAT       * continue
          GOTO      CKCS9999
.
CKCS9500  MOVE      TWO,ACCSTAT        * cc/subj related
          LOAD      ACCSTAT,COPTION,ACCSTAT,ONE,ACCSTAT
          BRANCH    ACCSTAT,CKCS9999
.
          DISPLAY   *P1:23,*B,*EF:
                           "This Data May Only Be Modified Via Cost Centre ":
                    *P1:24,"Or Subjective Maintenance - ";
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
          MATCH     "*",ANS            **** back door entry to data ****
          GOTO      CKCS9000 IF EQUAL
          GOTO      CKCS9999
.
CKCS9700  MOVE      TWO,ACCSTAT        * ccc/subj related
          DISPLAY   *P1:23,*B,*EF:
                           "This Data May Only Be Modified Via Consolidated ":
                    *P1:24,"Cost Centre Maintenance - ";
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
          MATCH     "*",ANS            **** back door entry to data ****
          GOTO      CKCS9000 IF EQUAL
          GOTO      CKCS9999
.
CKCS9999  RETURN
.****************************************************************************
.  GCOD - get subjective and cost codes from account code    Called by lots
.        Requires - ACCOUNT   (account code)
.                   FMSUUD2A  (separate CC/Subj 1=yes)
.                   FMSUUD4A  (cost centre code length)
.        Returns  - SUBJCODE  (subjective code)
.                   COSTCODE  (cost centre code)
.****************************************************************************
GCOD0000  UNPACK    SP70,SUBJCODE,COSTCODE       * clear old vars
          BRANCH    FMSUUD4A,GCOD0100,GCOD0200,GCOD0300,GCOD0400:
                             GCOD0500,GCOD0600,GCOD0700,GCOD0800:
                             GCOD0900,GCOD1000,GCOD1100,GCOD1200
.
GCOD0100  BRANCH    FMSUUD2A,GCOD0110
          UNPACK    ACCOUNT,KEY1,SUBJCODE
          GOTO      GCOD90000
.
GCOD0110  UNPACK    ACCOUNT,KEY1,ANS,SUBJCODE
          GOTO      GCOD90000
.
GCOD0200  BRANCH    FMSUUD2A,GCOD0210
          UNPACK    ACCOUNT,KEY2,SUBJCODE
          GOTO      GCOD90000
.
GCOD0210  UNPACK    ACCOUNT,KEY2,ANS,SUBJCODE
          GOTO      GCOD90000
.
GCOD0300  BRANCH    FMSUUD2A,GCOD0310
          UNPACK    ACCOUNT,KEY3,SUBJCODE
          GOTO      GCOD90000
.
GCOD0310  UNPACK    ACCOUNT,KEY3,ANS,SUBJCODE
          GOTO      GCOD90000
.
GCOD0400  BRANCH    FMSUUD2A,GCOD0410
          UNPACK    ACCOUNT,KEY4,SUBJCODE
          GOTO      GCOD90000
.
GCOD0410  UNPACK    ACCOUNT,KEY4,ANS,SUBJCODE
          GOTO      GCOD90000
.
GCOD0500  BRANCH    FMSUUD2A,GCOD0510
          UNPACK    ACCOUNT,KEY5,SUBJCODE
          GOTO      GCOD90000
.
GCOD0510  UNPACK    ACCOUNT,KEY5,ANS,SUBJCODE
          GOTO      GCOD90000
.
GCOD0600  BRANCH    FMSUUD2A,GCOD0610
          UNPACK    ACCOUNT,KEY6,SUBJCODE
          GOTO      GCOD90000
.
GCOD0610  UNPACK    ACCOUNT,KEY6,ANS,SUBJCODE
          GOTO      GCOD90000
.
GCOD0700  BRANCH    FMSUUD2A,GCOD0710
          UNPACK    ACCOUNT,KEY7,SUBJCODE
          GOTO      GCOD90000
.
GCOD0710  UNPACK    ACCOUNT,KEY7,ANS,SUBJCODE
          GOTO      GCOD90000
.
GCOD0800  BRANCH    FMSUUD2A,GCOD0810
          UNPACK    ACCOUNT,KEY8,SUBJCODE
          GOTO      GCOD90000
.
GCOD0810  UNPACK    ACCOUNT,KEY8,ANS,SUBJCODE
          GOTO      GCOD90000
.
GCOD0900  BRANCH    FMSUUD2A,GCOD0910
          UNPACK    ACCOUNT,KEY9,SUBJCODE
          GOTO      GCOD90000
.
GCOD0910  UNPACK    ACCOUNT,KEY9,ANS,SUBJCODE
          GOTO      GCOD90000
.
GCOD1000  BRANCH    FMSUUD2A,GCOD1010
          UNPACK    ACCOUNT,KEY10,SUBJCODE
          GOTO      GCOD90000
.
GCOD1010  UNPACK    ACCOUNT,KEY10,ANS,SUBJCODE
          GOTO      GCOD90000
.
GCOD1100  BRANCH    FMSUUD2A,GCOD1110
          UNPACK    ACCOUNT,KEY11,SUBJCODE
          GOTO      GCOD90000
.
GCOD1110  UNPACK    ACCOUNT,KEY11,ANS,SUBJCODE
          GOTO      GCOD90000
.
GCOD1200  BRANCH    FMSUUD2A,GCOD1210
          UNPACK    ACCOUNT,KEY12,SUBJCODE
          GOTO      GCOD90000
.
GCOD1210  UNPACK    ACCOUNT,KEY12,ANS,SUBJCODE
          GOTO      GCOD90000
.
GCOD9000  LOAD      COSTCODE,FMSUUD4A,KEY1,KEY2,KEY3,KEY4:
                                      KEY5,KEY6,KEY7,KEY8:
                                      KEY9,KEY10,KEY11,KEY12
          PACK      SUBJCODE,SUBJCODE,SP70
          PACK      COSTCODE,COSTCODE,SP70
.
GCOD9999  RETURN
.**********************************************************************
.  CDAT - Check for data on file                       Called By lots
.        Requires - KEY14    (ledger/account)
.                   NORMFIL1 & 2, STATFILE (filenames - blank if closed)
.        Returns  - EXIT     (1=quit)
.**********************************************************************
CDAT0000  CALL      RDFMAM1
          BRANCH    OVRCD,CDAT9000     * account exists ?
.
          MATCH     "6",FMAMTYPE       * only check for posting accounts
          GOTO      CDAT1000 IF LESS
          MATCH     "8",FMAMTYPE
          GOTO      CDAT2000 IF EQUAL
          GOTO      CDAT9000
.
.---- check for normal data ----
.
CDAT1000  
          MOVE      FMAMLEDG,KEY2
          CALL      RDFMLA1
.
. Current Year
.
          PACK      FILENAME,FMST,FMLACYRR,SP70
          MATCH     FILENAME,NORMFIL1
          GOTO      CDAT1100 IF EQUAL  * file open ?
.
          MOVE      SP70,NORMFIL1
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FMSTRNA2,FILENAME
          TRAPCLR   IO
          BRANCH    OVRCD,CDAT1200     * file missing ?
          MOVE      FILENAME,NORMFIL1
.
CDAT1100  PACK      KEY32,KEY14,SP70
          CALL      RSFMTR2
          CALL      RKFMTR2
          BRANCH    OVRCD,CDAT1200
          PACK      KEY15,FMTRLEDG,FMTRACCT,SP70
          MATCH     KEY15,KEY14
          GOTO      CDAT1200 IF NOT EQUAL
          GOTO      CDAT9500           * data exists
.
. Last Year
.
CDAT1200  MOVE      FMLACYRR,F4YEAR
          SUB       ONE,F4YEAR
          PACK      FILENAME,FMST,F4YEAR,SP70
          REP       " 0",FILENAME
          MATCH     FILENAME,NORMFIL2
          GOTO      CDAT1300 IF EQUAL  * file open ?
.
          MOVE      SP70,NORMFIL2
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FMSCTRA2,FILENAME
          TRAPCLR   IO
          BRANCH    OVRCD,CDAT9000     * file missing ?
          MOVE      FILENAME,NORMFIL2
.
CDAT1300  PACK      KEY32,KEY14,SP70
          CALL      RSFMCT2
          CALL      RKFMCT2
          BRANCH    OVRCD,CDAT9000
          PACK      KEY15,FMCTLEDG,FMCTACCT,SP70
          MATCH     KEY15,KEY14
          GOTO      CDAT9000 IF NOT EQUAL
          GOTO      CDAT9500           * data exists
.
.---- check for stat data ----
.
CDAT2000  
          MOVE      FMAMLEDG,KEY2
          CALL      RDFMLA1
.
          PACK      FILENAME,FMSA,FMLACYRR,SP70
          MATCH     FILENAME,STATFILE
          GOTO      CDAT2100 IF EQUAL  * file open ?
.
          MOVE      SP70,STATFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FMSSTRA2,FILENAME
          TRAPCLR   IO
          BRANCH    OVRCD,CDAT9000     * file missing ?
          MOVE      FILENAME,STATFILE
.
CDAT2100  PACK      KEY30,KEY14,SP70
          CALL      RSFMSR2
          CALL      RKFMSR2
          BRANCH    OVRCD,CDAT9000
          PACK      KEY15,FMSRLEDG,FMSRACCT,SP70
          MATCH     KEY15,KEY14
          GOTO      CDAT9000 IF NOT EQUAL
          GOTO      CDAT9500           * data exists
.
.
CDAT9000  MOVE      ZERO,EXIT          * continue
          GOTO      CDAT9999
.
CDAT9500  MOVE      ONE,EXIT           * quit
          DISPLAY   *P1:23,*B,*EF:
                    "Data Found In Transaction File ",*V2LON,FILENAME,*HOFF:
                    *P1:24:
                    "For Account ",*V2LON,FMAMLEDG,SLASH,FMAMACCT,*HOFF," - ";
          CALL      HITENTER
          DISPLAY   *P1:23,*EF;
          MATCH     "1426",PASSCODE
          GOTO      CDAT9999 IF NOT EQUAL
          MATCH     "*",ANS
          GOTO      CDAT9000 IF EQUAL
.
CDAT9999  RETURN
.*****************************************************************************
. WAUD - Write Audit Record                           Called by ML0000
.    Requires - WAUDTYPE (1=write, 2=before change, 3=after change, 4=delete)
.*****************************************************************************
WAUD0000  COMPARE   ONE,FMSAUDA
          GOTO      WAUD9999 IF NOT EQUAL
          PACK      FMAMAUDD,CCC,CYY,CMM,CDD
          REP       " 0",FMAMAUDD
          CLOCK     TIME,FMAMAUDT
          MOVE      PORT,FMAMAUDP
          LOAD      FMAMAUDR,WAUDTYPE,ANSA,ANSB,ANSC,ANSD
          MOVE      ONE,FMAMAUDS
          MOVE      PASSCODE,FMAMAUDO
          PACK      KEY19,FMAMAUDD,FMAMAUDT,FMAMAUDP,FMAMAUDR
          MOVE      KEY19,SAVKEY19
          CALL      AWFMAM                       * write audit file record
WAUD9999  RETURN
.*****************************************************************************
. DAUD - Delete Before Change Audit                    Called by ML0000
.*****************************************************************************
DAUD0000  COMPARE   ONE,FMSAUDA
          GOTO      DAUD9999 IF NOT EQUAL
          MOVE      SAVKEY19,KEY19
          CALL      ADFMAM                       * write audit file record
DAUD9999  RETURN
.******************************************************************************
.* INCLUDES FOR I/O'S                                                         *
.******************************************************************************
RLMAFM00  COMPARE   "2",SCRNO
          GOTO      RDISB000 IF EQUAL
          DISPLAY   *P1:4,"    Ledger               : ",*EF;
          RETURN
.
RAMAFM00  COMPARE   "2",SCRNO
          GOTO      RDISB000 IF EQUAL
          DISPLAY   *P1:4,"    Ledger               : ":
                    *V2LON,FMLALEDG,*HOFF,SP12,FMLADESC,*EL:
                    *P1:5,"    Posting Account      : ",*EF;
          RETURN
.
RDISB000                     * note : screen B does not exist
          RETURN
.
RCATFM00  
RCODFM00  
RONCFM00  
RSECFM00  
          BRANCH    SCRNO,RCODFM10,RCODFM20
.
RCODFM10  
          CALL      SCRA0000
          CALL      DISA0000
          GOTO      RCODFM99
.
RCODFM20                     * note : screen B does not exist
          CALL      SCRB0000
          CALL      DISB0000
          GOTO      RCODFM99
.
RCODFM99  RETURN
.
          INC       FMSAMAIO/INC       * account master file
          INC       FMSCAFIO/INC       * control account file
          INC       FMSCCAIO/INC       * cost centre file
          INC       FMSCCCIO/INC       * consolidated cost centre file
          INC       FMSCODIO/INC       * codes file
          INC       FMSCONIO/INC       * control file
          INC       FMSCTRIO/INC       * normal tran file
          INC       FMSLMAIO/INC       * ledger
          INC       FMSLMBIO/INC       * ledger report desc.
          INC       FMSONCIO/INC       * oncost
          INC       FMSRSFIO/INC       * report seq file
          INC       FMSSBAIO/INC       * subjective file
          INC       FMSSSCIO/INC
          INC       FMSSTRIO/INC       * stat tran file
          INC       FMSTCFIO/INC       * total calc file
          INC       FMSTRNIO/INC       * normal tran file
          INC       FMSUWPIO/INC       * user defined wp. file
          INC       FMSXCSIO/INC       * xref file
.
          INC       FMSAMAKY           * account master file
          INC       FMSCODKY           * codes file
          INC       FMSLMAKY           * ledger
          INC       FMSONCKY           * oncost
          INC       FMSSTDCD           * FMS Acc. standard routines
.
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
          INC       TFILENAM
