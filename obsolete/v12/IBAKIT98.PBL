.******************************************************************************
.* System   : Kits recording system                                           *
.* Program  : IBAKIT98                                                        *
.* Desc.    : System Parameter Maintenance                                    *
.******************************************************************************
.* Date     : 19/09/90                                                        *
.* Author   : Paul Duncan                                                     *
.* Mods.    : V5.01.02  26/02/91  ROD                                         *
.*                      Fixed IBA screen to read correct sector               *
.*        V5.03.01  04/01/1996  Greg Horvat                                   *
.*                  Removed the menu program, security key, IBA screen        *
.*                  password & data directory keyins & changed to use a       *
.*                  standard keyin for the IBA screen security password       *
.*                                                                            *
.******************************************************************************
.
.$$$$$
.
.  System Parameter Maintenance (IBAOPR98)
.  ---------------------------------------
.
.  - Display Header
.
.  - Open Files
.         CONTROLF
.         PATCODE1
.
.   -Processing Options
.
.       - 0 = Exit
.       - 1 = System Audits
.    
.           - Display audits screen
.           - Process required fields
.
.       - 2 = user controlled
.
.           - Display user controlled screen 
.           - Read CONTROLF sector 35
.           - Process required fields
.           - Update CONTROLF sector 35
.
.       - 9 = I.B.A. Controlled Screen 1
.
.           - Enter Password
.           - Read CONTROLF sector 35, position 1
.           - Display I.B.A. screen 
.           - Process required fields
.           - Update CONTROLF sector 35, position 1
.
.   -END
.
.$$$$$
          INC       STD001FD
          INC       IBACONFD/INC
          INC       KITCONFD/INC
          INC       PATCODFD/INC
+
.**********************************************************************
.*                       VARIABLES                                    *
.**********************************************************************
.
ARTIME    DIM       5               * summary breakdown time
.
COL       FORM      2
CDESC     DIM       10              * using icd9-cm or mbs description
CDESC0    INIT      "ICD9-CM   "
CDESC1    INIT      "MBS       "
CLSDESC   DIM       10              * using clinic or surgeon
CLSDESC0  INIT      "Clinic    "
CLSDESC1  INIT      "Surgeon   "
.
CODEOA    INIT      "OA"            * operating anaesthetic code
CODEOP    INIT      "OP"            * operating anaesthetic code
CODEOY    INIT      "OY"            * emergency operation type
.
CODEQ1    INIT      "Q1"            * nurse user define fields
CODEQ2    INIT      "Q2"
CODEQ3    INIT      "Q3"
CODEQ4    INIT      "Q4"
CODEQ5    INIT      "Q5"
CODEQ6    INIT      "Q6"            * operating team user define fields
CODEQ7    INIT      "Q7"
CODEQ8    INIT      "Q8"
CODEQ9    INIT      "Q9"
.
CODEY1    INIT      "Y1"            * operating room user define fields
CODEY2    INIT      "Y2"
CODEY3    INIT      "Y3"
CODEY4    INIT      "Y4"
CODEY5    INIT      "Y5"
CODEY6    INIT      "Y6"            * operating detail user define fields
CODEY7    INIT      "Y7"
CODEY8    INIT      "Y8"
CODEY9    INIT      "Y9"
.
CODEYA    INIT      "YA"            * operating assistant code
CODEYS    INIT      "YS"            * operating surgeon code
.
CODEZ1    INIT      "Z1"            * operating usage user define fields
CODEZ2    INIT      "Z2"
CODEZ3    INIT      "Z3"
CODEZ4    INIT      "Z4"
CODEZ5    INIT      "Z5"
CODEZ6    INIT      "Z6"
CODEZ7    INIT      "Z7"
CODEZ8    INIT      "Z8"
.
DDESC1    DIM       20              * operating detail user define description
DDESC2    DIM       20
DDESC3    DIM       20
DDESC4    DIM       20
.
DATIME    DIM       5               * summary breakdown time
DANSX     DIM       3
.
DANS1     DIM       3               * nurse user define fields
DANS2     DIM       3
.
DIM6      DIM       6
DIM8      DIM       8
DIM20     DIM       20
.
EOTDESC   DIM       20              * emergency operating type 
.
FORM8     FORM      8
FIELD     FORM      2
.
.
NDESC1    DIM       20              * nursing user define description
NDESC2    DIM       20
NDESC3    DIM       20
NDESC4    DIM       20
NDESC5    DIM       20
NUM42     FORM      "42"
.
OSCDESC   DIM       20              * operating surgeon code
OASDESC   DIM       20              * operating assistant code
OANDESC   DIM       20              * operating anaesthetic code
OPSDESC   DIM       20              * Operating surgeon code
.
PASS      DIM       8               * password
.
RDESC1    DIM       20              * operating room user define description
RDESC2    DIM       20
RDESC3    DIM       20
RDESC4    DIM       20
RDESC5    DIM       20
REPFLAG   FORM      1
.
.
TIME      DIM       5               * summary breakdown time
TDESC1    DIM       20              * operating team user define description
TDESC2    DIM       20
TDESC3    DIM       20
TDESC4    DIM       20
.
UDESC1    DIM       20              * operating usage user define description
UDESC2    DIM       20
UDESC3    DIM       20
UDESC4    DIM       20
UDESC5    DIM       20              
UDESC6    DIM       20
UDESC7    DIM       20
UDESC8    DIM       20
.
VERT      FORM      2
.
PRGID     INIT      "IBAKIT98"
PRGNAM    INIT      "System Parameter Maintenance"
VERSION   INIT      "V12.00.00"
+
.**********************************************************************
.*                          MAINLINE                                  *
.**********************************************************************
.
ML0000    CALL      INIT0000           * initialisation
.
ML1000    CALL      SCR0000            * select option  
          BRANCH    EXIT,ML9000
.
          CALL      DOPT0000           * display option selected
.
          BRANCH    OPTION,ML1500,ML2000,ML2500
.
.         Audit Screen
.
ML1500    CALL      DAD0000            * display audits
          CALL      KAD0000            * keyin audits
          BRANCH    EXIT,ML1000
          CALL      WAD0000            * write audits 
          GOTO      ML1000
.
.         Hospital controlled
.
ML2000    CALL      DHC0000            * display Hospital controlled
          CALL      UHCS0000            * update Hospital controlled
          BRANCH    EXIT,ML1000
          CALL      WHCS0000            * write Hospital controlled
          GOTO      ML1000
.
.         I.B.A. Screen                
.
ML2500    CALL      IBASECUR                * Keyin IBA screen security passwd
          BRANCH    EXIT,ML1000
.
          CALL      IBA0000            * display i.b.a. screen
          CALL      UIBA0000           * update i.b.a. screen
          BRANCH    EXIT,ML1000
          CALL      WIBA0000           * write i.b.a. screen
          GOTO      ML1000
.
.----------------------------------------------------------------------
.                The End...
.----------------------------------------------------------------------
ML9000    CHAIN     PGM
+
.**********************************************************************
.*                        INIT0000                                    *
.*                       Initialisation                               *
.**********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          RETURN
+
.**********************************************************************
.*                        SCR0000                                     *
.*                       Select Option                                *
.**********************************************************************
.
SCR0000   MOVE      FALSE,EXIT
          MOVE      ZERO,REPFLAG
          HLINE     *G37,2,54,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Audits":
                    *P1:6,*V2LON,TWO,*HOFF," = Hospital Controlled Screen 1":
                    *P1:7,*V2LON,THREE,*HOFF:
                          " = IBA Controlled Screen 1"
.
SCR1000   DISPLAY   *P1:9,*EL,"Select Option : "
.
          KEYIN     *P17:9,*V2LON,OPTION
.
          BRANCH    OPTION,SCR9999,SCR9999,SCR9999
.
          COMPARE   ZERO,OPTION
          GOTO      SCR9000 IF EQUAL
          BEEP
          GOTO      SCR1000
.
SCR9000   MOVE      TRUE,EXIT
SCR9999   RETURN
+
.**********************************************************************
.*                        DOPT0000                                    *
.*                   Display Option Selected                          *
.**********************************************************************
.
DOPT0000  BRANCH    OPTION,DOPT1000,DOPT2000,DOPT3000
.
DOPT1000  DISPLAY   *P54:2,*V2LON,"- Audits "
          GOTO      DOPT9999
.
DOPT2000  DISPLAY   *P54:2,*V2LON,"- Hospital 1 "
          GOTO      DOPT9999
.
DOPT3000  DISPLAY   *P54:2,*V2LON,"- IBA Screen 1 "
          GOTO      DOPT9999
.
DOPT9999  RETURN
+
.**********************************************************************
.*                           DAD0000                                  *
.*                       Display audits                               *
.**********************************************************************
.
DAD0000   READ      CONTROLF,FORTY;*43,KTCNAUDA,KTCNAUDB
.
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 1.",*HOFF," Kits             (kitmasaf) :":
                    *P1:5,*V2LON," 2.",*HOFF," Kits Recording   (kitrecof) :"
.
          MOVE      ZERO,FORM2
          MOVE      THREE,CVERT
          MOVE      "35",CCOL
.
DAD1000   ADD       ONE,FORM2
          ADD       ONE,CVERT
.
          COMPARE   THREE,FORM2
          RETURN    IF EQUAL
.
DAD2000   LOAD      FIELD USING FORM2 OF KTCNAUDA,KTCNAUDB
.
          MOVE      DYES,DANSX
          LOAD      DANSX USING FIELD OF DNO
          DISPLAY   *PCCOL:CVERT,*V2LON,DANSX;
          GOTO      DAD1000
+
.**********************************************************************
.*                            KAD0000                                 *
.*                         Keyin Audits                               *
.**********************************************************************
.
KAD0000   MOVE      FALSE,EXIT
          CALL      MAINQST            
          COMPARE   ZERO,CCITEM
          GOTO      KAD4000 IF EQUAL
          COMPARE   "-1",CCITEM
          GOTO      KAD9000 IF EQUAL
          COMPARE   "3",CCITEM
          GOTO      KAD0000 IF NOT LESS
.
          MOVE      THREE,CVERT
          MOVE      "35",CCOL
          ADD       CCITEM,CVERT
.
KAD1000   LOAD      FORM1 USING CCITEM OF KTCNAUDA,KTCNAUDB
          DISPLAY   *PCCOL:CVERT;
.
          ADD       ONE,FORM1          * set up toggle switch for audits
          LOAD      FORM1,FORM1,ONE,ZERO
          BRANCH    FORM1,KAD2000
          DISPLAY   *V2LON,DYES
          GOTO      KAD3000
KAD2000   DISPLAY   *V2LON,DNO
.
KAD3000   STORE     FORM1 USING CCITEM OF KTCNAUDA,KTCNAUDB
          GOTO      KAD0000 
.
KAD4000   MOVE      FALSE,EXIT 
          GOTO      KAD9999
KAD9000   MOVE      TRUE,EXIT
KAD9999   RETURN
+
.**********************************************************************
.*                        WAD0000                                     *
.*                       Write Audits                                 *
.**********************************************************************
.
WAD0000   WRITAB    CONTROLF,FORTY;*43,KTCNAUDA,KTCNAUDB,KTCNAUDC
          DISPLAY   *P60:24,*EL,*V2LON,"*** Posted *** ",*W2;
          RETURN
+
.**********************************************************************
.*                            MISC0000                                *
.*                  Display Miscellaneous Screen                      *
.**********************************************************************
.
DHC0000   READ      CONTROLF,THIRTY5;*71,KTCNCPKS,KTCNCPCM
.
          MOVE      DNO,DANS1
          LOAD      DANS1,KTCNCPKS,DYES,DNO
.
          MOVE      DNO,DANS2
          LOAD      DANS2,KTCNCPCM,DYES,DNO
.
DHC1000   DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 1.",*HOFF:
                                 " Copy Arrival Status def. to Return Status":
                                 " (Y/N) :":
                    *P1:5,*V2LON," 2.",*HOFF:
                                 " Copy Arrival Comnts def. to Return Comnts":
                                 " (Y/N) :":
                    *P55:4,*V2LON,DANS1:
                    *P55:5,DANS2
.
DHC9999   RETURN
.**********************************************************************
.*                            UHCS0000                                *
.*                  Update Miscellaneous Screen                       *
.**********************************************************************
.
UHCS0000  MOVE      FALSE,EXIT
.
          CALL      MAINQST            * post cancel or select item
          COMPARE   ZERO,CCITEM
          GOTO      UHCS9999 IF EQUAL
          COMPARE   "-1",CCITEM
          GOTO      UHCS9000 IF EQUAL
.
          MOVE      CCITEM,FIELD
          BRANCH    FIELD,UHCS1500,UHCS2000
          BEEP
          GOTO      UHCS0000
.
.         Copy arival staus to return status
.
UHCS1500  KEYIN     *P55:4,*EL,*DV,UNDLN1,*P55:4,*V2LON,ANS
          CMATCH    ANSY,ANS
          GOTO      UHCS1520 IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      UHCS1550 IF EQUAL
          BEEP      
          GOTO      UHCS1500
.
UHCS1520  MOVE      ONE,KTCNCPKS
          GOTO      UHCS1570
.
UHCS1550  MOVE      ZERO,KTCNCPKS
.
UHCS1570  MOVE      DNO,DANS1
          LOAD      DANS1,KTCNCPKS,DYES,DNO
          DISPLAY   *P55:4,*V2LON,DANS1
          MOVE      ZERO,OVRCD
          GOTO      UHCS0000
.
.         Copy Arrival comments to Return comments
.
UHCS2000  KEYIN     *P55:5,*EL,*DV,UNDLN1,*P55:5,*V2LON,ANS
          CMATCH    ANSY,ANS
          GOTO      UHCS2020 IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      UHCS2050 IF EQUAL
          BEEP      
          GOTO      UHCS2000
.
UHCS2020  MOVE      ONE,KTCNCPCM
          GOTO      UHCS2070
.
UHCS2050  MOVE      ZERO,KTCNCPCM
.
UHCS2070  MOVE      DNO,DANS2
          LOAD      DANS2,KTCNCPCM,DYES,DNO
          DISPLAY   *P55:5,*V2LON,DANS2
          MOVE      ZERO,OVRCD
          GOTO      UHCS0000
.
UHCS9000  MOVE      TRUE,EXIT
UHCS9999  RETURN
+
.**********************************************************************
.*                            WHCS0000                                *
.*                  Write Miscellaneous Screen                        *
.**********************************************************************
.
WHCS0000  WRITAB    CONTROLF,THIRTY5;*71,KTCNCPKS,KTCNCPCM
          DISPLAY   *P60:24,*EL,*V2LON,"*** Posted *** ",*W2;
          RETURN
+
.**********************************************************************
.*                            IBA0000                                 *
.*                     Display I.B.A. Screen                          *
.**********************************************************************
.
IBA0000   DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 1.",*HOFF:
                                 " (No longer used)                        :":
                    *P1:5,*V2LON," 2.",*HOFF:
                                 " (No longer used)                        :":
                    *P1:6,*V2LON," 3.",*HOFF:
                                 " (No longer used)                        :":
                    *P1:7,*V2LON," 4.",*HOFF:
                                 " (No longer used)                        :"
          RETURN
+
.**********************************************************************
.*                            UIBA0000                                *
.*                      Update I.B.A. Screen                          *
.**********************************************************************
.
UIBA0000  MOVE      FALSE,EXIT
.
          CALL      MAINQST            * post cancel or select item
          COMPARE   ZERO,CCITEM
          GOTO      UIBA9999 IF EQUAL
          COMPARE   "-1",CCITEM
          GOTO      UIBA9000 IF EQUAL
          COMPARE   "12",CCITEM
          GOTO      UIBA0000 IF NOT LESS
.
          MOVE      CCITEM,FIELD
          BRANCH    FIELD,UIBA1100,UIBA1200,UIBA1300,UIBA1400
.
          BEEP
          GOTO      UIBA0000
.
UIBA1100  MOVE      ZERO,OVRCD
          GOTO      UIBA0000
.
UIBA1200  MOVE      ZERO,OVRCD
          GOTO      UIBA0000
.
UIBA1300  MOVE      ZERO,OVRCD
          GOTO      UIBA0000
.
UIBA1400  MOVE      ZERO,OVRCD
          GOTO      UIBA0000
.
UIBA9000  MOVE      TRUE,EXIT
UIBA9999  RETURN
+
.**********************************************************************
.*                            WIBA0000                                *
.*                      Write I.B.A. Screen                           *
.**********************************************************************
.
WIBA0000  DISPLAY   *P60:24,*EL,*V2LON,"*** Posted *** ",*W2;
          RETURN
+
.**********************************************************************
.*                   I/O ROUTINES                                     *
.**********************************************************************
.
          INCLUDE   STD001IO
          INC       IBASECUR                * Keyin IBA screen security passwd
          INC       PATCODIO/INC
