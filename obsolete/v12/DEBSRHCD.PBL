.---------------------------------------------------------------------------
.   System    :   Accounts Receiveable               
.   Include   :   DEBDBTKY
.   Routine   :   KDBDBS00 Keyin Debtor              
.   Parameter :   CKEYTYP 0 = Keyin , 1 = Accept, 2 = Either
.   Return    :   EXIT = 1 null entry, 2 EXITCHAR entered
.-------------------------------------------------------------------------------
.   Mods      :   26/03/2003  Sandra Barcham    37464
.                 Fixed for below change which was done incorrectly
.             :   17.Oct.2002  Sunil Kumar  srf 16477
.                 Fixed for Terms and Warning Description in Debtors Enq.
.-------------------------------------------------------------------------------
KDBDBS00  TOPIC     "DBDBDEB "
          MOVE      UNDLN70,DBDBDEB 
          KEYIN     *PCCOL:CVERT,*DV,DBDBDEB:                                   
                    *PCCOL:CVERT,*V2LON,DBDBDEB:                                
                    *PCCOL:CVERT,*DV,DBDBDEB 
.
          ENDSET    DBDBDEB 
          APPEND    SP70,DBDBDEB 
          RESET     DBDBDEB 
.
          MATCH     SP70,DBDBDEB 
          GOTO      KDBDBS90 IF EQUAL
.
          CMATCH    EXITCHAR,DBDBDEB 
          GOTO      KDBDBS95 IF EQUAL
.
          CMATCH    QUEST,DBDBDEB 
          GOTO      KDBDBS10 IF NOT EQUAL
.
          MOVE      CCITEM,SCCITEM
          READ      CONTROLF,EIGHTY7;CFDBLOCS,CFDBSTDS,CFDBIDDS,CFDBSOPT
          MATCH     "1",CFDBSOPT
          IF        @EQUAL
            CALL      HDBFHC00
            MOVE      DBFHDEB,DBDBDEB
          ELSE
            CALL      HDBDBA00
          ENDIF
          MOVE      SCCITEM,CCITEM
.
          ENDSET    DBDBDEB 
          APPEND    SP70,DBDBDEB 
          RESET     DBDBDEB 
.
          MATCH     SP70,DBDBDEB 
          GOTO      KDBDBS00 IF EQUAL
.
          DISPLAY   *PCCOL:CVERT,*V2LON,DBDBDEB                                 
KDBDBS10
          PACK      KEY8,DBDBDEB                                                
          CALL      RADBDB1                 * Does Debtor Exist?
          BRANCH    OVRCD,KDBDBS80
          BRANCH    CKEYTYP,KDBDBS75        * Is the Debtor being input (=1) ?
          CALL      RDDBDB1                 * No - Read Debtor Master Details
.
          MATCH     SP3,DBDBWRN
          IF        !@EQUAL
            CLEAR     DBCDDES
            APPEND    "Invalid Warning Code - ",DBCDDES
            APPEND    DBDBWRN,DBCDDES
            APPEND    "         ",DBCDDES
            RESET     DBCDDES
            MOVE      "DBWR",DBCDCAT
            PACK      KEY7,DBCDCAT,DBDBWRN
            CALL      RDDBCD1
            PACK      DISPMSG,DBCDDES,SP70
            PACK      DBDBWRND,DBCDDES,SP70 * SRF 16477 (Warning Desc.)
            CALL      MESSAGE1
          ENDIF
.
          MOVE      SP70,DBTEDES            * clear Terms description  *I-f1101
          PACK      KEY3,DBDBTER,SP70
          CALL      RDDBTE1                 * Read A/R Terms Code table
.                                           * SRF 16477 End
KDBDBS70  MOVE      ZERO,OVRCD              * reset OVRCD              *I-f1101
          MOVE      ZERO,EXIT
          GOTO      KDBDBS99
.
KDBDBS75  MOVE      "Debtor Already Exists - ",DISPMSG
          CALL      MESSAGE1
          GOTO      KDBDBS00
.
KDBDBS80  MOVE      ZERO,EXIT
          BRANCH    CKEYTYP,KDBDBS99,KDBDBS99
          MOVE      "Debtor Does Not Exist - ",DISPMSG
          CALL      MESSAGE1
          GOTO      KDBDBS00
.
KDBDBS90  MOVE      ONE,EXIT
          GOTO      KDBDBS99
.
KDBDBS95  MOVE      TWO,EXIT
.
KDBDBS99  TOPIC    PRGID
          RETURN
.---------------------------------------------------------------------------
.   System    :   Accounts Receiveable               
.   Include   :   DEBSRHKY
.   Routine   :   HDBFHC00 Invoice Search
.-------------------------------------------------------------------------------
HDBFHC00  MOVE      ZERO,CSCRSIZ
          MOVE      "10",HTOP
          MOVE      "24",HBOT
          MOVE      "01",HLEF
          MOVE      "80",HRIG
HDBFHC04  CALL      GSCRN000
          BRANCH    EXIT,HDBFHC99
          MOVE      STDCOL00,STDCOL01
          ADD       " 5",STDCOL01
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON,"Invoice No  "
.
          MOVE      STDCOL01,STDCOL02
          ADD       "13",STDCOL02
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"  Date  "               
.
          MOVE      STDCOL02,STDCOL03
          ADD       "11",STDCOL03
          DISPLAY   *PSTDCOL03:STDROW,*V2LON,*ULON,"Debtor             "
.
          MOVE      STDCOL03,STDCOL04
          ADD       "20",STDCOL04
          DISPLAY   *PSTDCOL04:STDROW,*V2LON,*ULON,"Reference          "
.
          MOVE      STDCOL04,STDCOL05
          ADD       "16",STDCOL05
          DISPLAY   *PSTDCOL05:STDROW,*V2LON,*ULON,"      Amount"          
.
          MOVE      STDCOL05,STDCOL06
          ADD       "14",STDCOL06
.
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
          GOTO      HDBFHC50
.
HDBFHC08  MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          CALL      RSDBFH00
.
HDBFHC10  CALL      RKDBFH00
          BRANCH    OVRCD,HDBFHC15
          BRANCH    CSEARCH,HDBFHC12
          CALL      RDBFHC00          * Check Search Ranges
          BRANCH    OVRCD,HDBFHC10
HDBFHC12
          ADD       ONE,XITEMNO
          ADD       ONE,STDROW
          PACK      KEY21,DBFHDEB,DBFHDTY,DBFHDOC
          PACK      KEY22,DBFHDTY,DBFHDOC,DBFHDEB
          PACK      KEY46,DBFHDTY,DBFHUR1,DBFHDOC,DBFHDEB
          LOAD      KEY46,CKEYTYPE,KEY21,KEY22,KEY46
          STORE     KEY46,XITEMNO,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    DBFHDDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      DBFHDEB,KEY8
          CALL      RDDBDB1
.
          ASSIGN    DBFHTOT+DBFHTAX,F12P2
          MOVE      "9,999,999.99",KEY12
          FEDIT     F12P2,KEY12
          MOVE      DBDBNA1,DEBNAM
          MOVE      DBFHUR1,DEBREF
.
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,XITEMNO,DOT
          DISPLAY   *PSTDCOL01:STDROW,DBFHDOC 
          DISPLAY   *PSTDCOL02:STDROW,CPCDATE 
          DISPLAY   *PSTDCOL03:STDROW,DEBNAM 
          DISPLAY   *PSTDCOL04:STDROW,DEBREF 
          DISPLAY   *PSTDCOL05:STDROW,KEY12   
          COMPARE   MAXITEM,XITEMNO
          GOTO      HDBFHC10 IF NOT EQUAL
.
HDBFHC15  IF        CKEYTYPE=3
            UNPACK    SAVKEY80,DBFHDTY,DBFHUR1,DBFHDOC,DBFHDEB
          ELSE
            IF        CKEYTYPE=2
              UNPACK    SAVKEY80,DBFHDTY,DBFHDOC,DBFHDEB
            ELSE
              UNPACK    SAVKEY80,DBFHDEB,DBFHDTY,DBFHDOC
            ENDIF
          ENDIF
          MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
HDBFHC20  CALL      HSCRI000
          BRANCH    EXIT,HDBFHC30,HDBFHC35,HDBFHC40,HDBFHC45,HDBFHC04:
                              HDBFHC91,HDBFHC50
          GOTO      HDBFHC92
.
HDBFHC30  CALL      RKDBFH00
          BRANCH    OVRCD,HDBFHC85
          CALL      RPDBFH00
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HDBFHC10
.
HDBFHC35  CALL      RDDBFH00
          CALL      RPDBFH00
          BRANCH    OVRCD,HDBFHC85
          MATCH     SP70,DBFHDOC                                                
          GOTO      HDBFHC85 IF EQUAL                                           
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
HDBFHC36
          CALL      RPDBFH00
          BRANCH    OVRCD,HDBFHC37
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HDBFHC36 IF NOT EQUAL
HDBFHC37
          MOVE      ZERO,XITEMNO
          GOTO      HDBFHC10
.
HDBFHC40  CALL      RDDBFH00
          CALL      RPDBFH00
          BRANCH    OVRCD,HDBFHC85
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HDBFHC08
.
HDBFHC45  CALL      RKDBFH00
          BRANCH    OVRCD,HDBFHC85
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
          CALL      RLDBFH00    * Position at Last
.
HDBFHC46  CALL      RPDBFH00
          BRANCH    OVRCD,HDBFHC47
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HDBFHC46 IF NOT EQUAL
HDBFHC47
          MOVE      ZERO,XITEMNO
          GOTO      HDBFHC10
.
HDBFHC50  CALL      SDBFHC00           * Enter Search Details
.
.
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HDBFHC08
.
HDBFHC85  CALL      LKDBFH00
          BRANCH    CKEYTYPE,HDBFHC86,HDBFHC87,HDBFHC88
HDBFHC86  CALL      RDDBFH1 
          GOTO      HDBFHC90
HDBFHC87  CALL      RDDBFH6 
          GOTO      HDBFHC90
HDBFHC88  CALL      RDDBFH7 
          GOTO      HDBFHC90
.
HDBFHC90  IF        CKEYTYPE=3
            UNPACK    SAVKEY80,DBFHDTY,DBFHUR1,DBFHDOC,DBFHDEB
          ELSE
            IF        CKEYTYPE=2
              UNPACK    SAVKEY80,DBFHDTY,DBFHDOC,DBFHDEB
            ELSE
              UNPACK    SAVKEY80,DBFHDEB,DBFHDTY,DBFHDOC
            ENDIF
          ENDIF
          BEEP
          GOTO      HDBFHC20
HDBFHC91
          MOVE      SP70,DBFHDDAT
          MOVE      SP70,DBFHTER 
          MOVE      ZERO,DBFHTOT 
          MOVE      ZERO,DBFHTAX 
          MOVE      SP70,DBFHDPRT
          MOVE      SP70,DBFHDSTA
          MOVE      SP70,DBFHCDAT
          MOVE      SP70,DBFHCTIM
          MOVE      SP70,DBFHCUSR
          MOVE      SP70,DBFHUR2 
          MOVE      SP70,DBFHUD1 
          MOVE      SP70,DBFHUD2 
          MOVE      SP70,DBFHUY1 
          MOVE      SP70,DBFHUY2 
          MOVE      SP70,DBFHUC1 
          MOVE      SP70,DBFHUC2 
          MOVE      SP70,DBFHSPA 
          MOVE      SP70,KEY46
          MOVE      SP70,DBFHDOC 
          GOTO      HDBFHC96
.
HDBFHC92  MOVE      SP70,KEY46
          MOVE      SP70,KEY21
          MOVE      SP70,DBFHDOC 
          MOVE      CCITEM,XITEMNO
          CALL      LKDBFH00
.
HDBFHC96  PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
HDBFHC99  RETURN
.
LKDBFH00  BRANCH    CKEYTYPE,LKDBFH01,LKDBFH02,LKDBFH03
LKDBFH01  LOAD      KEY21,XITEMNO,ST1,ST2,ST3,ST4,ST5:  
                                 ST6,ST7,ST8,ST9,ST10:
                                 ST11,ST12,ST13,ST14,ST15:
                                 ST16,ST17
          UNPACK    KEY21,DBFHDEB,DBFHDTY,DBFHDOC
          GOTO      LKDBFH99
LKDBFH02  LOAD      KEY21,XITEMNO,ST1,ST2,ST3,ST4,ST5:  
                                 ST6,ST7,ST8,ST9,ST10:
                                 ST11,ST12,ST13,ST14,ST15:
                                 ST16,ST17
          UNPACK    KEY21,DBFHDTY,DBFHDOC,DBFHDEB                       
          GOTO      LKDBFH99
LKDBFH03  LOAD      KEY46,XITEMNO,ST1,ST2,ST3,ST4,ST5:  
                                 ST6,ST7,ST8,ST9,ST10:
                                 ST11,ST12,ST13,ST14,ST15:
                                 ST16,ST17
          UNPACK    KEY46,DBFHDTY,DBFHUR1,DBFHDOC,DBFHDEB                       
          GOTO      LKDBFH99
LKDBFH99  RETURN
.------------------------------------------------------------
. Position at First Record for Search
.------------------------------------------------------------
RSDBFH00  BRANCH    CKEYTYPE,RSDBFH01,RSDBFH02,RSDBFH03
.
RSDBFH01  PACK      KEY21,SRCHDEB,ONE,SP70                  
          PACK      SAVKEY80,SRCHDEB,ONE,SP70                  
          STRIP     SAVKEY80
          CALL      RSDBFH1      * by Debtor Code, Type, Document
          GOTO      RSDBFH99
.
RSDBFH02  PACK      KEY21,ONE,SRCHDOC,SP70                  
          PACK      SAVKEY80,ONE,SP70                  
          STRIP     SAVKEY80
          CALL      RSDBFH6      * by Type, Docment, Debtor Code
          GOTO      RSDBFH99
.
RSDBFH03  PACK      KEY46,ONE,SRCHUR1,SP70                  
          PACK      SAVKEY80,ONE,SRCHUR1,SP70                  
          STRIP     SAVKEY80
          CALL      RSDBFH7      * By Type, Reference, Document, Debtor
          GOTO      RSDBFH99
RSDBFH99  RETURN
.------------------------------------------------------------
. Position at First Record for Search
.------------------------------------------------------------
RLDBFH00  BRANCH    CKEYTYPE,RLDBFH01,RLDBFH02,RLDBFH03
.
RLDBFH01  PACK      KEY21,SAVKEY80,Z70
          CALL      RSDBFH1      * by Debtor Code, Type, Document
          GOTO      RSDBFH99
.
RLDBFH02  PACK      KEY21,SAVKEY80,Z70
          CALL      RSDBFH6      * by Type, Docment, Debtor Code
          GOTO      RSDBFH99
.
RLDBFH03  PACK      KEY46,SAVKEY80,Z70
          CALL      RSDBFH7      * By Type, Reference, Document, Debtor
          GOTO      RLDBFH99
RLDBFH99  RETURN
.------------------------------------------------------------
. Read Next Search
.------------------------------------------------------------
RKDBFH00  BRANCH    CKEYTYPE,RKDBFH01,RKDBFH02,RKDBFH03
.
RKDBFH01  CALL      RKDBFH1      * by Debtor Code, Type, Document
          PACK      KEY21,DBFHDEB,DBFHDTY,DBFHDOC,SP70                  
          MATCH     SAVKEY80,KEY21
          GOTO      OVERCOND IF NOT EQUAL
          GOTO      RKDBFH99
.
RKDBFH02  CALL      RKDBFH6      * by Type, Docment, Debtor Code
          PACK      KEY21,DBFHDTY,DBFHDOC,DBFHDEB,SP70                  
          MATCH     SAVKEY80,KEY21
          GOTO      OVERCOND IF NOT EQUAL
          GOTO      RKDBFH99
.
RKDBFH03  CALL      RKDBFH7      * By Type, Reference, Document, Debtor
          PACK      KEY46,DBFHDTY,DBFHUR1,DBFHDOC,DBFHDEB,SP70                  
          MATCH     SAVKEY80,KEY46
          GOTO      OVERCOND IF NOT EQUAL
          GOTO      RKDBFH99
.
RKDBFH99  RETURN
.------------------------------------------------------------
. Read Previous Search
.------------------------------------------------------------
RPDBFH00  BRANCH    CKEYTYPE,RPDBFH01,RPDBFH02,RPDBFH03
.
RPDBFH01  CALL      RPDBFH1      * by Debtor Code, Type, Document
          PACK      KEY21,DBFHDEB,DBFHDTY,DBFHDOC,SP70                  
          MATCH     SAVKEY80,KEY21
          GOTO      OVERCOND IF NOT EQUAL
          GOTO      RPDBFH99
.
RPDBFH02  CALL      RPDBFH6      * by Type, Docment, Debtor Code
          PACK      KEY21,DBFHDTY,DBFHDOC,DBFHDEB,SP70                  
          MATCH     SAVKEY80,KEY21
          GOTO      OVERCOND IF NOT EQUAL
          GOTO      RPDBFH99
.
RPDBFH03  CALL      RPDBFH7      * By Type, Reference, Document, Debtor
          PACK      KEY46,DBFHDTY,DBFHUR1,DBFHDOC,DBFHDEB,SP70                  
          MATCH     SAVKEY80,KEY46
          GOTO      OVERCOND IF NOT EQUAL
          GOTO      RPDBFH99
.
RPDBFH99  RETURN
.------------------------------------------------------------
. Read Previous Search
.------------------------------------------------------------
RDDBFH00  BRANCH    CKEYTYPE,RDDBFH01,RDDBFH02,RDDBFH03
.
RDDBFH01  MOVE      ST1,KEY21
          CALL      RDDBFH1      * by Debtor Code, Type, Document
          GOTO      RKDBFH99
.
RDDBFH02  MOVE      ST1,KEY21
          CALL      RDDBFH6      * by Type, Docment, Debtor Code
          GOTO      RKDBFH99
.
RDDBFH03  MOVE      ST1,KEY46
          CALL      RDDBFH7      * By Type, Reference, Document, Debtor
          GOTO      RKDBFH99
.
RDDBFH99  RETURN
.
          DEFPROC   KEYDEB00
.
          INC       STDHLPDF
.
          CALL      KDBDBA00
          GOTO      ENDDBDBA
.
          INC       DEBDBTKY
          INC       STDHLPCD
.
ENDDBDBA  ENDPROC      
.------------------------------------------------------------
. Check Search Ranges Returns OVRCD=1 if Out of Range
.------------------------------------------------------------
RDBFHC00  MOVE      ZERO,OVRCD
.          MATCH     SP70,SRCHDOC
.          IF        !@EQUAL
.            MATCH     SRCHDOC,DBFHDOC 
.            GOTO      RDBFHC90 IF NOT EQUAL
.          ENDIF
          MATCH     SP70,SRCHDAT
          IF        !@EQUAL
            MATCH     SRCHDAT,DBFHDDAT 
            GOTO      RDBFHC90 IF NOT EQUAL
          ENDIF
          MATCH     SP70,SRCHUR1
          IF        !@EQUAL
            MOVE      SRCHUR1,KEY25 
            STRIP     KEY25 
            MATCH     KEY25,DBFHUR1 
            GOTO      RDBFHC90 IF NOT EQUAL
          ENDIF
          MATCH     SP70,SRCHDEB
          IF        !@EQUAL
            MATCH     SRCHDEB,DBFHDEB 
            GOTO      RDBFHC90 IF NOT EQUAL
          ENDIF
          IF        !(SRCHAMT=0)
            ASSIGN    DBFHTOT+DBFHTAX,F12P2
            COMPARE   SRCHAMT,F12P2   
            GOTO      RDBFHC90 IF NOT EQUAL
          ENDIF
          GOTO      HDBFHC99
.
RDBFHC90  MOVE      ONE,OVRCD
.
RDBFHC99  RETURN
.------------------------------------------------------------
. Enter Search Parameters
.------------------------------------------------------------
SDBFHC00  MOVE      CCOL,SAVCOL
          MOVE      CVERT,SAVVERT
          MOVE      ZERO,CSEARCH
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          MOVE      TWO,CKEYTYPE   
.
          UNPACK    SP70,SRCHDOC,SRCHDAT,SRCHUR1,SRCHDEB
          MOVE      ZERO,SRCHAMT
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          DISPLAY   *PSTDCOL00:STDROW,*V2LON,*HON,"Sch"
.
. Document Reference
.--------------------
          KEYIN     *PSTDCOL01:STDROW,*V2LON,*JR,*ZF,SRCHDOC
          RESET     SRCHDOC
          MATCH     "000000000000",SRCHDOC
          IF        @EQUAL
            MOVE      SP70,SRCHDOC
          ENDIF
.
. Date
.-----
          MOVE      STDCOL02,CCOL 
          MOVE      STDROW,CVERT
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      SP2,CMON
          MOVE      SP2,CDAY
          MOVE      ZERO,CHIGHLT
          MOVE      ONE,CCENTRY
          MOVE      ZERO,CDEFDTE
          MOVE      ONE,CCANLDTE     *  1 Default dates cancelled by a space 
          PACK      SRCHDAT,SP70
          CALL      KEYDATE
          IF        OVRCD=0
            PACK      SRCHDAT,CCENT,CYEAR,CMON,CDAY
            REP       " 0",SRCHDAT
          ENDIF
.
. Debtor
.-------
          MOVE      STDCOL03,CCOL
          MOVE      STDROW,CVERT
          PROC      KEYDEB00
          IF        EXIT=0
            MOVE      DBDBNA1,DEBNAM
            DISPLAY   *PSTDCOL03:STDROW,*V2LON,DEBNAM  
            MOVE      DBDBDEB,SRCHDEB
            MOVE      ONE,CKEYTYPE
          ELSE
            MOVE      SP70,SRCHDEB
          ENDIF
.
. Statement Reference
.---------------------
          KEYIN     *PSTDCOL04:STDROW,*V2LON,SRCHUR1  
          ENDSET    SRCHUR1 
          APPEND    SP70,SRCHUR1 
          RESET     SRCHUR1 
          MATCH     SP70,SRCHUR1 
          IF        !@EQUAL
            MOVE      "3",CKEYTYPE
          ENDIF
.
. Amount
.--------
          KEYIN     *PSTDCOL05:STDROW,*V2LON,SRCHAMT  
.
          MOVE      SAVCOL,CCOL
          MOVE      SAVVERT,CVERT
          RETURN
.
