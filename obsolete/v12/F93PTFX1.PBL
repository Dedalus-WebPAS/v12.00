.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   F93PTFX1.PBL                                              *
.* Desc      :   Fixit for PATFX1FD (Patient Health Fund Extension Table)  *
.***************************************************************************
.* Date      :   08/10/2002                                                *
.* Author    :   Tracey Nguyen                                             *
.* Function  :   SRF20135  HOSClaims Phase#1                               * 
.*               This program will loop through the Health Fund table      * 
.*               (PATFN1FD) and for every Health Fund record, write an     *
.*               equivalent record to the Health Fund Extension table      *
.*               (PATFX1FD).                                               *
.*                                                                         *
.* Mods      :                                                             *
.*        V10.01.01 09/11/2010  Peter Vela       CAR 233037                *
.*                  Space filled the new variables in PATFX1               *
.*        V9.03.00  15/03/2004  Steve Armstrong  HosClaims Phase 1 (48280) *
.*                  Ported from 6.11.                                      *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       PATFN1FD/INC
          INC       PATFX1FD/INC 
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
COUNTER   FORM      3                      * count no. of records updated
.
.
. LOCAL CONSTANT DEFINTIONS
. -------------------------
.
ZERO8     INIT      "0000    "
TILDA8    INIT      "~~~~~~~~"
.
.
PRGID     INIT      "F93PTFX1"
PRGNAM    INIT      "Fixit for PATFX1FD"
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with fixit
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD               * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af" 
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patfx1af";
          OPEN      PATFX1A1,"patfx1af" 
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                              *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION           * exit selected ?
          GOTO      OPTN9500 IF EQUAL      * yes
.
          BRANCH    COPTION,OPTN9000       * run fixit 
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*        This section reads the patfn1af table from the beginning        * 
.*        and looping through every record to write an equivalent record  *
.*        to the Health Fund extension table (patfx1af) if it does not    *
.*        exist.                                                          *
.**************************************************************************
.
PROC0000  MOVE      ZERO,COUNTER
          MOVE      SP20,KEY14                   * position key at start 
          CALL      RDSFUND1 
PROC1000  CALL      RDKFUND1           
          BRANCH    OVRCD,PROC9000               * exit at end of file
.
          MATCH     ZERO8,HFTABL
          GOTO      PROC3000 IF NOT EQUAL
.
          MOVE      HCODE,KEY6
          CALL      RDPTFX11                                      
          BRANCH    OVRCD,PROC2000                 
.
          GOTO      PROC3000
.
.         Populate the values to insert to the extension file
.
PROC2000  MOVE      HCODE,PTFXHFND        
          MOVE      ZERO,PTFXEXTR
          MOVE      SP1,PTFXTFLG
          MOVE      SP30,PTFXSUBJ
          MOVE      SP70,PTFXADDR
          MOVE      SP70,PTFXSUBJ
          MOVE      SP70,PTFXADD1
          MOVE      SP70,PTFXADD2
          MOVE      SP70,PTFXADD3
          MOVE      SP70,PTFXADD4
          MOVE      SP70,PTFXPOST
          MOVE      SP70,PTFXZINV
          PACK      PTFXSCEM,SP70,SP70
          MOVE      SP70,PTFXSCPN
          MOVE      SP70,PTFXSCPP
          MOVE      SP70,PTFXECLP
          MOVE      SP70,PTFXCEFR
          MOVE      SP70,PTFXRHCD
          PACK      PTFXRHFT,SP70,SP70
          PACK      PTFXSPAR,SP70,SP70
.
          CALL      WRPTFX11                     * write to extension file
          ADD       ONE,COUNTER                  
          DISPLAY   *P1:24,*EL,"Writing : ",*V2LON,KEY6
.
PROC3000  PACK      KEY14,HCODE,TILDA8 
          CALL      RDSFUND1                     * position at next Health Fund
          GOTO      PROC1000                     * loop back to read next rec
.
PROC9000  DISPLAY *P1:24,*EL,COUNTER," records have been written.  "; 
          CALL    HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATFN1IO/INC
          INC       PATFX1IO/INC
+
