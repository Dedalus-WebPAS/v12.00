.*******************************************************************************
.* Program         : IBAPCP33                                                  *
.* System          : PATIENT CARE                                              *
.* Desc            : Nurses Master listing                                     *
.*                                                                             *
.* Date            : 18-01-90                                                  *
.* Programmer      : Paul Duncan                                               *
.* Modifications   :                                                           *
.*        V9.02.00  30/12/2002 Tracey Nguyen srf 23497                         *
.*                  r5.10 Care Plans Port to V9                                *
.*-----------------------------------------------------------------------------*
.* Modifications   : V5.01.02 23/5/90 Glen Hobby                               *
.*                            Fixed screen display bugs                        *
.*                                                                             *
.*******************************************************************************
.
.$$$$$
. Print Database (IBAPCP33)
.--------------------------
.
.   -Initialization
.     
.         -Display Header
.         -Open Files
.             PCPNUR01
.             PCPNUR02
.             PCPNUR03
.             PATCODE1
.
.   -Main Options
.       
.         -0 = Exit
.         -1 = Print a range of nurse codes
.                - CDRN0000 - Enter range of nurses codes to be printed.
.                             call VALD0000 to validate entry.
.                _ NURS0000 - Print all records with nurses codes falling
.                             in range from CODEFR to CODETO.
.
.         -2 = Print a range of nurses names.
.                - NMRN0000 - Enter range of nurses names to be printed.
.                           _ call validate to set start and finish, but
.                             actual name doesn't have to be entered;
.                             therefore no exact validation
.                _ DESC0000 - printout all records whose name falls within
.                             the range NAMEFR to NAMETO.
.
.         
.         -3 = Print a range of staff levels
.                - SLRN0000 - Enter range of staff levels to be printed.
.                             call VALD0000 to validate entry.
.                - SLEV0000 - prints all records whose staff level falls
.                             in the range SLEVFR to SLEVFR.
.
.  - PRNT0000 - All printouts are produced by looping through the file 
.               printing required  fields.
.              
.
.  -End
.
.*******************************************************************************
          INC       STD001FD
          INC       PCPNURFD/INC
          INC       PATCODFD/INC
.*******************************************************************************
.*                                 CONSTANTS                                   *
.*******************************************************************************
ONE2      FORM      " 1"
TWO2      FORM      " 2"
THREE2    FORM      " 3"
FOUR2     FORM      " 4"
FIVE2     FORM      " 5"
SIX2      FORM      " 6"
.
SD        INIT      "SD" 
.
Z20       INIT      "ZZZZZZZZZZZZZZZZZZZZ"
.
.******************************************************************************* .*                             GLOBAL VARIABLES                               *
.*******************************************************************************
COD       DIM       6
CODE      DIM       2
CODEFR    DIM       6
CODETO    DIM       6
.
DESC      DIM       49             *Temporary Description
.
END       DIM       1
.
FINISH    FORM      1
.
.
LINE      FORM      2
LNO       FORM      2
.
NAME      DIM       20
NAMEFR    DIM       20
NAMETO    DIM       20
.
PRFR      DIM       10
PRTO      DIM       10
.
RECPR     FORM      3
.
SLEV      DIM       3
SLEVFR    DIM       3
SLEVTO    DIM       3
START     FORM      1
STORF     FORM      1
.
PRGID     INIT      "IBAPCP33"
PRGNAM    INIT      "Nurses Master Listing"
VERSION   INIT      "V12.00.00"
.*******************************************************************************
.*                            MAINLINE                                         *
.*******************************************************************************
ML0000    CALL      INIT0000                     * Display header and open files
.
ML1000    CALL      SLCT0000                     * Select which printout or
                                                 * exit
          BRANCH    EXIT,ML9999
.
          BRANCH    OPTION,ML3000,ML4000,ML5000
          GOTO      ML1000
.
ML3000    CALL      NURS0000                     * print a range of nurses codes
          GOTO      ML1000 
.
ML4000    CALL      DESC0000                     * print a range of descriptions
          GOTO      ML1000
.
ML5000    CALL      SLEV0000                     * print a range of staff levels
          GOTO      ML1000
.
ML9999    CHAIN     PGM                    
.
.*******************************************************************************
.*                                 INIT0000                                    *
.*                       Display header and open files                         *.*******************************************************************************
.
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CNOUNDLN
.
          DISPLAY   *P50:24,"Opening pcpnuraf";
          OPEN      PCPNURA1,"pcpnuraf"
.
          DISPLAY   *P50:24,"Opening pcpnuraf";
          OPEN      PCPNURA2,"pcpnuraf"

          DISPLAY   *P50:24,"Opening pcpnuraf";
          OPEN      PCPNURA3,"pcpnuraf"
.
          DISPLAY   *P50:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
INIT9999  RETURN
.
.*******************************************************************************
.*                                 SLCT0000                                    *
.*                  Select main options and store in exit                      *.*                   1. Print single Patient Care Plan                         *
.*                   2. Print PCPs for a Diagnostic category                   *
.*                   3. Print all Patient Care Plans                           *
.*******************************************************************************.
SLCT0000  CALL      SLSC0000
.
SLCT1000  DISPLAY   *P18:9,*EL,*P18:9,UNDLN2;
          KEYIN     *P18:9,*V2LON,OPTION;
.
          COMPARE   ZERO,OPTION             * Exit?
          GOTO      SLCT2000 IF EQUAL
.
          DISPLAY   *P18:9,*V2LON,OPTION; 
.
          BRANCH    OPTION,SLCT3000,SLCT3000,SLCT3000
.
.
          BEEP
          GOTO      SLCT1000
.
SLCT2000  MOVE      TRUE,EXIT              * Set exit flag
          GOTO      SLCT9999
.
SLCT3000  MOVE      FALSE,EXIT
.
SLCT9999  RETURN                   
.
.*******************************************************************************
.*                                 SLSC0000                                    *
.*                      Display main selection screen                          *
.*******************************************************************************
. 
SLSC0000  DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = Print a range of nurses codes":
                    *P2:6,*V2LON,TWO,*HOFF," = Print a range of nurses names":
                    *P2:7,*V2LON,THREE,*HOFF," = Print a range of staff levels":
                    *P2:9,*EL,"Select Option : "
.
SLSC9999  RETURN
.
.*******************************************************************************
.*                                 NURS0000                                    *
.*                      Print a range of nurses codes                          *
.*******************************************************************************
.
NURS0000  CALL      CDRN0000                * get code range
          BRANCH    EXIT,NURS9999
          MOVE      ZERO,RECPR
.
NURS1000  MOVE      CODEFR,KEY6             * read first record
          CALL      RDPCNUR1
.
          CALL      GTDS0000                * get description
.
.************* Set HEAD80 parameters ****************
.
NURS2000  MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:24,*EL,"Printing code : "
          CALL      HEAD0000                * initial heading
          GOTO      NURS4000
.
NURS3000  DISPLAY   *P1:24,*B,"Invalid code ";
          CALL      HITENTER   
          GOTO      NURS0000
.
NURS3500  CALL      RKPCNUR1                * read next record
          BRANCH    OVRCD,NURS8000
.
          MATCH     PCNUCODE,CODETO         * read too far?
          GOTO      NURS8000 IF LESS
.
          CALL      GTDS0000                * get desc
.         
NURS4000  DISPLAY   *P17:24,PCNUCODE
          CALL      PRNT0000                * print record
          ADD       ONE,RECPR               * increment print count
          GOTO      NURS3500
.
NURS8000  CALL      UND80
          PRINT     *N,"Records printed : ",RECPR,*N,*N:
                    *N,"*** End of Report ***" 
         
.
NURS9999  RETURN

.*******************************************************************************
.*                                 CDRN0000                                    *
.*                             Get code range                                  *
.*******************************************************************************
CDRN0000  DISPLAY   *P1:12,*EF,"Code from : ":
                    *P1:13,"Code to   : "
          MOVE      FALSE,START
          MOVE      FALSE,FINISH
          CLEAR     DESC                     * Clear the description
          CLEAR     PRFR
          CLEAR     PRTO
.
CDRN1000  KEYIN     *P13:12,"______",*P13:12,*V2LON,CODEFR,*P13:12,*DV,CODEFR
.
          ENDSET    CODEFR
          APPEND    SP6,CODEFR
          RESET     CODEFR
.
          MOVE      ZERO,STORF
          MATCH     QUEST,CODEFR
          CALL      CDQT0000 IF EQUAL
.
          MOVE      ZERO,STORF
          MOVE      CODEFR,COD
          MOVE      FALSE,EXIT
          CALL      VALD0000                 * Validate entry
          BRANCH    EXIT,CDRN0000            * error?
          BRANCH    START,CDRN2000           * start?
          MOVE      COD,PRFR                  
.
          MOVE      TEN2,LINE
          CALL      DSSP0000                 * display description if not start
.
CDRN2000  KEYIN     *P13:13,"______",*P13:13,*V2LON,CODETO,*P13:13,*DV,CODETO
.
          ENDSET    CODETO
          APPEND    SP6,CODETO
          RESET     CODETO
.
          MOVE      ONE,STORF 
          MATCH     QUEST,CODETO
          CALL      CDQT0000 IF EQUAL
.
          MOVE      ONE,STORF
          MOVE      CODETO,COD
          CALL      VALD0000                * validate entry
.
          BRANCH    EXIT,CDRN2000           * error?
          BRANCH    FINISH,CDRN8000         * finish?
          MOVE      COD,PRTO
.
          MOVE      TEN3,LINE
          CALL      DSSP0000                * display desc if not finish
CDRN8000  CALL      CONTQST
          BRANCH    CEXIT,CDRN9999,CDRN0000,CDRN9000
.
CDRN9000  MOVE      TRUE,EXIT               * set exit flag
.
CDRN9999  RETURN 
.
.*******************************************************************************
.*                                 SLRN0000                                    *
.*                             Get code range                                  *
.*******************************************************************************
SLRN0000  DISPLAY   *P1:12,"Staff Level from : ":
                    *P1:13,"Staff level to   : "
          MOVE      FALSE,START
          MOVE      FALSE,FINISH
.
SLRN1000  KEYIN     *P20:12,"___",*EL:
                    *P20:13,"___",*EL:
                    *P20:12,*V2LON,SLEVFR,*P20:12,*DV,SLEVFR
.
          ENDSET    SLEVFR
          APPEND    SP6,SLEVFR
          RESET     SLEVFR
.
          MOVE      ZERO,STORF              * Set flag to start 
          MATCH     QUEST,SLEVFR            * question mark?
          CALL      SLQT0000 IF EQUAL
.
          MATCH     SP3,SLEVFR              * no staff level
          GOTO      SLRN1500 IF EQUAL
.
          PACK      KEY5,SD,SLEVFR
          CALL      RDCODE1                 * get sl desc if staff level not sp
          DISPLAY   *P30:12,*V2LON,TDESC
.
SLRN1500  MOVE      ZERO,STORF 
          MOVE      SLEVFR,SLEV
          MOVE      FALSE,EXIT
          CALL      VALD0000                 * Validate entry
          BRANCH    EXIT,SLRN0000
          BRANCH    START,SLRN2000
          MOVE      SLEVFR,PRFR
.
          BRANCH    EXIT,SLRN9000,SLRN0000
.
SLRN2000  KEYIN     *P20:13,"___",*EL,*P20:13,*V2LON,SLEVTO,*P20:13,*DV,SLEVTO
.
          ENDSET    SLEVTO
          APPEND    SP6,SLEVTO
          RESET     SLEVTO
.
          MOVE      ONE,STORF               * set start flag
          MATCH     QUEST,SLEVTO            * question mark?
          CALL      SLQT0000 IF EQUAL 
.
          MATCH     SP3,SLEVTO
          GOTO      SLRN2500 IF EQUAL
.
          PACK      KEY5,SD,SLEVTO
          CALL      RDCODE1
          DISPLAY   *P30:13,*V2LON,TDESC
.
SLRN2500  MOVE      ONE,STORF
          
          MOVE      SLEVTO,SLEV
          CALL      VALD0000                * validate entry
          BRANCH    EXIT,SLRN2000           * error?
          BRANCH    FINISH,SLRN8000         * finish?
          MOVE      SLEVTO,PRTO
.
          BRANCH    EXIT,SLRN0000,SLRN2000
.
SLRN8000  CALL      CONTQST
          BRANCH    CEXIT,SLRN9999,SLRN0000,SLRN9000
.
SLRN9000  MOVE      TRUE,EXIT
.
SLRN9999  RETURN
.*******************************************************************************
.*                                 NMRN0000                                    *
.*                             Get code range                                  *
.*******************************************************************************
NMRN0000  DISPLAY   *P1:12,"Name from : ":
                    *P1:13,"Name to   : "
          MOVE      FALSE,START
          MOVE      FALSE,FINISH
.
NMRN1000  KEYIN     *P13:13,*EL:
                    *P13:12,"____________________",*P13:12,*V2LON,NAMEFR:
                    *P13:12,*DV,NAMEFR
.
          ENDSET    NAMEFR
          APPEND    SP6,NAMEFR
          RESET     NAMEFR
.
          MOVE      ZERO,STORF              * set flag to start
          MOVE      NAMEFR,NAME
          CALL      VALD0000
          BRANCH    START,NMRN2000          * start flag still set?
          MOVE      NAME,PRFR               * set up print variable
.
NMRN2000  KEYIN     *P13:13,"____________________",*P13:13,*V2LON,NAMETO:
                    *P13:13,*DV,NAMETO   
.
          ENDSET    NAMETO
          APPEND    SP6,NAMETO
          RESET     NAMETO
.
          MOVE      ONE,STORF               *set flag to finish
          MOVE      NAMETO,NAME
          CALL      VALD0000
          BRANCH    FINISH,NMRN3000         * still finish/
          MOVE      NAME,PRTO
.
NMRN3000  CALL      CONTQST
          BRANCH    CEXIT,NMRN9999,NMRN0000,NMRN9000
.
NMRN9000  MOVE      TRUE,EXIT
NMRN9999  RETURN
.

.*******************************************************************************
.*                                 VALD0000                                    *
.*                           Validate entries                                  *
.*******************************************************************************
VALD0000  BRANCH    OPTION,VALD1000,VALD2000,VALD3000
.
.*** Validate Nurses code ***
.
VALD1000  MATCH     SP6,COD                 * nothing entered?
          GOTO      VALD1300 IF EQUAL
.
          MATCH     "ZZZZZZ",COD
          GOTO      VALD1300 IF EQUAL
.
          MOVE      COD,KEY6                * on file?
          CALL      RDPCNUR1
          BRANCH    OVRCD,VALD1200
.
          MOVE      FALSE,EXIT
          GOTO      VALD9999
.          
VALD1200  DISPLAY   *P1:24,*V2LON,"Invalid code. ";
          CALL      HITENTER
          MOVE      TRUE,EXIT
          GOTO      VALD9999
.
VALD1300  BRANCH    STORF,VALD1400          * start or finish?
          MOVE      TRUE,START              * start
          DISPLAY   *P13:12,*EL,*V2LON,"Start"
          MOVE      "Start",PRFR            * set up print variable
          MOVE      SP20,CODEFR
          MOVE      FALSE,EXIT
          GOTO      VALD9999
.
          
VALD1400  MOVE      "Finish",PRTO           * finish 
          MOVE      TRUE,FINISH
          DISPLAY   *P13:13,*EL,*V2LON,"Finish"
          MOVE      Z20,CODETO
          MOVE      FALSE,EXIT
          GOTO      VALD9999
.
.*** Validate Nurses Name ***
.
VALD2000  MOVE      FALSE,EXIT
          MATCH     SP20,NAME               * nothing entered?
          GOTO      VALD2100 IF EQUAL
.
          GOTO      VALD9999
.
VALD2100  BRANCH    STORF,VALD2200          * start or finish?
          MOVE      TRUE,START              * start
          DISPLAY   *P13:12,*EL,*V2LON,"Start"
          MOVE      "Start",PRFR
          MOVE      SP20,NAMEFR
          GOTO      VALD9999
.
          
VALD2200  MOVE      "Finish",PRTO           * finish
          MOVE      TRUE,FINISH
          DISPLAY   *P13:13,*EL,*V2LON,"Finish"
          MOVE      Z20,NAMETO
          GOTO      VALD9999
.
.*** Validate staff level ***
.
VALD3000  MOVE      FALSE,EXIT
          MATCH     SP3,SLEV
          GOTO      VALD3300 IF EQUAL
.
          PACK      KEY5,SD,SLEV            * on file?
          CALL      RDCODE1
          BRANCH    OVRCD,VALD3200
.
          GOTO      VALD9999
          
VALD3200  DISPLAY   *P1:24,*V2LON,"Invalid staff level. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      VALD9999
.
VALD3300  BRANCH    STORF,VALD3400          * start or finish?
          MOVE      TRUE,START              * start
          DISPLAY   *P20:12,*EL,*V2LON,"Start"
          MOVE      "Start",PRFR
          MOVE      SP20,SLEVFR
          GOTO      VALD9999
.
          
VALD3400  MOVE      "Finish",PRTO           * finish
          MOVE      TRUE,FINISH
          DISPLAY   *P20:13,*EL,*V2LON,"Finish"
          MOVE      Z20,SLEVTO
          GOTO      VALD9999
VALD9999  RETURN
.
.*******************************************************************************
.*                                 DESC0000                                    *
.*                      Print a range of description codes                     *
.*******************************************************************************
.
DESC0000  CALL      NMRN0000                * get code range
          BRANCH    EXIT,DESC9999
          MOVE      ZERO,RECPR
.
DESC1000  PACK      KEY26,NAMEFR,SP6         * read first record
          CALL      RSPCNUR2
.
.************* Set HEAD80 parameters ****************
.
DESC2000  DISPLAY   *P1:24,*EL,"Printing Code :"
          MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
          CALL      HEAD0000                * initial heading
          GOTO      DESC3500
.
DESC3000  DISPLAY   *P1:24,*B,"Invalid code ";
          CALL      HITENTER   
          GOTO      DESC0000
.
DESC3500  CALL      RKPCNUR2                * read next record
          BRANCH    OVRCD,DESC8000
.
          MATCH     PCNUNAME,NAMETO         * read too far?
          GOTO      DESC8000 IF LESS
.
          CALL      GTDS0000
.
DESC4000  DISPLAY   *P17:24,PCNUCODE
          CALL      PRNT0000                * print care plan
          ADD       ONE,RECPR
          GOTO      DESC3500
.
DESC8000  CALL      UND80
          PRINT     *N,"Records printed : ",RECPR,*N,*N:
                    *N,"*** End of Report ***" 
         
.
DESC9999  RETURN
.*******************************************************************************
.*                                 SLEV0000                                    *
.*                      Print a range of nurses codes                          *
.*******************************************************************************
.
SLEV0000  CALL      SLRN0000                * get code range
          BRANCH    EXIT,SLEV9999
          MOVE      ZERO,RECPR
.
SLEV1000  PACK      KEY29,SLEVFR,SP30       * read first record
          CALL      RSPCNUR3
.
.************* Set HEAD80 parameters ****************
.
SLEV2000  DISPLAY   *P1:24,*EL,"Printing Code : "
          MOVE      ZERO,CPAGENO
          CLOCK     TIME,CTIMEIS
          CALL      HEAD0000                * initial heading
          GOTO      SLEV3500
.
SLEV3000  DISPLAY   *P1:24,*B,"Invalid code ";
          CALL      HITENTER   
          GOTO      SLEV0000
.
SLEV3500  CALL      RKPCNUR2                * read next record
          BRANCH    OVRCD,SLEV8000
.
          MATCH     PCNUCLSS,SLEVTO         * read too far?
          GOTO      SLEV8000 IF LESS
.
          CALL      GTDS0000                * get desc
.         
SLEV4000  DISPLAY   *P17:24,PCNUCODE
          CALL      PRNT0000                * print one record
          ADD       ONE,RECPR
          GOTO      SLEV3500
.
SLEV8000  CALL      UND80
          PRINT     *N,"Records printed : ",RECPR,*N,*N:
                    *N,"*** End of Report ***" 
         
.
SLEV9999  RETURN
.*******************************************************************************
.*                              GTDS0000                                       *
.*                      Get staff description from file                        *
.*******************************************************************************
GTDS0000  MOVE      SP30,TDESC
          MATCH     SP3,PCNUCLSS            * no staff level?
          GOTO      GTDS9999 IF EQUAL
.
          PACK      KEY5,SD,PCNUCLSS        * get level description
          CALL      RDCODE1           
.
GTDS9999  MOVE      TDESC,DESC
.
          RETURN
.
.*******************************************************************************
.*                                 DSSP0000                                    *
.*              Displays Diagnosis description for verification                *.*******************************************************************************
.
DSSP0000  MOVE      COD,KEY6
          CALL      RDPCNUR1                * get description
.
          BRANCH    OPTION,DSSP1000,DSSP9999,DSSP2000
.
DSSP1000  MOVE      PCNUNAME,DESC
          GOTO      DSSP3000
.
DSSP2000  PACK      KEY5,SD,PCNUCLSS        * get level description
          CALL      RDCODE1
          MOVE      TDESC,DESC
.
DSSP3000  DISPLAY   *P30:LINE,DESC
.
DSSP9999  RETURN 
         
.
.*******************************************************************************
.*                              PRNT0000                                       *
.*                   Print a line of output                                    *
.*******************************************************************************
PRNT0000  COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
PRNT1000  PRINT     "| ",PCNUCODE," | ",PCNUNAME,*40,"|  ",PCNUCLSS,"  | ":
                    DESC,*80,"|"
.
          ADD       ONE,CLNO
.
PRNT9999  RETURN
          

.*******************************************************************************
.*                              HEAD0000                                       *
.*                     Print the heading for a new page                        *
.*******************************************************************************
HEAD0000  CALL      HEAD80
.
          BRANCH    OPTION,HEAD1000,HEAD2000,HEAD3000
.
HEAD1000  PRINT     *N,*15,"Nurses code from : ",PRFR,*45,"to : ",PRTO,*N
.
          GOTO      HEAD4000
.
HEAD2000  PRINT     *N,*15,"Nurses name from : ",PRFR,*45,"to : ",PRTO,*N
.
          GOTO      HEAD4000
.
HEAD3000  PRINT     *N,*15,"Staff level from : ",PRFR,*45,"to : ",PRTO,*N
.
          GOTO      HEAD4000
.
HEAD4000  CALL      UND80
          PRINT     "| Code   | Nurses Name",*40,"| Level | Description",*80,"|"
          CALL      UND80
          ADD       SIX,CLNO 
.
HEAD9999  RETURN
.
.*******************************************************************************
.*                              CDQT0000                                       *
.*                  Question mark handling routine for nurses codes            *
.*******************************************************************************
CDQT0000  CALL      PCPNURDS
.
CDQT1000  BRANCH    STORF,CDQT2000
.
CDQT1500  DISPLAY   *P1:24,*EL,"Code From :"
          KEYIN     *P13:24,"______",*P13:24,*V2LON,CODEFR
.
          ENDSET    CODEFR
          APPEND    SP6,CODEFR
          RESET     CODEFR
.
          MOVE      CODEFR,COD
          CALL      VALD0000                * validate code
          BRANCH    EXIT,CDQT1500           * exit?
          BRANCH    START,CDQT1600          * start?
          MOVE      COD,PRFR
.
CDQT1600  CALL      SLSC0000
          DISPLAY   *P1:9,"Select Option : ",*V2LON,"1",*HOFF:
                    *P1:12,"Code from : ",*V2LON,PRFR,*HOFF:
                    *P1:13,"Code to   : "
.
          BRANCH    START,CDQT9999
          MOVE      TEN2,LINE
          CALL      DSSP0000
.
          GOTO      CDQT9999
.
CDQT2000  DISPLAY   *P1:24,*EL,"Code to :"
          KEYIN     *P11:24,"______",*P11:24,*V2LON,CODETO
.
          ENDSET    CODETO
          APPEND    SP6,CODETO
          RESET     CODETO
.
          MOVE      CODETO,COD
          CALL      VALD0000                * validate code
          BRANCH    EXIT,CDQT2000           * exit?
          BRANCH    FINISH,CDQT2600         * finish?
          MOVE      COD,PRTO
.
CDQT2600  CALL      SLSC0000
          DISPLAY   *P1:9,"Select Option : ",*V2LON,"1",*HOFF:
                    *P1:12,"Code from : ",*V2LON,PRFR,*HOFF:
                    *P1:13,"Code to   : ",*V2LON,PRTO
.
          MATCH     "Start",PRFR
          GOTO      CDQT2610 IF NOT EQUAL
.
          CLEAR     PCNUNAME
          MOVE      "ZZZZZZZZ",COD
.
CDQT2610  MOVE      PRFR,COD
          MOVE      TEN2,LINE                * Display start description
          CALL      DSSP0000
.
CDQT2700  BRANCH    FINISH,CDQT9999
          MOVE      PRTO,COD
          MOVE      TEN3,LINE                * Display finish description
          CALL      DSSP0000
.
CDQT9999 RETURN
.
.*******************************************************************************
.*                              SLQT0000                                       *
.*                  Question mark handling routine for nurses codes            *
.*******************************************************************************
SLQT0000  MOVE      SD,COD
          CALL      PATCODDS
.
SLQT1000  BRANCH    STORF,SLQT2000
.
          DISPLAY   *P1:24,"Staff Level From :"
SLQT1500  KEYIN     *P20:24,"___",*P20:24,*V2LON,SLEVFR
.
          ENDSET    SLEVFR
          APPEND    SP6,SLEVFR
          RESET     SLEVFR
.
          MOVE      SLEVFR,SLEV
          CALL      VALD0000                * validate staff level
          BRANCH    EXIT,SLQT1500           * exit?
          BRANCH    START,SLQT1600          * start?
          MOVE      SLEV,PRFR
.
SLQT1600  CALL      SLSC0000
          DISPLAY   *P1:9,"Select Option : ",*V2LON,"2":
                    *P1:12,"Staff Level from : ",*V2LON,SLEVFR:
                    *P1:13,"Staff Level to   : "
          BRANCH    START,SLQT9999
          PACK      KEY5,SD,SLEVFR
          CALL      RDCODE1
          DISPLAY   *P30:12,*V2LON,TDESC
          MOVE      TDESC,DESC
.
          GOTO      SLQT9999
.
SLQT2000  DISPLAY   *P1:24,"Staff Level to :"
.
SLQT2500  KEYIN     *P18:24,"______",*V2LON,SLEVTO
.
          ENDSET    SLEVTO
          APPEND    SP6,SLEVTO
          RESET     SLEVTO
.
          MOVE      SLEVTO,SLEV
          CALL      VALD0000                * validate staff level
          BRANCH    EXIT,CDQT2000           * exit/
          BRANCH    FINISH,CDQT2600         * finish?
          MOVE      SLEV,PRTO
.
SLQT2600  CALL      SLSC0000
          DISPLAY   *P1:9,"Select Option : ",*V2LON,"1":
                    *P1:12,"Code from : ",*V2LON,CODEFR:
                    *P1:13,"Code to   : ",*V2LON,CODETO
.
          BRANCH    START,SLQT3000
          PACK      KEY5,SD,SLEVFR
          CALL      RDCODE1
          DISPLAY   *P30:12,*V2LON,TDESC
          MOVE      TDESC,DESC
.
SLQT3000  BRANCH    FINISH,SLQT9999
          PACK      KEY5,SD,SLEVTO
          CALL      RDCODE1
          DISPLAY   *P30:13,*V2LON,TDESC
          MOVE      TDESC,DESC
SLQT9999 RETURN


.*******************************************************************************
          INC       STD001IO
          INC       PATCODIO/INC
          INC       PCPNURDS
          INC       PATCODKY
          INC       PCPNURIO/INC
