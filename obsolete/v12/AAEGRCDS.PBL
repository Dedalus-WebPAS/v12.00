.******************************************************************************
.* System         : Accident and Emergency                                    *
.*                                                                            *
.* Include Name   : AAEGRCDS.PBL                                              *
.*                                                                            *
.* Function       : Display the codes from AAEGRCFD                           *
.*                                                                            *
.* Author         : Justin Coates      03/08/1993                             *
.*                                                                            *
.* Parameter      : AERCFRGN    Blank = display all codes                     *
.*                              "1" = display for Foreign Body Injury         *
.*                              "2" = display for non-Foreign Body Injury     *
.*                                                                            *
.* Returns        : AERCCODE           region code from AAEGRCFD              *
.*                                                                            *
.* Modifications  : 17/12/1997  Steve Armstrong    EMR Mods                   *
.*                  Mods for new field - AERCFLAG                             *
.*                  22/11/93  ROD                                             *
.*                            Changed REGION CODES to ANATOMICAL SITE CODES   *
.******************************************************************************
.
AAEGRCDS  MOVE      FIVE,CVERT              * line counter
          MOVE      ONE,CCOL                * start on LHS of screen
          MOVE      ZERO,AERCQCNT           * clear the count
          MOVE      ONE,CPAGENO             * set page number
          MOVE      SP2,KEY2                * key of blanks
          CALL      RSAEGRC1                * positional read
.
          DISPLAY   *P1:3,*EF,*V2LON,*P25:4,"ANATOMICAL SITE CODES":
                    *P1:5,*ULON,"Item",*P6:5,"Code":
                    *P11:5,"Description         ":
                    *P41:5,"Item",*P46:5,"Code":
                    *P51:5,"Description         "
.
. ---------------------------------------------------------------
. ------- next read on diagnosis file and position on link ------
. ---------------------------------------------------------------
.
HAERC100  CALL      RKAEGRC1
          BRANCH    OVRCD,HAERC500          * end of file
.
          MATCH     SP1,AERCFRGN            * limit display ?
          GOTO      HAERC200 IF EQUAL       * no - display all
.
          MATCH     AERCFRGN,AERCFLAG       * display this code ?
          GOTO      HAERC100 IF NOT EQUAL   * no - get next code
.
. ----------------------------------
. ------- increment counters -------
. ----------------------------------
.
HAERC200  ADD       ONE,CVERT               * inc line counter
.
          COMPARE   "23",CVERT
          GOTO      HAERC300 IF LESS        * will fit on the page
.
          ADD       "40",CCOL               * inc column counter
          MOVE      SIX,CVERT               * reset row position
.
          COMPARE   "80",CCOL  
          GOTO      HAERC400 IF NOT LESS    * need a new page
.
. -------------------------------------------
. ------- display all the information -------
. -------------------------------------------
.
HAERC300  ADD       ONE,AERCQCNT
          DISPLAY   *PCCOL:CVERT,*V2LON,SP1,AERCQCNT,*HOFF,DOT:
                                        SP1,AERCCODE,SP3,AERCDESC
          MOVE      AERCCODE,AERCQARR[AERCQCNT]
          GOTO      HAERC100
.
. ------- new page needed -------
.
HAERC400  BRANCH    CPAGENO,HAERC450        * first page
.
HAERC410  MOVE      ONE,AERCQLIN            * (N)ext, (F)irst
          CALL      HAERCK00
          BRANCH    EXIT,HAERC999,AAEGRCDS,HAERC700
.                        exit     first    next
          GOTO      HAERC600
.
HAERC450  MOVE      TWO,AERCQLIN            * (N)ext only
          CALL      HAERCK00
          BRANCH    EXIT,HAERC999,HAERC590,HAERC700
.                        exit     first    next
          GOTO      HAERC600
.
. ------- end of data -------
.
HAERC500  BRANCH    CPAGENO,HAERC550        * first page
.
HAERC510  MOVE      THREE,AERCQLIN          * (F)irst
          CALL      HAERCK00
          BRANCH    EXIT,HAERC999,AAEGRCDS,HAERC590
.                        exit     first    next
          GOTO      HAERC600
.
HAERC550  MOVE      FOUR,AERCQLIN           * no paging
          CALL      HAERCK00
          BRANCH    EXIT,HAERC999,HAERC590,HAERC590
.                        exit     first    next
          GOTO      HAERC600
.
HAERC590  BEEP
          BRANCH    AERCQLIN,HAERC410,HAERC450,HAERC510,HAERC550
.
. ------- item selected -------
.
HAERC600  MOVE      AERCQARR[FORM2],KEY2
          CALL      RDAEGRC1
          BRANCH    OVRCD,HAERC590
.
          MATCH     AERCFRGN,AERCFLAG            * valid flag for code selected?
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Not valid for this injury.  ";   * no
            CALL      HITENTER
            GOTO      AAEGRCDS
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      HAERC999
.
. ------- (N)ext screen selected -------
.
HAERC700  DISPLAY   *P1:6,*EF;
          MOVE      SIX,CVERT               * first display row 
          MOVE      ONE,CCOL                * get back to LHS of screen
          MOVE      ZERO,AERCQCNT           * reset count
          ADD       ONE,CPAGENO             * add to page count
          GOTO      HAERC300                * display the data
.
HAERC999  RETURN
+
.*********************************************************************
.         keyin 
.*********************************************************************
HAERCK00  DISPLAY   *P1:24,*EL,"Select item, (":
                    *V2LON,ANSE,*HOFF,")xit";
          MOVE      "19",CCOL
.
          IF        AERCQLIN = ONE | AERCQLIN = TWO
            DISPLAY   ", (",*V2LON,ANSN,*HOFF,")ext";
            ADD       EIGHT,CCOL
          ENDIF
          IF        AERCQLIN = ONE | AERCQLIN = THREE
            DISPLAY   ", (",*V2LON,ANSF,*HOFF,")irst";
            ADD       NINE,CCOL
          ENDIF
.
          DISPLAY   " : ";
          ADD       FOUR,CCOL
.
HAERCK10  KEYIN     *PCCOL:24,*V2LON,*JR,DIM2
          PACK      DIM2,DIM2,SP2
          TYPE      DIM2
          GOTO      HAERCK50 IF EQUAL       * number entered
.
          UNPACK    DIM2,ANS,ANS
          REP       UPPLOW,ANS
          MOVE      ZERO,EXIT
          REP       "E1F2N3",ANS
          MOVE      ANS,EXIT
          TYPE      ANS
          GOTO      HAERCK99 IF EQUAL       * valid number
.
HAERCK20  BEEP
          GOTO      HAERCK10
.
HAERCK50  MOVE      DIM2,FORM2
          MOVE      ZERO,EXIT
          COMPARE   FORM2,ZERO
          GOTO      HAERCK20 IF NOT LESS    * negative invalid
          COMPARE   FORM2,AERCQCNT
          GOTO      HAERCK20 IF LESS        * too high
.
HAERCK99  RETURN
+
