.***************************************************************************
.* System    :   Billing                                                   *
.* Program   :   F93BLCMN                                                  *
.* Desc      :   This program will copy existing admission comments from   *
.*           :   patcmntf to viscmntf with a type of 003 (billing)         *
.***************************************************************************
.* Date      :   23/02/2004                                                *
.* Author    :   Jill Habasque CAR 47709                                   *
.*               V9.04.01 18/10/04 J.Tan  CAR 55293                        *
.*               Fixed setting up line number (Billing Line number is DIM3 *
.*               where as Admission line number is only DIM2)              *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATCMNFD/INC
          INC       VISCMTFD/INC
.
RECNUM    FORM      8
ZERO3     INIT      "003"
. LOCAL VARIABLE DEFINITION
. -------------------------
.
PRGID     INIT      "F93BLCMN"
PRGNAM    INIT      "Copy admission comments to billing comments"
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PATCMNT1,"patcmntf"
.
          OPEN      VISCMTA1,"viscmtaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  MOVE      ZERO,RECNUM
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
.
          PACK      KEY10,SP70
          CALL      RDSCMNT1
PROC1000  CALL      RDKCMNT1
          BRANCH    OVRCD,PROC9999
.
          ADD       ONE,RECNUM
          IF        (RECNUM%100) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          PACK      VSCTLINE,SP1,DPCLNO,SP70
          PACK      KEY14,DPCADMN,ZERO3,VSCTLINE,SP70
          CALL      RDVSCMT1
          IF        OVRCD<>1
            GOTO      PROC1000
          ENDIF
.
          PACK      VSCTVIST,DPCADMN,SP70
          PACK      VSCTTYPE,ZERO3,SP70
          PACK      VSCTLINE,SP1,DPCLNO,SP70
          PACK      VSCTDATA,PCLINE,SP70,SP70
          CALL      WRVSCMT1
.
          GOTO      PROC1000
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       VISCMTIO/INC
          INC       PATCMNIO/INC
