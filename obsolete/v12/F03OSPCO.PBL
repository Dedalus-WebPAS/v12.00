.*****************************************************************************
.*    Program      : F03OSPCO                                                *
.*    Description  : Conversion obspcoaf to new File layout                  *
.*                                                                           *
.*    Author       : Mike Laming    CAR 249362                               *
.*    Date         : 25/11/2011                                              *
.*    Modifications:                                                         *
.*        V10.03.01  06/07/2012  Steve Armstrong CAR 249362                  *
.*                   Changed fixit so that c-isam and Oracle fix uses the    *
.*                   same routine as the only thing this fixit needs to do   *
.*                   is to populate the new field.                           *
.*        V10.03.00  Program created                                         *
.*****************************************************************************
.
          INC       STD001FD
.
OBSPCOA1   IFILE    SQL, FIXED=283, KEY="U1-8,9-16,17-20"
..OBSPCOA2   IFILE    SQL, FIXED=283, KEY="U9-16,21-28,17-20,1-8"
..OBSPCOA3   IFILE    SQL, FIXED=283, KEY="U21-28,1-8,9-16,17-20"
..OBSPCOA4   IFILE    SQL, FIXED=283, KEY="U29-31,21-28,1-8,9-16,17-20"
..OBSPCOA5   IFILE    SQL, FIXED=283, KEY="U1-8,21-28,9-16,17-20"
..OBSPCOA6   IFILE    SQL, FIXED=283, KEY="U1-8,253-254,255-257,9-16,17-20"

.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
OBPCURNO  DIM       8      8        1     U/R Number
OBPCCVIS  DIM       8      8        9     Visit Number
OBPCCPID  DIM       4      4        17    Correspondance ID
OBPCINDT  DIM       8      8        21    Input Date
OBPCCTYP  DIM       3      3        29    Type of Correspondance
OBPCINTM  DIM       8      8        32    Time hh:mm:ss
OBPCINUS  DIM       10     10       40    Input Web User ID
OBPCFEXT  DIM       6      6        50    File Extension
OBPCMDEL  DIM       1      1        56    Marked as Deleted
OBPCSLID  DIM       4      4        57    Storage Location ID
OBPCSLEV  DIM       2      2        61    Security Level
OBPCCOM1  DIM       40     40       63    Comment 1
OBPCCOM2  DIM       40     40       103   Comment 2
OBPCCFRM  DIM       30     30       143   From
OBPCCTOO  DIM       30     30       173   To
OBPCUDF1  DIM       3      3        203   User Def Code 1 (Cat xx)
OBPCUDF2  DIM       3      3        206   User Def Code 2 (Cat xx)
OBPCUYN1  DIM       1      1        209   User Def Y/N 1
OBPCUYN2  DIM       1      1        210   User Def Y/N 2
OBPCUDD1  DIM       8      8        211   User Def Date 1
OBPCUDD2  DIM       8      8        219   User Def Date 2
OBPCDTLU  DIM       8      8        227   Date Last Update
OBPCTMLU  DIM       8      8        235   Time Last Update
OBPCLUID  DIM       10     10       243   Last Web User ID
OBPCACAT  DIM       2      2        253   Alert File Category (H1-H9)
OBPCACOD  DIM       3      3        255   Alert File Code (for category)
OBPCCNTR  DIM       3      3        258   Alert File Counter
OBPCSPAR  DIM       22     22       261   Spare
.
.End of Record                      283
.
. LOCAL VARIABLES
. ---------------
RECNUM    FORM      8
.
.
PRGNAM    INIT      "CONVERSION OBSPCOFD"
PRGID     INIT      "F03OSPCO"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                             MAIN0000                                      *
.*                         Program Mainline                                  *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
MAIN0500  CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000              * run fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN5000:               * yes
                          MAIN0500:               * no
                          MAIN9999                * cancel
.
.         Running fix
.
MAIN5000  CALL      READ0000                      * run fix
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
.
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening ":
                    *P64:24,"obspcoaf"
          OPEN      OBSPCOA1,"obspcoaf"
          
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:6,*EL,"Select Option : ":
                    *P17:6,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
.
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          MOVE      ZERO,RECNUM                   * initialise record count
.
          PACK      KEY20,SP70 
          CALL      RSOBPC1                       * position at start of file
READ0500  CALL      RKOBPC1                       * read next record
          BRANCH    OVRCD,READ6000                * eof - finished
.
          ADD       ONE,RECNUM                    * update record counter
.
          IF        (RECNUM%1000) = 0 | RECNUM = 1
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MOVE      "  1",OBPCCNTR                * load counter
          CALL      UPOBPC1                       * update record
.
          GOTO      READ0500                      * get next record
.
READ6000  CLOSE     OBSPCOA1                      * close new file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P15:20,*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
READ9999  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
RSOBPC1   RESET     KEY20     
          READ      OBSPCOA1,KEY20;;                                      
          RETURN        
.
RKOBPC1   MOVE      ZERO,OVRCD
          READKS    OBSPCOA1;OBPCURNO,OBPCCVIS,OBPCCPID,OBPCINDT,OBPCCTYP:
                        OBPCINTM,OBPCINUS,OBPCFEXT,OBPCMDEL,OBPCSLID,OBPCSLEV:
                        OBPCCOM1,OBPCCOM2,OBPCCFRM,OBPCCTOO,OBPCUDF1,OBPCUDF2:
                        OBPCUYN1,OBPCUYN2,OBPCUDD1,OBPCUDD2,OBPCDTLU,OBPCTMLU:
                        OBPCLUID,OBPCACAT,OBPCACOD,OBPCCNTR,OBPCSPAR
          GOTO      OVERCOND IF OVER
          RETURN
.
UPOBPC1
          UPDATE    OBSPCOA1;OBPCURNO,OBPCCVIS,OBPCCPID,OBPCINDT,OBPCCTYP:
                        OBPCINTM,OBPCINUS,OBPCFEXT,OBPCMDEL,OBPCSLID,OBPCSLEV:
                        OBPCCOM1,OBPCCOM2,OBPCCFRM,OBPCCTOO,OBPCUDF1,OBPCUDF2:
                        OBPCUYN1,OBPCUYN2,OBPCUDD1,OBPCUDD2,OBPCDTLU,OBPCTMLU:
                        OBPCLUID,OBPCACAT,OBPCACOD,OBPCCNTR,OBPCSPAR
          GOTO      UPDFLOCK IF LOCKED
          RETURN
.
          INC       STD001IO
