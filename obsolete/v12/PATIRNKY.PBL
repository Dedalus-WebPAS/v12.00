.******************************************************************************
.*  Include Name  : PATIRNKY.PBL                                              *
.*  Description   : Keyin of invoice range codes                              *
.*  Author        : Ian Rutt                                                  *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF3 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMODE - is keyin mode                                  *
.*                                                                            *
.*  Returns       : IRANGE    - Keyed in code                                 *
.*                  IRDESC    - Description                                   *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.******************************************************************************
PATIRNKY
PTIRKY00  MATCH     SP3,CKYIDEF3                  * using a default ?
          GOTO      PTIRKY10 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN3:      * no default used
                    *PECOL:EVERT,*V2LON,IRANGE:
                    *PECOL:EVERT,*DV,IRANGE
          GOTO      PTIRKY20
.
PTIRKY10  MOVE      CKYIDEF3,IRANGE                 * default
          KEYIN     *PECOL:EVERT,*DV,IRANGE:     * default used
                    *PECOL:EVERT,*V2LON,*RV,IRANGE:
                    *PECOL:EVERT,*DV,IRANGE
.
.         check what was entered
.
PTIRKY20  CALL      IRNCK000
          BRANCH    EXIT,PTIRKY25,PTIRKY31,PTIRKY41,PTIRKY95
.                        "?"      valid    nothing  exitchar
          GOTO      PTIRKY00
.
.         a "?" was input
.
PTIRKY25  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
PTIRKY26  CALL      PATIRNDS
          BRANCH    CNEWDS,PTIRKY27 
.
          CALL      PUTSCR00                * redisplay screen
          GOTO      PTIRKY00                * rekey code
.
.         now ask for code while leaving displayed codes on screen
.
PTIRKY27  MATCH     SP3,CKYIDEF3                  * using a default
          GOTO      PTIRKY28 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Invoice Range Code : ":
                    *P24:24,*DV,UNDLN3:      * no default used
                    *P24:24,*V2LON,IRANGE:
                    *P24:24,*DV,IRANGE
          GOTO      PTIRKY29
.
PTIRKY28  MOVE      CKYIDEF3,IRANGE
          KEYIN     *P1:24,*EL,*DV,"Invoice Range Code : ":
                    *P24:24,*DV,IRANGE:     * default used
                    *P24:24,*V2LON,*RV,IRANGE:
                    *P24:24,*DV,IRANGE
.
.         repeat the validation of the code
.         check what was entered
.
PTIRKY29  CALL      IRNCK000
          BRANCH    EXIT,PTIRKY26,PTIRKY30,PTIRKY40,PTIRKY90
.                        "?"      valid    nothing  exitchar
.
          GOTO      PTIRKY27
.
.         a valid code was entered 
. 
PTIRKY30  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,IRANGE
.
PTIRKY31  MOVE      FALSE,EXIT                    * valid input
          GOTO      PTIRKY99 
.
.         nothing was input
.
PTIRKY40  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,IRANGE
.
PTIRKY41  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      PTIRKY99 
.
.         EXITCHAR input
.
PTIRKY90  CALL      FRESCR00                * free the screen saved
.
PTIRKY95  MOVE      TWO,EXIT                * EXITCHAR
.
PTIRKY99  RETURN
+
.*********************************************************************
.*                  IRNCK000                    Called by : PATIRNKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
IRNCK000  ENDSET    IRANGE
          APPEND    SP3,IRANGE
          RESET     IRANGE
.
          MATCH     EXITCHAR,IRANGE
          GOTO      IRNCK600 IF EQUAL       * exit char
.
          MATCH     SP3,IRANGE
          GOTO      IRNCK700 IF EQUAL       * nothing
.
          MATCH     QUEST,IRANGE
          GOTO      IRNCK800 IF EQUAL       * ? mark
.
.         a code was entered so validate
. 
IRNCK500  PACK      KEY3,IRANGE,SP3
          CALL      RDIRNG1
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,IRNCK900          * invalid code
          ELSE
            COMPARE   ZERO,OVRCD   
            GOTO      IRNCK900 IF EQUAL
          ENDIF  
.
          MOVE      TWO,EXIT                * valid code
          GOTO      IRNCK999
.
.         exitchar entered
.
IRNCK600  MOVE      FOUR,EXIT
          MOVE      SP3,IRANGE
          PACK      IRDESC,SP20,SP20        * clear desc field
          GOTO      IRNCK999
.
.         nothing entered
.
IRNCK700  BRANCH    CKYIMAND,IRNCK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP3,IRANGE
          PACK      IRDESC,SP20,SP20        * clear desc field
          GOTO      IRNCK999
.
.         "?" entered
.
IRNCK800  MOVE      ONE,EXIT
          GOTO      IRNCK999
.
.         invalid code entered
.
IRNCK900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
.
          IF        CKYIMODE = 0
            DISPLAY   *P1:24,*EL,*B,"Invalid Invoice Range Code. ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      IRNCK999
.
.         field is mandatory
.
IRNCK950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is Mandatory. ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
IRNCK999  RETURN
.
          INCLUDE   PATIRNDS
+
