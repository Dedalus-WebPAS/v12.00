.******************************************************************************
.                                  OPRSESDS
.                          display session details
.
. Description : Display date, start and endtime and time booked in session
.               to current date.  This is BULLSHIT free code : This routine
.               has no dead code or poor programming styles.
.
. Requirements: This routine requires the following includes OPRSESFD,
.               OPRSESIO OPRDEAFD OPRDEAIO these are the session and 
.               operating details.
.               KEY30 KEY19 SESNCODE (clinic or surgcode code) must be 
.               defined.
.               KEY8 must be todays date in ccyymmdd order or starting date.
.
.               This routine affects CVERT and CCOL also FORM1 and ANS
.
. Parameters  : SESNCODE  DIM       6         Clinic/Surgeon code
.               KEY8      DIM       8         Starting date for scan (CCYYMMDD)
.
.             OPRSESDS was programed by Bill Dew on the 20/03/1990
.                        I.B.A. Development Department
.                             D. Jones supervisor
.
.         Mods:
.         V11.03.01 06/03/2023  Ebon Clements     TSK 0909393
.                   Added hospital to oprsesaf(1-6) and oprdetfa(1&5) indexes
.         V5.01.02  10/12/90 Graeme Williams
.                   Changed the include to use the 2nd index of the
.                   session file instead of the 1st (the 2nd index
.                   is more efficient).
.         V5.01.03  27/05/1991    Justin Coates  SRF 108534
.                   Changed so won't display cancelled sessions
.
.******************************************************************************
.
.         OPRDATDS : Show all sessions from KEY8 onwards. Highlight the
.                    dates.
.
OPRDATDS  MOVE       ONE,FORM1
          GOTO       OPRSESDS
.
.         OPRTIMDS : Show all sessions on the date KEY8. Highlight the times.
.
OPRTIMDS  MOVE       ZERO,FORM1
.
.         Display headings
.
OPRSESDS  DISPLAY    *P1:4,*EF,*P32:4,*V2LON,*ULON,"SESSION DETAILS":
                     *P1:6,*ULON,"  Date    ",*P13:6,"Start",*P20:6," End ":
                     *P29:6,"Booked":
                     *P41:6,*ULON,"  Date    ",*P53:6,"Start",*P60:6," End ":
                     *P69:6,"Booked"
          MOVE       SEVEN,CVERT
          MOVE       ONE,CCOL
.
.         KEY8 contains the starting date for the scan.
.
          PACK       KEY22,SESNHOSP,SESNCODE,KEY8,SP30
          CALL       RSOPSES2
.
SESDS222  CALL       RKOPSES2
          BRANCH     OVRCD,SESDS999
.
          MATCH      SESNHOSP,OPSEHOSP
          GOTO       SESDS999 IF NOT EQUAL  * different hospital
.
          MATCH      SESNCODE,OPSECLIN
          GOTO       SESDS999 IF NOT EQUAL  * different clinic
.
          COMPARE    ZERO,OPSESTAT
          GOTO       SESDS222 IF EQUAL      * cancelled session
.
.         For a time scan, we only want the requested date
.
          BRANCH     FORM1,SESDS444    * branch on ? type date or time
.
          MATCH      OPSEDATE,KEY8
          GOTO       SESDS999 IF LESS       * different date
.
.          Display this session
.
SESDS444  UNPACK     OPSEDATE,CCENT,CYEAR,CMON,CDAY
          CALL       PACDATE            * format the date
.
.         Check the whether the date or time is to be highlighted
.
          BRANCH     FORM1,SESDS666         * highlight date
.
.         display time in highlight
.
          DISPLAY    *PCCOL:CVERT,CPCDATE,SP2:
                     *V2LON,OPSETIME,*HOFF,SP2,OPSEENDT,SP4,OPSETIMB
          GOTO       SESDS777
.
.         display date in highlight
.
SESDS666  DISPLAY    *PCCOL:CVERT,*V2LON,CPCDATE,*HOFF,SP2:
                     OPSETIME,SP2,OPSEENDT,SP4,OPSETIMB
.
SESDS777  ADD        ONE,CVERT
          COMPARE    TWENTY3,CVERT
          GOTO       SESDS222 IF LESS       * keep displaying data
.
          MOVE       SEVEN,CVERT
          ADD        "40",CCOL              * go to next column
.
          COMPARE    "70",CCOL
          GOTO       SESDS222 IF LESS       * keep displaying data
.
.         New standards question
.
          KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, ":
                    "(",*V2LON,"F",*HOFF,")irst Screen, ":
                    "(",*V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
          REP       UPPLOW,ANS
          CMATCH    ANSC,ANS
          GOTO      SESDS999 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      OPRSESDS IF EQUAL
.
.         Clear the screen
.
          MOVE      SEVEN,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:CVERT,*EF
          GOTO      SESDS222
.
SESDS999  RETURN
................................................................................
