.*****************************************************************************
.* System    :   Patient Management                                          *
.* Program   :   F0227986                                                    *
.* Desc      :   Fixit to update Eclipse receipt type                        *
.*****************************************************************************
.* Date      :   27/09/2010                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   This program will loop through rcpdteaf looking for Eclipse *
.*               related records.  Where a match is found, the program will  *
.*               make sure that the corresponding rcpbnkaf record has a      *
.*               "Received from type" of "Health Fund" instead of "Health    *
.*               Fund Group".                                                *
.* Mods      :                                                               *
.*        V10.00.01 06/10/2010  Steve Armstrong   CAR 227986                 *
.*                  Mods to also update rcpdteaf.rcdtrcod.                   *
.*****************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PMSECTFD/INC
          INC       RCPBNKFD/INC
          INC       RCPDTEFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      10
INVNUMBR  DIM       8
UPDTFLAG  FORM      1             * Update flag
.                                     1 = No update required
.                                     0 = Update Required
.
.
PRGID     INIT      "F0227986"
PRGNAM    INIT      "Fixit for Eclipse Receipt Type"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*                                INIT0000             Called by: MAIN0000   *
.*                             initialisation                                *
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"rcpdetaf";
          OPEN      RCPDTEA1,"rcpdteaf"
.
          DISPLAY   *P64:24,"rcpbnkaf";
          OPEN      RCPBNKA1,"rcpbnkaf"
.
          DISPLAY   *P64:24,"pmsectaf";
          OPEN      PMSECTA2,"pmsectaf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option number selected                             *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.*                               PROC0000              Called by: MAIN0000   *
.*       Fix rcpbnkaf.rcbnttyp to be "1" instead of "4" for Eclipse records  *
.*****************************************************************************
.
PROC0000  MOVE      ZERO,COUNTER                 * initialise record counter
.
.         Loop through all rcpdteaf records
.
          MOVE      SP20,KEY15
          CALL      RSRCDTE1                     * position at start of file
PROC0500  CALL      RKRCDTE1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
.         Check if this is an Eclipse receipt (batch number is not blank)
.
          MATCH     SP8,RCDTBATN                 * blank batch number ?
          GOTO      PROC0500 IF EQUAL            * yes - ignore record
.
.         We have an Eclipse receipt, so check the corresponding receipt
.         header file.
.
          MOVE      RCDTRECN,KEY12
          CALL      RDRCBNK1                     * bank header record found ?
          BRANCH    OVRCD,PROC0500               * no - ignore record
.
          MOVE      ONE,UPDTFLAG                 * initialise update flag
.
.         If the "Received from type" is set to "Health Fund Group" (4),
.         than update it to "Health Fund" (1).
.
          MATCH     "4",RCBNTTYP                 * health fund group ?
          IF        @EQUAL
            MOVE      "1",RCBNTTYP               * set for health fund
            MOVE      ZERO,UPDTFLAG              * set flag for update
          ENDIF
.
.         Get the HF for the corresponding pmsectaf and use this
.         to populate rcpbnkaf.rcbnrcod instead of the Eclipse participant
.         code
.
          UNPACK    RCDTINVN,ANS,ANS,ANS,ANS,INVNUMBR
          MATCH     SP8,INVNUMBR                 * invoice number blank ?
          GOTO      PROC5000 IF EQUAL            * yes - finished with record
.
          MATCH     SP8,RCDTSPAR                 * batch number blank ?
          GOTO      PROC5000 IF EQUAL            * yes - finished with record
.
          PACK      KEY16,INVNUMBR,RCDTSPAR
          CALL      RDPMECT2
          IF        OVRCD = 0
            MOVE      PMECHFND,RCBNRCOD          * load health fund code
            MOVE      ZERO,UPDTFLAG              * set flag for update
          ENDIF
.
PROC5000  BRANCH    UPDTFLAG,PROC0500            * no update required
.
          CALL      UPRCBNK1                     * update record
          ADD       ONE,COUNTER                  * increment counter
.
          GOTO      PROC0500                     * get next record
.
PROC9000  DISPLAY   *P1:24,*EL,*V2LON,COUNTER,*HOFF," records updated.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PMSECTIO/INC
          INC       RCPBNKIO/INC
          INC       RCPDTEIO/INC
