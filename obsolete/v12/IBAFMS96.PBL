.**********************************************************************
.*                                                                    *
.*  Program   :  IBAFMSxx                                             *
.*                                                                    *
.*  Name      :  Transaction Balancing                                *
.*                                                                    *
.*  Author    :  B.G.Ackland 12.08.91                                 *
.*                                                                    *
.*  Mod's     :                                                       *
.**********************************************************************
.* V9.02.00  03.05.2002 Glenn Saunders                                *
.*           Export to new release i.e. v9.02 from vf.09              *
.**********************************************************************
.
          INC       FMSSTDDF
          INC       FMSAMAFD/INC
          INC       FMSCTRFD/INC
          INC       FMSTRNFD/INC
.
PRGID     INIT      "IBAFMS96"
PRGNAM    INIT      "Transaction Balancing"
VERSION   INIT      "V12.00.00"
.
.**********************************************************************
.*                   Data variables required                          *
.**********************************************************************
.
LOCYEAR   DIM       4
BATCH     DIM       5
CASH      FORM      1
FMSR      INIT      "fmsr"
FMST      INIT      "fmst"
DTOT      FORM      12.2
CTOT      FORM      12.2
FILENAME  DIM       8
BATDATE   DIM       8
.
          CALL      DISPHEAD
MAIN1000  KEYIN     *P1:4,"Financial Year   : ",LOCYEAR
          KEYIN     *P1:5,"From Batch       : ",*JR,*ZF,*DE,BATCH
          KEYIN     *P1:6,"Enter 1 for Cash : ",CASH
          DISPLAY   *P1:4,"Financial Year   : ",*V2LON,LOCYEAR
          DISPLAY   *P1:5,"From Batch       : ",*V2LON,BATCH
          DISPLAY   *P1:6,"Enter 1 for Cash : ",*V2LON,CASH
          CALL      CONTQST
          BRANCH    CEXIT,MAIN2000,MAIN1000,MAIN9999
MAIN2000  BRANCH    CASH,MAIN2500
          PACK      FILENAME,FMST,LOCYEAR
          GOTO      MAIN3000
MAIN2500  PACK      FILENAME,FMSR,LOCYEAR
MAIN3000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FMSTRNA1,FILENAME
          TRAPCLR   IO
          BRANCH    OVRCD,MAIN9000
          OPEN      FMSAMAA1,"fmsamaaf"
          MOVE      ZERO,DTOT
          MOVE      ZERO,CTOT
.
          PACK      KEY10,BATCH,SP70
          CALL      RSFMTR1
          CALL      RKFMTR1
          BRANCH    OVRCD,MAIN5000
          MOVE      FMTRPDAT,BATDATE
.
          PACK      KEY10,BATCH,SP70
          CALL      RSFMTR1
MAIN4000  CALL      RKFMTR1
          BRANCH    OVRCD,MAIN5000
          MATCH     BATCH,FMTRBAT
          GOTO      MAIN4200 IF NOT EQUAL
.
MAIN4100  CALL      FAMT0000
          CALL      CCODE000
          MATCH     FMTRPDAT,BATDATE
          GOTO      MAIN4000 IF EQUAL
          DISPLAY   *P1:24,*EL,"Date Mismatch : ",FMTRBAT,"/",FMTRUNIQ:
                    "  - ",FMTRPDAT
          GOTO      MAIN4000
.
MAIN4200  COMPARE   DTOT,CTOT
          GOTO      MAIN4900 IF EQUAL
          DISPLAY   *P1:24,"Batch Unbalanced - ",BATCH,DTOT,CTOT," - ";
          CALL      HITENTER
.
MAIN4900  MOVE      ZERO,DTOT
          MOVE      ZERO,CTOT
          MOVE      FMTRBAT,BATCH
          MOVE      FMTRPDAT,BATDATE
          GOTO      MAIN4100
.
MAIN5000  DISPLAY   *P1:24,"Complete - ";
          CALL      HITENTER
          GOTO      MAIN9999
MAIN9000  DISPLAY   *P1:24,"File not Found - ",FILENAME," - ";
          CALL      HITENTER

MAIN9999  CHAIN     PGM
.
.**********************************************************************
FAMT0000  MATCH     "JC",FMTRTYPE
          GOTO      FAMT1000 IF EQUAL
.
          MATCH     "CC",FMTRTYPE
          GOTO      FAMT1000 IF EQUAL
.
          MATCH     "CS",FMTRTYPE
          GOTO      FAMT1000 IF EQUAL
.
          MATCH     "CR",FMTRTYPE
          GOTO      FAMT1000 IF EQUAL
.
          MATCH     "RF",FMTRTYPE
          GOTO      FAMT1000 IF EQUAL
.
          MATCH     "RI",FMTRTYPE
          GOTO      FAMT1000 IF EQUAL
.
          MATCH     "IN",FMTRTYPE
          GOTO      FAMT0500 IF EQUAL
.
          MATCH     "PY",FMTRTYPE
          GOTO      FAMT0500 IF EQUAL
.
          MATCH     "JA",FMTRTYPE
          GOTO      FAMT0500 IF EQUAL
          DISPLAY   *P1:24,*EL,"Strange Type : ",FMTRBAT,"/",FMTRUNIQ,FMTRTYPE;
          CALL      HITENTER
.
FAMT0500  ADD       FMTRAMT,DTOT
          GOTO      FAMT9999
.
FAMT1000  ADD       FMTRAMT,CTOT
.
FAMT9999  RETURN
.
CCODE000  PACK      KEY14,FMTRLEDG,FMTRACCT
          CALL      RDFMAM1
          BRANCH    OVRCD,CCODE100
          TYPE      FMAMTYPE
          GOTO      CCODE050 IF NOT EQUAL
          MOVE      FMAMTYPE,F1
          BRANCH    F1,CCODE999,CCODE999,CCODE999,CCODE999,CCODE999 
CCODE050  DISPLAY   *P1:23,"Account Type Invalid - ",KEY14,FMTRAMT," - ":
                    *P1:24,"Batch/Line           - ",FMTRBAT,"/",FMTRUNIQ," - ";
          CALL      HITENTER
          GOTO      CCODE999
CCODE100  DISPLAY   *P1:23,"Account Code Missing - ",KEY14,FMTRAMT," - ":
                    *P1:24,"Batch/Line           - ",FMTRBAT,"/",FMTRUNIQ," - ";
          CALL      HITENTER
CCODE999  RETURN
.
          INC       FMSSTDCD
          INC       FMSCTRIO/INC
          INC       FMSAMAIO/INC
          INC       FMSTRNIO/INC
.
