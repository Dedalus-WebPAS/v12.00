. **********************************************************************
. * System    :   Accounting System                                    *
. * Program   :   IBAFMS67                                             *
. * Desc      :   Budget Link Master Listing                           *
. **********************************************************************
. * Date      :   31.05.1991                                           *
. * Author    :   Neeriem Dye                                          *
. * Mods      :                                                        *
. **********************************************************************
. * V9.02.00  03.05.2002 Glenn Saunders                                *
. *           Export to new release i.e. v9.02 from vf.09              *
. **********************************************************************
.$$$$$$
.        PROGRAM IBAFMS67
.             open files and initialise variables
.             WHILE user wants to change data DO
.                  get desired option
.                  allow user to modify option
.             END WHILE
.             IF user wants to print data
.                  THEN print data
.             END IF
.        END
.$$$$$$
. Standard FMS Accounting System Include
.---------------------------------------
          INC       FMSSTDDF
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
.
          INC       FMSAMAFD/INC
          INC       FMSBTYFD/INC
          INC       FMSLBUFD/INC
          INC       FMSTCFFD/INC
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
.
ADDT      INIT      "   "    * add to values
ADDT0     INIT      "Add"
ADDT1     INIT      "Sub"
ADDT2     INIT      "Mul"
ADDT3     INIT      "Div"
.
FMSL      INIT      "fmsl"   * file variables
FILENAME  INIT      "        "
.
ACCEPT    FORM      1        * accept mode (1=on)
OPTIONA   FORM      2        * screen A selection keyin var
.
PRGID     INIT      "IBAFMS67"
PRGNAM    INIT      "Budget Link Master Listing"
VERSION   INIT      "V12.00.00"
.
.******************************************************************************
.   MAINLINE - Controlling Logic
.******************************************************************************
ML0000    CALL      INIT0000           * display heading and open files
.
ML0100    CALL      SELA0000           * perform screen A
          BRANCH    EXIT,ML9999        * exit program
.
          CALL      PRNT0000           * print data
          GOTO      ML0100             * get next selection
.
ML9999    CHAIN     PGM                * chain back to menu
.
.******************************************************************************
.  INIT - Open Files                             Called by ML
.******************************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ONE,CCENTRY
          MOVE      ONE,CDEFDTE
          MOVE      ZERO,CHIGHLT
.
          DISPLAY   *P64:24,"fmsbtyaf";  * budget type
          OPEN      FMSBTYA1,"fmsbtyaf"
.
          DISPLAY   *P64:24,"fmsamaaf";  * account
          OPEN      FMSAMAA1,"fmsamaaf"
.
          DISPLAY   *P64:24,"fmstcfaf";  * total calc
          OPEN      FMSTCFA1,"fmstcfaf"
.
INIT9999  DISPLAY   *P1:24,*EF;
          RETURN
.******************************************************************************
.  SCRA - Display Screen A                       Called by SELA, redisps
.******************************************************************************
SCRA0000  DISPLAY   *P1:3,*EF,*V2LON:
                    *P1:4," 1",*HOFF,". Budget Type          : ";
.
SCRA9999  RETURN
.******************************************************************************
.  SELA - Perform Screen A                       Called by ML
.         Returns : EXIT     (1=exit program)
.******************************************************************************
SELA0000  UNPACK    SP70,ANS
          MOVE      ONE,ACCEPT         * in accept mode
          MOVE      ZERO,OPTIONA       * reset item desired
          CALL      SCRA0000           * display screen A
.
SELA0100  ADD       ONE,OPTIONA        * get next item
          BRANCH    ACCEPT,SELA0110    * in accept mode ?
          CALL      MQST0000           * get option if not in accept mode
          MOVE      CCITEM,OPTIONA
.
SELA0110  MOVE      "28",CCOL          * get screen address
          MOVE      "3",CVERT
          ADD       OPTIONA,CVERT
          MOVE      OPTIONA,F2         * adjust selection for quit/continue
          ADD       TWO,F2
          BRANCH    F2,SELA9500,SELA9000:                       * quit, continue
                       SELA1100                                 * execute option
          BRANCH    ACCEPT,SELA0500    * at end of accept mode ?
          BEEP                         * illegal option
          GOTO      SELA0100           * get another option
.
SELA0500  MOVE      ZERO,ACCEPT        * clear accept mode
          GOTO      SELA0100
.
.---- option 1 ----
.
SELA1100  CALL      KBTYFM00           * get budget code
          BRANCH    EXIT,SELA1190,SELA9500
          DISPLAY   *PCCOL:CVERT,*V2LON,FMBTCODE,*HOFF,SP10,FMBTDESC;
          GOTO      SELA0100
.
SELA1190  BRANCH    ACCEPT,SELA9500
          GOTO      SELA1100
.
.
SELA9000  MOVE      ZERO,EXIT          * continue
          GOTO      SELA9999
.
SELA9500  MOVE      ONE,EXIT           * quit
.
SELA9999  RETURN
.**********************************************************************
.  MQST - select, post, cancel, run report              Called By lots
.        Returns  - CCITEM   (0=run report, -1=exit, other=select
.**********************************************************************
MQST0000  MOVE      SP10,CCITEMD
          KEYIN     *P1:24,*EF,"Select Item, (",*V2LON:
                    "R",*HOFF,")un Report, e(",*V2LON:
                    "X",*HOFF,")it ? ",*V2LON,*JR,CCITEMD;
.
          RESET     CCITEMD
          GOTO      MQST0000 IF EOS    * nothing entered ?
          REP       UPPLOW,CCITEMD
.
          MOVE      " 0",CCITEM
          MATCH     "  R",CCITEMD      * post ?
          GOTO      MQST9999 IF EQUAL
.
          MOVE      "-1",CCITEM
          MATCH     "  X",CCITEMD      * cancel ?
          GOTO      MQST9999 IF EQUAL
.
          MOVE      ZERO,CCITEM
          MOVE      CCITEMD,CCITEM
          COMPARE   ONE,CCITEM         * select item ?
          GOTO      MQST9999 IF NOT LESS
.
          BEEP
          GOTO      MQST0000
.
MQST9999  RETURN
.********************************************************************
. PRNT - print report                               Called by ML
.********************************************************************
PRNT0000  DISPLAY   *P50:24,"Printing :   ";
          MOVE      ZERO,CPAGENO                 * reset page no.
          CLOCK     TIME,CTIMEIS                 * get current time
          MOVE      ZERO,CNOUNDLN                * print line at end of page
          MOVE      "999",CLNO                   * init last line number
.
          CALL      OPEN0000                     * open file
          BRANCH    OVRCD,PRNT9000               * file no found ?
.
          CALL      FILE0000                    * go to correct position in file
.
PRNT1000  CALL      NREC0000                     * get next record
          BRANCH    OVRCD,PRNT9000               * end of file ?
.
          DISPLAY   *P61:24,*V2LON,FMBLTLED,SLASH,FMBLTACC,*EL;
          COMPARE   "55",CLNO                    * end of page ?
          CALL      HEAD0000 IF NOT LESS         * start new page
.
          CALL      WRIT0000                     * print details
          GOTO      PRNT1000                     * get next record
.
PRNT9000  COMPARE   ZERO,CPAGENO
          CALL      HEAD0000 IF EQUAL          * print header if nothing printed
          COMPARE   ZERO,CNOUNDLN
          CALL      UND132 IF EQUAL              * print line if required
          PRINT     "****   End of Report   ****"
.
PRNT9999  RETURN
.**********************************************************************
.  OPEN - open file                                    Called By PRNT
.         Returns - OVRCD    (1=file not found)
.**********************************************************************
OPEN0000  PACK      FILENAME,FMSL,FMBTCODE,SP70   * get desired filename
          REP       " 0",FILENAME
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO               * look for file opening error
          OPEN      FMSLBUA1,FILENAME            * open file (if possible)
          TRAPCLR   IO
.
OPEN9999  RETURN
.**********************************************************************
.  FILE - Set file pointer                             Called By PRNT
.**********************************************************************
FILE0000  
.
.---- print by type 1 ----
.
FILE0100  PACK      KEY14,SP70
          CALL      RSFMBL1
          GOTO      FILE9999
.
FILE9999  RETURN
.**********************************************************************
.  NREC - Get next record                              Called By PRNT
.**********************************************************************
NREC0000  
.
.---- print by type 1 ----
.
NREC0100  CALL      RKFMBL1
          BRANCH    OVRCD,NREC9500
.
          GOTO      NREC9000
.
NREC9000  MOVE      ZERO,OVRCD         * continue
          GOTO      NREC9999
.
NREC9500  MOVE      ONE,OVRCD          * quit
.
NREC9999  RETURN
.********************************************************************
. HEAD - print report                               Called by PRNT
.********************************************************************
HEAD0000  CALL      HEAD132                      * print heading
.
          PRINT     *40,"Report for Budget Type : ",FMBTCODE,SP2,FMBTDESC,*N
          ADD       "2",CLNO
          CALL      UND132                       * print line
          PRINT     "| Ledger/Account",*60,"Linked to Ledger/Account":
                    *132,"|"
          ADD       "1",CLNO
          CALL      UND132                       * print line
.
HEAD9999  RETURN
.********************************************************************
. WRIT - print data line                            Called by PRNT
.********************************************************************
WRIT0000  MOVE      SP70,FMAMDESC
          PACK      KEY14,FMBLTLED,FMBLTACC,SP70
          CALL      RDFMAM1
          PRINT     "|",FMBLTLED,SLASH,FMBLTACC,SP2,FMAMDESC;
.
          MOVE      SP70,FMAMDESC
          PACK      KEY14,FMBLFLED,FMBLFACC,SP70
          CALL      RDFMAM1
.
          COMPARE   "50",CLNO                    * end of page ?
          CALL      HEAD0000 IF NOT LESS         * start new page
          PRINT         *60,FMBLFLED,SLASH,FMBLFACC,SP2,FMAMDESC,*132,"|",*N:
                    "|",*60,"Calculated From",*132,"|",*N:
                    "|",*60,"---------------",*132,"|"
          ADD       "3",CLNO                     * add to line number
.
          PACK      KEY33,FMBLFLED,FMBLFACC,SP70
          CALL      RSFMTC1
.
WRIT1000  CALL      RKFMTC1
          BRANCH    OVRCD,WRIT2000
          PACK      KEY14,FMTCTLED,FMTCTACC,SP70
          MATCH     KEY14,KEY33
          GOTO      WRIT2000 IF NOT EQUAL
.
          MOVE      ZERO,CNOUNDLN
          MOVE      SP70,FMAMDESC
          PACK      KEY14,FMTCSLED,FMTCSACC,SP70
          CALL      RDFMAM1
          MOVE      ZERO,F1
          MOVE      FMTCADDT,F1
          MOVE      ADDT0,ADDT
          LOAD      ADDT,F1,ADDT1,ADDT2,ADDT3
          COMPARE   "55",CLNO                    * end of page ?
          CALL      HEAD0000 IF NOT LESS         * start new page
          PRINT     "|",*54,ADDT:
                        *60,FMTCSLED,SLASH,FMTCSACC,SP2,FMAMDESC,*132,"|"
          ADD       "1",CLNO                     * add to line number
          GOTO      WRIT1000
.
WRIT2000  CALL      UND132                       * print line
          MOVE      ONE,CNOUNDLN
.
WRIT9999  RETURN
.******************************************************************************
.  Redisplays
.******************************************************************************
RBTYFM00  CALL      SCRA0000
.
RZZZFM00  RETURN
.******************************************************************************
.  INCLUDES FOR I/O'S
.******************************************************************************
.
          INC       FMSSTDCD           * FMS Acc. standard routines
.
          INC       FMSAMAIO/INC
          INC       FMSBTYIO/INC
          INC       FMSLBUIO/INC
          INC       FMSTCFIO/INC
          INC       FMSBTYKY
