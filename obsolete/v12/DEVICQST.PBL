.*****************************************************************************
.*    Program      : DEVICQST                                                *
.*    Description  : Question mark routine for Device codes                  *
.*                                                                           *
.*    Author       : Chuck                                                   *
.*    Date         : 28/04/92                                                *
.*                                                                           *
.*    Parameters   : CKYIDEF3 - Default Priority  (Blank if no default)      *
.*                                                                           *
.*    Returns      : KDEVICE    - Device code                                *
.*                   TDESC    - Priority description                         *
.*                   EXIT = 0 - Code entered                                 *
.*                          1 - Abort (EXITCHAR)                             *
.*                                                                           *
.*    Modifications:                                                         *
.*****************************************************************************
          DEFPROC   DEVICQST
.
ITEMNO    FORM      2
ICURS     FORM      2
CODENP    INIT      "NP"
SAVKEY5   DIM       5
NXTSCRN   FORM      1
SCREEN    FORM      2
DESC16    DIM       16
DESC16A   DIM       16
DESC16B   DIM       16
DIMA      DIM       1
DIMB      DIM       1
ITEM      DIM       3[11]
ITEMSEL   DIM       3[11]
.
          TOPIC     "DEVICE"                      * Help facility
          MOVE      TEN,CVERT                     * init vars
          MOVE      ONE,SCREEN
          MOVE      ONE,ITEMNO
          MOVE      ONE,ICURS
          CALL      IPRTY000                      * init arrays used
.
          MOVE      "19",HLEF
          MOVE      "9",HTOP
          MOVE      "79",HRIG
          MOVE      "23",HBOT
          CALL      GETSCR00                      * save portion of screen
.
          SUSPEND   
          BOX       16,19,9,79,23                 * draw a box
          BOXCLR    20,10,78,22                   * clear box
          DISPLAY   *P42:9,*HON," DEVICES "
          DISPLAY   *P19:21,*G33
          HLINE     *G37,21,20,78
          DISPLAY   *P79:21,*G34
          FLUSH     19,9,79,23                    * draw a box
.
          CALL      DDFLT000                      * get/display default
.
. Now loop thru Device file in code order and display
.
NQCOD100  CALL      RKNQDEV1                      * read next record
          BRANCH    OVRCD,NQCOD600                * no more records
.
          MOVE      NQDEDNUM,SAVKEY3                * save key
.
. check if we need a new screen
.
          COMPARE   TEN2,ITEMNO
          GOTO      NQCOD500 IF LESS
.
. we need a new screen
.
          MOVE      ONE,NXTSCRN                   * we have a next screen
          CALL      KYSEL000                      * keyin routine 1
          BRANCH    EXIT,NQCOD800:                * Item selected
                         NQCOD900:                * Abort selected
                         NQCOD200:                * Previous screen selected
                         NQCOD250                 * Next screen selected
.
. Previous screen was selected
.
NQCOD200  MOVE      ITEM[SCREEN],KEY3             * save 1st item on screen
.
          CALL      RSNQDEV1                      * position on 1st key on scrn
.
. read back 11 records until 1st record on previous screen is reached
.
          MOVE      ZERO,FORM2                    * init counter
NQCOD220  CALL      RPNQDEV1                      * read back 1 record
          ADD       ONE,FORM2                     * increment counter
          COMPARE   TEN1,FORM2                    * read back far enough?
          GOTO      NQCOD220 IF NOT EQUAL
.
. we have read back far enough
.
          BOXCLR    20,10,78,20                   * clear box
          MOVE      TEN,CVERT                     * init screen row
          MOVE      ONE,ITEMNO                    * init item no.
          SUB       ONE,SCREEN                    * update screen number
          MOVE      ONE,ICURS
          GOTO      NQCOD500
.
. Next screen was selected
.
NQCOD250  ADD       ONE,SCREEN                    * increment screen No.
.
          MOVE      SAVKEY3,ITEM[SCREEN]          * save 1st item on screen
          BOXCLR    20,10,78,20                   * clear box
          MOVE      TEN,CVERT                     * init screen row
          MOVE      ONE,ITEMNO                    * init item no.
.
. display record
.
NQCOD500  MOVE      NQDENAME,DESC16               * shorten description
          MOVE      NQDEDESC,DESC16A              * shorten description
          MOVE      NQDEMACH,DESC16B              * shorten description
.
          DISPLAY   *P20:CVERT,*V2LON,ITEMNO,*HOFF,". ",*V2LON,NQDEDNUM,*HOFF:
                    SP2,DESC16B,DESC16,SP1,DESC16A
.
. save each record as it is displayed for selection purposes
.
          MOVE      NQDEDNUM,KEY3
          MOVE      KEY3,ITEMSEL[ITEMNO]
.
          ADD       ONE,CVERT                     * increment vertical position
          ADD       ONE,ITEMNO                    * increment item counter
          GOTO      NQCOD100
.
. We have no more records to display
.
NQCOD600  MOVE      ZERO,NXTSCRN                  * No next screen
          CALL      KYSEL000                      * keyin routine 2
          BRANCH    EXIT,NQCOD800:                * Item selected
                         NQCOD900:                * Abort selected
                         NQCOD200                 * Previous screen selected
.
. an item was selected so get record selected
.
NQCOD800  MOVE      ITEMSEL[ICURS],KEY3
.
          CALL      RDNQDEV1
          MOVE      NQDEDNUM,KDEVICE
          MOVE      ZERO,EXIT
          GOTO      NQCOD950
.
NQCOD900  MOVE      ONE,EXIT
NQCOD950  CALL      PUTSCR00                      * restore portion of screen
.
NQCOD999  GOTO      ENDPRTYC                      * goto ENDPROC
+
.*************************************************************************
.*  KYSEL000  :  Keyin routine                                           *
.*************************************************************************
KYSEL000  MOVE      ICURS,CVERT
          ADD       NINE,CVERT
          DISPLAY   *P20:CVERT,*V2LON,*HON,ICURS,"."
.
             IF    SCREEN = 1
             IF   NXTSCRN = 1
                   DISPLAY  *P21:22,SP20,SP5,*P21:22,*V2LON,"E",*HOFF:
                   "xit, ",*V2LON,"N",*HOFF,"ext"
             ELSE
                  DISPLAY  *P21:22,SP20,SP5,*P21:22,*V2LON,"E",*HOFF,"xit"
             ENDIF
          ELSE
             IF   NXTSCRN = 1
               DISPLAY  *P21:22,SP20,SP5,*P21:22,*V2LON,"E",*HOFF,"xit, ":
                  *V2LON,"N",*HOFF,"ext, ",*V2LON,"P",*HOFF,"revious"
             ELSE
               DISPLAY  *P21:22,SP20,SP5,*P21:22,*V2LON,"E",*HOFF,"xit, ":
                  *V2LON,"P",*HOFF,"revious"
             ENDIF
          ENDIF
.
KYSEL100  KEYIN     *NOEDIT,*EOFF,*+,DIMA
.
KYSEL090  GOTO      KYSEL200 IF LEFT
          GOTO      KYSEL200 IF RIGHT
          GOTO      KYSEL300 IF UP  
          GOTO      KYSEL500 IF DOWN
          GOTO      KYSEL800 IF PAGEDOWN
          GOTO      KYSEL850 IF PAGEUP
          REP       UPPLOW,DIMA
.
          RESET     DIMA
          GOTO      KYSEL870 IF EOS
.
          TYPE      DIMA
          GOTO      KYSEL250 IF EQUAL
.
          MATCH     ANSE,DIMA
          GOTO      KYSEL900 IF EQUAL
.
          MATCH     ANSN,DIMA
          GOTO      KYSEL800 IF EQUAL
.
          MATCH     ANSP,DIMA
          GOTO      KYSEL850 IF EQUAL
.
KYSEL200  BEEP
          GOTO      KYSEL100
.
. A numeric was input
.
KYSEL250 MOVE       DIMA,FORM1
         COMPARE    ONE,FORM1
         GOTO       KYSEL200 IF LESS
          IF        FORM1 > 1
            MOVE      FORM1,DIM2
            GOTO      KYSEL280
          ENDIF
          IF        ITEMNO < 11
            MOVE      FORM1,DIM2
            GOTO      KYSEL280
          ENDIF
          KEYIN     *NOEDIT,*EOFF,*+,DIMB
          TYPE      DIMB
          GOTO      KYSEL200 IF NOT EQUAL
          PACK      DIM2,DIMA,DIMB
.
KYSEL280  MOVE      DIM2,FORM2
          IF        FORM2 >= ITEMNO
             BEEP
             GOTO   KYSEL100
          ENDIF
          MOVE      DIM2,ICURS
          GOTO      KYSEL870
.
. UP was selected
.
KYSEL300  IF   ICURS = 1
                  BEEP
                  GOTO   KYSEL100
          ELSE
                  ASSIGN  (NINE+ICURS),FORM2      * get row to de-highlite
                  DISPLAY *P20:FORM2,*V2LON,ICURS,"."  * de-highlite
                  SUB     ONE,ICURS               * new position
                  SUB     ONE,FORM2               * row for highlite
                  DISPLAY *P20:FORM2,*V2LON,*HON,ICURS,"."
                  GOTO    KYSEL100
          ENDIF  
.
. DOWN was selected
.
KYSEL500  MOVE      ITEMNO,FORM2                  * Get correct No items on scrn
          SUB       ONE,FORM2
          IF   ICURS = FORM2
                  BEEP
                  GOTO   KYSEL100
          ELSE
                  ASSIGN  (NINE+ICURS),FORM2      * get row to de-highlite
                  IF  !(FORM2 = 0)
                  DISPLAY *P20:FORM2,*V2LON,ICURS,"."  * de-highlite
                  ENDIF
                  ADD     ONE,ICURS               * new position
                  ADD     ONE,FORM2               * row for highlite
                  DISPLAY *P20:FORM2,*V2LON,*HON,ICURS,"."
                  GOTO    KYSEL100
          ENDIF  
.
KYSEL800  IF        NXTSCRN =1
                    MOVE    FOUR,EXIT             * Next screen selected
                    MOVE    ONE,ICURS
                    GOTO    KYSEL999
          ELSE
                    BEEP
                    GOTO    KYSEL100
          ENDIF
.
KYSEL850  IF        !(SCREEN = 1)
                    MOVE    THREE,EXIT            * Previous screen selected
                    MOVE    ONE,ICURS
                    GOTO    KYSEL999
          ELSE
                    BEEP
                    GOTO    KYSEL100
          ENDIF
.
KYSEL870  MOVE      ONE,EXIT                      * 'Enter' input
          GOTO      KYSEL999
.
KYSEL900  MOVE      TWO,EXIT                      * Abort selected
.
KYSEL999  RETURN
+
.***************************************************************************
.*  IPRTY000  :  Initialise Arrays                                         *
.***************************************************************************
IPRTY000  MOVE      ONE,FORM2
          WHILE     FORM2 < 12
            MOVE     SP3,ITEM[FORM2]
            MOVE     SP3,ITEMSEL[FORM2]
            ADD      ONE,FORM2
          DO
IPRTY999  RETURN
+
.*************************************************************************
.*  DDFLT000  :  Display Default                                         *
.*************************************************************************
DDFLT000  MATCH     SP3,CKYIDEF3                  * any default?
          GOTO      DDFLT900 IF EQUAL
.
          MOVE      ONE,FORM2
          MOVE      ONE,SCREEN
          PACK      KEY3,SP3
          CALL      RSNQDEV1
DDFLT100  CALL      RKNQDEV1
          BRANCH    OVRCD,DDFLT900
.
          MOVE      NQDEDNUM,KEY3
.
          MATCH     NQDEDNUM,CKYIDEF3                * found default?
          GOTO      DDFLT300 IF EQUAL
.
          ADD       ONE,FORM2 
          IF        FORM2 = 12
                      ADD  ONE,SCREEN
                      MOVE ONE,FORM2
          ENDIF
          IF        FORM2 = 2
           MOVE      KEY3,ITEM[SCREEN]
          ENDIF
          GOTO      DDFLT100
.
. we have found the default
.
DDFLT300  MOVE      FORM2,ICURS 
          ASSIGN    (SCREEN-1)*TEN1,CCITEM
.
          PACK      KEY3,SP3
          CALL      RSNQDEV1
          WHILE     CCITEM
             CALL    RKNQDEV1
             SUB     ONE,CCITEM
          DO
          GOTO      DDFLT999
.
DDFLT900  PACK      KEY3,SP3
          CALL      RSNQDEV1
          MOVE      ONE,SCREEN
.
DDFLT999  RETURN
.
ENDPRTYC  TOPIC     SP8
          ENDPROC
.
+
