.***************************************************************************
.* System    :   Theatre                                                   *
.* Program   :   F93DAYP1.PBL                                              *
.* Desc      :   Fix program for Theatre Day Procedures - Part 1.          *
.***************************************************************************
.* Date      :   28/07/2003                                                *
.* Author    :   Sylvek Litewka                                            *
.* Function  :   This program sets the type of all Theatre Session Master  *
.*               records (oprsemaf) and Theatre Session records (oprsesaf) *
.*               to 'General Operating Room Surgery'. The session type is  *
.*               stored in the new fields oprsemaf.OPSMDGSI and            *
.*               oprsesaf.OPSEDGSI. These are coded fields - Category YD.  *
.*                                                                         *
.*               To set the type of specific sessions to 'Day Procedure',  *
.*               the fix program F93DAYP2 (Day Procedures - Part 2) needs  *
.*               to be run after running this program.                     *
.*                                                                         *
.*               NOTE: This program does not save the original files       *
.*                     (oprsemaf and oprsesaf). These files should be      *
.*                     backed-up before running this program.              *
.*                                                                         *
.* Modifications:                                                          *
.*      V9.03.01 08/12/2003 Sylvek Litewka CAR 45947                       *
.*               Modified program to write to Category Codes file for      *
.*               Cat YD.                                                   *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       OPRSEMFD/INC
          INC       OPRSESFD/INC
          INC       PATCODFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
CATYD     INIT      "YD"
GORSCODE  DIM       3   * General Operation Room Surgery CODE (Cat YD)
.
PRGID     INIT      "F93DAYP1"                    
PRGNAM    INIT      "Day Procedures - Fix Program 1 (oprsemaf and oprsesaf)"
VERSION   INIT      "V10.14.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      CHKCAT00               * check category code
          CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
          MOVE      SP70,GORSCODE             * initialize Gen Opr Room Code
          OPEN      OPRSEMA1,"oprsemaf"
          OPEN      OPRSESA1,"oprsesaf"
          OPEN      PATCODE1,"patcodes"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P3:4,*EF,"This program sets the type of all Theatre ":
                           "Session Master records (oprsemaf)":
                    *P3:5,"and Theatre Session records (oprsesaf) to ":
                          "'General Operating Room Surgery'. ":
                    *P3:6,"To set the type of spefific sessions to ":
                          "'Day Procedure', please run F93DAYP2 ":
                    *P3:7,"after running this program."
.
          DISPLAY   *P5:9,*EF,*V2LON,"Warning: ",*HOFF:
                    *P14:9,"This program does not save the current versions":
                    *P14:10,"of oprsemaf and oprsesaf. Please Back-Up these":
                    *P14:11,"files before running this program!": 
                    *P5:13,*V2LON,ZERO,*HOFF,". Exit":
                    *P5:14,*V2LON,ONE,*HOFF,". Continue"
.
OPTN0500  KEYIN     *P5:16,*EF,"Select Option : ",*DV,UNDLN1:
                    *P21:16,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN1000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN1000  MOVE      "GOR",GORSCODE               * Default value - "GOR"
          KEYIN     *P5:18,*EL,"Enter CODE for General Operating Room Surgery":
                               " (Cat YD) : ":
                    *P62:18,*EL,*DV,*RV,GORSCODE:
                    *P62:18,*V2LON,GORSCODE;
.
          PACK      GORSCODE,GORSCODE,SP70
          MATCH     SP70,GORSCODE
          IF        @EQUAL
            BEEP
            GOTO      OPTN0500
          ENDIF
.
          DISPLAY   *P5:20,*EF,*V2LON,"NOTE: ",*HOFF:
                    *P11:20,"This program will create Category YD and ":
                            "specified":
                    *P11:21,"CODE if required.";

          MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               CHKCAT00              Called by: ML0000  *
.*                                                                        *
.************************************************************************** 
CHKCAT00  PACK      KEY5,CATYD,SP70              * Does Category YD exist?
          CALL      RDACODE1
          IF        OVRCD=1 
            CALL      CLRCOD00                   * No, clear file variables
            MOVE      CATYD,TCODE                * Set Category 
            MOVE      "Surgery Type Ind",TDESC   * Set Description
            PACK      KEY5,CATYD,SP70
            CALL      WRCODE1                    * Write record
          ENDIF
. 
          PACK      KEY5,CATYD,GORSCODE,SP70     * Does specified code exist?
          CALL      RDACODE1
          IF        OVRCD=1
            CALL      CLRCOD00                   * No, clear file variables
            MOVE      CATYD,TCODE                * Set Category 
            MOVE      GORSCODE,ACODE             * Set Category Code
            MOVE      "General Opr Room",TDESC   * Set Description
            MOVE      "A",PTCOACTV               * Set as Active
            MOVE      "0",PTCDDEFT               * Default Code (0 = No)
            PACK      KEY5,CATYD,GORSCODE,SP70
            CALL      WRCODE1                    * Write record
          ENDIF
.
CHKCAT99  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  PACK      KEY12,SP70              * Precess oprsemaf records
          CALL      RSOPSEM1
PROC0100  CALL      RKOPSEM1
          BRANCH    OVRCD,PROC0200
.
.  Set the Day/General Surgery Indicator (OPSMDGSI) to the code representing
.  the General Operating Room Surgery. Clear the SPARE variable - due to 
.  incorrect OPRSEMIO.INC (which has been fixed), the SPARE variable does not 
.  contain SPACES and therefore needs to be cleared.
.
          MOVE      GORSCODE,OPSMDGSI       * Set to GOR (default)
          MOVE      SP70,OPSMSPAR           * Clear SPARE variable
          CALL      UPOPSEM1                * Update record
.
          GOTO      PROC0100                * Fetch next oprsemaf record
.
PROC0200  PACK      KEY19,SP70              * Process oprsesaf records
          CALL      RSOPSES1
PROC0300  CALL      RKOPSES1
          BRANCH    OVRCD,PROC9000
.
          MOVE      GORSCODE,OPSEDGSI       * Set to GOR (default)
          CALL      UPOPSES1                * Update record
.
          GOTO      PROC0300                * Fetch next oprsesaf record
.
PROC9000  CLOSE     OPRSEMA1
          CLOSE     OPRSESA1
          CLOSE     PATCODE1
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
PROC9999  RETURN
+
.-----------------------------------------------------------
. Initialize patcodes file data
.-----------------------------------------------------------
CLRCOD00  MOVE      SP70,TCODE
          MOVE      SP70,ACODE
          MOVE      SP70,TDESC
          MOVE      SP70,THCSCOD
          MOVE      ZERO,TCFORM6
          MOVE      SP70,TCINDC1
          MOVE      SP70,TCINDC2
          MOVE      SP70,TCINDC3
          MOVE      SP70,TCINDC4
          MOVE      SP70,TCINDC5
          MOVE      SP70,PTCOACTV
          MOVE      SP70,TCINDC6
          MOVE      SP70,TCINDC7
          MOVE      SP70,TCINDC8
          MOVE      SP70,TCINDC9
          MOVE      SP70,TCINDC10
          MOVE      SP70,TCINDC11
          MOVE      SP70,PTCDGRPV
          MOVE      SP70,PTCDDEFT
          MOVE      SP70,PTCDNHCP
          MOVE      SP70,TCSPARE
.
CLRCOD99  RETURN
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       OPRSEMIO/INC
          INC       OPRSESIO/INC
          INC       PATCODIO/INC
