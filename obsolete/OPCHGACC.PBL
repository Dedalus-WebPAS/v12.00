.******************************************************************************
.* Include Name   : OPCHGACC.PBL           Release 5.01                       *
.* System         : Theatre System                                            *
.* Description    : Change preferred accomodation                             *
.* Author         : Rod Knowles                                               *
.* Date           : 08/11/90                                                  *
.******************************************************************************
.* Parameters     : CURSESS - must be current session                         *
.*                  CURCASE - case number of current booking                  *
.* Includes Req'd : OPRSESFD  BOKRC1FD                                        *
.* Returns        : EXIT = 0  Change made                                     *
.*                       = 1  No change made                                  *
.******************************************************************************
.* Modifications  : 
.*        V5.07.01  27/09/1999  Jill Habasque                                 *
.*                  Added inactive code error message                         *
.*                  09/10/1991   Justin Coates                                *
.*                  Changed keyin to line 24                                  *
.******************************************************************************
OPCHGACC  DISPLAY   *PSUBOPT2:2," (Change Accommodation) "
.
          PACK      KEY23,CURSESS,ZERO,CURCASE
          CALL      RDOPDEA1
          BRANCH    OVRCD,CHACC900
.
          MOVE      OPDAADMN,KEY8
          CALL      RDBKREC1
          BRANCH    OVRCD,CHACC900
.
.         we have a booking record so proceed
.
          DISPLAY   *P1:24,*EL,"Pref. Accomodation : "
.
CHACC000  MOVE      BKREACCM,PREFACOM       * set up default
.
          KEYIN     *P22:24,*DV,PREFACOM:
                    *P22:24,*V2LON,*RV,PREFACOM:
                    *P22:24,*DV,*HOFF,PREFACOM
.
          CALL      CHECK000  .(0=Invalid  1="?"  2=blank  3=valid)
          BRANCH    EXIT,CHACC100,CHACC800,CHACC800
          GOTO      CHACC000                      * invalid code
.
.         a "?" was input
.
CHACC100  PACK      CODE,ANSB,ANSP
          MOVE      ONE,CNEWDS
          CALL      PATCODDS
.
CHACC200  MOVE      BKREACCM,PREFACOM
          KEYIN     *P1:24,*EL,*DV,"Pref. Accomodation : ",BKREACCM:
                    *P22:24,*V2LON,*RV,PREFACOM:
                    *P22:24,*DV,*HOFF,PREFACOM
.
          CALL      CHECK000  .(0=Invalid  1="?"  2=blank  3=valid)
          BRANCH    EXIT,CHACC100,CHACC780,CHACC780
          GOTO      CHACC200                      * invalid code
.
.         update after ? mark
.
CHACC780  PACK      KEY8,BKREBOOK
          CALL      RDBKREC1                      * read record before update
          MOVE      PREFACOM,BKREACCM
          CALL      UPBKREC1                      * update BOKREC record
.
.         redisplay after a ? mark (on nothing entered and valid entry)
.
CHACC790  CALL      OPDSPSHD
          CALL      OPDSPCUR
          PACK      KEY23,CURSESS,ZERO,CURCASE
          CALL      RDOPDEA1
          MOVE      PREFACOM,BKREACCM       * reset entered accomadation
          GOTO      CHACC850
.
.         update
.         
CHACC800  PACK      KEY8,BKREBOOK
          CALL      RDBKREC1                      * read record before update
          MOVE      PREFACOM,BKREACCM
          CALL      UPBKREC1                      * update BOKREC record
.
CHACC850  MOVE      ZERO,EXIT
          GOTO      CHACC999    
.
.         No BOKREC record found
.
CHACC900  MOVE      ONE,EXIT
.
CHACC999  RETURN
+
.**************************************************************************
.*                             CHECK000              Called by: CHACC000  *
.*                   check routiune for preferred accommodation           *
.**************************************************************************
CHECK000  ENDSET    PREFACOM
          APPEND    SP3,PREFACOM
          RESET     PREFACOM
.
          MATCH     SP3,PREFACOM
          GOTO      CHECK700 IF EQUAL
.
          MATCH     QUEST,PREFACOM
          GOTO      CHECK900 IF EQUAL
.
          PACK      KEY5,ANSB,ANSP,PREFACOM
          CALL      RDCODE1
          BRANCH    OVRCD,CHECK950 IF EQUAL
.
          MATCH     "I",PTCOACTV
          GOTO      CHECK960 IF EQUAL             * Inactive code
.
          MOVE      THREE,EXIT                    * valid code
          GOTO      CHECK999
.
CHECK700  MOVE      TWO,EXIT                      * nothing entered
          GOTO      CHECK999
.
CHECK900  MOVE      ONE,EXIT                      * "?"
          GOTO      CHECK999
.
CHECK950  DISPLAY   *P1:24,*EL,*B,"Invalid code.  ";
          CALL      HITENTER
          MOVE      ZERO,EXIT
          GOTO      CHECK999
.
CHECK960  DISPLAY   *P1:24,*EL,*B,"Inactive Code  ";
          CALL      HITENTER
          MOVE      ZERO,EXIT
.
CHECK999  RETURN
.
