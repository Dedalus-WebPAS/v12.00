+
.**********************************************************************
.* System   : Operating Room System                                   *
.* Program  : IBAOPR04                                                *
.* Desc     : Nurse File Maintenance                                  *
.**********************************************************************
.* Date     :  15.12.89                                               *
.* Author   :  D.Di.Paola.                                            *
.* Mod's    :                                                         *
.**********************************************************************
.
.$$$$$
.    Operating Room System Nurse File Maintenance ( IBAOPR04 )
.    ---------------------------------------------------------
.
.    - Initialisation
.
.    - Display Header
.            - Open Files
.               OPRNURA1
.               PATCODE1
.               CONTROLF
.
.    - Processing Options
.
.            - 0 = Exit
.            - 1 = Add
.
.                - Clear Input Variables
.                - Display Screen Format
.                - Input Nurse Code
.                     - Read OPRNURA1, check if already exists
.                     - If Not, Continue...
.                - Accept Field Values 
.                             - Surname
.                             - Given Name 
.                             - Status 
.                - Read CONTROLF sector 40 position *213 onwards
.                     - If user defined fields switched on accept fields....
.                             - User Defined 1.
.                             - User Defined 2.
.                             - User Defined 3.
.                             - User Defined 4.
.                             - User Defined 5.
.                - If "?" entered, Call PATCODDS routine
.                     - display summary screen of relevant codes from PATCODE1
.                - Select Item, Post or Cancel
.                - If Post then Write Data to OPRNURA1
.
.            - 2 = Modify
.
.                - Clear Input Variables
.                - Display Screen Format
.                - Input Nurse Code
.                     - Read OPRNURA1, check if exists
.                     - If Exists, Continue...
.                - Accept Field Values 
.                             - Surname 
.                             - Given Name 
.                             - Status 
.                - Read CONTROLF sector 40 position *213 onwards
.                     - If user defined fields switched on accept fields....
.                             - User Defined 1.
.                             - User Defined 2.
.                             - User Defined 3.
.                             - User Defined 4.
.                             - User Defined 5.
.                - If "?" entered, Call PATCODDS routine
.                     - display summary screen of relevant codes from PATCODE1
.                - Select Item, Post or Cancel
.                - If Post then Update Data to OPRNURA1
.
.            - 3 = Delete
.
.                - Clear Input Variables
.                - Display Screen Format
.                - Input Nurse Code
.                     - Read OPRNURA1, check if exists
.                     - If Exists, Continue...
.                - Display Details 
.                             - Surname
.                             - Given Name 
.                             - Status 
.                - Read CONTROLF sector 40 position *213 onwards
.                     - If user defined fields switched on display details
.                             - User Defined 1.
.                             - User Defined 2.
.                             - User Defined 3.
.                             - User Defined 4.
.                             - User Defined 5.
.                - O.K. to Delete Yes or No ?
.                - If Yes then Delete Record from OPRNURA1 File
.
.    - END
.$$$$$
.
          INC       STD001FD
          INC       OPRNURFD/INC           * Operating Room Nurse File
          INC       OPRCONFD/INC           * Operating Room Control file
          INC       PATCODFD/INC           * patient codes file
+
.**********************************************************************
.*                           CONSTANTS                                *
.**********************************************************************
.
CODEQ1    INIT      "Q1"                   * category for user def. 1
CODEQ2    INIT      "Q2"                   * category for user def. 2 
CODEQ3    INIT      "Q3"                   * category for user def. 3
CODEQ4    INIT      "Q4"                   * category for user def. 4
CODEQ5    INIT      "Q5"                   * category for user def. 5
.
STATUS1   INIT      "Active"
STATUS2   INIT      "Non Active"
.
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
.
CODE      DIM       5
.
FIELDNO   FORM      1
.
INXA      FORM      1
ITEM      FORM      2
.
.
SP40      DIM       40
STATIND   DIM       1
.
USERDEF1  FORM      1 
USERDEF2  FORM      1 
USERDEF3  FORM      1 
USERDEF4  FORM      1 
USERDEF5  FORM      1 
.
USERDES1  DIM       20
USERDES2  DIM       20
USERDES3  DIM       20
USERDES4  DIM       20
USERDES5  DIM       20
UVERT1    FORM      2
.
VAR       FORM      2
. 
PRGID     INIT      "IBAOPR04"
PRGNAM    INIT      "Nurse Maintenance"
VERSION   INIT      "V10.12.00"
+
.**********************************************************************
.*                           MAINLINE                                 *
.*                     controlling logic                              *
.**********************************************************************
.
ML0000    CALL      INIT0000               * initialization and open files
          CALL      STOR0000               * store user defined parameters
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML2000,ML3000
.
. ---- add a Nurse code ----
.
ML1000    CALL      CLR0000                * clear input variables
          CALL      SCR0000                * display screen format
          CALL      PROC0000               * input Nurse code
          BRANCH    EXIT,ML0100 
          CALL      ACPT0000               * accept initial values
          CALL      SEL0000                * select field to modify
.                                           * or ok to post
          BRANCH    EXIT,ML1000
          CALL      WRT0000                * write to file
          GOTO      ML1000
.
. ---- modify a Nurse code ----
.
ML2000    CALL      CLR0000                * clear input variables
          CALL      SCR0000                * display screen format 
          CALL      ENTR0000               * enter Nurse code
          BRANCH    EXIT,ML0100
          CALL      DISP0000               * display details
          CALL      SEL0000                * select field to modify
.                                           * or ok to post
          BRANCH    EXIT,ML2000
          CALL      UPD0000                * update record
          GOTO      ML2000
.
. ---- delete a Nurse code ----
.
ML3000    CALL      CLR0000                * clear input variables
          CALL      SCR0000                * display screen format 
          CALL      INPT0000               * enter Nurse code
          BRANCH    EXIT,ML0100
          CALL      DISP0000               * display detail 
          CALL      CONT0000               * ok to delete ?
          BRANCH    EXIT,ML3000
          CALL      DEL0000                * delete this record
          GOTO      ML3000
.
ML9999    CHAIN     PGM                    * chain back to program
+
.**********************************************************************
.*                             INIT0000                               *
.*                      Initialization  Routine                       *
.**********************************************************************
.
INIT0000    
          CALL      DISPHEAD
.
          DISPLAY   *P57:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P57:24,"Opening oprnuraf";
          OPEN      OPRNURA1,"oprnuraf"
.
          DISPLAY   *P57:24,"Opening oprnuraf";
          OPEN      OPRNURA2,"oprnuraf"
.
          DISPLAY   *P57:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
.-------- read controlf to get user def fields for nurse file ------
.
          READ      CONTROLF,FORTY;*213,OPCNUNU1,OPCNUNM1,OPCNUNU2,OPCNUNM2:
                                        OPCNUNU3,OPCNUNM3,OPCNUNU4,OPCNUNM4:
                                        OPCNUNU5,OPCNUNM5
INIT9999 
          RETURN
.
+
.**********************************************************************
.*                             STOR0000                               *
.*                   store user defined fields parameters             *
.**********************************************************************
STOR0000  MOVE      ONE,INXA                   * 1st index to loop thru 5 fields
          MOVE      ONE,FORM2                  * 2nd index for field no.
          MOVE      ZERO,FIELDNO               * clear field no. variable
.
          MOVE      SP20,USERDEF1              * clear temp user def variables
          MOVE      SP20,USERDEF2
          MOVE      SP20,USERDEF3
          MOVE      SP20,USERDEF4
          MOVE      SP20,USERDEF5
.
.------- determine which user defined fields are switched on -----
.
STOR1000  ADD       ONE,FIELDNO                   * increment field no.
          COMPARE   SIX,INXA                      * check index
          GOTO      STOR9999 IF EQUAL
          LOAD      FORM1,INXA,OPCNUNU1,OPCNUNU2,OPCNUNU3,OPCNUNU4,OPCNUNU5
          COMPARE   ZERO,FORM1                    * if "0" = No, switched off
          GOTO      STOR2000 IF EQUAL
.
          STORE     FIELDNO,FORM2,USERDEF1,USERDEF2,USERDEF3,USERDEF4,USERDEF5
          ADD       ONE,FORM2         * One more field to be added
.
STOR2000  ADD       ONE,INXA                     * increment index by 1.
          GOTO      STOR1000                     * get next user def variable
.
STOR9999  RETURN
+
.**********************************************************************
.*                             CLR0000                                *
.*                 Routine to Clear Local Variables                   *
.**********************************************************************
.
CLR0000
.
. ----- Clear the Nurse File Variables -----
.
          MOVE      SP6,OPNRCODE 
          MOVE      SP30,OPNRSNAM
          MOVE      SP30,OPNRGNAM
          UNPACK    SP30,OPNRUSR1,OPNRUSR2,OPNRUSR3,OPNRUSR4,OPNRUSR5
          MOVE      ZERO,OPNRSTAT
.
.------ clear working variables ----
.
          MOVE      SP20,USERDES1
          MOVE      SP20,USERDES2
          MOVE      SP20,USERDES3
          MOVE      SP20,USERDES4
          MOVE      SP20,USERDES5
.
          RETURN
+
.****************************************************************************
.*                                OPTN0000                                  *
.*                        get user options or exit                          *
.*                                                                          *
.*    valid options     --->  (0) Exit                                      *
.*                            (1) Add                                       *
.*                            (2) Modify                                    *
.*                            (3) Delete                                    *
.****************************************************************************
.
. ---- display option screen ----
.
OPTN0000    MOVE      FALSE,EXIT
.
            HLINE     *G37,2,68,80
.
            CALL      SELOPTN                        * select option
            COMPARE   ZERO,COPTION
            GOTO      OPTN9000 IF EQUAL              * exit      
            COMPARE   FOUR,COPTION
            GOTO      OPTN0000 IF NOT LESS           * invalid option, re-enter
            GOTO      OPTN9999
.
OPTN9000    MOVE      TRUE,EXIT                      * set EXIT = 1 
.
OPTN9999    RETURN
+
.**********************************************************************
.*                            SCRN0000                                *
.*                     Main Data Screen Display                       *
.**********************************************************************
.
SCR0000
          DISPLAY   *P1:3,*EF:
                    *P5:4,"Nurse Code",*P25:4," : ":
                    *P2:6,*V2LON,ONE,*HOFF,". Surname              : ":
                    *P2:7,*V2LON,TWO,*HOFF,". Given Name(s)        : ":
                    *P2:8,*V2LON,THREE,*HOFF,". Status               : ";
.
.------ process screen for user defined fields -----
.
          MOVE      "9",CVERT                       * set up line
          MOVE      "4",ITEM                        * set up item no.
          COMPARE   ONE,OPCNUNU1                    * "0" = No, "1" = Yes 
          GOTO      SCR1000 IF NOT EQUAL            * if No, get next user def
          PACK      KEY5,CODEQ1,SP3                 * read codes file to get 
          CALL      RDCODE1                         * user def 1 description
          BRANCH    OVRCD,SCR1000
          DISPLAY   *P1:CVERT,*V2LON,ITEM,*HOFF,". ",TDESC,*P25:CVERT," : ";
          ADD       ONE,ITEM                        * increment item by 1.
          ADD       ONE,CVERT                       * increment line by 1.
.
.------ display user defined field 2, if valid -----
.
SCR1000   COMPARE   ONE,OPCNUNU2                    * "0" = No, "1" = Yes
          GOTO      SCR1100 IF NOT EQUAL            * if No, get next user def
          PACK      KEY5,CODEQ2,SP3                 * read codes file to get
          CALL      RDCODE1                         * user def 2 description
          BRANCH    OVRCD,SCR1100
          DISPLAY   *P1:CVERT,*V2LON,ITEM,*HOFF,". ",TDESC,*P25:CVERT," : ";
          ADD       ONE,ITEM                        * increment item by 1.
          ADD       ONE,CVERT                       * increment line by 1.
.
.------ display user defined field 3, if valid ------
.
SCR1100   COMPARE   ONE,OPCNUNU3                    * "0" = No, "1" = Yes
          GOTO      SCR1200 IF NOT EQUAL            * if No, get next user def
          PACK      KEY5,CODEQ3,SP3                 * read codes file to get 
          CALL      RDCODE1                         * user def 3 description
          BRANCH    OVRCD,SCR1200
          DISPLAY   *P1:CVERT,*V2LON,ITEM,*HOFF,". ",TDESC,*P25:CVERT," : ";
          ADD       ONE,ITEM                        * increment item by 1.
          ADD       ONE,CVERT                       * increment line by 1.
.
.------- display user defined field 4, if vaild -----
.
SCR1200   COMPARE   ONE,OPCNUNU4                    * "0" = No, "1" = Yes
          GOTO      SCR1300 IF NOT EQUAL            * if No, get next user def
          PACK      KEY5,CODEQ4,SP3                 * read codes file to get 
          CALL      RDCODE1                         * user def 4 description
          BRANCH    OVRCD,SCR1300
          DISPLAY   *P1:CVERT,*V2LON,ITEM,*HOFF,". ",TDESC,*P25:CVERT," : ";
          ADD       ONE,ITEM                        * increment line by 1.
          ADD       ONE,CVERT                       * increment item by 1.
.
.------- display user defined field 5, if valid ------
.
SCR1300   COMPARE   ONE,OPCNUNU5                    * "0" = No, "1" = Yes
          GOTO      SCR9999 IF NOT EQUAL            * If No, get next user def.
          PACK      KEY5,CODEQ5,SP3                 * read codes file to get 
          CALL      RDCODE1                         * user def 5 description
          BRANCH    OVRCD,SCR9999
          DISPLAY   *P1:CVERT,*V2LON,ITEM,*HOFF,". ",TDESC,*P25:CVERT," : ";
.
SCR9999   RETURN
+
.**********************************************************************
.*                            PROC0000                                *
.*                 Routine to Keyin Nurse Codes                       *
.**********************************************************************
.
PROC0000
          MOVE      FALSE,EXIT                       * initialise exit flag
.
          DISPLAY   *P28:4,*EL,UNDLN6;
          KEYIN     *P28:4,*V2LON,OPNRCODE;
          ENDSET    OPNRCODE
          APPEND    SP6,OPNRCODE
          RESET     OPNRCODE
          DISPLAY   *P28:4,*V2LON,OPNRCODE;
.
          MATCH     SP6,OPNRCODE                   * check for spaces
          GOTO      PROC9000 IF EQUAL
.
.-------- read Nurse file to see if code already exists -----
.
          PACK      KEY6,OPNRCODE
          CALL      RDOPNUR1
          BRANCH    OVRCD,PROC9999
.
.-------- Record already exists ------
.
          DISPLAY   *P1:24,*B,"Record Already Exists - ",*EL;
          CALL      HITENTER
          GOTO      PROC0000
.
PROC9000
          MOVE      TRUE,EXIT                         * set exit flag
PROC9999
          RETURN
+
.**********************************************************************
.*                           ENTR00000                                *
.*                  Read Nurse File for Record                        *
.**********************************************************************
.
ENTR0000
          MOVE      FALSE,EXIT                          * initialise flag
.
          DISPLAY   *P28:4,*EL,UNDLN6;
          KEYIN     *P28:4,*V2LON,OPNRCODE;
          ENDSET    OPNRCODE
          APPEND    SP6,OPNRCODE
          RESET     OPNRCODE
          DISPLAY   *P28:4,*V2LON,OPNRCODE;
.
          MATCH     SP6,OPNRCODE                        * check for spaces
          GOTO      ENTR9500 IF EQUAL
.
.-------- read Nurse File to see if code is on file -----
.
          PACK      KEY6,OPNRCODE
          CALL      RDOPNUR1
          BRANCH    OVRCD,ENTR9000
          MOVE      OPNRSTAT,STATIND
          GOTO      ENTR9999
.
.------ record not on file -----
.
ENTR9000  DISPLAY   *P1:24,*B,"Record Not on File - ",*EL;
          CALL      HITENTER
          GOTO      ENTR0000
ENTR9500
          MOVE      TRUE,EXIT                    * nothing entered
ENTR9999
         RETURN
.
.**********************************************************************
.*                           INPT00000                                *
.*                  Read Nurse File for record                        *
.**********************************************************************
.
INPT0000
          MOVE      FALSE,EXIT
.
          DISPLAY   *P28:4,*EL,UNDLN6;
          KEYIN     *P28:4,*V2LON,OPNRCODE;
          ENDSET    OPNRCODE
          APPEND    SP6,OPNRCODE
          RESET     OPNRCODE
          DISPLAY   *P28:4,*V2LON,OPNRCODE;
.
          MATCH     SP6,OPNRCODE
          GOTO      INPT9500 IF EQUAL
.
.--------- check if Nurse code exists on file -----
.
          PACK      KEY6,OPNRCODE
          CALL      RDOPNUR1
          BRANCH    OVRCD,INPT9000
          MOVE      OPNRSTAT,STATIND         * status indicator "0" = Active
.                                                               "1" = Non Active
          GOTO      INPT9999
.
.------- Nurse code Not On File ----
.
INPT9000  DISPLAY   *P1:24,*B,"Record Not on File - ",*EL;
          CALL      HITENTER
          GOTO      INPT0000
.
INPT9500  MOVE      TRUE,EXIT                    * nothing entered
INPT9999  RETURN
+
.**********************************************************************
.*                           ACPT00000                                *
.*                      Accept Initial Values                         *
.**********************************************************************
.
ACPT0000
          CALL      SURN0000                    * keyin description
.
          CALL      GIVN0000                    * keyin location 
.
          CALL      STAT0000                    * keyin Status Indicator
.
          CALL      USER0000                    * keyin user defined field 1
.
          CALL      USER1000                    * keyin user defined field 2
. 
          CALL      USER2000                    * keyin user defined field 3 
. 
          CALL      USER3000                    * keyin user defined field 4
.
          CALL      USER4000                    * keyin user defined field 5 
.
          RETURN
+
.**************************************************************************
.*                                   SEL0000                              *
.*                         select field to modify or post                 *
.**************************************************************************
.
SEL0000   MOVE      FALSE,EXIT
          CALL      MAINQST
          COMPARE   ZERO,CCITEM                * ok to post
          GOTO      SEL9999 IF EQUAL
          COMPARE   "-1",CCITEM                * cancel option
          GOTO      SEL9000 IF EQUAL
          BRANCH    CCITEM,SEL1000,SEL2000,SEL3000,SEL4000,SEL4100,SEL4200:
                           SEL4300,SEL4400
.
          BEEP
          GOTO      SEL0000
.
SEL1000   CALL      SURN0000                        * keyin surname
          GOTO      SEL0000
.
SEL2000   CALL      GIVN0000                        * keyin given name
          GOTO      SEL0000
.
SEL3000   CALL      STAT0000                        * keyin status indicator
          GOTO      SEL0000
.
.------- process user defined field 1.  ------
.
SEL4000   MOVE      "9",UVERT1
          BRANCH    USERDEF1,SEL8000,SEL8100,SEL8200,SEL8300,SEL8400
          BEEP
          GOTO      SEL0000
.
.------ process user defined field 2. -------
.
SEL4100   MOVE      "10",UVERT1
          BRANCH    USERDEF2,SEL8000,SEL8100,SEL8200,SEL8300,SEL8400
          BEEP
          GOTO      SEL0000
.
.------ process user defined field 3. -------
.
SEL4200   MOVE      "11",UVERT1
          BRANCH    USERDEF3,SEL8000,SEL8100,SEL8200,SEL8300,SEL8400
          BEEP
          GOTO      SEL0000
.
.------ process user defined field 4. -------
.
SEL4300   MOVE      "12",UVERT1
          BRANCH    USERDEF4,SEL8000,SEL8100,SEL8200,SEL8300,SEL8400
          BEEP
          GOTO      SEL0000
.
.------ process user defined field 5. -------
.
SEL4400   MOVE      "13",UVERT1
          BRANCH    USERDEF5,SEL8000,SEL8100,SEL8200,SEL8300,SEL8400
          BEEP
          GOTO      SEL0000
.
.------ the following calls are to the various user defined fields ------
.------ process user defined field 1. -------
.
SEL8000   CALL      USER0100
          GOTO      SEL0000
.
.------ process user defined field 2. -------
.
SEL8100   CALL      USER1100
          GOTO      SEL0000
.
.------ process user defined field 3. -------
.
SEL8200   CALL      USER2100
          GOTO      SEL0000
.
.------ process user defined field 4. -------
.
SEL8300   CALL      USER3100
          GOTO      SEL0000
.
.------ process user defined field 5. -------
.
SEL8400   CALL      USER4100
          GOTO      SEL0000
.
SEL9000   MOVE      TRUE,EXIT                          * set exit
.
SEL9999   RETURN
+
.**************************************************************************
.*                               DISP0000                                 *
.*                   Display Data from the Nurse room file                *
.**************************************************************************
.
DISP0000
.
.------- display details ----
.
          DISPLAY   *P28:4,*V2LON,OPNRCODE:
                    *P28:6,OPNRSNAM:
                    *P28:7,OPNRGNAM:
                    *P28:8,STATIND;
.
          MATCH     "1",STATIND
          GOTO      DISP1000 IF EQUAL
.
          DISPLAY   *P34:8,"Active";
          GOTO      DISP2000
DISP1000
          DISPLAY   *P34:8,"Non Active";
.
.-------- get user def field 1. details ------
.
DISP2000  MOVE      "9",CVERT                       * set line no.
          COMPARE   ZERO,OPCNUNU1                   * check if user switched on
          GOTO      DISP2100 IF EQUAL
          MATCH     SP3,OPNRUSR1
          GOTO      DISP2050 IF EQUAL
          PACK      KEY5,CODEQ1,OPNRUSR1
          CALL      RDCODE1
          BRANCH    OVRCD,DISP2100
          MOVE      TDESC,USERDES1                  * get user def description
DISP2050  DISPLAY   *P28:CVERT,*V2LON,OPNRUSR1,*HOFF:
                    *P34:CVERT,USERDES1;
          ADD       ONE,CVERT                       * increment line number
.
.-------- display user def field 2. details ------
.
DISP2100  COMPARE   ZERO,OPCNUNU2,                  * test if switched on
          GOTO      DISP2200 IF EQUAL               * goto next user def if not
          MATCH     SP3,OPNRUSR2
          GOTO      DISP2150 IF EQUAL
          PACK      KEY5,CODEQ2,OPNRUSR2
          CALL      RDCODE1
          BRANCH    OVRCD,DISP2200
          MOVE      TDESC,USERDES2                  * get user def description
DISP2150  DISPLAY   *P28:CVERT,*V2LON,OPNRUSR2,*HOFF:
                    *P34:CVERT,USERDES2;
          ADD       ONE,CVERT                       * increment line number
.
.-------- display user def field 3. details ------
.
DISP2200  COMPARE   ZERO,OPCNUNU3,                  * check if switched on
          GOTO      DISP2300 IF EQUAL
          MATCH     SP3,OPNRUSR3                    * check for spaces
          GOTO      DISP2250 IF EQUAL
          PACK      KEY5,CODEQ3,OPNRUSR3
          CALL      RDCODE1                         * read codes file
          BRANCH    OVRCD,DISP2300
          MOVE      TDESC,USERDES3                  * get user def description 
DISP2250  DISPLAY   *P28:CVERT,*V2LON,OPNRUSR3,*HOFF:
                    *P34:CVERT,USERDES3;
          ADD       ONE,CVERT                       * increment line number 
.
.-------- display user def field 4. details -------
.
DISP2300  COMPARE   ZERO,OPCNUNU4                   * check user def switched on
          GOTO      DISP2400 IF EQUAL
          MATCH     SP3,OPNRUSR4                    * check for spaces
          GOTO      DISP2350 IF EQUAL
          PACK      KEY5,CODEQ4,OPNRUSR4
          CALL      RDCODE1                         * read codes file 
          BRANCH    OVRCD,DISP2400
          MOVE      TDESC,USERDES4                  * get user def description
DISP2350  DISPLAY   *P28:CVERT,*V2LON,OPNRUSR4,*HOFF:
                    *P34:CVERT,USERDES4;
          ADD       ONE,CVERT                       * increment line number
.
.-------- display user def field 5. details --------
.
DISP2400  COMPARE   ZERO,OPCNUNU5                   * check user def switched on
          GOTO      DISP9999 IF EQUAL
          MATCH     SP3,OPNRUSR5                    * check code for spaces
          GOTO      DISP2450 IF EQUAL
          PACK      KEY5,CODEQ5,OPNRUSR5 
          CALL      RDCODE1                         * read codes file
          BRANCH    OVRCD,DISP9999
          MOVE      TDESC,USERDES5                  * get description
DISP2450  DISPLAY   *P28:CVERT,*V2LON,OPNRUSR5,*HOFF:
                    *P34:CVERT,USERDES5;
DISP9999
          RETURN
+
.**************************************************************************
.*                              SURN0000                                  *
.*                       Keyin Surname Description                        *
.**************************************************************************
.
SURN0000
          DISPLAY   *P28:6,*EL,UNDLN30;
          KEYIN     *P28:6,*V2LON,OPNRSNAM;
          DISPLAY   *P28:6,*V2LON,OPNRSNAM;
.
          ENDSET    OPNRSNAM
          APPEND    SP30,OPNRSNAM
          RESET     OPNRSNAM
          GOTO      SURN9000 IF EOS                * check for e.o.s.
          MATCH     SP30,OPNRSNAM                  * check for spaces
          GOTO      SURN9000 IF EQUAL
          GOTO      SURN9999
.
.--------- Surname is mandatory -----
.
SURN9000  DISPLAY   *P1:24,*B,"Surname Is Mandatory - ",*EL;
          CALL      HITENTER                      * re enter description
          GOTO      SURN0000       
.
SURN9999
          RETURN
+
.**************************************************************************
.*                              GIVN0000                                  *
.*                     Keyin the Given name                               *
.**************************************************************************
.
GIVN0000
          DISPLAY   *P28:7,*EL,UNDLN30;
          KEYIN     *P28:7,*V2LON,OPNRGNAM;
          DISPLAY   *P28:7,*V2LON,OPNRGNAM;
. 
GIVN9999  RETURN
.
.**************************************************************************
.*                              STAT0000                                  *
.*                          Input the Status Indicator                    *
.**************************************************************************
STAT0000  MOVE      SP1,STATIND 
          DISPLAY   *P1:24,*EL,*V2LON,"0 ",*HOFF,"= Active, ":
                               *V2LON,"1 ",*HOFF,"= Non Active":
                    *P28:8,*EL,UNDLN1;
          KEYIN     *P28:8,*V2LON,STATIND;
          DISPLAY   *P28:8,*V2LON,STATIND;
          RESET     STATIND  
          GOTO      STAT0500 IF EOS
          MATCH     SP1,STATIND
          GOTO      STAT8000 IF EQUAL
          MATCH     "0",STATIND                      * 0 = Active
          GOTO      STAT1000 IF EQUAL
          MATCH     "1",STATIND                      * 1 = Non Active
          GOTO      STAT2000 IF EQUAL
          BEEP 
          GOTO      STAT0000                         * invalid entry
.
STAT0500  MOVE      "0",STATIND                      * default to active
          DISPLAY   *P28:8,*EL,*V2LON,STATIND,*HOFF;
.
STAT1000  DISPLAY   *P34:8,*EL,STATUS1;             * 0 = Active
          DISPLAY   *P1:24,*EL;
          GOTO      STAT9999
STAT2000  DISPLAY   *P34:8,*EL,STATUS2;             * 1 = Non Active
          DISPLAY   *P1:24,*EL;
          GOTO      STAT9999
.
.-------- Status indicator is mandatory -----
.
STAT8000  DISPLAY   *P1:24,*B,"Status Indicator Is Mandatory - ",*EL; 
          CALL      HITENTER
          GOTO      STAT0000                         * re enter
.
STAT9999  RETURN
+
.**************************************************************************
.*                         USER0000                                       *
.*                     process user defined fields 1.                     *
.**************************************************************************
USER0000  
          MOVE      "9",UVERT1
          COMPARE   ONE,OPCNUNU1                  * check if user field 1 used
          GOTO      USER0900 IF NOT EQUAL         * "0" = No, "1" = Yes
.
.------- process user defined field 1. -----
.
USER0100  MOVE      SP3,OPNRUSR1                  * clear working variables 
          MOVE      SP20,TDESC
          DISPLAY   *P28:UVERT1,*EL,UNDLN3;
          KEYIN     *P28:UVERT1,*V2LON,OPNRUSR1,*HOFF;
          DISPLAY   *P28:UVERT1,*V2LON,OPNRUSR1,*HOFF;
          RESET     OPNRUSR1
          GOTO      USER0200 IF EOS               * if e.o.s. check if mandatory
          ENDSET    OPNRUSR1
          APPEND    SP3,OPNRUSR1
          RESET     OPNRUSR1
          MATCH     SP3,OPNRUSR1                  * if spaces check if mandatory
          GOTO      USER0200 IF EQUAL
.
.------- check if "?" entered ------
.
          MATCH     QUEST,OPNRUSR1
          GOTO      USER0500 IF NOT EQUAL
          MOVE      CODEQ1,CODE
          CALL      PATCODDS                      * call codes summary screen 
          CALL      RDSP0000                      * call redisplay routine
          GOTO      USER0100
.
.------- if spaces or e.o.s. entered, check if field is mandatory -------
.
USER0200  COMPARE   ZERO,OPCNUNM1               * check if user 1 is mandatory
          GOTO      USER0800 IF EQUAL           * "0" = No, "1" = Yes
          CALL      USER8500                    * if mandatory warn user
          GOTO      USER0100
.
.------- Valid code entered, however check to see if exists in code file ------
.
USER0500  MOVE      SP20,USERDES1                 * clear description
          PACK      KEY5,CODEQ1,OPNRUSR1
          CALL      RDCODE1                       * read patients codes file
          COMPARE   ONE,OVRCD
          GOTO      USER0700 IF NOT EQUAL
          CALL      USER8000                      * call error routine
          GOTO      USER0100                      * re enter
.
USER0700  MOVE      TDESC,USERDES1                * get description
          DISPLAY   *P34:UVERT1,USERDES1;
.
USER0800  ADD       ONE,UVERT1                    * increment line by one.
.
USER0900  RETURN
.
.
.**********************************************************************
.*                         USER1000                                   *
.*                  process user defined field 2.                     *
.**********************************************************************
.
USER1000  COMPARE   ONE,OPCNUNU2            * check if user def 2. is used
          GOTO      USER1900 IF NOT EQUAL
.
USER1100  MOVE      SP3,OPNRUSR2            * clear code
          MOVE      SP20,TDESC
          DISPLAY   *P28:UVERT1,*EL,UNDLN3;
          KEYIN     *P28:UVERT1,*V2LON,OPNRUSR2,*HOFF;
          DISPLAY   *P28:UVERT1,*V2LON,OPNRUSR2,*HOFF;
          RESET     OPNRUSR2
          GOTO      USER1200 IF EOS                    * check for e.o.s.
          ENDSET    OPNRUSR2
          APPEND    SP3,OPNRUSR2
          RESET     OPNRUSR2
          MATCH     SP3,OPNRUSR2                       * check for spaces
          GOTO      USER1200 IF EQUAL
.
          MATCH     QUEST,OPNRUSR2                     * check for "?"
          GOTO      USER1500 IF NOT EQUAL
          MOVE      CODEQ2,CODE
          CALL      PATCODDS                           * call code "?" routine
          CALL      RDSP0000                           * redisplay details 
          GOTO      USER1100                           * re enter
.
.------- if e.o.s. or spaces entered, check if mandatory ------
.
USER1200  COMPARE   ZERO,OPCNUNM2                      * check if mandatory
          GOTO      USER1800 IF EQUAL                  * if not exit routine
          CALL      USER8500                           * if mandatory call error
          GOTO      USER1100                           * re enter
.
.------- valid code entered, however check if on file -------
.
USER1500  MOVE      SP20,USERDES2
          PACK      KEY5,CODEQ2,OPNRUSR2
          CALL      RDCODE1                         * read codes file
          COMPARE   ONE,OVRCD
          GOTO      USER1700 IF NOT EQUAL
          CALL      USER8000                        * if doesn't exist warn user
          GOTO      USER1100                        * re enter
.
.------- get user def description -------
.
USER1700  MOVE      TDESC,USERDES2               * move description to temp var
          DISPLAY   *P34:UVERT1,USERDES2;        * display description
.
USER1800  ADD       ONE,UVERT1                   * increment line number
.
USER1900  RETURN
.
.************************************************************************
.*                            USER2000                                  *
.*                   process user defined field 3                       *
.************************************************************************
.
USER2000  COMPARE   ONE,OPCNUNU3                 * "0" = No, "1" = Yes
          GOTO      USER2900 IF NOT EQUAL
.
USER2100  MOVE      SP3,OPNRUSR3                 * clear code 
          MOVE      SP20,TDESC
          DISPLAY   *P28:UVERT1,*EL,UNDLN3;
          KEYIN     *P28:UVERT1,*V2LON,OPNRUSR3,*HOFF;
          DISPLAY   *P28:UVERT1,*V2LON,OPNRUSR3,*HOFF;
          RESET     OPNRUSR3
          GOTO      USER2200 IF EOS              * check for e.o.s.
          ENDSET    OPNRUSR3
          APPEND    SP3,OPNRUSR3
          RESET     OPNRUSR3
          MATCH     SP3,OPNRUSR3                 * check for spaces
          GOTO      USER2200 IF EQUAL
.
          MATCH     QUEST,OPNRUSR3               * check if "?" entered
          GOTO      USER2500 IF NOT EQUAL
          MOVE      CODEQ3,CODE
          CALL      PATCODDS                     * call codes file "?" routine
          CALL      RDSP0000                     * redisplay details
          GOTO      USER2100                     * re enter 
.
.------- if e.o.s. or spaces entered, check to see if mandatory ------
.
USER2200  COMPARE   ZERO,OPCNUNM3                * if "0" = No, then exit 
          GOTO      USER2800 IF EQUAL
          CALL      USER8500                     * warn user that it's mandatory
          GOTO      USER2100                     * re enter code 
.
.------ valid code entered, however check to see if exists on file -----
.
USER2500  MOVE      SP20,USERDES3                * clear description
          PACK      KEY5,CODEQ3,OPNRUSR3
          CALL      RDCODE1                      * read codes file
          COMPARE   ONE,OVRCD
          GOTO      USER2700 IF NOT EQUAL
          CALL      USER8000                     * warn user if doesn't exists
          GOTO      USER2100                     * re enter 
.
USER2700  MOVE      TDESC,USERDES3               * get description
          DISPLAY   *P34:UVERT1,USERDES3;        * display description
.
USER2800  ADD       ONE,UVERT1                   * increment line number
USER2900  RETURN
.
.**************************************************************************
.*                           USER3000                                     *
.*                  process user defined field 4.                         *
.**************************************************************************
.
USER3000  COMPARE   ONE,OPCNUNU4                 * "0" = No, "1" = Yes
          GOTO      USER3900 IF NOT EQUAL
.
USER3100  MOVE      SP3,OPNRUSR4                 * clear code   
          MOVE      SP20,TDESC
          DISPLAY   *P28:UVERT1,*EL,UNDLN3;
          KEYIN     *P28:UVERT1,*V2LON,OPNRUSR4,*HOFF;
          DISPLAY   *P28:UVERT1,*V2LON,OPNRUSR4,*HOFF;
          RESET     OPNRUSR4
          GOTO      USER3200 IF EOS              * check for e.o.s.
          ENDSET    OPNRUSR4
          APPEND    SP3,OPNRUSR4
          RESET     OPNRUSR4
          MATCH     SP3,OPNRUSR4                 * check for spaces
          GOTO      USER3200 IF EQUAL
.
          MATCH     QUEST,OPNRUSR4               * check if "?" entered
          GOTO      USER3500 IF NOT EQUAL
          MOVE      CODEQ4,CODE
          CALL      PATCODDS                     * call codes "?" routine
          CALL      RDSP0000                     * redisplay details
          GOTO      USER3100                     * re enter
.
.-------- if e.o.s. or spaces entered, check if code is mandatory ------
.
USER3200  COMPARE   ZERO,OPCNUNM4                * check if code is mandatory
          GOTO      USER3800 IF EQUAL
          CALL      USER8500                     * warn user if code mandatory
          GOTO      USER3100                     * re enter code
.
.------- if valid code entered, check if exists on file ------
.
USER3500  MOVE      SP20,USERDES4                * clear description
          PACK      KEY5,CODEQ4,OPNRUSR4
          CALL      RDCODE1                      * read codes file
          COMPARE   ONE,OVRCD
          GOTO      USER3700 IF NOT EQUAL
          CALL      USER8000                     * warn user, code not on file
          GOTO      USER3100                     * re enter code
.
USER3700  MOVE      TDESC,USERDES4               * move description
          DISPLAY   *P34:UVERT1,USERDES4;        * display description
USER3800  ADD       ONE,UVERT1                   * increment line number
.
USER3900  RETURN
.
.***********************************************************************
.*                         USER5000                                    *
.*               process user defined field 5                          *
.***********************************************************************
.
USER4000  COMPARE   ONE,OPCNUNU5                 * "0" = No, "1" = Yes
          GOTO      USER4900 IF NOT EQUAL
USER4100  MOVE      SP3,OPNRUSR5                 * clear code
          MOVE      SP20,TDESC
          DISPLAY   *P28:UVERT1,*EL,UNDLN3;
          KEYIN     *P28:UVERT1,*V2LON,OPNRUSR5,*HOFF;
          DISPLAY   *P28:UVERT1,*V2LON,OPNRUSR5,*HOFF;
          RESET     OPNRUSR5
          GOTO      USER4200 IF EOS              * check for e.o.s.
          ENDSET    OPNRUSR5
          APPEND    SP3,OPNRUSR5
          RESET     OPNRUSR5
          MATCH     SP3,OPNRUSR5                 * check for spaces
          GOTO      USER4200 IF EQUAL
.
          MATCH     QUEST,OPNRUSR5               * check if "?" entered
          GOTO      USER4500 IF NOT EQUAL
          MOVE      CODEQ5,CODE                  * get category
          CALL      PATCODDS                     * call codes file "?" routine
          CALL      RDSP0000                     * redisplay details
          GOTO      USER4100                     * re enter code
.
.-------- e.o.s. or spaces entered, however check to see if mandatory -----
.
USER4200  COMPARE   ZERO,OPCNUNM5                * "0" = No, "1" = Yes
          GOTO      USER4900 IF EQUAL
          CALL      USER8500                     * warn user if mandatory 
          GOTO      USER4100                     * re enter code 
.
.-------- valid code entered, however check if exists ------
.
USER4500  MOVE      SP20,USERDES5                * clear description
          PACK      KEY5,CODEQ5,OPNRUSR5
          CALL      RDCODE1                      * read codes file
          COMPARE   ONE,OVRCD
          GOTO      USER4700 IF NOT EQUAL
          CALL      USER8000                     * warn user if invalid code
          GOTO      USER4100                     * re enter
.
USER4700  MOVE      TDESC,USERDES5               * get description
          DISPLAY   *P34:UVERT1,USERDES5;
USER4900  RETURN
.
.************************************************************************
.*                       USER8000                                       *
.*        Error message to warn user if an invalid code entered         *
.************************************************************************
.
USER8000  DISPLAY   *P1:24,*B,"Code Does Not Exist - ",*EL;
          CALL      HITENTER
          RETURN
.
.************************************************************************
.*                          USER8500                                    *
.*        Message to warn user that Code is mandatory if applicable     *
.************************************************************************
.
USER8500  DISPLAY   *P1:24,*B,"Code Is Mandatory - ",*EL;
          CALL      HITENTER
          RETURN
.
.**************************************************************************
.*                             RDSP0000                                   *
.*                  Redisplay the Screen After a "?"                      *
.**************************************************************************
.
RDSP0000
          CALL      SCR0000                          * call screen
.
          DISPLAY   *P28:4,*V2LON,OPNRCODE:
                    *P28:6,OPNRSNAM:
                    *P28:7,OPNRGNAM:
                    *P28:8,STATIND;
.
          MATCH     "1",STATIND                     * "1" = Non Active
          GOTO      RDSP1000 IF EQUAL
.
          DISPLAY   *P34:8,"Active";                * "0" = Active
          GOTO      RDSP2000
RDSP1000
          DISPLAY   *P34:8,"Non Active";
.
.-------- redisplay user defined fields ------
.
RDSP2000  MOVE      "9",CVERT                      * set first line number
          COMPARE   ZERO,OPCNUNU1                  * check if switched on
          GOTO      RDSP2100 IF EQUAL
          DISPLAY   *P28:CVERT,*V2LON,OPNRUSR1,*HOFF:
                    *P34:CVERT,USERDES1;
          ADD       ONE,CVERT                      * increment line number
.
.-------- redisplay user defined field 2. if switched on ------
.
RDSP2100  COMPARE   ZERO,OPCNUNU2                  * check if switched on
          GOTO      RDSP2200 IF EQUAL
          DISPLAY   *P28:CVERT,*V2LON,OPNRUSR2,*HOFF:
                    *P34:CVERT,USERDES2;
          ADD       ONE,CVERT                      * increment line number
.
.-------- redisplay user defined field 3. if switched on -------
.
RDSP2200  COMPARE   ZERO,OPCNUNU3                  * check if switched 
          GOTO      RDSP2300 IF EQUAL
          DISPLAY   *P28:CVERT,*V2LON,OPNRUSR3,*HOFF:
                    *P34:CVERT,USERDES3;
          ADD       ONE,CVERT                      * increment line number
.
.------- redisplay user defined field 4. if switched on ------
.
RDSP2300  COMPARE   ZERO,OPCNUNU4                  * check if switched on
          GOTO      RDSP2400 IF EQUAL
          DISPLAY   *P28:CVERT,*V2LON,OPNRUSR4,*HOFF:
                    *P34:CVERT,USERDES4;
          ADD       ONE,CVERT                      * increment line number
. 
.------- redisplay user defined field 5. if switched on ------
.
RDSP2400  COMPARE   ZERO,OPCNUNU5                  * check if switched on
          GOTO      RDSP9999 IF EQUAL
          DISPLAY   *P28:CVERT,*V2LON,OPNRUSR5,*HOFF:
                    *P34:CVERT,USERDES5;           * display details
RDSP9999
          RETURN
+
.**************************************************************************
.*                              WRT0000                                   *
.*                   Write Data to the Nurse File                         *
.**************************************************************************
.
WRT0000
          DISPLAY   *P1:24,"Writing to Nurse File - ":
                           *V2LON,*BLINKON,"Please Wait ",*W:
                    *P1:24,*EL;
.
          MOVE      STATIND,OPNRSTAT                 * move temp indic to var.
          PACK      KEY6,OPNRCODE
          CALL      WROPNUR1                         * write to Nurse file
.
          RETURN
+
.**************************************************************************
.*                              UPD0000                                   *
.*                   Update Data in the Nurse File                        *
.**************************************************************************
.
UPD0000
          DISPLAY   *P1:24,"Updating Nurse File - ":
                           *V2LON,*BLINKON,"Please Wait",*W:
                    *P1:24,*EL;
.
          MOVE      STATIND,OPNRSTAT              * move status indicator
          CALL      UPOPNUR1                      * update Nurse file
.
          RETURN
+
.**************************************************************************
.*                              DEL0000                                   *
.*               Delete a Record from the Nurse File                      *
.**************************************************************************
.
DEL0000
          DISPLAY   *P1:24,"Deleting Nurse File Record ":
                           *V2LON,*BLINKON,"- Please Wait",*W:
                    *P1:24,*EL;
.
          CALL      DEOPNUR1                      * delete record from 
.                                                 * Nurse file
          RETURN
+
.**************************************************************************
.*                             CONT0000                                   *
.*                      O.K. to Continue for Delete                       *
.**************************************************************************
.
CONT0000
          MOVE      FALSE,EXIT
.
          DISPLAY   *P1:24,"O.K. to Delete Record (",*V2LON,"Y",*HOFF:
                           "/",*V2LON,"N",*HOFF,") ";
          KEYIN     *V2LON,ANS;
.
          CMATCH    ANSY,ANS                        * Yes delete record 
          GOTO      CONT9999 IF EQUAL
.
          CMATCH    ANSN,ANS                        * No don't delete
          GOTO      CONT9000 IF EQUAL
.
          BEEP
          GOTO      CONT0000                        * invalid entry renter
CONT9000
          MOVE      TRUE,EXIT                       * set exit
CONT9999
          RETURN
+
.********************************************************************** 
.*              I/O INCLUDES FOR FILES AND GENERAL ROUTINES           *
.**********************************************************************
.
         INC       STD001IO
         INC       PATCODKY                * patient codes file routine
         INC       PATCODIO/INC            * patient i/o for codes
         INC       OPRNURIO/INC            * Nurse i/o for operating system
.
.
.
