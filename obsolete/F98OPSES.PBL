.***************************************************************************
.* System    :   Theatre                                                   *
.* Program   :   F98OPSES                                                  *
.* Desc      :   Populate theatre session file hospital id                 *
.***************************************************************************
.* Date      :   02/10/2006                                                *
.* Author    :   Ebon Clements                                             *
.* Function  :   This program will populate the session file hospital      *
.*           :   with the hospital from the session master. If no exact    *
.*           :   session master is found(AdHoc or start time change) it    *
.*           :   will use the session master from the same day             *
.*           :                                                             *
.*           :   NOTE: This should only be run for Multi Hopsital sites.   *
.*           :         Please ensure all session masters have a hospital   *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       OPRSESFD/INC
          INC       OPRSEMFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
PRGID     INIT      "F98OPSES"
PRGNAM    INIT      "Populate theatre session file hospital id"
VERSION   INIT      "V11.03.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      OPRSESA1,"oprsesaf"
          OPEN      OPRSEMA1,"oprsemaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  PACK      KEY19,SP70
          CALL      RSOPSES1
PROC1000  CALL      RKOPSES1
          BRANCH    OVRCD,PROC9999
.
          PACK      OPSEHOSP,OPSEHOSP,SP70
.
          MATCH     SP70,OPSEHOSP                * Skip if hospital populated
          GOTO      PROC1000 IF NOT EQUAL
.
          PACK      KEY12,OPSECLIN,OPSEDAY,OPSETIME,SP70
          CALL      RDOPSEM1
          BRANCH    OVRCD,PROC4000 
.
          MATCH     SP70,OPSMHOSP
          GOTO      PROC1000 IF EQUAL
.
          GOTO      PROC8000
.
PROC4000  PACK      KEY12,OPSECLIN,OPSEDAY,SP70
          CALL      RSOPSEM1
          CALL      RKOPSEM1                * Check for master on same day
          BRANCH    OVRCD,PROC1000
.
          MATCH     OPSECLIN,OPSMCLIN
          GOTO      PROC1000 IF NOT EQUAL
.
          COMPARE   OPSEDAY,OPSMDAYI
          GOTO      PROC1000 IF NOT EQUAL
.
          MATCH     SP70,OPSMHOSP
          GOTO      PROC1000 IF EQUAL
.
PROC8000  MOVE      OPSMHOSP,OPSEHOSP            * Populate hospital
          CALL      UPOPSES1                     * Update session
.
          GOTO      PROC1000
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       OPRSESIO/INC
          INC       OPRSEMIO/INC
