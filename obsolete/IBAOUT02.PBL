.******************************************************************************
.*                                                                            *
.*                        P R O G R A M   S U M M A R Y                       *
.*                        -------------   -------------                       *
.*                                                                            *
.*      PROGRAM NAME   : IBAOUT02                                             *
.*                                                                            *
.*      FUNCTION       : CLINIC TYPE MAINTENANCE                              *
.*                                                                            *
.*      DATE WRITTEN   : 22/06/1988                                           *
.*                                                                            *
.*      AUTHOR         : E.PHAN                                               *
.*                                                                            *
.*      MODIFICATIONS  :                                                      *
.*        V5.01.02              L.P.             SRF 102514                   *
.*                  Recompiled with new OUTAUXFD.INC                          *
.*        V5.01.03  05/10/1989  K.Peak           SRF 102601                   *
.*                  Replaced uppercase audit to lowercase                     *
.*        V5.01.04  13/03/1992  Justin Coates  (Southland changes)            *
.*                  Added the keyin of Extra screen Id's                      *
.*        V5.01.05  08/07/1992  i chung          SRF 115638                   *
.*                  Fixed incorrect AUTYPE set for audit file (fr "I" to "T") *
.*        V5.01.06  21/09/1992  Graeme Williams                               *
.*                  Removed *FILL on KEYIN's                                  *
.*        V5.01.07  10/12/1992    Justin Coates  (DUDLEY changes)             *
.*                  Extra screens must now be >= 10                           *
.*        V5.01.08  20/04/1994    WHIRLPOOL                                   *
.*                  removed updflock label because now in ibaioovr            *
.******************************************************************************
.$$$$$
... NOTE: error messages should be of the form
.         DISPLAY   *P1:24,*B,*EL,"Invalid Code.  ";
.         CALL      HITENTER
... as per the standards manual point 2.04 - Error Messages
.$$$$$
.
          INC       STD001FD
          INC       OUTAUXFD/INC
          INC       OUTCONFD/INC
          INC       OUTCTYFD/INC
          INC       PATCODFD/INC
          INC       SCRMASFD/INC
+
.         Variables and Constants
.
CHG       FORM      1
CODE      INIT      "CG"
COL       FORM      3
DIM3      DIM       3
DIM14     DIM       14
DNAME     DIM       25
FIELD     FORM      1
HDEFWH    DIM       5
HMULTWH   FORM      1
MODE      DIM       1
MODE1     DIM       1
SRDIM20   DIM       20
SRDIM20A  DIM       20
VERT      FORM      2
VSITE     DIM       6
.
PRGID     INIT      "IBAOUT02"
PRGNAM    INIT      "Clinic Type Maintenance"
VERSION   INIT      "V10.13.00"
+
.
.         START OF PROGRAM
.
          MOVE      IDDD TO VSITE      /* Site code held in common */
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening";
.
          PACK      CFNAME WITH UID,FILCTYA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCTYA1,CFNAME
.
          PACK      CFNAME WITH UID,FILCTYA2
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCTYA2,CFNAME
.
          PACK      CFNAME WITH UID,FILCTYA3
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCTYA3,CFNAME
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"scrmasaf";
          OPEN      SCRMASA1,"scrmasaf"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,THIRTY1;*70,HOAUDT
          CLOSE     CONTROLF
.
          MOVE      SP1,AUMODE
          BRANCH    HOAUDT OF MENU
          MOVE      ANST,AUTYPE
          REP       "Tt",AUTYPE
          CALL      STRTAUDX
.
MENU      HLINE     *G37,2,52,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Insert":
                    *P1:6,*V2LON,TWO,*HOFF," = Change":
                    *P1:7,*V2LON,THREE,*HOFF," = Delete":
                    *P1:9,*EL,"Select Option : ";
.
BEGIN     KEYIN     *P17:9,*EL,*DV,UNDLN1,*P17:9,*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
REPEAT    MOVE      SP6,OCTSITE
          MOVE      SP6,OCTCTYP
          MOVE      SP3,OCTGRP
          MOVE      SP30,OCTDESC
          MOVE      SP2,OCTXBOK
          MOVE      SP2,OCTXATT
.
          BRANCH    OPTION OF INSERT,CHANGE,DELETE
          BEEP
          GOTO      BEGIN
.
.         ADD A CLINIC TYPE
.
INSERT    CALL      DISPI
          CALL      DISPSC
          MOVE      ZERO,CHG
.
          MOVE      ANSI,MODE1
          MOVE      MODE1,MODE
          BRANCH    HOAUDT OF KCLIN
          MOVE      ANSI,AUMODE
          GOTO      KCLIN
.
.         CHANGE A CLINIC TYPE
.
CHANGE    CALL      DISPC
          CALL      DISPSC
          MOVE      ZERO,CHG
.
          MOVE      ANSC,MODE1
          MOVE      MODE1,MODE
          BRANCH    HOAUDT OF KCLIN
          MOVE      ANSC,AUMODE
          GOTO      KCLIN
.
.         DELETE A CLINIC TYPE
.
DELETE    CALL      DISPD
          CALL      DISPSC
.
          MOVE      ANSD,MODE1
          MOVE      MODE1,MODE
          BRANCH    HOAUDT OF KCLIN
          MOVE      ANSD,AUMODE
          GOTO      KCLIN
.
DELCS     CALL      DISPDA
.
REKEY     KEYIN     *P1:24,*EL,"Correct CLINIC TYPE (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF:
                    ") ? ",*V2LON,ANS;
          MATCH     ANSY,ANS
          GOTO      REKEY1 IF EQUAL
          MATCH     ANSN,ANS
          GOTO      REPEAT IF EQUAL
          BEEP
          GOTO      REKEY
.
REKEY1    KEYIN     *P1:24,*EL,"Are you sure you want to delete this CLINIC ":
                               "TYPE (",*V2LON,*DV,ANSY,*HOFF,*DV,SLASH:
                               *V2LON,*DV,ANSN,*HOFF,") ? ",*V2LON,ANS;
          MATCH     ANSY,ANS
          GOTO      WARDEL IF EQUAL
          MATCH     ANSN,ANS
          GOTO      REPEAT IF EQUAL
          BEEP
          GOTO      REKEY1
.
WARDEL    PACK      KEY12 WITH OCTSITE,OCTCTYP
          CALL      DECTYA1
.
          BRANCH    HOAUDT OF ENDDELT
.
          MOVE      "CLINIC TYPE        ",AUDESC
          PACK      AUITEM WITH OCTSITE,SLASH,OCTCTYP,SP20
          PACK      AUCONBEF WITH SP30,SP30
          PACK      AUCONAFT WITH SP30,SP20
          CALL      WRITAUDX
          CALL      POSTAUDX
.
ENDDELT   DISPLAY   *P1:24,*EL,*P55:24,*V2LON,"** Deleted **"
          GOTO      REPEAT
.
.------------------------------------------------------------------------------
.         INPUT CLINIC TYPE CODE
.------------------------------------------------------------------------------
KCLIN     MOVE      SP6,OCTCTYP
          KEYIN     *P31:4,*EL,*DV,UNDLN6,*P31:4,*V2LON,OCTCTYP:
                    *P31:4,*DV,OCTCTYP;
          RESET     OCTCTYP
          GOTO      MENU IF EOS
.
          ENDSET    OCTCTYP
          APPEND    SP6,OCTCTYP
          RESET     OCTCTYP
.
          CMATCH    QUEST TO OCTCTYP
          GOTO      NOTQUE1 IF NOT EQUAL
          CALL      OUTDSCTY
          MOVE      SP6,OCTCTYP
          CALL      DISPSC
          GOTO      KCLIN
.
NOTQUE1   DISPLAY   *P31:4,*V2LON,OCTCTYP;
.
          PACK      KEY12 WITH VSITE,OCTCTYP
          CALL      RDCTYA1
          BRANCH    OVRCD OF ENDNO,INUSE
.
          PACK      AUITEM WITH OCTSITE,SLASH,OCTCTYP,SP20
          CMATCH    ANSC,MODE
          GOTO      NEXTM IF NOT EQUAL
          CALL      DISPDA
          GOTO      SFIELD
.
NEXTM     CMATCH    ANSD,MODE
          GOTO      DELCS IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Clinic Type Code already exists.  ";
          CALL      HITENTER
          GOTO      KCLIN
.
ENDNO     CMATCH    ANSI,MODE
          GOTO      KGROUP IF EQUAL
.
          DISPLAY   *P1:24,*EL,*B,"Clinic Type Code does not exist.  ";
          CALL      HITENTER
          GOTO      KCLIN
.
INUSE     DISPLAY   *P1:24,*EL,*B,"Clinic Type in use on Terminal somewhere.  ";
          CALL      HITENTER
          GOTO      KCLIN
+
.------------------------------------------------------------------------------
.         INPUT GROUP TYPE
.------------------------------------------------------------------------------
KGROUP    MOVE      " Group Type         ",AUDESC
          PACK      AUCONBEF WITH OCTGRP,SP30,SP20
.
KGROUP1   KEYIN     *P31:6,*EL,*DV,UNDLN3,*P31:6,*V2LON,OCTGRP:
                    *P31:6,*DV,OCTGRP;
.
          ENDSET    OCTGRP
          APPEND    SP6,OCTGRP
          RESET     OCTGRP
.
          MATCH     SP6 TO OCTGRP
          GOTO      KGROUP3 IF EQUAL
.
. "?" option to check doctor's codes
.
          CMATCH    QUEST TO OCTGRP
          GOTO      KGROUP2 IF NOT EQUAL
.
          CALL      PATCODDS
          CALL      DISPSC
          MOVE      SP6,OCTGRP
          CALL      DISPDA
          GOTO      KGROUP1
.
KGROUP2   MOVE      SP20 TO TDESC
          PACK      KEY5 WITH CODE,OCTGRP
          CALL      RDCODE1
          BRANCH    OVRCD TO KGROUP4
.
          DISPLAY   *P41:6,*V2LON,TDESC;
.
          PACK      AUCONAFT WITH OCTGRP,SP30,SP20
.
          CMATCH    ANSC,AUMODE
          CALL      WRITAUDX IF EQUAL
.
          CMATCH    ANSI TO MODE
          GOTO      KDESC IF EQUAL
          MOVE      ONE,CHG
          GOTO      SFIELD
.
KGROUP3   DISPLAY   *P1:24,*EL,*B,"Input is mandatory.  ";
          CALL      HITENTER
          GOTO      KGROUP 
.
KGROUP4   DISPLAY   *P1:24,*EL,*B,"Invalid Group Type.  ";
          CALL      HITENTER
          GOTO      KGROUP
+
.------------------------------------------------------------------------------
.         INPUT DESCRIPTION
.------------------------------------------------------------------------------
KDESC     MOVE      " Description        ",AUDESC
          PACK      AUCONBEF WITH OCTDESC,SP30,SP20
.
          KEYIN     *P31:7,*EL,*DV,UNDLN30,*P31:7,*V2LON,OCTDESC:
                    *P31:7,*DV,OCTDESC;
.
          ENDSET    OCTDESC
          APPEND    SP30,OCTDESC
          RESET     OCTDESC
.
          MATCH     SP30 TO OCTDESC
          GOTO      ERRDESC IF EQUAL
.
          PACK      AUCONAFT WITH OCTDESC,SP30,SP20
.
          CMATCH    ANSC,AUMODE
          CALL      WRITAUDX IF EQUAL
.
          CMATCH    ANSI TO MODE
          GOTO      KXBOK000 IF EQUAL
          MOVE      ONE,CHG
          GOTO      SFIELD
.
ERRDESC   DISPLAY   *P1:24,*EL,*B,"Blanks not allowed.  ";
          CALL      HITENTER
          GOTO      KDESC
+
.------------------------------------------------------------------------------
.                   KXBOK000
.         Input Bookings extra screen Id 
.------------------------------------------------------------------------------
KXBOK000  DISPLAY   *P1:24,"Screen Id must be between 10 and 99",*EL;
          KEYIN     *P31:8,*DV,UNDLN2:
                    *P31:8,*V2LON,OCTXBOK:
                    *P31:8,*DV,OCTXBOK;
.
          PACK      OCTXBOK,OCTXBOK,SP20
          MATCH     SP2,OCTXBOK
          GOTO      KXBOK999 IF EQUAL       * nothing entered
.
          MATCH     "10",OCTXBOK
          GOTO      KXBOK100 IF NOT LESS    * valid
.
          DISPLAY   *P1:24,*EL,*B,"Invalid Screen Id.  ";
          CALL      HITENTER
          GOTO      KXBOK000
.
KXBOK100  MOVE      "IBAOUT66",KEY8
          PACK      KEY10,KEY8,OCTXBOK
          CALL      RDSCMA1
          COMPARE   ZERO,OVRCD
          GOTO      KXBOK999 IF EQUAL       * valid code
.
          DISPLAY   *P1:24,*EL,*B,"Screen Id. not set up.  ";
          CALL      HITENTER
          GOTO      KXBOK000
.
KXBOK999  MOVE      ONE,CHG
          DISPLAY   *P1:24,*EL;
          CMATCH    ANSC,MODE
          GOTO      SFIELD IF EQUAL
+
.------------------------------------------------------------------------------
.                   KXATT000
.         Input Attendees extra screen Id
.------------------------------------------------------------------------------
KXATT000  DISPLAY   *P1:24,"Screen Id must be between 10 and 99",*EL;
          KEYIN     *P31:9,*DV,UNDLN2:
                    *P31:9,*V2LON,OCTXATT:
                    *P31:9,*DV,OCTXATT;
.
          PACK      OCTXATT,OCTXATT,SP20
          MATCH     SP2,OCTXATT
          GOTO      KXATT999 IF EQUAL       * nothing entered
.
          MATCH     "10",OCTXATT
          GOTO      KXATT100 IF NOT LESS    * valid
.
          DISPLAY   *P1:24,*EL,*B,"Invalid Screen Id.  ";
          CALL      HITENTER
          GOTO      KXATT000
.
.         validate id entered
.
KXATT100  MOVE      "IBAOUT66",KEY8
          PACK      KEY10,KEY8,OCTXATT
          CALL      RDSCMA1
          COMPARE   ZERO,OVRCD
          GOTO      KXATT999 IF EQUAL       * valid code
.
          DISPLAY   *P1:24,*EL,*B,"Screen Id. not set up.  ";
          CALL      HITENTER
          GOTO      KXATT000
.
KXATT999  MOVE      ONE,CHG
          DISPLAY   *P1:24,*EL;
          GOTO      SFIELD
+
.       SELECT THE FIELD TO UPDATE
.
SFIELD    KEYIN     *P1:24,*EL,"Select field (0 to post) ? ",*V2LON,FIELD;
          COMPARE   ZERO,FIELD
          GOTO      POST IF EQUAL
          MOVE      ANSC TO MODE
.
SBRCH     BRANCH    FIELD OF KGROUP,KDESC,KXBOK000,KXATT000
.
          BEEP
          GOTO      SFIELD
.
POST      KEYIN     *P1:24,*EL,"Ok to post (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH:
                    *V2LON,*DV,ANSN,*HOFF,*DV,SLASH:
                    *V2LON,*DV,ANSC,*HOFF,") ? ",*V2LON,ANS;
.
          MATCH     ANSN,ANS
          GOTO      SFIELD IF EQUAL
          MATCH     ANSY,ANS
          GOTO      POSTCNT IF EQUAL
          MATCH     ANSC,ANS
          GOTO      WRONG IF NOT EQUAL
.
          BRANCH    HOAUDT OF REPEAT
          CALL      CANCAUDX
          GOTO      REPEAT
.
WRONG     BEEP
          GOTO      POST
.
.         CHECK CHG INDICATOR TO SEE IF ANYTHING WAS CHANGED
.
POSTCNT   CMATCH    ANSC,MODE1
          GOTO      WARWR IF NOT EQUAL
.
          COMPARE   ZERO,CHG
          GOTO      REPEAT IF EQUAL
          CALL      UPCTYA1
          GOTO      ENDPST
.
WARWR     MOVE      IDDD,OCTSITE
          PACK      KEY12 WITH OCTSITE,OCTCTYP
          CALL      WRCTYA1
          BRANCH    OVRCD OF TOOLATE
.
          BRANCH    HOAUDT OF ENDPST
.
          MOVE      " CLINIC TYPE :     ",AUDESC
          PACK      AUITEM WITH OCTSITE,SLASH,OCTCTYP,SP20
          PACK      AUCONBEF WITH SP30,SP20
          PACK      AUCONAFT WITH SP30,SP20
          CALL      WRITAUDX
.
ENDPST    CMATCH    SP1,AUMODE
          CALL      POSTAUDX IF NOT EQUAL
.
          DISPLAY   *P40:24,*EL,*V2LON:
                    "*** Data Successfully Posted ***"
          GOTO      REPEAT
.
TOOLATE   DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Clinic Type Code already exists ** ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL
          GOTO      REPEAT
.
.         MENUS FOR EACH MODE
.
DISPI     DISPLAY   *P52:2,*V2LON,"- Insert ";
          RETURN
.
DISPC     DISPLAY   *P52:2,*V2LON,"- Change ";
          RETURN
.
DISPD     DISPLAY   *P52:2,*V2LON,"- Delete ";
          RETURN
+
.*********************************************************************
.         MAIN SCREEN
.*********************************************************************
DISPSC    DISPLAY   *P1:4,*EF:
                    *P1:4,"   Clinic Type              :":
                    *P1:6,"1. Group Type               :":
                    *P1:7,"2. Description              :":
                    *P1:8,"3. Extra Booking Screen Id  :":
                    *P1:9,"4. Extra Attendee Screen Id :";
          RETURN
+
.*********************************************************************
.         SUBROUTINE TO DISPLAY DATA ONTO THE SCREEN
.*********************************************************************
DISPDA    MOVE      SP20 TO TDESC
          MATCH     SP3,OCTGRP
          GOTO      DISPDA1 IF EQUAL
          PACK      KEY5 WITH CODE,OCTGRP
          CALL      RDCODE1
.
DISPDA1   DISPLAY   *P31:4,*V2LON,OCTCTYP:
                    *P31:6,*V2LON,OCTGRP,*HOFF,*P41:6,TDESC,*V2LON:
                    *P31:7,OCTDESC:
                    *P31:8,OCTXBOK,*P31:9,OCTXATT;
          RETURN
+
.
ENDIT     BRANCH    HOAUDT OF ABORT
          CALL      STOPAUDX
.
ABORT     CHAIN     PGM
          STOP
.
+
.==============================================================================
.
          INC       STD001IO
          INC       OUTDSCTY
          INC       PATCODKY
.
          INC       OUTAUXIO/INC
          INC       OUTCTYIO/INC
          INC       PATCODIO/INC
          INC       SCRMASIO/INC
+
