.*******************************************************************************
.    System          : Theatre System
.    Include Function: OPTMALLC.PBL
.    Description     : Routines concerning operating team personnel
.*******************************************************************************
.    Date            : 11/10/1990 Almost the end of a good year's programming.
.    Author          : Bill Dew
.
.    Modifications   : 16/01/91 ROD          
.                      Added code to get 1st Anaesthetist in default session
.                      24/01/91 Justin Coates
.                      Set up AURNO before calling OPTMALLC in OPBTMALC
.                      25/06/91 Justin Coates    SRF 108585
.                      If not on admission file. check bokrc1af and set AURNO
.                      26/07/91 ROD              SRF 109233
.                      Added 2nd Anaesthetist to defaults (insert mode)       
.                      19/08/92 J.Tan     SRF 116653
.                      Mods to exlude inactive nurses
.                      05/10/92 Graeme Williams  SRF 117811
.                      Set up PATNAME variable
.*                  08/05/96   Howellsy       5.04                            *
.*                  Added 2 Nurse Types & 2 Nurse Codes                       *
.*                  05/08/96   Howellsy       srf 641783                      *
.*                  Only display 2nd Team Screen if a Team user defined code  *
.*                  is being used.                                            *
.*******************************************************************************
.  
.    FUNCTION ROSTER  DESCRIPTION
.    ---------------  ------------------------------------------------------
.    OPSTMALC         The function obtains default session operating team.
.
.    OPBTMALC         The function Inputs team allocation details.
.
.*******************************************************************************
.
.    INCLUDES AND FILES.
.
.    FD's  : OPR001FD, OPRCONFD, OPRDEAFD, OPRDEBFD, PATDOCFF, PATCODFD
.                                1      
.            OPRNURFD, 
.
.    I/O's : OPRDEAIO, OPRDEBIO, PATIODOC, PATIOCOD, OPRNURIO
.
.    PBL's : STDOPR01, PATCODDS, PATDOCDS, OPRNURDS
.
.*******************************************************************************
.                                 OPSTMALC
.    The function obtains default session operating team.
.
.    Function : Display current team allocation, and allow the user to
.               modify it. It will then update the OPRSESFD file with the new
.               default team.
.
.    PARAMETERS.
.    CURSESS  : Current session header Date time and clinic/doctor
.
.    RETURN PARAMETERS.
.    EXIT = 0 : OK
.    EXIT = 1 : Cancel or exitchar keyin
.*******************************************************************************
OPSTMALC  DISPLAY   *PSUBOPT1:2,*V2LON,"- (Team Alloc. for Session) ";
          MOVE      ZERO,EXIT
          PACK      KEY19,CURSESS,SP20
          CALL      RDOPSES1           * get session details
          BRANCH    OVRCD,STMALC99
.
          MOVE      TWO,AUDIFLAG
          CALL      AUSES000           * write a B audit to session file
.
          CALL      TMSTB000           * transfer OPRSESFD vars to OPRDEBFD
.
          MOVE      SP8,OPDAADMN       * signal default session team.
          MOVE      ONE,OPDBTEAM       * signal 1st team.
          CALL      OPTMALLC           * do team allocation
          BRANCH    EXIT,STMALC77      * exit=1 if cancel or exitchar
.
          CALL      OPT2MALL           * do team allocation screen 2
          BRANCH    EXIT,STMALC77      * exit=1 if cancel or exitchar
.
STMALC50  COMPARE   ONE,MODEFLAG       * test if anything was changed
          GOTO      STMALC77 IF NOT EQUAL
.
          CALL      TMBTS000           * Transfer OPRDEBFD vars to OPRSESFD
          CALL      GANAE000           * get 1st anaesthetist in def team   
          CALL      UPOPSES1           * update session file
.
          MOVE      THREE,AUDIFLAG
          CALL      AUSES000           * write C audit
          MOVE      ZERO,EXIT
          GOTO      STMALC99
.
. ------- nothing changed ---------------
.
STMALC77  MOVE      FIVE,AUDIFLAG
          CALL      AUSES000           * erase B audit
          MOVE      ZERO,EXIT
.
STMALC99  RETURN
+
.*******************************************************************************
.                                 TMSTB000
.    Transfer session team to specialise team vars
.*******************************************************************************
TMSTB000
          PACK      DESC55,OPSETYD1,OPSETYD2,OPSETYD3,OPSETYD4:
                           OPSETYD5,OPSETYD6,OPSETYD7,OPSETYD8
          UNPACK    DESC55,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                           OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
.
          PACK      DESC55,OPSEDCC1,OPSEDCC2,OPSEDCC3,OPSEDCC4:
                           OPSEDCC5,OPSEDCC6,OPSEDCC7,OPSEDCC8
          UNPACK    DESC55,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                           OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
.
          PACK      DESC55,OPSELVD1,OPSELVD2,OPSELVD3,OPSELVD4:
                           OPSELVD5,OPSELVD6,OPSELVD7,OPSELVD8
          UNPACK    DESC55,OPDBLVD1,OPDBLVD2,OPDBLVD3,OPDBLVD4:
                           OPDBLVD5,OPDBLVD6,OPDBLVD7,OPDBLVD8
.
          PACK      DESC55,OPSETYN1,OPSETYN2,OPSETYN3,OPSETYN4:
                           OPSETYN5,OPSETYN6:
                           OPSENUC1,OPSENUC2,OPSENUC3,OPSENUC4:
                           OPSENUC5,OPSENUC6
.
          UNPACK    DESC55,OPDBTYN1,OPDBTYN2,OPDBTYN3,OPDBTYN4:
                           OPDBTYN5,OPDBTYN6:
                           OPDBNUC1,OPDBNUC2,OPDBNUC3,OPDBNUC4:
                           OPDBNUC5,OPDBNUC6
.
          PACK      DESC55,OPSEUSR1,OPSEUSR2,OPSEUSR3,OPSEUSR4
.
          UNPACK    DESC55,OPDBUSR1,OPDBUSR2,OPDBUSR3,OPDBUSR4
.
TMSTB999  RETURN
+
.*******************************************************************************
.                                 TMBTS000
.    Transfer OPRDEBFD to OPRSESFD
.*******************************************************************************
TMBTS000
          PACK      DESC55,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                           OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
          UNPACK    DESC55,OPSETYD1,OPSETYD2,OPSETYD3,OPSETYD4:
                           OPSETYD5,OPSETYD6,OPSETYD7,OPSETYD8
.
          PACK      DESC55,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                           OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
          UNPACK    DESC55,OPSEDCC1,OPSEDCC2,OPSEDCC3,OPSEDCC4:
                           OPSEDCC5,OPSEDCC6,OPSEDCC7,OPSEDCC8
.
          PACK      DESC55,OPDBLVD1,OPDBLVD2,OPDBLVD3,OPDBLVD4:
                           OPDBLVD5,OPDBLVD6,OPDBLVD7,OPDBLVD8
          UNPACK    DESC55,OPSELVD1,OPSELVD2,OPSELVD3,OPSELVD4:
                           OPSELVD5,OPSELVD6,OPSELVD7,OPSELVD8
.
          PACK      DESC55,OPDBTYN1,OPDBTYN2,OPDBTYN3,OPDBTYN4:
                           OPDBTYN5,OPDBTYN6:
                           OPDBNUC1,OPDBNUC2,OPDBNUC3,OPDBNUC4:
                           OPDBNUC5,OPDBNUC6
.
          UNPACK    DESC55,OPSETYN1,OPSETYN2,OPSETYN3,OPSETYN4:
                           OPSETYN5,OPSETYN6:
                           OPSENUC1,OPSENUC2,OPSENUC3,OPSENUC4:
                           OPSENUC5,OPSENUC6
.
          PACK      DESC55,OPDBUSR1,OPDBUSR2,OPDBUSR3,OPDBUSR4
          UNPACK    DESC55,OPSEUSR1,OPSEUSR2,OPSEUSR3,OPSEUSR4
.
TMBTS999  RETURN
+
.*******************************************************************************
.                                 OPBTMALC
.    The function obtains operating teams for a patient
.
.    Function : Will ask for the team number they want to modify (defaults to 1)
.               Will display the current team allocation, and allow the user to
.               modify it. If no current team allocation for this booking,
.               the default allocation for the session will be displayed.
.               It will then update the OPRDEBFD file with the new team.
.
.    PARAMETERS.
.    CURSESS  : Current session header Date time and clinic/doctor
.    CURCASE  : Patient operation case number
.
.    RETURN PARAMETERS.
.    EXIT = 0 : OK
.    EXIT = 1 : Cancel or exitchar keyin
.*******************************************************************************
OPBTMALC  DISPLAY   *PSUBOPT2:2," (Patient Team Alloc.) ";
.
.    Get operating team number.  Team Number one will always be the default
.    team on the session file otherwise a specialise team on the oprdetbf.
.
          PACK      KEY23,CURSESS,ZERO,CURCASE,SP30
          CALL      RDOPDEA1                * get unique id
          BRANCH    OVRCD,BTMALC90          * invalid case number
.
          MOVE      OPDAADMN,KEY8           * set up admission numbr
          CALL      RDMISS1                 * read admission file (opr43)
          COMPARE   ZERO,OVRCD
          GOTO      BTMALC00 IF EQUAL       * valid admission
.
          CALL      RDBKREC1                * read booking file (opr42)
          BRANCH    OVRCD,BTMALC88          * invalid visit number
          MOVE      BKREURNO,AURNO          * set UR number
.
BTMALC00  MOVE      ONE,OPDBTEAM            * default to one
          DISPLAY   *P1:24,*EL:
                    "Please enter the team number to be allocated : ",OPDBTEAM;
          KEYIN     *P48:24,*DE,*RV,OPDBTEAM,*P1:24,*EL
.
          MATCH     "1",OPDBTEAM            * Test for team one
          GOTO      BTMALC99 IF LESS        * Test for invalid team number
          GOTO      BTMALC44 IF NOT EQUAL
.
.         team one entered
.
          PACK      KEY11,OPDAUNIQ,OPDBTEAM
          CALL      RDOPDEB1
          BRANCH    OVRCD,BTMALC22          * no team one on file
.
BTMALC10  MOVE      TWO,AUDIFLAG            * set for before change audit
          CALL      AUDEB000                * write B audit
.
.         do team allocation (code jumps back to here from many spots)
.
BTMALC11  CALL      OPTMALLC                * do team allocation
          BRANCH    EXIT,BTMALC90           * cancel selected
.
          CALL      OPT2MALL           * do team allocation screen 2
          BRANCH    EXIT,BTMALC90      * exit=1 if cancel or exitchar
.
          PACK      KEY11,OPDAUNIQ,OPDBTEAM
          CALL      RAOPDEB1                * re-check if record already existed
          BRANCH    OVRCD,BTMALC20          * are actually in add mode
          CALL      UPOPDEB1                * update team details
.
          MOVE      THREE,AUDIFLAG          * set for after change audit
          CALL      AUDEB000                * write C audit
.
          MOVE      FALSE,EXIT              * valid addition
          GOTO      BTMALC99
.
.         write record as in add mode
.
BTMALC20  UNPACK    KEY11,OPDBUNIQ,OPDBTEAM
          CALL      WROPDEB1                * write team to file
.
          MOVE      ONE,AUDIFLAG            * set for add audit
          CALL      AUDEB000                * write A audit
.
          MOVE      FALSE,EXIT              * valid addition
          GOTO      BTMALC99
.
.         no team one on file so set up default session team as team one
.
BTMALC22  PACK      KEY19,CURSESS,SP20
          CALL      RDOPSES1                * get default operating team
          CALL      TMSTB000                * transfer OPRSESFD vars to OPRDEBFD
          GOTO      BTMALC11
.
.         team > 1 entered
.
BTMALC44  MATCH     "2",OPDBTEAM            * check for case number two
          GOTO      BTMALC66 IF NOT EQUAL   * not team 2 so check if valid team
.
.         team two entered
.
          PACK      KEY11,OPDAUNIQ,OPDBTEAM
          CALL      RDOPDEB1
          BRANCH    OVRCD,BTMALC55          * doesnt exist so add it
          GOTO      BTMALC10                * write before change audit
.
.         Clear OPRDEBFD vars for entry of a new team
.
BTMALC55  PACK      DESC55,SP30,SP30
          UNPACK    DESC55,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                           OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
.
          UNPACK    DESC55,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                           OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
.
          UNPACK    DESC55,OPDBLVD1,OPDBLVD2,OPDBLVD3,OPDBLVD4:
                           OPDBLVD5,OPDBLVD6,OPDBLVD7,OPDBLVD8
.
          UNPACK    DESC55,OPDBTYN1,OPDBTYN2,OPDBTYN3,OPDBTYN4:
                           OPDBTYN5,OPDBTYN6
.
          UNPACK    DESC55,OPDBNUC1,OPDBNUC2,OPDBNUC3,OPDBNUC4:
                           OPDBNUC5,OPDBNUC6:
                           OPDBUSR1,OPDBUSR2,OPDBUSR3,OPDBUSR4
          GOTO      BTMALC11                * add a team
.
.         team > 2 entered, checks if a valid team# has been entered
.
BTMALC66  PACK      KEY11,OPDAUNIQ,OPDBTEAM
          CALL      RDOPDEB1                * if the team doesnt exist must see
          BRANCH    OVRCD,BTMALC77          * if it is a valid addition
          GOTO      BTMALC10                * get team details and write B audit
.
.         can only add a team with number one after the last team number
.
BTMALC77  MOVE      OPDBTEAM,SCRNFLAG       * use scrnflag to save team number
          MOVE      OPDBTEAM,EXIT           * set up as a form
          SUB       ONE,EXIT                * get previous team number
.
          PACK      KEY11,OPDAUNIQ,EXIT     * see if prev. team exists
          CALL      RDOPDEB1                * check for a previous team
          BRANCH    OVRCD,BTMALC85          * no previous, enter another team#
.
          MOVE      SCRNFLAG,OPDBTEAM       * restore original team number
          GOTO      BTMALC55                * add a new team
.
BTMALC85  BEEP
          GOTO      BTMALC00
.
BTMALC88  DISPLAY   *P1:24,*B,"Invalid Visit number ",*V2LON,KEY8,*HOFF:
                    ".  ",*EL;
          CALL      HITENTER 
.
.         invalid case number, no admission details, exit selected
.
BTMALC90  MOVE      FIVE,AUDIFLAG           * set for delete B audit
          CALL      AUDEB000                * erase B audit
          MOVE      ONE,EXIT
.
BTMALC99  RETURN
+
.*******************************************************************************
.                                 OPTMALLC
.    The function Inputs team allocation details.
.
.    Function : Input/Change the team allocation details.
.               This routine will NOT update the OPRDEBFD record. This is
.               because this routine is also used to update the default 
.               allocation found in the OPRSESFD file.
.
.    PARAMETERS.
.    CURSESS  : Current session header Date time and clinic/doctor
.               This will be unpacked into SESNDATE, SESNTIME and SESNCODE
.    OPDAADMN : Patient admission number and should be blank if entering
.               default session team.
.    AURNO    : Should be set to patients UR number if allocating a 
.               specialised operating team.
. 
.    Note : This function assumes vars of OPRDEAFD OPRDEBFD and OPRSESFD
.           contain relevant data to current session and case number thus
.           read these records before calling this function.
.           If No OPRDEAFD record then function assumes Default team
.           allocation.
.    
.    RETURN PARAMETERS.
.
.    EXIT = 0 : OK
.    EXIT = 1 : Cancel or exitchar keyin
.    MODEFLAG : This is set to 1 if data was enetered or changed
.
.    Note : The entered team details are stored in OPRDEBFD vars
.*******************************************************************************
OPTMALLC  MOVE      ONE,SCRNFLAG
          CALL      TMRDP111           * display main screen
.
          PACK      DESC30,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                    OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8,SP30
          MATCH     DESC30,SP30        * check if any surgeon types exist
          GOTO      TMALLC33 IF NOT EQUAL
.
          PACK      DESC30,OPDBTYN1,OPDBTYN2,OPDBTYD3,OPDBTYN4,OPDBTYN5:
                    OPDBTYN6,SP30
          MATCH     DESC30,SP30        * check if nurse types and user def exist
          GOTO      TMALLC33 IF NOT EQUAL
.
.    Insert Mode  (ML)
.
          CALL      TMINS000           * Team allocation INSert mode
          BRANCH    EXIT,TMALLC99      * exit=1 if exitchar
          CALL      TMMOD000           * do Modifications to team screen
          MOVE      ONE,MODEFLAG       * set data entered
          GOTO      TMALLC99
TMALLC33
.
.    Change Mode
.
          CALL      TMMOD000           * do Modifications to team screen
.         
TMALLC99  RETURN
+
.*******************************************************************************
.                                 OPT2MALL
.                             team allocation screen 2
.*******************************************************************************
OPT2MALL  MOVE      ONE,SCRNFLAG
          MOVE      ZERO,FORM1
          ASSIGN    OPCNUTU1+OPCNUTU2+OPCNUTU3+OPCNUTU4,FORM1
          COMPARE   ZERO,FORM1
          GOTO      OPT2M900 IF EQUAL
.
          CALL      D2SCR111           * display screen 2
          BRANCH    EXIT,OPT2M900
.
          PACK      DESC30,OPDBUSR1,OPDBUSR2,OPDBUSR3,OPDBUSR4,SP30
          MATCH     DESC30,SP30                    * check if user def exist
          GOTO      OPT2M500 IF NOT EQUAL
.
. ------- Insert Mode ------------------
.
          CALL      TAINS000           * Team allocation INSert mode
          BRANCH    EXIT,OPT2M999      * exit=1 if exitchar
.
          CALL      TACHG000           * do Modifications to team screen
          BRANCH    EXIT,OPT2M999      * exit=1 if exitchar
          MOVE      ONE,MODEFLAG       * set data entered
          GOTO      OPT2M900
.
. ------- Change Mode ------------------
.
OPT2M500  CALL      TACHG000           * do Modifications to team screen
          BRANCH    EXIT,OPT2M999      * exit=1 if exitchar
          MOVE      ONE,MODEFLAG       * set data entered
.
OPT2M900  MOVE      ZERO,EXIT
          GOTO      OPT2M999
.
OPT2M950  MOVE      ONE,EXIT
.
OPT2M999  RETURN
.*******************************************************************************
.                                 TACHG000
.    Modify operating team details screen 2
.*******************************************************************************
TACHG000  CALL      MAINQST
          COMPARE   ZERO,CCITEM        * ok to post
          GOTO      TACHG900 IF EQUAL
.
          COMPARE   "-1",CCITEM        * cancel option
          GOTO      TACHG950 IF EQUAL
.
          BRANCH    CCITEM,TACHG100,TACHG100,TACHG100,TACHG100
.
          BEEP
          GOTO      TACHG000
.
TACHG100  CALL      TMMUS000           * Modify User define codes
          BRANCH    EXIT,TACHG999
          GOTO      TACHG000
.
TACHG900  MOVE      ZERO,EXIT
          GOTO      TACHG999
.
TACHG950  MOVE      ONE,EXIT
TACHG999  RETURN
+
.*******************************************************************************
.                                 TAINS000
.    Insert routine for team allocation screen 2
.*******************************************************************************
TAINS000  MOVE      TEN,YPOS
          MOVE      ZERO,CCITEM
.
TAINS100  ADD       ONE,CCITEM
.
          LOAD      EXIT,CCITEM,OPCNUTU1,OPCNUTU2,OPCNUTU3,OPCNUTU4
          COMPARE   ZERO,EXIT          * test if user define is ON
          GOTO      TAINS688 IF EQUAL
.
          LOAD      EXIT,CCITEM,SIX,SEVEN,EIGHT,NINE
          PACK      CODE,ANSQ,EXIT,SP2 * set patient category
          LOAD      MODEFLAG,CCITEM,OPCNUTM1,OPCNUTM2,OPCNUTM3,OPCNUTM4
          CALL      TMUSR000           * get user define for Team details
          BRANCH    EXIT,TAINS999
          STORE     REPLY,CCITEM,OPDBUSR1,OPDBUSR2,OPDBUSR3,OPDBUSR4
          ADD       ONE,YPOS
.
TAINS688  COMPARE   FOUR,CCITEM
          GOTO      TAINS100 IF LESS
.
TAINS999  RETURN
.
.*******************************************************************************
.                                 TMRDP000
.    Team Redisplay screen
.*******************************************************************************
TMRDP000  BRANCH    SCRNFLAG,TMRDP999
          MOVE      ONE,SCRNFLAG
.
          CALL      OPDSPSHD           * redisplay session detail header.
.
TMRDP111  CALL      TAHED000           * display team allocation header
.
TMRDP133  DISPLAY   *P1:10,*V2LON,*ULON,"Operating Personnel ":
                    *P23:10,"Doctor",SP20,SP5:
                    *P55:10,"Supervisor Level",SP9
.
          DISPLAY   *P2:11,*V2LON,"1.",*HOFF,*P21:11,COLON:
                    *P2:12,*V2LON,"2.",*HOFF,*P21:12,COLON:
                    *P2:13,*V2LON,"3.",*HOFF,*P21:13,COLON:
                    *P2:14,*V2LON,"4.",*HOFF,*P21:14,COLON:
                    *P2:15,*V2LON,"5.",*HOFF,*P21:15,COLON:
                    *P2:16,*V2LON,"6.",*HOFF,*P21:16,COLON:
                    *P2:17,*V2LON,"7.",*HOFF,*P21:17,COLON:
                    *P2:18,*V2LON,"8.",*HOFF,*P21:18,COLON
.
          DISPLAY   *P1:19,*V2LON,*ULON,"Nursing Personnel":
                    *P19:19,"Nurse",SP10,SP6:
                    *P41:19,*V2LON,*ULON,"Nursing Personnel":
                    *P59:19,"Nurse",SP10,SP6
.
          DISPLAY   *P1:20,*V2LON," 9.",*HOFF,*P17:20,COLON:
                    *P41:20,*V2LON,"13.",*HOFF,*P57:20,COLON:
                    *P1:21,*V2LON,"10.",*HOFF,*P17:21,COLON:
                    *P41:21,*V2LON,"14.",*HOFF,*P57:21,COLON:
                    *P1:22,*V2LON,"11.",*HOFF,*P17:22,COLON:
                    *P1:23,*V2LON,"12.",*HOFF,*P17:23,COLON
.
.-------------------------------------------------------------------------------
          MOVE      TEN,CVERT          * set screen counter
          MOVE      ZERO,CLNO          * set doctor count
TMRDP222
          ADD       ONE,CVERT
          ADD       ONE,CLNO           * set doctor count
          LOAD      KEY3,CLNO,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                              OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
          MATCH     SP3,KEY3
          GOTO      TMRDP288 IF EQUAL
          MOVE      MISSCODE,TDESC
          PACK      KEY5,ANSO,ANSP,KEY3
          CALL      RDCODE1
          MOVE      TDESC,KEY15        * truncate doctor type code desc
          DISPLAY   *P5:CVERT,KEY15
.
          LOAD      KEY6,CLNO,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                              OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
          CALL      RDDOCT1
          BRANCH    OVRCD,TMRDP288
          CALL      DOCNM000           * setup doctors name
          MOVE      DESC40,KEY24
          DISPLAY   *P23:CVERT,*V2LON,KEY6,*HOFF," ",KEY24
.
          LOAD      KEY3,CLNO,OPDBLVD1,OPDBLVD2,OPDBLVD3,OPDBLVD4:
                              OPDBLVD5,OPDBLVD6,OPDBLVD7,OPDBLVD8
          MATCH     SP3,KEY3           * test if doctor requires supervision
          GOTO      TMRDP288 IF EQUAL
          MOVE      MISSCODE,TDESC
          PACK      KEY5,ANSO,ANSS,KEY3
          CALL      RDCODE1            * get supervision desc
          DISPLAY   *P55:CVERT,*V2LON,KEY3,*HOFF,SP2,TDESC
TMRDP288
          COMPARE   EIGHT,CLNO
          GOTO      TMRDP222 IF LESS
.
.-------------------------------------------------------------------------------
          MOVE      TEN9,CVERT         * set screen counter
          MOVE      ZERO,CLNO          * set Nurse count
TMRDP333
          ADD       ONE,CVERT
          ADD       ONE,CLNO
          LOAD      KEY3,CLNO,OPDBTYN1,OPDBTYN2,OPDBTYN3,OPDBTYN4,OPDBTYN5:
                              OPDBTYN6
          MATCH     SP3,KEY3
          GOTO      TMRDP388 IF EQUAL
          PACK      KEY5,ANSO,ANSN,KEY3
          CALL      RDCODE1
          BRANCH    OVRCD,TMRDP388
          MOVE      TDESC,KEY11        * truncate nurse type
          IF        CLNO < 5
            DISPLAY   *P5:CVERT,KEY11
          ELSE
            ASSIGN    15 + CLNO, CVERT
            DISPLAY   *P45:CVERT,KEY11
          ENDIF
.
          LOAD      KEY6,CLNO,OPDBNUC1,OPDBNUC2,OPDBNUC3,OPDBNUC4,OPDBNUC5:
                              OPDBNUC6
          CALL      RDOPNUR1
          BRANCH    OVRCD,TMRDP388
          CALL      TMNNM000           * setup nurse name
          IF        CLNO < 5
            DISPLAY   *P19:CVERT,*V2LON,KEY6,*HOFF," ",KEY14
          ELSE
            ASSIGN    15 + CLNO, CVERT
            DISPLAY   *P59:CVERT,*V2LON,KEY6,*HOFF," ",KEY14
          ENDIF
TMRDP388
          COMPARE   SIX,CLNO
          GOTO      TMRDP333 IF LESS
.
TMRDP999  RETURN
.*****************************************************************************
.* Display header for team allocation
.*****************************************************************************
TAHED000  DISPLAY   *P1:8,*EF;         * clear the screen.
.
          MATCH     SP8,OPDAADMN       * test using default allocation.
          GOTO      TAHED122 IF EQUAL     
.
          MATCH     "       0",OPDAADMN
          GOTO      TAHED122 IF EQUAL     
.
          PACK      PATNAME,SP30,SP30
          MATCH     ZEROUR,AURNO
          GOTO      TAHED110 IF EQUAL       * on pre-admission file
.
          MOVE      AURNO,KEY8              * UR number is key
          CALL      RDMAST1                 * read master file
          BRANCH    OVRCD,TAHED120          * invalid UR number
          GOTO      TAHED115                * format the name
.
TAHED110  MOVE      OPDAADMN,KEY8           * admission number
          CALL      RDPRAM1                 * pre-admission file
          MOVE      ZEROUR,PURNO            * indicate no UR number
          BRANCH    OVRCD,TAHED120          * invalid booking number
.
TAHED115  MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME        * patients name
.
TAHED120  DISPLAY   *P5:8,"Admission No.   : ",*V2LON,OPDAADMN,*HOFF:
                    *P38:8,"Case Number : ",*V2LON,OPDACASE,*HOFF:
                    *P61:8,"Team : ",*V2LON,OPDBTEAM
          DISPLAY   *P5:9,"U/R Number      : ",*V2LON,AURNO,*HOFF:
                    *P33:9,PATNAME
          GOTO      TAHED999
.
TAHED122  DISPLAY   *V2LON,*P25:9,"Default Team Allocation"
.
TAHED999  RETURN
+
.*****************************************************************************
.    Display Screen 2         
.*****************************************************************************
D2SCR000  MOVE      ZERO,FORM1
          ASSIGN    OPCNUTU1+OPCNUTU2+OPCNUTU3+OPCNUTU4,FORM1
          COMPARE   ZERO,FORM1
          GOTO      D2SCR950 IF EQUAL
.
          BRANCH    SCRNFLAG,TMRDP999
          MOVE      ONE,SCRNFLAG
.
          CALL      OPDSPSHD           * redisplay session detail header.
D2SCR111  CALL      TAHED000           * display team allocation header
.
          MOVE      TEN,CVERT
          MOVE      ONE,FORM2         * set select item index
          MOVE      ZERO,CLNO          * Set user define counter
.
D2SCR444  ADD       ONE,CLNO           * increment user define counter
          LOAD      EXIT,CLNO,OPCNUTU1,OPCNUTU2,OPCNUTU3,OPCNUTU4
          COMPARE   ONE,EXIT           * test if user define is ON
          GOTO      D2SCR500 IF NOT EQUAL
.
          LOAD      EXIT,CLNO,SIX,SEVEN,EIGHT,NINE
          PACK      KEY5,ANSQ,EXIT,SP3
          CALL      RDCODE1            * get user define category desc
          BRANCH    OVRCD,D2SCR500
.
          MOVE      TDESC,KEY20        * truncate user define cat desc
          DISPLAY   *P1:CVERT,*V2LON,FORM2,".",*HOFF,*P5:CVERT,KEY20:
                    *P26:CVERT,":"
.
          LOAD      KEY3,CLNO,OPDBUSR1,OPDBUSR2,OPDBUSR3,OPDBUSR4
          MATCH     SP3,KEY3           * test blank user define code
          GOTO      D2SCR488 IF EQUAL
.
          MOVE      SP20,TDESC
          PACK      KEY5,ANSQ,EXIT,KEY3
          CALL      RDCODE1            * get user define code desc
          MOVE      TDESC,KEY20        * truncate user define code desc
          DISPLAY   *P28:CVERT,*V2LON,KEY3,*HOFF," ",KEY20
.
D2SCR488  ADD       ONE,CVERT          * increment line counter
          ADD       ONE,FORM2          * increment screen index
.
D2SCR500  COMPARE   FOUR,CLNO          * test if check all 4 user defines
          GOTO      D2SCR444 IF LESS
.
D2SCR900  MOVE      ZERO,EXIT
          GOTO      D2SCR999
.
D2SCR950  MOVE      ONE,EXIT
.
D2SCR999  RETURN
.*******************************************************************************
.                                 TMMOD000
.    Modify operating team details
.*******************************************************************************
TMMOD000
TMMOD111  CALL      MAINQST
          COMPARE   ZERO,CCITEM        * ok to post
          GOTO      TMMOD123 IF EQUAL
.
          COMPARE   "-1",CCITEM        * cancel option
          GOTO      TMMOD900 IF EQUAL
.
          BRANCH    CCITEM,TMMOD222,TMMOD222,TMMOD222,TMMOD222:
                           TMMOD222,TMMOD222,TMMOD222,TMMOD222:
                           TMMOD444,TMMOD444,TMMOD444,TMMOD444:
                           TMMOD444,TMMOD444
.
          BEEP
          GOTO      TMMOD111
.
TMMOD123  CALL      TMCFS000           * test team has atleast 1 surgeon
          BRANCH    EXIT,TMMOD111      * exit=1 if no surgeon found
          MOVE      ZERO,EXIT          * ensure exit is zero
          GOTO      TMMOD999
TMMOD222
          MOVE      ONE,MODEFLAG       * something was changed
          MOVE      CCITEM,YPOS        * calculate screen pos from ccitem select
          ADD       TEN,YPOS
TMMOD233
          STORE     SP3,CCITEM,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                               OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
          CALL      TMDPC000           * Doctor personnel code
          STORE     DOCTPERS,CCITEM,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                                    OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
.
          COMPARE   ZERO,EXIT          * test if deleting this personnel
          GOTO      TMMOD255 IF EQUAL
TMMOD244
          STORE     SP6,CCITEM,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                               OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
          STORE     SP3,CCITEM,OPDBLVD1,OPDBLVD2,OPDBLVD3,OPDBLVD4:
                               OPDBLVD5,OPDBLVD6,OPDBLVD7,OPDBLVD8
          GOTO      TMMOD111
TMMOD255
          STORE     SP6,CCITEM,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                               OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
          CALL      TMDOC000           * doctor code
          STORE     DOCTCODE,CCITEM,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                                    OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
          BRANCH    EXIT,TMMOD233      * set exit in DOCC0000
.
          STORE     SP3,CCITEM,OPDBLVD1,OPDBLVD2,OPDBLVD3,OPDBLVD4:
                               OPDBLVD5,OPDBLVD6,OPDBLVD7,OPDBLVD8
          CALL      TMSUP000           * supervisor level
          STORE     SUPVCODE,CCITEM,OPDBLVD1,OPDBLVD2,OPDBLVD3,OPDBLVD4:
                                    OPDBLVD5,OPDBLVD6,OPDBLVD7,OPDBLVD8
          BRANCH    EXIT,TMMOD244      * exit=1 if exitchar from TMSUP000
          GOTO      TMMOD111
.
TMMOD444  MOVE      ONE,MODEFLAG       * something was changed
          SUB       EIGHT,CCITEM       * re-adjust ccitem
.
          IF        CCITEM < 5
            ASSIGN    19 + CCITEM, YPOS
            MOVE      "5",NTCOL          * nurse type column
            MOVE      "19",NUCOL         * nurse column
            MOVE      "26",NUDCOL        * nurse desc column
          ELSE
            ASSIGN    15 + CCITEM, YPOS
            MOVE      "45",NTCOL          * nurse type column
            MOVE      "59",NUCOL         * nurse column
            MOVE      "66",NUDCOL        * nurse desc column
          ENDIF
.
TMMOD455  STORE     SP3,CCITEM,OPDBTYN1,OPDBTYN2,OPDBTYN3,OPDBTYN4,OPDBTYN5:
                               OPDBTYN6
          CALL      TMNPC000           * nursing personnel code
          STORE     NURSPERS,CCITEM,OPDBTYN1,OPDBTYN2,OPDBTYN3,OPDBTYN4:
                                    OPDBTYN5,OPDBTYN6
          COMPARE   ZERO,EXIT          * test for delete of nurse personnel
          GOTO      TMMOD488 IF EQUAL
          STORE     SP6,CCITEM,OPDBNUC1,OPDBNUC2,OPDBNUC3,OPDBNUC4,OPDBNUC5:
                               OPDBNUC6
          GOTO      TMMOD111
.
TMMOD488  STORE     SP6,CCITEM,OPDBNUC1,OPDBNUC2,OPDBNUC3,OPDBNUC4,OPDBNUC5:
                               OPDBNUC6
          CALL      TMNUR000           * nurse code
          STORE     NURSCODE,CCITEM,OPDBNUC1,OPDBNUC2,OPDBNUC3,OPDBNUC4:
                                    OPDBNUC5,OPDBNUC6
          BRANCH    EXIT,TMMOD455
          GOTO      TMMOD111
.
TMMOD900
          MOVE      ZERO,MODEFLAG
          MOVE      ONE,EXIT
TMMOD999  RETURN
+
.*******************************************************************************
.                                 TMINS000
.    Insert routine for team allocation.
.    First get operating surgeons, 2nd Nursing data and 3rd user define code.
.*******************************************************************************
TMINS000
          CALL      TMDSA000           * set default surgeon and anaesthetist
          BRANCH    EXIT,TMINS999
          GOTO      TMINS122           * Input the rest of the doctors
TMINS111
          ADD       ONE,CCITEM
TMINS122
          CALL      TMDPC000           * get doctor personnel code.
          STORE     DOCTPERS,CCITEM,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                                    OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
          BRANCH    EXIT,TMINS444
.
          CALL      TMDOC000           * get operating surgeon.
          STORE     DOCTCODE,CCITEM,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                                    OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
          BRANCH    EXIT,TMINS122
.
          CALL      TMSUP000           * get doctors supervision level 
          STORE     SUPVCODE,CCITEM,OPDBLVD1,OPDBLVD2,OPDBLVD3,OPDBLVD4:
                                    OPDBLVD5,OPDBLVD6,OPDBLVD7,OPDBLVD8
          BRANCH    EXIT,TMINS122      * exit=1 if exitchar
.
          ADD       ONE,YPOS
          COMPARE   EIGHT,CCITEM       * test end of doctor code keyin
          GOTO      TMINS111 IF LESS
TMINS444
.
          MOVE      ZERO,CCITEM
          MOVE      "20",YPOS
          MOVE      "5",NTCOL          * nurse type column
          MOVE      "19",NUCOL         * nurse column
          MOVE      "26",NUDCOL        * nurse desc column
TMINS500
          ADD       ONE,CCITEM
TMINS511
          CALL      TMNPC000           * get nursing personnel code
          STORE     NURSPERS,CCITEM,OPDBTYN1,OPDBTYN2,OPDBTYN3,OPDBTYN4:
                                    OPDBTYN5,OPDBTYN6
          BRANCH    EXIT,TMINS900
.
          CALL      TMNUR000           * nurse code
          BRANCH    EXIT,TMINS511
          STORE     NURSCODE,CCITEM,OPDBNUC1,OPDBNUC2,OPDBNUC3,OPDBNUC4:
                                    OPDBNUC5,OPDBNUC6
.
          ADD       ONE,YPOS
          COMPARE   SIX,CCITEM
          GOTO      TMINS999 IF EQUAL
.
          IF        CCITEM = 4
            MOVE      "20",YPOS
            MOVE      "45",NTCOL          * nurse type column
            MOVE      "59",NUCOL         * nurse column
            MOVE      "66",NUDCOL        * nurse desc column
          ENDIF
.
          GOTO      TMINS500
.
TMINS900  MOVE      ZERO,EXIT
.
TMINS999  RETURN
+
.*******************************************************************************
.                                 TMDSA000
.    Get default surgeon and anaesthetist
.    If OPCNCLSU=1 (using surgeons) the insert routine will set 1st doctor
.    code to SESNCODE and if OPSEANAE is non-blank then the 2nd doctor
.    code will default to OPSEANAE.  Note theses defaults may require
.    supervision level code.
.    If OPCNCLSU=0 (using clinics) then if OPSEANAE is non-blank the 1st doctor
.    code will default to OPSEANAE as anaesthetist.
.*******************************************************************************
TMDSA000
          MOVE      TEN1,YPOS          * set first display line
          MOVE      ONE,CCITEM         * set select index
.
.         Only the 1st team may have a default surgeon/anaesthetist
.
          MATCH     "1",OPDBTEAM       * 1st team ?
          GOTO      TMDSA900 IF NOT EQUAL
.
          BRANCH    OPCNCLSU,TMDSA555  * check if clinic or surgeon
.
.    Get default anaesthetist and display personnel code and doctors name.
.
TMDSA111
          MATCH     SP6,OPSEANAE       * check for default anaesthetist
          GOTO      TMDSA900 IF EQUAL  * if none then begin team keyin
.
          MOVE      MISSCODE,TDESC
          PACK      KEY5,ANSO,ANSP,OPCNANAE
          CALL      RDCODE1
          MOVE      TDESC,KEY15        * truncate doctor type code desc
          DISPLAY   *P5:YPOS,KEY15
.
          PACK      KEY6,OPSEANAE,SP6
          CALL      RDDOCT1            * validate doctor code enetered
          BRANCH    OVRCD,TMDSA300
          CALL      DOCNM000           * format doctors name
          MOVE      DESC40,DESC24      * tuncate desc
          DISPLAY   *P23:YPOS,*V2LON,OPSEANAE,*HOFF,*P30:YPOS,DESC24
          STORE     OPSEANAE,CCITEM,OPDBDCC1,OPDBDCC2
          STORE     OPCNANAE,CCITEM,OPDBTYD1,OPDBTYD2
.
          CALL      TMSUP000           * get default supervision
          STORE     SUPVCODE,CCITEM,OPDBLVD1,OPDBLVD2
          BRANCH    EXIT,TMDSA999      * exit=1 if exitchar
.
          ADD       ONE,YPOS           * increment display line
          ADD       ONE,CCITEM         * increment selection index
.
. get default for the 2nd anaesthetist if the paramater is turned on and if 
. the 2nd anaesthetist code is not blank
.
TMDSA300  COMPARE   ONE,OPCNUSAN       * using 2nd anaesthetist?
          GOTO      TMDSA900 IF NOT EQUAL
.
          MATCH     SP6,OPSEXDOC       * check if 2nd anaesthetist is blank
          GOTO      TMDSA900 IF EQUAL  * if none then begin team keyin
.
          MOVE      MISSCODE,TDESC
          PACK      KEY5,ANSO,ANSP,OPCNPCOT
          CALL      RDCODE1
          MOVE      TDESC,KEY15        * truncate doctor type code desc
          DISPLAY   *P5:YPOS,KEY15
.
          PACK      KEY6,OPSEXDOC,SP6
          CALL      RDDOCT1            * validate doctor code enetered
          BRANCH    OVRCD,TMDSA900
          CALL      DOCNM000           * format doctors name
          MOVE      DESC40,DESC24      * tuncate desc
          DISPLAY   *P23:YPOS,*V2LON,OPSEXDOC,*HOFF,*P30:YPOS,DESC24
.
          STORE     OPSEXDOC,CCITEM,OPDBDCC1,OPDBDCC2,OPDBDCC3
          STORE     OPCNPCOT,CCITEM,OPDBTYD1,OPDBTYD2,OPDBTYD3
.
          CALL      TMSUP000           * get default supervision
          STORE     SUPVCODE,CCITEM,OPDBLVD1,OPDBLVD2,OPDBLVD3
          BRANCH    EXIT,TMDSA999      * exit=1 if exitchar
.
          ADD       ONE,YPOS           * increment display line
          ADD       ONE,CCITEM         * increment selection index
.
          GOTO      TMDSA900
.
.    Get default surgeon and display personnel code and doctors name.
.
TMDSA555
          MOVE      MISSCODE,TDESC
          PACK      KEY5,ANSO,ANSP,OPCNOPSU
          CALL      RDCODE1
          MOVE      TDESC,KEY15        * truncate doctor type code desc
          DISPLAY   *P5:YPOS,KEY15
.
          PACK      KEY6,SESNCODE,SP6
          CALL      RDDOCT1            * validate doctor code enetered
          BRANCH    OVRCD,TMDSA111
          CALL      DOCNM000           * format doctors name
          MOVE      DESC40,DESC24      * tuncate desc
          DISPLAY   *P23:YPOS,*V2LON,SESNCODE,*HOFF,*P30:YPOS,DESC24
          MOVE      SESNCODE,OPDBDCC1  * copy default anaesthetist
          MOVE      OPCNOPSU,OPDBTYD1  * copy anaesthetist type to personnel
.
          CALL      TMSUP000
          MOVE      SUPVCODE,OPDBLVD1  * save supervision code
          BRANCH    EXIT,TMDSA999      * exit=1 if exitchar
.
          ADD       ONE,YPOS           * increment display line
          ADD       ONE,CCITEM         * increment selection index
          GOTO      TMDSA111
.
TMDSA900  MOVE      ZERO,EXIT          * Okay to proceed (EXITCHAR not input)
.
TMDSA999  RETURN
+
.*******************************************************************************
.                                 TMCFS000
.    test team has atleast 1 surgeon or a completely blank screen, why because
.    thats what they want.
.    EXIT=1 : If team entered and doesnt have a surgeon
.    EXIT=0 : team has a surgeon or blank team entered.
.*******************************************************************************
TMCFS000
.
.    Test if entire team is blank
.
          PACK      CDESC40A,SP20,SP20  * use CDESC40 as SP40
          PACK      DESC40,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                    OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8,SP20,SP20
          MATCH     DESC40,CDESC40A     * check team is non-blank
          GOTO      TMCFS111 IF NOT EQUAL
.
          PACK      DESC40,OPDBTYN1,OPDBTYN2,OPDBTYN3,OPDBTYN4,OPDBTYN5:
                           OPDBTYN6,SP20,SP20
          MATCH     DESC40,CDESC40A     * check team is non-blank
          GOTO      TMCFS111 IF NOT EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      TMCFS999
.
.    Test for atleast one surgeon.
.
TMCFS111
          MOVE      ONE,CCITEM
TMCFS222
          LOAD      KEY3,CCITEM,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                                   OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
          MATCH     SP3,KEY3
          GOTO      TMCFS444 IF EQUAL
.
          PACK      KEY5,ANSO,ANSP,KEY3
          CALL      RDCODE1            * get indicator from codes file
          BRANCH    OVRCD,TMCFS444
          CMATCH    "S",TCINDC1        * personnel code 1st ind is S Surgeon
          GOTO      TMCFS444 IF NOT EQUAL
          MOVE      ZERO,EXIT          * surgeon found exit=0 lets go
          GOTO      TMCFS999
TMCFS444
          ADD       ONE,CCITEM
          COMPARE   NINE,CCITEM
          GOTO      TMCFS222 IF NOT EQUAL   
TMCFS888
          DISPLAY   *P1:24,*EL,*B:
                    "Team must have at least one chief surgeon.  ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
TMCFS999  RETURN
+
.*******************************************************************************
.                                 TMDPC000
.    Get operating team doctor personnel code.
.*******************************************************************************
TMDPC000
          MOVE      ONE,SCRNFLAG
          DISPLAY   *P5:YPOS,SP10,SP6,*P23:YPOS,*EL
          DISPLAY   *P5:YPOS,UNDLN3,*P5:YPOS;
TMDPC111
          KEYIN     *V2LON,DOCTPERS    * keyin DOCTor PERSonnel code
.
          ENDSET    DOCTPERS
          APPEND    SP3,DOCTPERS
          RESET     DOCTPERS
.
          MATCH     SP3,DOCTPERS
          GOTO      TMDPC777 IF EQUAL
          MATCH     QUEST,DOCTPERS
          GOTO      TMDPC666 IF NOT EQUAL
.
          PACK      CODE,ANSO,ANSP,SP3
          CALL      PATCODDS
          MOVE      ZERO,SCRNFLAG      * signal redisplay required
TMDPC444
          DISPLAY   *P1:24,*EL,"Operating Personnel Code : ___",*P28:24;
          GOTO      TMDPC111
TMDPC666
          PACK      KEY5,ANSO,ANSP,DOCTPERS
          CALL      RDCODE1            * validate code
          BRANCH    OVRCD,TMDPC800
          MOVE      TDESC,DESC15       * truncate description
          CALL      TMRDP000           * redisplay team screen if necessary
          DISPLAY   *P5:YPOS,DESC15
          MOVE      ZERO,EXIT
          GOTO      TMDPC999
TMDPC777
          BRANCH    SCRNFLAG,TMDPC788
          CALL      TMRDP000
          GOTO      TMDPC000
TMDPC788
          DISPLAY   *P5:YPOS,SP6,SP10,*P23:YPOS,*EL
          MOVE      ONE,EXIT
          GOTO      TMDPC999
TMDPC800  
          DISPLAY   *P1:24,*EL,*B,"Personnel Code not on file.  ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,TMDPC000
          GOTO      TMDPC444
.
TMDPC999  RETURN
.*******************************************************************************
.                                 TMDOC000
.    Get operating team surgeon.
.*******************************************************************************
TMDOC000  MOVE      SP10,DOCTCODE
          MOVE      "23",ECOL
          MOVE      YPOS,EVERT
          MOVE      ZERO,CKYIMAND
          MOVE      SP6,CKYIDEF6
          MOVE      CCITEM,DCCITEM
          CALL      PATDOCKY
          MOVE      DCCITEM,CCITEM
          BRANCH    EXIT,TMDOC777,TMDOC000
.
          MOVE      DCODE,DOCTCODE
          MOVE      DOCTCODE,KEY6
          CALL      RDDOCT1            * validate doctor code enetered
          CALL      DOCNM000           * format doctors name
          MOVE      DESC40,DESC24      * tuncate desc
          CALL      TMRDP000           * redisplay 
          DISPLAY   *P23:YPOS,*V2LON,DOCTCODE,*HOFF,*P30:YPOS,DESC24
          MOVE      ZERO,EXIT
          GOTO      TMDOC999
.
TMDOC777  DISPLAY   *P5:YPOS,SP10,SP5,*P23:YPOS,SP30
          MOVE      ONE,EXIT
          GOTO      TMDOC999
.
TMDOC999  RETURN
.****************************************************************************
.                                 TMSUP000
.    Get supervision level for apprentice doctors
.    EXIT = 0 : code OK
.    EXIT = 1 : exit char keyin
.****************************************************************************
TMSUP000
          MOVE      SP3,SUPVCODE
          MATCH     SP3,DALEVEL        * test if doctor requires a supervision
          GOTO      TMSUP999 IF EQUAL
.
          PACK      KEY5,ANSD,ANSL,DALEVEL
          CALL      RDCODE1
          BRANCH    OVRCD,TMSUP999 
          MATCH     ANSS,TCINDC1       * test Doctor level is a S
          GOTO      TMSUP999 IF NOT EQUAL
.
TMSUP100
          MOVE      ONE,SCRNFLAG
          DISPLAY   *P55:YPOS,*EL,UNDLN3,*P55:YPOS;
TMSUP111
          KEYIN     *V2LON,SUPVCODE
          ENDSET    SUPVCODE
          APPEND    SP3,SUPVCODE
          RESET     SUPVCODE
.
          MATCH     EXITCHAR,SUPVCODE
          GOTO      TMSUP777 IF EQUAL
          MATCH     SP3,SUPVCODE
          GOTO      TMSUP888 IF EQUAL
.
          MATCH     QUEST,SUPVCODE
          GOTO      TMSUP666 IF NOT EQUAL
          PACK      CODE,ANSO,ANSS
          CALL      PATCODDS
          MOVE      ZERO,SCRNFLAG
TMSUP444
          DISPLAY   *P1:24,*EL,"Supervision Code : ",UNDLN3,*P20:24;
          GOTO      TMSUP111
TMSUP666
          PACK      KEY5,ANSO,ANSS,SUPVCODE
          CALL      RDCODE1
          BRANCH    OVRCD,TMSUP800
          MOVE      TDESC,DESC20 
          CALL      TMRDP000
          DISPLAY   *P55:YPOS,*EL,*V2LON,SUPVCODE,*HOFF,SP2,DESC20
          MOVE      ZERO,EXIT
          GOTO      TMSUP999
TMSUP777  
          CALL      TMRDP000
          DISPLAY   *P5:YPOS,SP10,SP6,*P23:YPOS,*EL
          MOVE      SP3,SUPVCODE       * clear supervision level parameter
          MOVE      ONE,EXIT           * exitchar hit
          GOTO      TMSUP999
TMSUP800  
          DISPLAY   *P1:24,*EL,*B,"Supervisor Level not on file - ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,TMSUP100
          GOTO      TMSUP444
TMSUP888 
          DISPLAY   *P1:24,*EL,*B,"Supervisor level is mandatory - ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,TMSUP000
          GOTO      TMSUP444
.
TMSUP999  RETURN
.****************************************************************************
.                                 TMNPC000
.    Get nursing personnel code
.****************************************************************************
TMNPC000  MOVE      ONE,SCRNFLAG
.
. ------- clear & position on line ---------------
.
          DISPLAY   *PNTCOL:YPOS,UNDLN3,SP8,*PNUCOL:YPOS,SP1,SP20:
                    *PNTCOL:YPOS;
.
. ------- keyin nurse type --------------------------
.
TMNPC111  KEYIN     *V2LON,NURSPERS
          ENDSET    NURSPERS
          APPEND    SP3,NURSPERS
          RESET     NURSPERS
.
          MATCH     SP3,NURSPERS
          GOTO      TMNPC777 IF EQUAL
.
          MATCH     QUEST,NURSPERS
          GOTO      TMNPC666 IF NOT EQUAL
.
. ------- Question Mark Entered ---------------------------
.
          PACK      CODE,ANSO,ANSN
          CALL      PATCODDS
          MOVE      ZERO,SCRNFLAG              * flag as question mark
.
TMNPC444  DISPLAY   *P1:24,*EL,"Nursing Personnel Code : ",UNDLN3,*P26:24;
          GOTO      TMNPC111
.
. ------- display nurse type description -----------------------
.
TMNPC666  PACK      KEY5,ANSO,ANSN,NURSPERS
          CALL      RDCODE1
          BRANCH    OVRCD,TMNPC888
.
          MOVE      TDESC,DESC11
          CALL      TMRDP000               * redisplay the screen for ?
          DISPLAY   *PNTCOL:YPOS,DESC11
          MOVE      ZERO,EXIT
          GOTO      TMNPC999
.
TMNPC777  BRANCH    SCRNFLAG,TMNPC788
.
. ------- Nothing entered from ? mark keyin --------------------
.
          CALL      TMRDP000                * redisplay the screen
          GOTO      TMNPC000
.
. ------- nothing input for nurse type ------------------
.
TMNPC788  DISPLAY   *PNTCOL:YPOS,SP10,SP1,*PNUCOL:YPOS,SP20,SP1
          MOVE      ONE,EXIT
          GOTO      TMNPC999
.
TMNPC888  DISPLAY   *P1:24,*EL,*B,"Nursing Personnel Code not on file.  ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,TMNPC000
          GOTO      TMNPC444
.
TMNPC999  RETURN
.****************************************************************************
.                                 TMNUR000
.    Get theatre Nursing code 
.****************************************************************************
TMNUR000  MOVE      ONE,SCRNFLAG
          DISPLAY   *PNUCOL:YPOS,UNDLN6,SP10,SP5,*PNUCOL:YPOS;
.
TMNUR111  KEYIN     *V2LON,NURSCODE
          ENDSET    NURSCODE
          APPEND    SP6,NURSCODE
          RESET     NURSCODE
.
          MATCH     SP6,NURSCODE
          GOTO      TMNUR777 IF EQUAL
.
          MATCH     QUEST,NURSCODE
          GOTO      TMNUR666 IF NOT EQUAL
.
. ------- ? mark ---------------------
.
          MOVE      ONE,CNEWDS
          CALL      OPRNURDS           * nurse "?" option
          MOVE      ZERO,SCRNFLAG
.
TMNUR444  DISPLAY   *P1:24,*EL,"Nurse Code : ",UNDLN6,*P14:24;
          GOTO      TMNUR111
.
. ------- nurse code entered -----------------------
.
TMNUR666  MOVE      NURSCODE,KEY6
          CALL      RDOPNUR1
          BRANCH    OVRCD,TMNUR888
.
          COMPARE   ONE,OPNRSTAT
          GOTO      TMNUR800 IF EQUAL  * Non-active
.
          CALL      TMNNM000           * setup nurse name in desc18
          MOVE      KEY14,DESC14        * copy nurse name
          CALL      TMRDP000
          DISPLAY   *PNUCOL:YPOS,*V2LON,NURSCODE,*HOFF,*PNUDCOL:YPOS,DESC14
          MOVE      ZERO,EXIT
          GOTO      TMNUR999
.
TMNUR777  BRANCH    SCRNFLAG,TMNUR788
          CALL      TMRDP000
          GOTO      TMNUR000
.
TMNUR788  DISPLAY   *PNTCOL:YPOS,SP10,SP1,*PNUCOL:YPOS,SP20,SP1
          MOVE      ONE,EXIT
          GOTO      TMNUR999
.
TMNUR800  DISPLAY   *P1:24,*EL,*B,"Non-Active Nurse. ";
          GOTO      TMNUR900
.
TMNUR888  DISPLAY   *P1:24,*EL,*B,"Nurse Code not on file.  ";
.
TMNUR900  CALL      HITENTER
          BRANCH    SCRNFLAG,TMNUR000
          GOTO      TMNUR444
.
TMNUR999  RETURN
.****************************************************************************
.                                 TMNNM000
.    Setup Nurse name.
.****************************************************************************
TMNNM000
          MOVE      TWO,PACFRMT        * title name surname of doctor
          MOVE      OPNRSNAM,PACSNAME  * use pacdate to fix doctors name
          MOVE      OPNRGNAM,PACGNAME  * get first letter in given name
          MOVE      SP10,PACTITLE
          CALL      PACNAME            * pack up doctors name
          MOVE      PACFNAME,KEY14
TMNNM999  RETURN
.****************************************************************************
.                                 TMUSR000
.    Get user define code for specialise team data.
.****************************************************************************
TMUSR000  MOVE      ONE,SCRNFLAG
          DISPLAY   *P28:YPOS,*EL,UNDLN3,*P28:YPOS;
.
TMUSR111  KEYIN     *V2LON,REPLY
          ENDSET    REPLY
          APPEND    SP3,REPLY
          RESET     REPLY
.
          MATCH     EXITCHAR,REPLY     * test exitchar hit
          GOTO      TMUSR700 IF EQUAL
.
          MATCH     SP3,REPLY          * test return hit
          GOTO      TMUSR777 IF EQUAL
.
          CMATCH    QUEST,REPLY
          GOTO      TMUSR666 IF NOT EQUAL
          CALL      PATCODDS           * CODE is set before call to routine
          MOVE      ZERO,SCRNFLAG
.
TMUSR444  DISPLAY   *P1:24,*EL,"Code : ___",*P8:24;
          GOTO      TMUSR111
.
TMUSR666  PACK      KEY5,CODE,REPLY
          CALL      RDCODE1
          BRANCH    OVRCD,TMUSR888     * must keyin valid code
.
          MOVE      TDESC,DESC20
          CALL      D2SCR000
          DISPLAY   *P28:YPOS,*V2LON,REPLY,*HOFF,*P32:YPOS,DESC20
          MOVE      ZERO,EXIT
          GOTO      TMUSR999
.
TMUSR700  MOVE      ONE,EXIT           * signal exitchar hit
          GOTO      TMUSR999
.
TMUSR777  BRANCH    MODEFLAG,TMUSR800  * check if code is mandatory
          BRANCH    SCRNFLAG,TMUSR788
          CALL      D2SCR000
          GOTO      TMUSR000
.
TMUSR788  DISPLAY   *P28:YPOS,*EL      * clear any underline 
          MOVE      SP3,REPLY          * clear user define parameter
          MOVE      ZERO,EXIT
          GOTO      TMUSR999
.
TMUSR800  DISPLAY   *P1:24,*EL,*B,"Code is mandatory.  ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,TMUSR000
          GOTO      TMUSR444
.
TMUSR888  DISPLAY   *P1:24,*EL,*B,"Code is not on file.  ";
          CALL      HITENTER
          BRANCH    SCRNFLAG,TMUSR000
          GOTO      TMUSR444
.
TMUSR999  RETURN
.****************************************************************************
.                                 TMMUS000
.    Modify 1-4 user define code
.****************************************************************************
TMMUS000  MOVE      NINE,YPOS          * set Display line position
          MOVE      ZERO,CLNO          * zero user define code counter
.
TMMUS200  ADD       ONE,CLNO
          COMPARE   FIVE,CLNO          * test if user define in range
          GOTO      TMMUS999 IF NOT LESS
.
          LOAD      EXIT,CLNO,OPCNUTU1,OPCNUTU2,OPCNUTU3,OPCNUTU4
.
          COMPARE   ONE,EXIT
          GOTO      TMMUS200 IF NOT EQUAL
.
          ADD       ONE,YPOS
          SUB       ONE,CCITEM
.
          COMPARE   ZERO,CCITEM        * Is the one we have selected?
          GOTO      TMMUS200 IF NOT EQUAL
.
. ----  Input User Defined Fields 1-4 ----
.
          MOVE      CLNO,CCITEM        * copy index because redisplay uses CLNO
          LOAD      EXIT,CCITEM,SIX,SEVEN,EIGHT,NINE
          PACK      CODE,ANSQ,EXIT,SP2 * set patient category
          LOAD      MODEFLAG,CCITEM,OPCNUTM1,OPCNUTM2,OPCNUTM3,OPCNUTM4
          STORE     SP3,CCITEM,OPDBUSR1,OPDBUSR2,OPDBUSR3,OPDBUSR4
          CALL      TMUSR000           * get user define for Team details
          BRANCH    EXIT,TMMUS999      * exit=1 exitchar
          STORE     REPLY,CCITEM,OPDBUSR1,OPDBUSR2,OPDBUSR3,OPDBUSR4
.
TMMUS999  RETURN
+
.****************************************************************************
.*                                 GANAE000          Called by: STMALC000   *
.*                  get 1st Anaethetist in default team                     *
.****************************************************************************
GANAE000  MOVE      ZERO,FORM1  
GANAE100  ADD       ONE,FORM1                     * increment counter
.
          COMPARE   NINE,FORM1                    * no more doctor types?
          GOTO      GANAE900 IF EQUAL
.
          LOAD      DIM3,FORM1,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                                    OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
.
          PACK      KEY5,ANSO,ANSP,DIM3
          CALL      RDCODE1
          BRANCH    OVRCD,GANAE100
.
. check if indicator is an N
.
          MATCH     ANSN,TCINDC1
          GOTO      GANAE100 IF NOT EQUAL
.
. we have an Anaesthetist so get code
.
          LOAD      OPSEANAE,FORM1,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                                   OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
          GOTO      GANAE999
.
GANAE900  MOVE      SP6,OPSEANAE                  * blank out code
.
GANAE999  RETURN   
.
.****************************************************************************
