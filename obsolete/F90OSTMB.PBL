.***************************************************************************
.* System    :   Theatre                                                   *
.* Program   :   F90OSTMB                                                  *
.* Desc      :   Populate theatre session Total time booked                *
.***************************************************************************
.* Date      :   22/09/2008                                                *
.* Author    :   Peter Vela                                                *
.* Function  :   This program will populate the session file total time    *
.*           :   booked with the expected duration + the prep time         *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       OPRSESFD/INC
          INC       OPRDEAFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
RECNUM    FORM      8
.
PRGID     INIT      "F90OSTMB"
PRGNAM    INIT      "Populate theatre session Total time booked"
VERSION   INIT      "V11.03.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      OPRSESA1,"oprsesaf"
          OPEN      OPRDETA1,"oprdetaf"
.
          MOVE      ZERO,RECNUM
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
PROC0000  PACK      KEY19,SP70
          CALL      RSOPSES1
PROC1000  CALL      RKOPSES1
          BRANCH    OVRCD,PROC9999
.
          MOVE      ZERO,FORM4
.
          PACK      KEY23,OPSEDATE,OPSETIME,OPSECLIN,SP70
          CALL      RSOPDEA1
PROC2000  CALL      RKOPDEA1
          BRANCH    OVRCD,PROC3000
.
          ADD       ONE,RECNUM                    * update record counter
.
          MATCH     OPSEDATE,OPDADATE
          GOTO      PROC3000 IF NOT EQUAL
.
          MATCH     OPSETIME,OPDATIME
          GOTO      PROC3000 IF NOT EQUAL
.
          MATCH     OPSECLIN,OPDACLIN
          GOTO      PROC3000 IF NOT EQUAL
.
          MATCH     "3",DOPDASTA
          GOTO      PROC2000 IF EQUAL
.
          ADD       OPSEPREP,FORM4
          ADD       OPDAEXPD,FORM4
.
          GOTO      PROC2000
.
PROC3000  MOVE      FORM4,OPSETIMB
          CALL      UPOPSES1                     * Update session
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,"Records Processed: ",RECNUM
          ENDIF
.
          GOTO      PROC1000
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       OPRSESIO/INC
          INC       OPRDEAIO/INC
