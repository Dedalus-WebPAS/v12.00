.***************************************************************************
.* System    :   Theatre                                                   *
.* Program   :   FX159069                                                  *
.* Desc      :   Update oprdetaf.opdacrdt with bokrc1af.bkrxcrdt and       *
.*               update oprdetaf.opdactim with bokrc1af.bkrxcrtm and       *
.*               update oprdetaf.opdawebc with bokrc1af.bkreoper and       *
.*               update oprdetaf.opdaudat with bokrc1af.bkrxlupd and       *
.*               update oprdetaf.opdautim with bokrc1af.bkrxlupt and       *
.*               update oprdetaf.opdawebu with bokrc1af.bkrxwebu           *
.***************************************************************************
.* Date      :   16/01/2008                                                *
.* Author    :   Jill Habasque                                             *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       OPRDEAFD/INC
          INC       BOKRC1FD/INC
          INC       BOKRX1FD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
PRGID     INIT      "FX159069"
PRGNAM    INIT      "Update oprdetaf date/times "
VERSION   INIT      "V11.03.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      OPRDETA1,"oprdetaf"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRX1A1,"bokrx1af"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  PACK      KEY23,SP70
          CALL      RSOPDEA1
PROC1000  CALL      RKOPDEA1
          BRANCH    OVRCD,PROC9000
.
          PACK      KEY8,OPDAADMN,SP70
          CALL      RDBKREC1
          BRANCH    OVRCD,PROC2000
          CALL      RDBKRX11
          BRANCH    OVRCD,PROC3000
.
          MATCH     SP70,OPDACRDT
          IF        @EQUAL
            PACK      OPDACRDT,BKRXCRDT,SP70
          ENDIF
.
          MATCH     SP70,OPDACTIM
          IF        @EQUAL
            PACK      OPDACTIM,BKRXCRTM,SP70
          ENDIF
.
          MATCH     SP70,OPDAWEBC
          IF        @EQUAL
            PACK      OPDAWEBC,BKREOPER,SP70
          ENDIF
.
          MATCH     SP70,OPDAUDAT
          IF        @EQUAL
            PACK      OPDAUDAT,BKRXLUPD,SP70
          ENDIF
.
          MATCH     SP70,OPDAUTIM
          IF        @EQUAL
            PACK      OPDAUTIM,BKRXLUPT,SP70
          ENDIF
.
          MATCH     SP70,OPDAWEBU
          IF        @EQUAL
            PACK      OPDAWEBU,BKRXWEBU,SP70
          ENDIF
.
          CALL      UPOPDEA1
          GOTO      PROC1000
.
PROC2000  PRINT     "Missing bokrc1af record for ",OPDAADMN
          GOTO      PROC1000
.
PROC3000  PRINT     "Missing bokrx1af record for ",OPDAADMN
          GOTO      PROC1000
.
PROC9000  DISPLAY   "Finished processing";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       BOKRC1IO/INC
          INC       BOKRX1IO/INC
          INC       OPRDEAIO/INC
