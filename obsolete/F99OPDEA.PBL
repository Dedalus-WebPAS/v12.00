.*****************************************************************************
.*    Program      : F99OPDEA                                                 *
.*    Description  : Conversion oprdetaf to new File layout                   *
.*                                                                           *
.*    Author       : Jill Habasque CAR 126282                                *
.*    Date         : 05/12/2007                                              *
.*    Modifications:                                                         *
.*****************************************************************************
.
          INC       STD001FD
.Data File Definition
.--------------------
OPRDETA1  IFILE     SQL, FIXED=500, KEY="U1-8,9-13,14-19,20-20,21-23"
OPRDETA2  IFILE     SQL, FIXED=500, KEY="U24-31,20-20,1-8,9-13,14-19,21-23"
OPRDETA3  IFILE     SQL, FIXED=500, KEY="U32-41"
OPRDETA4  IFILE     SQL, FIXED=500, KEY="U387-394,395-403,404-405,32-41"
OPRDETA5  IFILE     SQL, FIXED=500, KEY="U1-8,9-13,14-19,21-23,20-20"
.
. NAME    TYPE   LENGTH  PHYSICAL START LOC.   DESCRIPTION
. ----    ----   ------  -------- ----------   -----------
OPDADATE  DIM       8        8        1        Session Date (CCYYMMDD)
OPDATIME  DIM       5        5        9        Start Time (HH:MM)
OPDACLIN  DIM       6        6        14       Clinic/Surgeon
DOPDASTA  DIM       1        1        20       Booking Status
.                                                0 = Booked
.                                                1 = Preadmitted
.                                                2 = Admitted
.                                                3 = Cancelled
.                                                4 = Discharged
OPDACASE  DIM       3        3        21       Case Number
OPDAADMN  DIM       8        8        24       Admission Number
.
OPDAUNIQ  DIM       10       10       32       Unique id for theatre booking
OPDAEXPS  DIM       5        5        42       Expected Start Time
OPDAEXPD  FORM      4        3        47       Expected Duration (min)
OPDAANAE  DIM       3        3        50       Anaesthetic Code (Cat. OA )
OPDATYPE  DIM       3        3        53       Operation Type   (Cat. OY )
OPDATRAN  DIM       3        3        56       Transport Code   (Cat. OR )
OPDAUSR1  DIM       3        3        59       Unplan return to theatre (Cat Y6)
OPDAUSR2  DIM       3        3        62                    2 (Category Y7)
OPDAUSR3  DIM       3        3        65                    3 (Category Y8)
OPDAUSR4  DIM       3        3        68                    4 (Category Y9)
OPDAWAIT  DIM       1        1        71       Waiting Lists (Y/N)
OPDAPROV  DIM       9        9        72       Provisional item # 1 ( MBS or
.                                              ICD9CM)
OPDAPRV2  DIM       9        9        81       Provisional item # 2 ( MBS or
.                                              ICD9CM)
OPDAPRV3  DIM       9        9        90       Provisional item # 3 ( MBS or
.                                              ICD9CM)
OPDADES1  DIM       55       55       99      Operation description line 1
OPDADES2  DIM       55       55       154                                2
OPDADES3  DIM       55       55       209                                3
OPDACOMM  DIM       40       40       264       Comments
OPDACANC  DIM       3        3        304      Cancellation Code (Category SC)
OPDATHET  DIM       6        6        307      Operating Room Code
OPDAPRE1  DIM       5        5        313      Pre-operative time 1 (HH:MM)
OPDAPRE2  DIM       5        5        318      Pre-operative time 2 (HH:MM)
OPDAPRE3  DIM       5        5        323      Pre-operative time 3 (HH:MM)
OPDAPRE4  DIM       5        5        328      Pre-operative time 4 (HH:MM)
OPDAPRE5  DIM       5        5        333      Pre-operative time 5 (HH:MM)
OPDAPOS1  DIM       5        5        338      Post-operative time 1 (HH:MM)
OPDAPOS2  DIM       5        5        343      Post-operative time 2 (HH:MM)
OPDAPOS3  DIM       5        5        348      Post-operative time 3 (HH:MM)
OPDAPOS4  DIM       5        5        353      Post-operative time 4 (HH:MM)
OPDATDUR  FORM      4        3        358      Actual duration in theatre (mins)
OPDAKNOW  DIM       3        3        361      Known Infection (Category OK)
OPDACDAT  DIM       8        8        364      Cancellation Date
OPDAEQR1  DIM       3        3        372      Equipment Required 1(Category XE)
OPDAEQR2  DIM       3        3        375      Equipment Required 2(Category XE)
OPDALEQR  DIM       3        3        378      Loan Equip. Required(Category XO)
OPDAEQR3  DIM       3        3        381      Equipment Required 3(Category XE)
OPDAUNIT  DIM       3        3        384      Specialty/Unit (Category ST)
OPDAURNO  DIM       8        8        387      UR Number
OPDAPROC  DIM       9        9        395      WL Procedure Code(watproaf)
OPDACONT  DIM       2        2        404      WL Procedure Count
OPDAASRC  DIM       3        3        406      Admission Source
OPDACSST  DIM       1        1        409      Consent Obtained (N=No, Y=Yes)
OPDACSDT  DIM       8        8        410      Consect Changed Date (ccyymmdd)
OPDACRDT  DIM       8        8        418      Date record created (ccyymmdd)
OPDACTIM  DIM       8        8        426      Time record created (hh:mm:ss)
OPDAWEBC  DIM       10       10       434      WEB User Id rec.creator(websecaf)
OPDAUDAT  DIM       8        8        444      Last Update Date (ccyymmdd)
OPDAUTIM  DIM       8        8        452      Last Update Time (hh:mm:ss)
OPDAWEBU  DIM       10       10       460      WEB User Id rec.updater(websecaf)
OPDASPAR  DIM       30       30       470      Spare
.
.End of record                        500
.
.Redifined forms for key
.-----------------------
.
OPDASTAT  FORM      1        2        1       Booking Status
.
FINDFILE  FILE      ASCII, FIXED=256
.
OLDFILE   IFILE     SQL, FIXED=459, KEY="U1-8,9-13,14-19,20-20,21-23"
.
CMDLINE   DIM       100
DIM4      DIM       4
DIM4A     DIM       4
DIM6      DIM       6
DIM6A     DIM       6
DIM60     DIM       60
DDCENT    DIM       2
.
RECNUM    FORM      8
NEWFILE   DIM       60
NEWPATH   DIM       60
OLDPATH   DIM       60
DEFPATH   DIM       60
SAVEFLG   FORM      1
SP50      INIT    "                                                  "
SP60      INIT    "                                                            "
.
PRGNAM    INIT      "CONVERSION OPRDEAFD"
PRGID     INIT      "F99OPDEA"
VERSION   INIT      "V11.03.00"
.
. Start of Program
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN1000,MAIN0000,MAIN9999 * Yes, no, cancel
.
MAIN1000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
. Loop thru old file and WROPDEA1 records to new file
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/compress saved file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          
INIT9999  RETURN
.
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
PREP0000 
. open old file
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE,"oprdetaf"
          TRAPCLR   IO
          BRANCH    OVRCD,PREP5000       * Original file does not exist
          CLOSE     OLDFILE
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
. Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat oprdetaf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sopdetaf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat oprdetaf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sopdetaf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
. remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "oprdetaf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "sopdetaf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "oprdetaf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
.
.  The files new record length and unique key must be written here
.      eg    APPEND    " 195 U9-16",CMDLINE
.
          APPEND    " 500 U1-8,9-13,14-19,20-20,21-23",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has 
.  to be repeated with for each individual index
.
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " U24-31,20-20,1-8,9-13,14-19,21-23",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " U32-41",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " U387-394,395-403,404-405,32-41",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
           PACK      CMDLINE,SP60,SP60
           CLEAR     CMDLINE
           APPEND    "isadd ",CMDLINE
           APPEND    NEWFILE,CMDLINE
           APPEND    " U1-8,9-13,14-19,21-23,20-20",CMDLINE
           APPEND    SP60,CMDLINE
           RESET     CMDLINE
           EXECUTE   CMDLINE,TASKID
.
          OPEN      OPRDETA1,NEWFILE
.
. set the flag to say we want to continue
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
.
PREP9000  MOVE      ONE,EXIT
PREP9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and WROPDEA1 each record               *
.**********************************************************************
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
          PACK      KEY23,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
          ADD       ONE,RECNUM                    * update record counter
.
.  All necessary changes to the files field lengths, date changes
.  or any other sort of modification must be made here
.
          PACK      OPDACRDT,SP70
          PACK      OPDACTIM,SP70
          PACK      OPDAWEBC,SP70
          PACK      OPDAUDAT,SP70
          PACK      OPDAUTIM,SP70
          PACK      OPDAWEBU,SP70
.                                               * Pack key here
          PACK      KEY23,OPDADATE,OPDATIME,OPDACLIN,DOPDASTA,OPDACASE,SP70
          CALL      WROPDEA1                       * WROPDEA1 to new file
.
          IF        (RECNUM%1000) = 0
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     OPRDETA1                      * close new file
          CLOSE     OLDFILE                       * close old file
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS
.
READ9999  RETURN
.
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
VALD0000  MOVE      ZERO,EXIT
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or compress save file                                *
.**********************************************************************
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "sopdetaf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "compress -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not compressed or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.         Get the default directory
.**********************************************************************
DEFT0000  EXECUTE   "ldat oprdetaf.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding oprdetaf.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
DEFT9999  RETURN
+
.**********************************************************************
. ---- OLD IO ROUTINES ----
.**********************************************************************
READSOLD  RESET     KEY23
          READ      OLDFILE,KEY23;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE;OPDADATE,OPDATIME,OPDACLIN,DOPDASTA,OPDACASE:
                                   OPDAADMN,OPDAUNIQ,OPDAEXPS,OPDAEXPD:
                                   OPDAANAE,OPDATYPE,OPDATRAN,OPDAUSR1:
                                   OPDAUSR2,OPDAUSR3,OPDAUSR4,OPDAWAIT:
                                   OPDAPROV,OPDAPRV2,OPDAPRV3,OPDADES1:
                                   OPDADES2,OPDADES3,OPDACOMM,OPDACANC:
                                   OPDATHET,OPDAPRE1,OPDAPRE2,OPDAPRE3:
                                   OPDAPRE4,OPDAPRE5,OPDAPOS1,OPDAPOS2:
                                   OPDAPOS3,OPDAPOS4,OPDATDUR,OPDAKNOW:
                                   OPDACDAT,OPDAEQR1,OPDAEQR2,OPDALEQR:
                                   OPDAEQR3,OPDAUNIT,OPDAURNO,OPDAPROC:
                                   OPDACONT,OPDAASRC,OPDACSST,OPDACSDT:
                                   OPDASPAR
          GOTO      OVERCOND IF OVER
          MOVE      DOPDASTA,OPDASTAT
          RETURN
.
RAOPDEA1  MOVE      ZERO,OVRCD
          RESET     KEY23
          READ      OPRDETA1,KEY23;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RSOPDEA1  RESET     KEY23
          READ      OPRDETA1,KEY23;;
          RETURN
.
RDOPDEA1  MOVE      ZERO,OVRCD
          RESET     KEY23
          READ      OPRDETA1,KEY23;OPDADATE,OPDATIME,OPDACLIN,DOPDASTA,OPDACASE:
                                   OPDAADMN,OPDAUNIQ,OPDAEXPS,OPDAEXPD:
                                   OPDAANAE,OPDATYPE,OPDATRAN,OPDAUSR1:
                                   OPDAUSR2,OPDAUSR3,OPDAUSR4,OPDAWAIT:
                                   OPDAPROV,OPDAPRV2,OPDAPRV3,OPDADES1:
                                   OPDADES2,OPDADES3,OPDACOMM,OPDACANC:
                                   OPDATHET,OPDAPRE1,OPDAPRE2,OPDAPRE3:
                                   OPDAPRE4,OPDAPRE5,OPDAPOS1,OPDAPOS2:
                                   OPDAPOS3,OPDAPOS4,OPDATDUR,OPDAKNOW:
                                   OPDACDAT,OPDAEQR1,OPDAEQR2,OPDALEQR:
                                   OPDAEQR3,OPDAUNIT,OPDAURNO,OPDAPROC:
                                   OPDACONT,OPDAASRC,OPDACSST,OPDACSDT:
                                   OPDACDAT,OPDACTIM,OPDAWEBC,OPDAUDAT:
                                   OPDAUTIM,OPDAWEBU,OPDASPAR
          GOTO      OVERCOND IF OVER
          MOVE      DOPDASTA,OPDASTAT
          RETURN
.
RKOPDEA1  MOVE      ZERO,OVRCD
          READKS    OPRDETA1;OPDADATE,OPDATIME,OPDACLIN,DOPDASTA,OPDACASE:
                                   OPDAADMN,OPDAUNIQ,OPDAEXPS,OPDAEXPD:
                                   OPDAANAE,OPDATYPE,OPDATRAN,OPDAUSR1:
                                   OPDAUSR2,OPDAUSR3,OPDAUSR4,OPDAWAIT:
                                   OPDAPROV,OPDAPRV2,OPDAPRV3,OPDADES1:
                                   OPDADES2,OPDADES3,OPDACOMM,OPDACANC:
                                   OPDATHET,OPDAPRE1,OPDAPRE2,OPDAPRE3:
                                   OPDAPRE4,OPDAPRE5,OPDAPOS1,OPDAPOS2:
                                   OPDAPOS3,OPDAPOS4,OPDATDUR,OPDAKNOW:
                                   OPDACDAT,OPDAEQR1,OPDAEQR2,OPDALEQR:
                                   OPDAEQR3,OPDAUNIT,OPDAURNO,OPDAPROC:
                                   OPDACONT,OPDAASRC,OPDACSST,OPDACSDT:
                                   OPDACRDT,OPDACTIM,OPDAWEBC,OPDAUDAT:
                                   OPDAUTIM,OPDAWEBU,OPDASPAR
          GOTO      OVERCOND IF OVER
          MOVE      DOPDASTA,OPDASTAT
          RETURN
.
RPOPDEA1  MOVE      ZERO,OVRCD
          READKP    OPRDETA1;OPDADATE,OPDATIME,OPDACLIN,DOPDASTA,OPDACASE:
                                   OPDAADMN,OPDAUNIQ,OPDAEXPS,OPDAEXPD:
                                   OPDAANAE,OPDATYPE,OPDATRAN,OPDAUSR1:
                                   OPDAUSR2,OPDAUSR3,OPDAUSR4,OPDAWAIT:
                                   OPDAPROV,OPDAPRV2,OPDAPRV3,OPDADES1:
                                   OPDADES2,OPDADES3,OPDACOMM,OPDACANC:
                                   OPDATHET,OPDAPRE1,OPDAPRE2,OPDAPRE3:
                                   OPDAPRE4,OPDAPRE5,OPDAPOS1,OPDAPOS2:
                                   OPDAPOS3,OPDAPOS4,OPDATDUR,OPDAKNOW:
                                   OPDACDAT,OPDAEQR1,OPDAEQR2,OPDALEQR:
                                   OPDAEQR3,OPDAUNIT,OPDAURNO,OPDAPROC:
                                   OPDACONT,OPDAASRC,OPDACSST,OPDACSDT:
                                   OPDACRDT,OPDACTIM,OPDAWEBC,OPDAUDAT:
                                   OPDAUTIM,OPDAWEBU,OPDASPAR
          GOTO      OVERCOND IF OVER
          MOVE      DOPDASTA,OPDASTAT
          RETURN
.
UPOPDEA1  MOVE      OPDASTAT,DOPDASTA
          UPDATE    OPRDETA1;OPDADATE,OPDATIME,OPDACLIN,DOPDASTA,OPDACASE:
                                   OPDAADMN,OPDAUNIQ,OPDAEXPS,OPDAEXPD:
                                   OPDAANAE,OPDATYPE,OPDATRAN,OPDAUSR1:
                                   OPDAUSR2,OPDAUSR3,OPDAUSR4,OPDAWAIT:
                                   OPDAPROV,OPDAPRV2,OPDAPRV3,OPDADES1:
                                   OPDADES2,OPDADES3,OPDACOMM,OPDACANC:
                                   OPDATHET,OPDAPRE1,OPDAPRE2,OPDAPRE3:
                                   OPDAPRE4,OPDAPRE5,OPDAPOS1,OPDAPOS2:
                                   OPDAPOS3,OPDAPOS4,OPDATDUR,OPDAKNOW:
                                   OPDACDAT,OPDAEQR1,OPDAEQR2,OPDALEQR:
                                   OPDAEQR3,OPDAUNIT,OPDAURNO,OPDAPROC:
                                   OPDACONT,OPDAASRC,OPDACSST,OPDACSDT:
                                   OPDACRDT,OPDACTIM,OPDAWEBC,OPDAUDAT:
                                   OPDAUTIM,OPDAWEBU,OPDASPAR
          RETURN
.
WROPDEA1  RESET     KEY23
          MOVE      OPDASTAT,DOPDASTA
          WRITE     OPRDETA1,KEY23;KEY23,OPDAADMN,OPDAUNIQ:
                                   OPDAEXPS,OPDAEXPD:
                                   OPDAANAE,OPDATYPE,OPDATRAN,OPDAUSR1:
                                   OPDAUSR2,OPDAUSR3,OPDAUSR4,OPDAWAIT:
                                   OPDAPROV,OPDAPRV2,OPDAPRV3,OPDADES1:
                                   OPDADES2,OPDADES3,OPDACOMM,OPDACANC:
                                   OPDATHET,OPDAPRE1,OPDAPRE2,OPDAPRE3:
                                   OPDAPRE4,OPDAPRE5,OPDAPOS1,OPDAPOS2:
                                   OPDAPOS3,OPDAPOS4,OPDATDUR,OPDAKNOW:
                                   OPDACDAT,OPDAEQR1,OPDAEQR2,OPDALEQR:
                                   OPDAEQR3,OPDAUNIT,OPDAURNO,OPDAPROC:
                                   OPDACONT,OPDAASRC,OPDACSST,OPDACSDT:
                                   OPDACRDT,OPDACTIM,OPDAWEBC,OPDAUDAT:
                                   OPDAUTIM,OPDAWEBU,OPDASPAR
          RETURN
.
DEOPDEA1  RESET     KEY23
          DELETE    OPRDETA1,KEY23
          RETURN
.
          INC       STD001IO
