.******************************************************************************
.                                 OPRSDTDS
.                    Display session dates and start times
.
. Description : Display Operating room day indicator and start time for a given
.               clinic.  This routine is BULLSHIT FREE in otherwords NO
.               dead code or poor programming.
.
. Requirements: This routine requires OPRSEMFD and OPRSEMIO.INC and KEY12,
.               KEY10, and CLINIC var must be define in program
.
.             OPRSDTDS was programed by Bill Dew on the 05/03/1990
.                        I.B.A. Development Department
.                      D. Jones and G. Williams Supervisor
.
.   Modifications : 
.
.******************************************************************************
OPRSDTDS 
.
          DISPLAY    *P1:4,*EF,*P28:4,*V2LON,*ULON,"SESSION MASTER RECORDS"
          DISPLAY    *ULON,*P1:6,"Operating Room                        ":
                     *P41:6,"Day of week",*P55:6,"start time"
          MOVE       SEVEN,CVERT
          MOVE       ONE,CCOL
.
          PACK       KEY12,CLINIC,SP20
          CALL       RSOPSEM1
SDTDS222  
          CALL       RKOPSEM1          * read session master details
          BRANCH     OVRCD,SDTDS990
.
          MATCH      OPSMCLIN,CLINIC   * test for change in clinic 
          GOTO       SDTDS990 IF NOT EQUAL
.
          BRANCH     OPSMDAYI,SDTDS233,SDTDS244,SDTDS255,SDTDS266,SDTDS277:
                              SDTDS288,SDTDS299
SDTDS233  MOVE      "Monday",KEY10
          GOTO      SDTDS333
SDTDS244  MOVE      "Tuesday",KEY10
          GOTO      SDTDS333
SDTDS255  MOVE      "Wednesday",KEY10
          GOTO      SDTDS333
SDTDS266  MOVE      "Thursday",KEY10
          GOTO      SDTDS333
SDTDS277  MOVE      "Friday",KEY10
          GOTO      SDTDS333
SDTDS288  MOVE      "Saturday",KEY10
          GOTO      SDTDS333
SDTDS299  MOVE      "Sunday",KEY10
.
SDTDS333
          MOVE       "Unknown",OPRMDESC
          PACK       KEY6,OPSMTHET,SP6
          CALL       RDOPOPR1
.
          DISPLAY    *PCCOL:CVERT,*V2LON,OPRMDESC,SP2,*HOFF,KEY10,SP5,OPSMTIME
          ADD        ONE,CVERT
          COMPARE    TWENTY3,CVERT
          GOTO       SDTDS222 IF LESS
.
.         New standards question
.
          KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, ":
                    "(",*V2LON,"F",*HOFF,")irst Screen, ":
                    "(",*V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
          CMATCH    ANSC,ANS
          GOTO      SDTDS999 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      OPRSDTDS IF EQUAL
.
.         Clear the screen
.
SDTDS400  MOVE      SEVEN,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:CVERT,*EF
          GOTO      SDTDS222
.
SDTDS990  
SDTDS999  RETURN
