.******************************************************************************
.* System    :   Outpatients                                                  *
.* Program   :   FX817112                                                     *
.* Desc      :   Loop through the Outpatient Booking Table for future bookings*
.*               and update Referring Practice and Count with the Referring   *
.*               Practice & Count of the linked Referral if it exists.        *
.******************************************************************************
.* Date      :   21/06/2017                                                   *
.* Author    :   Richa Phogat                                                 *
.* Function  :   This program will update outpatient booking table (OUTBB1AF) *
.* Modifications  :                                                           *
.*    V10.10.00  21/06/2017  Richa Phogat CAR 0817112                         *
.*               Created program                                              *
.******************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       ALLCONFD/INC
          INC       ALLLNKFD/INC
          INC       ALLREFFD/INC
          INC       OUTBB1FD/INC
          INC       OUTBOAFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
CURRDATE  DIM       8
.
.
PRGID     INIT      "FX817112"
PRGNAM    INIT      "Outpatient Booking Table     "
VERSION   INIT      "V10.10.00"
+
.**************************************************************************
.*                              MAIN0000                                  *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      OUTB0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: MAIN0000  *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"alllnkaf";
          OPEN      ALLLNKA1,"alllnkaf"
.
          DISPLAY   *P64:24,"alllnkaf";
          OPEN      ALLLNKA2,"alllnkaf"
.
          DISPLAY   *P64:24,"allrefaf";
          OPEN      ALLREFA1,"allrefaf"       
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"outbb1af";
          OPEN      OUTBB1A1,"outbb1af"
.
          DISPLAY   *P64:24,"outbb1af";
          OPEN      OUTBB1A2,"outbb1af"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.****************************************************************************
.*                                OUTB0000             Called by: MAIN0000  *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
OUTB0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,*HOFF,"Hit Enter to start"
          CALL      HITENTER
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD  * Get Current Date
          REP       " 0",CURRDATE
.
          PACK      KEY16,CURRDATE,SP70   * outpatient booking table key
          CALL      RDSBOKB2              * position at start of file
OUTB0100  CALL      RDKBOKB2              * read next record
          BRANCH    OVRCD,OUTB9900        * eof - finished
.
. REMOVE BELOW 3 LINES - ADDED ONLY FOR TESTING
.         MATCH     " 4642799",DOBAOUTN
.         GOTO      OUTB0100 IF NOT EQUAL
.         CALL      HITENTER
.
          PACK      KEY16,DOBAOUTN,SP70   * referral table key
          CALL      RSALLNK2              * position at start of file
OUTB0200  CALL      RKALLNK2              * read next record
          BRANCH    OVRCD,OUTB0100        * eof - finished
.
          MATCH     ALLNLNKV,DOBAOUTN
          GOTO      OUTB0200 IF NOT EQUAL
.
          PACK      KEY8,ALLNVISN,SP70
          CALL      RDALREF1                     
          BRANCH    OVRCD,OUTB0100         * record not found 
.
          MATCH     OTBBGPPC,ALRERHCR
          GOTO      OUTB0300 IF NOT EQUAL
.
          MATCH     OTBBGPPT,ALRERHCT
          GOTO      OUTB0300 IF NOT EQUAL
.
          GOTO      OUTB0200
.
OUTB0300  MOVE      ALLNLNKV,KEY8
          CALL      RDBOKB1
          BRANCH    OVRCD,OUTB0100
.
          MOVE      ALRERHCP,OTBBRFGP
          MOVE      ALRERHCR,OTBBGPPC
          MOVE      ALRERHCT,OTBBGPPT
.
          CALL      UPBOKB1
          GOTO      OUTB0100
.
OUTB9900  MOVE      ONE,EXIT
.
OUTB9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       ALLREFIO/INC
          INC       OUTBB1IO/INC
          INC       ALLLNKIO/INC

