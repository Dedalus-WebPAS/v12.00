. *----------------------------------------------------------------------
. * Program Name  :   IBAHIC06.PBL
. *
. * Function      :   Program to print DB1N HIC Claim Form
. *
. *  Mods          :  
. *       V10.13.01 30/07/2018  Richa Phogat     TSK 0848506                   
. *                 Updated keylength for RSPRDO3 & RKPRDO3 from KEY28 to KEY32
. *       V10.11.01 25/07/2017  Ebon Clements   TSK 0268970     
. *                 Change to use OUTCLIFD index 1 instead of index 2
. *       V10.03.01 26/11/2012  Mike Laming     CAR 268385
. *                 Mods to INIT0000 & OUTFIL00 to stop I*C to file "outcliaf"
. *       V9.05.B01 20/02/2006  Ebon Clements     CAR 76341
. *                 Added referral and encounter file audit   
. *       V9.04.08  26/08/2005  J.Tan          CAR 71442 
. *                 Mods to payee name
. *       V9.04.07  01/08/2005  J.Tan          CAR 69062 
. *                 Changed claim index 7 to include voucher number 
. *       V9.04.06  01/08/2005 J.Tan        CAR 69062
. *                 Mods to print voucher number form claim file as trans.no
. *       V9.04.05  01/07/2005 Davin        CAR 61563
. *                 Print full item amount rather then 85%
. *                 Added routine to calculate batch total for header
. *       V9.04.04  22/06/2005 Davin        CAR 64290
. *                 Transaction number is right justified & zero filled
. *                 Remove new page functionality (handled via create forms)
. *       V9.04.03  15/06/2005 Davin        CAR 56698
. *                 Print patient name on new line for each voucher item
. *       V9.04.02  08/06/2005 Davin        CAR 56698
. *                 Print indefinite referral period as '99' months
. *                 Print counter instead of 'voucher position'
. *       V9.04.01  06/06/2005 Davin
. *                 Mods for DB1N - added two lines for 'create forms'
. *       V9.04.00  23/04/2005 Davin
. *                 Created program
. *
. * Parameter     :   BATCHKEY (batch number,serv.provider,MCI,payee provider)
. * Variables     :  
. *
. *----------------------------------------------------------------------
.
          INC       STD001FD
          INC       ALLCONFD/INC
          INC       ALLLNKFD/INC
          INC       ALLREFFD/INC
          INC       HICBTCFD/INC
          INC       HICCITFD/INC
          INC       HICCLMFD/INC
          INC       OUTSITFD/INC
          INC       OUTCLIFD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
          INC       PMSHCLFD/INC
          INC       PMSHCPFD/INC
          INC       PATCODFD/INC
          INC       PRIPRAFD/INC
          INC       PRIDOCFD/INC
.
BATCHKEY  DIM       33
CFILEPRE  DIM       3
VOUCHCNT  DIM       2
CLAIMDAT  DIM       8
DIM5      DIM       5
DIM8      DIM       8
DIM10     DIM       10
DIM80     DIM       80
FORM62    FORM      6.2
ITEMAMT   FORM      12.2
PATDOB    DIM       8
PROVNUMB  DIM       10
REFRDATE  DIM       8
REFRPERD  FORM      6
SERVDATE  DIM       8
SERVPROV  DIM       30
TOTALBTC  FORM      12.2
TOTALCLM  FORM      12.2
.
PRGID     INIT      "IBAHIC06"
PRGNAM    INIT      "Print DB1N HIC Claim Form"
VERSION   INIT      "V10.13.01"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
ML0000    CALL      INIT0000                * initialization and open files
.
ML1000    CALL      INPT0000                * Input Batch Key
          BRANCH    EXIT,ML9999
.
          CALL      CONTQST                 * see if O.K. to continue
          BRANCH    CEXIT,ML2000,ML1000,ML9999
.
ML2000    CALL      PRDB0000                * set up & print form
          GOTO      ML1000
.
ML9999    CHAIN     PGM                     * chain back to program
+
.------------------------------------------------------------
. Input Batch Key
.------------------------------------------------------------
INIT0000  CALL      DISPHEAD                * display heading
.
          OPEN      ALLREFA1,"allrefaf"
          OPEN      ALLLNKA2,"alllnkaf"
          OPEN      HICBTCA1,"hicbtcaf"
          OPEN      HICCITA1,"hiccitaf"
          OPEN      HICCLMA7,"hicclmaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      PMSHCLA1,"pmshclaf"
          OPEN      PMSHCPA1,"pmshcpaf"
          OPEN      PRIPRAC1,"pripracf"
          OPEN      PRIDOCT3,"pridoctf"
.
          MOVE      "out",CFILEPRE
          OPEN      OUTSITA1,"outsitaf"
.                                                                     *I-268385
          PACK      CFNAME,CFILEPRE,FILCLIA1    * Get the name of the CLIA1 file
          OPEN      OUTCLIA1,CFNAME             * Open the file
.
INIT9999  RETURN
+
.------------------------------------------------------------
. Check Correct Files are Open for System
.------------------------------------------------------------
OUTFIL00  MOVE      HCBTSITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,OUTFIL99
.                                                                     *C-268385
          MATCH     OSTFILE,CFILEPRE            * Save Current File Prefix
          IF        !@EQUAL
            MOVE      OSTFILE,CFILEPRE          * Save Current File Prefix
.
            PACK      CFNAME,CFILEPRE,FILCLIA1  * Get the name of the CLIA1 file
            CLOSE     OUTCLIA1
            OPEN      OUTCLIA1,CFNAME           * Open the file
          ENDIF
.
OUTFIL99  RETURN
+
.------------------------------------------------------------
. Input Batch Key
.------------------------------------------------------------
INPT0000  DISPLAY   *P1:3,*EF:
                    *P1:4,"Batch Number   ",*P25:4,COLON:
                    *P1:5,"Service Provider Number ",*P25:5,COLON:
                    *P1:6,"MCI    ",*P25:6,COLON:
                    *P1:7,"Payee Provider Number ",*P25:7,COLON:
                    *P1:8,"Batch Key ",*P25:8,COLON;
.
.>>>>     KEYIN     *P30:4,*V2LON,DIM5
.>>>>     PACK      DIM5,DIM5,SP70
.>>>>     MATCH     DIM5,SP70
.>>>>     GOTO      INPT9500 IF EQUAL
.
.>>>>     KEYIN     *P30:5,*V2LON,DIM10
.>>>>     PACK      DIM10,DIM10,SP70
.>>>>     KEYIN     *P30:6,*V2LON,DIM8
.>>>>     PACK      DIM8,DIM8,SP70
.>>>>     KEYIN     *P30:7,*V2LON,KEY10
.>>>>     PACK      KEY10,KEY10,SP70
.
          KEYIN     *P30:8,*V2LON,BATCHKEY
          PACK      BATCHKEY,BATCHKEY,SP70
          MATCH     BATCHKEY,SP70
          GOTO      INPT9500 IF EQUAL
.
.>>>>     PACK      BATCHKEY,DIM5,DIM10,DIM8,KEY10,SP70
.
          MOVE      ZERO,EXIT
          GOTO      INPT9999
.
INPT9500  MOVE      ONE,EXIT
INPT9999  RETURN
+
.------------------------------------------------------------
. Print DB1N HIC Claim Form
.------------------------------------------------------------
PRDB0000  MOVE      BATCHKEY,KEY33          * read batch file
          CALL      RDHCBTC1
          BRANCH    OVRCD,PRDB5000
.
          UNPACK    HCBTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPDATE
          MOVE      CPDATE,CLAIMDAT         * date of claim
.
          CALL      OUTFIL00                * Open prefixed Outpatient Files
.
          CALL      CBTC0000                * calculate batch total
          CALL      PTOP0000                * Print header
.
          MOVE      ZERO,FORM2
          MOVE      SP70,VOUCHCNT
          PACK      KEY15,HCBTBTCH,SP70     * get all claims for the batch
          CALL      RSHCCLM7
PRDB1000  CALL      RKHCCLM7
          BRANCH    OVRCD,PRDB5000
.
          MATCH     HCBTBTCH,HCCLBTCH       * Same Batch number?
          GOTO      PRDB5000 IF NOT EQUAL
.
          CALL      SETHIC00                * get voucher details
          BRANCH    EXIT,PRDB5000
.
          MOVE      ZERO,TOTALCLM
          MOVE      ZERO,FORM1
          PACK      KEY18,HCCLCLMN,HCCLVISN,SP70
          CALL      RSHCCIT1
PRDB2000  CALL      RKHCCIT1
          BRANCH    OVRCD,PRDB3000
.
          MATCH     HCCLCLMN,HCCICLMN       * Same claim number
          GOTO      PRDB3000 IF NOT EQUAL
.
          MATCH     HCCLVISN,HCCIVISN       * Same visit number
          GOTO      PRDB3000 IF NOT EQUAL
.
          UNPACK    HCCIIDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPDATE
          MOVE      CPDATE,SERVDATE         * date of service
.
          MOVE      HCCICAMT,ITEMAMT
.
.         ADD       ONE,FORM2               * add to voucher counter
.         MOVE      FORM2,VOUCHCNT
.
          MOVE      HCCLVPOS,VOUCHCNT
          REP       " 0",VOUCHCNT
.
          PRINT      *1,VOUCHCNT,*4,PMEDI,SP1,PTMXMCCD,*18,SP30,SP30,*79,PATDOB:
                    *88,SERVDATE,*97,HCCIITMN,*107,HCCIIDES,*138,ITEMAMT:
                    *154,SP15,*170,HCCIPUSE,*191,REFRDATE,*200,PROVNUMB:
                    *211,REFRPERD
          PRINT     *18,PACFNAME
.
          ADD       ITEMAMT,TOTALCLM        * add to total
          MOVE      ONE,FORM1
          ADD       TWO,CLNO
          GOTO      PRDB2000
.
PRDB3000  IF        FORM1=0
            PRINT     *N;
            ADD       ONE,CLNO
          ENDIF
.
          PRINT     *154,TOTALCLM
          GOTO      PRDB1000
.
PRDB5000
.
PRDB9999  RETURN
+
.------------------------------------------------------------
. Setup HIC Claim (Voucher) Details
.------------------------------------------------------------
SETHIC00  PACK      KEY16,HCCLVISN,SP70
          CALL      RSALLNK2
          CALL      RKALLNK2                * get referral link
          BRANCH    OVRCD,SETHIC95
.
          MATCH     HCCLVISN,ALLNLNKV
          GOTO      SETHIC95 IF NOT EQUAL   * same visit?
.
          MOVE      HCCLURNO,KEY8           * read master record
          CALL      RDMAST1
          BRANCH    OVRCD,SETHIC95
          CALL      RDPMPX21
          BRANCH    OVRCD,SETHIC95
.
          PACK      KEY8,ALLNVISN,SP70
          CALL      RDALREF1                * get referral record
          BRANCH    OVRCD,SETHIC95
.
          UNPACK    ALRERDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPDATE
          MOVE      CPDATE,REFRDATE         * date of referral
.
          MOVE      ZERO,REFRPERD
          PACK      KEY5,ANSR,ANSI,ALRERTYP
          CALL      RDCODE1
          BRANCH    OVRCD,SETHIC20
.
          COMPARE   ZERO,TCFORM6
          IF        @EQUAL
            MOVE    NINTY9,REFRPERD
            GOTO      SETHIC20
          ENDIF
.
          ASSIGN    ((TCFORM6/365)*12),FORM62   * referral period
          MOVE      FORM62,REFRPERD
.
SETHIC20  PACK      PROVNUMB,SP70           * initialise provider number
          MATCH     SP70,ALREFHCP
          IF        !@EQUAL
            MOVE      ALREHCPP,PROVNUMB     * hcp provider number (free format)
            GOTO      SETHIC50
          ENDIF
.
          PACK      KEY10,ALRERHCP,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,SETHIC50          * cannot find referring hcp
.
          PACK      KEY20,ALRERHCP,ALRERHCR,SP70
          CALL      RDPMHCL1
          IF        OVRCD<>1
            MOVE      PMHLPRV1,PROVNUMB     * hcl provider number
          ELSE
            MOVE      PMHCPRV1,PROVNUMB     * hcp provider number
          ENDIF
.
SETHIC50  UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPDATE
          MOVE      CPDATE,PATDOB           * patient date of birth
.
          MOVE      SP70,SERVPROV
          PACK      KEY20,HCCLSITE,HCCLCLID,Z70
          CALL      RDSCLIA1
          CALL      RDPCLIA1                * To get Clinic ID Description
          IF        OVRCD=0
            MATCH     HCCLSITE,OCASITE
            IF        @EQUAL
              MATCH     HCCLCLID,OCACLIN
              IF        @EQUAL
                MOVE      OCADESC,SERVPROV    * service provider
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      TWO,PACFRMT
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME                 * set patient name
.
SETHIC90  MOVE      ZERO,EXIT               * hicclaim record exists
          GOTO      SETHIC99
.
SETHIC95  MOVE      ONE,EXIT                * hicclaim record doesn't exist
SETHIC99  RETURN
+
.------------------------------------------------------------
. Print header
.------------------------------------------------------------
PTOP0000  MOVE      SP70,SERVPROV
          PACK      KEY20,HCBTSITE,HCBTCLID,Z70
          CALL      RDSCLIA1
          CALL      RDPCLIA1                * To get Clinic ID Description
          IF        OVRCD=0
            MATCH     HCBTSITE,OCASITE
            IF        @EQUAL
              MATCH     HCBTCLID,OCACLIN
              IF        @EQUAL
                MOVE      OCADESC,SERVPROV    * default service provider
              ENDIF
            ENDIF
          ENDIF
.
          PACK      KEY32,HCBTPSRV,SP70
          CALL      RSPRDO3
          CALL      RKPRDO3
          BRANCH    OVRCD,PTOP2000
.
          MATCH     HCBTPSRV,PRDOPROV         * Same provider number?
          GOTO      PTOP2000 IF NOT EQUAL
.
.         Get doctor name
.
          PACK      KEY10,PRDODOCT,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,PTOP2000
.
          MOVE      "1",PACFRMT
          MOVE      PMHCTITL,PACTITLE
          MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          CALL      PACNAME
          MOVE      PACFNAME,SERVPROV
.
PTOP2000  MOVE      SP70,PRPRPDOC
          MOVE      SP70,PRPRDESC
          PACK      DIM80,SP70,SP70
          PACK      KEY6,HCBTMPRA,SP70
          CALL      RDPRPR1                 * get medical practice
          BRANCH    OVRCD,PTOP4000
          PACK      DIM80,PRPRADD1,PRPRADD2,PRPRADD3,PRPRPOST,SP70,SP70
.
PTOP4000  MATCH     HCBTPYEE,HCBTPSRV
          GOTO      PTOP6000 IF NOT EQUAL
.
          MOVE      SERVPROV,PRPRDESC
          GOTO      PTOP8000
.
PTOP6000  MATCH     SP70,PRPRPDOC
          GOTO      PTOP8000 IF EQUAL
.
          PACK      KEY10,PRPRPDOC,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,PTOP8000
.
          MOVE      "1",PACFRMT
          MOVE      PMHCTITL,PACTITLE
          MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          CALL      PACNAME
          MOVE      PACFNAME,PRPRDESC
.
. Lines 3-10, all starting at position 35
.
PTOP8000  PRINT     *1,"CREATEFORMDB1N":
                    *N,"medicaredb1n":
                    *N,"MCI: ",*35,HCBTPMCI:
                    *N,"Claim Number: ",*35,HCBTBTCH:
                    *N,"Date of Claim: ",*35,CLAIMDAT:
                    *N,"Service Provider Name: ",*35,SERVPROV:
                    *N,"Service Provider Address: ",*35,DIM80:
                    *N,"Service Provider Number: ",*35,HCBTPSRV:
                    *N,"Number of Assignment forms: ",*35,HCBTSIZE:
                    *N,"TOTAL BENEFIT AMOUNT: ",*35,TOTALBTC:
                    *N,"Provider to receive benefits: ",*35,PRPRDESC:
                    *N,"Payee's Provider No.: ",*35,HCBTPYEE:
                    *N,"CN ",*4,"Medicare No. ",*18,"Patient Name ",*79,"DOB ":
                       *88,"ServDate ",*97,"MBS Item ",*107,"Description ":
                       *138,"Benefit Assignd ",*154,"Total ":
                       *170,"Provider Use ",*191,"RefrDate ",*200,"RefProvNum ":
                       *211,"RefPrd "
.
PTOP9999  RETURN
+
.------------------------------------------------------------
. Calculate batch total for header
.------------------------------------------------------------
CBTC0000  MOVE      ZERO,TOTALBTC
          PACK      KEY15,HCBTBTCH,SP70     * get all claims for the batch
          CALL      RSHCCLM7
CBTC1000  CALL      RKHCCLM7
          BRANCH    OVRCD,CBTC9999
.
          MATCH     HCBTBTCH,HCCLBTCH       * Same Batch number?
          GOTO      CBTC9999 IF NOT EQUAL
.
          PACK      KEY18,HCCLCLMN,HCCLVISN,SP70
          CALL      RSHCCIT1
CBTC2000  CALL      RKHCCIT1
          BRANCH    OVRCD,CBTC1000
.
          MATCH     HCCLCLMN,HCCICLMN       * Same claim number
          GOTO      CBTC1000 IF NOT EQUAL
.
          MATCH     HCCLVISN,HCCIVISN       * Same visit number
          GOTO      CBTC1000 IF NOT EQUAL
.
          MOVE      HCCICAMT,ITEMAMT
.
          ADD       ITEMAMT,TOTALBTC        * add to total
          GOTO      CBTC2000
.
CBTC9999  RETURN
+
.
          INC       STD001IO
          INC       ALLLNKIO/INC
          INC       ALLREFIO/INC
          INC       HICBTCIO/INC
          INC       HICCITIO/INC
          INC       HICCLMIO/INC
          INC       OUTSITIO/INC
          INC       OUTCLIIO/INC
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC
          INC       PMSHCLIO/INC
          INC       PMSHCPIO/INC
          INC       PATCODIO/INC
          INC       PRIPRAIO/INC
          INC       PRIDOCIO/INC
.
