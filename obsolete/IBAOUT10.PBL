. **********************************************************
. *              P R O G R A M  S U M M A R Y              *
. *              -------------  -------------              *
. *                                                        *
. *     PROGRAM NAME:     IBAOUT10                         *
. *                                                        *
. *     FUNCTION:         CHARGE MAINTENANCE               *
. *                                                        *
. *     DATE WRITTEN:     02/09/88                         *
. *                                                        *
. *     AUTHOR:           M.HAYSE                          *
. *                                                        *
. *     MODIFICATIONS:                                     *
. *              V9.02.01 11/10/2001 J.Tan                 *
. *                       Fixed key for patnip file        *
. *              V5.01.01 14.08.89 S.Barcham               *
. *                       Fix up headings                  *
. *              V5.01.02 02.10.89 L.P.                    *
. *                       Recompiled with new OUTAUXFD     *
. *                       SRF102514                        *
. *              V5.01.03 05.10.89 K.Peak                  *
. *                       Replaced uppercase audits with   *
. *                       lowercase  SRF 102601            *
. *              V5.01.04 22/05/91  Steve O'Gorman         *
. *                       Fixed Errors found by the        *
. *                       New Compiler                     *
. *                                                        *
. **********************************************************
.
          INC       STD001FD
.
          INC       PATCODFD/INC
.
          INC       OUTAUXFD/INC
          INC       OUTCONFD/INC
.
          INC       PATNIPFD/INC       * patients charge file
.
.         Extra variables
.
CODE      DIM       2
CODET     INIT      "T "
CODECL    INIT      "CL"
CODECI    INIT      "CI"        **** CLINIC INDICATOR
CODEFI    INIT      "FI"
CODECV    INIT      "CV"
.
CHG       FORM      1        * indicator to see if anything has changed
.
VERT      FORM      2
COL       FORM      2
.
FIELD     FORM      1
MODE1     DIM       1
MODE      DIM       1
.
RESDDESC  DIM       20
COMPDESC  DIM       20
ITEMDESC  DIM       20
INDCDESC  DIM       20
VISTDESC  DIM       20
.
AMNT1     FORM      7.2
.
.
PRGID     INIT      "IBAOUT10"
PRGNAM    INIT      "Charge Maintenance"
VERSION   INIT      "V10.13.00"
+
.         Start of program
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening patnipbf";
          OPEN      PATNIPB1,"patnipbf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
          READ      CONTROLF,THIRTY1;*53,HOAUDC
.
          MOVE      SP1,AUMODE
          BRANCH    HOAUDC OF MENU
          MOVE      ANSC,AUTYPE
          REP       "Cc",AUTYPE
          CALL      STRTAUDX
.
MENU      DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Insert":
                    *P1:6,*V2LON,TWO,*HOFF," = Change":
                    *P1:7,*V2LON,THREE,*HOFF," = Delete"
.
BEGIN     KEYIN     *P1:9,*EL,"Please Select : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
REPEAT    BRANCH    OPTION OF INSERT,CHANGE,DELETE
          BEEP
          GOTO      BEGIN
+
.
.       Add a charge
.
INSERT    CALL      DISPI                            * menu for insert
          CALL      DISPSC                           * main menu
.
          MOVE      ANSI,MODE1
          MOVE      MODE1,MODE
          MOVE      ZERO,CHG                         * initialise CHG
.
          BRANCH    HOAUDC OF INPFLDS
          MOVE      ANSI,AUMODE
          GOTO      INPFLDS
.
.       Change a charge
.
CHANGE    CALL      DISPC
          CALL      DISPSC
.
          MOVE      ZERO,CHG
          MOVE      ANSC,MODE1
          MOVE      MODE1,MODE
.
          BRANCH    HOAUDC OF INPFLDS
          MOVE      ANSC,AUMODE
          GOTO      INPFLDS
+
.
.       Delete a Charge
.
DELETE    CALL      DISPD
          CALL      DISPSC
.
          MOVE      ANSD,MODE1
          MOVE      MODE1,MODE
.
          BRANCH    HOAUDC OF INPFLDS
          MOVE      ANSD,AUMODE
          GOTO      INPFLDS
.
DELCS     CALL      DISPDA
.
.REKEY   KEYIN        *P1:24,*EL,"Correct ",*DV,"Rate  "," (":
.                     *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF:
.                     ") ? ",*V2LON,ANS;
.        CMATCH       ANSN,ANS
.        GOTO         REPEAT IF EQUAL
.
REKEY1    KEYIN     *P1:24,*EL,"Are you sure ":
                    "you want to delete this ",*DV,"Rate  "," (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF:
                    ") ? ",*V2LON,ANS;
.
          CMATCH    ANSY,ANS
          GOTO      WARDEL IF EQUAL
.
          CMATCH    ANSN,ANS
          GOTO      WRONG1 IF NOT EQUAL
.
          GOTO      REPEAT
.
WRONG1    BEEP
          GOTO      REKEY1
.
WARDEL    MOVE      TWO,NBSYST      * System - Outpatients
.
          PACK      KEY13 WITH NBSYST,NBINDC,NBRESD,NBCOMP,NBINDV
          CALL      DENIPB1
.
          BRANCH    HOAUDC OF ENDDELT
.
          PACK      AUCONBEF WITH KEY13,SP30,SP30
          MOVE      "syst/resd/comp/indc ",AUDESC
          PACK      AUCONAFT WITH SP30,SP20
          CALL      WRITAUDX
          CALL      POSTAUDX
.
ENDDELT   DISPLAY   *P1:24,*EL,*V2LON,"** Deleted ** ";
          CALL      HITENTER
          GOTO      REPEAT
.
.         Input fields
.
INPFLDS   MOVE      SP3,NBITEM
          MOVE      SP3,NBRESD
          MOVE      SP3,NBCOMP
          MOVE      SP3,NBINDC
          MOVE      SP3,NBINDV
          MOVE      ZERO,NBAMNT
          MOVE      SP30,ITEMDESC
          MOVE      SP30,RESDDESC
          MOVE      SP30,COMPDESC
          MOVE      SP30,INDCDESC
          MOVE      SP30,VISTDESC
          MOVE      ZERO,NBAMNT
          MOVE      SP30,NBDESC
.
.       Input clinic indicator
.
KINDC     MOVE      SP3,NBINDC
          MOVE      SP30,INDCDESC
.
          KEYIN     *P29:4,*EL,*DV,UNDLN3,*P29:4,*V2LON,NBINDC:
                    *P29:4,*DV,NBINDC
.
          ENDSET    NBINDC
          APPEND    SP5,NBINDC
          RESET     NBINDC
.
          MATCH     SP3,NBINDC
          GOTO      MENU IF EQUAL
.
          CMATCH    QUEST,NBINDC
          GOTO      KINDC1 IF NOT EQUAL
.
          MOVE      SP3,NBINDC
          MOVE      CODECI,CODE
          CALL      PATCODDS
          CALL      DISPSC
          CALL      DISPDA
          GOTO      KINDC
.
KINDC1    PACK      KEY5 WITH CODECI,NBINDC
          CALL      RDCODE1
          BRANCH    OVRCD OF ENDN1
          MOVE      TDESC,INDCDESC
          DISPLAY   *P40:4,*V2LON,INDCDESC
          GOTO      KKRESD
.
ENDN1     DISPLAY   *P40:4,*EL,*B,*V2LON:
                    "** Clinic Type Does Not Exist **",*W
          GOTO      INPFLDS
.
.
.         Input resident status
.
KKRESD    KEYIN     *P29:5,*EL,*DV,UNDLN3,*P29:5,*V2LON,NBRESD:
                    *P29:5,*DV,NBRESD
.
          ENDSET    NBRESD
          APPEND    SP5,NBRESD
          RESET     NBRESD
.
          MATCH     SP3,NBRESD
          GOTO      KINDC IF EQUAL
.
          CMATCH    QUEST,NBRESD
          GOTO      KRESD1 IF NOT EQUAL
.
          MOVE      SP3,NBRESD
          MOVE      CODET,CODE
          CALL      PATCODDS
          CALL      DISPSC
          CALL      DISPDA
          GOTO      KKRESD
.
KRESD1    PACK      AUCONAFT WITH NBRESD,SP30,SP30
.
          PACK      KEY5 WITH CODET,NBRESD
          CALL      RDCODE1
          BRANCH    OVRCD OF ENDN2
.
          MOVE      TDESC,RESDDESC
          DISPLAY   *P40:5,*V2LON,RESDDESC
.
          PACK      AUITEM WITH NBRESD,SP20
          GOTO      KCOMP
.
ENDN2     DISPLAY   *P40:5,*EL,*B,*V2LON:
                    "** Resident Status Does Not Exist **",*W
          GOTO      KKRESD
.
.
.       Input compensation code
.
KCOMP     MOVE      SP3,NBCOMP
          MOVE      SP30,COMPDESC
.
          KEYIN     *P29:6,*EL,*DV,UNDLN3,*P29:6,*V2LON,NBCOMP:
                    *P29:6,*DV,NBCOMP
.
          ENDSET    NBCOMP
          APPEND    SP5,NBCOMP
          RESET     NBCOMP
.
          MATCH     SP3,NBCOMP
          GOTO      KKRESD IF EQUAL
.
          CMATCH    QUEST,NBCOMP
          GOTO      KCOMP1 IF NOT EQUAL
.
          MOVE      SP3,NBCOMP
          MOVE      CODECL,CODE
          CALL      PATCODDS
          CALL      DISPSC
          CALL      DISPDA
          GOTO      KCOMP
.
KCOMP1    PACK      KEY5 WITH CODECL,NBCOMP
          CALL      RDCODE1
          BRANCH    OVRCD OF ENDN3
.
          MOVE      TDESC,COMPDESC
          DISPLAY   *P40:6,*V2LON,COMPDESC
          GOTO      KVIST
.
ENDN3     DISPLAY   *P40:6,*EL,*B,*V2LON:
                    "** Compensation Does Not Exist **",*W
          GOTO      KCOMP
.
.
.       Input visit type
.
KVIST     MOVE      SP3,NBINDV
          MOVE      SP30,VISTDESC
.
          KEYIN     *P29:7,*EL,*DV,UNDLN3,*P29:7,*V2LON,NBINDV:
                    *P29:7,*DV,NBINDV
.
          ENDSET    NBINDV
          APPEND    SP5,NBINDV
          RESET     NBINDV
.
          MATCH     SP3,NBINDV
          GOTO      KCOMP IF EQUAL
.
          CMATCH    QUEST,NBINDV
          GOTO      KVIST1 IF NOT EQUAL
.
          MOVE      SP3,NBINDV
          MOVE      CODECV,CODE
          CALL      PATCODDS
          CALL      DISPSC
          CALL      DISPDA
          GOTO      KVIST
.
KVIST1    PACK      KEY5 WITH CODECV,NBINDV
          CALL      RDCODE1
          BRANCH    OVRCD OF ENDN4
.
          MOVE      TDESC,COMPDESC
          DISPLAY   *P40:7,*V2LON,COMPDESC
          GOTO      CHKREC
.
ENDN4     DISPLAY   *P40:7,*EL,*B,*V2LON:
                    "** Visit Type Not Exist **",*W
          GOTO      KVIST
.
.
.         Check if record exist
.
CHKREC    PACK      AUITEM WITH NBRESD,NBCOMP,NBINDC,SP20
.
          MOVE      TWO,NBSYST      * System - Outpatients
          PACK      KEY13 WITH NBSYST,NBINDC,NBRESD,NBCOMP,NBINDV
          CALL      RDNIPB1
          BRANCH    OVRCD OF ENDNO
.
          CMATCH    ANSC,MODE
          GOTO      RDCHR IF EQUAL
.
          CMATCH    ANSD,MODE
          GOTO      DELCS IF EQUAL
.
          DISPLAY   *P41:8,*B,*V2LON:
                    "** Charge Code Already Exists **",*W2,*P41:7,*EL
          MOVE      SP3,NBITEM
          MOVE      ZERO,NBAMNT
          MOVE      SP30,NBDESC
          GOTO      KVIST
.
ENDNO     CMATCH    ANSI,MODE
          GOTO      KITEM IF EQUAL
.
          DISPLAY   *P41:8,*B,*V2LON:
                    "** Charge Code Does Not Exist **",*W2,*P41:7,*EL
          GOTO      KVIST
.
RDCHR     CALL      DISPDA
          GOTO      SFIELD
.
.       Input "Item code"
.
KITEM     PACK      AUCONBEF WITH NBITEM,SP30,SP30
          MOVE      "Item Code          ",AUDESC
.
KITEM0    MOVE      SP3,NBITEM
          KEYIN     *P29:9,*DV,UNDLN3,*P29:9,*V2LON,NBITEM,*P29:9,*DV,NBITEM
.
          ENDSET    NBITEM
          APPEND    SP5,NBITEM
          RESET     NBITEM
.
          MATCH     SP3,NBITEM
          GOTO      INVITEM IF EQUAL
.
          CMATCH    QUEST,NBITEM
          GOTO      KITEM1 IF NOT EQUAL
.
          MOVE      SP3,NBITEM
          MOVE      CODEFI,CODE
          CALL      PATCODDS
          CALL      DISPSC
          CALL      DISPDA
          GOTO      KITEM0
.
KITEM1    PACK      KEY5 WITH CODEFI,NBITEM
          CALL      RDCODE1
          BRANCH    OVRCD OF INVITEM
.
          MOVE      TDESC,ITEMDESC
          DISPLAY   *P40:9,*V2LON,ITEMDESC
          PACK      AUCONAFT WITH NBITEM,SP30,SP20
.
          CMATCH    ANSC,AUMODE
          CALL      WRITAUDX IF EQUAL
.
          MOVE      ONE,CHG
          CMATCH    ANSC,MODE
          GOTO      SFIELD IF EQUAL
          GOTO      KDESC
.
INVITEM   DISPLAY   *P40:9,*EL,*B,*V2LON:
                    "** Code Does Not Exist **",*W2
          GOTO      KITEM0
.
.       Input "Description   "
.
KDESC     PACK      AUCONBEF WITH NBDESC,SP30,SP30
          MOVE      "Description        ",AUDESC
.
          MOVE      SP30,NBDESC
          KEYIN     *P29:10,*DV,UNDLN20,*DV,UNDLN10:
                    *P29:10,*V2LON,NBDESC,*P29:10,*DV,NBDESC
.
          PACK      AUCONAFT WITH NBDESC,SP30,SP20
.
          CMATCH    ANSC,AUMODE
          CALL      WRITAUDX IF EQUAL
.
          MOVE      ONE,CHG
          CMATCH    ANSC,MODE
          GOTO      SFIELD IF EQUAL
.
.       Input "charged amount"
.
KAMNT     PACK      AUCONBEF WITH NBAMNT,SP30,SP30
          MOVE      "Charged amount     ",AUDESC
.
          MOVE      ZERO,NBAMNT
          KEYIN     *P29:11,*DV,UNDLN9:
                    *P29:11,*V2LON,NBAMNT,*P29:11,*DV,NBAMNT
.
          PACK      AUCONAFT WITH NBAMNT,SP30,SP20
.
          CMATCH    ANSC,AUMODE
          CALL      WRITAUDX IF EQUAL
.
          MOVE      ONE,CHG
          CMATCH    ANSC,MODE
          GOTO      SFIELD IF EQUAL
.
.       Select the field to update
.
SFIELD    KEYIN     *P1:24,*EL,"Select field (0 to post) ? ":
                    *V2LON,FIELD;
          COMPARE   ZERO,FIELD
          GOTO      POST IF EQUAL
          MOVE      ANSC,MODE
.
SBRCH     BRANCH    FIELD OF KITEM,KDESC,KAMNT
.
INVFLD    DISPLAY   *P40:24,*EL,*B,*V2LON,"** Invalid Field **",*W2;
          GOTO      SFIELD
.
POST      KEYIN     *P1:24,*EL,"Ok to post (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH,*V2LON,*DV,ANSN,*HOFF:
                    *DV,SLASH,*V2LON,*DV,ANSC,*HOFF:
                    ") ? ",*V2LON,ANS;
.
          CMATCH    ANSN,ANS
          GOTO      SFIELD IF EQUAL
.
          CMATCH    ANSY,ANS
          GOTO      POSTCNT IF EQUAL
.
          CMATCH    ANSC,ANS
          GOTO      WRONG IF NOT EQUAL
.
          BRANCH    HOAUDC OF REPEAT
          CALL      CANCAUDX
          GOTO      REPEAT
.
WRONG     BEEP
          GOTO      POST
.
.       Check CHG indicator to see if anything was changed
.
POSTCNT   CMATCH    ANSC,MODE1
          GOTO      WARWR IF NOT EQUAL
.
          COMPARE   ZERO,CHG
          GOTO      REPEAT IF EQUAL
.
          CALL      UPNIPB1
          GOTO      ENDPST
.
WARWR     MOVE      TWO,NBSYST      * System - Outpatients
          PACK      KEY13 WITH NBSYST,NBINDC,NBRESD,NBCOMP,NBINDV
          CALL      WRNIPB1
          BRANCH    OVRCD OF TOOLATE
.
          BRANCH    HOAUDC OF ENDPST
.
          PACK      AUCONBEF WITH SP30,SP30
          MOVE      "Resd/comp/indc            ",AUDESC
.
          PACK      AUCONAFT WITH KEY13,SP30,SP20
          CALL      WRITAUDX
.
ENDPST    CMATCH    SP1,AUMODE
          CALL      POSTAUDX IF NOT EQUAL
.
          DISPLAY   *P40:24,*EL,*V2LON:
                    "** Data Successfully Posted **",*W2;
          GOTO      REPEAT
.
TOOLATE   DISPLAY   *P40:24,*EL,*B,*V2LON:
                    "** Charge Already Exists For This Combination **",*W2;
          GOTO      REPEAT
.
.        Menus for each mode
.
DISPI     DISPLAY   *P49:2,*V2LON,"- Insert Mode "
          RETURN
.
DISPC     DISPLAY   *P49:2,*V2LON,"- Change Mode "
          RETURN
.
DISPD     DISPLAY   *P49:2,*V2LON,"- Delete Mode "
          RETURN
.
.        Main menu
.
DISPSC    DISPLAY   *P1:4,*EF:
                    *P1:4,"    Clinic Type           :":
                    *P1:5,"    Resident status       :":
                    *P1:6,"    Compensation code     :":
                    *P1:7,"    Visit Type            :":
                    *P1:9," 1. Item                  :":
                    *P1:10," 2. Description           :":
                    *P1:11," 3. Charged amount      $ :"
          RETURN
.
.        Subroutine to display data onto the screen
.
DISPDA    MOVE      SP30,INDCDESC
          MATCH     SP3,NBINDC
          GOTO      DISPDA1 IF EQUAL
          PACK      KEY5 WITH CODECI,NBINDC
          CALL      RDCODE1
          BRANCH    OVRCD OF DISPDA1
          MOVE      TDESC,INDCDESC
.
DISPDA1   MOVE      SP30,RESDDESC
          MATCH     SP3,NBRESD
          GOTO      DISPDA2 IF EQUAL
          PACK      KEY5 WITH CODET,NBRESD
          CALL      RDCODE1
          BRANCH    OVRCD OF DISPDA2
          MOVE      TDESC,RESDDESC
.
DISPDA2   MOVE      SP30,COMPDESC
          MATCH     SP3,NBCOMP
          GOTO      DISPDA3 IF EQUAL
          PACK      KEY5 WITH CODECL,NBCOMP
          CALL      RDCODE1
          BRANCH    OVRCD OF DISPDA3
          MOVE      TDESC,COMPDESC
.
DISPDA3   MOVE      SP30,VISTDESC
          MATCH     SP3,NBINDV
          GOTO      DISPDA4 IF EQUAL
          PACK      KEY5 WITH CODECV,NBINDV
          CALL      RDCODE1
          BRANCH    OVRCD OF DISPDA4
          MOVE      TDESC,VISTDESC
.
DISPDA4   MOVE      SP30,ITEMDESC
          MATCH     SP3,NBITEM
          GOTO      DISPDA5 IF EQUAL
          PACK      KEY5 WITH CODEFI,NBITEM
          CALL      RDCODE1
          BRANCH    OVRCD OF DISPDA5
          MOVE      TDESC,ITEMDESC
.
DISPDA5   DISPLAY   *P29:4,*V2LON,NBINDC,*P40:4,INDCDESC:
                    *P29:5,NBRESD,*P40:5,RESDDESC:
                    *P29:6,NBCOMP,*P40:6,COMPDESC:
                    *P29:7,NBINDV,*P40:7,VISTDESC:
                    *P29:9,NBITEM,*P40:9,ITEMDESC:
                    *P29:10,NBDESC:
                    *P29:11,NBAMNT
          RETURN
.
          INC       PATCODKY
          INC       PATCODIO/INC
          INC       PATNIPIO/INC
.
          INC       OUTAUXIO/INC
.
          INCLUDE   STD001IO
.
ENDIT     BRANCH    HOAUDC OF ABORT
          CALL      STOPAUDX
.
ABORT     CHAIN     PGM
          STOP
.
