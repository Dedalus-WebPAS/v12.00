.******************************************************************************
.* System         : Waiting Lists                                             *
.* Program        : IBAWAT62.PBL                                              *
.* Name           : VIC HDP Waiting List Extraction                           *
.******************************************************************************
.* Date           : 07/08/1997                                                *
.* Author         : Greg Horvat                                               *
.* Function       : Extract waiting lists details via a date range & load the *
.*                  details into a text file.                                 *
.* Modifications  :                                                           *
.*                                                                            *
.*        V10.05.01 01/08/2015  Ania P                      CAR 261630        *
.*                  Removed use of PORT for temp file naming                  *
.*        V10.03.01 18/11/2011  Mike Laming     CAR 240184                    *
.*                  Changes to IBAPOSTF Post Code table - added State to Keys *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.04.01  01/03/2005  Peter Vela    CAR 57547                       *
.*                  Recompiled for PATVADFD and PATVADIO                      *
.*        V9.03.08  12/07/2004  Mike Laming   CAR 51411                       *
.*                  Correct Location & PostCode for OS countries              *
.*        V9.03.07  03/06/2004  Jill Habasque CAR 49017                       *
.*                  2004 Changes - New Intersex added, OS addresses           *
.*        V9.03.06  26/03/2004  Guomin Fu     CAR 44438                       *
.*                  Changed to read Scheduled Admission Date from WMDATE3     *
.*                  for removal reason "S" in GWLD0000 and make sure Removal  *
.*                  Date equals to Scheduled Admission date for removal reason*
.*                  W and S in WRIT0000                                       *
.*        V9.03.05  10/10/2003  Mike Laming   CAR 44238                       *
.*                  Correct Error 375 display at VALD1000                     *
.*        V9.03.04  24/06/2003  Jill Habasque SRF 40381                       *
.*                  Changed to allow wtsufdat equal to end date               *
.*        V9.03.03  30/05/2003  Jill Habasque SRF 37933                       *
.*                  Added error S303 and changed valid procedures over 500    *
.*        V9.03.02  19/05/2003 Jill Habasque SRF 37933                        *
.*                  HDP 2003/2004 Changes                                     *
.*        V9.03.01  13/05/2003 Jill Habasque SRF 38982                        *
.*                  Mods to exclude patients without any unit                 *
.*        V9.02.33  08/04/2003 Lina Vo        CAR 38048                       *
.*                  Initialise PRPRPROC to 0 before use in GWLD0000           *
.*        V9.02.32  23/01/2003 Traceyn Nguyen SRF 23896                       *
.*                  Added error codes to error messages                       *
.*        V9.02.31  16/01/2003 Jill Habasque  SRF 34856 Rebound               *
.*                  Added checks for T as removal reason                      *
.*        V9.02.30  24/12/2002 Jill Habasque  SRF 34856                       *
.*                  Added checks for N as removal reason (transferred to      *
.*                  non-ESIS)                                                 *
.*                  Added check for ADATE when WL status is admitted, if this *
.*                  is before the start of the extract, exclude the record    *
.*        V9.02.29  13/11/2002 Jill Habasque SRF 23744                        *
.*                  Added move of tcindc3 to clurct in GTPC0000 for the 1st   *
.*                  record                                                    *
.*        V9.02.28  31/10/2002 Jill Habasque                                  *
.*                  Added save of the latest listing status date              *
.*        V9.02.27  15/10/2002 Jill Habasque SRF 23063                        *
.*                  Added clear of trndest if not removed                     *
.*        V9.02.26  11/10/2002 Jill Habasque SRF 23138                        *
.*                  Added move of spaces to LOCALITY if postcode is 1000      *
.*                  SRF 23095 Added check for procedure date equal to removal *
.*        V9.02.25  30/09/2002 Jill Habasque SRF 22563                        *
.*                  Fixed error for category 1 >30 days                       *
.*        V9.02.24  16/09/2002 Jill Habasque SRF 22251                        *
.*                  Fixed calculation of NRFC after urgency change or increase*
.*                  if the to date is blank                                   *
.*        V9.02.23  10/09/2002 Jill Habasque  SRF 22030                       *
.*                  Fixed calculation of NRFC after urgency change or increase*
.*        V9.02.22  22/08/2002 Jill Habasque  SRF 21354                       *
.*                  Added move of spaces to LOCALITY if postcode is 9988      *
.*        V9.02.21  16/08/2002 Jill Habasque  SRF 21043                       *
.*                  2002/2003 Mods                                            *
.*        V9.02.20  12/08/2002 Jill Habasque  SRF 20176                       *
.*                  Changed GBKD0000 to compare bkrebkdt to enddate instead   *
.*                  of bkreadat and to pack bokdte with current booking date  *
.*                  (in case backdated bookings have been entered after the   *
.*                  correct booking)                                          *
.*                  SRF 20789 Added check of wtcaedat to 20010701 in GTPC0000 *
.*                  SRF 20780 Fixed category changes if equal to DOL          *
.*        V9.02.19  01/08/2002 Ebon Clements  SRF 20485                       *
.*                  Changed DNRC0000 to add one day to the NRFC days if the   *
.*                  patient is still suspended at the report end date         *
.*        V9.02.18  30/07/2002 Ebon Clements  SRF 19538                       *
.*                  Changed MVEXT000 to use ibawat62.us2 to move the header   *
.*                  file record to the extract directory.                     *
.*        V9.02.17  08/07/2002 Jill Habasque  SRF 19546                       *
.*                  Changed to use CFILENO instead of PTCNHCOD                *
.*        V9.02.16  13/06/2002 Jill Habasque  WL10123                         *
.*                  Fixed clinical urgency                                    *
.*        V9.02.15  13/06/2002 Jill Habasque                                  *
.*                  Fixed tempfile length                                     *
.*        V9.02.14  12/06/2002 Jill Habasque SRF 18403                        *
.*                  Fixed error for Category 1 and 2 patients                 *
.*        V9.02.13  11/06/2002 Jill Habasque SRF 18403                        *
.*                  Added error for Category 1 and 2 patients                 *
.*        V9.02.12  28/05/2002 Jill Habasque SRF WL 10106                     *
.*                  Fixed booking details up to end date                      *
.*        V9.02.11  21/05/2002 Jill Habasque SRF WL 10106                     *
.*                  Fixed booking count, status reassignment                  *
.*        V9.02.10  14/05/2002 Jill Habasque                                  *
.*                  Added WTCNUSXB                                            *
.*        V9.02.09  13/05/2002 Jill Habasque                                  *
.*                  Fixed errors to only appear if within extract range       *
.*        V9.02.08  18/04/2002 Jill Habasque  3584                            *
.*                  Added date on list to error report                        *
.*        V9.02.07  19/03/2002 Jill Habasque                                  *
.*                  Mods for PTLTST                                           *
.*        V9.02.06  06/03/2002 Jill Habasque                                  *
.*                  Mods to use bokrc1af for cancellations instead of oprdetaf*
.*        V9.02.05  10/01/2001 Jill Habasque                                  *
.*                  Added error for WMSTAT=9                                  *
.*        V9.02.04  20/12/2001 Jill Habasque                                  *
.*                  Added back in changes for V9.02.02                        *
.*        V9.02.03  07/12/2001  Greg Horvat                                   *
.*                  Changes to make this program as similar as possible to    *
.*                  release 5.09 version of this program                      *
.*        V9.02.02  16/11/2001 Dean Cramer                                    *
.*                  Fixed calculation of not ready for care                   *
.*        V9.02.01  10/11/2001 Dean Cramer                                    *
.*                  Fix where PRPRPROC not right justified before rep " 0".   *
.*        V9.00.01  06/08/2001 Dean Cramer                                    *
.*                  Create new temp file (TEMP4) for post 30/06/2001 records. *
.*                  Include ESIS seal date file WATESTFD for run range        *
.*                  verification.                                             * 
.*                  Include the treatment extension file WATTX1FD.            *
.*                  Extract patient status from WATSUSFD instead of WATCATFD  *
.*                  using field from WATTX1FD rather than WATTR1FD.           *
.*                  Add patient lsiting status reassignment date (PTLTSTDT).  *
.*        V9.00.B02 27/07/2001 Bronko                                         *
.*                  Defect Fixing                                             *
.*        V9.00.B01 21/06/2001  Tonii                                         *
.*                  2001/2002 Mods                                            *
.*                  - Newfile date to be after 1/7/2001                       *
.*                  - Added 2 new fields to Calculate Total Waiting time      *
.*                     - Date of last Clinical Urgency Increase               *
.*                     - Total NRFC Days following Last Clinical Urg. Increase*
.*                  - Merged Reason for Cancellation with Reason for Removal  *
.*                                                                            *
.******************************************************************************
.*        V5.08.09  08/12/2000  Jill Habasque SRF 7726                        *
.*                  Added default of P to REAREBOK                            *
.*        V5.08.08  30/11/2000  Jill Habasque                                 *
.*                  Stopped check of OPDADATE,ENDDTE8 in GBKD2000             *
.*        V5.08.07  23/11/2000  Jill Habasque SRF 6775                        *
.*                  Added new validation for WGH                              *
.*        V5.08.06  01/09/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.*        V5.08.05  31/08/2000  J Habasque                                    *
.*                  Fixed booking date                                        *
.*        V5.08.04  28/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.03  03/08/2000  J Habasque                                    *
.*                  Fixed newfile date to be after 1/7/2000                   *
.*        V5.08.02  18/05/2000  J Habasque                                    *
.*                  2000/2001 Mods                                            *
.*        V5.08.01  11/05/2000  J Habasque                                    *
.*                  Changed to only count a booking if an oprdetaf record     *
.*        V5.08.B01 19/01/2000  J Habasque                                    *
.*                  Removed packing CCC with CCENT                            *
.******************************************************************************
.*        V5.07.12  08/12/1999  J Habasque                                    *
.*                  Changed header record to be comma delimited               *
.*        V5.07.11  01/12/1999  J Habasque                                    *
.*                  Mods to last NRFC since urgency reassignment date         *
.*        V5.07.10  25/11/1999  J Habasque  SRF 635954                        *
.*                  Changed text file to be comma delimited                   *
.*        V5.07.09  04/11/1999  J Habasque  SRF 635683                        *
.*                  Changed NRFC to be determined by TCINDC4 (not 3)          *
.*        V5.07.08  26/10/1999  J Habasque                                    *
.*                  Added routine to get postcode from ibapostf               *
.*        V5.07.07  29/09/1999  J Habasque SRF 634952                         *
.*                  Added default to one of source of referral                *
.*        V5.07.06  28/09/1999  J Habasque                                    *
.*                  Fixed booking date                                        *
.*        V5.07.05  21/09/1999  J Habasque                                    *
.*                  Changed source of referral to use PTCNCSRL                *
.*        V5.07.04  16/09/1999  J Habasque                                    *
.*                  Rebounds                                                  *
.*        V5.07.03  09/08/1999  J.Tan                                         *
.*                  Mods for 1999                                             *
.*        V5.07.02  11/06/1999  Davin  SRF 632270                             *
.*                  Ignore procedure codes with HDP Eqiv. of "200"            *
.*        V5.07.01  07/05/1999  Steve Armstrong                               *
.*                  Fixed unpacking of ADATE to include century               *
.******************************************************************************
.*        V5.06.05  20/08/1998  Greg Horvat                                   *
.*                  Changed to get the correct default priority               *
.*        V5.06.04  03/08/1998  Greg Horvat                                   *
.*                  Changed to work out the W/L count differently             *
.*        V5.06.03  20/07/1998  Greg Horvat                                   *
.*                  - Changed to not send commas in the locality              *
.*                  - Changed to clear the removal reason for each patient    *
.*                  - Changed heaps of other stuff which I cant remember      *
.*        V5.06.02  10/06/1998  Greg Horvat                                   *
.*                  - Changed to extract all W/L patients regardless of status*
.*                    expect for discharged & removed patients who weren't in *
.*                    hospital during the date range                          *
.*                  - Changed to default the clinical urgency category last   *
.*                    code from the patients 1st record                       *
.*                  - Changed to send the removal date in the correct format  *
.*                  - Changed to set the registration date when the ready for *
.*                    care status changes                                     *
.*                  - Changed to ignore the 1st TP change for a patient       *
.*        V5.06.01  28/04/1998  Greg Horvat                                   *
.*                  - Changed read the admin file with the correct key        *
.*                  - Changed to not extract patients details if theyre before*
.*                    the on list date                                        *
.*                                                                            *
.******************************************************************************
+
          INC       STD001FD
.
          INC       BOKRC1FD/INC            * Booking file
          INC       IBAPOSFD/INC            * Postcode file
          INC       PATCALAG/INC            * calcage   
          INC       PATCODFD/INC            * Codes file
          INC       PATCONFD/INC            * Patient control file
          INC       PATDSCFD/INC            * Discharge file
          INC       PATMA1FD/INC            * Master file
          INC       PATMI1FD/INC            * Admission file
          INC       PMSVX1FD/INC            * Admission file
          INC       PATMMBFD/INC            * MBS file
          INC       PATVADFD/INC            * Transfer source file
          INC       WATCATFD/INC            * Code changes file
          INC       WATCONFD/INC
          INC       WATESIFD/INC            * ESIS History
          INC       WATESTFD/INC            * ESIS Seal Table     
          INC       WATPROFD/INC            * Procedure file
          INC       WATRTDFD/INC            * Referral Destination file
          INC       WATSUSFD/INC            * Procedure Suspension file
          INC       WATTR1FD/INC            * Treatment file
          INC       WATTX1FD/INC            * Treatment Extention file
          INC       IBASEQFD/INC
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
.
. ----- Tempfile variables -----
.
TEMP1A    IFILE     SQL, FIXED=204, KEY="U1-8,9-17,18-19"
TEMP1B    IFILE     SQL, FIXED=204, KEY="U1-8,20-27,9-17,18-19"
FILELIN1  INIT      " 204 U1-8,9-17,18-19 U1-8,20-27,9-17,18-19"
.
.Name     Type    Length  Physical  Start  Description
.-------  ----    ------  --------  -----  -----------
TMPURNO   DIM       8         8       1    U/R no
TMPROCOD  DIM       9         9       9    Procedure code
DTMPROCN  DIM       2         2       18   Procedure count
TMPDATE1  DIM       8         8       20   Procedure on list date (ccyymmdd)
WATNO     FORM      2         2       28   W/L counter
DOB       DIM       8         8       30   DOB (ddmmccyy)
SEX       DIM       1         1       38   Sex
POSTCOD   DIM       4         4       39   Postcode
LOCALITY  DIM       30       30       43   Locality
PRPRPROC  DIM       3         3       73   Procedure code indicator
SURGSPEC  DIM       2         2       76   Surgical speciality
CLURCT    DIM       1         1       78   Clinical urgency category
INTLOS    DIM       1         1       79   Intended LOS
DTOFREG   DIM       8         8       80   Date of registration (ddmmccyy)
CLURCTDT  DIM       8         8       88   Cli urgency cat last date (ddmmccyy)
DTLURGIN  DIM       8         8       96   Date of Last Clin. Urgency Increase
CLURCTCD  DIM       1         1      104   Clinical urgency category last code
PTLTST    DIM       1         1      105   Patient listing status
PTLTSTDT  DIM       8         8      106   Pat list status last date (ddmmccyy)
PTLTSTCD  DIM       1         1      114   Patient listing status last code
BOKDTE    DIM       8         8      115   Booking date (ddmmccyy)
BOKNO     FORM      2         2      123   Booking U/R no count
REAREBOK  DIM       1         1      125   Reason for rebooking
DTSCDT    DIM       8         8      126   Date scheduled for admin (ddmmccyy)
DTOFREM   DIM       8         8      134   Date of removal (ddmmccyy)
REFRREM   DIM       1         1      142   Reason for removal
INSDEC    DIM       1         1      143   Insurance declaration
REFRCAN   DIM       1         1      144   Reason for cancellation
MEDCARE   DIM       11       11      145   Medicare number
MEDSUFX   DIM       3         3      156   Medicare suffix
SRCREF    DIM       1         1      159   Source referral
REFHOS    DIM       4         4      160   Referring hospital
NRFCSTAT  DIM       1         1      164   Reason for NRFC Status
TRNDEST   DIM       4         4      165   Transfer Destination
DYNRFCRE  DIM       4         4      169   Total not ready for care days
LSNRFCRE  DIM       4         4      173   Total not ready for care days since
.                                          Last Urgency reassignment
TNRFCLCU  DIM       4         4      177   Total not ready for care days since
.                                          Last Urgency increase
UNIQKEY   DIM       8         8      181   Unique key
INVPCOD   DIM       1         1      189   0=Valid postcode 1=invalid 
HOSPOST   FORM      2         2      190
DOFPROC   DIM       8         8      192   Date of Procedure
TCAMPUS   DIM       4         4      200   Intened/Actual Treatment Campus
.End of Record                       204
.
.Extract file layout
.-------------------
.
HTEMP     FILE      ASCII, FIXED=21
.Header Record
.
.Name     Type    Length    Start  Description
.-------  ----    ------    -----  -----------
EXTDTE    DIM       8         1    Extract date (ddmmccyy)
.COMMA    DIM       1         9    
HOSPCOD   DIM       4         10   Hospital code
.COMMA    DIM       1         14   
DRECCNT   DIM       6         15   Record no
.End of Record                21
.
.
TEMP4     FILE      ASCII, FIXED=205    * after 1/7/2001
.
.Name     Type    Length    Start  Description
.-------  ----    ------    -----  -----------
.UNIQKEY  DIM       8         1    Unique key
.COMMA    DIM       1         9
EURNO     DIM       10        10   U/R no
.COMMA    DIM       1         20
.MEDCARE  DIM       11        21   Medicare number
.COMMA    DIM       1         32
.MEDSUFX  DIM       3         33   Medicare suffix
.COMMA    DIM       1         36
DWATNO    DIM       2         37   W/L counter
.COMMA    DIM       1         39
.DOB      DIM       8         40   DOB (ddmmccyy)
.COMMA    DIM       1         48
.SEX      DIM       1         49   Sex
.COMMA    DIM       1         50
.POSTCOD  DIM       4         51   Postcode
.COMMA    DIM       1         55
.LOCALITY DIM       30        56   Locality
.COMMA    DIM       1         86
.PRPRPROC DIM       3         87   Procedure code indicator
.COMMA    DIM       1         90
.SURGSPEC DIM       2         91   Surgical speciality
.COMMA    DIM       1         93
.CLURCT   DIM       1         94   Clinical urgency category
.COMMA    DIM       1         95
.INTLOS   DIM       1         96   Intended LOS
.COMMA    DIM       1         97
.DTOFREG  DIM       8         99   Date of registration (ddmmccyy)
.COMMA    DIM       1        106
.SRCREF   DIM       1        107   Source referral
.COMMA    DIM       1        108
.REFHOS   DIM       4        109   Referring hospital
.COMMA    DIM       1        113
.CLURCTDT DIM       8        114   Clinical urgency cat last date (ddmmccyy)
.COMMA    DIM       1        122
.DTLURGIN DIM       8        123   Date of Last Clinical Urgency Increase
.COMMA    DIM       1        131
.CLURCTCD DIM       1        132   Clinical urgency category last code
.COMMA    DIM       1        133
.PTLTST   DIM       1        134   Patient listing status
.COMMA    DIM       1        135
.PTLTSTDT DIM       8        136   Patient listing status last date (ddmmccyy)
.COMMA    DIM       1        144
.NRFCSTAT DIM       1        145   Reason for NRFC Status
.COMMA    DIM       1        146
.BOKDTE   DIM       8        147   Booking date (ddmmccyy)
.COMMA    DIM       1        155
DBOKNO    DIM       2        156   Booking U/R no count
.COMMA    DIM       1        158
.REAREBOK DIM       1        159   Reason for rebooking
.COMMA    DIM       1        160
.DTSCDT   DIM       8        161   Date scheduled for admin (ddmmccyy)
.COMMA    DIM       1        169
.DTOFREM  DIM       8        170   Date of removal (ddmmccyy)
.COMMA    DIM       1        178
.REFRREM  DIM       1        179   Reason for removal
.COMMA    DIM       1        180
.INSDEC   DIM       1        181   Insurance declaration
.COMMA    DIM       1        182
.TRNDEST  DIM       4        183   Transfer Destination
.COMMA    DIM       1        187
.DYNRFCRE DIM       4        188   Total not ready for care days
.COMMA    DIM       1        192
.LSNRFCRE DIM       4        193   Total not ready for care days since Last
.                                  Urgency reassignment
.COMMA    DIM       1        197
.TNRFCLCU DIM       4        198   Total NRFC days following Last Clinical
.                                  Urgency Increase
.COMMA    DIM       1        202
DHOSPOST  DIM       2        203   Hospital initiated postponement
.
.End of Record               205
.
.
TEMP5     FILE      ASCII, FIXED=218    * after 1/7/2002
.
.Name     Type    Length    Start  Description
.-------  ----    ------    -----  -----------
.UNIQKEY  DIM       8         1    Unique key
.COMMA    DIM       1         9
.EURNO    DIM       10        10   U/R no
.COMMA    DIM       1         20
.MEDCARE  DIM       11        21   Medicare number
.COMMA    DIM       1         32
.MEDSUFX  DIM       3         33   Medicare suffix
.COMMA    DIM       1         36
.DWATNO   DIM       2         37   W/L counter
.COMMA    DIM       1         39
.DOB      DIM       8         40   DOB (ddmmccyy)
.COMMA    DIM       1         48
.SEX      DIM       1         49   Sex
.COMMA    DIM       1         50
.POSTCOD  DIM       4         51   Postcode
.COMMA    DIM       1         55
.LOCALITY DIM       30        56   Locality
.COMMA    DIM       1         86
.PRPRPROC DIM       3         87   Procedure code indicator
.COMMA    DIM       1         90
.SURGSPEC DIM       2         91   Surgical speciality
.COMMA    DIM       1         93
.CLURCT   DIM       1         94   Clinical urgency category
.COMMA    DIM       1         95
.INTLOS   DIM       1         96   Intended LOS
.COMMA    DIM       1         97
.DTOFREG  DIM       8         99   Date of registration (ddmmccyy)
.COMMA    DIM       1        106
.SRCREF   DIM       1        107   Source referral
.COMMA    DIM       1        108
.REFHOS   DIM       4        109   Referring hospital
.COMMA    DIM       1        113
.CLURCTDT DIM       8        114   Clinical urgency cat last date (ddmmccyy)
.COMMA    DIM       1        122
.DTLURGIN DIM       8        123   Date of Last Clinical Urgency Increase
.COMMA    DIM       1        131
.CLURCTCD DIM       1        132   Clinical urgency category last code
.COMMA    DIM       1        133
.PTLTST   DIM       1        134   Patient listing status
.COMMA    DIM       1        135
.PTLTSTDT DIM       8        136   Patient listing status last date (ddmmccyy)
.COMMA    DIM       1        144
.NRFCSTAT DIM       1        145   Reason for NRFC Status
.COMMA    DIM       1        146
.BOKDTE   DIM       8        147   Booking date (ddmmccyy)
.COMMA    DIM       1        155
.DBOKNO   DIM       2        156   Booking U/R no count
.COMMA    DIM       1        158
.REAREBOK DIM       1        159   Reason for rebooking
.COMMA    DIM       1        160
.DTSCDT   DIM       8        161   Date scheduled for admin (ddmmccyy)
.COMMA    DIM       1        169
.DTOFREM  DIM       8        170   Date of removal (ddmmccyy)
.COMMA    DIM       1        178
.REFRREM  DIM       1        179   Reason for removal
.COMMA    DIM       1        180
.INSDEC   DIM       1        181   Insurance declaration
.COMMA    DIM       1        182
.TRNDEST  DIM       4        183   Transfer Destination
.COMMA    DIM       1        187
.DYNRFCRE DIM       4        188   Total not ready for care days
.COMMA    DIM       1        192
.LSNRFCRE DIM       4        193   Total not ready for care days since Last
.                                  Urgency reassignment
.COMMA    DIM       1        197
.TNRFCLCU DIM       4        198   Total NRFC days following Last Clinical
.                                  Urgency Increase
.COMMA    DIM       1        202
.DHOSPOST DIM       2        203   Hospital initiated postponement
.COMMA    DIM       1        205
.DOFPROC  DIM       8        206   * Date of Procedure
.COMMA    DIM       1        214
.TCAMPUS  DIM       4        215   * Intended/Actual Treatment Campus
.
.End of Record               219
.
. ----- Program Variables -----
.
ADDCNT    FORM      5                       * Additions to list count
ADMCNT    FORM      5                       * Admins count
BJDAY     FORM      3     
.
CFILENAM  DIM       8
CFNAMEB   DIM       20
CJDAY     FORM      3      
CMDLINE   DIM       100
CURDTE8   DIM       8                       * Current date (ccyymmdd)
CDYSDAYS  FORM      6                       * For CALCDAYS
CDYSFDTE  DIM       8                       * For CALCDAYS
CDYSTDTE  DIM       8                       * For CALCDAYS
CDYSYEAR  FORM      2                       * For CALCDAYS
CURGIFLG  FORM      1                       * Clinical Urgency increase flag
.
DATE1     DIM       8
DATE2     DIM       8
DATOLIST  DIM       10
DIM4      DIM       4
DIM8      DIM       8
DIM8A     DIM       8
DIM10     DIM       10
DSPCNT    FORM      6                       * Display counter
DNRFCFLG  FORM      1                       * Days not ready for care flag
.
ENDDTE8   DIM       8                       * End date (ccyymmdd)
ERRCNT    FORM      6                       * Error count
ERRMSG    DIM       98                      * Error message
.
FILENAME  DIM       8                       * Extract filename
FORM3     FORM      3
FORM8     FORM      8
.
IBCNMHOS  FORM      1
IGNFLAG   FORM      1                       * Ignore flag
INDEFNR   FORM      1
MBOOKFLG  FORM      1                       * Missing booking record flag
.
NEWFILE   FORM      1
NRDYCARE  FORM      1
NRDYCODE  DIM       3
NRFCDAYS  FORM      4
NRFCLAST  FORM      4
PSAGECON  DIM       3
PSAGETYP  DIM       1
.
LASTURGT  DIM       8
RECCNT    FORM      6                       * Record count
REMCNT    FORM      5                       * Removals count
.
SAVINDC1  DIM       1                       * Save 1st indicator
SAVINDC3  DIM       1                       * Save 3rd indicator
SAVINDC4  DIM       1                       * Save 4th indicator
SAVURNO   DIM       8                       * Save U/R no
SLISTDAT  DIM       8
STRTDTE8  DIM       8                       * Start date (ccyymmdd)
.
TESTDATE  DIM       8
TMPROCNT  FORM      2                       * Procedure count
.
. ----- Program Constants -----
.
CATW1     INIT      "W1"
CATW2     INIT      "W2"
CATW3     INIT      "W3"
CATW4     INIT      "W4"
CATX5     INIT      "X5"
CATX6     INIT      "X6"
CATX7     INIT      "X7"
CATX8     INIT      "X8"
CATS      INIT      "S "
.
PRGID     INIT      "IBAWAT62"
PRGNAM    INIT      "VIC HDP Waiting List Extraction"
VERSION   INIT      "V10.10.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.****************************************************************************** 
.
ML0000    CALL      INIT0000                * Initialise variables & open files 
.
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    OPTION,ML2000
          BRANCH    EXIT,ML9999
.
ML2000    CALL      DATE0000                * Keyin the date range
          BRANCH    EXIT,ML9999
.
ML3000    CALL      FILE0000                * Keyin the extract filename
          BRANCH    EXIT,ML9999
.
          CALL      CONTQST                 * Ok to Continue (Y/N/C) ?
          BRANCH    CEXIT,ML4000,ML3000,ML1000
.
ML4000    CALL      OPEN0000                * Open the tempfile
          CALL      HEAD0000                * Print report header
.
          DISPLAY   *P1:24,*EL,"Scanning : ",*P35:24,"Loading : ";
          MOVE      ZERO,ADDCNT             * Reset the additions to list count
          MOVE      ZERO,ADMCNT             * Reset the admins count
          MOVE      ZERO,ERRCNT             * Reset the error counter
          MOVE      ZERO,REMCNT             * Reset the removals count
.
          CALL      GTRE0000                * Get treatment patients
          CALL      VALD0000                * Validate the extract data
          CALL      WRIT0000                * Write to the extract file
          CALL      TAIL0000                * Print report tail
          CALL      KILL0000                * Delete the tempfile
          CALL      MVEXT000                * Move Extract for Web
          GOTO      ML1000
.
ML9999    CHAIN     PGM
.******************************************************************************
.*                                  INIT0000              Called by: ML0000   *
.*                       Initialise Variable & Open Files                     *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
          CALL      TFILENAM
.
          DISPLAY   *P56:24,"Opening controlf"
          OPEN      CONTROLF,"controlf"
.
          DISPLAY   *P64:24,"bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A6,"bokrc1af"
.
          DISPLAY   *P64:24,"ibapostf";
          OPEN      IBAPOST1,"ibapostf"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdschf";
          OPEN      PATDSCH1,"patdschf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patmi1af";
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
          DISPLAY   *P64:24,"patmmbsf";
          OPEN      PATMMBS1,"patmmbsf"
.
          DISPLAY   *P64:24,"watcataf";
          OPEN      WATCATA1,"watcataf"
          OPEN      WATCATA2,"watcataf"
.
          DISPLAY   *P64:24,"watesiaf";
          OPEN      WATESIA1,"watesiaf"
.
          DISPLAY   *P64:24,"watestaf";
          OPEN      WATESTA1,"watestaf"
.
          DISPLAY   *P64:24,"watproaf";
          OPEN      WATPROA1,"watproaf"
.
          DISPLAY   *P64:24,"watsusaf";
          OPEN      WATSUSA1,"watsusaf"
.
          DISPLAY   *P64:24,"wattr1af";
          OPEN      WATTR1A4,"wattr1af"
.
          DISPLAY   *P64:24,"wattx1af";
          OPEN      WATTX1A1,"wattx1af"
.
          CLOCK     TIME,CTIMEIS
          PACK      CURDTE8,CCC,CYY,CMM,CDD
          REP       " 0",CURDTE8
          READ      CONTROLF,TEN;*54,CFILENO
          READ      CONTROLF,THIRTY7;*242,WTCNUSXB,*243,WTCNRJUR
          READ      CONTROLF,FIFTY9;*220,PTCNUADT
          READ      CONTROLF,NINTY8;*81,PTCNHCOD
.
          IF        PTCNUADT = 1
            OPEN      WATRTDA1,"watrtdaf"
            OPEN      PATVADA1,"patvadaf"
          ENDIF
.
INIT9000  MOVE      ZERO,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Select Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Date Range":
                    *P1:7,"Select Option : ";
OPTN1000  KEYIN     *P17:7,*V2LON,*DV,UNDLN2,*P17:7,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                  DATE0000              Called by: ML0000   *
.*                            Keyin The Date Range                            *
.******************************************************************************
DATE0000  MOVE      "21",CCOL
          MOVE      "9",CVERT
          DISPLAY   *P1:CVERT,"Starting Date     : ",*EF;
          ADD       ONE,CVERT
          DISPLAY   *P1:CVERT,"Ending Date       : ";
.
          MOVE      ZERO,NEWFILE
          MOVE      ONE,CCANLDTE            * Blank cancel default
          MOVE      ZERO,CHIGHLT            * Use highlight
.
. ----- Enter starting date -----
.
DATE1000  SUB       ONE,CVERT
DATE2000  MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY     * No defaults
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE9500          * Nothing entered
          BRANCH    CQUEST,DATE2000
.
          PACK      STRTDTE8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDTE8
.
          MATCH     STRTDTE8,CURDTE8
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"Date must not be in the future.  ";
            CALL      HITENTER
            GOTO      DATE2000
          ENDIF
.
. ----- Enter ending date -----
.
          ADD       ONE,CVERT
DATE3000  UNPACK    STRTDTE8,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE1000          * Nothing entered
          BRANCH    CQUEST,DATE3000
.
          PACK      ENDDTE8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDTE8
.
          MATCH     ENDDTE8,CURDTE8
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"Date must not be in the future.  ";
            CALL      HITENTER
            GOTO      DATE3000
          ENDIF
.
          MATCH     STRTDTE8,ENDDTE8
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must be after start date.  ";
            CALL      HITENTER
            GOTO      DATE3000
          ENDIF
.
          CALL      CHDT0000                * Check the date range
          BRANCH    EXIT,DATE9500
.
          MATCH     "20020701",STRTDTE8
          GOTO      DATE9000 IF LESS
          MOVE      ONE,NEWFILE
.
DATE9000  MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.******************************************************************************
.*                                  CHDT0000              Called by: DATE0000 *
.*                            Check The Date Range                            *
.******************************************************************************
CHDT0000  MOVE      SP8,CKYIDEF8
          PACK      KEY8,SP8
          CALL      RSWTEST1                * Position on & read
CHDT1000  CALL      RDWTEST1                  file record
          BRANCH    OVRCD,CHDT2000
.
          MOVE      WTESSDAT,CKYIDEF8
          GOTO      CHDT1000
.
CHDT2000  MATCH     STRTDTE8,CKYIDEF8
          GOTO      CHDT9500 IF NOT LESS
.
CHDT9000  MOVE      ZERO,EXIT
          GOTO      CHDT9999
.
CHDT9500  MOVE      ONE,EXIT
CHDT9999  RETURN
+
.******************************************************************************
.*                                  FILE0000              Called by: ML0000   *
.*                         Keyin The Extract Filename                         *
.******************************************************************************
FILE0000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,NINTY8;*84,PTCNCSRL
.
          MOVE      ZERO,KEY1
.
FILE1000  UNPACK    ENDDTE8,CCENT,DIM1,DIM1,CMON,CDAY
          MOVE      SP70,DIM4
          MOVE      CFILENO,DIM4
          PACK      FILENAME,ANSD,CMON,DIM1,DIM4 * Detail file
          PACK      CFILENAM,ANSC,CMON,DIM1,DIM4 * Header file
.
          DISPLAY   *P1:12,*EL,"Extract Filename":
                    *PCCOL:12,*EL,*V2LON,*+,FILENAME,".txt";
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          IF        NEWFILE=1
            OPEN      TEMP5,FILENAME
            TRAPCLR   IO
            BRANCH    OVRCD,FILE9000        * File doesnt exist
            CLOSE     TEMP5
          ELSE
            OPEN      TEMP4,FILENAME
            TRAPCLR   IO
            BRANCH    OVRCD,FILE9000        * File doesnt exist
            CLOSE     TEMP4
          ENDIF
.
          PREP      HTEMP,CFILENAM
.
FILE2000  KEYIN     *P1:24,*B,*EL,"An extraction file with this name":
                    " already exists. Overwrite it (",*V2LON,*DV,ANSY:
                    *HOFF,"/",*V2LON,*DV,ANSN,*HOFF,") ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          GOTO      FILE9000 IF EQUAL       * Overwrite file
.
          MATCH     ANSN,ANS
          GOTO      FILE9100 IF EQUAL       * Keyin filename again
.
          BEEP
          GOTO      FILE2000
.
FILE9000  MOVE      ZERO,EXIT
          GOTO      FILE9999
.
FILE9100  MOVE      ONE,EXIT
          GOTO      FILE9999
.
FILE9200  MOVE      TWO,EXIT
FILE9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: ML0000   *
.*                             Print Report Header                 & PRNT0000 *
.******************************************************************************
HEAD0000  CALL      HEAD132                 * Print report header
          PRINT     *N;
          UNPACK    STRTDTE8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *35,"Starting Date : ",CPCDATE
          UNPACK    ENDDTE8,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *35,"Ending Date   : ",CPCDATE
.
          CALL      UND132

          PRINT     *1,"| U/R No |Proc Code|Date On List":
                    "|Error Message",*132,"|"
          CALL      UND132
          ADD       "2",CLNO
.
HEAD9000  MOVE      ZERO,EXIT
HEAD9999  RETURN
+
.******************************************************************************
.*                                  GTRE0000              Called by: ML0000   *
.*                           Get Treatment Patients                           *
.******************************************************************************
GTRE0000  MOVE      ZERO,DSPCNT
          MOVE      SP8,SAVURNO
          PACK      KEY27,SP30
          CALL      RDSTREA4                * Position on & read a treatment
GTRE1000  CALL      RDKTREA4                  file record
          BRANCH    OVRCD,GTRE9000
.
          PACK      KEY19,WMURNO,WMCODE,WMCNT
          CALL      RDWTTX11
          BRANCH    OVRCD,GTRE1000
. 
          ADD       ONE,DSPCNT              * Increment the record counter
          IF        DSPCNT%10=1
            DISPLAY   *P12:24,*V2LON,WMSTAT,SP2,WMURNO;
          ENDIF
.
          CALL      VPTD0000                * Validate patient details
          BRANCH    EXIT,GTRE1000
.
          CALL      IWLC0000                * Increment the W/L counter
          BRANCH    EXIT,GTRE1000
.
          CALL      CLRV0000                * Clear tempfile variables
          CALL      GMFD0000                * Get master file data
          BRANCH    EXIT,GTRE1000
.
          CALL      GPTD0000                * Get patient details
          BRANCH    EXIT,GTRE1000
.
          CALL      GTPC0000                * Get category TP changes
          CALL      DNRC0000                * Calc the total no of NRFC days
          CALL      URNR0000                * Calc days since last urg resassign
          CALL      CINR0000                * Calc days since clinic urg increas
.
          PACK      KEY19,TMPURNO,TMPROCOD,TMPROCNT
          CALL      RATEMP1A                * Position read a tempfile record
          IF        OVRCD=1
            CALL      WRTEMP1A              * Write a tempfile record
          ELSE
            CALL      UPTEMP1A              * Update a tempfile record
          ENDIF
          IF        DSPCNT%10=1
            DISPLAY   *P45:24,*V2LON,TMPURNO;
          ENDIF
          GOTO      GTRE1000
.
GTRE9000  MOVE      ZERO,EXIT
GTRE9999  RETURN
+
.******************************************************************************
.*                                  VPTD0000              Called by: GTRE0000 *
.*                          Validate Patient Details                          *
.******************************************************************************
VPTD0000  MOVE      WMURNO,TMPURNO          * U/R no
          MOVE      WMCODE,TMPROCOD         * Procedure code
          MOVE      WMCNT,TMPROCNT          * Procedure count
          MOVE      WMDATE1,TMPDATE1        * Procedure on list date
          MOVE      ZERO,MBOOKFLG
.
          MATCH     WMDATE1,ENDDTE8
          GOTO      VPTD1000 IF LESS        * End date < on list date
.
          IF        WMSTAT=9
            MOVE      "Patient has cancelled booking status",ERRMSG
            CALL      PRNT0000          * Print error message
            GOTO      VPTD9500
          ENDIF
          MOVE      ZERO,IGNFLAG            * Ignore flag - no
          COMPARE   ZERO,WMBOOK
          IF        !@EQUAL
            PACK      KEY8,WMBOOK
            CALL      RDBKREC1              * Read a booking file record
            IF        OVRCD=1
              IF        WMSTAT=2 | WMSTAT=3 | WMSTAT=4 | WMSTAT=5
                MOVE      "Missing Booking file record",ERRMSG
                CALL      PRNT0000          * Print error message
                GOTO      VPTD9500
              ENDIF
            ENDIF
            COMPARE   WMBOOK,BKREBOOK
            IF        !@EQUAL
              MOVE      ONE,MBOOKFLG
            ENDIF
          ENDIF
.
          IF        WMSTAT=3 | WMSTAT=4 | WMSTAT=5
            MOVE      WMBOOK,AADMNO
            PACK      KEY8,AADMNO
            CALL      RDMISS1               * Read an admin file record
            IF        OVRCD=1
              MOVE      "Missing Admission file record",ERRMSG
              CALL      PRNT0000            * Print error message
              GOTO      VPTD9500
            ENDIF
          ENDIF
.
          IF        WMSTAT=5
            MOVE      WMBOOK,DADMNO
            PACK      KEY8,DADMNO
            CALL      RDDSCH1               * Read a disch file record
            IF        OVRCD=1
              MOVE      "Missing Discharge file record",ERRMSG
              CALL      PRNT0000            * Print error message
              GOTO      VPTD9500
            ENDIF
          ENDIF
.
          IF        WMSTAT=3
            COMPARE   ONE,ASTAT
            IF        !@EQUAL
             MOVE     "Waiting list status not equal to admission status",ERRMSG
             CALL      PRNT0000          * Print error message
             GOTO      VPTD9500
            ENDIF
          ENDIF
.
          IF        WMSTAT=4
            MATCH     STRTDTE8,ADATE
            GOTO      VPTD1000 IF LESS      * Admin date < start date
.
            COMPARE   TWO,ASTAT
            IF        !@EQUAL 
             MOVE     "Waiting list status not equal to admission status",ERRMSG
             CALL      PRNT0000          * Print error message
             GOTO      VPTD9500
            ENDIF
          ENDIF
.
          IF        WMSTAT=5
            MATCH     STRTDTE8,ADATE
            GOTO      VPTD1000 IF LESS      * Admin date < start date
.
            COMPARE   THREE,ASTAT
            IF        !@EQUAL
             MOVE     "Waiting list status not equal to admission status",ERRMSG
             CALL      PRNT0000          * Print error message
             GOTO      VPTD9500
            ENDIF
          ENDIF
.
          IF        WMSTAT=6
            MATCH     STRTDTE8,WMDATE4
            GOTO      VPTD1000 IF LESS      * Removal date < start date
          ENDIF
          GOTO      VPTD9000
.
VPTD1000  MOVE      ONE,IGNFLAG             * Ignore flag - yes
.
VPTD9000  MOVE      ZERO,EXIT
          GOTO      VPTD9999
.
VPTD9500  MOVE      ONE,EXIT
VPTD9999  RETURN
+
.******************************************************************************
.*                                  IWLC0000              Called by: GTRE0000 *
.*                          Increment The W/L Counter                         *
.******************************************************************************
IWLC0000  MATCH     SAVURNO,WMURNO
          IF        !@EQUAL
            MOVE      ZERO,WATNO            * Reset the W/L counter
          ENDIF
.
          MOVE      WMURNO,SAVURNO          * Save the U/R no
          ADD       ONE,WATNO               * Increment the W/L counter
.
          BRANCH    IGNFLAG,IWLC9500        * Ignore this patient
.
IWLC9000  MOVE      ZERO,EXIT
          GOTO      IWLC9999
.
IWLC9500  MOVE      ONE,EXIT
IWLC9999  RETURN
+
.******************************************************************************
.*                                  CLRV0000              Called by: GTRE0000 *
.*                        Clear The Tempfile Variables                        *
.******************************************************************************
CLRV0000  UNPACK    SP70,DOB,SEX,POSTCOD,LOCALITY,PRPRPROC
          UNPACK    SP70,SURGSPEC,CLURCT,INTLOS,DTOFREG,CLURCTDT
          UNPACK    SP70,CLURCTCD,PTLTST,PTLTSTDT,PTLTSTCD,BOKDTE
          UNPACK    SP70,REAREBOK,DTSCDT,DTOFREM,REFRREM,INSDEC
          UNPACK    SP70,REFRCAN,MEDCARE,MEDSUFX,SRCREF,REFHOS
          UNPACK    SP70,NRFCSTAT,TRNDEST,DYNRFCRE,LSNRFCRE,UNIQKEY,DBOKNO
          UNPACK    SP70,DTLURGIN,TNRFCLCU
          MOVE      ZERO,INVPCOD
          MOVE      ZERO,BOKNO              * Reset the Booking U/R no count
          MOVE      ZERO,HOSPOST
          UNPACK    SP70,DOFPROC,TCAMPUS
.
CLRV9000  MOVE      ZERO,EXIT
CLRV9999  RETURN
+
.******************************************************************************
.*                                  GMFD0000              Called by: GTRE0000 *
.*                            Get Master File Data                            *
.******************************************************************************
GMFD0000  PACK      KEY8,WMURNO
          CALL      RDMAST1                 * Read a master file record
          BRANCH    OVRCD,GMFD9500
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          PACK      DOB,CDAY,CMON,CCENT,CYEAR
          REP       " 0",DOB                * DOB
.
          MATCH     SP2,PTMXMCCD
          IF        @EQUAL
            PACK      MEDCARE,PMEDI,ONE
          ELSE
            SQUEEZE   PTMXMCCD
            PACK      MEDCARE,PMEDI,PTMXMCCD,SP20
          ENDIF
          CALL      MSFX0000
. 
          MOVE      PSEX,SEX
          REP       "M1F2I3U3R4",SEX
          TYPE      SEX
          IF        !@EQUAL
            MOVE      "R:S091  Sex Code Invalid",ERRMSG
            CALL      PRNT0000
            GOTO      GMFD1000
          ENDIF
          MOVE      SEX,F1
          IF        F1<1|F1>4
            MOVE      "R:S091  Sex Code Invalid",ERRMSG
            CALL      PRNT0000
          ENDIF
.
GMFD1000  CALL      PTSB0000                * Postcode and Locality
.
GMFD9000  MOVE      ZERO,EXIT
          GOTO      GMFD9999
.
GMFD9500  MOVE      ONE,EXIT
GMFD9999  RETURN
+
.******************************************************************************
.*                                  PTSB0000              Called by: GCEP0000 *
.*                          Get The Postcode & Suburb                         *
.******************************************************************************
PTSB0000  MOVE      ZERO,FORM1
PTSB1000  PACK      KEY45,SP70
          ADD       ONE,FORM1
          LOAD      KEY45,FORM1,PADD2,PSUBRB
          PACK      KEY45,KEY45,SP70
          REP       UPPLOW,KEY45
          PACK      KEY56,PPOST,KEY45,PADD4,SP70                      *C-240184
          CALL      RDIBPOS1                * Read a postcode file record
          COMPARE   ONE,OVRCD
          GOTO      PTSB4000 IF NOT EQUAL   * Valid record, continue
.
          PACK      KEY56,PPOST,SP70                                  *C-240184
          CALL      RSIBPOS1                * Position on & read a postcode
          CALL      RKIBPOS1                  file record
          BRANCH    OVRCD,PTSB6000
.
          MATCH     PPOST,IBPOPCOD
          GOTO      PTSB6000 IF NOT EQUAL   * Invalid postcode, error
.
          MOVE      IBPOSUBR,KEY22          * Save the 1st suburb for postcode
          MATCH     SP70,KEY45
          GOTO      PTSB5000 IF EQUAL       * Blank suburb
          GOTO      PTSB3000
.
PTSB2000  CALL      RKIBPOS1                * Read a postcode file record
          BRANCH    OVRCD,PTSB5000
.
          MATCH     PPOST,IBPOPCOD
          GOTO      PTSB5000 IF NOT EQUAL   * Different postcode
.
PTSB3000  MATCH     KEY45,IBPOSUBR
          GOTO      PTSB2000 IF NOT EQUAL   * Different suburb
.
PTSB4000  MOVE      PPOST,KEY4
          MOVE      KEY4,POSTCOD            * Postcode
          MOVE      IBPOSUBR,LOCALITY       * Suburb
          GOTO      PTSB9000
.
PTSB5000  COMPARE   TWO,FORM1
          GOTO      PTSB1000 IF LESS        * Counter < 2, get next suburb
.
          MOVE      PPOST,KEY4
          MOVE      KEY4,POSTCOD            * Postcode
          MOVE      KEY22,LOCALITY          * Suburb - 1st suburb for postcode
          GOTO      PTSB9000
.
PTSB6000  MOVE      ONE,INVPCOD
.
PTSB9000  MOVE      ZERO,EXIT
          MATCH     POSTCOD,IBPOPCOD
          IF        @EQUAL
            MATCH     "OS",IBPOSTAT
            IF        @EQUAL
              PACK      LOCALITY,IBPOPCOD,SP30
              MOVE      "8888",POSTCOD
            ENDIF
          ENDIF
          MATCH     "OS",PADD4
          IF        @EQUAL
            MOVE      "8888",POSTCOD
          ENDIF
.
PTSB9999  RETURN
+
.******************************************************************************
.*                                  GPTD0000              Called by: GTRE0000 *
.*                             Get Patient Details                            *
.******************************************************************************
GPTD0000  IF        WMSTAT=4 | WMSTAT=5 | WMSTAT=6
            CALL      GRSD0000              * Get removal status defaults
          ENDIF
.
          CALL      GWLD0000                * Get waiting list data
          BRANCH    EXIT,GPTD9500
.
          CALL      GPLD0000                * Get patient listing status default
.
          IF        WMSTAT=1|WMSTAT=2|WMSTAT=3|WMSTAT=4|WMSTAT=5|WMSTAT=6
            CALL      GBKD0000              * Get booking details
          ENDIF
.
          IF        WMSTAT=4 | WMSTAT=5
            CALL      GAFD0000              * Get admin file details
          ENDIF
.
          IF        WMSTAT=1
            ADD       ONE,ADDCNT            * Increment the add to list counter
          ENDIF
.
          IF        WMSTAT=4
            MATCH     ADATE,ENDDTE8
            IF        !@LESS
              ADD       ONE,ADMCNT          * Increment the admin counter
              ADD       ONE,REMCNT          * Increment the removal counter
            ENDIF
          ENDIF
.
          IF        WMSTAT=5
            MATCH     STRTDTE8,ADATE
            IF        !@LESS
              MATCH     ADATE,ENDDTE8
              IF        !@LESS
                ADD       ONE,ADMCNT        * Increment the admin counter
                ADD       ONE,REMCNT        * Increment the removal counter
              ENDIF
            ENDIF
          ENDIF
.
          IF        WMSTAT=6
            MATCH     WMDATE4,ENDDTE8
            IF        !@LESS
              ADD       ONE,REMCNT          * Increment the removal counter
            ENDIF
          ENDIF
.
GPTD9000  MOVE      ZERO,EXIT
          GOTO      GPTD9999
.
GPTD9500  MOVE      ONE,EXIT
GPTD9999  RETURN
+
.******************************************************************************
.*                                  GTPC0000              Called by: GTRE0000 *
.*                           Get Category TP Changes                          *
.******************************************************************************
GTPC0000  MOVE      ZERO,RECCNT
          MOVE      ZERO,DNRFCFLG
          MOVE      ZERO,NRFCDAYS
          MOVE      ZERO,NRFCLAST
          MOVE      ZERO,CURGIFLG
          MOVE      SP10,LASTURGT
.
          PACK      KEY29,WMURNO,WMCODE,WMCNT,ANST,ANSP,SP20
          CALL      RSWTCAT2                * Position on & read code changes
GTPC1000  CALL      RKWTCAT2                  file record
          BRANCH    OVRCD,GTPC3000
.
          MATCH     WMURNO,WTCAURNO
          GOTO      GTPC3000 IF NOT EQUAL   * Different U/R no
.
          MATCH     WMCODE,WTCAPROC
          GOTO      GTPC3000 IF NOT EQUAL   * Different procedure code
.
          COMPARE   WMCNT,WTCAPCNT
          GOTO      GTPC3000 IF NOT EQUAL   * Different procedure count
.
          MATCH     "TP",WTCACATF
          GOTO      GTPC3000 IF NOT EQUAL   * Different category
.
          MATCH     WTCAEDAT,ENDDTE8
          GOTO      GTPC3000 IF LESS        * End date < effective date
.
          MOVE      SP1,TCINDC3
          MATCH     SP3,WTCACODF
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSP,WTCACODF
            CALL      RDCODE1               * Read a codes file record
          ENDIF
.
          MOVE      TCINDC3,SAVINDC3        * Save the 3rd indicator
.
          MOVE      SP1,TCINDC3
          MATCH     SP3,WTCACODT
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSP,WTCACODT
            CALL      RDCODE1               * Read a codes file record
          ENDIF
.
GTPC2000  ADD       ONE,RECCNT
          IF        RECCNT=1
            MOVE      TCINDC3,CLURCT
            MATCH     SP3,WTCACODF
            GOTO      GTPC1000 IF EQUAL     * Blank 1st from change code
          ENDIF
.
          MATCH     SAVINDC3,TCINDC3
          GOTO      GTPC1000 IF EQUAL
.
          MOVE      TCINDC3,CLURCT
          MATCH     WTCAEDAT,WMDATE1
          IF        @EQUAL
            MOVE      SP8,WTCAEDAT
            MOVE      TCINDC3,CLURCT        * Clinical urgency cat last code
            GOTO      GTPC1000
          ENDIF
.
          MATCH     SP8,WTCAEDAT
          GOTO      GTPC2500 IF EQUAL
.
          UNPACK    WTCAEDAT,CCENT,CYEAR,CMON,CDAY
          PACK      CLURCTDT,CDAY,CMON,CCENT,CYEAR
          REP       " 0",CLURCTDT     * Clinical urgency cat last date
.
          MATCH     WTCACODF,WTCACODT
          IF        @LESS
            MATCH     "20010701",WTCAEDAT     * Effective date < 20010701
            IF        !@LESS
              PACK      DTLURGIN,CDAY,CMON,CCENT,CYEAR
              REP       " 0",DTLURGIN     * Date of last Clinical Urgency
.                                           * Increase
              MOVE      ONE,CURGIFLG
            ENDIF
          ENDIF
.
GTPC2500  MATCH     SP3,WTCACODF          * The first record ?
          IF        !@EQUAL
            IF        CURGIFLG = 1 
              MOVE      DTLURGIN,LASTURGT * no,save the next urgency date
            ELSE
              MOVE      CLURCTDT,LASTURGT * no,save the next urgency date
            ENDIF
          ENDIF
.
          MOVE      SAVINDC3,CLURCTCD    
          MOVE      TCINDC3,CLURCT        * Clinical urgency cat last code
.
          GOTO      GTPC1000
.
GTPC3000  MATCH     DTOFREG,CLURCTDT
          IF        @EQUAL
            MOVE      SP8,DTLURGIN
            MOVE      SP8,CLURCTDT
            MOVE      CLURCTCD,CLURCT
            MOVE      SP8,CLURCTCD
          ENDIF
.
GTPC9000  MOVE      ZERO,EXIT
GTPC9999  RETURN
+
.******************************************************************************
.*                                  DNRC0000              Called by: GTRE0000 *
.*         Calculate the total number of not ready for care days              *
.******************************************************************************
DNRC0000  MOVE      ZERO,DNRFCFLG
          MOVE      ZERO,NRFCDAYS
.
          PACK      KEY21,WMURNO,WMCODE,WMCNT,SP70
          PACK      KEY19,WMURNO,WMCODE,WMCNT,SP70
          CALL      RSWTSUS1
DNRC1000  CALL      RKWTSUS1
          BRANCH    OVRCD,DNRC3000
.
          MATCH     WMURNO,WTSUURNO
          GOTO      DNRC3000 IF NOT EQUAL   * Different U/R no
.
          MATCH     WMCODE,WTSUCODE
          GOTO      DNRC3000 IF NOT EQUAL   * Different procedure code
.
          COMPARE   WMCNT,WTSUCNT
          GOTO      DNRC3000 IF NOT EQUAL
.
          MATCH     WTSUFDAT,ENDDTE8
          GOTO      DNRC1000 IF LESS        * End date < suspension start
.
          PACK      TCINDC1,SP2       
          MATCH     SP3,WTSULSTS
          IF        !@EQUAL
            PACK      KEY5,ANSV,ANSL,WTSULSTS
            CALL      RDCODE1               * Read a codes file record
          ENDIF
.
          MATCH     "C",TCINDC1             * not ready for care
          GOTO      DNRC1500 IF EQUAL
.
          MATCH     "D",TCINDC1
          GOTO      DNRC1500 IF EQUAL
.
          MATCH     "S",TCINDC1
          GOTO      DNRC1000 IF NOT EQUAL
.
DNRC1500  MOVE      WTSUFDAT,CDYSFDTE
          MOVE      WTSUTDAT,CDYSTDTE
.
          MATCH     SP8,WTSUTDAT
          IF        @EQUAL
            MOVE      ENDDTE8,CDYSTDTE
          ELSE
            MATCH     ENDDTE8,WTSUTDAT
            IF        !@LESS
              MOVE      ENDDTE8,CDYSTDTE
            ENDIF
          ENDIF
.
          CALL      CALCDAYS              * Calculate diff between dates
          SUB       ONE,CDYSDAYS
          ADD       CDYSDAYS,NRFCDAYS     * Incre the days not ready for care
.
          MATCH     SP70,WTSUTDAT
          IF        @EQUAL  
            ADD       ONE,NRFCDAYS        * still NRFC at end date, so add 1
          ELSE
            MATCH     ENDDTE8,WTSUTDAT
            IF        !@LESS
              ADD       ONE,NRFCDAYS      * still NRFC at end date, so add 1
            ENDIF
          ENDIF   
.
          GOTO      DNRC1000
.
DNRC3000  MOVE      NRFCDAYS,DYNRFCRE
.
DNRC9000  MOVE      ZERO,EXIT
DNRC9999  RETURN
+
.******************************************************************************
.*                                  URNR0000              Called by: GTRE0000 *
.*         Calculate the number of days since last urgency reassignment       *
.******************************************************************************
URNR0000  MOVE      ZERO,DNRFCFLG
          MOVE      ZERO,NRFCDAYS
          MOVE      SP70,TESTDATE
.
          MATCH     CLURCTDT,SP70
          IF        @EQUAL
            MOVE      ZERO,LSNRFCRE
            GOTO      URNR9000
          ENDIF
.
          PACK      KEY21,WMURNO,WMCODE,WMCNT,SP70
          PACK      KEY19,WMURNO,WMCODE,WMCNT,SP70
          CALL      RSWTSUS1
URNR1000  CALL      RKWTSUS1
          BRANCH    OVRCD,URNR3000
.
          MATCH     WMURNO,WTSUURNO
          GOTO      URNR3000 IF NOT EQUAL   * Different U/R no
.
          MATCH     WMCODE,WTSUCODE
          GOTO      URNR3000 IF NOT EQUAL   * Different procedure code
.
          COMPARE   WMCNT,WTSUCNT
          GOTO      URNR3000 IF NOT EQUAL
.
          MATCH     WTSUFDAT,ENDDTE8
          GOTO      URNR1000 IF LESS        * End date < suspension start
.
          UNPACK    CLURCTDT,CDAY,CMON,CCENT,CYEAR
          PACK      TESTDATE,CCENT,CYEAR,CMON,CDAY
.
          MATCH     SP70,WTSUTDAT
          GOTO      URNR1500 IF EQUAL
.
          MATCH     TESTDATE,WTSUTDAT
          GOTO      URNR1000 IF LESS        * End date < suspension start
.
URNR1500  PACK      TCINDC1,SP2
          MATCH     SP3,WTSULSTS
          IF        !@EQUAL
            PACK      KEY5,ANSV,ANSL,WTSULSTS
            CALL      RDCODE1               * Read a codes file record
          ENDIF
.
          MATCH     "C",TCINDC1             * not ready for care
          GOTO      URNR2000 IF EQUAL
.
          MATCH     "D",TCINDC1
          GOTO      URNR2000 IF EQUAL
.
          MATCH     "S",TCINDC1
          GOTO      URNR1000 IF NOT EQUAL
.
URNR2000  MOVE      WTSUFDAT,CDYSFDTE
          MOVE      WTSUTDAT,CDYSTDTE
.
          UNPACK    CLURCTDT,CDAY,CMON,CCENT,CYEAR
          PACK      TESTDATE,CCENT,CYEAR,CMON,CDAY
          MATCH     TESTDATE,WTSUFDAT
          IF        @LESS
            UNPACK    CLURCTDT,CDAY,CMON,CCENT,CYEAR
            PACK      CDYSFDTE,CCENT,CYEAR,CMON,CDAY
          ENDIF
.
          MATCH     SP8,WTSUTDAT
          IF        @EQUAL
            MOVE      ENDDTE8,CDYSTDTE
          ELSE
            MATCH     ENDDTE8,WTSUTDAT
            IF        !@LESS
              MOVE      ENDDTE8,CDYSTDTE
            ENDIF
          ENDIF
.
          CALL      CALCDAYS              * Calculate diff between dates
          SUB       ONE,CDYSDAYS
          ADD       CDYSDAYS,NRFCDAYS     * Incre the days not ready for care
.
          MATCH     SP70,WTSUTDAT
          IF        @EQUAL
            ADD       ONE,NRFCDAYS        * still NRFC at end date, so add 1
          ELSE
            MATCH     ENDDTE8,WTSUTDAT
            IF        !@LESS
              ADD       ONE,NRFCDAYS      * still NRFC at end date, so add 1
            ENDIF
          ENDIF
.
          GOTO      URNR1000
.
URNR3000  MOVE      NRFCDAYS,LSNRFCRE
.
URNR9000  MOVE      ZERO,EXIT
URNR9999  RETURN
+
.******************************************************************************
.*                                  CINR0000              Called by: GTRE0000 *
.*         Calculate the number of days since clinical urgency increase       *
.******************************************************************************
CINR0000  MOVE      ZERO,DNRFCFLG
          MOVE      ZERO,NRFCDAYS
          MOVE      SP70,TESTDATE
.
          MATCH     DTLURGIN,SP70
          IF        @EQUAL
            MOVE      ZERO,TNRFCLCU
            GOTO      CINR9000
          ENDIF
.
          PACK      KEY21,WMURNO,WMCODE,WMCNT,SP70
          PACK      KEY19,WMURNO,WMCODE,WMCNT,SP70
          CALL      RSWTSUS1
CINR1000  CALL      RKWTSUS1
          BRANCH    OVRCD,CINR3000
.
          MATCH     WMURNO,WTSUURNO
          GOTO      CINR3000 IF NOT EQUAL   * Different U/R no
.
          MATCH     WMCODE,WTSUCODE
          GOTO      CINR3000 IF NOT EQUAL   * Different procedure code
.
          COMPARE   WMCNT,WTSUCNT
          GOTO      CINR3000 IF NOT EQUAL
.
          MATCH     WTSUFDAT,ENDDTE8
          GOTO      CINR1000 IF LESS        * End date < suspension start
.
          UNPACK    CLURCTDT,CDAY,CMON,CCENT,CYEAR
          PACK      TESTDATE,CCENT,CYEAR,CMON,CDAY
.
          MATCH     SP70,WTSUTDAT
          GOTO      CINR1500 IF EQUAL
.
          MATCH     TESTDATE,WTSUTDAT
          GOTO      CINR1000 IF LESS        * End date < suspension start
.
CINR1500  PACK      TCINDC1,SP2
          MATCH     SP3,WTSULSTS
          IF        !@EQUAL
            PACK      KEY5,ANSV,ANSL,WTSULSTS
            CALL      RDCODE1               * Read a codes file record
          ENDIF
.
          MATCH     "C",TCINDC1             * not ready for care
          GOTO      CINR2000 IF EQUAL
.
          MATCH     "D",TCINDC1
          GOTO      CINR2000 IF EQUAL
.
          MATCH     "S",TCINDC1
          GOTO      CINR1000 IF NOT EQUAL
.
CINR2000  MOVE      WTSUFDAT,CDYSFDTE
          MOVE      WTSUTDAT,CDYSTDTE
.
          UNPACK    DTLURGIN,CDAY,CMON,CCENT,CYEAR
          PACK      TESTDATE,CCENT,CYEAR,CMON,CDAY
          MATCH     TESTDATE,WTSUFDAT
          IF        @LESS
            UNPACK    CLURCTDT,CDAY,CMON,CCENT,CYEAR
            PACK      CDYSFDTE,CCENT,CYEAR,CMON,CDAY
          ENDIF
.
          MATCH     SP8,WTSUTDAT
          IF        @EQUAL
            MOVE      ENDDTE8,CDYSTDTE
          ELSE
            MATCH     ENDDTE8,WTSUTDAT
            IF        !@LESS
              MOVE      ENDDTE8,CDYSTDTE
            ENDIF
          ENDIF
.
          CALL      CALCDAYS              * Calculate diff between dates
          SUB       ONE,CDYSDAYS
          ADD       CDYSDAYS,NRFCDAYS     * Incre the days not ready for care
.
          MATCH     SP70,WTSUTDAT
          IF        @EQUAL
            ADD       ONE,NRFCDAYS        * still NRFC at end date, so add 1
          ELSE
            MATCH     ENDDTE8,WTSUTDAT
            IF        !@LESS
              ADD       ONE,NRFCDAYS      * still NRFC at end date, so add 1
            ENDIF
          ENDIF
.
          GOTO      CINR1000
.
CINR3000  MOVE      NRFCDAYS,TNRFCLCU
.
CINR9000  MOVE      ZERO,EXIT
CINR9999  RETURN
+
.******************************************************************************
.*                                  GRSD0000              Called by: GPTD0000 *
.*                         Get Removed Status Defaults                        *
.******************************************************************************
GRSD0000  IF        WMSTAT=4 | WMSTAT=5
            MATCH     ADATE,ENDDTE8
            GOTO      GRSD9000 IF LESS      * End date < admin date
          ENDIF
.
          IF        WMSTAT=6
            MATCH     WMDATE4,ENDDTE8
            GOTO      GRSD9000 IF LESS      * End date < removal date
          ENDIF
.
          MOVE      "W",REFRREM             * After 1/7/00 default W
          MOVE      WMBOOK,AADMNO
          PACK      KEY8,AADMNO
          CALL      RDMISS1                 * Read an admin file record
          IF        OVRCD<>1
            UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
            PACK      DTOFREM,CDAY,CMON,CCENT,CYEAR
            REP       " 0",DTOFREM          * Date of removal - default to
          ENDIF                               admin date
.
          PACK      KEY10,WMBOOK,SP70
          CALL      RDSMMBS1
          CALL      RDKMMBS1
          BRANCH    OVRCD,GRSD9000
.
          COMPARE   WMBOOK,MMADMN
          GOTO      GRSD9000 IF NOT EQUAL
.
          UNPACK    MMDATE,CCENT,CYEAR,CMON,CDAY
          PACK      DOFPROC,CDAY,CMON,CCENT,CYEAR
.
GRSD9000  MOVE      ZERO,EXIT
GRSD9999  RETURN
+
.******************************************************************************
.*                                  GPLD0000              Called by: GPTD0000 *
.*                     Get Patient Listing Status Default                     *
.******************************************************************************
GPLD0000  MOVE      SP1,TCINDC1
          MOVE      SP70,SLISTDAT
          MOVE      ZERO,INDEFNR
.
          PACK      KEY5,ANSV,ANSL,WTTXPLST
          CALL      RDCODE1                 * Read a codes file record
          MOVE      TCINDC1,SAVINDC1
.
          MOVE      ZERO,NRDYCARE           * Current State - Ready For Care
          MOVE      SP70,NRDYCODE           * Current Not Ready For Care Code
          MOVE      ZERO,NRFCDAYS
          MOVE      WMDATE1,KEY8
          PACK      KEY21,WMURNO,WMCODE,WMCNT,SP70
          PACK      KEY19,WMURNO,WMCODE,WMCNT,SP70
          CALL      RSWTSUS1
GPLD1000  CALL      RKWTSUS1
          BRANCH    OVRCD,GPLD2000
.
          PACK      KEY21,WTSUURNO,WTSUCODE,WTSUCNT,SP70
          MATCH     KEY19,KEY21
          GOTO      GPLD2000 IF NOT EQUAL
.
          MOVE      "VL",KEY2
          PACK      KEY5,KEY2,WTSULSTS
          CALL      RDCODE1
          BRANCH    OVRCD,GPLD1000
.
          MATCH     "R",TCINDC1
          GOTO      GPLD1000 IF EQUAL
.
          MATCH     WTSUFDAT,ENDDTE8
          GOTO      GPLD2000 IF LESS
.
          MATCH     SP70,WTSUTDAT
          IF        @EQUAL
            MOVE      ONE,INDEFNR
            MOVE      ENDDTE8,WTSUTDAT
          ENDIF
.
          DAYSEP    WTSUFDAT,WTSUTDAT,F4
          ASSIGN    F4+1+NRFCDAYS,NRFCDAYS
.
          MOVE      SP70,DATE1
          UNPACK    WTSUFDAT,CCENT,CYEAR,CMON,CDAY
          MATCH     ENDDTE8,WTSUTDAT
          IF        @EQUAL|@LESS
            COMPARE   ONE,INDEFNR
            IF        !@EQUAL
              UNPACK    WTSUTDAT,CCENT,CYEAR,CMON,CDAY
            ENDIF
          ENDIF
.
          PACK      DATE1,CCENT,CYEAR,CMON,CDAY
          MATCH     SP70,SLISTDAT
          GOTO      GPLD1500 IF EQUAL
.          
          MATCH     SLISTDAT,DATE1
          GOTO      GPLD1000 IF LESS
.
GPLD1500  PACK      PTLTSTDT,CDAY,CMON,CCENT,CYEAR
          MOVE      TCINDC1,PTLTSTCD
          PACK      SLISTDAT,CCENT,CYEAR,CMON,CDAY
.
          COMPARE   ONE,INDEFNR
          IF        @EQUAL
            MOVE      ONE,NRDYCARE          * Current State - Not Ready For Care
            MOVE      WTSULSTS,NRDYCODE       * Current Not Ready For Care Code
            GOTO      GPLD1000
          ENDIF

          MATCH     WTSUTDAT,ENDDTE8
          IF        @LESS
            MOVE      ONE,NRDYCARE          * Current State - Not Ready For Care
            MOVE      WTSULSTS,NRDYCODE       * Current Not Ready For Care Code
          ENDIF
          GOTO      GPLD1000
.
GPLD2000  PACK      KEY19,WMURNO,WMCODE,WMCNT,SP70
          CALL      RLWTTX11
          BRANCH    OVRCD,GPLD6000,GPLD6000
.
          IF        NRDYCARE=1
            MOVE      NRDYCODE,WTTXPLST
            GOTO      GPLD4000
          ENDIF
.
          MOVE      "VL",KEY2
          PACK      KEY5,KEY2,SP70
          CALL      RDSCODE1
GPLD3000  CALL      RDKCODE1
          BRANCH    OVRCD,GPLD6000
.
          MATCH     "VL",TCODE
          GOTO      GPLD6000 IF NOT EQUAL
.
          MATCH     "R",TCINDC1 
          GOTO      GPLD3000 IF NOT EQUAL
.
          MOVE      ACODE,WTTXPLST
.
GPLD4000  MATCH     "1",WTSUAUTO
          GOTO      GPLD5000 IF EQUAL
          GOTO      GPLD6000
.
GPLD5000  CALL      UPWTTX11
          CALL      UUWTTX11
.
GPLD6000  PACK      KEY5,ANSV,ANSL,WTTXPLST
          CALL      RDCODE1                 * Read a codes file record
          BRANCH    OVRCD,GPLD9000
.
          MOVE      TCINDC1,SAVINDC1
.
          MOVE      SAVINDC1,PTLTST         * Patient listing status
          MATCH     ANSD,SAVINDC1
          IF        @EQUAL
            MOVE      ANSN,PTLTST           * Patient list status - not ready
          ENDIF
.
          MATCH     ANSS,SAVINDC1
          IF        @EQUAL
            MOVE      ANSN,PTLTST           * Patient list status - not ready
          ENDIF
.
          MATCH     ANSC,SAVINDC1
          IF        @EQUAL
            MOVE      ANSN,PTLTST           * Patient list status - not ready
          ENDIF
.
          MOVE      SAVINDC1,NRFCSTAT       * NRFC status
.
GPLD9000  MOVE      ZERO,EXIT
GPLD9999  RETURN
+
.******************************************************************************
.*                                  GWLD0000              Called by: GPTD0000 *
.*                            Get Waiting List Data                           *
.******************************************************************************
GWLD0000  MOVE      ZERO,FORM8
          MOVE      WTWMUNIQ,FORM8
          MOVE      FORM8,UNIQKEY
          REP       " 0",UNIQKEY
          MOVE      CFILENO,TCAMPUS
          MOVE      "000",PRPRPROC       * initialise principal prescribed proc.
.
          MATCH     SP9,WMCODE
          IF        !@EQUAL
            PACK      KEY9,WMCODE
            CALL      RDPROA1               * Read a procedure file record
            IF        OVRCD<>1
              SQUEEZE   WTWPHDPE
              PACK      WTWPHDPE,WTWPHDPE,SP5
              MATCH     "200",WTWPHDPE
              GOTO      GWLD9500 IF EQUAL   * Ignore this procedure
.
              SQUEEZE   WTWPHDPE
              MOVE      WTWPHDPE,FORM3
              MOVE      FORM3,PRPRPROC   * Principal prescribed procedure
              REP       " 0",PRPRPROC
            ELSE
              MOVE      "Invalid Procedure Code",ERRMSG
              CALL      PRNT0000              * Print error message
            ENDIF
          ENDIF
.
          LOAD      TCODE,PTCNCSRL,CATW1,CATW2,CATW3,CATW4,CATX5,CATX6:
                                   CATX7,CATX8,CATS
.
.         LOAD      ACODE,PTCNCSRL,WMUDEF1,WMUDEF2,WMUDEF3,WMUDEF4,WMUDEF5:
.                                  WMUDEF6,WMUDEF7,WMUDEF8,WTWMSRCR
.
          MOVE      "1",SRCREF
          MATCH     SP3,WTWMSRCR
          IF        !@EQUAL
            PACK      KEY5,TCODE,WTWMSRCR
            CALL      RDCODE1
            IF        OVRCD <>1
              UNPACK    THCSCOD,DIM1,SRCREF * Source of referral
              REP       " 0",SRCREF
            ENDIF
          ENDIF
.
          IF        PTCNUADT = 1
            PACK      KEY19,WMURNO,WMCODE,DWMCNT
            CALL      RDWTRTD1
            IF        OVRCD=0
              UNPACK    WTRTREFR,REFHOS     * Referring hospital
              UNPACK    WTRTTDES,TRNDEST    * Transfer destination
            ENDIF
          ENDIF
.
          MATCH     SP3,WMUNIT
          IF        !@EQUAL
            PACK      KEY5,ANSW,ANSU,WMUNIT
            CALL      RDCODE1               * Read a codes file record
            IF        OVRCD<>1
              MOVE      THCSCOD,SURGSPEC    * Surgical speciality
              REP       " 0",SURGSPEC
              MATCH     "00",SURGSPEC
              GOTO      GWLD9500 IF EQUAL   * Ignore this unit
            ENDIF
          ENDIF
.
          MATCH     SP3,WMPTY
          IF        !@EQUAL
            PACK      KEY5,ANST,ANSP,WMPTY
            CALL      RDCODE1               * Read a codes file record
            IF        OVRCD<>1
              MOVE      TCINDC3,CLURCT      * Clinical urgency catgeory
              REP       " 0",CLURCT
            ENDIF
          ENDIF
.
          MOVE      "2",INTLOS              * Intended LOS - overnight
          IF        WTCNUSXB=0
            IF        WMSTAY=0
              MOVE      "1",INTLOS            * Intended LOS - sameday
            ENDIF
          ELSE
            PACK      KEY5,ANSX,ANSB,WTTXIDYC,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              MATCH     SP70,TCINDC1
              IF        !@EQUAL
                MOVE      TCINDC1,INTLOS
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP8,WMDATE1
          IF        !@EQUAL
            UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
            PACK      DTOFREG,CDAY,CMON,CCENT,CYEAR
            REP       " 0",DTOFREG          * Date of registration
          ENDIF
.
          COMPARE   SIX,WMSTAT
          GOTO      GWLD9000 IF NOT EQUAL   * Patient not removed
.
          MATCH     WMDATE4,ENDDTE8
          GOTO      GWLD9000 IF LESS        * End date < removal date
.
          MATCH     SP8,WMDATE4
          IF        !@EQUAL
            UNPACK    WMDATE4,CCENT,CYEAR,CMON,CDAY
            PACK      DTOFREM,CDAY,CMON,CCENT,CYEAR
            REP       " 0",DTOFREM          * Date of removal
          ENDIF
.
          MATCH     SP3,WMREMOVE
          GOTO      GWLD9000 IF EQUAL
.
          PACK      KEY5,ANSW,ANSR,WMREMOVE
          CALL      RDCODE1               * Read a codes file record
          BRANCH    OVRCD,GWLD9000
          MOVE      TCINDC3,REFRREM     * Reason for removal
          MOVE      TCINDC4,REFRCAN     * Reason for cancellation
          MATCH     ANSX,REFRREM
          IF        !@EQUAL
            MATCH     ANSS,REFRREM
            IF        !@EQUAL
              MATCH     ANSW,REFRREM    * I CAR 44438
              GOTO      GWLD9000 IF NOT EQUAL
            ENDIF
          ENDIF
.
          PACK      KEY5,ANSC,ANSL,WTTXCTYP
          CALL      RDCODE1               * Read a codes file record
          IF        OVRCD<>1
            MOVE      THCSCOD,INSDEC
          ENDIF
..... I CAR 44438
          MATCH     ANSS,REFRREM
          IF        @EQUAL
            MATCH     SP8,WMDATE3
            IF        !@EQUAL
              UNPACK    WMDATE3,CCENT,CYEAR,CMON,CDAY
              PACK      DTSCDT,CDAY,CMON,CCENT,CYEAR
              REP       " 0",DTSCDT           * Scheduled admission date when
            ENDIF                             * removal reason is "S"
          ENDIF
.... I CAR 44438
.
GWLD9000  MOVE      ZERO,EXIT
          GOTO      GWLD9999
.
GWLD9500  MOVE      ONE,EXIT
GWLD9999  RETURN
+
.******************************************************************************
.*                                  GBKD0000              Called by: GPTD0000 *
.*                             Get Booking Details                            *
.******************************************************************************
GBKD0000  MOVE      ZERO,HOSPOST
.
GBKD0500  PACK      KEY16,WMURNO,SP20
          CALL      RSBKREC6                * Position on & read a booking
GBKD1000  CALL      RKBKREC6                  file record
          BRANCH    OVRCD,GBKD3000
.
          MATCH     WMURNO,BKREURNO
          GOTO      GBKD3000 IF NOT EQUAL   * Different U/R no
.
           MATCH     WMCODE,BKREPROC
          GOTO      GBKD1000 IF NOT EQUAL   * Different procedure code
.
          COMPARE   WMCNT,BKRECNT
          GOTO      GBKD1000 IF NOT EQUAL   * Different procedure count
.
          MATCH     BKREBKDT,ENDDTE8
          GOTO      GBKD1000 IF LESS        * End date < date booking made
.
          ADD       ONE,BOKNO
          UNPACK    BKREBKDT,CCENT,CYEAR,CMON,CDAY
          PACK      BOKDTE,CDAY,CMON,CCENT,CYEAR
          REP       " 0",BOKDTE             * Booking date
.
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          IF        WMSTAT<>1
            COMPARE   TWO,BKRESTAT
            IF        !@EQUAL
              PACK      DTSCDT,CDAY,CMON,CCENT,CYEAR
              REP       " 0",DTSCDT           * Date scheduled for admin
            ENDIF
          ENDIF
.
          MATCH     SP3,BKRECANC
          IF        @EQUAL
            GOTO      GBKD1000              * Blank cancellation code, get next
          ENDIF
.
          PACK      KEY5,ANSB,ANSC,BKRECANC
          CALL      RDCODE1                 * Read a codes file record
          IF        OVRCD<>1
            MOVE      TCINDC1,REAREBOK      * Reason for rebooking
            MATCH     ANSD,TCINDC1
            IF        !@EQUAL
              MATCH     ANSH,TCINDC1
              GOTO      GBKD1000 IF NOT EQUAL
            ENDIF
            ADD       ONE,HOSPOST
          ENDIF
.
          GOTO        GBKD1000
.
GBKD3000  PACK      KEY8,WMBOOK
          CALL      RDBKREC1                * Read a booking file record
          IF        OVRCD=0
            MATCH     BKREBKDT,ENDDTE8
            GOTO      GBKD9000 IF LESS        * End date < date booking made
            UNPACK    BKREBKDT,CCENT,CYEAR,CMON,CDAY
            PACK      BOKDTE,CDAY,CMON,CCENT,CYEAR
            REP       " 0",BOKDTE             * Booking date
          ENDIF
.
GBKD9000  MOVE      ZERO,EXIT
GBKD9999  RETURN
+
.******************************************************************************
.*                                  GAFD0000              Called by: GPTD0000 *
.*                           Get Admin File Details                           *
.******************************************************************************
GAFD0000  IF        WMSTAT=4 | WMSTAT=5
            MATCH     ADATE,ENDDTE8
            GOTO      GAFD9000 IF LESS      * End date < admin date
          ENDIF
.
          MATCH     SP3,ACLAIM
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,ACLAIM
            CALL      RDCODE1               * Read a codes file record
            IF        OVRCD<>1
              MOVE      THCSCOD,INSDEC
            ENDIF
          ENDIF
.
          MOVE      "W",REFRREM
.
GAFD9000  MOVE      ZERO,EXIT
GAFD9999  RETURN
+
.******************************************************************************
.*                                  VALD0000              Called by: ML0000   *
.*                          Validate The Extract Data                         *
.******************************************************************************
VALD0000  DISPLAY   *P1:24,*EL,"Validating : ";
          MOVE      ZERO,DSPCNT
          PACK      KEY27,SP30
          CALL      RSTEMP1B                * Position on & read a tempfile
VALD1000  CALL      RKTEMP1B                  record
          BRANCH    OVRCD,VALD9000
.
          ADD       ONE,DSPCNT              * Increment the record counter
          IF        DSPCNT%10=1
            DISPLAY   *P14:24,*V2LON,TMPURNO;
          ENDIF
.
.....          CALL      VWGH0000                * new validations for srf 6755
.
          MATCH     "1",INVPCOD
          IF        @EQUAL
            MOVE      "R:S121  Postcode/Locality Combination Invalid",ERRMSG
            CALL      PRNT0000              * Print error message
          ENDIF
.
          MOVE    ZERO,FORM3
          MOVE    PRPRPROC,FORM3
          IF      FORM3<500
            IF      FORM3<1 | FORM3>260
              MOVE      "R:S134  Principal Prescribed Procedure Invalid",ERRMSG
              CALL      PRNT0000              * Print error message
            ENDIF
          ENDIF
.
          MATCH   "031",PRPRPROC
          IF      @EQUAL
            MATCH   "19990701",DTOFREG
            IF      !@LESS
              MOVE     "R:S134  Procedure 031 is invalid from 01/07/1999",ERRMSG
              CALL      PRNT0000
            ENDIF
          ENDIF
.
          MOVE    ZERO,FORM2
          MOVE    SURGSPEC,FORM2
          IF      FORM2<1 | FORM2>11
            MOVE      "R:S147  Surgical Speciality Invalid",ERRMSG
            CALL      PRNT0000              * Print error message
          ENDIF
.
          MOVE    ZERO,FORM1
          MOVE    CLURCT,FORM1
          IF      FORM1<1 | FORM1>3
            MOVE      "R:S161  Clinical Urgency Category Invalid",ERRMSG
            CALL      PRNT0000              * Print error message
          ENDIF
.
          MATCH   ANSS,REFRREM
          IF      @EQUAL
            MATCH   "1",CLURCT
.....       IF      !@EQUAL                                             D-90305
            IF      @EQUAL
              MOVE      "S375: Invalid Clinical Urgency category for ",ERRMSG
              ENDSET    ERRMSG
              APPEND    "ESAS Reason for Removal",ERRMSG
              RESET     ERRMSG
              CALL      PRNT0000              * Print error message
            ENDIF
          ENDIF
.
          MATCH   ANSS,REFRREM
          IF      !@EQUAL
            MATCH   ANSW,REFRREM
            IF      !@EQUAL
              MATCH   ANSX,REFRREM
              GOTO    VALD1500 IF NOT EQUAL
            ENDIF
          ENDIF
.
          MATCH    SP70,INSDEC
          GOTO     VALD1500 IF NOT EQUAL
.
          MOVE      "S303: Insurance Declaration Invalid ",ERRMSG
          CALL      PRNT0000              * Print error message
.
VALD1500  MOVE    SP1,KEY1 
          MOVE    PTLTST,KEY1
          REP     "R1N2",KEY1
          MOVE    ZERO,FORM1
          MOVE    KEY1,FORM1
          IF      FORM1<1 | FORM1>2
            MOVE      "R:S265  Readiness Status Invalid",ERRMSG
            CALL      PRNT0000              * Print error message
          ENDIF
.
          MOVE    ZERO,FORM4
          MATCH   DTOFREG,PTLTSTDT
          IF      @EQUAL
            IF      FORM1=1
              MOVE     DYNRFCRE,FORM4
              COMPARE  ZERO,FORM4
              IF       !@EQUAL
                MOVE     "R:S323  Not ready for care days should be zero",ERRMSG
                CALL     PRNT0000
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      ZERO,FORM4
          UNPACK    DTOFREG,CDAY,CMON,CCENT,CYEAR
          PACK      CDYSFDTE,CCENT,CYEAR,CMON,CDAY
          CALL      IBACLOCK
          PACK      CDYSTDTE,CCC,CYY,CMM,CDD
          CALL      CALCDAYS
          MOVE      ZERO,FORM4
          MOVE      DYNRFCRE,FORM4
          COMPARE   FORM4,CDYSDAYS
          IF        @LESS
            MOVE      "R:S324/325  Number of DNRFC must be less than ",ERRMSG
            ENDSET    ERRMSG
            APPEND    "days on list",ERRMSG
            RESET     ERRMSG
            CALL      PRNT0000
          ENDIF
.
          MOVE    SP1,KEY1
          MOVE    REAREBOK,KEY1
          REP     " 0A1X1F1C1P2H3D4",KEY1
          MOVE    ZERO,FORM1
          MOVE    KEY1,FORM1
          IF      FORM1<0 | FORM1>4
            MOVE      "R:S279  Invalid Reason for Rebooking",ERRMSG
            CALL      PRNT0000              * Print error message
          ENDIF
.
          MOVE    SP1,KEY1
          MOVE    REFRREM,KEY1
          REP     " 1W1M1B1I1U1X1N1T1R1Z1Q1S1F1E1O1",KEY1
          MOVE    ZERO,FORM1
          MOVE    KEY1,FORM1
          IF      FORM1<>1
            MOVE      "R:S298  Invalid Reason for Removal",ERRMSG
            CALL      PRNT0000              * Print error message
          ENDIF
.
          MATCH   ANSX,REFRREM
          IF      !@EQUAL
            MATCH   ANSN,REFRREM
            IF      !@EQUAL
              MATCH   ANSS,REFRREM
              IF      !@EQUAL
                MATCH   ANST,REFRREM
                GOTO    VALD1200 IF NOT EQUAL
              ENDIF
            ENDIF
          ENDIF
.
          MATCH   SP4,HOSPCOD
          IF      !@EQUAL
            MATCH   SP8,DTOFREM
            IF      @EQUAL
              MOVE    "R:S293  Blank Removal date, Reason ",ERRMSG
              ENDSET   ERRMSG
              APPEND   "Removal is X",ERRMSG
              RESET    ERRMSG
              CALL     PRNT0000
            ENDIF
          ELSE
            MOVE     "R:S309  Blank Hospital Code, Reason for ",ERRMSG
            ENDSET   ERRMSG
            APPEND   "Removal is X",ERRMSG
            RESET    ERRMSG
            CALL     PRNT0000
          ENDIF
.
VALD1200  CALL      NEWV0000              * new validations for 01/07/2002
.
          MOVE      ZERO,FORM1
          MOVE      SRCREF,FORM1
          IF        FORM1<1|FORM1>5
            MOVE      "R:S192  Source of Referral is mandatory",ERRMSG
            CALL      PRNT0000
          ENDIF
.
          IF        BOKNO=0
            MATCH     SP8,BOKDTE
            IF        !@EQUAL
              MOVE      "R:S288  Booking Date but Booking Number ",ERRMSG
              ENDSET    ERRMSG
              APPEND    "blank/zero",ERRMSG
              RESET     ERRMSG
              CALL      PRNT0000
            ENDIF
            GOTO      VALD2000
          ENDIF
.
          MATCH     SP8,BOKDTE
          IF        @EQUAL
            MOVE      "R:S289  Booking Number but Booking Date blank",ERRMSG
            CALL      PRNT0000
          ENDIF
.
VALD2000  MATCH     DTLURGIN,CLURCTDT
          IF        @LESS
            MOVE      "R:S346  Urgency Reassgn. < Clinical Urgency ",ERRMSG
            ENDSET    ERRMSG
            APPEND    "Incr. Date",ERRMSG
            RESET     ERRMSG
            CALL      PRNT0000
          ENDIF
.
          MATCH     SP8,CLURCTDT
          IF        @EQUAL
            MATCH     SP8,DTLURGIN
            IF        !@EQUAL
              MOVE      "R:S162  Urgency Reassgn. Date Blank Clinical ",ERRMSG
              ENDSET    ERRMSG
              APPEND    "Urgency Incr. not blank",ERRMSG
              RESET     ERRMSG
              CALL      PRNT0000
            ENDIF
          ENDIF
.
          MATCH     "1",CLURCT
          IF        !@EQUAL
            MATCH     "2",CLURCT
            GOTO      VALD1000 IF NOT EQUAL
          ENDIF
.
          UNPACK    DTOFREG,CDAY,CMON,CCENT,CYEAR
          PACK      DATE1,CCENT,CYEAR,CMON,CDAY
          MATCH     SP70,DTLURGIN
          IF        !@EQUAL
            UNPACK    DTLURGIN,CDAY,CMON,CCENT,CYEAR
            PACK      DATE1,CCENT,CYEAR,CMON,CDAY
          ENDIF
          MATCH     SP70,DTOFREM
          IF        !@EQUAL
            UNPACK    DTOFREM,CDAY,CMON,CCENT,CYEAR
            PACK      DATE2,CCENT,CYEAR,CMON,CDAY
          ELSE
            MOVE      ENDDTE8,DATE2
          ENDIF
.
          MOVE      ZERO,F4
          MOVE      ZERO,FORM4
          DAYSEP    DATE1,DATE2,F4
          MATCH     SP70,DTLURGIN
          IF        !@EQUAL
            MOVE      TNRFCLCU,FORM4
            SUB       FORM4,F4
          ELSE
            MOVE      DYNRFCRE,FORM4
            SUB       FORM4,F4
          ENDIF
.
          MATCH     "1",CLURCT
          IF        @EQUAL
            COMPARE   THIRTY,F4
            GOTO      VALD1000 IF LESS
            GOTO      VALD1000 IF EQUAL
            MOVE      "W:S315  Clinical Urgency Category 1, wait>30 days",ERRMSG
            CALL      PRNT0000
          ENDIF
.
          MATCH     "2",CLURCT
          IF        @EQUAL
            COMPARE   NINTY,F4
            GOTO      VALD1000 IF LESS
            GOTO      VALD1000 IF EQUAL
            MOVE      "R:S316  Clinical Urgency Category 2, wait>90 days",ERRMSG
            CALL      PRNT0000
          ENDIF
.
          GOTO      VALD1000
.      
VALD9000  MOVE      ZERO,EXIT
VALD9999  RETURN
+
.******************************************************************************
.*                                  NEWV0000              Called by: VALD0000 *
.*              New validations after 01/07/2002                              *
.******************************************************************************
NEWV0000  IF        NEWFILE<>1
            GOTO      NEWV9999
          ENDIF
.
. --- Warn if admitted to hospital and date of procedure is blank ---
.
          MATCH     ANSW,REFRREM
          IF        @EQUAL
            MATCH     SP70,DOFPROC
            IF        @EQUAL
              MOVE      "W:S363  Date of procedure is blank",ERRMSG
              CALL      PRNT0000
            ENDIF
          ENDIF
.
. --- Only send date of procedure if patient admitted to hospital ---
.
          MATCH     ANSW,REFRREM
          IF        !@EQUAL
            MOVE      SP70,DOFPROC
            GOTO      NEWV9999
          ENDIF
.
          UNPACK    DTOFREM,CDAY,CMON,CCENT,CYEAR
          PACK      DATE2,CCENT,CYEAR,CMON,CDAY
          MATCH     SP70,DOFPROC
          IF        !@EQUAL
            UNPACK    DOFPROC,CDAY,CMON,CCENT,CYEAR
            PACK      DATE1,CCENT,CYEAR,CMON,CDAY
            MATCH     DATE2,DATE1
            IF        @LESS
              MOVE      "R:S364  Date of Procedure before removal",ERRMSG
              CALL      PRNT0000
            ENDIF
          ENDIF
.
NEWV1000  MATCH     "1",INTLOS
          GOTO      NEWV2000 IF NOT EQUAL
.
          MATCH     DATE1,DATE2
          GOTO      NEWV2000 IF EQUAL
          IF        !@LESS
            MOVE      "W:S365  Date of Procedure>Removal Date but ",ERRMSG 
            ENDSET    ERRMSG
            APPEND    "Intended Day Case",ERRMSG
            RESET     ERRMSG
            CALL      PRNT0000
          ENDIF
.
NEWV2000  MOVE      DATE2,CDYSFDTE
          MOVE      DATE1,CDYSTDTE
          CALL      CALCDAYS              * Calculate diff between dates
          SUB       ONE,CDYSDAYS
          IF        CDYSDAYS > 7
            MOVE      "W:S366 Date of Procedure > 7 days ",ERRMSG
            ENDSET    ERRMSG
            APPEND    "after Removal Date",ERRMSG
            RESET     ERRMSG
            CALL      PRNT0000
          ENDIF
.
          MATCH     SP70,CLURCTCD
          GOTO      NEWV3100 IF EQUAL
.
          MATCH     CLURCTCD,CLURCT
          GOTO      NEWV3000 IF NOT LESS
.
          MATCH     SP8,DTLURGIN
          GOTO      NEWV3000 IF NOT EQUAL
.
          MOVE      "R:S350 Clin Urgency more urgent but ",ERRMSG
          ENDSET    ERRMSG
          APPEND    "Date Last Clinical Urgency Increase blank ",ERRMSG
          RESET     ERRMSG
          CALL      PRNT0000
.
          MATCH     SP1,CLURCTCD
          IF        @EQUAL
            MOVE      "R:S200 Clinical Urgency increase ",ERRMSG
            ENDSET    ERRMSG
            APPEND    "incomplete ",ERRMSG
            RESET     ERRMSG
            CALL      PRNT0000
          ENDIF
.
          MATCH     SP4,TNRFCLCU
          IF        @EQUAL
            MOVE      "R:S318  Total NRFC days Following Last Clin. ",ERRMSG
            ENDSET    ERRMSG
            APPEND    "Urg. increase blank ",ERRMSG
            RESET     ERRMSG
            CALL      PRNT0000
          ELSE
            MATCH      SP8,DTLURGIN
            IF         @EQUAL
              MOVE       "R:S199  Date of Last Clincal Urgency Increase ",ERRMSG
              ENDSET     ERRMSG
              APPEND     "blank ",ERRMSG
              RESET      ERRMSG
              CALL       PRNT0000
            ENDIF
          ENDIF
.
NEWV3000  MATCH     SP8,CLURCTDT
          IF        @EQUAL
            MOVE      "R:S351 Clin Urgency less urgent but ",ERRMSG
            ENDSET    ERRMSG
            APPEND    "Urg. Reassign. Date blank ",ERRMSG
            RESET     ERRMSG
            CALL      PRNT0000
          ENDIF
.
NEWV3100
NEWV9999  RETURN
+
.******************************************************************************
.*                                  VWGH0000              Called by: VALD0000 *
.*              New validations added for WGH srf 6755                        *
.******************************************************************************
VWGH0000  UNPACK    SP70,DIM10,DIM1
          UNPACK    MEDCARE,DIM10,DIM1
          MATCH     "0",DIM1
          GOTO      VWGH1000 IF NOT EQUAL
. 
          UNPACK    DOB,CDAY,CMON,CCENT,CYEAR
          PACK      PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      IBACLOCK
          PACK      AGEDATE,CCC,CYY,CMM,CDD
          CALL      CALCAGE
.
          COMPARE   SIX,PSAGEMNT
          GOTO      VWGH1000 IF LESS
.
          MOVE      "R:S081  Medicare Number Invalid",ERRMSG
          CALL      PRNT0000
.
VWGH1000  MATCH     SP8,BOKDTE
          GOTO      VWGH2000 IF EQUAL
.
          UNPACK    SP70,DIM8,DIM8A
          UNPACK    DTOFREG,CDAY,CMON,CCENT,CYEAR
          PACK      DIM8,CCENT,CYEAR,CMON,CDAY
          UNPACK    BOKDTE,CDAY,CMON,CCENT,CYEAR
          PACK      DIM8A,CCENT,CYEAR,CMON,CDAY
          MATCH     DIM8,DIM8A
          IF        @LESS
            MOVE      "R:S276  Booking Date before Registration Date",ERRMSG
            CALL      PRNT0000
          ENDIF
.
          MATCH     "20000701",DIM8A
          IF        @LESS
            MOVE      ZERO,HOSPOST
          ENDIF
.
VWGH2000  MATCH     SP8,DTSCDT
          GOTO      VWGH3000 IF EQUAL
.
          MATCH     DTOFREM,DTSCDT
          IF        !@EQUAL
            MATCH     ANSW,REFRREM
            IF        @EQUAL
              MOVE      DTSCDT,DTOFREM
            ENDIF
          ENDIF
.
          MATCH     SP8,REFRREM
          GOTO      VWGH3000 IF NOT EQUAL
.
          UNPACK    DTSCDT,CDAY,CMON,CCENT,CYEAR
          PACK      DIM8A,CCENT,CYEAR,CMON,CDAY
          MATCH     DIM8A,ENDDTE8
          GOTO      VWGH3000 IF EQUAL
.
          MATCH     DIM8A,ENDDTE8
          IF        !@LESS
            MOVE      "R:S287  Scheduled Admission Date is in the past",ERRMSG
            CALL      PRNT0000
          ENDIF
.
VWGH3000  MATCH     ANSW,REFRREM
          IF        @EQUAL
            MATCH     ANSN,PTLTST
            IF        @EQUAL
              MOVE      "R:S296  Removed as Admitted But Status Not ",ERRMSG
              ENDSET    ERRMSG
              APPEND    "Ready For Care",ERRMSG
              RESET     ERRMSG
              CALL      PRNT0000
            ENDIF
          ENDIF
.
          MATCH     SP8,BOKDTE
          GOTO      VWGH9000 IF EQUAL
.
          COMPARE   ZERO,BOKNO
          GOTO      VWGH9000 IF EQUAL
.
          MATCH     SP8,DTSCDT
          GOTO      VWGH9000 IF NOT EQUAL
.
          MATCH     SP1,REAREBOK
          IF        @EQUAL
            MOVE      ANSP,REAREBOK
.....            MOVE      "Blank reason for rebooking",ERRMSG
.....            CALL      PRNT0000
          ENDIF
.
VWGH9000  MOVE      ZERO,EXIT
VWGH9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000              Called by: VALD0000 *
.*                             Print Error Message                            *
.******************************************************************************
PRNT0000  COMPARE   CLNO,FIFTY8
          CALL      HEAD0000 IF LESS        * Print report header
.
          PACK      ERRMSG,ERRMSG,SP70
          UNPACK    TMPDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DATOLIST
          PRINT     *1,"|",TMPURNO,"|",TMPROCOD:
                    "| ",DATOLIST," |",ERRMSG,"|"
          ADD       ONE,CLNO
          ADD       ONE,ERRCNT              * Increment the error counter
          PACK      ERRMSG,SP70
.
PRNT9000  MOVE      ZERO,EXIT
PRNT9999  RETURN
+
.******************************************************************************
.*                                  WRIT0000              Called by: ML0000   *
.*                           Write The Extract File                           *
.******************************************************************************
WRIT0000  IF        NEWFILE=1
            PREP      TEMP5,FILENAME
          ELSE
            PREP      TEMP4,FILENAME
          ENDIF
          PREP      HTEMP,CFILENAM
          DISPLAY   *P1:24,*EL,"Writing : ";
          UNPACK    ENDDTE8,CCENT,CYEAR,CMON,CDAY
          PACK      EXTDTE,CDAY,CMON,CCENT,CYEAR
          REP       " 0",EXTDTE             * Extract date
.
          PACK      HOSPCOD,CFILENO   * Hospital code
.
          MOVE      ZERO,DSPCNT
          MOVE      ZERO,RECCNT
          MOVE      RECCNT,DRECCNT
          REP       " 0",DRECCNT
.
          WRITE     HTEMP,SEQ;EXTDTE,COMMA,HOSPCOD,COMMA,DRECCNT
.
          PACK      KEY27,SP30
          CALL      RSTEMP1B                * Position on & read a tempfile
WRIT1000  CALL      RKTEMP1B                  record
          BRANCH    OVRCD,WRIT2000
.
          ADD       ONE,DSPCNT
          IF        DSPCNT%10=1
            DISPLAY   *P11:24,*V2LON,TMPURNO;
          ENDIF
.
          ADD       ONE,RECCNT              * Increment the record counter
          IF        WTCNRJUR=1
            PACK      EURNO,SP70
            PACK      EURNO,SP2,TMPURNO
            REP       " 0",EURNO
          ELSE
            SQUEEZE   TMPURNO
            PACK      EURNO,TMPURNO,SP10
          ENDIF
.
          CALL      VWGH0000                * new validations for srf 6755
.
          MOVE      WATNO,DWATNO
          REP       " 0",DWATNO
          MOVE      BOKNO,DBOKNO
          REP       " 0",DBOKNO
          MATCH     ANSR,PTLTST
          IF        @EQUAL
            MOVE      SP1,NRFCSTAT
          ENDIF
          REP       " 0",DYNRFCRE
.
          MATCH     SP8,DTSCDT
          IF        !@EQUAL
            MATCH     DTOFREM,DTSCDT
            IF        !@EQUAL
              MATCH     ANSW,REFRREM
              IF        !@EQUAL
               MATCH      ANSS,REFRREM
               IF          !@EQUAL
                 GOTO        WRIT1050
               ENDIF
              ENDIF
              MOVE      DTSCDT,DTOFREM
            ENDIF
          ENDIF
.
WRIT1050  MATCH   ANSX,REFRREM
          IF      !@EQUAL
            MATCH    ANSN,REFRREM
            IF       !@EQUAL
              MATCH    ANST,REFRREM
              IF       !@EQUAL
                MATCH    ANSS,REFRREM
                IF       !@EQUAL
                  MOVE     SP70,TRNDEST
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          UNPACK    BOKDTE,CDAY,CMON,CCENT,CYEAR
          PACK      DIM8A,CCENT,CYEAR,CMON,CDAY
          MATCH     "20000701",DIM8A
          IF        @LESS
            MOVE      ZERO,HOSPOST
          ENDIF
.
          MOVE      HOSPOST,DHOSPOST
          SQUEEZE   DHOSPOST
          PACK      DHOSPOST,DHOSPOST,SP2
          REP       " 0",DYNRFCRE
          REP       " 0",LSNRFCRE
          REP       " 0",TNRFCLCU
.
          MATCH   "9988",POSTCOD
          IF      @EQUAL
            MOVE    SP70,LOCALITY
          ENDIF
.
          MATCH   "1000",POSTCOD
          IF      @EQUAL
            MOVE    SP70,LOCALITY
          ENDIF
.
.
WRIT1500  IF        NEWFILE=1
            WRITE     TEMP5,SEQ;UNIQKEY,COMMA,EURNO,COMMA,MEDCARE,COMMA:
                                MEDSUFX,COMMA,DWATNO,COMMA,DOB,COMMA:
                                SEX,COMMA,POSTCOD,COMMA:
                                LOCALITY,COMMA,PRPRPROC,COMMA,SURGSPEC:
                                COMMA,CLURCT,COMMA,INTLOS,COMMA:
                                DTOFREG,COMMA,SRCREF,COMMA,REFHOS,COMMA:
                                CLURCTDT,COMMA,DTLURGIN,COMMA,CLURCTCD,COMMA:
                                PTLTST,COMMA,PTLTSTDT,COMMA,NRFCSTAT,COMMA:
                                BOKDTE,COMMA,DBOKNO,COMMA,REAREBOK,COMMA:
                                DTSCDT,COMMA:
                                DTOFREM,COMMA,REFRREM,COMMA,INSDEC,COMMA:
                                TRNDEST,COMMA,DYNRFCRE,COMMA,LSNRFCRE,COMMA:
                                TNRFCLCU,COMMA,DHOSPOST,COMMA,DOFPROC,COMMA:
                                TCAMPUS
          ELSE
            WRITE     TEMP4,SEQ;UNIQKEY,COMMA,EURNO,COMMA,MEDCARE,COMMA:
                                MEDSUFX,COMMA,DWATNO,COMMA,DOB,COMMA:
                                SEX,COMMA,POSTCOD,COMMA:
                                LOCALITY,COMMA,PRPRPROC,COMMA,SURGSPEC:
                                COMMA,CLURCT,COMMA,INTLOS,COMMA:
                                DTOFREG,COMMA,SRCREF,COMMA,REFHOS,COMMA:
                                CLURCTDT,COMMA,DTLURGIN,COMMA,CLURCTCD,COMMA:
                                PTLTST,COMMA,PTLTSTDT,COMMA,NRFCSTAT,COMMA:
                                BOKDTE,COMMA,DBOKNO,COMMA,REAREBOK,COMMA:
                                DTSCDT,COMMA:
                                DTOFREM,COMMA,REFRREM,COMMA,INSDEC,COMMA:
                                TRNDEST,COMMA,DYNRFCRE,COMMA,LSNRFCRE,COMMA:
                                TNRFCLCU,COMMA,DHOSPOST
          ENDIF
          CALL      ESIHIS00    * Save to ESIS History Table
          GOTO      WRIT1000
.
WRIT2000  MOVE      RECCNT,DRECCNT
          REP       " 0",DRECCNT
          WRITAB    HTEMP,ZERO;EXTDTE,COMMA,HOSPCOD,COMMA,DRECCNT
.
          DISPLAY   *P1:23,*EF,*B,"Extraction Finished.  ":
                    *V2LON,DSPCNT,*HOFF," record(s) read.  ":
                    *P1:24,*V2LON,RECCNT,*HOFF," record(s) extracted.  ":
                    *V2LON,ERRCNT,*HOFF," error record(s).  ";
          CALL      HITENTER
.
WRIT9000  MOVE      ZERO,EXIT
WRIT9999  RETURN
+
.******************************************************************************
.*                                  TAIL0000              Called by: ML0000   *
.*                              Print Report Tail                             *
.******************************************************************************
TAIL0000  CALL      UND132
          PRINT     *N:
                    *1,"Number of Records Read         : ",DSPCNT,*N:
                    *1,"Number of Records Extracted    : ",RECCNT,*N:
                    *1,"Number of Error Records        : ",ERRCNT,*N,*N:
                    *1,"Number of Patients on List     :  ",ADDCNT,*N:
                    *1,"Number of Removals from List   :  ",REMCNT,*N:
                    *1,"Number of Admissions from List :  ",ADMCNT,*N,*N:
                    *1,"*** End of Report ***"
.
TAIL9000  MOVE      ZERO,EXIT
TAIL9999  RETURN
+
.******************************************************************************
.*                                  OPEN0000              Called by: ML0000   *
.*                              Open The Tempfile                             *
.******************************************************************************
OPEN0000  CALL      KILL0000                * Delete the tempfile
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          APPEND    FILELIN1,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMP1A,TFILNAME
          OPEN      TEMP1B,TFILNAME
.
OPEN9000  MOVE      ZERO,EXIT
OPEN9999  RETURN
+
.******************************************************************************
.*                                  KILL0000              Called by: ML0000   *
.*                             Delete The Tempfile                 & OPEN0000 *
.******************************************************************************
KILL0000  CLOSE     TEMP1A
          CLOSE     TEMP1B
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TFILNAME,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9000  MOVE      ZERO,EXIT
KILL9999  RETURN
.------------------------------------------------------------
. Move Extract to Web Directory
.------------------------------------------------------------
MVEXT000  CLEAR     CMDLINE                 * header file
          APPEND    "ibawat62.us2 ",CMDLINE
          APPEND    CFILENAM,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CLEAR     CMDLINE
          APPEND    "ibawat62.us1 ",CMDLINE
          APPEND    FILENAME,CMDLINE
          APPEND    ".txt",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
MVEXT999  RETURN
.----------------------------------------------------------------------
. Save to ESIS History Table
.----------------------------------------------------------------------
ESIHIS00  MOVE      ENDDTE8,WTEIDATE
          REP       " 0",WTEIDATE
          MOVE      UNIQKEY,WTEIUNIQ
          PACK      KEY16,WTEIUNIQ,WTEIDATE,SP70
          CALL      RDWTESI1
          IF        OVRCD=0
            CALL      MVESI000
            CALL      UPWTESI1
          ELSE
            CALL      MVESI000
            CALL      WRWTESI1
          ENDIF
          RETURN
.
MVESI000  MOVE      EURNO,WTEIURNO     *U/R no
          MOVE      MEDCARE,WTEIMEDC   *Medicare number
          MOVE      MEDSUFX,WTEIMEDS   *Medicare suffix
          MOVE      DWATNO,WTEIWTNO    *W/L counter
          UNPACK    DOB,CDAY,CMON,CCENT,CYEAR
          PACK      WTEIDOB,CCENT,CYEAR,CMON,CDAY
          MOVE      SEX,WTEISEX        *Sex
          MOVE      POSTCOD,WTEIPSTC   *Postcode
          MOVE      LOCALITY,WTEILOCA  *Locality
          MOVE      PRPRPROC,WTEIPROC  *Procedure code indicator
          MOVE      SURGSPEC,WTEISRGS  *Surgical speciality
          MOVE      CLURCT,WTEICLCT    *Clinical urgency category
          MOVE      INTLOS,WTEIILOS    *Intended LOS
          UNPACK    DTOFREG,CDAY,CMON,CCENT,CYEAR
          PACK      WTEIDTRG,CCENT,CYEAR,CMON,CDAY
          MOVE      SRCREF,WTEISRCR    *Source referral
          MOVE      REFHOS,WTEIREFH    *Referring hospital
          UNPACK    CLURCTDT,CDAY,CMON,CCENT,CYEAR
          PACK      WTEICLDT,CCENT,CYEAR,CMON,CDAY
          UNPACK    DTLURGIN,CDAY,CMON,CCENT,CYEAR
          PACK      WTEIDTIN,CCENT,CYEAR,CMON,CDAY
          MOVE      CLURCTCD,WTEICLCD  *Clinical urgency category last code
          MOVE      PTLTST,WTEILTST    *Patient listing status
          UNPACK    PTLTSTDT,CDAY,CMON,CCENT,CYEAR
          PACK      WTEIPSDT,CCENT,CYEAR,CMON,CDAY
          MOVE      NRFCSTAT,WTEIRFCS  *Reason for NRFC Status
          UNPACK    BOKDTE,CDAY,CMON,CCENT,CYEAR
          PACK      WTEIBKDT,CCENT,CYEAR,CMON,CDAY
          MOVE      DBOKNO,WTEIBKNO    *Booking U/R no count
          MOVE      REAREBOK,WTEIREBK  *Reason for rebooking
          UNPACK    DTSCDT,CDAY,CMON,CCENT,CYEAR
          PACK      WTEISCDT,CCENT,CYEAR,CMON,CDAY
          UNPACK    DTOFREM,CDAY,CMON,CCENT,CYEAR
          PACK      WTEIDTRM,CCENT,CYEAR,CMON,CDAY
          MOVE      REFRREM,WTEIRREM   *Reason for removal
          MOVE      INSDEC,WTEIINSD    *Insurance declaration
          MOVE      TRNDEST,WTEITRND   *Transfer Destination
          MOVE      DYNRFCRE,WTEINRFC  *Total not ready for care days
          MOVE      LSNRFCRE,WTEINRRE  *Total not ready for care days since Last
          MOVE      TNRFCLCU,WTEINRLC  *Total NRFC days following Last Clinical
          MOVE      DHOSPOST,WTEIHOSP  *Hospital initiated postponement
          MOVE      SP70,WTEISPAR      *Spare
          RETURN
+
.******************************************************************************
.*                                  MSFX0000                                  *
.*                              Get Medicare Suffix      called by : GAEP0000 *
.******************************************************************************
MSFX0000  MATCH     SP10,PMEDI
          GOTO      MSFX2100 IF NOT EQUAL
.
          PACK      KEY5,ANST,SP1,PTYPE  
          CALL      RDCODE1                 * read PATCODFD record
.
          MATCH     SP1,TCINDC1             * blank 1st Indicator ?
          GOTO      MSFX1000 IF EQUAL       * yes

          TYPE      TCINDC1                 * is 1st Indicator numeric ?
          GOTO      MSFX1000 IF NOT EQUAL   * no
          MOVE      TCINDC1,FORM1           * yes
.
          BRANCH    FORM1,MSFX1000,MSFX1100,MSFX1200
          GOTO      MSFX2000                * invalid 1st Indicator
.
. ------  1st Indicator = "1"  ------
.
MSFX1000  MOVE      "C-U",MEDSUFX
          GOTO      MSFX1300
.
. ------  1st Indicator = "2"  ------
.
MSFX1100  MOVE      "N-E",MEDSUFX
          GOTO      MSFX1300
.
. ------  1st Indicator = "3"  ------
.
MSFX1200  MOVE      "P-N",MEDSUFX
.
. ------  when Suffix is C-U, N-E or P-N, Medicare Number is set to blank  -----
.
MSFX1300  MOVE      SP10,MEDCARE
          GOTO      MSFX9999
.
. ------  invalid 1st Indicator  ------
.
MSFX2000  MATCH     SP10,PMEDI
          GOTO      MSFX9999 IF EQUAL
.
. ------  valid Medicare Number  ------
.
MSFX2100  MOVE      PGNAME,KEY25
          REP       UPPLOW,KEY25
          SCAN      "SON OF",KEY25
          GOTO      MSFX2200 IF EQUAL
.
          SCAN      "DAUGHTER OF",KEY25
          GOTO      MSFX2200 IF EQUAL
.
          SCAN      "BABY OF",KEY25
          GOTO      MSFX2200 IF EQUAL
.
          RESET     KEY25,3
          LENSET    KEY25
          RESET     KEY25
          MOVE      KEY25,MEDSUFX
          GOTO      MSFX9999
.
MSFX2200  MOVE      "BAB",MEDSUFX
          GOTO      MSFX9999
.
.         We are not sending any medicare number
.
MSFX2500  MOVE      SP10,MEDCARE            * set to blanks
          PACK      KEY5,ANST,SP1,PTYPE
          CALL      RDCODE1                 * read PATCODFD record
          TYPE      TCINDC1                 * is 1st Indicator numeric ?
          GOTO      MSFX2600 IF NOT EQUAL   * no - invalid value
.
          MOVE      TCINDC1,FORM1           * yes
          BRANCH    FORM1,MSFX2600,MSFX2700,MSFX2800
.
. ------  1st Indicator = "1"  ------
.
MSFX2600  MOVE      "C-U",MEDSUFX
          GOTO      MSFX9999
.
. ------  1st Indicator = "2"  ------
.
MSFX2700  MOVE      "N-E",MEDSUFX
          GOTO      MSFX9999
.
. ------  1st Indicator = "3"  ------
.
MSFX2800  MOVE      "P-N",MEDSUFX
.
MSFX9999  RETURN
.******************************************************************************
.*                       I/O Routines For The Tempfile                        *
.******************************************************************************
RATEMP1A  MOVE      ZERO,OVRCD
          RESET     KEY19
          READ      TEMP1A,KEY19;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1A  MOVE      TMPROCNT,DTMPROCN
          UPDATE    TEMP1A;TMPURNO,TMPROCOD,DTMPROCN,TMPDATE1,WATNO:
                           DOB,SEX,POSTCOD,LOCALITY,PRPRPROC:
                           SURGSPEC,CLURCT,INTLOS,DTOFREG,CLURCTDT:
                           DTLURGIN,CLURCTCD,PTLTST,PTLTSTDT,PTLTSTCD:
                           BOKDTE,BOKNO,REAREBOK,DTSCDT,DTOFREM:
                           REFRREM,INSDEC,REFRCAN,MEDCARE,MEDSUFX:
                           SRCREF,REFHOS,NRFCSTAT,TRNDEST,DYNRFCRE:
                           LSNRFCRE,TNRFCLCU,UNIQKEY,INVPCOD,HOSPOST:
                           DOFPROC,TCAMPUS
          RETURN
.
WRTEMP1A  RESET     KEY19
          MOVE      TMPROCNT,DTMPROCN
          WRITE     TEMP1A,KEY19;TMPURNO,TMPROCOD,DTMPROCN,TMPDATE1,WATNO:
                                 DOB,SEX,POSTCOD,LOCALITY,PRPRPROC:
                                 SURGSPEC,CLURCT,INTLOS,DTOFREG,CLURCTDT:
                                 DTLURGIN,CLURCTCD,PTLTST,PTLTSTDT,PTLTSTCD:
                                 BOKDTE,BOKNO,REAREBOK,DTSCDT,DTOFREM:
                                 REFRREM,INSDEC,REFRCAN,MEDCARE,MEDSUFX:
                                 SRCREF,REFHOS,NRFCSTAT,TRNDEST,DYNRFCRE:
                                 LSNRFCRE,TNRFCLCU,UNIQKEY,INVPCOD,HOSPOST:
                                 DOFPROC,TCAMPUS
          RETURN
.
RSTEMP1B  MOVE      ZERO,OVRCD
          RESET     KEY27
          READ      TEMP1B,KEY27;;
          RETURN
.
RKTEMP1B  MOVE      ZERO,OVRCD
          READKS    TEMP1B;TMPURNO,TMPROCOD,DTMPROCN,TMPDATE1,WATNO:
                           DOB,SEX,POSTCOD,LOCALITY,PRPRPROC:
                           SURGSPEC,CLURCT,INTLOS,DTOFREG,CLURCTDT:
                           DTLURGIN,CLURCTCD,PTLTST,PTLTSTDT,PTLTSTCD:
                           BOKDTE,BOKNO,REAREBOK,DTSCDT,DTOFREM:
                           REFRREM,INSDEC,REFRCAN,MEDCARE,MEDSUFX:
                           SRCREF,REFHOS,NRFCSTAT,TRNDEST,DYNRFCRE:
                           LSNRFCRE,TNRFCLCU,UNIQKEY,INVPCOD,HOSPOST:
                           DOFPROC,TCAMPUS
          GOTO      OVERCOND IF OVER
          MOVE      DTMPROCN,TMPROCNT
          RETURN
+
.==============================================================================
.
          INC       STD001IO
          INC       CALCDAYS                * Calculate diff between dates
          INC       CALCAGE 
.
          INC       BOKRC1IO/INC            * Booking file
          INC       IBAPOSIO/INC            * Postcode file
          INC       PATDSCIO/INC            * Discharge file
          INC       PATCODIO/INC            * Codes file
          INC       PATMA1IO/INC            * Master file
          INC       PATMI1IO/INC            * Admission file
          INC       PMSVX1IO/INC            * Admission file
          INC       PATMMBIO/INC            * MBS file
          INC       PATVADIO/INC            * Transfer source file
          INC       WATCATIO/INC            * Code changes file
          INC       WATESIIO/INC            * ESIS History
          INC       WATESTIO/INC            * ESIS Seal Table     
          INC       WATPROIO/INC            * Procedure file
          INC       WATRTDIO/INC            * Referral Destination file
          INC       WATSUSIO/INC            * Procedure Suspension file
          INC       WATTR1IO/INC            * Treatment file
          INC       WATTX1IO/INC            * Treatment Extention file
          INC       TFILENAM
          INC       IBASEQIO/INC
          INC       WEBERRIO/INC
+
.

