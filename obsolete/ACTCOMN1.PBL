.******************************************************************************
.*                                 GACTD000               Called by: Other    *
.*                          Get ACT Patient Details                  Programs *
.*   Modifications                                                            *
.*        V10.02.02 13/07/2011  Steve Armstrong   CAR 240722                  *
.*                  Further mods to cater for second given name as part of    *
.*                  PATGSRFD keys                                             *
.*        V10.02.01 22/06/2011  Steve Armstrong   CAR 240722                  *
.*                  Mods to cater for changes to PATGSRFD:                    *
.*                       - GSRSNAM & GSRGNAM extended to 30 chars.            *
.*                       - PTGSGNM2 added.                                    *
.*                       - all indexes changed in length.                     *
.******************************************************************************
.*        V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                  *
.*                  Added HL7TRGID & HL7INCLD setting for all broadcast       *
.*                  messages                                                  *
.******************************************************************************
.*        V9.02.04  12/02/2002  Steve Armstrong                               *
.*                  Mods to set KEY70 instead of KEY45 prior to calling       *
.*                  DGCLICUA                                                  *
.*        V9.02.03  08/11/2001  Mike Laming                                   *
.*                  Rename DGCLIUAL to DGCLICUA                               *
.*        V9.02.02  01/11/2001  Greg Horvat                                   *
.*                  Added xtra option to the parameter PNSWRACE               *
.*        V9.02.01  08/09/2001  Glenn Saunders                                *
.*                  Change keys for patgsrn reads to reflect enhanced indexes.*
.******************************************************************************
.
          DEFPROC   GACTD000
.
ACTFNM    DIM       8
ACTTMP    FILE      ASCII, FIXED=256
.
CNSWLNG   FORM      1
.
KEYPSNAM  DIM       30
KEYPGNAM  DIM       30
KEYPADD1  DIM       30
KEYPADD2  DIM       30
KEYPADDX  DIM       30
KEYPADD3  DIM       25
KEYPADD4  DIM       30
KEYPPOST  DIM       4
KEYSLAC   DIM       5
ACTPCNTY  DIM       8
KEYTELEP  DIM       12
KEYTELEB  DIM       12
KEYPDFL   DIM       1
KEYNSNAM  DIM       30
KEYNGNAM  DIM       30
KEYNADD1  DIM       30
KEYNADD2  DIM       30
KEYNADDX  DIM       30
KEYNADD3  DIM       25
KEYNADD4  DIM       30
KEYNPOST  DIM       4
ACTNCNTY  DIM       8
KEYNTELP  DIM       12
KEYNTELB  DIM       12
KEYESNAM  DIM       30
KEYEGNAM  DIM       30
KEYXECA1  DIM       30
KEYXECA2  DIM       30
KEYXECAX  DIM       30
KEYXECA3  DIM       25
KEYXECA4  DIM       30
KEYXECPC  DIM       4
ACTECNTY  DIM       8
KEYXECPP  DIM       12
KEYXECBP  DIM       12
.
LANGCOD   DIM       3
.
PNSWRACE  FORM      2
.
RESICOD   DIM       3
.
. ----- Open the files & initialise variables -----
.
GACTD000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*222,CNSWLNG
          READ      CONTROLF,EIGHTY8;*242,PNSWRACE
.
          CLEAR     ACTFNM
          APPEND    "curpmi",ACTFNM
          APPEND    PORT,ACTFNM
          RESET     ACTFNM
          REP       " 0",ACTFNM
          RESET     ACTFNM
.
. ----- Get the ACT no & validate it -----
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      ACTTMP,ACTFNM
          TRAPCLR   IO
          BRANCH    OVRCD,GACTD950
.
. ----- Get the local U/R no & validate it -----
.
          READ      ACTTMP,SEQ;ACTLOCAL,KEY8;
          GOTO      GACTD950 IF OVER
.
          MATCH     EXITCHAR,ACTLOCAL
          GOTO      GACTD950 IF EQUAL       * Exit
.
          MATCH     PURNO,KEY8
          GOTO      GACTD950 IF NOT EQUAL   * Different U/R no, exit
.
          MATCH     ZEROUR,ACTURNO
          IF        @EQUAL
            MOVE      ACTLOCAL,ACTURNO      * save ACT health number
          ENDIF
.
. ----- Read the remaining changed data -----
.
          READ      ACTTMP,SEQ;PTITL,KEYPSNAM,KEYPGNAM,KEYPADD1,KEYPADD2:
                               KEYPADDX,KEYPADD3,KEYPADD4,KEYPPOST,KEYSLAC:
                               ACTPCNTY,KEYTELEP,KEYTELEB,PBDATE,PSEX,PMSTAT:
                               PCONT,PREG,RESICOD,LANGCOD:
                               KEYPDFL,PDECDTE:
                               KEYNSNAM,KEYNGNAM:
                               KEYNADD1,KEYNADD2,KEYNADDX,KEYNADD3,KEYNADD4:
                               KEYNPOST,ACTNCNTY,KEYNTELP,KEYNTELB,PNKRELP:
                               KEYESNAM,KEYEGNAM:
                               KEYXECA1,KEYXECA2,KEYXECAX,KEYXECA3,KEYXECA4:
                               KEYXECPC,ACTECNTY,KEYXECPP,KEYXECBP,PTMXECRE:
                               NAMEFLAG
          GOTO      GACTD950 IF OVER
.
          MOVE      KEYPSNAM,PSNAME
          MOVE      KEYPGNAM,PGNAME
          PACK      PADD1,KEYPADD1,SP70     * set master file variables
          PACK      PADD2,KEYPADD2,SP70
          PACK      PSUBRB,KEYPADD3,SP70
          PACK      PADD4,KEYPADD4,SP70
          PACK      PPOST,KEYPPOST,SP70
          PACK      PTMXSLAC,KEYSLAC,SP70
          PACK      PTELEP,KEYTELEP,SP70
          PACK      PTELEB,KEYTELEB,SP70
          MOVE      KEYPDFL,PCEASE
.
          MOVE      SP20,PNKNAME
          MATCH     SP30,KEYNSNAM                * NoK surname present ?
          IF        !@EQUAL
            MOVE      KEYNSNAM,PNKNAME
            MATCH     SP30,KEYNGNAM              * Given name present ?
            IF        !@EQUAL
              STRIP     PNKNAME
              ENDSET    PNKNAME
              APPEND    SP1,PNKNAME
              APPEND    KEYNGNAM,PNKNAME
              RESET     PNKNAME
            ENDIF
            PACK      PNKNAME,PNKNAME,SP20
          ENDIF
.
          PACK      PNKADD1,KEYNADD1,SP70   
          PACK      PNKADD2,KEYNADD2,SP70
          PACK      PNKSUBR,KEYNADD3,SP70
          PACK      PNKADD4,KEYNADD4,SP70
          PACK      PNKPOST,KEYNPOST,SP70
          PACK      PNKTELP,KEYNTELP,SP70
          PACK      PNKTELB,KEYNTELB,SP70
.
          MOVE      SP20,PTMXECNM
          MATCH     SP30,KEYESNAM                * NoK surname present ?
          IF        !@EQUAL
            MOVE      KEYESNAM,PTMXECNM          * yes
            MATCH     SP30,KEYEGNAM              * Given name present ?
            IF        !@EQUAL
              STRIP     PTMXECNM
              ENDSET    PTMXECNM
              APPEND    SP1,PTMXECNM
              APPEND    KEYEGNAM,PTMXECNM
              RESET     PTMXECNM
            ENDIF
            PACK      PTMXECNM,PTMXECNM,SP20
          ENDIF
.
          PACK      PTMXECA1,KEYXECA1,SP70
          PACK      PTMXECA2,KEYXECA2,SP70
          PACK      PTMXECA3,KEYXECA3,SP70
          PACK      PTMXECA4,KEYXECA4,SP70
          PACK      PTMXECPC,KEYXECPC,SP70
          PACK      PTMXECPP,KEYXECPP,SP70
          PACK      PTMXECBP,KEYXECBP,SP70
.
          STORE     RESICOD,PNSWRACE,PUSR1,PUSR2,PUSR3,PUSR4,PUSR5:
                                     PUSR6,AUSR1,AUSR2,AUSR3,AUSR4:
                                     AUSR5,AUSR6,AUSR7,PMVXABRG
.
          STORE     LANGCOD,CNSWLNG,PUSR1,PUSR2,PUSR3,AUSR1,AUSR2:
                                    PUSR4,PUSR5,PUSR6
.
GACTD900  MOVE      ZERO,EXIT
          GOTO      GACTD999
.
GACTD950  MOVE      ONE,EXIT
GACTD999  GOTO      ENDGACTD
.
          INC       IBAOVRIO/INC
.
ENDGACTD  ENDPROC
+
.******************************************************************************
.*                                 GACTN000               Called by: ACTSRC00 *
.*                                Get ACT No                                  *
.******************************************************************************
.
          DEFPROC   GACTN000
.
ACTFNM    DIM       8
ACTTMP    FILE      ASCII, FIXED=256
.
. ----- Open the files & initialise variables -----
.
GACTN000  CLEAR     ACTFNM
          APPEND    "curpmi",ACTFNM
          APPEND    PORT,ACTFNM
          RESET     ACTFNM
          REP       " 0",ACTFNM
          RESET     ACTFNM
.
. ----- Get the ACT no & validate it -----
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      ACTTMP,ACTFNM
          TRAPCLR   IO
          BRANCH    OVRCD,GACTN950
.
          READ      ACTTMP,SEQ;ACTLOCAL;
          GOTO      GACTN950 IF OVER
.
          MATCH     EXITCHAR,ACTLOCAL
          GOTO      GACTN970 IF EQUAL       * Exitchar
.
.>>>>     TYPE      ACTLOCAL
.>>>>     GOTO      GACTN950 IF NOT EQUAL   * Not a numeric ACT no
.
          MOVE      ACTLOCAL,ACTURNO
.
. ----- Get the local U/R no & validate it -----
.
          READ      ACTTMP,SEQ;ACTLOCAL;
          GOTO      GACTN950 IF OVER
.
.>>>>     TYPE      ACTLOCAL
.>>>>     GOTO      GACTN950 IF NOT EQUAL   * Not a numeric local U/R no
.
          MATCH     ZEROUR,ACTLOCAL
          GOTO      GACTN800 IF EQUAL       * new patient
.
          PACK      KEY8,ACTLOCAL
          CALL      RDAMAST1                * Read a master file record
          BRANCH    OVRCD,GACTN950
.
. ----- Setup all the different U/R no's -----
.
GACTN800  MOVE      ACTLOCAL,PURNO
          MOVE      PURNO,CPPURNO
          PACK      C12URNO,SP4,CPPURNO
          PACK      C17URNO,SP9,CPPURNO
          PACK      CMPURNO,SP10,SP2,CPPURNO
.
          PACK      NMPNUMB,SP10,SP2,ACTURNO
.
GACTN900  MOVE      ZERO,EXIT
          GOTO      GACTN999
.
GACTN950  MOVE      ONE,EXIT                * error
          GOTO      GACTN999
.
GACTN970  MOVE      TWO,EXIT                * exitchar
GACTN999  GOTO      ENDGACTN
.
          INC       IBAOVRIO/INC
.
ENDGACTN  ENDPROC
+
.******************************************************************************
.*                                 ACT04000               Called by: Other    *
.*                     Get ACT No (saved from ACT Health)            Programs *
.******************************************************************************
.
          DEFPROC   ACT04000
.
ACTFNM    DIM       8
ACTTMP    FILE      ASCII, FIXED=256
.
. ----- Open the files & initialise variables -----
.
ACT04000  CLEAR     ACTFNM
          APPEND    "act04a",ACTFNM
          APPEND    PORT,ACTFNM
          RESET     ACTFNM
          REP       " 0",ACTFNM
          RESET     ACTFNM
.
. ----- Get the ACT no & validate it -----
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      ACTTMP,ACTFNM
          TRAPCLR   IO
          BRANCH    OVRCD,ACT04950
.
          READ      ACTTMP,SEQ;ACTLOCAL;
          GOTO      ACT04950 IF OVER
.
.>>>>     TYPE      ACTLOCAL
.>>>>     GOTO      ACT04950 IF NOT EQUAL   * Not a numeric ACT no
.
          MOVE      ACTLOCAL,ACTURNO
.
ACT04900  MOVE      ZERO,EXIT
          GOTO      ACT04999
.
ACT04950  MOVE      ONE,EXIT
ACT04999  CLOSE     ACTTMP,DELETE
          GOTO      ENDACT04
.
          INC       IBAOVRIO/INC
.
ENDACT04  ENDPROC
+
. *****************************************************************************
. * ACTSUR00   : Update files for a change in surname/given name              *
. *                                                                           *
. *              NOTE: This include is a copy of UPDSUR (PATCOMN2) which has  *
. *                    been set up specifically for the ACT Health Interface. *
. * Requires:  NAMEFLAG   0 = no change to names                              *
. *                       1 = keep old name as alias                          *
. *                       2 = don't keep old name as alias                    *
. *****************************************************************************
.
          DEFPROC   ACTSUR00
.
ACTSRC    DIM       8
ACTTMS    FILE      ASCII, FIXED=256
.
. Save variables for actsrc file
.
.
SAVSNAM   DIM       30
SAVGNAM   DIM       30
.
ACTSUR00  COMPARE   ZERO,NAMEFLAG                * names changed ?
          GOTO      ENDSUR99 IF EQUAL            * no - finished
.
          CLEAR     ACTSRC
          APPEND    "actsrc",ACTSRC
          APPEND    PORT,ACTSRC
          RESET     ACTSRC
          REP       " 0",ACTSRC
          RESET     ACTSRC
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      ACTTMS,ACTSRC
          TRAPCLR   IO
          BRANCH    OVRCD,ACTSUR90
.
          READ      ACTTMS,SEQ;ACTLOCAL;
          GOTO      ACTSUR90 IF OVER
.
          MATCH     ACTURNO,ACTLOCAL
          GOTO      ACTSUR90 IF NOT EQUAL   * Different ACT no
.
          READ      ACTTMS,SEQ;ACTLOCAL;
          GOTO      ACTSUR90 IF OVER
.
          MATCH     PURNO,ACTLOCAL
          GOTO      ACTSUR90 IF NOT EQUAL   * Different local U/R no
.
          READ      ACTTMS,SEQ;KEY4,SAVSNAM,SAVGNAM;
          GOTO      ACTSUR90 IF OVER
.
.         Keeping the old name as an alias
.
          PACK      GSRSNAM,SAVSNAM,SP70   * old surname
          PACK      FULLGNAM,SAVGNAM,SP70,SP70   * old given name
          CALL      SEPRNAME
          MOVE      FRSTGNAM,GSRGNAM
          MOVE      SCNDGNAM,PTGSGNM2
          MOVE      PURNO,GSRURNO
          MOVE      ZERO,GSRBILL
          CALL      SOUNDEX                * Calculate old sound-ex key
          CALL      SOUNDX2
.
          PACK      CPOLD115,GSRURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                             GSRSEX
.
          COMPARE   TWO,NAMEFLAG            * keeping old name as alias ?
          GOTO      ACTSUR50 IF EQUAL       * no
.
          MOVE      ONE,FORM1               * set alias status = Add (datagate)
          MOVE      CPOLD115,KEY115         * load key fields for KEY115
          MOVE      ONE,HL7TRGID
          MOVE      "ACTCOMN1",HL7INCLD
          PROC      DGCLICUA                * sent details to DG      *C-090203
.
          MOVE      PTMASNAM,GSRSNAM        * New surname
          MOVE      PMPXFNAM,GSRGNAM        * New first given name
          MOVE      PMPXSNAM,PTGSGNM2       * New second given name
          CALL      SOUNDEX                 * Calculate new sound-ex key
          CALL      SOUNDX2
.
.         Check if new record already exists
.
          OPEN      PATGSRN1,"patgsrnf"
          PACK      KEY115,GSRURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                           GSRSEX
          CALL      RAPTGSR1            * Check for new record
          IF        OVRCD = 1
            CALL      WRPTGSR1          * Write new surname index record
          ENDIF
          GOTO      ENDSUR99            * Finished.
.
.         Not keeping the old name as an alias
.
ACTSUR50  MOVE      PTMASNAM,GSRSNAM    * New surname
          MOVE      PMPXFNAM,GSRGNAM    * New first given name
          MOVE      PMPXSNAM,PTGSGNM2   * New second given name
          CALL      SOUNDEX             * Calculate new sound-ex key
          CALL      SOUNDX2             * calculate given name soundex key
.
.         Check if new record already exists
.
          OPEN      PATGSRN1,"patgsrnf"
          PACK      KEY115,GSRURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                           GSRSEX
          CALL      RAPTGSR1            * Check for new record
          IF        OVRCD = 0
.
.           New record exists, delete old record
.
            MOVE      CPOLD115,KEY115     * Use the old saved key
            CALL      DEPTGSR1            * Delete the old record
.
            GOTO      ENDSUR99            * Finished
          ENDIF
.
.         Update the old record with the new name
.
          OPEN      PATGSRN1,"patgsrnf"
          MOVE      CPOLD115,KEY115     * Get ready to go to old record
          CALL      RAPTGSR1            * Reposition on old record
          IF        OVRCD = 0
            CALL      UPPTGSR1          * Update with the new data
            GOTO      ENDSUR99          * Finished
          ENDIF
.
          OPEN      PATGSRN1,"patgsrnf"
          PACK      KEY115,GSRURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                           GSRSEX
          CALL      RAPTGSR1            * Reposition on old record
          IF        OVRCD = 1
            CALL      WRPTGSR1          * Write the new record
          ELSE
            CALL      UPPTGSR1          * update old record
          ENDIF
.
ACTSUR90  DISPLAY   *P1:24,*EL,*B,"Error reading ACT Health details.  ";
          CALL      HITENTER
          GOTO      ENDSUR99
.
          INC       IBAOVRIO/INC
.
ENDSUR99  ENDPROC
+
.*****************************************************************************
.*                         GETER000                                          *
.*                  See if an error message was returned from ACT04          *
.* Returns:  EXIT 0 = no error returned                                      *
.*                1 = error returned                                         *
.*****************************************************************************
.
          DEFPROC   GETER000
.
ACTERR    DIM       8
ACTTME    FILE      ASCII, FIXED=256
.
ERRMESSG  DIM       80
.
GETER000  CLEAR     ACTERR
          APPEND    "acterr",ACTERR
          APPEND    PORT,ACTERR
          RESET     ACTERR
          REP       " 0",ACTERR
          RESET     ACTERR
.
.         See if an error file exists
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      ACTTME,ACTERR                * file found ?
          TRAPCLR   IO
          BRANCH    OVRCD,GETER900               * no - no errors
.
.         An error file has been found, so read the error message
.
          READ      ACTTME,SEQ;ERRMESSG
          GOTO      GETER950
.
GETER900  MOVE      ZERO,EXIT
          GOTO      GETER999
.
GETER950  CLOSE     ACTTME,DELETE                * remove temp file
          MOVE      ONE,EXIT                     * set error flag
          GOTO      GETER999
.
GETER999  GOTO      ENDACERR
.
          INC       IBAOVRIO/INC
.
ENDACERR  ENDPROC
+
