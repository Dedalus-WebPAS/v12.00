.***************************************************************************
.* System    :   Waiting List                                              *
.* Program   :   FXWATESE.PBL                                              *
.* Desc      :   Program to update wateseaf.wteeremd and wateseaf.wteerrem *
.*           :   from oprdetaf.opdadate and category WR ind3=W code        *
.***************************************************************************
.* Date      :   02/06/2006                                                *
.* Author    :   Peter Vela                                                *
.* Function  :   This program will loop thru the oprdetaf files and        *
.*           :   update wateseaf wteeremd (rem date) and wteerrem (rem rea)* 
.* Mods      :                                                             *
.*           :   V9.06.01 02/06/2006 Peter Vela CAR 107342                 *
.*           :            Created program                                  *
.*=========================================================================*
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       OPRDEAFD/INC   * Operation detail file
          INC       BOKRC1FD/INC   * Booking file
          INC       WATTR1FD/INC   * Waiting List File
          INC       WATESEFD/INC   * Waiting List Episode File
          INC       PATCODFD/INC   * Category/Codes File
.
CMDLINE   DIM       50
DIM10     DIM       10
DIM10A    DIM       10
DIM10B    DIM       10
RECNUM    FORM      6
PRNTFLAG  FORM      1
TMPFILNM  DIM       8
.
TEMPFILE  IFILE     SQL,FIXED=18, KEY="UC1-9,10-17"
.
.Name     Type   Length   Physical   Start   Description
.----     ----   ------   --------   -----   -----------
TMPUNIQ   DIM       9        9         1     Unique Key
TMPEDAT   DIM       8        8         10    Extract sent date (CCYYMMDD) Date
.
. End of Record                        18
.
PIPE      INIT      "|"
STRTDATE  INIT      "20050701"
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
.----------------------------------------------------------------------
PRGID     INIT      "FXWATESE"
PRGNAM    INIT      "Fix wteeremd and wteerrem in wateseaf"
VERSION   INIT      "V11.03.00"
.----------------------------------------------------------------------
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      PRFL0000               * Set Print/Update Flag
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD         * display heading
          OPEN      OPRDETA1,"oprdetaf"
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATESEA1,"wateseaf"
          OPEN      PATCODE1,"patcodes"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
               *P1:5,*V2LON,ONE,*HOFF,". Fix wteeremd and wteerrem in wateseaf."
.
          DISPLAY   *P1:20,"REMEMBER TO MAKE SAVE COPIES OF THE wateseaf DATABASE FILES."
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Fixing ..."
.
.          CALL      CTMF0000
.
          CALL      IBACLOCK
          CALL      HEAD0000                     * print 1st page header
.
          MOVE      ZERO,RECNUM
          PACK      KEY23,STRTDATE,SP70
          CALL      RSOPDEA1
PROC1000  CALL      RKOPDEA1
          BRANCH    OVRCD,PROC9999
.
          COMPARE   SIXTY,CLNO                   * page full ?
          CALL      HEAD0000 IF NOT LESS         * yes
.
          COMPARE   THREE,OPDASTAT
          GOTO      PROC1000 IF EQUAL
.
          PACK      KEY8,OPDAADMN,SP70
          CALL      RDBKREC1
          BRANCH    OVRCD,PROC1000
.
          PACK      KEY19,BKREURNO,BKREPROC,BKRECNT,SP70
          CALL      RDTREA1
          BRANCH    OVRCD,PROC1000
.
          COMPARE   FIVE,WMSTAT
          GOTO      PROC1000 IF NOT EQUAL
.
          PACK     KEY17,WTWMECNT,SP70
          CALL     RDWTESE1
          IF       OVRCD=0
            GOTO     PROC2000
          ENDIF
.
          PACK     KEY17,WTWMECNT,Z70
          CALL     RSWTESE1
          CALL     RPWTESE1
          BRANCH   OVRCD,PROC1000
.
          MATCH    WTEEUNIQ,WTWMECNT
          GOTO     PROC1000 IF NOT EQUAL
.
PROC2000  MATCH    SP70,WTEEADAT
          GOTO     PROC1000 IF EQUAL
.
          MATCH    SP70,WTEEREMD
          IF       @EQUAL
            MOVE     OPDADATE,WTEEREMD
          ELSE
            GOTO     PROC1000
          ENDIF
.
          PACK     KEY5,ANSW,ANSR,SP70
          CALL     RDSCODE1
PROC3000  CALL     RDKCODE1
          BRANCH   OVRCD,PROC4000
.
          MATCH    "WR",TCODE
          GOTO     PROC4000 IF NOT EQUAL
.
          MATCH    ANSW,TCINDC3
          GOTO     PROC3000 IF NOT EQUAL
.
          MOVE     ACODE,WTEERREM
.
PROC4000  UNPACK   WTEEEDAT,CCENT,CYEAR,CMON,CDAY
          PACK     DIM10,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP      " 0",DIM10
.
          UNPACK   WTEEREMD,CCENT,CYEAR,CMON,CDAY
          PACK     DIM10A,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP      " 0",DIM10A
.
          UNPACK   WTEEADAT,CCENT,CYEAR,CMON,CDAY
          PACK     DIM10B,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP      " 0",DIM10B
.
          PRINT    *1,PIPE,*3,WTEEUNIQ,*12,PIPE,*24,DIM10,*45,PIPE:
                   *47,WTEEURNO,*60,PIPE,*70,DIM10B,*85,PIPE:
                   *90,DIM10A,*105,PIPE,*107,TDESC,*132,PIPE
          ADD      ONE,CLNO   
.
          IF       PRNTFLAG=1
            CALL     UPWTESE1 
          ENDIF
.
          ADD      ONE,RECNUM
.
          IF       PRNTFLAG=0
            DISPLAY  *P1:23,"Records Printed: ",*P20:23,*V2LON,RECNUM
          ELSE
            DISPLAY  *P1:23,"Records Updated: ",*P20:23,*V2LON,RECNUM
          ENDIF
.
          GOTO     PROC1000
.
PROC9999  CALL     LINE0000
.
          IF       PRNTFLAG=0
            PRINT    *1,"Records Printed: ",RECNUM
          ELSE
            PRINT    *1,"Records Updated: ",RECNUM
          ENDIF
.
.          CALL     KILL0000
.
          CALL     HITENTER
          RETURN
+
.****************************************************************************
.*                            HEAD0000                 Called by:           *
.*                       print page heading                                 *
.****************************************************************************
.
HEAD0000  CALL      HEAD132                      * display page header
.
          IF        PRNTFLAG=0
            PRINT     *40,"wateseaf records printed",*N
          ELSE
            PRINT     *40,"wateseaf records updated",*N
          ENDIF
.
          CALL      LINE0000                     * draw line across page
.
          PRINT    *1,PIPE,*3,"Unique No",*12,PIPE,*24,"Sent Date",*45,PIPE:
                   *47,"UR Number",*60,PIPE,*66,"Admission Date",*85,PIPE:
                   *90,"Removal Date",*105,PIPE,*107,"Removal Reason",*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
          MOVE      EIGHT,CLNO                   * increment line count
.
HEAD9999  RETURN
+
.****************************************************************************
.*                            LINE0000                 Called by:           *
.*                      draw line across page                               *
.****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------":
                    "-----------------------------------*"
.
LINE9999  RETURN
+
.****************************************************************************
.*                                PRFL0000             Called by: ML0000    *
.*                        get user print flag option                        *
.*                                                                          *
.*    Returns:  PRNTFLAG = FALSE (0)  Print only                            *
.*                         TRUE  (1)  Print and Update wateseaf             *
.****************************************************************************
.
PRFL0000  DISPLAY   *P1:10,*P1:11,*V2LON,ZERO,*HOFF,". Print Report Only":
               *P1:12,*V2LON,ONE,*HOFF,". Print Report and Update wateseaf"
.
PRFL0500  KEYIN     *P1:14,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:14,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      PRFL9500 IF EQUAL            * yes
.
          BRANCH    COPTION,PRFL9000             * run clean-up
.
          BEEP
          GOTO      PRFL0500
.
PRFL9000  MOVE      ONE,PRNTFLAG
          GOTO      PRFL9999
.
PRFL9500  MOVE      ZERO,PRNTFLAG
.
PRFL9999  RETURN
+
.*************************************************************************
.*                             CTMF0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
CTMF0000  CALL      KILL0000
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    TMPFILNM,CMDLINE
          APPEND    " 18 UC1-9,10-17",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFILE,TMPFILNM
.
CTMF9999  RETURN
+
.*************************************************************************
.*                             KILL0000             Called by:           *
.*                   erase temporary file                                *
.*************************************************************************
KILL0000  CLOSE     TEMPFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    TMPFILNM,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
KILL9999  RETURN
+
.*************************************************************************
.*                             CLER0000            Called by:            *
.*               close and erase the temporary file                      *
.*************************************************************************
.
CLER0000  PACK      KEY17,SP70
          CALL      RSTMPF1
          CALL      RKTMPF1
          BRANCH    OVRCD,CLER9999
.
          PACK      KEY17,TMPUNIQ,TMPEDAT,SP70
          CALL      DETMPF1
.
          GOTO      CLER0000
.
CLER9999  RETURN
.
.==============================================================================
.                Tempfile I/O
.==============================================================================
RSTMPF1   RESET     KEY17
          READ      TEMPFILE,KEY17;;
          RETURN
.
RKTMPF1   MOVE      ZERO,OVRCD
          READKS    TEMPFILE;TMPUNIQ,TMPEDAT
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTMPF1   MOVE      ZERO,OVRCD
          READKP    TEMPFILE;TMPUNIQ,TMPEDAT
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTMPF1   MOVE      ZERO,OVRCD
          RESET     KEY17
          READ      TEMPFILE,KEY17;TMPUNIQ,TMPEDAT
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTMPF1   RESET     KEY17
          WRITE     TEMPFILE,KEY17;TMPUNIQ,TMPEDAT
          RETURN
.
DETMPF1   RESET     KEY17
          DELETE    TEMPFILE,KEY17
          RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       OPRDEAIO/INC   * Operation detail file
          INC       BOKRC1IO/INC
          INC       WATTR1IO/INC
          INC       WATESEIO/INC
          INC       PATCODIO/INC

          INC       STD001IO
.
