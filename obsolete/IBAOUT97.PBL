.******************************************************************************
.*                                                                            *
.*              P R O G R A M  S U M M A R Y                                  *
.*              -------------  -------------                                  *
.*                                                                            *
.*     PROGRAM NAME:     IBAOUT97                                             *
.*                                                                            *
.*     FUNCTION:         EMERGENCY FLAG CLEAR                                 *
.*                                                                            *
.*     DATE WRITTEN:     25/07/88                                             *
.*                                                                            *
.*     AUTHOR :          N.SPIJKMAN                                           *
.*                                                                            *
.*     MODIFICATIONS:                                                         *
.*        V10.07.01 30/10/2015  Ebon Clements CAR 268970                      *
.*                  Change to use OUTCLIFD index 1 instead of index 2         *
.*        V9.09.02  04/03/2008  Peter Vela    CAR 162914                      *
.*                  Recompiled for KYOUTCLI - readk of outcliaf               *
.*        V9.09.01  28/07/2007  Jill Habasque CAR 103645                      *
.*                  Recompiled for KYOUTCLI - read of pmshcpaf                *
.*        V9.03.01  20/11/2003  Steve Armstrong CAR 44631                     *
.*                  Removed PI command                                        *
.******************************************************************************
.*        V9.02.00  28/11/2002  Lina Vo                                       *
.*                  Removed site specific open of outsitaf and opened outcliaf*
.*                  with site "out" and suffix FILCLIA2 in INIT0000.          *
.*        V5.09.03  04/09/2001  Steve Downing  SRF 12290                      *
.*                  Fixed display of Site & Clinic ID/Type keyins             *
.*        V5.09.02  16/08/2001  Steve Downing  SRF 11803                      *
.*                  Mods to re-open XXXcliaf key #2 after changing site prefix*
.*        V5.09.01  15/08/2001  Steve Downing  SRF 11803                      *
.*                  Fixed validation of clinic ID & clinic Type when using    *
.*                  different site code to that entered in outpatient system  *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*        V5.08.B01 16/03/2000  Steve Armstrong    5.08 Mods                  *
.*                  Mods for changes to OUTCLIFD (effective date)             *
.******************************************************************************
. *                       V5.01 03.06.89 S.Barcham         *
. *                             Split OUTBOK & OUTMAS incl *
. *                             SRF 100751                 *
. *                       V5.02 05.06.89 S.Barcham         *
. *                             Recompiled for locking in  *
. *                             PATIOMAS         SRF 100751*
. *                       V5.03 24.06.89 S.Barcham         *
. *                             Recompiled for OUTBB1FD    *
. *                       V5.04 30/06/89 DIG               *
. *                             Re-Compiled for changes to *
. *                             OUTSITFD                   *
. *                       V5.05 10/07/89 E.Phan            *
. *                             Open site dependent files  *
. *                             AFTER site id entered.     *
. *                             Added unlocking of outbb1af*
. *                             and patma1af.              *
. *                    V5.01.01 02/08/89 E.Phan  SRF101587 *
. *                             More desc for day indicator*
. *                             07/08/89 E.Phan            *
. *                             Error mess more meaningful *
. *                    V5.01.02 03/10/89 L.P.    SRF102514 *
. *                             Recompiled with new        *
. *                             OUTAUXFD.INC               *
. *                    V5.01.03 05/10/89 K.Peak  SRF102601 *
. *                             Replaced uppercase audit   *
. *                             with lowercase             *
. *                    V5.01.04 16/02/90 J.Tan  SRF 103832 *
. *                             Fix OUTIOBOB,Pos.of Port No*
. *                    V5.01.05 11/07/90 Paul Duncan       *
. *                             recomp for OUTBOA & OUTBOB *
. *               V5.01.121 04/10/91 Steve O'Gorman        *
. *                         Recompiled for Change OUTMA1FD *
. *               V5.01.122 02/03/93  ROD                  *
. *                         Recompiled for Change OUTMA1FD *
. *               V5.03.001 07/09/95 Howellsy  SRF 640704  *
. *                         Don't say you've cleared the   *
. *                         slot when you haven't.         *
. *                                                        *
. **********************************************************
.
          INC       STD001FD
.
          INC       PATCONFD/INC      * control file
          INC       PATMA1FD/INC      * patient master file

          INC       PATDO1FD/INC      * doctor file
          INC       PMSHCPFD/INC            * National HCP file
          INC       PATCODFD/INC
.
          INC       OUTCLIFD/INC      * clinic ID file
          INC       OUTCTYFD/INC      * clinic type file
          INC       OUTSITFD/INC      * site code file
          INC       OUTCONFD/INC      * control file
          INC       OUTMA1FD/INC      * clinic master file
          INC       OUTMABFD/INC      * clinic master file
          INC       OUTATTFD/INC      * attendance file
          INC       OUTBOAFD/INC      * clinic booking file
          INC       OUTBB1FD/INC      * clinic booking file
          INC       OUTAUXFD/INC      * audit file
.
.         Extra variables
.
.
CLRFLAG   FORM      1          * slot cleared?
SAVIDDD   DIM       6          * save variable for IDDD
SITE      DIM       6          * site keyed in
DESCSIT   DIM       30         * site description
COL       FORM      2
VERT      FORM      2
.
DESCID    DIM       30         * description clinic ID
CLID      DIM       6          * clinic ID keyin
.
MONDAT    DIM       6          * ccyymm
DATE      DIM       8          * ccyymmdd
TIME      DIM       5          * hh:mm
.
GRUP      DIM       3          * groupcode keyin
CODECG    INIT      "CG"
DESCGRUP  DIM       20         * group description
.
CTYP      DIM       6          * clinictype keyin
DESCTYP   DIM       30         * clinic type description
.
DSLOT     FORM      3          * slot number
SLOT      FORM      3          * slot number keyin
.
DIM14     DIM       14
DIM40     DIM       40
DIND      FORM      1          * day indicator keyin
DDIND     FORM      1          * day indicator
.
DESCREF   DIM       20         * referral code description
REF       DIM       3          * referral code keyin
ROW       FORM      2
CODECF    INIT      "CF"
.
DESC      DIM       20
DESC1     DIM       20
DESC2     DIM       20
DESC3     DIM       20
.
FILETYP   DIM       6
.
INPFILE   FILE      ASCII, FIXED=256
.
CODE      DIM       2
.
MONTH     DIM       6     ccyymm
.
OUT       INIT      "out"
PRGNAM    INIT      "Emergency Flag Clear"
PRGID     INIT      "IBAOUT97"
VERSION   INIT      "V10.13.00"
+
.-----------------------------------------------------------------------------
.         Start of program
.-----------------------------------------------------------------------------
.
          CALL      INIT0000                 * Open general files
.
          CALL      DISPHEAD
.
START     HLINE     *G37,2,50,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Outpatient Files":
                    *P1:6,*V2LON,TWO,*HOFF," = Audit Files"
.
REKSTRT0  KEYIN     *P1:8,*EL,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      ENDIT IF EQUAL
.
          BRANCH    OPTION,START1,START2
          BEEP
          GOTO      REKSTRT0
.
.------------------------------------------------------------------------------
.
.         Normal outpatient files
.
.------------------------------------------------------------------------------
.
START1    MOVE      "Booking           ",DESC1
          MOVE      "Master            ",DESC2
.
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = ",DESC1:
                    *P2:6,*V2LON,TWO,*HOFF," = ",DESC2
.
REKST1    KEYIN     *P1:8,*EL,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      START IF EQUAL
.
REPEAT    MOVE      TEN,CVERT                    * Starting position
          DISPLAY   *P1:CVERT,*EF;
.
          CALL      INIT2000                 * Close site dependent files
.
          LOAD      DESC,OPTION,DESC1,DESC2
.
          BRANCH    OPTION,KBOK0000,KMAS
.
          BEEP
          GOTO      REKST1
.
.------------------------------------------------------------------------------
.         Error messages
.------------------------------------------------------------------------------
.
INVDATA   ADD       TWO,CVERT
          DISPLAY   *P1:24,*EL,*B:
                    DESC,"does not Exist. ";
          CALL      HITENTER
          GOTO      REPEAT
.
NOTBUSY   ADD       TWO,CVERT
          DISPLAY   *P1:24,*EL,*B:
                    "Master Record is not Busy, NOT Cleared. ";
          CALL      HITENTER
          GOTO      REPEAT
.
.------------------------------------------------------------------------------
.         Booking
.------------------------------------------------------------------------------
.
KBOK0000  DISPLAY   *P50:2,*V2LON,"- ",DESC,"File "
.
          CALL      KSITE                    * site code
          BRANCH    OVRCD OF START1
.
          CALL      INIT1000                 * Open site dependent files
.
          ADD       ONE,CVERT
          CALL      KCLID                    * clinic ID
          BRANCH    OVRCD OF START1
.
          ADD       ONE,CVERT
          CALL      KDATE                    * date (ccyymmdd)
          BRANCH    OVRCD OF START1
.
          ADD       ONE,CVERT
          CALL      KTIME                    * start time (hh:mm)
          BRANCH    OVRCD OF START1
.
          ADD       ONE,CVERT
          CALL      KSLOT                    * slot number
          BRANCH    OVRCD OF START1
.
. ------- Unlock booking record -------
.
          MOVE      ZERO,CLRFLAG    
          PACK      KEY28,SITE,CLID,DATE,TIME,SLOT
          CALL      RDBOKA1
          BRANCH    OVRCD,INVDATA
.
          MATCH     SP2,OBALOCK
          GOTO      KBOK0500 IF EQUAL
.
          MOVE      ONE,CLRFLAG
          CALL      ULBOKA1
.
. ------- Unlock booking extension record -------
.
KBOK0500  PACK      KEY8,OBAOUTNO
          CALL      RDBOKB1
          BRANCH    OVRCD,KBOK1000
.
          MATCH     SP2,OBBPORT
          GOTO      KBOK1000 IF EQUAL
.
          MOVE      ONE,CLRFLAG
          CALL      UUOTBOB1
.
. ------- Unlock booking extension record -------
.
KBOK1000  MOVE      OBAURNO,PURNO
          PACK      KEY8,PURNO
          CALL      RDMAST1
          BRANCH    OVRCD,KBOK9000
.
          MATCH     SP2,PPORT  
          GOTO      KBOK9000 IF EQUAL
.
          MOVE      ONE,CLRFLAG
          CALL      UUPTMAS1
.
. ------- Finished -------
.
KBOK9000  IF        CLRFLAG = 1  
            DISPLAY   *P1:24,*EL,*B:
                      "Slot Cleared. ";
            CALL      HITENTER
          ELSE
            DISPLAY   *P1:24,*EL,*B:
                      "Slot is not Busy, NOT Cleared. ";
            CALL      HITENTER
          ENDIF
.
          GOTO      REPEAT
.    
+
.*****************************************************************************
.
.         Master
.
..............................................................................
.
KMAS      DISPLAY   *P50:2,*V2LON,"- ",DESC,"File "
.
          CALL      KSITE                              * site code
          BRANCH    OVRCD OF START1
.
          CALL      INIT3000                 * Open site dependent files
.
          ADD       ONE,CVERT
          CALL      KCTYP                              * clinic type
          BRANCH    OVRCD OF START1
.
          ADD       ONE,CVERT
          CALL      KCLID                              * clinic ID
          BRANCH    OVRCD OF START1
.
          ADD       ONE,CVERT
          CALL      KTIME                              * start time (hh:mm)
          BRANCH    OVRCD OF START1
.
          ADD       ONE,CVERT
          CALL      KDIND                              * day indicator
          BRANCH    OVRCD OF START1
.
          PACK      KEY24,SITE,CTYP,CLID,DIND,TIME
          CALL      RDMASA2
          BRANCH    OVRCD OF INVDATA
.
          MATCH     SP2,OMALOCK
          GOTO      NOTBUSY IF EQUAL
.
          PACK      KEY18,SITE,CLID,DIND,TIME
          CALL      UUOTMAS1
          DISPLAY   *P1:24,*EL,*B:
                    "Slot Cleared. ";
          CALL      HITENTER
.
          GOTO      REPEAT
.
.****************************************************************
.       All the keyins are based on CVERT and are called
.****************************************************************
.
.       Input clinic ID
.
KCLID     MOVE      SP8,CLID
          DISPLAY   *P1:CVERT,*EL," Enter Clinic ID Code",*P24:CVERT,COLON
.
          MOVE      CVERT,EVERT
          MOVE      TWENTY6,ECOL
          MOVE      ZERO,CKYIMAND
          MOVE      IDDD,SAVIDDD
          MOVE      SITE,IDDD
          CALL      KYOUTCLI
          MOVE      EVERT,CVERT
          MOVE      ECOL,CCOL
          MOVE      SAVIDDD,IDDD
          MOVE      OCACLIN,CLID
          ENDSET    CLID
          APPEND    SP8,CLID
          RESET     CLID
.
          MATCH     SP8,CLID
          GOTO      OVERCOND IF EQUAL
.
VALID     CMATCH    EXITCHAR,CLID
          GOTO      OVERCOND IF EQUAL
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY20,SITE,CLID,KEY8
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     SITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     CLID,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          BRANCH    OVRCD,INVID
.
          MATCH     SP6,OCADOCT
          GOTO      ID3 IF EQUAL
.
          MOVE      OCADOCT,KEY6
          CALL      RDDOCT1
          COMPARE   OVRCD,ONE
          GOTO      ID1 IF NOT EQUAL
          GOTO      ID2
.
ID1       MOVE      DGNAME,DIM1
          PACK      DESCID WITH DIM1,COMMA,DSNAME,SP10
          GOTO      ID2
.
ID3       PACK      DESCID WITH OCADESC
          GOTO      ID2
.
INVID     CLEAR     DESCID
          APPEND    "Invalid Clinic ID : ",DESCID
          APPEND    CLID,DESCID
          APPEND    SP10,DESCID
          RESET     DESCID
          DISPLAY   *P35:CVERT,*V2LON,DESCID,*W
          GOTO      KCLID
.
ID2       DISPLAY   *P35:CVERT,*V2LON,DESCID
          RETURN
.
.............................................................................
.        Input site code
.
KSITE     MOVE      TEN,CVERT
          MOVE      SP6,SITE
          DISPLAY   *P1:CVERT,*EL," Enter Site Code",*P24:CVERT,COLON
          KEYIN     *P26:CVERT,*DV,UNDLN6:
                    *P26:CVERT,*V2LON,SITE,*P26:CVERT,*DV,SITE
.
          ENDSET    SITE
          APPEND    SP6,SITE
          RESET     SITE
.
          MATCH     SP6,SITE
          GOTO      OVERCOND IF EQUAL
.
          CMATCH    QUEST,SITE
          GOTO      VALSIT IF NOT EQUAL
.
          DISPLAY   *PCOL:ROW,*EL
          MOVE      "1",HLEF
          MOVE      "3",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          MOVE      ONE TO CNEWDS
          CALL      OUTDSSIT                  * display the valid sites
          DISPLAY   *P1:24;
          CALL      HITENTER
          CALL      PUTSCR00
          GOTO      KSITE
.
VALSIT    CMATCH    EXITCHAR,CLID
          GOTO      OVERCOND IF EQUAL
.
          CLEAR     DESCSIT                          * get site description
          APPEND    "Invalid site code : ",DESCSIT
          APPEND    SITE,DESCSIT
          APPEND    SP20,DESCSIT
          RESET     DESCSIT
          MOVE      SITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD OF SITE1
          MOVE      OSTDESC,DESCSIT
          DISPLAY   *P35:CVERT,*V2LON,DESCSIT
          RETURN
.
SITE1     DISPLAY   *P35:CVERT,*V2LON,DESCSIT,*W
          GOTO      KSITE
.
.
..............................................................................
.       Input date  (ccyymmdd)
.
KDATE     MOVE      SP8,DATE
          DISPLAY   *P1:CVERT,*EL," Enter Date ",*P24:CVERT,COLON
.
          UNPACK    DATE INTO CCENT,CYEAR,CMON,CDAY
.
          MOVE      "26",CCOL
.
          MOVE      ZERO,CHIGHLT
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    OVRCD OF OVERCOND                        * USED TO BE BEGIN
.
          PACK      DATE WITH CCENT,CYEAR,CMON,CDAY
          REP       " 0",DATE
          RETURN
.
.
...............................................................................
.        Input time  (hh:mm)
.
KTIME     MOVE      SP5,TIME
          DISPLAY   *P1:CVERT,*EL," Enter Time ",*P24:CVERT,COLON
.
          UNPACK    TIME INTO CHOUR,ANS,CMIN
          MOVE      "26",CCOL
          CALL      KEYTIME
          BRANCH    OVRCD OF OVERCOND
.
          PACK      TIME WITH CHOUR,COLON,CMIN
          RETURN
.
.
...............................................................................
.         Input clinic type
.
KCTYP     MOVE      SP6,CTYP
          DISPLAY   *P1:CVERT,*EL," Enter Clinic type ",*P24:CVERT,COLON
.
          MOVE      CVERT,EVERT
          MOVE      TWENTY6,ECOL
          MOVE      ZERO,CKYIMAND
          MOVE      IDDD,SAVIDDD
          MOVE      SITE,IDDD
          CALL      KYOUTCTY
          MOVE      EVERT,CVERT
          MOVE      ECOL,CCOL
          MOVE      SAVIDDD,IDDD
          MOVE      OCTCTYP,CTYP
.
          ENDSET    CTYP
          APPEND    SP6,CTYP
          RESET     CTYP
.
          MATCH     SP6,CTYP
          GOTO      OVERCOND IF EQUAL
.
VALCTYP   CMATCH    EXITCHAR,CTYP
          GOTO      OVERCOND IF EQUAL
.
          CLEAR     DESCTYP                          * get clin type descript
          APPEND    "Invalid clinic type : ",DESCTYP
          APPEND    CTYP,DESCTYP
          APPEND    SP20,DESCTYP
          RESET     DESCTYP
          PACK      KEY12 WITH SITE,CTYP
          CALL      RDCTYA1
          BRANCH    OVRCD OF CTYP1
          MOVE      OCTDESC,DESCTYP
          DISPLAY   *P35:CVERT,*V2LON,DESCTYP
          RETURN
.
CTYP1     DISPLAY   *P35:CVERT,*V2LON,DESCTYP,*W
          GOTO      KCTYP
.
...............................................................................
.         Input slotnumber
.
KSLOT     MOVE      ZERO,SLOT
          DISPLAY   *P1:CVERT,*EL," Enter Slot number ",*P24:CVERT,COLON
          KEYIN     *P26:CVERT,*DV,UNDLN3:
                    *P26:CVERT,*V2LON,SLOT,*P26:CVERT,*DV,SLOT
.
          COMPARE   ZERO,SLOT
          GOTO      START1 IF EQUAL
.
          MOVE      SLOT,DSLOT
.
          PACK      KEY28 WITH SITE,CLID,DATE,TIME,DSLOT
          CALL      RDBOKA1
          BRANCH    OVRCD OF NOSLOT
          RETURN
.
NOSLOT    DISPLAY   *P35:CVERT,"Invalid slot : ",DSLOT,*W
          GOTO      KSLOT
.
. ------- Input day indicator -------
.
KDIND     MOVE      ZERO,DIND
          DISPLAY   *P1:CVERT,*EL," Enter Clinic Day    ",*P24:CVERT,COLON:
                    *P1:24,*EL,*V2LON,ONE,*HOFF,"=Monday ":
                               *V2LON,TWO,*HOFF,"=Tuesday ":
                               *V2LON,THREE,*HOFF,"=Wednesday ":
                               *V2LON,FOUR,*HOFF,"=Thursday ":
                               *V2LON,FIVE,*HOFF,"=Friday ":
                               *V2LON,SIX,*HOFF,"=Saturday ":
                               *V2LON,SEVEN,*HOFF,"=Sunday";
          KEYIN     *P26:CVERT,*DV,UNDLN1:
                    *P26:CVERT,*V2LON,DIND,*P26:CVERT,*DV,DIND
.
          COMPARE   ZERO,DIND
          GOTO      START1 IF EQUAL
          BRANCH    DIND,KDIND1,KDIND1,KDIND1,KDIND1,KDIND1,KDIND1,KDIND1
          BEEP
          GOTO      KDIND
.
KDIND1    MOVE      DIND,DDIND
.
. ------- Read record -------
.
          PACK      KEY24 WITH SITE,CTYP,CLID,DDIND,TIME
          CALL      RDMASA2
          BRANCH    OVRCD OF NOIND
          COMPARE   DDIND,SEVEN
          GOTO      NOIND IF LESS
          RETURN
.
NOIND     DISPLAY   *P35:CVERT,*V2LON,"*** Record not found *** ",*W
          GOTO      KDIND
.
...............................................................................
.         Input referral code  (CF)
.
KREF      MOVE      SP3,REF
          DISPLAY   *P1:CVERT,*EL," Enter Referral code ",*P24:CVERT,COLON
          KEYIN     *P26:CVERT,*DV,UNDLN3:
                    *P26:CVERT,*V2LON,REF,*P26:CVERT,*DV,REF
.
          ENDSET    REF
          APPEND    SP3 TO REF
          RESET     REF
.
          MOVE      SP20,DESCREF
          CLEAR     DESCREF
          APPEND    "Inval Ref Type : ",DESCREF
          APPEND    REF,DESCREF
          APPEND    SP5,DESCREF
          RESET     DESCREF
.
          PACK      KEY5 WITH CODECF,REF
          CALL      RDCODE1
          BRANCH    OVRCD OF NOREF
          MOVE      TDESC,DESCREF
          DISPLAY   *P35:CVERT,*V2LON,DESCREF
          RETURN
.
NOREF     DISPLAY   *P35:CVERT,*V2LON,"Invalid referral type : ",REF,*W
          GOTO      KREF
.
.
..............................................................................
.       Audit files
..............................................................................
.
START2    HLINE     *G37,2,50,80
          DISPLAY   *P1:3,*EF:
                    *P2:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P2:5,*V2LON,ONE,*HOFF," = Attendance Audit File":
                    *P2:6,*V2LON,TWO,*HOFF," = Booking Audit File":
                    *P2:7,*V2LON,THREE,*HOFF," = Master Audit File"
.
.
REKSTRT2  KEYIN     *P1:9,*EL,"Option : ",*V2LON,OPTION;
.
          COMPARE   ZERO TO OPTION
          GOTO      START IF EQUAL
.
          BRANCH    OPTION OF FILETYPA,FILETYPB,FILETYPM
.
          BEEP
          GOTO      REKSTRT2
.
.
.
FILETYPA  MOVE      ANSA,AUTYPE
          REP       "Aa",AUTYPE
          DISPLAY   *P50:2,*V2LON,"- Attendance Audit File "
          GOTO      KPORT
.
FILETYPB  MOVE      ANSB,AUTYPE
          REP       "Bb",AUTYPE
          DISPLAY   *P50:2,*V2LON,"- Booking Audit File "
          GOTO      KPORT
.
FILETYPM  MOVE      ANSM,AUTYPE
          REP       "Mm",AUTYPE
          DISPLAY   *P50:2,*V2LON,"- Master Audit File "
.
KPORT     MOVE      ZEROUR,PPORT
          KEYIN     *P1:4,*EF,"Terminal Port ## : ",*V2LON,PPORT
          MATCH     ZEROUR,PPORT
          GOTO      START2 IF EQUAL
.
          MATCH     SP2,PPORT
          GOTO      START IF EOS
.
          DISPLAY   *P56:24,*EL,"Opening :";
.
          MOVE      PPORT,AUPORT
          PACK      AUFILE WITH AUOUTAU,AUTYPE,AUPORT
          REP       " 0",AUFILE
.
          DISPLAY   *P64:24,*V2LON,AUFILE;
          TRAP      NOAUD IF IO
          OPEN      INPFILE,AUFILE
          TRAPCLR   IO
.
          MOVE      "OUX",PRXCODE                * set lock file to OUTAUXFD
          CALL      GETSLK00                     * lock file
          READ      INPFILE,ZERO;SECTOR,AUFLAG
          COMPARE   ZERO,AUFLAG
          GOTO      NOTBUSD IF EQUAL
.
          DISPLAY   *P38:4,*V2LON,"*** CLEARED ***",*W;
          MOVE      ZERO,AUFLAG
          WRITAB    INPFILE,ZERO;*7,AUFLAG
          CLOSE     INPFILE
          CALL      RELSLK00                     * release lock
          GOTO      KPORT
.
NOAUD     DISPLAY   *P38:4,*B,*V2LON:
                    "*** Audit File Does Not Exist ***",*W;
          NORETURN
          GOTO      KPORT
.
NOPORT    DISPLAY   *P38:4,*B,*V2LON:
                    "*** Invalid Port Number ***",*W;
          GOTO      KPORT
.
NOTBUSD   DISPLAY   *P38:4,*B,*V2LON:
                    "*** Audit File is NOT Busy, NOT Cleared ***",*W;
          CLOSE     INPFILE
          CALL      RELSLK00                     * release lock
          GOTO      KPORT
.
AUDBUSY   MOVE      ZERO,AUFLAG
          WRITAB    INPFILE,ZERO;*7,AUFLAG
          CLOSE     INPFILE
.
          DISPLAY   *P38:4,*V2LON,"*** CLEARED ***",*W;
          GOTO      KPORT
.
.-----------------------------------------------------------------------------
.
.         INIT0000 - Open general files
.
.-----------------------------------------------------------------------------
.
INIT0000 
          DISPLAY   *ES,PRGID,*P1:2,VERSION:
                    *P56:24,"Opening";
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TEN;*138,CNOPORT
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          PACK      CFNAME,OUT,FILCLIA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCLIA1,CFNAME
.
          RETURN
.
.-----------------------------------------------------------------------------
.
.         INIT1000 - Open site dependent files
.
.-----------------------------------------------------------------------------
.
INIT1000  DISPLAY   *P56:24,*EL,"Opening ";
.
          PACK      CFNAME,OSTFILE,FILBOKA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA1,CFNAME
.
          PACK      CFNAME,OSTFILE,FILBB1A1
          DISPLAY   *P64:24,CFNAME;
          CALL       OPOTBB11
.
          PACK      CFNAME,OSTFILE,FILCTYA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCTYA1,CFNAME
.
          PACK      CFNAME,OSTFILE,FILCLIA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME,OSTFILE,FILCLIA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCLIA1,CFNAME
.
          DISPLAY   *P56:24,*EL;
.
          RETURN
.
.-----------------------------------------------------------------------------
.
.         INIT3000 - Open site dependent files
.
.-----------------------------------------------------------------------------
.
INIT3000  DISPLAY   *P56:24,*EL,"Opening ";
.
          PACK      CFNAME,OSTFILE,FILMA1A1
          DISPLAY   *P64:24,CFNAME;
          CALL       OPOTMA11
.
          PACK      CFNAME,OSTFILE,FILMA1A2
          DISPLAY   *P64:24,CFNAME;
          CALL       OPOTMA12
.
          PACK      CFNAME,OSTFILE,FILCTYA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCTYA1,CFNAME
.
          PACK      CFNAME,OSTFILE,FILCLIA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCLIA1,CFNAME
.
          PACK      CFNAME,OSTFILE,FILCLIA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCLIA1,CFNAME
.
          DISPLAY   *P56:24,*EL;
.
          RETURN
.
.-----------------------------------------------------------------------------
.
.         INIT2000 - Close site dependent files
.
.-----------------------------------------------------------------------------
.
INIT2000  CLOSE     OUTBOKA1
          CLOSE     OUTBB1A1
          CLOSE     OUTCTYA1
          CLOSE     OUTCLIA1
          RETURN
.
.-----------------------------------------------------------------------------
.
.         INIT4000 - Close site dependent files
.
.-----------------------------------------------------------------------------
.
INIT4000  CLOSE     OUTMA1A1
          CLOSE     OUTMA1A2
          CLOSE     OUTCTYA1
          CLOSE     OUTCLIA1
          RETURN
.
.
          INC       STD001IO
.
          INC       KYOUTCLI
          INC       KYOUTCTY
          INC       OUTDSSIT 
          INC       OUTDSCTY
.
          INC       PATCODIO/INC
          INC       PATDO1IO/INC
          INC       PMSHCPIO/INC            * National HCP file
          INC       PATMA1IO/INC
.
          INC       OUTCLIIO/INC
          INC       OUTCTYIO/INC
          INC       OUTSITIO/INC
          INC       OUTMA1IO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTDSCLN
          INC       OUTAUXIO/INC
.
ENDIT     CHAIN     PGM
          STOP
