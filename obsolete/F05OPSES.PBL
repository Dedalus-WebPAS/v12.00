.*****************************************************************************
.*    Program      : F05OPSES                                                *
.*    Description  : Conversion oprsesaf to new File layout                  *
.*                                                                           *
.*    Author       : Peter Vela                                              *
.*    Date         : 07/08/2014                                              *
.*    Modifications:                                                         *
.*        V10.05.03 Don Nguyen       CAR 304059                              *
.*                  Recompiled for changes to OPRSESIO                       *
.*        V10.05.02 Steve Armstrong  CAR 304059                              *
.*                  Modified to cater for new fields in OPRSESFD             *
.*        V10.05.01 Peter Vela     CAR 283302                                *
.*                  Created program                                          *
.*****************************************************************************
.
          INC       STD001FD
.
FINDFILE  FILE      ASCII, FIXED=256
.
.
. Old File Definition
.
OLDFILE    IFILE     SQL, FIXED=445, KEY="D1-8,9-16,17-18,19-19"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.OPSEAUDD  DIM       8         8       1        Date of change
.OPSEAUDT  DIM       8         8       9        Time of change
.OPSEAUDP  DIM       2         2       17       Port doing change
.OPSEAUDR  DIM       1         1       19       Record type
.                                                 A = Added
.                                                 B = Before image
.                                                 C = After image
.                                                 D = Deleted
.OPSEAUDS  FORM      1         2       20      Record status
.                                                 1 = Not printed
.                                                 2 = Printed
.OPSEAUDO  DIM       4         4       22      Operator id that did the change
.
OLDFIL1    IFILE     SQL, FIXED=420, KEY="U1-8,9-13,14-19"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
.OPSEDATE  DIM       8        8        1        Session Date (CCYYMMDD)
.OPSETIME  DIM       5        5        9        Start Time (HH:MM)
.OPSECLIN  DIM       6        6        14       Clinic/Surgeon
.DOPSESUS  DIM       1        1        20       Booking session suspended ?
.                                              1 = yes   2 = No
.OPSEDAY   FORM      1        2        21       Day Indicator same as OPRSEMFD
.OPSEBOOK  DIM       8        8        23       Booking Date (CCYYMMDD)
.OPSEENDT  DIM       5        5        31       Expected End of session (HH:MM)
.OPSEDURA  FORM      4        3        36       Expected Duration (min)
.OPSEBRKT  FORM      3        3        39       Time for Breaks   (min)
.OPSEPREP  FORM      2        2        42       Preparation time/patient (min)
.OPSETPER  DIM       3        3        44       Time Period (cat SP)
.OPSETYPE  DIM       3        3        47       Specialty (Category ST)
.OPSETHET  DIM       6        6        50       Default Operating Room Code
.OPSECORD  DIM       6        6        56       Session co-ordinator
.OPSEANAE  DIM       6        6        62       Anaesthetist
.OPSESTAT  FORM      1        2        68       Session Status
.                                                   0 = Cancelled
.                                                   1 = Active - Booked
.                                                   2 = Active - Extra session
.OPSETIMB  FORM      4        3        70       Total time currently booked
.
.OPSEACTS  DIM       5        5        73       Actual Start Time   (HH:MM)
.OPSEACTE  DIM       5        5        78       Actual End Time     (HH:MM)
.OPSEACTD  FORM      4        3        83       Actual Duration     (mins)
.OPSEOVER  DIM       3        3        86      Overtime Code       (Category OO)
.OPSEDELA  DIM       3        3        89      Delay Code          (Category OD)
.OPSECANC  DIM       3        3        92      Cancellation Reason (Category OC)
.                                      = 94
.          Default team details
.
.OPSETYD1  DIM       3        3        95       Doctor Type # 1 (Category OP)
.OPSETYD2  DIM       3        3        98                   # 2 (Category OP)
.OPSETYD3  DIM       3        3        101                  # 3 (Category OP)
.OPSETYD4  DIM       3        3        104                  # 4 (Category OP)
.OPSETYD5  DIM       3        3        107                  # 5 (Category OP)
.OPSETYD6  DIM       3        3        110                  # 6 (Category OP)
.OPSETYD7  DIM       3        3        113                  # 7 (Category OP)
.OPSETYD8  DIM       3        3        116                  # 8 (Category OP)
.
.OPSEDCC1  DIM       6        6        119      Doctor Code # 1
.OPSEDCC2  DIM       6        6        125                  # 2
.OPSEDCC3  DIM       6        6        131                  # 3
.OPSEDCC4  DIM       6        6        137                  # 4
.OPSEDCC5  DIM       6        6        143                  # 5
.OPSEDCC6  DIM       6        6        149                  # 6
.OPSEDCC7  DIM       6        6        155                  # 7
.OPSEDCC8  DIM       6        6        161                  # 8
.
.OPSELVD1  DIM       3        3        167      Supervision Level # 1 (Cat OS)
.OPSELVD2  DIM       3        3        170                        # 2 (Cat OS)
.OPSELVD3  DIM       3        3        173                        # 3 (Cat OS)
.OPSELVD4  DIM       3        3        176                        # 4 (Cat OS)
.OPSELVD5  DIM       3        3        179                        # 5 (Cat OS)
.OPSELVD6  DIM       3        3        182                        # 6 (Cat OS)
.OPSELVD7  DIM       3        3        185                        # 7 (Cat OS)
.OPSELVD8  DIM       3        3        188                        # 8 (Cat OS)
.
.OPSETYN1  DIM       3        3        191      Nurse Type # 1 (Category ON)
.OPSETYN2  DIM       3        3        194                 # 2 (Category ON)
.OPSETYN3  DIM       3        3        197                 # 3 (Category ON)
.OPSETYN4  DIM       3        3        200                 # 4 (Category ON)
.OPSETYN5  DIM       3        3        203                 # 5 (Category ON)
.OPSETYN6  DIM       3        3        206                 # 6 (Category ON)
.
.OPSENUC1  DIM       6        6        209      Nurse Code # 1
.OPSENUC2  DIM       6        6        215                 # 2
.OPSENUC3  DIM       6        6        221                 # 3
.OPSENUC4  DIM       6        6        227                 # 4
.OPSENUC5  DIM       6        6        233                 # 5
.OPSENUC6  DIM       6        6        239                 # 6
.
.OPSEUSR1  DIM       3        3        245      User Defined 1 (Category Q6)
.OPSEUSR2  DIM       3        3        248                   2 (Category Q7)
.OPSEUSR3  DIM       3        3        251                   3 (Category Q8)
.OPSEUSR4  DIM       3        3        254                   4 (Category Q9)
.
.OPSEXDOC  DIM       6        6        257      Extra doctor for team
.OPSEENDD  DIM       8        8        263      Expected End date of session
.OPSEACDS  DIM       8        8        271      Actual Start Date   (CCYYMMDD)
.OPSEACDE  DIM       8        8        279      Actual End Date     (CCYYMMDD)
.OPSESPI1  DIM      50       50        287      Special Instructions 1
.OPSESPI2  DIM      50       50        337      Special Instructions 2
.OPSENDLK  DIM       2        2        387      Number of Days to Lock Session
.OPSESTYP  DIM       3        3        389      Session Type (category TU)
.OPSEDGSI  DIM       3        3        392      Day/General Surgery Ind (Cat YD)
.OPSEFIND  DIM       8        8        395      Finalisation Date (CCYYMMDD)
.OPSEFINT  DIM       8        8        403      Finalisation Time (HH:MM:SS)
.OPSEHOSP  DIM       3        3        411      Hospital (pathspaf)
OLDXSPAR  DIM       6        6        414      Spare
.
.End of Record                        420
.
.
. New File Definition
.
OPRAUDSE  IFILE     SQL, FIXED=568, KEY="D1-8,9-16,17-18,19-19"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
OPSEAUDD  DIM       8         8       1        Date of change
OPSEAUDT  DIM       8         8       9        Time of change
OPSEAUDP  DIM       2         2       17       Port doing change
OPSEAUDR  DIM       1         1       19       Record type
.                                                 A = Added
.                                                 B = Before image
.                                                 C = After image
.                                                 D = Deleted
OPSEAUDS  FORM      1         2       20      Record status
.                                                 1 = Not printed
.                                                 2 = Printed
OPSEAUDO  DIM       4         4       22      Operator id that did the change
.
OPRSESA1  IFILE     SQL, FIXED=543, KEY="U1-8,9-13,14-19"
.
.Name     Type      Length Physical Start Description
.----     ----      ------ -------- ----- -----------
OPSEDATE  DIM       8        8        1        Session Date (CCYYMMDD)
OPSETIME  DIM       5        5        9        Start Time (HH:MM)
OPSECLIN  DIM       6        6        14       Clinic/Surgeon
DOPSESUS  DIM       1        1        20       Booking session suspended ?
.                                              1 = yes   2 = No
OPSEDAY   FORM      1        2        21       Day Indicator same as OPRSEMFD
OPSEBOOK  DIM       8        8        23       Booking Date (CCYYMMDD)
OPSEENDT  DIM       5        5        31       Expected End of session (HH:MM)
OPSEDURA  FORM      4        3        36       Expected Duration (min)
OPSEBRKT  FORM      3        3        39       Time for Breaks   (min)
OPSEPREP  FORM      2        2        42       Preparation time/patient (min)
OPSETPER  DIM       3        3        44       Time Period (cat SP)
OPSETYPE  DIM       3        3        47       Specialty (Category ST)
OPSETHET  DIM       6        6        50       Default Operating Room Code
OPSECORD  DIM       6        6        56       Session co-ordinator
OPSEANAE  DIM       6        6        62       Anaesthetist
OPSESTAT  FORM      1        2        68       Session Status
.                                                   0 = Cancelled
.                                                   1 = Active - Booked
.                                                   2 = Active - Extra session
OPSETIMB  FORM      4        3        70       Total time currently booked
.
OPSEACTS  DIM       5        5        73       Actual Start Time   (HH:MM)
OPSEACTE  DIM       5        5        78       Actual End Time     (HH:MM)
OPSEACTD  FORM      4        3        83       Actual Duration     (mins)
OPSEOVER  DIM       3        3        86       Overtime Code       (Category OO)
OPSEDELA  DIM       3        3        89       Delay Code          (Category OD)
OPSECANC  DIM       3        3        92       Cancellation Reason (Category OC)
.                                      = 94
.          Default team details
.
OPSETYD1  DIM       3        3        95       Doctor Type # 1 (Category OP)
OPSETYD2  DIM       3        3        98                   # 2 (Category OP)
OPSETYD3  DIM       3        3        101                  # 3 (Category OP)
OPSETYD4  DIM       3        3        104                  # 4 (Category OP)
OPSETYD5  DIM       3        3        107                  # 5 (Category OP)
OPSETYD6  DIM       3        3        110                  # 6 (Category OP)
OPSETYD7  DIM       3        3        113                  # 7 (Category OP)
OPSETYD8  DIM       3        3        116                  # 8 (Category OP)
.
OPSEDCC1  DIM       6        6        119      Doctor Code # 1
OPSEDCC2  DIM       6        6        125                  # 2
OPSEDCC3  DIM       6        6        131                  # 3
OPSEDCC4  DIM       6        6        137                  # 4
OPSEDCC5  DIM       6        6        143                  # 5
OPSEDCC6  DIM       6        6        149                  # 6
OPSEDCC7  DIM       6        6        155                  # 7
OPSEDCC8  DIM       6        6        161                  # 8
.
OPSELVD1  DIM       3        3        167      Supervision Level # 1 (Cat OS)
OPSELVD2  DIM       3        3        170                        # 2 (Cat OS)
OPSELVD3  DIM       3        3        173                        # 3 (Cat OS)
OPSELVD4  DIM       3        3        176                        # 4 (Cat OS)
OPSELVD5  DIM       3        3        179                        # 5 (Cat OS)
OPSELVD6  DIM       3        3        182                        # 6 (Cat OS)
OPSELVD7  DIM       3        3        185                        # 7 (Cat OS)
OPSELVD8  DIM       3        3        188                        # 8 (Cat OS)
.
OPSETYN1  DIM       3        3        191      Nurse Type # 1 (Category ON)
OPSETYN2  DIM       3        3        194                 # 2 (Category ON)
OPSETYN3  DIM       3        3        197                 # 3 (Category ON)
OPSETYN4  DIM       3        3        200                 # 4 (Category ON)
OPSETYN5  DIM       3        3        203                 # 5 (Category ON)
OPSETYN6  DIM       3        3        206                 # 6 (Category ON)
.
OPSENUC1  DIM       6        6        209      Nurse Code # 1
OPSENUC2  DIM       6        6        215                 # 2
OPSENUC3  DIM       6        6        221                 # 3
OPSENUC4  DIM       6        6        227                 # 4
OPSENUC5  DIM       6        6        233                 # 5
OPSENUC6  DIM       6        6        239                 # 6
.
OPSEUSR1  DIM       3        3        245      User Defined 1 (Category Q6)
OPSEUSR2  DIM       3        3        248                   2 (Category Q7)
OPSEUSR3  DIM       3        3        251                   3 (Category Q8)
OPSEUSR4  DIM       3        3        254                   4 (Category Q9)
.
OPSEXDOC  DIM       6        6        257      Extra doctor for team
OPSEENDD  DIM       8        8        263      Expected End date of session
OPSEACDS  DIM       8        8        271      Actual Start Date   (CCYYMMDD)
OPSEACDE  DIM       8        8        279      Actual End Date     (CCYYMMDD)
OPSESPI1  DIM      50       50        287      Special Instructions 1
OPSESPI2  DIM      50       50        337      Special Instructions 2
OPSENDLK  DIM       2        2        387      Number of Days to Lock Session
OPSESTYP  DIM       3        3        389      Session Type (category TU)
OPSEDGSI  DIM       3        3        392      Day/General Surgery Ind (Cat YD)
OPSEFIND  DIM       8        8        395      Finalisation Date (CCYYMMDD)
OPSEFINT  DIM       8        8        403      Finalisation Time (HH:MM:SS)
OPSEHOSP  DIM       3        3        411      Hospital (pathspaf)
OPSECNDT  DIM       8        8        414      Date Cancellation Notified
OPSECNTM  DIM       8        8        422      Time Cancellation Notified
OPSECNID  DIM       10       10       430      User ID who Cancelled the session
.                                              (websecaf.WBSEUID)
OPSEDTCM  DIM       1        1        440      Default Team Completed
.                                               0 or Blank = No
.                                               1 = Yes
OPSEUCDT  DIM       10       10       441      User who Completed Default Team
.                                              (websecaf.wbseuid)
OPSEDCDT  DIM       8        8        451      Date Default Team Completed
.                                              (ccyymmdd)
OPSETCDT  DIM       8        8        459      Time Default Team Completed
.                                              (hh:mm:ss)
OPSEUUDT  DIM       10       10       467      User who Updated Default Team
.                                              (websecaf.wbseuid)
OPSEDUDT  DIM       8        8        477      Date Default Team Updated
.                                              (hh:mm:ss)
OPSETUDT  DIM       8        8        485      Time Default Team Updated
.                                              (ccyymmdd)
OPSESPAR  DIM       50       50       493      Spare
.
.End of Record                        543
.
. Redefine FORM fields from key
. -----------------------------
OPSESUSP  FORM      1
.
.
.
CMDLINE   DIM       100
DIM4      DIM       4
DIM4A     DIM       4
DIM6      DIM       6
DIM6A     DIM       6
DIM60     DIM       60
DDCENT    DIM       2
DISNUM    FORM      4
.
RECNUM    FORM      8
NEWFILE   DIM       60
NEWPATH   DIM       60
OLDPATH   DIM       60
DEFPATH   DIM       60
SAVEFLG   FORM      1
SP50      INIT    "                                                  "
SP60      INIT    "                                                            "
.
.
PRGNAM    INIT      "CONVERSION OPRSESFD"
PRGID     INIT      "F05OPSES"
VERSION   INIT      "V10.14.00"
+
.*****************************************************************************
.*                              MAIN0000                                     *
.*                       Program Mainline                                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000                      * init and open files
          BRANCH    EXIT,MAIN9999                 * init failure
.
          CALL      OPTN0000                      * get option
          BRANCH    COPTION,MAIN1000              * run c-isam fixit
          GOTO      MAIN9999                      * exit selected
.
MAIN1000  CALL      KDIR0000                      * Keyin directory
          BRANCH    EXIT,MAIN9999
.
          CALL      CONTQST                       * Ok to continue ?
          BRANCH    CEXIT,MAIN2000,MAIN0000,MAIN9999 * Yes, no, cancel
.
MAIN2000  CALL      PREP0000                      * preparing files
          BRANCH    EXIT,MAIN9999
.
          CALL      READ0000                      * read old records and write
          CALL      ENDP0000                      * save/compress saved file
.
MAIN9999  CHAIN     PGM
+
.********************************************************************
.*                          INIT0000                                *
.*  Display heading and check that the files needed exist&open them *
.********************************************************************
INIT0000  CALL      DISPHEAD
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
          
INIT9999  RETURN
.
.*****************************************************************************
.*                                OPTN0000             Called by: MAIN0000   *
.*                        Get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Ok to proceed                           *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION - option selected                                    *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (c-isam only)"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ":
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run c-isam fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.********************************************************************
.*                          KDIR0000                                *
.*  Keyin directories                                               *
.********************************************************************
KDIR0000  MOVE      ONE,SAVEFLG        * Default to keep file
          KEYIN     *P1:10,*EL,"Do you want to save the original file (Y/N) ? ":
                    ANS;
          REP       "yYnN",ANS
          CMATCH    "Y",ANS
          GOTO      KDIR1000 IF EQUAL
          MOVE      ZERO,SAVEFLG       * not to keep file
          CMATCH    "N",ANS
          GOTO      KDIR1000 IF EQUAL
          BEEP
          GOTO      KDIR0000
.
KDIR1000  CALL      DEFT0000                * Get the default path
          BRANCH    EXIT,KDIR9999
          MOVE      SP60,OLDPATH
.
          DISPLAY   *P1:24,*EL,"The directory path must end with a slash (/) ";
.
          MOVE      DEFPATH,OLDPATH
          BRANCH    SAVEFLG,KDIR2000
.
.         Keyin the path for the original file
.
          KEYIN     *P1:12,*EL,"Keyin path for temporarily saving original ":
                    "file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
          GOTO      KDIR3000
.
KDIR2000  KEYIN     *P1:12,*EL,"Keyin path for saving original file: ":
                    *P10:13,*EL,*DV,*RV,OLDPATH:
                    *P10:13,OLDPATH;
.
KDIR3000  ENDSET    OLDPATH
          APPEND    SP60,OLDPATH
          RESET     OLDPATH
.
          MATCH     SP60,OLDPATH
          IF        @EQUAL
            PACK      OLDPATH,DEFPATH      * use the default
            DISPLAY   *P10:13,*EL,*V2LON,OLDPATH;
          ENDIF
.
          SQUEEZE   OLDPATH
          CMATCH    EXITCHAR,OLDPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,OLDPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR1000
.
.         Keyin the path for the new file
.
KDIR5000  MOVE      SP60,NEWPATH
          MOVE      DEFPATH,NEWPATH
          KEYIN     *P1:15,*EL,"Keyin path for the new file: ":
                    *P10:16,*EL,*DV,*RV,NEWPATH:
                    *P10:16,NEWPATH;
          ENDSET    NEWPATH
          APPEND    SP60,NEWPATH
          RESET     NEWPATH
.
          MATCH     SP60,NEWPATH
          IF        @EQUAL
            PACK      NEWPATH,DEFPATH      * use the default
            DISPLAY   *P10:16,*V2LON,NEWPATH;
          ENDIF
.
          SQUEEZE   NEWPATH
          CMATCH    EXITCHAR,NEWPATH
          GOTO      KDIR9000 IF EQUAL
.
          PACK      DIM60,NEWPATH,SP60
          CALL      VALD0000         * check if it's a valid directory
          BRANCH    EXIT,KDIR5000
          GOTO      KDIR9999
.
KDIR9000  MOVE      ONE,EXIT
KDIR9999  DISPLAY   *P1:24,*EL;
          RETURN
+
.
.********************************************************************
.*                          PREP0000                                *
.*  Preparing files                                                 *
.********************************************************************
PREP0000 
. open old audit file
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE,"opraudse"
          TRAPCLR   IO
          BRANCH    OVRCD,PREP5000       * Original file does not exist
          CLOSE     OLDFILE
.
. open old file
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFIL1,"oprsesaf"
          TRAPCLR   IO
          BRANCH    OVRCD,PREP5100       * Original file does not exist
          CLOSE     OLDFIL1
.
          DISPLAY   *P1:24,*EL,"Copying files ... Please wait !! ";
.
. Copy old audit file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat opraudse.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sopaudse.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat opraudse.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sopaudse.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8000
          ENDIF
.
. Copy old file to the keyin directory
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat oprsesaf.dat` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sopsesaf.dat",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8100
          ENDIF
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cp `ldat oprsesaf.idx` ",CMDLINE
          APPEND    OLDPATH,CMDLINE
          APPEND    "sopsesaf.idx",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            GOTO      PREP8100
          ENDIF
.
. remove the original audit files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "opraudse",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
          DISPLAY   *P1:24,*EL,*B,"Unable to remove the original audit file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. remove the original files
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    DEFPATH,CMDLINE
          APPEND    "oprsesaf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
          DISPLAY   *P1:24,*EL,*B,"Unable to remove the original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Opening old audit file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "sopaudse",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFILE,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
          DISPLAY   *P1:24,*EL,*B,"Unable to open saved original audit file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Opening old file after being copied
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60
          APPEND    "sopsesaf",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OLDFIL1,DIM60
          TRAPCLR   IO
          IF        OVRCD = 1
            DISPLAY   *P1:24,*EL,*B,"Unable to open saved original file.  ";
            CALL      HITENTER
            GOTO      PREP9000
          ENDIF
.
. Create the new audit file
.
          DISPLAY   *P1:24,*EL,"Creating new audit files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "opraudse",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP1000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " 568 D1-8,9-16,17-18,19-19",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      OPRAUDSE,NEWFILE
.
. Create the new file
.
          DISPLAY   *P1:24,*EL,"Creating new files ... Please wait !! ";
          PACK      NEWFILE,SP60
          CLEAR     NEWFILE
          APPEND    NEWPATH,NEWFILE
          APPEND    "oprsesaf",NEWFILE
          APPEND    SP60,NEWFILE
          RESET     NEWFILE
          SQUEEZE   NEWFILE
.
PREP2000  PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " 543 UC1-8,9-13,14-19",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
.  If the file has more than one index, this piece of code below has
.  to be repeated with for each individual index
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC14-19,1-8,9-13",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC20-20,14-19,1-8,9-13",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC1-8,50-55,9-13,14-19",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC47-49,1-8,9-13,50-55,14-19",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "isadd ",CMDLINE
          APPEND    NEWFILE,CMDLINE
          APPEND    " UC62-67,1-8,9-13,14-19",CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      OPRSESA1,NEWFILE
.
. set the flag to say we want to continue
.
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P1:18,"Started : ",CDATE,SP1,CTIMEIS:
                    *P1:20,"Record : "
          GOTO      PREP9999
.
PREP5000  DISPLAY   *P1:24,*EL,*B,"Old audit file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP5100  DISPLAY   *P1:24,*EL,*B,"Old file not found.  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8000  DISPLAY   *P1:24,*EL,*B,"Unable to copy original audit file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP8100  DISPLAY   *P1:24,*EL,*B,"Unable to copy original file.  ":
                    "(Error: ",TASKID,")  ";
          CALL      HITENTER
          GOTO      PREP9000
.
PREP9000  MOVE      ONE,EXIT
PREP9999  RETURN
+
.**********************************************************************
.*                               READ0000                             *
.*             loop thru old file and write each record               *
.**********************************************************************
READ0000  DISPLAY   *P1:20,*EL,"Record No. : ";
.
          PACK      KEY19,SP60
          CALL      READSOLD                      * position at start
READ1000  CALL      READKOLD                      * read next record
          BRANCH    OVRCD,READ6000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 5000 | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:20,*V2LON,RECNUM
          ENDIF
.
          MOVE      SP70,OPSECNDT
          MOVE      SP70,OPSECNTM
          MOVE      SP70,OPSECNID
          MOVE      SP70,OPSEDTCM
          MOVE      SP70,OPSEUCDT
          MOVE      SP70,OPSEDCDT
          MOVE      SP70,OPSETCDT
          MOVE      SP70,OPSEUUDT
          MOVE      SP70,OPSEDUDT
          MOVE      SP70,OPSETUDT
          MOVE      SP70,OPSESPAR
.                                               * Pack key here
          PACK      KEY19,OPSEAUDD,OPSEAUDT,OPSEAUDP,OPSEAUDR,SP70
          CALL      AWOPSES                     * write to new file
.
          GOTO      READ1000                      * get next record
.
READ6000  CLOSE     OPRAUDSE                      * close new file
          CLOSE     OLDFILE                       * close old file
.
          DISPLAY   *P15:20,"opraudse: ",*V2LON,RECNUM,*HOFF:
                    *P1:21,*EL,"Record No. : ";
          MOVE      ZERO,RECNUM
          MOVE      ZERO,DISNUM
.
          PACK      KEY19,SP60
          CALL      READSOL1                      * position at start
READ7000  CALL      READKOL1                      * read next record
          BRANCH    OVRCD,READ8000                * no more records
.
          ADD       ONE,RECNUM                    * update record counter
          ADD       ONE,DISNUM                    * Display counter
.
          IF        DISNUM = 5000  | RECNUM = 1
            MOVE      ZERO,DISNUM
            DISPLAY   *P15:21,*V2LON,RECNUM
          ENDIF
.
          MOVE      SP70,OPSECNDT
          MOVE      SP70,OPSECNTM
          MOVE      SP70,OPSECNID
          MOVE      SP70,OPSEDTCM
          MOVE      SP70,OPSEUCDT
          MOVE      SP70,OPSEDCDT
          MOVE      SP70,OPSETCDT
          MOVE      SP70,OPSEUUDT
          MOVE      SP70,OPSEDUDT
          MOVE      SP70,OPSETUDT
          MOVE      SP70,OPSESPAR
.
          PACK      KEY19,OPSEDATE,OPSETIME,OPSECLIN,SP70
          CALL      WROPSES1
.
          GOTO      READ7000
.
READ8000  CLOSE     OPRSESA1
          CLOSE     OLDFIL1
.
          CALL      IBACLOCK
          REP       " 0",CDATE
          DISPLAY   *P15:21,"oprsesaf: ",*V2LON,RECNUM,*HOFF:
                    *P1:22,"Ended  : ",CDATE,SP1,CTIMEIS:
                    *P1:24;
.
READ9999  RETURN
+
.**********************************************************************
.*                               VALD0000                             *
.*        Check if it a valid directory                               *
.**********************************************************************
VALD0000  MOVE      ZERO,EXIT
          SQUEEZE   DIM60
          ENDSET    DIM60
          CMATCH    SLASH,DIM60
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory path must end with a slash (/) ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          RESET     DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          APPEND    "cd ",CMDLINE
          APPEND    DIM60,CMDLINE
          APPEND    SP60,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Directory does not exist ";
            CALL      HITENTER
            GOTO      VALD9000
          ENDIF
          GOTO      VALD9999
.
VALD9000  MOVE      ONE,EXIT            * Not valid
VALD9999  RETURN
+
.**********************************************************************
.*                               ENDP0000                             *
.*        Remove or compress save file                                *
.**********************************************************************
ENDP0000  MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "sopaudse*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "gzip -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original audit file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
         DISPLAY   *P1:24,*EL,"Removing original audit file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
        DISPLAY   *P1:24,*EL,*B,"Saved audit file not compressed or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
.
          MOVE      SP60,DIM60
          CLEAR     DIM60
          APPEND    OLDPATH,DIM60        * set up the saved file with path
          APPEND    "sopsesaf*",DIM60
          APPEND    SP60,DIM60
          RESET     DIM60
          SQUEEZE   DIM60
.
          PACK      CMDLINE,SP60,SP60
          CLEAR     CMDLINE
          IF        SAVEFLG = 1
            APPEND    "gzip -f ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Compressing original file.. ":
                      "Please wait !!  ";
          ELSE
            APPEND    "rm ",CMDLINE
            APPEND    DIM60,CMDLINE
            APPEND    SP60,CMDLINE
            RESET     CMDLINE
            DISPLAY   *P1:24,*EL,"Removing original file.. Please wait !!  ";
          ENDIF
          EXECUTE   CMDLINE,TASKID      * check if exists
.
          CMATCH    "0",TASKID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Saved file not compressed or removed.  ";
            CALL      HITENTER
            GOTO      ENDP9999
          ENDIF
.
          CALL      IBACLOCK
          DISPLAY   *P1:24,*EL,*B,"Finish processing.  ";
          CALL      HITENTER
.
ENDP9999  RETURN
+
.**********************************************************************
.         Get the default directory
.**********************************************************************
DEFT0000  EXECUTE   "ldat oprsesaf.dat > temp.txt",TASKID
.
          PACK      DEFPATH,SP30,SP30
          CLOSE     FINDFILE
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      FINDFILE,"temp"
          TRAPCLR   IO
          BRANCH    OVRCD,DEFT3000
.
          READ      FINDFILE,SEQ;DEFPATH
          GOTO      DEFT3000 IF OVER
.
          ENDSET    DEFPATH
DEFT1000  CMATCH    "/",DEFPATH
          GOTO      DEFT2000 IF EQUAL
.
          BUMP      DEFPATH,-1
          GOTO      DEFT1000 IF NOT EOS
          GOTO      DEFT3000
.
DEFT2000  LENSET    DEFPATH
          RESET     DEFPATH
.
          PACK      DEFPATH,DEFPATH,SP30,SP30,SP30,SP30
          STRIP     DEFPATH
          ENDSET    DEFPATH
          RESET     DEFPATH
          CLOSE     FINDFILE
          MOVE      ZERO,EXIT
          GOTO      DEFT9999
.
DEFT3000  DISPLAY   *P1:24,*EL,*B,"Error finding oprsesaf.  ";
          CALL      HITENTER
          CLOSE     FINDFILE
          MOVE      ONE,EXIT
DEFT9999  RETURN
+
.*****************************************************************************
.*        I/O Routines                                                       *
.*****************************************************************************
.
.         Old I/O Routines
.
READSOLD  MOVE      ZERO,OVRCD
          RESET     KEY19
          READ      OLDFILE,KEY19;;
          RETURN
.
READSOL1  MOVE      ZERO,OVRCD
          RESET     KEY19
          READ      OLDFIL1,KEY19;;
          RETURN
.
READKOLD  MOVE      ZERO,OVRCD
          READKS    OLDFILE;OPSEAUDD,OPSEAUDT,OPSEAUDP,OPSEAUDR,OPSEAUDS:
                    OPSEAUDO:
                    OPSEDATE,OPSETIME,OPSECLIN,OPSESUSP:
                    OPSEDAY,OPSEBOOK,OPSEENDT,OPSEDURA,OPSEBRKT,OPSEPREP:
                    OPSETPER,OPSETYPE,OPSETHET,OPSECORD,OPSEANAE,OPSESTAT:
                    OPSETIMB,OPSEACTS,OPSEACTE,OPSEACTD,OPSEOVER,OPSEDELA:
                    OPSECANC:
                    OPSETYD1,OPSETYD2,OPSETYD3,OPSETYD4:
                    OPSETYD5,OPSETYD6,OPSETYD7,OPSETYD8:
                    OPSEDCC1,OPSEDCC2,OPSEDCC3,OPSEDCC4:
                    OPSEDCC5,OPSEDCC6,OPSEDCC7,OPSEDCC8:
                    OPSELVD1,OPSELVD2,OPSELVD3,OPSELVD4:
                    OPSELVD5,OPSELVD6,OPSELVD7,OPSELVD8:
                    OPSETYN1,OPSETYN2,OPSETYN3,OPSETYN4:
                    OPSETYN5,OPSETYN6:
                    OPSENUC1,OPSENUC2,OPSENUC3,OPSENUC4:
                    OPSENUC5,OPSENUC6:
                    OPSEUSR1,OPSEUSR2,OPSEUSR3,OPSEUSR4:
                    OPSEXDOC,OPSEENDD,OPSEACDS,OPSEACDE:
                    OPSESPI1,OPSESPI2,OPSENDLK,OPSESTYP,OPSEDGSI,OPSEFIND:
                    OPSEFINT,OPSEHOSP,OLDXSPAR
          GOTO      OVERCOND IF OVER
          RETURN 
.
READKOL1  MOVE      ZERO,OVRCD
          READKS    OLDFIL1;OPSEDATE,OPSETIME,OPSECLIN,DOPSESUS:
                    OPSEDAY,OPSEBOOK,OPSEENDT,OPSEDURA,OPSEBRKT,OPSEPREP:
                    OPSETPER,OPSETYPE,OPSETHET,OPSECORD,OPSEANAE,OPSESTAT:
                    OPSETIMB,OPSEACTS,OPSEACTE,OPSEACTD,OPSEOVER,OPSEDELA:
                    OPSECANC:
                    OPSETYD1,OPSETYD2,OPSETYD3,OPSETYD4:
                    OPSETYD5,OPSETYD6,OPSETYD7,OPSETYD8:
                    OPSEDCC1,OPSEDCC2,OPSEDCC3,OPSEDCC4:
                    OPSEDCC5,OPSEDCC6,OPSEDCC7,OPSEDCC8:
                    OPSELVD1,OPSELVD2,OPSELVD3,OPSELVD4:
                    OPSELVD5,OPSELVD6,OPSELVD7,OPSELVD8:
                    OPSETYN1,OPSETYN2,OPSETYN3,OPSETYN4:
                    OPSETYN5,OPSETYN6:
                    OPSENUC1,OPSENUC2,OPSENUC3,OPSENUC4:
                    OPSENUC5,OPSENUC6:
                    OPSEUSR1,OPSEUSR2,OPSEUSR3,OPSEUSR4:
                    OPSEXDOC,OPSEENDD,OPSEACDS,OPSEACDE:
                    OPSESPI1,OPSESPI2,OPSENDLK,OPSESTYP,OPSEDGSI,OPSEFIND:
                    OPSEFINT,OPSEHOSP,OLDXSPAR
          GOTO      OVERCOND IF OVER
          MOVE      DOPSESUS,OPSESUSP
          RETURN
.
.         New I/O Routines
.
AWOPSES   RESET     KEY19
          WRITE     OPRAUDSE,KEY19;KEY19,OPSEAUDS,OPSEAUDO:
                    OPSEDATE,OPSETIME,OPSECLIN,OPSESUSP:
                    OPSEDAY,OPSEBOOK,OPSEENDT,OPSEDURA,OPSEBRKT,OPSEPREP:
                    OPSETPER,OPSETYPE,OPSETHET,OPSECORD,OPSEANAE,OPSESTAT:
                    OPSETIMB,OPSEACTS,OPSEACTE,OPSEACTD,OPSEOVER,OPSEDELA:
                    OPSECANC:
                    OPSETYD1,OPSETYD2,OPSETYD3,OPSETYD4:
                    OPSETYD5,OPSETYD6,OPSETYD7,OPSETYD8:
                    OPSEDCC1,OPSEDCC2,OPSEDCC3,OPSEDCC4:
                    OPSEDCC5,OPSEDCC6,OPSEDCC7,OPSEDCC8:
                    OPSELVD1,OPSELVD2,OPSELVD3,OPSELVD4:
                    OPSELVD5,OPSELVD6,OPSELVD7,OPSELVD8:
                    OPSETYN1,OPSETYN2,OPSETYN3,OPSETYN4:
                    OPSETYN5,OPSETYN6:
                    OPSENUC1,OPSENUC2,OPSENUC3,OPSENUC4:
                    OPSENUC5,OPSENUC6:
                    OPSEUSR1,OPSEUSR2,OPSEUSR3,OPSEUSR4:
                    OPSEXDOC,OPSEENDD,OPSEACDS,OPSEACDE:
                    OPSESPI1,OPSESPI2,OPSENDLK,OPSESTYP,OPSEDGSI,OPSEFIND:
                    OPSEFINT,OPSEHOSP,OPSECNDT,OPSECNTM,OPSECNID,OPSEDTCM:
                    OPSEUCDT,OPSEDCDT,OPSETCDT,OPSEUUDT,OPSEDUDT,OPSETUDT:
                    OPSESPAR
          RETURN        
.
WROPSES1  RESET     KEY19
          MOVE      OPSESUSP,DOPSESUS
          WRITE     OPRSESA1,KEY19;KEY19,DOPSESUS:
                    OPSEDAY,OPSEBOOK,OPSEENDT,OPSEDURA,OPSEBRKT,OPSEPREP:
                    OPSETPER,OPSETYPE,OPSETHET,OPSECORD,OPSEANAE,OPSESTAT:
                    OPSETIMB,OPSEACTS,OPSEACTE,OPSEACTD,OPSEOVER,OPSEDELA:
                    OPSECANC:
                    OPSETYD1,OPSETYD2,OPSETYD3,OPSETYD4:
                    OPSETYD5,OPSETYD6,OPSETYD7,OPSETYD8:
                    OPSEDCC1,OPSEDCC2,OPSEDCC3,OPSEDCC4:
                    OPSEDCC5,OPSEDCC6,OPSEDCC7,OPSEDCC8:
                    OPSELVD1,OPSELVD2,OPSELVD3,OPSELVD4:
                    OPSELVD5,OPSELVD6,OPSELVD7,OPSELVD8:
                    OPSETYN1,OPSETYN2,OPSETYN3,OPSETYN4:
                    OPSETYN5,OPSETYN6:
                    OPSENUC1,OPSENUC2,OPSENUC3,OPSENUC4:
                    OPSENUC5,OPSENUC6:
                    OPSEUSR1,OPSEUSR2,OPSEUSR3,OPSEUSR4:
                    OPSEXDOC,OPSEENDD,OPSEACDS,OPSEACDE:
                    OPSESPI1,OPSESPI2,OPSENDLK,OPSESTYP,OPSEDGSI,OPSEFIND:
                    OPSEFINT,OPSEHOSP,OPSECNDT,OPSECNTM,OPSECNID,OPSEDTCM:
                    OPSEUCDT,OPSEDCDT,OPSETCDT,OPSEUUDT,OPSEDUDT,OPSETUDT:
                    OPSESPAR
          RETURN
.
          INC       STD001IO
