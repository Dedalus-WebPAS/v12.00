.******************************************************************************
.* System         : Operating Rooms         Release 5.01                      *
.*                                                                            *
.* Include Name   : OPROPTNS.PBL                                              *
.*                                                                            *
.* Function       : Has two routines.                                         *
.*                  SETOP000 - Set the valid line 24 options in NEWOPTS1      *
.*                  DISOP000 - Display line 24 options using NEWOPTS1         *
.*                                                                            *
.* Author         : Justin Coates                                             *
.*                                                                            *
.* Date Written   : 08/08/1991                                                *
.*                                                                            * .* Parameters     : VALOPT1       the valid options for line 24               *
.*                  SECLEV        the users security level                    *
.*                                                                            *
.* Returns        : NEWOPTS1      new line 24 options                         *
.*                  COLUMN        column to keyin at                          *
.*                                                                            *
.* Modifications  : V5.01.01 25/06/93 J.Tan    SRF 120208                     *
.*                           Fixed to display previous screen                 *
.******************************************************************************
.
.*********************************************************************
.*                  SETOP000                    Called by : KOPT0000 *
.*        Set up the option string for line 24                       *
.*        Para's  : VALOPT1       the valid line 24 options          *
.*        Returns : NEWOPTS1      the options available on line 24   *
.*********************************************************************
SETOP000  CLEAR     NEWOPTS1                * clear the available options
          MOVE      ZERO,FORM2              * clear counter
          HLINE     *G37,2,SUBOPT1,80
.
SETOP100  ADD       "1",FORM2               * get to next option
          COMPARE   "31",FORM2
          GOTO      SETOP900 IF NOT LESS    * no more options available
.
.         get the level for the option
.
          MOVE      ZERO,OPTLEVEL
          LOAD      OPTLEVEL,FORM2,PTOPOP1,PTOPOP2,PTOPOP3,PTOPOP4,PTOPOP5:
                                   PTOPOP6,PTOPOP7,PTOPOP8,PTOPOP9,PTOPOP10:
                                   PTOPOP11,PTOPOP12,PTOPOP13,PTOPOP14,PTOPOP15:
                                   PTOPOP16,PTOPOP17,PTOPOP18,PTOPOP19,PTOPOP20:
                                   PTOPOP21,PTOPOP22,PTOPOP23,PTOPOP24,PTOPOP25:
                                   PTOPOP26,PTOPOP27,PTOPOP28,PTOPOP29,PTOPOP30
.
          COMPARE   OPTLEVEL,SECLEV
          GOTO      SETOP100 IF LESS        * user doesn't have clearance
.
.         user has high enough level, get the option letter
.
          RESET     VALOPT1,FORM2
          GOTO      SETOP900 IF EOS         * no more characters to check
.
          MOVE      VALOPT1,ANS
          RESET     VALOPT1
.
.         check if N or P as these require extra validation
.
          MATCH     "N",ANS
          GOTO      SETOP200 IF NOT EQUAL   * not a "N"
.
.         ensure there is a next screen before displaying option
.
          BRANCH    ISNEXT,SETOP100         * no next screen
. 
.         do extra check as some times it thinks there is a next 
.         screen but there actually isn't
.
          LOAD      CURCASE,NUMCASE,CASE01,CASE02,CASE03,CASE04,CASE05:
                                    CASE06,CASE07,CASE08,CASE09,CASE10:
                                    CASE11,CASE12,CASE13
.
          PACK      KEY23,SESNDATE,SESNTIME,SESNCODE,ZERO,Z70
          CALL      RSOPDEA1                * position on last case of session
          CALL      RPOPDEA1
          BRANCH    OVRCD,SETOP100          * no next screen
.
          MOVE      OPDACASE,CCITEM         * get last case as a form
          COMPARE   CCITEM,CURCASE          * if   current case < last case
          GOTO      SETOP300 IF LESS        * then there is a next screen
.
          MOVE      ONE,ISNEXT              * set as no next screen 
          GOTO      SETOP100                * there is no next screen
.
SETOP200  MATCH     "P",ANS
          GOTO      SETOP300 IF NOT EQUAL   * not a "P"
.
.         ensure there is a previous screen before displaying option
.
          BRANCH    ISPREV,SETOP100         * no previous screen
.         GOTO      SETOP100
.
.         option is valid so display and add to NEWOPTS1
.
SETOP300  APPEND    ANS,NEWOPTS1            * add letter to string
          GOTO      SETOP100
.
.         no more options available
.
SETOP900  APPEND    SP30,NEWOPTS1
          RESET     NEWOPTS1
.
SETOP999  RETURN
+
.*********************************************************************
.*                  DISOP000                    Called by : OPSN0000 *
.*        Display the valid line 24 options from NEWSOPT1            *
.*        Para's  : NEWOPTS1      the valid line 24 options          *
.*        Returns : COLUMN        column for the keyin               *
.*********************************************************************
DISOP000  DISPLAY   *P1:24,*EL,"Options : ";
          MOVE      "11",COLUMN             * starting column position
          RESET     NEWOPTS1
          MOVE      ZERO,FORM2              * reset counter
.
DISOP100  ADD       ONE,FORM2               * increment counter
          COMPARE   "31",FORM2
          GOTO      DISOP900 IF NOT LESS    * finished line
.
.         get the letter to display
.
          RESET     NEWOPTS1,FORM2
          GOTO      DISOP900 IF EOS         * no more options to display
.
          MOVE      NEWOPTS1,ANS
          RESET     NEWOPTS1
.
          MATCH     SP1,ANS
          GOTO      DISOP900 IF EQUAL       * no more options to display
.
          COMPARE   "11",COLUMN
          GOTO      DISOP500 IF NOT EQUAL   * not first item
.
          DISPLAY   *V2LON,ANS;
          ADD       "1",COLUMN              * add for letter added
          GOTO      DISOP100
.
DISOP500  DISPLAY   ", ",*V2LON,ANS;
          ADD       "3",COLUMN              * add for letter,space
          GOTO      DISOP100
.
.         have displayed all valid options
.
DISOP900  DISPLAY   " ? _ (";
          ADD       "3",COLUMN              * add extra for keyin position
.
          DISPLAY   *V2LON,"H",*HOFF,"=Help)",*EL;
.
DISOP999  RETURN
+
