.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   FX159072                                                  *
.* Desc      :   Clear cancellation date on oprdetaf if not cancelled      *
.***************************************************************************
.* Date      :   18/01/2008                                                *
.* Author    :   Jill Habasque                                             *
.***************************************************************************
.
          INC       STD001FD
          INC       OPRDEAFD/INC
.
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
PRGID     INIT      "FX159072"
PRGNAM    INIT      "Clear cancellation date on oprdetaf"
VERSION   INIT      "V11.03.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      OPRDETA1,"oprdetaf"
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Print Only":
                    *P1:6,*V2LON,TWO,*HOFF,". Print and Update"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  PACK      KEY23,SP70
          CALL      RSOPDEA1
PROC1000  CALL      RKOPDEA1
          BRANCH    OVRCD,PROC9999
.
          MATCH     SP70,OPDACDAT
          GOTO      PROC1000 IF EQUAL
.
          COMPARE   "3",OPDASTAT
          GOTO      PROC1000 IF EQUAL
.
          MOVE      SP70,OPDACDAT
          PRINT     "Updated ",OPDAUNIQ
          IF        COPTION=2
            CALL      UPOPDEA1
          ENDIF
          GOTO      PROC1000
.
PROC9000  DISPLAY   "Finished";
          CALL      HITENTER
.
PROC9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       OPRDEAIO/INC
