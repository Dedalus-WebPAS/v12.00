.******************************************************************************T
.* System         : Patient Management System    Release 5.04                 *
.* Include Name   : PATSTA28.PBL                                              *
.* Function       : Print statements for Layout type 19 - PTCNINVL = 9        *
.*                  for West Wimmera Health Service                           *
.*                                                                            *
.*                  This include has been copied from PATINV28 to separate    *
.*                  statement and invoice layouts so that OUT66 doesn't       *
.*                  need to include variables which are required only for     *
.*                  statements printed from HOS20                             *
.*                                                                            *
.******************************************************************************
.* Author         : Rob Leonard                                               *
.* Date           : 02/11/1994                                                *
.* Mods           :                                                           *
.*       V11.00.01  10/03/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*        V9.04.01  08/09/2004 Ebon Clements  CAR 53280                       *
.*                  Removed move "- " to sep in ENDPRT28                      *
.*        V9.02.00  10/05/2002 J.Tan                                          *
.*                  Rename invoice layout for West wimmera from 18 to 28      *
.*        V5.07.03  05/01/2000  J.Tan                                         *
.*                  Mods to use PACDATE for current date                      *
.*        V5.07.02  04/08/1999  Jill Habasque                                 *
.*                  Added OPPTWCO1 & OPPTINS1 instead of opening old files only*
.*        V5.07.01  01/04/1999  Jill Habasque SRF 630671                      *
.*                  Mods to print PRA details on statement                    *
.                   08/10/1998  Glenn Berry      5.07 changes
.******************************************************************************
.* Subroutines in this routine are :                                          *
.* ENDPRT28       : Print the tail section of the invoice.                    *
.* TAIL28         : Print the tail section of the invoice when going over page*
.* HEAD28         : Print the heading section of the invoice.                 *
.* PRTEOL28       : Print the end of line. Used when there is a right hand    *
.*                  side to the invoice.                                      *
.* PROMIS28       : Process any special code for normal miscellaneous items   *
.* PROTHE28       : Process any special code for theatre fees                 *
.* PROACC28       : Process any special code for accomodation records         *
.* PRTCLM28       : Print the claim details on the invoice                    *
.* INITIV28       : Initialise any special variables for the invoice          *
.* SETUPI28       : Set up any special variables for the invoice              *
.* PLINUP28       : Print lineup print for this invoice layout                *
.* PROOUT28       : Process any special code for outpatient misc. items       *
.*                                                                            *
.******************************************************************************
.
.******************************************************************************
.* ENDPRT28 : This subroutine prints the end section of the invoice
.******************************************************************************
ENDPRT28  MOVE      CNAME TO DIM30
          MOVE      CHPPOST TO DIM4
.
          COMPARE   ZERO TO CSAMT
          GOTO      N28CASHR IF EQUAL
.
.         Check if there is any cash to be printed
.
          BRANCH    CASHFLAG OF C28SHINV
.
.-------- No cash to print ---------
.
N28CASHR  MOVE      NETTOT,TPATAMTP
          GOTO      C28TMEM1
.
.-------- Print cash receipt total ------
.
C28SHINV  MULT      SEQ,CSAMT             * Print as a negative
          MOVE      CSDAY,CDAY
          MOVE      CSMON,CMON
          MOVE      CSCENT,CCENT
          MOVE      CSYEAR,CYEAR
          CALL      PACDATE
          PRINT     *2,CPDATE:
                    *25,"PAYMENT RECEIVED WITH THANKS":
                    *70,CSAMT
          MULT      SEQ,CSAMT
          MOVE      NETTOT TO TPATAMTP
          SUB       CSAMT FROM NETTOT
.
.-------- Load up the word MEMBER if this is a member ------
.
C28TMEM1  MOVE      SP6,DMEMB
.
          COMPARE   ONE,CMEMB
          GOTO      C28TMEM2 IF NOT EQUAL
.
          LOAD      DMEMB USING AMEMB OF MEMBER
.
C28TMEM2  MOVE      PKADD4,PKADD242
          IF HEADTYPE=3
             IF     HOPTION=2|HOPTION=3
                 PRINT     *N,*69,TOTBAL:
                    *N,*N,*5,AGEMASK4,*17,AGEMASK3,*28,AGEMASK2:
                    *39,AGEMASK1:
                    *N,*N,*N,*N,*N,*N,*N,*N,*N:
                    *66,"U/R",*70,PURNO,*N,*14,PFNAME:
                    *N,*14,PKADD1,*N,*14,PKADD2,*N,*14,PKSUBR:
                    *N,*14,PKADD242,SP1,PKPOST:
                    *N,*69,TOTBAL,*F;
             ELSE
                 PRINT     *N,*69,TOTBAL:
                    *N,*N,*5,AGEMASK4,*17,AGEMASK3,*28,AGEMASK2:
                    *39,AGEMASK1:
                    *N,*N,*N,*N,*N,*N,*N,*51,"ADMISSION NO.",*N,*N:
                    *53,AADMNO,*66,"U/R",*70,PURNO,*N,*14,PFNAME:
                    *N,*14,PKADD1,*N,*14,PKADD2,*N,*14,PKSUBR:
                    *N,*14,PKADD242,SP1,PKPOST:
                    *N,*69,TOTBAL,*F;
             ENDIF
          ELSE
             PRINT     *N,*69,NETTOT:
                    *N,*N,*N,*N,*N,*N,*N,*N,*N,*51,"ADMISSION NO.",*N,*N:
                    *53,AADMNO,*67,PINVNO:
                    *N,*14,PFNAME:
                    *N,*14,PKADD1,*N,*14,PKADD2,*N,*14,PKSUBR:
                    *N,*14,PKADD242,SP1,PKPOST:
                    *N,*69,NETTOT,*F;
         ENDIF
.
C28TMEM9  RETURN
+
.******************************************************************************
.* TAIL28 : This subroutine prints the tail section of the invoice when
.*          the invoice goes over two pages
.******************************************************************************
TAIL28    
.
TAIL28R   MOVE      PKADD4,PKADD242
          IF        HEADTYPE=3
              PRINT     *N,*3,"*** STATEMENT CONTINUED ON NEXT PAGE ***":
                    *N,*N,*68,"************":
                    *N,*N,*N,*N,*N,*N,*N,*N
.
              IF        HOPTION=2|HOPTION=3
                  PRINT     *55,"U/R NO.",*N
                  PRINT     *53,PURNO:
                    *N,*14,PFNAME,*53:                  
                    *N,*14,PKADD1,*N,*14,PKADD2,*N,*14,PKSUBR:
                    *N,*14,PKADD242,SP1,PKPOST:
                    *N,*68,"************"
              ELSE
                  PRINT     *51,"ADMISSION NO.",*N
                  PRINT     *53,AADMNO:
                    *N,*14,PFNAME,*53:                  
                    *N,*14,PKADD1,*N,*14,PKADD2,*N,*14,PKSUBR:
                    *N,*14,PKADD242,SP1,PKPOST:
                    *N,*68,"************"
              ENDIF
          ELSE
              PRINT     *N,*3,"***   INVOICE CONTINUED ON NEXT PAGE ***":
                    *N,*N,*68,"************":
                    *N,*N,*N,*N,*N,*N,*N,*N
.
              PRINT     *51,"ADMISSION NO.",*N
              MOVE      CDD,CDAY
              MOVE      CMM,CMON
              MOVE      CCC,CCENT
              MOVE      CYY,CYEAR
              CALL      PACDATE
              PRINT     *14,CPDATE,*53,AADMNO,*67,PINVNO:
                    *N,*14,PFNAME,*53:                  
                    *N,*14,PKADD1,*N,*14,PKADD2,*N,*14,PKSUBR:
                    *N,*14,PKADD242,SP1,PKPOST:
                    *N,*68,"************"
          ENDIF
.
+
.******************************************************************************
.* HEAD28 : This subroutine prints the heading section of the invoice
.******************************************************************************
HEAD28    MOVE      ZERO,VLINE
          CALL      IBACLOCK
          MOVE      CDD,CDAY
          MOVE      CMM,CMON
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          CALL      PACDATE
          IF        HEADTYPE = 3
             PRINT     *N,*N,*61,"STATEMENT":
                       *N,*N,*N
             PRINT     *54,"U/R",*58,PURNO,*70,CPCDATE
             GOTO      HEAD28ST
          ELSE
             IF        COPYINV=1
                 PRINT     *N,*N,*58,"COPY OF INVOICE":
                           *N,*N,*N
             ELSE
                 PRINT     *N,*N,*62,"INVOICE":
                           *N,*N,*N
             ENDIF
             CALL      IBACLOCK
             PRINT     *55,PINVNO,*70,CPCDATE
          ENDIF
.
HEAD28IN  PRINT     *N,*N,*N
          PRINT        *11,PFNAME,*52,PNAME:
                    *N,*11,PKADD1,*52,PADD1:
                    *N,*11,PKADD2,*52,PADD2:
                    *N,*11,PKSUBR,*52,PSUBRB:
                    *N,*11,PKADD4,*37,PKPOST,*52,PADD4,*75,PPOST,*N,*N
.
          PRINT     *3,"ADMISSION NO.",*24,"ADMISSION DATE",*44:
                    "DISCHARGE DATE",*68,"FUND",*N
.
          PRINT     *5,AADMNO,*27,A8DATE,*47,D8DATE,*63,AFUNDH,"/",AFNDTB:
                    *N,*N,*N,*N,*N
.
          MOVE      SP9,DINVTP
          LOAD      DINVTP USING IPASS OF SP9,DINSUR
.
          MOVE      SP20 TO TDESC
          MOVE      SP20 TO WCCLMNO
          MOVE      SP20 TO MREFNO
          MOVE      SP20 TO VCLMNO
.          BRANCH    CLAIM OF HEAD28B,HEAD28C,HEAD28A
          GOTO      HEAD28E
.
HEAD28ST  PRINT     *N,*N,*N
          IF        HOPTION=2 | HOPTION=3
            MATCH     SP8,WWADMN
            IF        !@EQUAL
              MOVE      WWADMN,AADMNO
              CALL      GETPRES
            ENDIF
          ENDIF
          PRINT        *11,PFNAME,*52,PNAME:
                    *N,*11,PKADD1,*52,PADD1:
                    *N,*11,PKADD2,*52,PADD2:
                    *N,*11,PKSUBR,*52,PSUBRB:
                    *N,*11,PKADD4,*37,PKPOST,*52,PADD4,*75,PPOST,*N,*N
.
          IF        HOPTION=1
              PRINT     *3,"ADMISSION NO.",*N
          ELSE
              PRINT     *N
          ENDIF
.
          IF        HOPTION=2|HOPTION=3
              PRINT     *N,*N,*N,*N,*N
          ELSE
              PRINT     *5,AADMNO,*N,*N,*N,*N,*N
          ENDIF
.
          MOVE      SP9,DINVTP
          LOAD      DINVTP USING IPASS OF SP9,DINSUR
.
          MOVE      SP20 TO TDESC
          MOVE      SP20 TO WCCLMNO
          MOVE      SP20 TO MREFNO
          MOVE      SP20 TO VCLMNO
.          BRANCH    CLAIM OF HEAD28B,HEAD28C,HEAD28A
          GOTO      HEAD28E
.
HEAD28A   OPEN      PATWVET1,"patwvetf"
          CALL      RDWVET1
          MOVE      VCLMNO TO WCCLMNO
          CLOSE     PATWVET1
          GOTO      HEAD28D
.
HEAD28B   OPEN      PATWC1A1,"patwc1af"
          CALL      RDWCOM1
          CLOSE     PATWC1A1
          GOTO      HEAD28D
.
HEAD28C   OPEN      PATWMAB1,"patwmabf"
          CALL      RDWMAB1
          MOVE      MREFNO TO WCCLMNO
          CLOSE     PATWMAB1
.
HEAD28D   PRINT     *2,TDESC,"    REFERENCE - ",WCCLMNO,*71,DINVTP;
          GOTO      HEAD28F
.
HEAD28E   
.          PRINT     *2,CNOCLMS,*71,DINVTP;
.
HEAD28F   MOVE      "8",COUNT
.
          RETURN
+
.******************************************************************************
.* PRTEOL28 : This subroutine prints the end of each line in the invoice body
.******************************************************************************
PRTEOL28  PRINT     *N;
          RETURN
+
.******************************************************************************
. PROMIS28 : This subroutine handles any special code for normal misc. items
.******************************************************************************
PROMIS28  RETURN
+
.******************************************************************************
. PROTHE28 : This subroutine handles any special code for theatre fees
.******************************************************************************
PROTHE28  MOVE      TITEMNO TO PRTITCOD
.
.------- Check if it exists in item file ( if it is, use it as the MBS code )
.
          OPEN      PATITEM1,"patitemn"
          PACK      KEY17,PRTITCOD,SP70
          CALL      PATITMRD
          CLOSE     PATITEM1
          COMPARE   ZERO,OVRCD
          RETURN    IF EQUAL
.
.-------- Use the receipt number as the MBS code -------
.
          MOVE      TRECEIPT TO PRTITCOD
          RETURN
+
.******************************************************************************
. PROACC28 : This subroutine handles any special code for accomodation records
.******************************************************************************
PROACC28  RETURN
+
.******************************************************************************
. PRTCLM28 : This subroutine handles the printing of the claim details in the
.            body of the invoice ( if they are wanted )
.******************************************************************************
PRTCLM28  BRANCH    CLAIM OF N28XTWCO,N28XTMAB,N28XTVET
.
N28XTVET  MOVE      AADMNO,KEY6
          OPEN      PATWVET1,"patwvetf"
          CALL      RDWVET1
          CLOSE     PATWVET1
          BRANCH    OVRCD,PTCLM28F
.
N28XTV0   COMPARE   "17",COUNT
          GOTO      N28XTV2 IF EQUAL
          COMPARE   "17",VLINE
          GOTO      N28XTV2 IF EQUAL
.
          COMPARE   "19",COUNT
          GOTO      N28XTV1 IF NOT LESS
          COMPARE   "19",VLINE 
          GOTO      N28XTV1 IF NOT LESS
.
          ADD       ONE,COUNT
          ADD       ONE,VLINE
          CALL      PRTEOL28
          GOTO      N28XTV0
.
N28XTV1   CALL      TAIL28
.
N28XTV2   PRINT     *N,*2,"Claim number : ",VCLMNO
          ADD       TWO,COUNT
          ADD       TWO,VLINE
          RETURN
.
N28XTMAB  MOVE      AADMNO,KEY6
          OPEN      PATWMAB1,"patwmabf"
          CALL      RDWMAB1
          CLOSE     PATWMAB1
          BRANCH    OVRCD,PTCLM28F
.
N28XTM0   COMPARE   "13",COUNT
          GOTO      N28XTM2 IF EQUAL
          COMPARE   "13",VLINE 
          GOTO      N28XTM2 IF EQUAL
.
          COMPARE   "19",COUNT
          GOTO      N28XTM1 IF NOT LESS
          COMPARE   "19",VLINE
          GOTO      N28XTM1 IF NOT LESS
.
          ADD       ONE,COUNT
          ADD       ONE,VLINE
          CALL      PRTEOL28
          GOTO      N28XTM0
.
N28XTM1   CALL      TAIL28
.
N28XTM2   UNPACK    MACDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *N,*2,"Reference Number         : ",MREFNO:
                    *N,*2,"Date of Accident         : ",CPCDATE:
                    *N,*2,"Police Station           : ",MPOLIC:
                    *N,*2,"Car Registration Numbers : ",MCREGO:
                    *N,*2,"Solicitor's Name         : ",MSNAME
          ADD       SIX,COUNT
          ADD       SIX,VLINE
          RETURN
.
N28XTWCO  MOVE      AADMNO,KEY8
          OPEN      PATWC1A1,"patwc1af"
          CALL      RDWCOM1
          CLOSE     PATWC1A1
          BRANCH    OVRCD,PTCLM28F
.
N28XTW0   COMPARE   "11",COUNT
          GOTO      N28XTW2 IF EQUAL
          COMPARE   "11",VLINE
          GOTO      N28XTW2 IF EQUAL
.
          COMPARE   "19",COUNT
          GOTO      N28XTW1 IF NOT LESS
          COMPARE   "19",VLINE
          GOTO      N28XTW1 IF NOT LESS
.
          ADD       ONE,COUNT
          ADD       ONE,VLINE
          CALL      PRTEOL28
          GOTO      N28XTW0
.
N28XTW1   CALL      TAIL28
.
N28XTW2   MOVE      SP30,INAME 
          MOVE      SP30,IADD1
          MOVE      SP30,IADD2
          MOVE      SP20,IADD3
          MOVE      SP4,IPOST
.
          COMPARE   ONE,WCACCPT
          GOTO      N28TWCP1 IF NOT EQUAL
. 
          MOVE      WCICODE,KEY6
          OPEN      PATIN1A1,"patin1af"
          CALL      RDINSR1
          CLOSE     PATIN1A1
.
N28TWCP1  UNPACK    WCACDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *N,*2,"Claim Number     : ",WCCLMNO:
                    *N,*2,"Date of Accident : ",CPCDATE:
                    *N,*2,"Employer:",*35,"Insurance Company:":
                    *N,*2,WCENAME,*35,INAME:
                    *N,*2,WCEADD1,*35,IADD1:
                    *N,*2,WCEADD2,*35,IADD2:
                    *N,*2,WCEADD3,*26,WCEPOST,*35,IADD3,*61,IPOST
          ADD       EIGHT,COUNT
          ADD       EIGHT,VLINE
.
PTCLM28F RETURN
+
.******************************************************************************
. INITIV28 : This subroutine initialises any special variables used
.******************************************************************************
INITIV28  RETURN
+
.******************************************************************************
. SETUPI28 : This subroutine sets up any special variables used
.******************************************************************************
SETUPI28  RETURN
+
.******************************************************************************
. PLINUP28 : This subroutine prints the line-up print for this invoice layout
.******************************************************************************
PLINUP28  PRINT     *N,*N,*61,"XXXXXXXXX",*N,*N,*N
.
          PRINT     *53,"99999999",*70,"**/**/****"
          PRINT     *N,*N,*N,*N:
                    *11,"XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX":
                    *52,"XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX",*N:
                    *11,"XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX":
                    *52,"XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX",*N:
                    *11,"XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX":
                    *52,"XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX",*N:
                    *11,"XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX":
                    *52,"XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX",*N:
                    *11,"XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX":
                    *52,"XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX",*N:
                    *11,"XXXXXXXXXXXXXXXXXXXXXXXXX 9999":
                    *52,"XXXXXXXXXXXXXXXXXXXXXXXXX 9999":
                    *N,*N;
          PRINT     *5,"DEBTOR NO.",*25,"ADMISSION DATE":
                    *46,"DISCHARGE DATE",*68,"FUND"
          PRINT     *N,*5,"99999999",*25,"**/**/****",*45,"**/**/****":
                    *63,"XXXXXX/XXXX"
          PRINT     *N,*N,*N
          PRINT     *1,"**/**/****",*23,"XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX":
                    *54,"XXXXXXXXX",*69,"999999.99"
          PRINT     *N,*38,"INVOICE TOTAL",*69,"999999.99"
.
          PRINT     *N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N
.
          PRINT     *N,*69,"999999.99":
                    *N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*52,"DEBTOR NO.",*N:
                    *14,"**/**/****":
                    *N,*14,"XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX":
                    *53,"99999999",*69,"99999999":
                    *N,*14,"XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX":
                    *N,*14,"XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX":
                    *N,*14,"XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX":
                    *N,*14,"XXXXXXXXXXXXXXXXXXXXXXXXX 9999":
                    *N,*69,"999999.99"
.
          RETURN
+
.******************************************************************************
. PREBAT28 : This subroutine prints the rebate figures, uses PTCNPEFR
.******************************************************************************
PREBAT28  CALL      PRBTTL00                * Print the rebate total
          RETURN
