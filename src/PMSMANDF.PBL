.-------------------------------------------------------------
. Check pregnancy mandatory fields
.-------------------------------------------------------------
CHKMAN00  PACK      DIM600,SP70,SP70
          MOVE      ZERO,MANDCNT
          PACK      KEY13,PREGUNIQ,Z70
          CALL      RSPMRGN2
          CALL      RPPMRGN2
          BRANCH    OVRCD,CHKMAN99
.
          MATCH     PREGUNIQ,PMRGUNIQ
          GOTO      CHKMAN99 IF NOT EQUAL
.
          IF        ALLMANDF=1
            GOTO      CHKMAN05
          ENDIF
.
          MATCH     SP70,PMRGCOMP       * pregnancy not complete - don't
          GOTO      CHKMAN99 IF EQUAL   * bother checking
.
CHKMAN05  PACK      KEY5,ANSF,ANSB,PMRGCOMP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CHKMAN99
.
          MATCH     ANSX,TCINDC1        * pregnancy did not complete - mandatory
          GOTO      CHKMAN80 IF EQUAL   * fields are not required
.
          PACK      KEY6,ONE,SP70
          CALL      RSPMWFM2
CHKMAN10  CALL      RKPMWFM2
          BRANCH    OVRCD,CHKMAN80
.
          MOVE      PMWFFLDN,FORM5
          PACK      DIM80,SP70,SP70
.
          IF        FORM5>48
            GOTO      CHKMAN20
          ENDIF
.
          CALL      LOADM000         * pregnancy field
          MATCH     SP70,DIM80
          GOTO      CHKMAN10 IF NOT EQUAL
.
          IF        ALLMANDF=1
            CALL      DISMAN00
            GOTO      CHKMAN10
          ELSE
            GOTO      CHKMAN99
          ENDIF
.
CHKMAN20  IF        FORM5>85
            GOTO      CHKMAN40
          ENDIF
.
          CALL      CHKLBR00        * check all labour records are complete
          IF        EXIT=1
            IF        ALLMANDF=1
              CALL      DISMAN00
              GOTO      CHKMAN10
            ELSE
              GOTO      CHKMAN99
            ENDIF
          ENDIF
.
          GOTO      CHKMAN10
.
CHKMAN40  CALL      CHKBTH00        * check all birth records are complete
          IF        EXIT=1
            IF        ALLMANDF=1
              CALL      DISMAN00
              GOTO      CHKMAN10
            ELSE
              GOTO      CHKMAN99
            ENDIF
          ENDIF
.
          GOTO      CHKMAN10
.
CHKMAN80  IF        ALLMANDF=1
            COMPARE   ZERO,MANDCNT
            GOTO      CHKMAN99 IF NOT EQUAL
          ENDIF
.
          PACK      KEY10,PMRGUNIQ,SP70
          CALL      DEPMINC1
          GOTO      CHKMAN99
.
CHKMAN99  MOVE      ZERO,EXIT
          RETURN
+
.-------------------------------------------------------------
. Load mandatory field data
.-------------------------------------------------------------
LOADM000  LOAD     DIM80,FORM5,PMRGGRAV,PMRGTOTL,PMRGTOPL,PMRGTOPB,PMRGCURL:
                               PMRGTOPS,PMRGTPND,PMRGTPAS,PMRGTPAI,PMRGTPEO:* 10
                               PMRGTPUO,PMRGDATE,PMRGOUTL,PMRGLASM,PMRGLASC:
                               PMRGTOTS,PMRGPLVB,PMRGISBR,PMRGDATM,PMRGESTD:* 20
                               PMRGEDDB,PMRGHGHT,PMRGWGHT,PMRGESGA,PMRGEGAS:
                               PMRGNU14,PMRGNU26,PMRGNU27,PMRGML20,PMRGMG20:* 30
                               PMRGACRC,PMRGDACP,PMRGANCP,PMRGASBR,PMRGASBF:
                               PMRGCIBS,PMRGCIBR,PMRGUDF1,PMRGUDF2,PMRGUDF3:* 40
                               PMRGUDF4,PMRGUDF5,PMRGUDF6,PMRGUDF7,PMRGUDF8:
                               PMRGUDF9,PMRGUDF0,PMRGCOMP,PMLBONSL,PMLBDATL:* 50
                               PMLBTIML,PMLBMIN1,PMLBDAT2,PMLBTIM2,PMLBMIN2:
                               PMLBDATR,PMLBINDI,PMLBINDF,PMLBINDO,PMLBINOF:* 60
                               PMLBANLG,PMLBPOSD,PMLBCOLL,PMLBPRES,PMLBMEMB:
                               PMLBNVES,PMLBTYPB,PMLBPROP,PMLBMAN3,PMLBPLAC:* 70
                               PMLBMPLA,PMLBEPIS,PMLBPERI,PMLBPERT,PMLBPERR:
                               PMLBBLDL,PMLBBLTR,PMLBDLIC,PMLBDMID,PMLBDDOC:* 80
                               PMLBDANA,PMLBNUMB,PMLBMIDR,PMBTADMN,PMBTADMM:
                               PMBTLURN,PMBTNBFR,PMBTPLUR,PMBTADOP,PMBTBBAR:* 90
                               PMBTBDAT,PMBTBTIM,PMBTESTG,PMBTGEND,PMBTBSTA:
                               PMBTMDEL,PMBTAPG1,PMBTAPG2,PMBTTRES,PMBTDRUG:*100
                               PMBTBWGH,PMBTLNGT,PMBTHEAD,PMBTMOUL,PMBTCAPU:
                               PMBTCORB,PMBTMECO,PMBTURIN,PMBTPAED,PMBTNICU:*110
                               PMBTVITK,PMBTHBDT,PMBTHBTM,PMBTBFAT,PMBTIFDD:
                               PMBTBEBY,PMBTLBAT,PMBTLBCH,PMBTGDAT,PMBTGTIM:*120
                               PMBTDWGH,PMBTBPRE,PMBTCANE,PMBTCABO
.
LOADM999  RETURN
+
.-------------------------------------------------------------
. Check Al Labour Records
.-------------------------------------------------------------
CHKLBR00  MOVE      ZERO,EXIT
          MOVE      ZERO,F1
          MOVE      ZERO,SAVECNTR
.
          PACK      KEY24,PMRGUNIQ,Z70
          CALL      RSPMLBR1
CHKLBR25  CALL      RPPMLBR1
          BRANCH    OVRCD,CHKLBR30
.
          MATCH     PMRGUNIQ,PMLBUNIQ
          GOTO      CHKLBR30 IF NOT EQUAL
.
          MATCH     SAVECNTR,PMLBCNTR
          GOTO      CHKLBR25 IF EQUAL
.
          PACK      SAVECNTR,PMLBCNTR
          MOVE      ONE,F1
.
          CALL      LOADM000         * labour field
          MATCH     SP70,DIM80
          GOTO      CHKLBR25 IF NOT EQUAL
.
          GOTO      CHKLBR90         * incomplete
.
CHKLBR30  COMPARE   ZERO,F1
          GOTO      CHKLBR90 IF EQUAL
.
          GOTO      CHKLBR99
.
CHKLBR90  MOVE      ONE,EXIT
.
CHKLBR99  RETURN
+
.-------------------------------------------------------------
. Check All Birth Records
.-------------------------------------------------------------
CHKBTH00  MOVE      ZERO,EXIT
          MOVE      ZERO,F1
          MOVE      SP70,DIM10
.
          PACK      KEY23,PMRGUNIQ,Z70
          CALL      RSPMBTH1
CHKBTH25  CALL      RPPMBTH1
          BRANCH    OVRCD,CHKBTH30
.
          MATCH     PMRGUNIQ,PMBTUNIQ
          GOTO      CHKBTH30 IF NOT EQUAL
.
          MATCH     DIM10,PMBTBUNQ
          GOTO      BTHLS100 IF EQUAL
.
          PACK      DIM10,PMBTBUNQ,SP70
          MOVE      ONE,F1
.
          CALL      LOADM000         * labour field
          MATCH     SP70,DIM80
          GOTO      CHKBTH25 IF NOT EQUAL
.
          GOTO      CHKBTH90         * incomplete
.
CHKBTH30  COMPARE   ZERO,F1
          GOTO      CHKBTH90 IF EQUAL
.
          GOTO      CHKBTH99
.
CHKBTH90  MOVE      ONE,EXIT
.
CHKBTH99  RETURN
+
.-------------------------------------------------------------------------------
.  Display Mandatory Fields etc
.-------------------------------------------------------------------------------
DISMAN00  ADD       ONE,MANDCNT
          STRIP     PMWFSDES
          APPEND    PMWFSDES,DIM600
          APPEND    BREAKLIN,DIM600
.
DISMAN99  RETURN
+
