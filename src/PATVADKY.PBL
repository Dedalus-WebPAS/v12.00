.******************************************************************************
.*  Include Name  : PATVADKY.PBL                                              *
.*  Description   : Keyin of Transfer destination code                        *
.*  Author        : Ian Rutt                                                  *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF5 - default value      (blank if no default)       *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMODE - is keyin mode      (1=Insert)                  *
.*                  CNEWDS   - is keyin position  (0=Hit enter to continue)   *
.*                                                (1=Keyin at Line 24)        * 
.*                                                                            *
.*  Returns       : PTVACODE - Keyed in code                                  *
.*                  EXIT     - 0   Everything Ok.                             *
.*                           - 1   Nothing or Spaces Input                    *
.*                           - 2   EXITCHAR input                             *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Modifications :                                                           *
.*        V5.03.01  23/11/1995  Greg Horvat      SRF 612434                   *
.*                  Changed the '?' option to display from line 5 instead of  *
.*                  line 9                                                    *
.*                                                                            *
.******************************************************************************
PATVADKY
PTVAKY00  MATCH     SP5,CKYIDEF5                  * using a default ?
          GOTO      PTVAKY10 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN5:      * no default used
                    *PECOL:EVERT,*V2LON,PTVACODE:
                    *PECOL:EVERT,*DV,PTVACODE
          GOTO      PTVAKY20
.
PTVAKY10  MOVE      CKYIDEF5,PTVACODE                 * default
          KEYIN     *PECOL:EVERT,*DV,PTVACODE:     * default used
                    *PECOL:EVERT,*V2LON,*RV,PTVACODE:
                    *PECOL:EVERT,*DV,PTVACODE
.
.         check what was entered
.
PTVAKY20  CALL      VADCK000
          BRANCH    EXIT,PTVAKY25,PTVAKY31,PTVAKY41,PTVAKY95
.                        "?"      valid    nothing  exitchar
          GOTO      PTVAKY00
.
.         a "?" was input
.
PTVAKY25  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
PTVAKY26  CALL      PATVADDS
          BRANCH    CNEWDS,PTVAKY27 
.
          CALL      PUTSCR00                * redisplay screen
          GOTO      PTVAKY00                * rekey code
.
.         now ask for code while leaving displayed codes on screen
.
PTVAKY27  MATCH     SP5,CKYIDEF5                  * using a default
          GOTO      PTVAKY28 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Transfer Source Code : ":
                    *P24:24,*DV,UNDLN5:      * no default used
                    *P24:24,*V2LON,PTVACODE:
                    *P24:24,*DV,PTVACODE
          GOTO      PTVAKY29
.
PTVAKY28  MOVE      CKYIDEF5,PTVACODE
          KEYIN     *P1:24,*EL,*DV,"Transfer Source Code : ":
                    *P24:24,*DV,PTVACODE:     * default used
                    *P24:24,*V2LON,*RV,PTVACODE:
                    *P24:24,*DV,PTVACODE
.
.         repeat the validation of the code
.         check what was entered
.
PTVAKY29  CALL      VADCK000
          BRANCH    EXIT,PTVAKY26,PTVAKY30,PTVAKY40,PTVAKY90
.                        "?"      valid    nothing  exitchar
.
          GOTO      PTVAKY27
.
.         a valid code was entered 
. 
PTVAKY30  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,PTVACODE
.
PTVAKY31  MOVE      FALSE,EXIT                    * valid input
          GOTO      PTVAKY99 
.
.         nothing was input
.
PTVAKY40  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,PTVACODE
.
PTVAKY41  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      PTVAKY99 
.
.         EXITCHAR input
.
PTVAKY90  CALL      FRESCR00                * free the screen saved
.
PTVAKY95  MOVE      TWO,EXIT                * EXITCHAR
.
PTVAKY99  RETURN
+
.*********************************************************************
.*                  VADCK000                    Called by : PATCODKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
VADCK000  ENDSET    PTVACODE
          APPEND    SP5,PTVACODE
          RESET     PTVACODE
.
          MATCH     EXITCHAR,PTVACODE
          GOTO      VADCK600 IF EQUAL       * exit char
.
          MATCH     SP5,PTVACODE
          GOTO      VADCK700 IF EQUAL       * nothing
.
          MATCH     QUEST,PTVACODE
          GOTO      VADCK800 IF EQUAL       * ? mark
.
.         a code was entered so validate
. 
VADCK500  PACK      KEY5,PTVACODE,SP5
          CALL      RDPTVAD1
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,VADCK900          * invalid code
          ELSE
            COMPARE   ZERO,OVRCD   
            GOTO      VADCK900 IF EQUAL
          ENDIF  
.
          MOVE      TWO,EXIT                * valid code
          GOTO      VADCK999
.
.         exitchar entered
.
VADCK600  MOVE      FOUR,EXIT
          MOVE      SP5,PTVACODE
          GOTO      VADCK999
.
.         nothing entered
.
VADCK700  BRANCH    CKYIMAND,VADCK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP5,PTVACODE
          GOTO      VADCK999
.
.         "?" entered
.
VADCK800  MOVE      ONE,EXIT
          GOTO      VADCK999
.
.         invalid code entered
.
VADCK900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
.
          IF        CKYIMODE = 0
            DISPLAY   *P1:24,*EL,*B,"Invalid Transfer Destination Code. ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      VADCK999
.
.         field is mandatory
.
VADCK950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is Mandatory. ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
VADCK999  RETURN
.
          INCLUDE   PATVADDS
+
