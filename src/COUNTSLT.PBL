.*******************************************************************************
.* System    :   Outpatient Management System                                  *
.* Program   :   COUNTSLT - Count Slots                                        *
.*                                                                             *
.*******************************************************************************
.* Date      :  10/10/2013                                                     *
.* Author    :  Ania Pietrusewicz                                              *
.* Function  :  Program that recalculates the total clinic used slot time      *
.*                                                                             *
.* Mods      :                                                                 *
.* V12.00.01    30/05/2025 Jacob Jackson      TSK 0955096                      *
.*              Alphanumeric changes                                           *
.* V10.03.00    10/10/2013  Ania P            CAR 283357                       *
.*              Created Program                                                *
.*                                                                             *
.*******************************************************************************
.
.------------------------------------------------------------
. Add Slot Time to Total Slot Time
.------------------------------------------------------------
ADDTIM00  CALL      CHKGRP00
          IF        CHKGRPIN=ONE
            RETURN
          ENDIF
.
          MATCH     SP70,OSETIMEU
          IF        @EQUAL
            MOVE      "00",KEY2
            MOVE      COLON,KEY1
            MOVE      "00",MIN
          ELSE
            UNPACK    OSETIMEU,KEY2,KEY1,MIN     * HH:MM
          ENDIF
.
          MOVE      KEY2,HOUR                    * HH to Form
          MOVE      MIN,MINUTES                  * MM to Form
          ASSIGN    (HOUR*60)+MINUTES+(OTHEDURN*TCFORM6),MINUTES
          ASSIGN    MINUTES%60,F2
          MOVE      F2,MIN
          ASSIGN    (MINUTES-(MINUTES%60))/60,HOUR
          PACK      OSETIMEU,HOUR,COLON,MIN      
          REP       " 0",OSETIMEU
.
ADDTIM99  RETURN
.
.------------------------------------------------------------
. Add Slot Time to Total Slot Time
.------------------------------------------------------------
SUBTIM00  CALL      CHKGRP00
          IF        CHKGRPIN=ONE
            RETURN
          ENDIF
.
          MATCH     SP70,OSETIMEU
          IF        @EQUAL
            MOVE      "00",KEY2
            MOVE      COLON,KEY1
            MOVE      "00",MIN
          ELSE
            UNPACK    OSETIMEU,KEY2,KEY1,MIN     * HH:MM
          ENDIF
.
          MOVE      KEY2,HOUR                    * HH to Form
          MOVE      MIN,MINUTES                  * MM to Form
          ASSIGN    (HOUR*60)+MINUTES-(OTHEDURN*TCFORM6),MINUTES
          ASSIGN    MINUTES%60,F2
          MOVE      F2,MIN
          ASSIGN    (MINUTES-(MINUTES%60))/60,HOUR
          PACK      OSETIMEU,HOUR,COLON,MIN
          REP       " 0",OSETIMEU
.
SUBTIM99  RETURN
.
.------------------------------------------------------------
. Check for Group Sessions
.------------------------------------------------------------
CHKGRP00  MOVE      ZERO,CHKGRPIN
.
          MATCH     "1",OTHEGBOK
          GOTO      CHKGRP99 IF NOT EQUAL
.
          PACK      SAVKEY28,OBASITE,OBACLIN,OBADATE,OBASTRT,DOBASLOT,SP70
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDATE,OSESTRT,SP70
          CALL      RDSBOKA1
CHKGRP10  CALL      RDKBOKA1
          BRANCH    OVRCD,CHKGRP90
.
          MATCH     OSESITE,OBASITE
          GOTO      CHKGRP90 IF NOT EQUAL
.
          MATCH     OSECLIN,OBACLIN
          GOTO      CHKGRP90 IF NOT EQUAL
.
          MATCH     OSEDATE,OBADATE
          GOTO      CHKGRP90 IF NOT EQUAL
.
          MATCH     OSESTRT,OBASTRT
          GOTO      CHKGRP90 IF NOT EQUAL
.
          MATCH     "       0",DOBAOUTN
          IF        !@EQUAL
            MATCH     "00:00",OSETIMEIU
            IF        !@EQUAL
              MOVE      ONE,CHKGRPIN
              GOTO      CHKGRP90
            ENDIF
          ENDIF
          GOTO      CHKGRP10
.
CHKGRP90  PACK      KEY28,SAVKEY28,SP70
          CALL      RDBOKA1
.
CHKGRP99  RETURN
.
.------------------------------------------------------------
.  Read OUTHED and MASTER
.------------------------------------------------------------
RDSLT000  PACK      KEY18,OSESITE,OSECLIN,OSEDAY,OSESTRT,SP70
          CALL      RDMASA1
          BRANCH    OVRCD,RDSLT999
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT,SP70
          CALL      RDOTHED1
          BRANCH    OVRCD,RDSLT999
.
RDSLT999  RETURN
+
