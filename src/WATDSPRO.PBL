.
.       WATDSPRO : Display Procedure code Entries         WAITING LISTS
.
.       This include is used to display procedure codes and descriptions 
.
.       To display all codes (including inactive codes) move one to DISPACT
.       before calling this routine or move zero to DISPACT to display 
.       only active codes.
.
.     Modifications: 
.         V5.07      25/10/1999  J Habasque
.                    Added DISPACT and check for inactive codes
.                    30/11/89 Graeme Williams
.                    Added the parameter CNEWDS to determine if the include
.                    is to use the new standards for "?" routines
.                    23/02/90 E.Phan
.                    Replaced WPTYPE with WPESTLS
.                    26/06/1990 Bill Dew
.                    Loop over the 3rd index (desc) of procedure file and
.                    user to enter a range of description codes
.                    15/03/1993  Steve O'Gorman
.                    Stop scanning file when description is no longer equal
.                    i.e if looking for F's don't scan G to Z.
.
WATDSPRO  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,SEVENTY9;*138,PTCNURHA
.
          PACK      WPDESCDS,SP30,SP30 * blank out the keyin desc
          MOVE      WTCNTPDS,KEY9      * set procedure or treatment
          REPLACE   UPPLOW,KEY9
          DISPLAY   *P1:4,*EF,WTCNTPDS," Description : ",UNDLN30,UNDLN20;
          KEYIN     *P25:4,*V2LON,WPDESCDS
.
          DISPLAY   *P1:3,*EF,*P30:4,*V2LON,*ULON,KEY9," CODES":
                    *P1:5,*ULON,"Code     ":
                    *P11:5,"Description",SP30,SP9:
                    *P62:5,"Time "
.
          IF        PTCNURHA = 1
            DISPLAY   *V2LON,*ULON:
                      *P68:5,"CHE Th":
                      *P75:5,"RHA Th" 
          ENDIF
.
          MOVE      SIX,VERT
          MOVE      ONE,COL
          PACK      KEY59,WPDESCDS,SP30,SP30
          CALL      RDSPROA3
PRO100
          CALL      RDKPROA3
          BRANCH    OVRCD OF PRO900
.
          RESET     WPDESCDS
          GOTO      PRO200 IF EOS
.
          MATCH     SP30,WPDESCDS
          GOTO      PRO200 IF EQUAL
.
          MATCH     WPDESC,WPDESCDS   * test if description match
          GOTO      PRO900 IF NOT EQUAL
PRO200    COMPARE   ONE,DISPACT       * display all codes (including inactive)
          GOTO      PRO300 IF EQUAL
.
          COMPARE   ONE,WTWPACTV      * dont display inactive code 
          GOTO      PRO100 IF EQUAL
.
PRO300    DISPLAY   *PCOL:VERT,*V2LON,WPCODE,SP1,*HOFF,WPDESC,SP1,WPTIME;
          IF         PTCNURHA = 1
            DISPLAY    SP1,WPCHET,SP1,WPRHAT
          ENDIF
          ADD        ONE,VERT
          COMPARE    "24",VERT
          GOTO       PRO100 IF LESS
.
          MOVE       SIX,VERT
          ADD        "80",COL                    * two columns of codes and desc
          COMPARE    "70",COL
          GOTO       PRO100 IF LESS
.
.         Check for new standards question, or old question
.
          BRANCH    CNEWDS,PRO120
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSE,ANS
          GOTO      PRO999 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      WATDSPRO IF EQUAL
.
          GOTO      PRO140
.
.         Use the new standards question
.
PRO120    KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSC,ANS
          GOTO      PRO999 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      WATDSPRO IF EQUAL
.
PRO140    MOVE      ONE,COL
          DISPLAY   *P1:6,*EF
          GOTO      PRO100
.
.         End of procedure codes
.
PRO900    BRANCH    CNEWDS,PRO999
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
PRO999    RETURN
