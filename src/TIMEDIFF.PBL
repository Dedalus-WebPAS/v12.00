.***************************************************************************
.* System    :   All                                                       *
.* Include   :   TIMEDIFF.PBL                                              *
.* Desc      :   Elapsed Time Calculation                                  *
.***************************************************************************
.* Date      :   01/10/1998                                                *
.* Author    :   Steve Armstrong                                           *
.* Function  :   This routine will calculate the difference in minutes     *
.*               between two dates/times                                   *
.* Requires  :   FIRSTDAT     DIM 8  (first date CCYYMMDD format)          *
.*               LASTDATE     DIM 8  (last date CCYYMMDD format)           *
.*               FIRSTIME     DIM 4  (first time HHMM format)              *
.*               LASTTIME     DIM 4  (last time HHMM format)               *
.*                                                                         *
.*               Working variables                                         *
.*                                                                         *
.*               CDYSDAYS  FORM      6             * for CALCDAYS          *
.*               CDYSFDTE  DIM       8             * for CALCDAYS          *
.*               CDYSTDTE  DIM       8             * for CALCDAYS          *
.*               CDYSYEAR  FORM      2             * for CALCDAYS          *
.*               FIRSTHR   DIM       2             * first hours (hh)      *
.*               FIRSTMIN  DIM       2             * first minutes (mm)    *
.*               LASTHOUR  DIM       2             * last hours (hh)       *
.*               LASTMIN   DIM       2             * last minutes (mm)     *
.*               HOUR1     FORM      6             * first hours           *
.*               HOUR2     FORM      6             * last hours            *
.*               MINUTE1   FORM      3             * first minutes         *
.*               MINUTE2   FORM      3             * last minutes          *
.*               TOTTIME1  FORM      4             * full first time (hhmm)*
.*               TOTTIME2  FORM      4             * full last time (hhmm) *
.*                                                                         *
.*               NB.  Routine assumes that last date/time is not less      *
.*                    than first date/time.                                *
.* Returns   :   CALCTIME     FORM  6   (elapsed time in minutes)          *
.***************************************************************************
.
.         Load first and last hours and minutes into numeric variables
.
TIMEDIFF  UNPACK    FIRSTIME,FIRSTHR,FIRSTMIN
          MOVE      FIRSTHR,HOUR1
          MOVE      FIRSTMIN,MINUTE1
          UNPACK    LASTTIME,LASTHOUR,LASTMIN
          MOVE      LASTHOUR,HOUR2
          MOVE      LASTMIN,MINUTE2
.
          MOVE      ZERO,CALCTIME                * initialise calculated time
.
          MATCH     FIRSTDAT,LASTDATE            * are dates the same ?
          IF        @EQUAL
            CALL      LESSTM00                   * yes - check minutes values
            CALL      DIFFTM00                   * calculate elapsed time
          ELSE
.
.           Dates are different, so first calculate the number of days
.           difference between the two dates
.
            MOVE      FIRSTDAT,CDYSFDTE          
            MOVE      LASTDATE,CDYSTDTE
            CALL      CALCDAYS                   * calculate days inclusive
            SUB       ONE,CDYSDAYS               * sub 1 day for difference
.
            MATCH     FIRSTIME,LASTTIME          * last time < first time ?
            IF        @LESS
              CALL      LESSTM00                 * yes - check minutes values
              COMPARE   HOUR1,HOUR2              * last hours < first hours ?
              IF        @LESS
                ADD       TWENTY4,HOUR2          * yes - add 1 day to last hour
                SUB       ONE,CDYSDAYS           * sub 1 day from days diff.
              ENDIF
            ENDIF
            CALL      DIFFTM00                   * check minutes values
            ASSIGN    (((CDYSDAYS*24)*60)+CALCTIME),CALCTIME
          ENDIF
.
TMDIFF99  RETURN
+
.****************************************************************************
.*                               LESSTM00              Called by: PROCTM99  *
.*        Check if last minutes are less than first minutes                 *
.****************************************************************************
.
LESSTM00  COMPARE   MINUTE1,MINUTE2              * last min < first min ?
          IF        @LESS
            ADD       SIXTY,MINUTE2              * yes - add 60 min to last min
            SUB       ONE,HOUR2                  * sub 1 hour from last hour
          ENDIF
.
LESSTM99  RETURN
+
.****************************************************************************
.*                               DIFFTM00              Called by: PROCTM99  *
.*                Calculate difference in times in minutes                  *
.****************************************************************************
.
DIFFTM00  SUB       MINUTE1,MINUTE2              * calc difference in minutes
          SUB       HOUR1,HOUR2                  * calc difference in hours
          IF        HOUR2 <> 0
            MULT      SIXTY,HOUR2                * convert hours to minutes
          ENDIF
          ASSIGN    (MINUTE2+HOUR2),CALCTIME     * calc total minutes
.
DIFFTM99  RETURN
+
