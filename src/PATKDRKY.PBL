.------------------------------------------------------------
. Function          : Procedure for Seach of DRG CodeS
.
. Parameters        : CKEYSUSP    1=Show Suspended 
.                   : CKEYORDR    0=Key Word Search, 1=Alphabetic Search
.
. Includes Required :
. Open Tables Req   :
. Returns Fields    : OVRCD    0=Valid Record Selected, 1=No Record Selected
.                     Record Read from xxxxxxx Table
.
. Modifications :
.         V9.08.01  19/04/2007  Mike Laming   CAR 137125
.                   Add DRG Version to keys of PATDGWaf - now requires DRGVERZ
.         V5.07.01  01/12/1999  Davin
.                   Changed for 4 character drgs
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Routine to Seach for
.  a coded field via either a Key Word Index or Alphabetical
.  Index and output a ? Selection List.
.  
.  A Key Word Index Table is Defined in the following Manner
.
.  patkdraf
.  PATKDRA1
.  PATKDRA2    
.  
.  PTKDITM     DIM       xx    * Code Field 
.  PTKDKWD     DIM       15    * Key Word
.
.  Index 1   PTKDITM, PTKDKWD
.  Index 2   PTKDKWD, PTKDxxxx
.
.To convert this Procedure the following should be performed in this sequence
.    Global Change patdgwaf to name of Alphabetic Search Table   eg patdo1af
.    Global Change PATDGWA3 to index of Alphabetic Search Table  eg PATDO1A2
.    Global Change RDPTDGW3  to the Read on Alpha Search Index    eg RDDOCT2
.    Global Change RKPTDGW3 to the Read Next Alpha Search Index  eg RDKDOCT2
.    Global Change RPPTDGW3 to the Read Prev Alpha Search Index  eg RDPDOCT2
.    Global Change RSPTDGW3 to the Read Post Alpha Search Index  eg RDSDOCT2
.    Global Change PTDWDRGN to the Code Field from the Table     eg DCODE
.    Global Change PTDWDESC to the Name Field from the Table     eg DSNAME
.    Global Change KEY63    to the Key for the Aplha Index       eg KEY26
.    Global Change patkdraf to name of Key Word Table            eg patdkiaf
.    Global Change PATKDRA1 to index 1 of Key Word Table         eg PATDKIA1
.    Global Change PATKDRA2 to index 2 of Key Word Table         eg PATDKIA2
.    Global Change PTKDITM to Key Word Index Code Field         eg PTDKDOC
.    Global Change PTKDKWD to Key Word Index Word Field         eg PTDKKWD
.    Global Change HPTKDA   to Routine Label                     eg HPTDKA00
.    Global Change KEY18    to the Key needed for Key Word Table eg KEY21
.    Global Change KEY3    to the Key for the Code Field        eg KEY6
.    Global Change RDPTDGW1 to the Read on Master Table          eg RDDOCT1
.    Global Change PTKD     to the IO Call ID for the Key Words  eg PTDK
.    Global Change ALLACTV to the field to Indicate Inactive Code eg DRSTAT
.
.    Modify List Display to Suit
.
.    Note : vi Global change command   :%s/FromString/ToString/g
.  
.  A Alphabetic Index on a Master Table is Defined in the following Manner
.                              
.  PTDWDRGN    DIM       xx    * Code Field 
.  PTDWDESC    DIM       xx    * Name or Description Field
.
.------------------------------------------------------------
           DEFPROC   HPTKDA00
.
           INC       STDKWSDF
           INC       PATKDRFD/INC
.
ALLACTV   FORM      1
SAVKEY19  DIM       19       * Key Word Table Index 
SAVKEY4   DIM       4        * Code Length
.------------------------------------------------------------
. Search 
.------------------------------------------------------------
HPTKDA00  MOVE      CKEYSUSP,SHOWSUSP
          MOVE      CKEYORDR,SEARCHTY
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATKDRA1,"patkdraf"
          TRAPCLR   IO
          IF        OVRCD=1
            BEEP
            MOVE      "ERROR: Search Table Missing (patkdraf)- ",DISPMSG
            CALL      MESSAGE1
            GOTO      HPTKDAZZ
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATDGWA3,"patdgwaf"
          TRAPCLR   IO
          IF        OVRCD=1
            BEEP
            MOVE      "ERROR: Search Table Missing (patdgwaf)- ",DISPMSG
            CALL      MESSAGE1
            GOTO      HPTKDAZZ
          ENDIF
.
          OPEN      PATKDRA2,"patkdraf"
          MOVE      ZERO,CSCRSIZ
.
          MOVE      "08",HTOP
          MOVE      "24",HBOT
          MOVE      "01",HLEF
          MOVE      "80",HRIG
.
HPTKDA10  CALL      KWINT000
          BRANCH    EXIT,HPTKDA99
          MOVE      STDCOL00,STDCOL01
          ADD       " 5",STDCOL01
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON,"DRG Code"            
          MOVE      STDCOL01,STDCOL02
          ADD       "9",STDCOL02
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"Description",*HOFF
          MOVE      STDCOL02,STDCOL03
          ADD       "60",STDCOL03
.
          CALL      SHWFLG00                * Display Flags
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
HPTKDA20  IF        SEARCHTY=1
            CALL      ALPSRH00              * Alphabetic Search
          ELSE
            CALL      KEYSRH00              * Key Word Search
          ENDIF
          BRANCH    DOAGAIN,HPTKDA10,HPTKDA20
.
          PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
.
HPTKDA99  CLOSE     PATKDRA1
          CLOSE     PATKDRA2
          GOTO      HPTKDAZZ           * End Procedure
.------------------------------------------------------------
. Perform Key Word Search
.------------------------------------------------------------
KEYSRH00  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    STDROW+1,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-13,FLAGCOL      
          DISPLAY   *PFLAGCOL:FLAGROW,*V2LON,*ULON,"Enter Key Words for Search"
          ASSIGN    STDROW+3,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-30,FLAGCOL
          KEYIN     *PFLAGCOL:FLAGROW,*V2LON,KEYWRD00
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ENDSET    KEYWRD00
          APPEND    SP70,KEYWRD00
          RESET     KEYWRD00
          CALL      GETWRD00     * Break Words into Seprate Fields
.
KEYSRH09  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          MOVE      SP70,PTKDITM 
.
          STRIP     KEYWRD01
          PACK      KEY19,KEYWRD01,SP70
          CALL      RSPTKD2 
KEYSRH10  CALL      RKPTKD2 
          BRANCH    OVRCD,KEYSRH15
.
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH10,KEYSRH15
.
          ADD       ONE,SELITMNO
          ADD       ONE,STDROW
          PACK      KEY19,PTKDKWD,PTKDITM
          STORE     KEY19,SELITMNO,ST1,ST2,ST3,ST4,ST5:                        
                                 ST6,ST7,ST8,ST9,ST10:
                                 ST11,ST12,ST13,ST14,ST15:
                                 ST16,ST17
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,SELITMNO,DOT;
          IF        CHECKSUS<>0
            DISPLAY   *V2LON,*BLINKON,"*"
          ENDIF
          DISPLAY   *PSTDCOL01:STDROW,PTKDITM
.
          DISPLAY   *PSTDCOL02:STDROW,PTDWDESC
. 
          COMPARE   MAXITEM,SELITMNO
          GOTO      KEYSRH10 IF NOT EQUAL
.
KEYSRH15  MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
.
KEYSRH20  CALL      KWSEL000
          BRANCH    EXIT,KEYSRH30,KEYSRH35,KEYSRH40,KEYSRH45,KEYSRH98:
                                  KEYSRH91,KEYSRH00,KEYSRH70
.                        Next     Last     Home     End      Expand/Reduce
.                                 Exit     Search   Flags
          GOTO      KEYSRH92
.
KEYSRH30  CALL      RKPTKD2 
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH30,KEYSRH85
.
          PACK      KEY19,PTKDKWD,PTKDITM,SP70                                  
          CALL      RPPTKD2 
          MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      KEYSRH10
.
KEYSRH35  MOVE      ST1,KEY19
          CALL      RDPTKD2 
.
KEYSRH36  CALL      RPPTKD2 
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH36,KEYSRH85
          PACK      KEY19,PTKDKWD,PTKDITM,SP70                                  
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
.
KEYSRH37  CALL      RPPTKD2 
          BRANCH    OVRCD,KEYSRH38
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH37,KEYSRH38
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      KEYSRH37 IF NOT EQUAL
.
KEYSRH38  MOVE      ZERO,SELITMNO
          GOTO      KEYSRH10
.
KEYSRH40  MOVE      ST1,KEY19
          CALL      RDPTKD2 
KEYSRH41  CALL      RPPTKD2 
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH41,KEYSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      KEYSRH09
.
KEYSRH45  CALL      RKPTKD2 
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH45,KEYSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,SELITMNO
          PACK      KEY19,KEYWRD01,Z70                                          
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
          CALL      RSPTKD2 
.
KEYSRH46  CALL      RPPTKD2 
          BRANCH    OVRCD,KEYSRH47
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH46,KEYSRH47
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      KEYSRH46 IF NOT EQUAL
.
KEYSRH47  MOVE      ZERO,SELITMNO
          GOTO      KEYSRH10
.
KEYSRH70  CALL      FLAGS000
          MOVE      TWO,DOAGAIN
          GOTO      KEYSRH99
.
KEYSRH85  LOAD      KEY19,SELITMNO,ST1,ST2,ST3,ST4,ST5:                         
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY19,PTKDKWD,PTKDITM
          CALL      RDPTKD2 
.
KEYSRH90  BEEP
          GOTO      KEYSRH20
.
KEYSRH91  MOVE      SP70,KEY19
          MOVE      SP70,PTKDITM 
          MOVE      SP70,PTKDKWD 
          MOVE      ONE,OVRCD
          GOTO      KEYSRH96
.
KEYSRH92  MOVE      SP70,KEY19
          MOVE      SP70,PTKDITM 
          UNPACK    SP70,PTDWDRGC,PTDWDRGV                            *I-137125
          LOAD      KEY19,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY19,PTKDKWD,PTKDITM
          MOVE      PTKDITM,KEY4
          PACK      KEY7,KEY4,DRGVERZ,SP10                            *I-137125
          CALL      RDPTDGW1
.
KEYSRH96  MOVE      ZERO,DOAGAIN
          GOTO      KEYSRH99
.
KEYSRH98  MOVE      ONE,DOAGAIN
.
KEYSRH99  RETURN
.------------------------------------------------------------
. Perform Alphabetic Search
.------------------------------------------------------------
ALPSRH00  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    STDROW+1,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-10,FLAGCOL
          DISPLAY   *PFLAGCOL:FLAGROW,*V2LON,*ULON,"Enter Name for Search"
          ASSIGN    STDROW+3,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-30,FLAGCOL
          KEYIN     *PFLAGCOL:FLAGROW,*V2LON,KEYWRD00
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ENDSET    KEYWRD00
          APPEND    SP70,KEYWRD00
          RESET     KEYWRD00
.
ALPSRH09  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          MOVE      SP70,PTDWDRGC 
.
          STRIP     KEYWRD00
          PACK      KEY67,KEYWRD00,SP70               * was KEY64     *C-137125
          CALL      RSPTDGW3 
ALPSRH10  CALL      RKPTDGW3 
          BRANCH    OVRCD,ALPSRH15
.
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH10,ALPSRH15
.
          ADD       ONE,SELITMNO
          ADD       ONE,STDROW
          PACK      KEY67,PTDWDESC,PTDWDRGC,DRGVERZ                   *C-137125
          STORE     KEY67,SELITMNO,ST1,ST2,ST3,ST4,ST5:               *C-137125
                                 ST6,ST7,ST8,ST9,ST10:
                                 ST11,ST12,ST13,ST14,ST15:
                                 ST16,ST17
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,SELITMNO,DOT;
          IF        CHECKSUS<>0
            DISPLAY   *V2LON,*BLINKON,"*"
          ENDIF
          DISPLAY   *PSTDCOL01:STDROW,PTDWDRGC
.
          DISPLAY   *PSTDCOL02:STDROW,PTDWDESC
. 
          COMPARE   MAXITEM,SELITMNO
          GOTO      ALPSRH10 IF NOT EQUAL
.
ALPSRH15  MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
.
ALPSRH20  CALL      KWSEL000
          BRANCH    EXIT,ALPSRH30,ALPSRH35,ALPSRH40,ALPSRH45,ALPSRH98:
                                  ALPSRH91,ALPSRH00,ALPSRH70
.                        Next     Last     Home     End      Expand/Reduce
.                                 Exit     Search   Flags
          GOTO      ALPSRH92
.
ALPSRH30  CALL      RKPTDGW3 
          BRANCH    OVRCD,ALPSRH85
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH30,ALPSRH85
.
          PACK      KEY67,PTDWDESC,PTDWDRGC,SP70      * was KEY64     *C-137125
          CALL      RPPTDGW3 
          MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      ALPSRH10
.
ALPSRH35  MOVE      ST1,KEY67                         * was KEY64     *C-137125
          CALL      RDPTDGW3 
.
ALPSRH36  CALL      RPPTDGW3 
          BRANCH    OVRCD,ALPSRH85
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH36,ALPSRH85
          PACK      KEY67,PTDWDESC,PTDWDRGC,SP70      * was KEY64     *C-137125
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
.
ALPSRH37  CALL      RPPTDGW3 
          BRANCH    OVRCD,ALPSRH38
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH37,ALPSRH38
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      ALPSRH37 IF NOT EQUAL
.
ALPSRH38  MOVE      ZERO,SELITMNO
          GOTO      ALPSRH10
.
ALPSRH40  MOVE      ST1,KEY67                         * was KEY64     *C-137125
          CALL      RDPTDGW3
ALPSRH41  CALL      RPPTDGW3 
          BRANCH    OVRCD,ALPSRH85
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH41,ALPSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      ALPSRH09
.
ALPSRH45  CALL      RKPTDGW3 
          BRANCH    OVRCD,ALPSRH85
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH45,ALPSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,SELITMNO
          PACK      KEY67,KEYWRD01,Z70                * was KEY64     *C-137125 
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
          CALL      RSPTDGW3 
.
ALPSRH46  CALL      RPPTDGW3 
          BRANCH    OVRCD,ALPSRH47
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH46,ALPSRH47
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      ALPSRH46 IF NOT EQUAL
.
ALPSRH47  MOVE      ZERO,SELITMNO
          GOTO      ALPSRH10
.
ALPSRH70  CALL      FLAGS000
          MOVE      TWO,DOAGAIN
          GOTO      ALPSRH99
.
ALPSRH85  LOAD      KEY67,SELITMNO,ST1,ST2,ST3,ST4,ST5:
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY67,PTDWDESC,PTDWDRGC                                       
          CALL      RDPTDGW3 
.
ALPSRH90  BEEP
          GOTO      ALPSRH20
.
ALPSRH91  MOVE      SP70,PTDWDRGC 
          MOVE      SP70,KEY67                                        *C-137125
          MOVE      SP70,PTDWDESC 
          MOVE      ONE,OVRCD
          GOTO      ALPSRH96
.
ALPSRH92  MOVE      SP70,KEY67                                        *C-137125
          MOVE      SP70,PTDWDRGC 
          LOAD      KEY67,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY67,PTDWDESC,PTDWDRGC                           *C-137125
          MOVE      PTDWDRGC,KEY4
          PACK      KEY7,KEY4,DRGVERZ,SP10                            *I-137125
          CALL      RDPTDGW1
.
ALPSRH96  MOVE      ZERO,DOAGAIN
          GOTO      ALPSRH99
.
ALPSRH98  MOVE      ONE,DOAGAIN
.
ALPSRH99  RETURN
.------------------------------------------------------------
. Alter Flags
.------------------------------------------------------------
FLAGS000  BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    MINITEM+2,F2
FLAGS110  MOVE      "N",ANS
          KEYIN     *PSTDCOL02:F2,"Show Inactive (",*V2LON,"Y",*HOFF,"/":
                                  *V2LON,"N",*HOFF,")    : ",*V2LON,*RV,ANS
          REP       UPPLOW,ANS
          MOVE      ONE,SHOWSUSP
          MATCH     "Y",ANS
          GOTO      FLAGS190 IF EQUAL
          MATCH     "N",ANS
          GOTO      FLAGS110 IF NOT EQUAL
          MOVE      ZERO,SHOWSUSP
FLAGS190  ASSIGN    MINITEM+3,F2
FLAGS210  MOVE      "K",ANS
          KEYIN     *PSTDCOL02:F2,*V2LON,"K",*HOFF,"ey Word or ":
                                  *V2LON,"A",*HOFF,"lphabetic Search : ":
                                  *V2LON,*RV,ANS
          REP       UPPLOW,ANS
          MOVE      ZERO,SEARCHTY
          MATCH     "K",ANS
          GOTO      FLAGS290 IF EQUAL
          MATCH     "A",ANS
          GOTO      FLAGS210 IF NOT EQUAL
          MOVE      ONE,SEARCHTY
FLAGS290
          CALL      SHWFLG00
          RETURN 
.------------------------------------------------------------
. Display Flag Settings
.------------------------------------------------------------
SHWFLG00  CLEAR     FLAGDESC
          IF        SHOWSUSP=1
            APPEND    "Show Inactive, ",FLAGDESC
          ELSE
            APPEND    "Hide Inactive, ",FLAGDESC
          ENDIF
          IF        SEARCHTY=1
            APPEND    "Alphabetic Search",FLAGDESC
          ELSE
            APPEND    "Key Word Search",FLAGDESC
          ENDIF
          RESET     FLAGDESC
.
          ASSIGN    HBOT,FLAGROW
          MOVELPTR  FLAGDESC,F2
          ADD       "9",F2
          ASSIGN    ((HRIG-HLEF)/2)-(F2/2),FLAGCOL
          MOVELPTR  FLAGDESC,F2
          SFORMAT   DISPVAR,F2
          MOVE      FLAGDESC,DISPVAR
          DISPLAY   *PFLAGCOL:FLAGROW,*V2LON,*ULON," Flags: ",DISPVAR,SP1
          SFORMAT   DISPVAR,127
.
          RETURN
.------------------------------------------------------------
. Check Record
.------------------------------------------------------------
CHREC000  BRANCH    SEARCHTY,CHREC100
          MATCH     KEYWRD01,PTKDKWD
          GOTO      CHREC920 IF NOT EQUAL                                       
          IF        KEYWRDNO>1
            PACK      SAVKEY19,PTKDKWD,PTKDITM
            CALL      MCHWRD00
            BRANCH    EXIT,CHREC910
            MOVE      SAVKEY19,KEY19
            CALL      RDPTKD2
          ENDIF
.
          MOVE      PTKDITM,KEY4
          PACK      KEY7,KEY4,DRGVERZ,SP10                            *I-137125
          CALL      RDPTDGW1
          BRANCH    OVRCD,CHREC910
          GOTO      CHREC800
.
CHREC100  MATCH     KEYWRD00,PTDWDESC
          GOTO      CHREC920 IF NOT EQUAL                                       
.
CHREC800  MOVE      ZERO,ALLACTV
          MOVE      ALLACTV,CHECKSUS
          BRANCH    SHOWSUSP,CHREC900
          BRANCH    ALLACTV,CHREC910
.
CHREC900  MOVE      ZERO,OVRCD
          GOTO      CHREC999
CHREC910  MOVE      ONE,OVRCD
          GOTO      CHREC999
CHREC920  MOVE      TWO,OVRCD
CHREC999  RETURN
.------------------------------------------------------------
. Check for Matching Words
.------------------------------------------------------------
MCHWRD00  PACK      SAVKEY4,PTKDITM
          MOVE      ONE,F2
          MOVE      ZERO,EXIT
.
MCHWRD10  COMPARE   KEYWRDNO,F2
          GOTO      MCHWRD99 IF EQUAL
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY19,SAVKEY4,KEYWRDXX
          CALL      RDPTKD1
          COMPARE   ZERO,OVRCD
          GOTO      MCHWRD10 IF EQUAL
.
          CALL      RKPTKD1
          BRANCH    OVRCD,MCHWRD90
          PACK      KEY4,PTKDITM,SP70
          MATCH     SAVKEY4,KEY4 
          GOTO      MCHWRD90 IF NOT EQUAL
.
          STRIP     KEYWRDXX
          MATCH     PTKDKWD,KEYWRDXX
          GOTO      MCHWRD90 IF NOT EQUAL
          GOTO      MCHWRD10
.
MCHWRD90  MOVE      ONE,EXIT
MCHWRD99  RETURN
.
          INC       PATKDRIO/INC
          INC       IBAOVRIO/INC
          INC       STDKWSCD
.
HPTKDAZZ  ENDPROC   
