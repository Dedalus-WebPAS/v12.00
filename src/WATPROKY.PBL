
.* Include Name   : WATPROKY.PBL                                              *
.*                                                                            *
.* Description    : Keyin of a Waiting List Procedure Code from WATPROFD      *
.*                                                                            *
.* Author         : LOFTY                   26/10/1995                        *
.*                                                                            *
.* Parameters     : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF9 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                                                                            *
.* Returns        : WPCODE   - Keyed in code                                  *
.*                  WPDESC   - Description                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing  input                                 *
.*                       = 2   EXITCHAR input                                 *
.*                       = 3   Spaces   input                                 *
.*                                                                            *
.* On return      : You must display the description yourself.                *
.* Mods           : 
.*        V5.07     26/10/1999  J Habasque                                    *
.*                  Added DISPACT - to not display inactive codes             *
.*                                                                            *
.******************************************************************************
WATPROKY  MOVE      ONE,CQUEST              * set ? as not entered
          MOVE      ECOL,CCOL
          MOVE      EVERT,CVERT
.
KWTPR100  MOVE      CKYIDEF9,WPCODE
          KEYIN     *PCCOL:CVERT,*V2LON,*RV,WPCODE:
                    *PCCOL:CVERT,*DV,WPCODE
          RESET     WPCODE
          GOTO      KWTPR600 IF EOS         * nothing entered
          PACK      WPCODE,WPCODE,SP9
          MATCH     SP6,WPCODE
          GOTO      KWTPR650 IF EQUAL       * spaces entered
          MATCH     EXITCHAR,WPCODE
          GOTO      KWTPR820 IF EQUAL       * exitchar entered
          MATCH     QUEST,WPCODE
          GOTO      KWTPR200 IF NOT EQUAL   * ? not entered
.
.         ? entered
.
          MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
          MOVE      ZERO,DISPACT
          CALL      WATDSPRO                * display valid entries
          DISPLAY   *P1:24,"Input the code : ",*EL;
          MOVE      "24",CVERT
          MOVE      "18",CCOL
          MOVE      ZERO,CQUEST             * set ? as entered
          GOTO      KWTPR100
.
.         validate what was entered
.
KWTPR200  MOVE      WPCODE,KEY9
          CALL      RDPROA1
          BRANCH    OVRCD,KWTPR400          * invalid code
.
          MOVE      ZERO,EXIT               * set as valid code
          GOTO      KWTPR800
.
.         invalid code
.
KWTPR400  DISPLAY   *P1:24,*B,"Invalid Code.  ",*EL;
          CALL      HITENTER
          BRANCH    CQUEST,KWTPR100
          DISPLAY   *P1:24,"Input the code : ",*EL;
          GOTO      KWTPR100
.
.         nothing entered
.
KWTPR600  MOVE      ONE,EXIT                * set exit
          GOTO      KWTPR700
.
.         spaces entered
.
KWTPR650  MOVE      THREE,EXIT               * set exit flag
.
KWTPR700  PACK      WPDESC,SP30,SP30,SP30    * clear desc
          COMPARE   ZERO,CKYIMAND
          GOTO      KWTPR800 IF EQUAL       * valid entry
.
.         field is mandatory
.
          DISPLAY   *P1:24,*B,"Field is mandatory.  ",*EL;
          CALL      HITENTER
          BRANCH    CQUEST,KWTPR100
          DISPLAY   *P1:24,"Input the code : ",*EL;
          GOTO      KWTPR100
.
.         valid entry
.
KWTPR800  IF        CQUEST = ZERO
            CALL      PUTSCR00                * redisplay screen
          ENDIF
          DISPLAY   *PECOL:EVERT,*V2LON,WPCODE
          GOTO      KWTPR999 
.
.         exitchar entered
.
KWTPR820  MOVE      TWO,EXIT                * EXITCHAR entered
          MOVE      SP9,WPCODE              * clear code
          PACK      WPDESC,SP30,SP30,SP30   * clear desc
          IF        CQUEST = ZERO
            CALL      FRESCR00                * free the screen
          ENDIF
.
KWTPR999  RETURN
+
          INC       WATDSPRO                * ? mark routine
