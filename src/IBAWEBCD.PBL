.------------------------------------------------------------
. Include File   : IBAWEBCD.PBL
.
. Function       : Standard Routines used by all Web Server Programs
.
. Author         : B.G.Ackland
.
. Modifications  :
.------------------------------------------------------------------------------
. V11.05.01  03/02/2025  Nikitha B       TSK 0953087
.            Added a new standard tag 46 for WBSEHCPC : HCP Code
.------------------------------------------------------------------------------
. V11.03.01  02/12/2022  Peter Vela     ClinicalAide Integration
.            Added FHIRUSER to indicate different user in fhir json header
.------------------------------------------------------------------------------
. V11.01.01  09/03/2021  Thanh T         TSK 0891886 
.            Changed COMPARVL to check for Inactive status 
. V10.11.04  23/11/2020  Ebon Clements   TSK 0876663                 
.            Added common parameters file standard tag 45 - DEFPROC COMPARVL
. V10.11.03  03/04/2020  Thanh T         TSK 0879163                 
.            Added SEXDSP00 routine
. V10.11.02  Jill Parkinson              TSK 0879163                 
.            Added SEXDSC00 routine
. V10.11.01  18/08/2017  Ania P          TSK 0843179
.            Added CFLONG00
.------------------------------------------------------------------------------
. V10.10.02  03.05.2017  B.G.Ackland     TSK 0835482
.            Include Version Control Data in FHIR Outcome Resource Types
. V10.10.01  02/05/2017  Ebon Clements   TSK 0832472
.            Added using web user id for application authentication - PAGSEC00
.------------------------------------------------------------------------------
. V10.09.01  09.12.2016  B.G.Ackland     TSK 0829696
.            Added FHIRTYPE Flag to indicate FHIR Output
.            Added FHIR OperaitonOutcome for Success and Error Returns
.------------------------------------------------------------------------------
. V10.08.05  19/09/2016  Peter McMullen  0826151               
.            Check user is active an WEBRED00                    
. V10.08.04  23/08/2016  Peter McMullen  0823848               
.            Add lower case login search to XMLRED00             
. V10.08.03  20/06/2016  Peter McMullen  0821135               
.            Remove lower case update of wbselogn (if apache file
.            authentication)                              
. V10.08.02  27/04/2016  Jill Parkinson TSK 0817063
.            Added *+ to output of PTCDLDES to remove trailing whitespace
. V10.08.01  08/04/2016  Jill Parkinson TSK 0279181
.            Added ORCACT00 and ORCCLI00 for Oracle audit use
. V10.07.03  07/01/2016  Ebon Clements  TSK 0294154
.            Updated INVALID SECURITY error - WEBRED90
. V10.07.02  04/12/2015  Don Nguyen     CAR 322481
.            Added 'id' to meta tag output in WEBHED00
. V10.07.01  26.10.2015  Peter Mc       CAR 323085
.            Match LOGINAME in lower case only. Convert WBSELOGN to lower case
.            if found in mixed case (no fixit program required). 
. V10.05.02  21.01.2015  Ebon Clements  CAR 310006
.            Changed SECTEM00 to read websecaf using USERID variable
. V10.05.01  19.12.2014  B.G.Ackland  CAR 310348
.            Escape ! in username and password verification
.------------------------------------------------------------------------------
. V10.04.04  10.04.2014  B.G.Ackland  CAR 299363
.            Revision to Redirect Options
.            Remove RDELAY00 routine not used. Should be RDIREC00 or RSAVED00
.            Reduce Timeout to 200ms on RSAVED00 and RFRESH00
. V10.04.03  09.05.2014  Peter McMullen  CAR 300563
.            Reinstate old version of std tag 8, add std tag 43 for login name
. V10.04.02  31.03.2014  B.G.Ackland  CAR 299363
.            META Tag Redirect with Javascript in Common RDIREC00 Routine
. V10.04.01  14.03.2014  Peter McMullen    CAR 294083 
.            Add support for login with long name (loginame)
.------------------------------------------------------------------------------
. V10.03.18  20.11.2013  Ebon Clements     CAR 212716
.            Added CATACTIV cgi - Only display active category codes
. V10.03.17  14.08.2013  Peter McMullen    CAR 289602
.            Add cgivar LOGINAME to get apache remote user via cfg file
. V10.03.16  05/07/2013  Jill Parkinson    CAR 287923
.            Moved PTCNUHSC to WEBINT00, added trap description to OPENHTML
. V10.03.15  12/06/2013  Peter McMullen    CAR 286727
.            Escape "`" (backtic) in uid or passwd to avoid shell var conflict
. V10.03.14  12/06/2013  Peter McMullen    CAR 286727
.            Escape $ in username or password to avoid shell variable conflict  
. V10.03.13  02/05/2013  Don Nguyen        CAR 284693
.            Modified TITLESEL and RELATSEL to test for active indicator
. V10.03.12  07.03.2013  B.G.Ackland       CAR 282508
.            WA Test Shared Fifo Trigger Method Change Handling of WEBFIFO1
. V10.03.11  26.02.2013  Peter McMullen    CAR 271555
.            Add new common routine VFYUSR00 and change CHKPAS00 to call it
. V10.03.10  29.11.2012  Patrick Adair     CAR 244064
.            STDA4200 - Web User Server Security Level
. V10.03.09  12.07.2012  Don Nguyen        CAR 270231
.            STDA4100 - Web Security Emergency Site Code
. V10.03.08  12.07.2012  B.G.Ackland
.            STDA4000 - Web Security Nurse Code
. V10.03.07  11/04/2012  Don Nguyen        CAR 261277
.            Appended template filename to error message upon open failure
. V10.03.06  21/03/2012  Peter McMullen    CAR 259587
.            Remove include for redundant patyears file
. V10.03.05  07/03/2012  Jill Parkinson    CAR 261444
.            Added include and open of ibapdfaf in LABELSEL
. V10.03.04  13/02/2012  Saroeun Soeur     CAR 260039
.            added STDA3900 - template title description
. V10.03.03  07/02/2012  Jill Parkinson    CAR 254812
.            Added output of PTHSNAME as a common tag
. V10.03.02  19/12/2011  Ebon Clements     CAR 257425
.            Changed WINENQ00 to redirect top content page
. V10.03.01  05/12/2011  Ebon Clements     CAR 248529
.            Added clear of ADMISSNO and URNUMBER to WEBRED10
. V10.02.03  27/09/2011  Jill Parkinson    CAR 248486
.            Added hospital security functionality
. V10.02.02  01/09/2011  Ebon Clements     CAR 242014
.            Added outpatient direct tag
. V10.02.01  29/03/2011  Jill Parkinson    CAR 239574
.            Added WEBERRIO
. V10.01.01  15/11/2010  Steve Armstrong   CAR 237753
.            Mods to WEBHED00 to cater for 10 template directories
. V10.00.01  25/05/2010 Saroeun Soeur  CAR 222635
.            Fixed bug on boundary condition when user has no default label
.            selection list is empty. LABELSEL
. V9.12.02   15/10/2009 Jill Parkinson CAR 206624
.            Fixed {} in WINEXT00
. V9.12.01   24/09/2009 Saroeun Soeur CAR  205504
.            modified LABELSEL to retain label selection
. V9.11.04   04/03/2009 Jill Habasque CAR  134681
.            Added { in WINALT00 before else
. V9.11.03   15/01/2009 Ebon Clements CAR  175204
.            Added standard tags for printing defaults:
.            - default label (stda3600)
. V9.11.02   29/12/2008 Peter Vela      CAR 183976
.            Added HFR (pathfraf) to system lock tables
. V9.11.01   29/19/2008 Peter McMullen  CAR 91011
.            Change WINCLS to use external close/reload procedure
. V9.10.03   12/08/2008 J.Tan         CAR 175949
.            Mods to read User id (websecaf) to get department before SELPRT00
. V9.10.02   23/07/2008 Peter McMullen  CAR 174726
.            Added id tags to PopUpFrame and PopUpFrame1
. V9.10.01   15/05/2008 Jill Habasque CAR 161608
.            Added standard tags for patcodes long description
. V9.09.02   18/10/2007 Davin         CAR  151903
.            Added 2 standard tags for printing defaults:
.            - default no. of copies (stda3300)
.            - default document code (stda3400)
. V9.09.01   18/07/2007 Jill Habasque CAR  145458
.            Mods to allways read websecaf before outputting wbse variables
. V9.08.01   08/02/2007 Jill Habasque CR 133135
.            Mods to allow extra template directories
. V9.05.02   31/03/2006 Ebon Clements CAR 78527
.            Added WBSEHOSP standard tag
. V9.05.01   Ebon Clements CAR 78533
.            Added MLTCODIO
. V9.05.B00  J.Tan  CAR 96072
.            Added new standard tag for yesterday date
. V9.04.02   20.09.2005 Ebon Clements CAR 76986
.            Created XMLRED00 from WEBRED00 for remote script servers
. V9.04.01   31.08.2005 Ebon Clements CAR 72916
.            Added date/time to fifo not found error - WEBHED00
. 07.07.2005 Peter Vela    CAR 66408
.            Added condition for active/inactive Stationery Template STDAOS00
. 10.06.2005 Jill Habasque CAR 61761
.            Added new standard tag title and relationship
. 13.05.2005 Ebon Clements CAR 60929
.            Added new standard tag for user allied health department
. 11.03.2005 Jill Habasque CAR 58443
.            Added new standard tag for label selection list
. 14.02.2005 Ebon Clements CAR 56675
.            Added standard tag for clinic id
. 11.01.2005 Lina Vo       CAR 50670
.            Fixed LABELSEL to display code correctly in key10
. 28.09.2004 Davin         CAR 53266
.            Added standard control file parameter routine (conflp00)
. 16.09.2004 Jill Habasque CAR 53540
.            Added a output of single label type
. 28.11.2003 Sylvek Litewka  CAR 20222
.            Added clear for variables ICDRDDTE and ICDRDVER.
. 12.08.2003 Sylvek Litewka  CAR 41293
.            Added WEBSCHIO include and OPEN of webschaf. Added procedures
.            SPLSCH00, NXTSNO00 and CLRSCH00 to WEBPRTCD.PBL.
. 11.07.2003 Lina Vo       CAR 41196
.            Added new standard tag for comcodaf using current date.
. 06.06.2003 Jill Habasque SRF 38184
.            Added a output of tmotfrst and tmotnext
. 10.04.2003 Ebon Clements SRF 37904
.            Added a standard tag for the users outpatient site
. 25.10.2002 Ebon Clements SRF 22413
.            Removed old timout functions
.
. 27.09.2002 Pat Dirito
.            Fixed routine STDA0000 had incorrect labels set..Added call to
.            routine STDAOS00.
.
. 29.06.2002 Sandra Barcham
.            Add FILENAM to fifo error
.            Add Trap SIGPIPE
.
. 20.06.2002 Sandra Barcham
.            Stop Deadlock
.
. 08.03.2002 Pat Dirito
.            Added routine POPERR00

. 14.01.2002 B.G.Ackland
.            Change Timeout routine not to modify LASTDATE variable
.
. 03.12.2001 K.C.Nelson/Harvinder
.            Added initialisation of NHI record read flag and read control
.            file PTCNNHII into NHI system in use flag.
.
. 29.11.2001 J.Habasque
.            Added loop if websecaf is locked
.
. 26.10.2001 B.G.Ackland
.            Changed Open for Servlet Gateway
.
. 26.10.2001 Jill Habasque
.            Added a new standard tag for WBSEDTYP (document type) and
.            WBSEHLOC (home location)
.
. 16.10.2001 Ebon Clements
.            Added a new standard tag - selection list with a default
.
. 27.09.2001 B.G.Ackland
.            Added Transaction Control For Oracle
.
. 27.09.2001 B.G.Ackland
.            Added Transaction Control For Oracle
.
. 18.07.2001 HPS
.            Modified for Encounters (ALLWEB03)
.
. 04.07.2001 Sandra Barcham
.            Modified SETMTH00 to handle Blank Dates
.
. 29.05.2001 Pat Dirito
.            Modified GETTAG00 to handle FLDSETNO as a FORM4
.
. 09.01.2001 B.G.Ackland
.            Added cache control header
.
. 02.11.2000 B.G.Ackland
.            Automatic Inclusion of PopUpFrame division in WEBEND00
.
. 10.10.2000 B.G.Ackland
.            Time Out Security Started to Implement
.
. 01.10.2000 B.G.Ackland
.            Added Forms Security
.            Added Optional Headers for XML/Word/excel/download
.            Added cache control header
.
. 15.03.2000 K.C.Nelson
.            Changed STX to be STXX
.
. 03.03.2000 B.G.Ackland
.            Open Templates in Read Only Mode
.
. 12.01.2000 B.G.Ackland
.            Added Template Name as Title for page if Title is Blank
.
. 17.11.1999 B.G.Ackland
.            Development for New Interface Standards
.
.------------------------------------------------------------
          INC       IBADATE
          INC       IBATIME
          INC       JULCON
          INC       DMYCON
          INC       IBACOMN
          INC       WEBPRTCD
          INC       TIMEDIFF
          INC       CALCDAYS
          INC       COMCODTM
          INC       PATCODTM
          INC       IBAOVRIO/INC
          INC       IBAPORIO/INC
          INC       IBACODIO/INC
          INC       WEBERRIO/INC
          INC       WEBSTEIO/INC
          INC       WEBSECIO/INC
          INC       WEBTPLIO/INC
          INC       COMCODIO/INC
          INC       PATCODIO/INC
          INC       MLTCODIO/INC
          INC       IBAPDFIO/INC
          INC       IBATMHIO/INC
          INC       WEBSCHIO/INC
.------------------------------------------------------------
. Open a XML Reply - NO HTTP HEADERS
.------------------------------------------------------------
XMLHED00  CALL      OPENHTML
          RETURN
.
XMLEND00  CALL      CLOSHTML
          RETURN
.------------------------------------------------------------
. Open a HTML Reply - NO HTTP HEADERS
.------------------------------------------------------------
HTMLOPEN  BRANCH    WGATEWAY,HTMLOP10
          PACK      FILNAM,HTMLDIR,WEBPID,HTMLEXT,SP70
          SQUEEZE   FILNAM
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      HTMLFILE,FILNAM,WRONLY
          TRAPCLR   IO
          MOVE      OVRCD,EXIT
          GOTO      HTMLOP99
.
HTMLOP10  WRITE     HTMLFILE;HTTPHEAD;    * Flag to Servlet HTTP Headers
          MOVE      ZERO,EXIT
.
HTMLOP99  RETURN
.------------------------------------------------------------
. Open a HTML Reply - With Standard HTTP Headers
.------------------------------------------------------------
OPENHTML  BRANCH    WGATEWAY,OPENHT10
          PACK      FILNAM,HTMLDIR,WEBPID,HTMLEXT,SP70
          SQUEEZE   FILNAM
          MOVE      ZERO,OVRCD
          MOVE      SP70,KEY60
          TRAP      OVERCOND GIVING KEY60 IF IO
          OPEN      HTMLFILE,FILNAM,WRONLY
          TRAPCLR   IO
          MOVE      OVRCD,EXIT
          BRANCH    EXIT,OPENHT90
.
          MATCH     SP70,TMOTFRST
          IF        !@EQUAL
            WRITE     HTMLFILE;"<!-- ibacgi timeout_first=#"",TMOTFRST,"#"":
                               " timeout_next=#"",TMOTNEXT,"#""," -->",012;
          ENDIF
          BRANCH    FHIRTYPE,OPENHT20
.
          WRITE     HTMLFILE;"Content-Type: text/html",012:
                             "Cache-Control: no-cache",012,012;
.
          GOTO      OPENHT99
.
OPENHT10  MOVE      ZERO,EXIT
          GOTO      OPENHT99
.
OPENHT20  WRITE     HTMLFILE;"Content-Type: application/json-fhir",012:
                             "Cache-Control: no-cache",012,012;
          GOTO      OPENHT99
.
OPENHT90  RESET     KEY60
          DISPLAY   "*** OPENHT90 ",KEY60," ***";
          GOTO      OPENHT99
.
OPENHT99  RETURN
.------------------------------------------------------------
. Standard Routine to Close a HTML Reply
.------------------------------------------------------------
CLOSHTML  BRANCH    WGATEWAY,CLOSHT10
          CLOSE     HTMLFILE
          GOTO      CLOSHT99
.
CLOSHT10  WRITE     HTMLFILE;STXXEOF;
.
CLOSHT99  RETURN
.------------------------------------------------------------
. Initialize WEB Program
.------------------------------------------------------------
WEBINT00  TRAP      WEBSIG00 IF SIGPIPE
          MOVE      SP70,FIFOMETH
          PACK      SP256,SP70,SP70,SP70,SP70,SP70
          MOVE      SP70,PGM
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*2,CDD,CMM,CYY,CCC
          READ      CONTROLF,TWENTY1;*138,SYSTNHI        * NHI in use Flag
          READ      CONTROLF,TWO;*4,CONAME
          MOVE      CONAME,CNAME
          MOVE      "91",SECTOR
          READ      CONTROLF,SECTOR;*19,CWEBPCMD,*59,CWEBPFIL
          READ      CONTROLF,HUND18;*154,PTCNUHSC    * using hospital security?
          READ      CONTROLF,HUND24;*147,PTCNSUID
.
          CLOCK     PORT,PORT
          MOVE      CDD,CDAY
          MOVE      CMM,CMON
          MOVE      CYY,CYEAR
          MOVE      CCC,CCENT
          OPEN      WEBSTEA1,"websteaf"
          OPEN      WEBSECA1,"websecaf"
          OPEN      WEBSECA7,"websecaf"
          OPEN      WEBTPLA1,"webtplaf"
          OPEN      WEBSCHA1,"webschaf"
          OPEN      WEBSCHA2,"webschaf"
          OPEN      WEBSCHA3,"webschaf"
          OPEN      WEBSCHA4,"webschaf"
.
          STRIP     FIFONAME
          STRIP     CONFNAME
          MOVE      CONFNAME,HTMLBIN
          STRIP     HTMLBIN
          STRIP     RPLYNAME
          MOVE      RPLYNAME,HTMLDIR
          STRIP     HTMLDIR
.
          MOVE      "1",MSGLEN
          OPEN      WEBFIFO1,FIFONAME
          IF        WGATEWAY=1
            OPEN      HTMLFILE,RPLYNAME
          ENDIF
.
.         CALL      ORCACT00     * Initialise the Oracle client info for audits
          TRANM     MANUAL
.
          BEGIN
.
          RETURN
.
WEBSIG00  CHAIN     "WRESTART"
          STOP
.------------------------------------------------------------
. Read WEB Fifo for User ID and Process ID
.------------------------------------------------------------
WEBRED00  COMMIT
.
WEBRED10  CALL      CLRPAR00              * Clear Parameters in Program
.
          MOVE      SP256,SECUREID        * Clear Page Level Security Parameters
          MOVE      SP256,SECUREPW
          MOVE      ZERO,SECURETY
          MOVE      ZERO,ALLOWAHS
          MOVE      ZERO,REPORTNO
          MOVE      SP70,TMOTFRST
          MOVE      SP70,TMOTNEXT
          MOVE      SP70,TEMPLATE
          MOVE      SP70,ICDRDDTE
          MOVE      ZERO,ICDRDVER
          MOVE      SP70,ADMISSNO
          MOVE      SP70,URNUMBER
          MOVE      ZERO,CATACTIV
          MOVE      ZERO,FHIRTYPE
          MOVE      SP256,FHIRUSER
.
WEBRED20  DISPLAY   *R,"<";                  * Log File Heart Beat
          MOVE      SP70,WEBPID           * Process ID
          MOVE      SP70,USERID           * User
          READ      WEBFIFO1;ANS,WEBPID,USERID;         * Fifo Read
          DISPLAY   ">"                   * Log File Heart Beat
          MATCH     STXXPING,ANS          * Check for Ping
          GOTO      WEBRED91 IF EQUAL     * Yes
          MATCH     STXXEXIT,ANS          * Check for Exit
          GOTO      WEBRED92 IF EQUAL     * Yes
          MATCH     STXX,ANS              * Wait for Start of Message Sync
          GOTO      WEBRED20 IF NOT EQUAL * Loop No Start of Message Sync
.
          MOVE      ZERO,NHIREAD          * NHI Record Not Read
.
          CALL      GETQRY00              * Get HTTP Request Details
.
          COMPARE   "999",REPORTNO        * End Server Process Report Option 999
          GOTO      WEBRED92 IF EQUAL
.
          MOVE      ZERO,WRPTSECT
          MOVE      SP70,WBSEUID
          MOVE      SP70,WBSENAM
          MOVE      SP70,WBSEDOC
          MOVE      SP70,WBSEPCD
          MOVE      SP70,WBSEWRD
.
          CALL      IBACLOCK               * Clock Current Date
          CLOCK     TIME,CTIMEIS
.
          IF        SECURETY<>0
            CALL      PAGSEC00
            BRANCH    EXIT,WEBRED00
            MOVE      WBSEPCD,PASSCODE     * Set Passcode for Audits & Security
            GOTO      WEBRED93             * NO Time Out Checks - Page Security
          ENDIF
.
.         *** Check for FHIR User
.
WEBRED21  MATCH     SP256,FHIRUSER
          IF        !@EQUAL & !@EOS
            PACK      LOGINAME,FHIRUSER,SP256
          ENDIF
.
.         *** Note: USERID from the FIFO is not reliable from v10.04  
.
          PACK      KEY80,LOGINAME
          CALL      RDWBSE7
          BRANCH    OVRCD,WEBRED25
          GOTO      WEBRED40
.
.         try again in lower case
.
WEBRED25  PACK      KEY80,LOGINAME
          REPLACE   LOWUPP,KEY80          * xlate to lower case
          CALL      RDWBSE7
          BRANCH    OVRCD,WEBRED90
          GOTO      WEBRED40              * all OK
.
.WEBRED30  PACK      KEY90,LOGINAME
.          REPLACE   LOWUPP,KEY90          * xlate to lower case
.          PACK      KEY80,SP256
.          CALL      RSWBSE7
.WEBRED35  CALL      RKWBSE7
.          BRANCH    OVRCD,WEBRED90
.          REPLACE   LOWUPP,WBSELOGN       * convert file loginame to lowercase
.          MATCH     KEY90,WBSELOGN        * check against loginame
.          GOTO      WEBRED35 IF NOT EQUAL
.
WEBRED40  MATCH     "1",WBSEACT           * Check user is active  
          GOTO      WEBRED80 IF NOT EQUAL
.                   
          MOVE      WBSEUID,USERID        * UserID derived from long login name
          MOVE      WBSEPCD,PASSCODE      * Set Passcode for Audits & Security
.
          MOVE      ZERO,EXIT
          GOTO      WEBRED93     * Time Out Performed in Session
.
WEBRED80  MOVE      "webPAS Security ID Locked",WEBTITLE
          CALL      WEBERR00   * Write HTML Page Header
          GOTO      WEBRED10
.
WEBRED90  MOVE      "webPAS Security ID Not Found",WEBTITLE
          CALL      WEBERR00   * Write HTML Page Header
          GOTO      WEBRED10
.
. Ping Return End of Document
.
WEBRED91  CALL      CLOSHTML   * End of Document
          GOTO      WEBRED10
.
. Shutdown Service
.
WEBRED92  CLOSE     HTMLFILE,DELETE
          CLOSE     WEBFIFO1
          MOVE      ONE,EXITFLAG
          CHAIN     "SHUTDOWN"
.
WEBRED93  MATCH     "1",PTCNUHSC
          GOTO      WEBRED99 IF NOT EQUAL
.
          COMPARE   ONE,ALLOWAHS
          GOTO      WEBRED99 IF EQUAL
.
          MATCH     SP70,ADMISSNO
          IF        !@EQUAL & !@EOS
            MATCH     Z70,ADMISSNO
            IF        !@EQUAL
              PROC      HOSSEC00
              BRANCH    EXIT,WEBRED00
            ENDIF
          ENDIF
.
WEBRED99  
.         CALL      ORCCLI00    * Oracle audit
          BEGIN
.
          RETURN
.
.------------------------------------------------------------
. Page Level Security
.------------------------------------------------------------
PAGSEC00  MATCH     "1",PTCNSUID
          IF        @EQUAL
            PACK      KEY10,SECUREID,SP70   * Using web user id for application
            CALL      RDWBSE1               * authentication
            BRANCH    OVRCD,PAGSEC91
.
            PACK      SECUREID,WBSELOGN,SP256    * re set to login name
          ELSE
            PACK      KEY80,SECUREID,SP256       * lookup SECUREID
            CALL      RDWBSE7
            BRANCH    OVRCD,PAGSEC91
          ENDIF
.
          BRANCH    SECURETY,PAGSEC10,PAGSEC20
          GOTO      PAGSEC90
.
PAGSEC10  MOVE      WBSEUID,USERID   * set USERID from SECUREID lookup
          MOVE      ZERO,EXIT
          GOTO      PAGSEC99
.
PAGSEC20  CALL      CHKPAS00
          BRANCH    EXIT,PAGSEC99
          MOVE      WBSEUID,USERID   * set USERID from SECUREID lookup 
          MOVE      ZERO,EXIT
          GOTO      PAGSEC99
.
PAGSEC90  MOVE      "Invalid Security Type Specified",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PAGSEC99
.
PAGSEC91  MOVE      "Invalid Security ID Specified",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PAGSEC99
.
PAGSEC99  RETURN
.------------------------------------------------------------
. Procedure to check the hospital Security
.------------------------------------------------------------
          DEFPROC  HOSSEC00
.
          INC      MLTSECFD/INC
          INC      BOKRC1FD/INC
          INC      BOKRX1FD/INC
          INC      OUTPREFD/INC
          INC      PATWR1FD/INC
          INC      PATCONFD/INC
          INC      PATHSPFD/INC
          INC      PATVISFD/INC
          INC      PMSVX1FD/INC
          INC      WATTR1FD/INC
.
HOSPCODE  DIM      3
.
          OPEN     BOKRC1A1,"bokrc1af"
          OPEN     BOKRX1A1,"bokrx1af"
          OPEN     MLTSECA1,"mltsecaf"
          OPEN     OUTPREA1,"outpreaf"
          OPEN     PATHSPA1,"pathspaf"
          OPEN     PATWR1A1,"patwr1af"
          OPEN     PATVISA1,"patvisaf"
          OPEN     PMSVX1A1,"pmsvx1af"
          OPEN     WATTR1A1,"wattr1af"
.
HOSSEC05  MOVE      ZERO,EXIT
          MATCH     SP70,ADMISSNO
          GOTO      HOSSEC20 IF EQUAL
          GOTO      HOSSEC20 IF EOS
.
          MATCH     Z70,ADMISSNO
          GOTO      HOSSEC99 IF EQUAL
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,HOSSEC10
.
          MATCH     PMVXMHOS,WBSEHOSP
          GOTO      HOSSEC90 IF NOT EQUAL
.
          GOTO      HOSSEC99
.
HOSSEC10  PACK      KEY8,ADMISSNO,SP70
          CALL      RDBKREC1
          BRANCH    OVRCD,HOSSEC20
          CALL      RDBKRX11
          BRANCH    OVRCD,HOSSEC99
.
          MATCH     SP70,BKREWARD
          GOTO      HOSSEC15 IF EQUAL
.
          PACK      KEY6,BKREWARD,SP70
          CALL      RDWARD1
          BRANCH    OVRCD,HOSSEC99
.
          MATCH     WBSEHOSP,PTWDHOSN
          GOTO      HOSSEC90 IF NOT EQUAL
          GOTO      HOSSEC99
.
HOSSEC15  PACK      KEY6,BKRXPOWD,SP70
          CALL      RDWARD1
          BRANCH    OVRCD,HOSSEC99
.
          MATCH     WBSEHOSP,PTWDHOSN
          GOTO      HOSSEC90 IF NOT EQUAL
          GOTO      HOSSEC99
.
HOSSEC20  GOTO      HOSSEC99
.
HOSSEC90  MOVE      "Invalid Hospital Access",WEBTITLE
          CALL      WINENQ00
          MOVE      ONE,EXIT
          GOTO      HOSSEC99
.
HOSSEC99  CLOSE     MLTSECA1
          CLOSE     BOKRC1A1
          CLOSE     BOKRX1A1
          CLOSE     OUTPREA1
          CLOSE     PATHSPA1
          CLOSE     PATVISA1
          CLOSE     PMSVX1A1
          CLOSE     PATWR1A1
          CLOSE     WATTR1A1
          GOTO      HOSSCEND
+
          INC       MLTSECIO/INC
          INC       BOKRC1IO/INC
          INC       BOKRX1IO/INC
          INC       OUTPREIO/INC
          INC       PATHSPIO/INC
          INC       PATVISIO/INC
          INC       PATWR1IO/INC
          INC       PMSVX1IO/INC
          INC       WATTR1IO/INC
          INC       IBAOVRIO/INC
.
HOSSCEND  ENDPROC
+
.------------------------------------------------------------
. Check password
.------------------------------------------------------------
CHKPAS00  CALL      VFYUSR00 
          BRANCH    EXIT,CHKPAS90
          GOTO      CHKPAS99
.
CHKPAS90  CLEAR     WEBTITLE
          APPEND    "Password invalid. Error: ",WEBTITLE
          APPEND    TASKID,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
.
CHKPAS99  RETURN
.
.------------------------------------------------------------
. Verify User: Check loginid, password combination
. (escape characters '"!$\' ' to avoid commmand line issues)
.------------------------------------------------------------
VFYUSR00  MOVE      SP70,TASKID               * password verification
          CLEAR     SYSCMDLN
          STRIP     CWEBPCMD
          APPEND    CWEBPCMD,SYSCMDLN
          APPEND    " -t ",SYSCMDLN
          STRIP     CWEBPFIL
          APPEND    CWEBPFIL,SYSCMDLN
          APPEND    SP1,SYSCMDLN
          APPEND    QUOTE,SYSCMDLN
.
          STRIP     SECUREID
VFYUSR10  MOVE      SECUREID,DIM1            * move uid one byte at at time
          MATCH     BSLASH,DIM1
          GOTO      VFYUSR20 IF EQUAL
          MATCH     QUOTE,DIM1
          GOTO      VFYUSR20 IF EQUAL
          MATCH     "!",DIM1
          GOTO      VFYUSR15 IF EQUAL
          MATCH     "$",DIM1
          GOTO      VFYUSR20 IF EQUAL
          MATCH     "`",DIM1
          GOTO      VFYUSR20 IF EQUAL
          GOTO      VFYUSR30
.
VFYUSR15  APPEND    QUOTE,SYSCMDLN            * escape exclamation character "'!'"
          APPEND    "'",SYSCMDLN        
          APPEND    DIM1,SYSCMDLN      
          APPEND    "'",SYSCMDLN      
          APPEND    QUOTE,SYSCMDLN   
          GOTO      VFYUSR35
.
VFYUSR20  APPEND    BSLASH,SYSCMDLN           * escape the next char 
VFYUSR30  APPEND    DIM1,SYSCMDLN             * append the current character 
VFYUSR35  BUMP      SECUREID                  * move the FP up by one
          GOTO      VFYUSR10 IF NOT EOS
.
          APPEND    QUOTE,SYSCMDLN
          APPEND    SP1,SYSCMDLN
          APPEND    QUOTE,SYSCMDLN
.
          STRIP     SECUREPW
VFYUSR40  MOVE      SECUREPW,DIM1            * move pwd one byte at at time
          MATCH     BSLASH,DIM1
          GOTO      VFYUSR50 IF EQUAL
          MATCH     QUOTE,DIM1
          GOTO      VFYUSR50 IF EQUAL
          MATCH     "!",DIM1
          GOTO      VFYUSR45 IF EQUAL
          MATCH     "$",DIM1
          GOTO      VFYUSR50 IF EQUAL
          MATCH     "`",DIM1
          GOTO      VFYUSR50 IF EQUAL
          GOTO      VFYUSR60
.
VFYUSR45  APPEND    QUOTE,SYSCMDLN            * escape exclamation character "'!'"
          APPEND    "'",SYSCMDLN        
          APPEND    DIM1,SYSCMDLN      
          APPEND    "'",SYSCMDLN      
          APPEND    QUOTE,SYSCMDLN   
          GOTO      VFYUSR65
.
VFYUSR50  APPEND    BSLASH,SYSCMDLN           * escape the next char 
.
VFYUSR60  APPEND    DIM1,SYSCMDLN             * append the current character 
.
VFYUSR65  BUMP      SECUREPW                  * move the FP up by one
          GOTO      VFYUSR40 IF NOT EOS
.
          APPEND    QUOTE,SYSCMDLN
          RESET     SECUREID
          RESET     SECUREPW
          RESET     SYSCMDLN
.
          EXECUTE   SYSCMDLN,TASKID
          MATCH     "0",TASKID
          IF        @EQUAL
            MOVE      ZERO,EXIT
          ELSE
            MOVE      ONE,EXIT
          ENDIF 
VFYUSR99  RETURN
.
.------------------------------------------------------------
. Get Query Details
.------------------------------------------------------------
GETQRY00  IF        WGATEWAY=0
            PACK      FILNAM,HTMLBIN,WEBPID,CFGEXT,SP70
            SQUEEZE   FILNAM
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      CONFFILE,FILNAM,RDONLY
            TRAPCLR   IO
            BRANCH    OVRCD,GETQRY99
          ELSE
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            OPEN      CONFFILE,CONFNAME,RDONLY
            TRAPCLR   IO
            BRANCH    OVRCD,GETQRY99
          ENDIF
.
GETQRY10  READ      CONFFILE,SEQ;QRYNAME,QRYDATA
          GOTO      GETQRY20 IF OVER
.
GETQRY15  MATCH     "reportno=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,REPORTNO
            GOTO      GETQRY10
          ENDIF
.
          MATCH     "template=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,TEMPLATE
            GOTO      GETQRY10
          ENDIF
.
          MATCH     "allowahs=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,ALLOWAHS
            GOTO      GETQRY10
          ENDIF
.
          MATCH     "loginame=",QRYNAME
          IF        @EQUAL
            PACK      LOGINAME,QRYDATA,SP256 
..          PACK      USERID,LOGINAME,SP70 
            GOTO      GETQRY10
          ENDIF
.
          MATCH     "securety=",QRYNAME  * Set Page Level Security Parameters
          IF        @EQUAL
            MOVE      QRYDATA,SECURETY
            GOTO      GETQRY10
          ENDIF
.
          MATCH     "secureid=",QRYNAME
          IF        @EQUAL
            PACK      SECUREID,QRYDATA,SP256
            GOTO      GETQRY10
          ENDIF
.
          MATCH     "securepw=",QRYNAME
          IF        @EQUAL
            PACK      SECUREPW,QRYDATA,SP256
            GOTO      GETQRY10
          ENDIF
.
          MATCH     "tmotfrst=",QRYNAME
          IF        @EQUAL
            PACK      TMOTFRST,QRYDATA,SP70
            GOTO      GETQRY10
          ENDIF
.
          MATCH     "tmotnext=",QRYNAME
          IF        @EQUAL
            PACK      TMOTNEXT,QRYDATA,SP70
            GOTO      GETQRY10
          ENDIF
.
          MATCH     "catactiv=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,CATACTIV
            GOTO      GETQRY10
          ENDIF
.
          MATCH     "fhirtype=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FHIRTYPE
            GOTO      GETQRY10
          ENDIF
.
          MATCH     "fhiruser=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,FHIRUSER
            GOTO      GETQRY10
          ENDIF
.
          MOVE      ZERO,TEXTAREA
          CALL      SETPAR00         * Set Parameters in Program
.
          BRANCH    TEXTAREA,GETQRY15
          GOTO      GETQRY10

GETQRY20  CLOSE     CONFFILE,DELETE
.
GETQRY99  RETURN
.------------------------------------------------------------
. Write WEB Page Header
.------------------------------------------------------------
WEBHED00  BRANCH    WRPTSECT,WEBHED05  * Report Security Already Checked
.
          CALL      SECTEM00           * Determine Template & Check Security
          BRANCH    EXIT,WEBHED99
.
WEBHED05  MATCH     SP70,TEMPLFIL
          GOTO      WEBHED90 IF EQUAL
.
          SQUEEZE   TEMPLFIL
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      BODYHTML,TEMPLFIL,RDONLY
          TRAPCLR   IO
          IF        OVRCD<>1
            GOTO      WEBHED50
          ENDIF
.
          PACK      TEMPLFIL,TEMPLDIR,WBTPFIL,SP70
          SQUEEZE   TEMPLFIL
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      BODYHTML,TEMPLFIL,RDONLY
          TRAPCLR   IO
          IF        OVRCD<>1
            GOTO      WEBHED50
          ENDIF
.
          PACK      TEMPLFIL,TEMP3DIR,WBTPFIL,SP70
          SQUEEZE   TEMPLFIL
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      BODYHTML,TEMPLFIL,RDONLY
          TRAPCLR   IO
          IF        OVRCD<>1
            GOTO      WEBHED50
          ENDIF
.
          PACK      TEMPLFIL,TEMP4DIR,WBTPFIL,SP70
          SQUEEZE   TEMPLFIL
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      BODYHTML,TEMPLFIL,RDONLY
          TRAPCLR   IO
          IF        OVRCD<>1
            GOTO      WEBHED50
          ENDIF
.
          PACK      TEMPLFIL,TEMP5DIR,WBTPFIL,SP70
          SQUEEZE   TEMPLFIL
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      BODYHTML,TEMPLFIL,RDONLY
          TRAPCLR   IO
          IF        OVRCD<>1
            GOTO      WEBHED50
          ENDIF
.
          PACK      TEMPLFIL,TEMP6DIR,WBTPFIL,SP70
          SQUEEZE   TEMPLFIL
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      BODYHTML,TEMPLFIL,RDONLY
          TRAPCLR   IO
          IF        OVRCD<>1
            GOTO      WEBHED50
          ENDIF
.
          PACK      TEMPLFIL,TEMP7DIR,WBTPFIL,SP70
          SQUEEZE   TEMPLFIL
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      BODYHTML,TEMPLFIL,RDONLY
          TRAPCLR   IO
          IF        OVRCD<>1
            GOTO      WEBHED50
          ENDIF
.
          PACK      TEMPLFIL,TEMP8DIR,WBTPFIL,SP70
          SQUEEZE   TEMPLFIL
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      BODYHTML,TEMPLFIL,RDONLY
          TRAPCLR   IO
          IF        OVRCD<>1
            GOTO      WEBHED50
          ENDIF
.
          PACK      TEMPLFIL,TEMP9DIR,WBTPFIL,SP70
          SQUEEZE   TEMPLFIL
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      BODYHTML,TEMPLFIL,RDONLY
          TRAPCLR   IO
          IF        OVRCD<>1
            GOTO      WEBHED50
          ENDIF
.
          PACK      TEMPLFIL,TEMP0DIR,WBTPFIL,SP70
          SQUEEZE   TEMPLFIL
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      BODYHTML,TEMPLFIL,RDONLY
          TRAPCLR   IO
          IF        OVRCD<>1
            GOTO      WEBHED50
          ENDIF
.
          GOTO      WEBHED90
.
WEBHED50  MOVE      ONE,CONTENTF         * Flag As Page Content Header
          READ      BODYHTML,SEQ;LINE127
          GOTO      WEBHED96 IF OVER
          MOVE      LINE127,DIM127
          MATCH     "Content-Type",DIM127
          GOTO      WEBHED70 IF NOT EQUAL
.
          CALL      HTMLOPEN
          BRANCH    EXIT,WEBHED95
.
          GOTO      WEBHED82
.
WEBHED70  MOVE      ZERO,CONTENTF         * Flag As Standard Content
          CALL      OPENHTML
          BRANCH    EXIT,WEBHED95
.
              WRITE HTMLFILE;"<html>",012:
                  "<head>",012,"<title>",WEBTITLE,"</title>",012:
                  "<meta name=#"ServerID#" id=#"ServerID#" content=#"",PRGID,"#">",012:
                  "<meta name=#"ServerVersion#" id=#"ServerVersion#" content=#"",VERSION,"#">",012:
                  "<meta name=#"ServerName#" id=#"ServerName#" content=#"",PRGNAM,"#">",012:
                  "<meta name=#"ServerOption#" id=#"ServerOption#" content=#"",REPORTNO,"#">",012:
                  "<meta name=#"ServerTemplate#" id=#"ServerTemplate#" content=#"",TEMPLATE,"#">",012:
                  "<meta name=#"TemplateFile#" id=#"TemplateFile#" content=#"",WBTPFIL,"#">",012:
                  "<link rel=#"stylesheet#" ":
                  "href=#"../html/standard.css#" type=#"text/css#">",012:
                  "<link rel=#"stylesheet#" ":
                  "href=#"../html/custom.css#" type=#"text/css#">",012:
                  "<script type=#"text/javascript#" ":
                  "src=#"../js/Standard.js#"></script>",012:
                  "<script type=#"text/javascript#" ":
                  "src=#"../js/Custom.js#"></script>"
          MOVE      ZERO,EXIT
          GOTO      WEBHED82
.
. Output HTMLBODY until the "%START.OF.PAGE" line is found.
.------------------------------------------------------------
WEBHED80  READ      BODYHTML,SEQ;LINE127
          GOTO      WEBHED96 IF OVER
WEBHED82  MOVE      LINE127,DIM127
          MATCH     "%START.OF.PAGE",DIM127
          GOTO      WEBHED85 IF EQUAL
.
          STRIP     LINE127
          MOVELPTR  LINE127,LENGTH
          IF        LENGTH=0
            WRITE     HTMLFILE;012;
            GOTO      WEBHED80
          ENDIF
          SFORMAT   VAR,LENGTH
          MOVE      LINE127,VAR
          WRITE     HTMLFILE;VAR
          SFORMAT   VAR,127
          GOTO      WEBHED80
.
WEBHED85  MOVE      ZERO,EXIT
          GOTO      WEBHED99
.
WEBHED90  MOVE      "Can't Open Template File: '",WEBTITLE
.
          ENDSET    WEBTITLE
          APPEND    PRGID,WEBTITLE
.
          MOVE      REPORTNO,KEY2
          REP       " 0",KEY2
          APPEND    KEY2,WEBTITLE
.
          MOVE      TEMPLATE,KEY3
          RJUSTIFY  KEY3
          REP       " 0",KEY3
          APPEND    KEY3,WEBTITLE
.
          APPEND    ".html'",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      WEBHED99
.
WEBHED95  PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          DISPLAY   "*** Fifo Not Found ",KEY8," ",CTIMEIS," ",FILNAM," ***"
.
          MOVE      ONE,EXIT
          GOTO      WEBHED99
.
WEBHED96  WRITE     HTMLFILE;"<H1>START OF PAGE MISSING</H1>"
          CALL      WEBEND00
          MOVE      ONE,EXIT
          GOTO      WEBHED99
.
WEBHED99  RETURN
.------------------------------------------------------------
. Determine Template & Check Security
.------------------------------------------------------------
SECTEM00  MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
          MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
          PACK      TEMPLFIL,SP70,SP70
          PACK      KEY13,PRGID,KEY2,KEY3
          CALL      RDWBTP1
          IF        OVRCD=0
            PACK      TEMPLFIL,TEMPCDIR,WBTPFIL,SP70
            PACK      WEBTITLE,WEBTITLE,SP70,SP70
            MATCH     SP70,WEBTITLE
            IF        @EQUAL
              MOVE      WBTPDES,WEBTITLE
            ENDIF
          ENDIF
          MOVE      ZERO,F2
          MOVE      WBTPLEV,F2
          COMPARE   ZERO,F2
          GOTO      SECTEM90 IF EQUAL
.
          PACK      KEY18,USERID,WBTPPRG
          CALL      RDWBST1
          BRANCH    OVRCD,SECTEM80
          IF        WBSTLEV<F2
            GOTO      SECTEM80
          ENDIF
          GOTO      SECTEM90
.
SECTEM80  MOVE      "Security Level Invalid",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      SECTEM99
.
SECTEM90  MOVE      ZERO,EXIT
SECTEM99  RETURN
.------------------------------------------------------------
. Process Body of HTML Template
.------------------------------------------------------------
WEBBOD00  CLEAR     HTMLTAGS                * Set Up Scan String
          APPEND    "%",HTMLTAGS
          APPEND    PRGID,HTMLTAGS
          APPEND    ".",HTMLTAGS
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
          APPEND    KEY2,HTMLTAGS
          APPEND    ".",HTMLTAGS
          RESET     HTMLTAGS
.
WEBBOD10  READ      BODYHTML,SEQ;LINE127    * Process Body of HTML Template
          GOTO      WEBBOD99 IF OVER
          MOVE      LINE127,DIM127
          SCAN      HTMLTAGS,DIM127         * Check for Scan String
          GOTO      WEBBOD20 IF EQUAL
          SCAN      "%STANDARD.01.",DIM127   * Check for Standard Scan String
          GOTO      WEBBOD40 IF EQUAL
.
          STRIP     LINE127
          MOVELPTR  LINE127,LENGTH
          COMPARE   ZERO,LENGTH
          GOTO      WEBBOD10 IF EQUAL
          SFORMAT   VAR,LENGTH
          MOVE      LINE127,VAR
          WRITE     HTMLFILE;VAR
          SFORMAT   VAR,127
          GOTO      WEBBOD10
.
WEBBOD20  MOVE      LINE127,DIM127          * Output Line Prior to Scan String
WEBBOD30  SCAN      HTMLTAGS,DIM127
          GOTO      WEBBOD80 IF NOT EQUAL
          CALL      GETTAG00                * Get Tag Details
          CALL      PROFLD00                * Process Field in Calling Program
          GOTO      WEBBOD30
.
WEBBOD40  MOVE      LINE127,DIM127          * Output Line Prior to Scan String
WEBBOD50  SCAN      "%STANDARD.01.",DIM127  * Check for Standard Scan String
          GOTO      WEBBOD80 IF NOT EQUAL
          CALL      GETTAG00                * Get Tag Details
          CALL      STDFLD00                * Process Field in Standard Routine
          GOTO      WEBBOD50
.
WEBBOD80  STRIP     DIM127       * Output Remaining String
          MOVELPTR  DIM127,LENGTH
          SFORMAT   VAR,LENGTH
          MOVE      DIM127,VAR
          WRITE     HTMLFILE;VAR
          SFORMAT   VAR,127
          GOTO      WEBBOD10
.
WEBBOD99  RETURN
.------------------------------------------------------------
. Get Tag Details
.------------------------------------------------------------
GETTAG00  MOVEFPTR  DIM127,LENGTH
          SUB       ONE,LENGTH
          IF        LENGTH>0
            RESET     DIM127
            SFORMAT   VAR,LENGTH
            MOVE      DIM127,VAR
            WRITE     HTMLFILE;VAR;
            SFORMAT   VAR,127
            ADD       ONE,LENGTH
            RESET     DIM127,LENGTH
            SUB       ONE,LENGTH
          ENDIF
.
          UNPACK    DIM127,KEY13,FLDITEM,DIM127
.
          MOVE      FLDITEM,FLDITMNO
          MOVE      FLDITEM,FLDSETNO
.          ADD       "100",FLDSETNO
.          DIV       "100",FLDSETNO
.
          MOVE      FLDSETNO,FORM4   * Added this code just to get it working
          ADD       "100",FORM4      * Should set FLDSETNO to a FORM 4 ?????
          DIV       "100",FORM4
          MOVE      FORM4,FLDSETNO
.
.
          MOVE      FLDITEM,FLDITMNO
          ASSIGN    FLDITMNO-(100*(FLDSETNO-1)),FLDITMNO
          MOVE      FLDITMNO,FLDITEM
          RETURN
.------------------------------------------------------------
. Standard Fields
.------------------------------------------------------------
STDFLD00  BRANCH    FLDSETNO,STDFLD10
          GOTO      STDFLD99
.
STDFLD10  CALL      STDA0000
          GOTO      STDFLD99
.
STDFLD99  RETURN
.------------------------------------------------------------
. Standard Fields
.------------------------------------------------------------
STDA0000  BRANCH    FLDITMNO,STDA0100,STDA0200,STDA0300,STDA0400,STDA0500:
                             STDA0600,STDA0700,STDA0800,STDA0900,STDA1000:
                             STDA1100,STDA1200,STDA1300,STDA1400,STDA1500:
                             STDA1600,STDA1700,STDA1800,STDA1900,STDA2000:
                             STDA2100,STDA2200,STDA2300,STDA2400,STDA2500:
                             STDA2600,STDA2700,STDA2800,STDA2900,STDA3000:
                             STDA3100,STDA3200,STDA3300,STDA3400,STDA3500:
                             STDA3600,STDA3700,STDA3800,STDA3900,STDA4000:
                             STDA4100,STDA4200,STDA4300,STDA4400,STDA4500:
                             STDA4600
          GOTO      STDA9999
.
STDA0100  PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,STDA9999
          CALL      SELPRT00       * Printer Selection
          GOTO      STDA9999
.
STDA0200  WRITE     HTMLFILE;REPORTNO;
          GOTO      STDA9999
.
STDA0300  WRITE     HTMLFILE;TEMPLATE;
          GOTO      STDA9999
.
STDA0400  WRITE     HTMLFILE;PRGID;
          GOTO      STDA9999
.
STDA0500  WRITE     HTMLFILE;PRGNAM;
          GOTO      STDA9999
.
STDA0600  WRITE     HTMLFILE;VERSION;
          GOTO      STDA9999
.
STDA0700  UNPACK    DIM127,D1,CATEGORY,DIM127
          MOVE      SP70,CODE
          CALL      CODSEL00
          GOTO      STDA9999
.
STDA0800  PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,STDA9999
          WRITE     HTMLFILE;WBSEUID;
          GOTO      STDA9999
.
STDA0900  PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,STDA9999
          WRITE     HTMLFILE;WBSENAM;
          GOTO      STDA9999
.
STDA1000  PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,STDA9999
          WRITE     HTMLFILE;WBSEDOC;
          GOTO      STDA9999
.
STDA1100  PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,STDA9999
          WRITE     HTMLFILE;WBSEWRD;
          GOTO      STDA9999
.
STDA1200  PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,STDA9999
          MOVE      "w0",CATEGORY
          MOVE      WBSEOCG,CODE
          CALL      CODITM00
          GOTO      STDA9999
.
STDA1300  PACK      KEY8,CCC,CYY,CMM,CDD,SP70
          REP       " 0",KEY8
          WRITE     HTMLFILE;KEY8;
          GOTO      STDA9999
.
STDA1400  PACK      KEY8,CDD,CMM,CYY,CCC,SP70
          REP       " 0",KEY8
          WRITE     HTMLFILE;KEY8;
          GOTO      STDA9999
.
STDA1500  MOVE      CMM,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          MOVE      CYY,CYEAR
          REP       " 0",CYEAR
          MOVE      CDD,KEY2
          REP       " 0",KEY2
          WRITE     HTMLFILE;KEY2,SP1,MTHNAM3,SP1,CCC,CYEAR;
          GOTO      STDA9999
.
STDA1600  CLOCK     TIME,CTIMEIS
          WRITE     HTMLFILE;CTIMEIS;
          GOTO      STDA9999
.
STDA1700  UNPACK    DIM127,D1,CATEGORY,DIM127  * Output Category Title
          PACK      KEY5,CATEGORY,SP70         * %STANDARD.01.017.xx
          CALL      RDCODE1                    * xx=Category
          IF        OVRCD=0
            STRIP     TDESC
            WRITE     HTMLFILE;*+,TDESC,SP1;
            RESET     TDESC
          ENDIF
          GOTO      STDA9999
.
STDA1800  UNPACK    DIM127,D1,CATEGORY,D1,CODE,DIM127
          CALL      CODITM00
          GOTO      STDA9999
.
STDA1900  UNPACK    DIM127,D1,KEY8,D1,KEY1,DIM127      * Output Label Selection
          PROC      LABELSEL
          GOTO      STDA9999
.
STDA2000  CALL      STDAOS00       * Output Stationary Selection
          GOTO      STDA9999

STDA2100  UNPACK    DIM127,D1,CATEGORY,DIM127
          MOVE      SP70,CODE
          CALL      DCDSEL00
          GOTO      STDA9999
.
STDA2200  PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,STDA9999
          MOVE      "TY",CATEGORY
          MOVE      WBSEDTYP,CODE
          CALL      CODITM00
          GOTO      STDA9999
.
STDA2300  PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,STDA9999
          WRITE     HTMLFILE;WBSESIT;
          GOTO      STDA9999
.
STDA2400  UNPACK    DIM127,D1,CATEGORY,D1,CODE,DIM127
          PACK      CODEDATE,CCC,CYY,CMM,CDD,SP70
          REP       " 0",CODEDATE
          OPEN      COMCODA1,"comcodaf"
          OPEN      COMCODA2,"comcodaf"
          CALL      COMITM00
          GOTO      STDA9999
.
STDA2500  CALL      CONFLP00           * get value of a control file parameter
          GOTO      STDA9999           * Use STDA4400 for variables > 9
.
STDA2600  PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,STDA9999
          WRITE     HTMLFILE;WBSECLI;
          GOTO      STDA9999
.
STDA2700  UNPACK    DIM127,D1,KEY1,D1,DIM1,DIM127  * Output Label Num Selection
          PROC      NUMLBSEL
          GOTO      STDA9999
.
STDA2800  PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,STDA9999
          WRITE     HTMLFILE;WBSEDALL;           * Allied Health Department
          GOTO      STDA9999
.
STDA2900  PROC      TITLESEL                     * Output Title Selection
          GOTO      STDA9999
.
STDA3000  PROC      RELATSEL                     * Output Relationship Selection
          GOTO      STDA9999
.
STDA3100  MOVE      CDD,DD
          MOVE      CMM,MM
          MOVE      CYY,YY
          MOVE      CCC,CC
          CALL      DMYCON
.
          SUB       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
.
          MOVE      CC,CCENT
          MOVE      YY,CYEAR
          REP       " 0",CYEAR
          MOVE      MM,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          MOVE      DD,CDAY
          REP       " 0",CDAY
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      STDA9999
.
STDA3200  PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,STDA9999
          WRITE     HTMLFILE;WBSEHOSP;           * Web User Hospital Code
          GOTO      STDA9999
.
STDA3300  OPEN      IBAPDFA1,"ibapdfaf"
          CALL      RETDEF00
          WRITE     HTMLFILE;IBPDDOC;     * Default stationery code
          GOTO      STDA9999
.
STDA3400  OPEN      IBAPDFA1,"ibapdfaf"
          CALL      RETDEF00
          WRITE     HTMLFILE;IBPDCOP;     * Default no. of copies for stat.code
          GOTO      STDA9999
.
STDA3500  UNPACK    DIM127,D1,CATEGORY,DIM127  * Output Category Title
          PACK      KEY5,CATEGORY,SP70         * %STANDARD.01.035.xx
          CALL      RDCODE1                    * xx=Category
          IF        OVRCD=0
            STRIP     PTCDLDES
            WRITE     HTMLFILE;*+,PTCDLDES,SP1;
            RESET     PTCDLDES
          ENDIF
          GOTO      STDA9999
.
STDA3600  OPEN      IBAPDFA1,"ibapdfaf"
          CALL      RETDEF00
          WRITE     HTMLFILE;IBPDDOC,IBPDNUM;  * Default label
          GOTO      STDA9999
.
STDA3700  PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,STDA9999
          MOVE      "Tz",CATEGORY
          MOVE      WBSEOPDU,CODE
          CALL      CODITM00
          GOTO      STDA9999
.
STDA3800  PROC      HOSNAM00           * Web User Hospital Name
          GOTO      STDA9999
.
STDA3900  MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
          MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
          PACK      TEMPLFIL,SP70,SP70
          PACK      KEY13,PRGID,KEY2,KEY3
          CALL      RDWBTP1
          IF        OVRCD=0
            WRITE     HTMLFILE;WBTPDES
          ENDIF
          GOTO      STDA9999
.
STDA4000  PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,STDA9999
          WRITE     HTMLFILE;WBSENURS;
          GOTO      STDA9999
.
STDA4100  PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,STDA9999
          WRITE     HTMLFILE;WBSEESC;
          GOTO      STDA9999
.
STDA4200  UNPACK    DIM127,D1,KEY8,DIM127        * Calling Server
          PACK      KEY18,USERID,KEY8            * %STANDARD.01.0xx.xxxxxxxx
.                                                * xxxxxxxx=Server Name
          CALL      RDWBST1
          IF        OVRCD = 1
            MOVE      ZERO,WBSTLEV               * Default Security Level
          ENDIF
          WRITE     HTMLFILE;WBSTLEV;
          GOTO      STDA9999
.
STDA4300  PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,STDA9999
          WRITE     HTMLFILE;WBSELOGN;
          GOTO      STDA9999
.
STDA4400  CALL      CFLONG00
          GOTO      STDA9999 
.
STDA4500  UNPACK    DIM127,D1,D8,DIM127        * %STANDARD.01.045.AAAAAAAA
          PROC      COMPARVL                     * AAAAAAAA = Parameter Name
          GOTO      STDA9999                     * comparaf
.
STDA4600  PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,STDA9999
          WRITE     HTMLFILE;WBSEHCPC;         * Web User HCP Code 
          GOTO      STDA9999 
.
STDA9999  RETURN
+
.------------------------------------------------------------
. * Output web user id's hospital name
.------------------------------------------------------------
          DEFPROC   HOSNAM00
.
          INC       PATHSPFD/INC
.
          OPEN      PATHSPA1,"pathspaf"
.
HOSN0000  PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,HOSN9500
.
          MATCH     SP70,WBSEHOSP
          GOTO      HOSN9500 IF EQUAL
.
          PACK      KEY3,WBSEHOSP,SP70
          CALL      RDPTHSP1
          BRANCH    OVRCD,HOSN9500
.
          WRITE     HTMLFILE;PTHSNAME;           * Web User Hospital Name
          GOTO      HOSN9999
.
HOSN9500  GOTO      HOSN9999
.
          INC       IBAOVRIO/INC
          INC       PATHSPIO/INC
.
HOSN9999  ENDPROC
+
.------------------------------------------------------------
. * Output Stationary Selection
.------------------------------------------------------------
STDAOS00  UNPACK    DIM127,D1,D3,D1,D6,DIM127
          MATCH     "000",D3
          GOTO      STDAOS20 IF NOT EQUAL
.
          PACK      KEY6,SP70
          CALL      RSIBTH1
STDAOS10  CALL      RKIBTH1
          BRANCH    OVRCD,STDAOS99
.
          MATCH     "1",IBTHACTV
          GOTO      STDAOS10 IF NOT EQUAL

          MATCH     IBTHSCOD,D6
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=#"",IBTHSCOD,"#" selected>":
                               IBTHDESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=#"",IBTHSCOD,"#">",IBTHDESC:
                               "</option>"
          ENDIF
          GOTO      STDAOS10
.
STDAOS20  PACK      KEY6,SP70
          CALL      RSIBTH1
STDAOS30  CALL      RKIBTH1
          BRANCH    OVRCD,STDAOS99
.
          MATCH     "1",IBTHACTV
          GOTO      STDAOS30 IF NOT EQUAL
.
          MATCH     D3,IBTHSTYP
          GOTO      STDAOS30 IF NOT EQUAL
.
          MATCH     IBTHSCOD,D6
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=#"",IBTHSCOD,"#" selected>":
                               IBTHDESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=#"",IBTHSCOD,"#">",IBTHDESC:
                               "</option>"
          ENDIF
          GOTO      STDAOS30
.
STDAOS99  RETURN
.------------------------------------------------------------
. Write WEB Page Trailer
.------------------------------------------------------------
WEBEND00  READ      BODYHTML,SEQ;LINE127    * Insert the rest of the Template
          GOTO      WEBEND10 IF OVER
          STRIP     LINE127
          MOVELPTR  LINE127,LENGTH
          COMPARE   ZERO,LENGTH
          GOTO      WEBEND00 IF EQUAL
          SFORMAT   VAR,LENGTH
          MOVE      LINE127,VAR
          WRITE     HTMLFILE;VAR
          SFORMAT   VAR,127
          GOTO      WEBEND00
.
WEBEND10  MOVE      USERID,KEY10
          CALL      RDWBSE1
          BRANCH    OVRCD,WEBEND90
          MATCH     "1",WBSEMESA
          GOTO      WEBEND20 IF NOT EQUAL
          MOVE      USERID,KEY10
          CALL      RLWBSE1
          BRANCH    OVRCD,WEBEND90,WEBEND90
          MOVE      ZERO,WBSEMESA
          CALL      UPWBSE1
          CALL      WEBMES00
WEBEND20  CALL      UUWBSE1
.
WEBEND90  BRANCH    CONTENTF,WEBEND98
          WRITE     HTMLFILE;"<div name=#"PopUpScreen#" id=#"PopUpScreen#" ":
                    "style=#"display: none;#">",012:
                    "<iframe scrolling=#"no#" width=#"100%#" height=#"100%#" ":
                    "name=#"PopUpFrame#" id=#"PopUpFrame#"></iframe>",012:
                    "<iframe scrolling=#"no#" style=#"display: none;#" ":
                    "name=#"PopUpFrame1#" id=#"PopUpFrame1#"></iframe>",012,"</div>",012:
                    "</body>",012,"</html>"
.
WEBEND98  CALL      CLOSHTML     * End of Document
          RETURN
.------------------------------------------------------------
. Output System Message for User
.------------------------------------------------------------
.
. Get Message from Message table and Delete it.
.
WEBMES00  WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "  alert('Message'); ":
                             "}":
                             "</script>"
          RETURN
.------------------------------------------------------------
. Refresh Parent and Close Window
.------------------------------------------------------------
WINCLS00  CALL      OPENHTML
          BRANCH    EXIT,WINCLS99
          BRANCH    FHIRTYPE,WINCLS10
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#" ":
                             "src=#"../js/Standard.js#"></script>":
                             "<script type=#"text/javascript#" ":
                             "src=#"../js/Custom.js#"></script>":
                             "<script type=#"text/javascript#">  ":
                             " if (self.name.match(/PopUpFrame/)) {":
                             " reloadParentFrame();}":
                             " else { ":
                             "  opener.history.go(0);":
                             "  self.close(); }":
                             "</script>"
          CALL      CLOSHTML     * End of Document
          GOTO      WINCLS99
WINCLS10  CALL      FHRSUC00  * Return FHIR Success
WINCLS99  RETURN
.------------------------------------------------------------
. Close Window
.------------------------------------------------------------
WINEXT00  CALL      OPENHTML
          BRANCH    EXIT,WINEXT99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " if (self.name.match(/PopUpFrame/)) {":
  "  parent.document.getElementById('PopUpScreen').style.display=#"none#"; ":
                             "} else { ":
                             "  self.close(); }":
                             "}":
                             "</script>"
          CALL      CLOSHTML     * End of Document
WINEXT99  RETURN
.------------------------------------------------------------
. Display Alert & Goto Next Page
.------------------------------------------------------------
WINNXT00  CALL      OPENHTML
          BRANCH    EXIT,WINNXT99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " alert(#"",WEBTITLE,"#");":
                             " location.href=#"",HTMLLINK,"#";":
                             "}":
                             "</script>"
          CALL      CLOSHTML     * End of Document
WINNXT99  RETURN
.------------------------------------------------------------
. Display Alert & Close Window  (no refresh)
.------------------------------------------------------------
WINALT00  CALL      OPENHTML
          BRANCH    EXIT,WINALT99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             " alert(#"",WEBTITLE,"#");":
                             " if (self.name.match(/PopUpFrame/)) {":
  "  parent.document.getElementById('PopUpScreen').style.display=#"none#"; ":
                             "} else { self.close() }":
                             "}":
                             "</script>"
          CALL      CLOSHTML     * End of Document
WINALT99  RETURN
.------------------------------------------------------------
. Display Alert  - history back 1 page.
.------------------------------------------------------------
DISALT00  CALL      OPENHTML
          BRANCH    EXIT,DISALT99
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    history.go(-1);":
                             "}":
                             "</script>"
          CALL      CLOSHTML     * End of Document
DISALT99  RETURN
.------------------------------------------------------------
. Display Alert, close window - history back 2 pages.
.------------------------------------------------------------
DAH20000  CALL      OPENHTML
          BRANCH    EXIT,DAH29999
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "    opener.history.go(-2);":
                             "}":
                             "</script>"
          CALL      CLOSHTML     * End of Document
DAH29999  RETURN
.------------------------------------------------------------
. Display Alert, continue to next page.
.------------------------------------------------------------
DISAC000  CALL      OPENHTML
          BRANCH    EXIT,DISAC999
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    self.close();":
                             "}":
                             "</script>"
          CALL      CLOSHTML     * End of Document
DISAC999  RETURN
.------------------------------------------------------------
. Output HTML Error File
.------------------------------------------------------------
WEBERR00  CALL      OPENHTML
          BRANCH    EXIT,WEBERR95
          BRANCH    FHIRTYPE,WEBERR10
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> ":
 "  var PopUpScreen = parent.document.getElementById('PopUpScreen');":
                    "  alert(#"",WEBTITLE,"#");":
                    "  if (window.name.substr(0,4)=='Hide') {":
                    "    self.close();} else {":
                    "  if (PopUpScreen) {":
                    "    PopUpScreen.style.display='none'; } else {":
                    "    history.go(-1); }":
                    "}":
                    "</script>",012
.
          CALL      CLOSHTML     * End of Document
          GOTO      WEBERR99
.
. FHIR Error Response.
.
WEBERR10  CALL      FHRERR00
          GOTO      WEBERR99
.
WEBERR95  DISPLAY   "*** Fifo Not Found - ",FILNAM," ***"
.
WEBERR99  RETURN
.---------------------------------------------------------------------
. Output HTML Error File when (javascript SubmitHidden() is used)
. Will output error and keep the popupframe open else will submit and
. close the popupframe as per usual.
.---------------------------------------------------------------------
POPERR00  CALL      OPENHTML
          BRANCH    EXIT,POPERR95
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                             "    alert(#"",WEBTITLE,"#");":
                             "    if (window.name.substr(0,4)=='Hide' ) {":
                             "    self.close() } ":
                             "    else {":
                             "    history.go(-1)}":
                             "}":
                             "</script>"
          CALL      CLOSHTML
          GOTO      POPERR99
.
.
POPERR95  DISPLAY   "*** Fifo Not Found - ",FILNAM," ***"
.
POPERR99  RETURN
.------------------------------------------------------------
. Set CMON from MTHNAM3
.------------------------------------------------------------
SETMTH00  MATCH     JAN,MTHNAM3
          GOTO      SETMON01 IF EQUAL
          MATCH     FEB,MTHNAM3
          GOTO      SETMON02 IF EQUAL
          MATCH     MAR,MTHNAM3
          GOTO      SETMON03 IF EQUAL
          MATCH     APR,MTHNAM3
          GOTO      SETMON04 IF EQUAL
          MATCH     MAY,MTHNAM3
          GOTO      SETMON05 IF EQUAL
          MATCH     JUN,MTHNAM3
          GOTO      SETMON06 IF EQUAL
          MATCH     JUL,MTHNAM3
          GOTO      SETMON07 IF EQUAL
          MATCH     AUG,MTHNAM3
          GOTO      SETMON08 IF EQUAL
          MATCH     SEP,MTHNAM3
          GOTO      SETMON09 IF EQUAL
          MATCH     OCT,MTHNAM3
          GOTO      SETMON10 IF EQUAL
          MATCH     NOV,MTHNAM3
          GOTO      SETMON11 IF EQUAL
          MATCH     DEC,MTHNAM3
          GOTO      SETMON12 IF EQUAL
          MOVE      SP2,CMON
          GOTO      SETMON99
SETMON01  MOVE      "01",CMON
          GOTO      SETMON99
SETMON02  MOVE      "02",CMON
          GOTO      SETMON99
SETMON03  MOVE      "03",CMON
          GOTO      SETMON99
SETMON04  MOVE      "04",CMON
          GOTO      SETMON99
SETMON05  MOVE      "05",CMON
          GOTO      SETMON99
SETMON06  MOVE      "06",CMON
          GOTO      SETMON99
SETMON07  MOVE      "07",CMON
          GOTO      SETMON99
SETMON08  MOVE      "08",CMON
          GOTO      SETMON99
SETMON09  MOVE      "09",CMON
          GOTO      SETMON99
SETMON10  MOVE      "10",CMON
          GOTO      SETMON99
SETMON11  MOVE      "11",CMON
          GOTO      SETMON99
SETMON12  MOVE      "12",CMON
          GOTO      SETMON99
SETMON99  RETURN
.------------------------------------------------------------
. Break Keywords into Words
.------------------------------------------------------------
GETWRD00  MOVE      ZERO,KEYWRDNO
          MOVE      SP70,KEYWRD01
          MOVE      SP70,KEYWRD02
          MOVE      SP70,KEYWRD03
          MOVE      SP70,KEYWRD04
          MOVE      SP70,KEYWRD05
          MOVE      SP70,KEYWRD06
          MOVE      SP70,KEYWRD07
          MOVE      SP70,KEYWRD08
          MOVE      SP70,KEYWRD09
          MOVE      SP70,KEYWRD10
          PACK      KEYWORDS,KEYWORDS,SP70
          MOVE      KEYWORDS,KEY61
.
GETWRD10  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      GETWRD10 IF NOT EOS
            GOTO      GETWRD99               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWORDS,SP70        * reset form pointer
          MOVE      KEY60,KEYWORDS
.
          SCAN      " ",KEYWORDS               * Locate the first blank
          GOTO      GETWRD20 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,F2              * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,F2              * set logical length to end of name
.
.         Save this Word
.
GETWRD20  MOVE      KEYWORDS,KEY15
          ADD       ONE,KEYWRDNO
          REP       UPPLOW,KEY15
          PACK      KEY15,KEY15,SP70
          STORE     KEY15,KEYWRDNO,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                                   KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          GOTO      GETWRD19
.
.         Check for any more words
.
GETWRD19  SETLPTR   KEYWORDS,SIXTY         * reset logical length
          ADD       ONE,F2                 * position of 1st blank
          RESET     KEYWORDS,F2            * reset to this point
          PACK      KEY60,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY60,KEYWORDS
          GOTO      GETWRD10
.
GETWRD99  MOVE      KEY61,KEYWORDS
          RETURN
.
.------------------------------------------------------------
.         Label Selection Standard Tag
.------------------------------------------------------------
          DEFPROC   LABELSEL
.
          INC       SCRMASFD/INC
          INC       IBAPDFFD/INC
.
LABEL     INIT       "LABEL"
.
          OPEN      SCRMASA1,"scrmasaf"
          OPEN      IBAPDFA1,"ibapdfaf"
.
LABE0000  PACK      KEY10,LABEL
          CALL      RSSCMA1
LABE1000  CALL      RKSCMA1
          BRANCH    OVRCD,LABE9500
.
          UNPACK    SCMAPID,KEY5
          MATCH     "LABEL",KEY5
          GOTO      LABE9999 IF NOT EQUAL
.
          PACK      KEY10,SCMAPID,SCMASID,SP70
.
          MATCH     "1",KEY1
          IF        @EQUAL
            MATCH     SCMAPID,KEY8
            GOTO      LABE1000 IF NOT EQUAL
.
            WRITE     HTMLFILE;"<option value=#"",KEY10,"#">",SCMADES:
                               "</option>"
            GOTO      LABE1000
          ENDIF
.
          MATCH     "2",KEY1
          IF        @EQUAL
            MOVE      "02",DIM2
            MOVE      "PATWEB70",D8
            PACK      KEY20,D8,DIM2,USERID,SP70
            CALL      RDIBPD1
            IF        OVRCD<>1
              PACK      D10,IBPDDOC,IBPDNUM,SP70
              MATCH     D10,KEY10
              IF        @EQUAL
                 WRITE     HTMLFILE;"<option value=#"",KEY10,"#" selected>":
                                  SCMADES,"</option>"
              ELSE
                  WRITE     HTMLFILE;"<option value=#"",KEY10,"#">",SCMADES:
                                    "</option>"
              ENDIF
            ELSE
              WRITE     HTMLFILE;"<option value=#"",KEY10,"#">",SCMADES:
                                    "</option>"
            ENDIF
            GOTO    LABE1000
          ENDIF

          MATCH     SCMAPID,KEY8
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=#"",KEY10,"#" selected>":
                               SCMADES,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=#"",KEY10,"#">",SCMADES:
                               "</option>"
          ENDIF
          GOTO      LABE1000
.
LABE9500  GOTO      LABE9999
.
          INC       IBAOVRIO/INC
          INC       IBAPDFIO/INC
          INC       SCRMASIO/INC
.
LABE9999  ENDPROC
.------------------------------------------------------------
.         Number of Labels Selection Standard Tag
.------------------------------------------------------------
          DEFPROC   NUMLBSEL
.
          INC       WEBLABFD/INC
.
          OPEN      WEBLABA1,"weblabaf"
.
NLAB0000  PACK      KEY4,KEY1,SP70
          CALL      RSWBLAB1
NLAB1000  CALL      RKWBLAB1
          BRANCH    OVRCD,NLAB9500
.
          MATCH     KEY1,WBLBSYST
          GOTO      NLAB9500 IF NOT EQUAL
.
          MATCH     "1",DIM1
          GOTO      NLAB2000 IF NOT EQUAL
.
          MATCH     DIM1,WBLBDEFT
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=#"",WBLBNUML,"#" selected>":
                               WBLBNUML,"</option>"
            GOTO      NLAB1000
          ENDIF
.
NLAB2000  WRITE     HTMLFILE;"<option value=#"",WBLBNUML,"#">",WBLBNUML:
                               "</option>"
          GOTO      NLAB1000
.
NLAB9500  GOTO      NLAB9999
.
          INC       IBAOVRIO/INC
          INC       WEBLABIO/INC
.
NLAB9999  ENDPROC
.------------------------------------------------------------
.         Title Selection Standard Tag
.------------------------------------------------------------
          DEFPROC   TITLESEL
.
          INC       PMSTLEFD/INC
.
          OPEN      PMSTLEA2,"pmstleaf"
.
TITL0000  PACK      KEY34,SP70
          CALL      RSPMTLE2
TITL1000  CALL      RKPMTLE2
          BRANCH    OVRCD,TITL9500
.
          MATCH     "1",PMTLACTV
          GOTO      TITL1000 IF EQUAL       * Inactive; so skip
.
          WRITE     HTMLFILE;"<option value=#"",PMTLTITL,"#">",PMTLDESC:
                               "</option>"
          GOTO      TITL1000
.
TITL9500  GOTO      TITL9999
.
          INC       IBAOVRIO/INC
          INC       PMSTLEIO/INC
.
TITL9999  ENDPROC
.------------------------------------------------------------
.         Relationship Selection Standard Tag
.------------------------------------------------------------
          DEFPROC   RELATSEL
.
          INC       PMSRELFD/INC
.
          OPEN      PMSRELA2,"pmsrelaf"
.
RELA0000  PACK      KEY40,SP70
          CALL      RSPMREL2
RELA1000  CALL      RKPMREL2
          BRANCH    OVRCD,RELA9500
.
          MATCH     "1",PMRLACTV
          GOTO      RELA1000 IF EQUAL       * Inactive; so skip
.
          WRITE     HTMLFILE;"<option value=#"",PMRLRELS,"#">",PMRLDESC:
                               "</option>"
          GOTO      RELA1000
.
RELA9500  GOTO      RELA9999
.
          INC       IBAOVRIO/INC
          INC       PMSRELIO/INC
.
RELA9999  ENDPROC
.------------------------------------------------------------
. Function   : System Locking Routines
.              These Routines are to be used in places where the
.              pi instruction would have been used to protect
.              updates to ASCII files etc.
.              System Lock Codes will be stored on the system
.              codes table ibacodes.
.              For Control File (controlf) updates the System Lock Code
.              will be the Sector that is being updated.
.              Other System Lock Code will arbitary Alpha values.
.              Arbitary System Lock Codes will be allocated as required
.              by Analysts or Senior Programmers.
.
. Lock Codes : Code  Description
.              BNK - PATBNKFD
.              FIN - PATFINFD
.              PFR - PRIFINFD
.              STA - PATSTAFD
.              AST - AAESTAFD
.              AUX - AAEAUXFD
.              OUX - OUTAUXFD
.              PDS - PCPDSCFD
.              PUC - PATAUCFD
.              PUM - PATAUMFD
.              PUR - PATRCAUD
.              PUF - PATAUFFD
.              PUL - PATAULFD
.              PUJ - PATAUJFD
.              PUD - PATAUDFD
.              PUB - PATAUBFD
.              PUI - PATAUIFD
.              PUP - PATAUPFD
.              PU1 - PATAU1FD
.              PZ1 - PATAZ1FD
.              PUE - PATAUEFD
.              PUT - PATAUTFD
.              PFG - PATFIGFD
.              PRF - PATRFNFD
.              PRG - PRIFIGFD
.              PRJ - PRIAUJFD
.              PUA - PATAUAFD
.              HFR - PATHFRFD
.
. Routine    : GETSLK00 - Get System Lock
.            : RELSLK00 - Release System Lock
.
. Input      : PRXCODE - System Lock Code
.
.------------------------------------------------------------
. Get Record Lock
.------------------------------------------------------------
GETSLK00   OPEN      IBACODE1,"ibacodef"
           MOVE      "~~",KEY2
           MOVE      PRXCODE,KEY3
           PACK      KEY5,KEY2,KEY3
           CALL      RLIBCOD1
           BRANCH    OVRCD,GETSLK90,GETSLK91
           GOTO      GETSLK99
.
GETSLK90   CALL      WRTLCK00     * Write Lock Record to ibacodes
           GOTO      GETSLK00
.
GETSLK91   DISPLAY   *W1,"Waiting on System Lock ",KEY5
           GOTO      GETSLK00
.
GETSLK99   RETURN
.------------------------------------------------------------
. Release Record Lock
.------------------------------------------------------------
RELSLK00   CALL      UUIBCOD1
           RETURN
.------------------------------------------------------------
. Write Lock Record to patcodes
.------------------------------------------------------------
WRTLCK00   MOVE      "~~",PRXCATA
           MOVE      KEY3,PRXCODE
           CLEAR     PRXDESC
           APPEND    "System Lock ",PRXDESC
           APPEND    PRXCODE,PRXDESC
           RESET     PRXDESC
           CALL      WRIBCOD1
           RETURN
.
.------------------------------------------------------------
. Control File Parameter Tag - %STANDARD.01.025.www.xxx.y.z where:
. www = sector number (numeric)
. xxx = field position (numeric)
.   y = dim or form (0/1)
.   z = field length (1-9)
.
. N.B: Be sure to use the actual control file variable name in templates.
. e.g. <input type=hidden name=ibcnmhos value="%STANDARD.01.025.000.043.1.1">
.------------------------------------------------------------
CONFLP00  UNPACK    DIM127,ANS,KEY3,DIM127
          MOVE      ZERO,SECTORNO       * Sector of Parameter
          MOVE      KEY3,SECTORNO       * Sector of Parameter
.
          UNPACK    DIM127,ANS,KEY3,DIM127
          MOVE      ZERO,CONPOSIT       * Position of Parameter
          MOVE      KEY3,CONPOSIT       * Position of Parameter
.
          UNPACK    DIM127,ANS,KEY1,DIM127
          MOVE      ZERO,FIELDTY        * Field Type 0=DIM 1=FORM
          MOVE      KEY1,FIELDTY
          UNPACK    DIM127,ANS,KEY1,DIM127
          MOVE      ZERO,FIELDLEN       * Field Length
          MOVE      KEY1,FIELDLEN
.
          BRANCH    FIELDTY,CONFLP50
          BRANCH    FIELDLEN,CONFLP11,CONFLP12,CONFLP13,CONFLP14,CONFLP15:
                             CONFLP16,CONFLP17,CONFLP18,CONFLP19
          GOTO      CONFLP99
CONFLP11  READ      CONTROLF,SECTORNO;*CONPOSIT,KEY1
          WRITE     HTMLFILE;KEY1;
          GOTO      CONFLP99
CONFLP12  READ      CONTROLF,SECTORNO;*CONPOSIT,KEY2
          WRITE     HTMLFILE;KEY2;
          GOTO      CONFLP99
CONFLP13  READ      CONTROLF,SECTORNO;*CONPOSIT,KEY3
          WRITE     HTMLFILE;KEY3;
          GOTO      CONFLP99
CONFLP14  READ      CONTROLF,SECTORNO;*CONPOSIT,KEY4
          WRITE     HTMLFILE;KEY4;
          GOTO      CONFLP99
CONFLP15  READ      CONTROLF,SECTORNO;*CONPOSIT,KEY5
          WRITE     HTMLFILE;KEY5;
          GOTO      CONFLP99
CONFLP16  READ      CONTROLF,SECTORNO;*CONPOSIT,KEY6
          WRITE     HTMLFILE;KEY6;
          GOTO      CONFLP99
CONFLP17  READ      CONTROLF,SECTORNO;*CONPOSIT,KEY7
          WRITE     HTMLFILE;KEY7;
          GOTO      CONFLP99
CONFLP18  READ      CONTROLF,SECTORNO;*CONPOSIT,KEY8
          WRITE     HTMLFILE;KEY8;
          GOTO      CONFLP99
CONFLP19  READ      CONTROLF,SECTORNO;*CONPOSIT,KEY9
          WRITE     HTMLFILE;KEY9;
          GOTO      CONFLP99
.
CONFLP50  BRANCH    FIELDLEN,CONFLP51,CONFLP52,CONFLP53,CONFLP54,CONFLP55:
                             CONFLP56,CONFLP57,CONFLP58,CONFLP59
          GOTO      CONFLP99
CONFLP51  READ      CONTROLF,SECTORNO;*CONPOSIT,CONFRM1
          WRITE     HTMLFILE;CONFRM1;
          GOTO      CONFLP99
CONFLP52  READ      CONTROLF,SECTORNO;*CONPOSIT,CONFRM2
          WRITE     HTMLFILE;CONFRM2;
          GOTO      CONFLP99
CONFLP53  READ      CONTROLF,SECTORNO;*CONPOSIT,CONFRM3
          WRITE     HTMLFILE;CONFRM3;
          GOTO      CONFLP99
CONFLP54  READ      CONTROLF,SECTORNO;*CONPOSIT,CONFRM4
          WRITE     HTMLFILE;CONFRM4;
          GOTO      CONFLP99
CONFLP55  READ      CONTROLF,SECTORNO;*CONPOSIT,CONFRM5
          WRITE     HTMLFILE;CONFRM5;
          GOTO      CONFLP99
CONFLP56  READ      CONTROLF,SECTORNO;*CONPOSIT,CONFRM6
          WRITE     HTMLFILE;CONFRM6;
          GOTO      CONFLP99
CONFLP57  READ      CONTROLF,SECTORNO;*CONPOSIT,CONFRM7
          WRITE     HTMLFILE;CONFRM7;
          GOTO      CONFLP99
CONFLP58  READ      CONTROLF,SECTORNO;*CONPOSIT,CONFRM8
          WRITE     HTMLFILE;CONFRM8;
          GOTO      CONFLP99
CONFLP59  READ      CONTROLF,SECTORNO;*CONPOSIT,CONFRM9
          WRITE     HTMLFILE;CONFRM9;
          GOTO      CONFLP99
.
CONFLP99  RETURN
.

.------------------------------------------------------------------------------
. Control File (Long) Parameter Tag - %STANDARD.01.044.xxx.yyy.zzz where:
. xxx = sector number (numeric)
. yyy = field position (numeric)
.   z = field length (1-100) - max 100, limited by CHAR100
.
. N.B: Be sure to use the actual control file variable name in templates.
. e.g. <input type=hidden name=ibcnmhos value="%STANDARD.01.044.000.043.999">
.------------------------------------------------------------------------------

CFLONG00  UNPACK    DIM127,ANS,KEY3,DIM127
          MOVE      ZERO,SECTORNO   
          MOVE      KEY3,SECTORNO                * Sector of Parameter
.
          UNPACK    DIM127,ANS,KEY3,DIM127
          MOVE      ZERO,CONPOSIT
          MOVE      KEY3,CONPOSIT                * Position of Parameter
.
          UNPACK    DIM127,ANS,KEY3,DIM127
          MOVE      ZERO,FIELDLNG
          MOVE      KEY3,FIELDLNG                * Field Length (Long)
.
          RESET     CHAR100
          READ      CONTROLF,SECTORNO;*CONPOSIT,CHAR100
          MOVE      FIELDLNG,LENGTH
          SETLPTR   CHAR100,LENGTH
          STRIP     CHAR100
          WRITE     HTMLFILE;*+,CHAR100;
.
CFLONG99  RETURN
.------------------------------------------------------------
. Remote Script Read WEB Fifo for User ID and Process ID
.------------------------------------------------------------
XMLRED00  COMMIT
.
XMLRED10  CALL      CLRPAR00              * Clear Parameters in Program
.
          MOVE      SP256,SECUREID        * Clear Page Level Security Parameters
          MOVE      SP256,SECUREPW
          MOVE      ZERO,SECURETY
          MOVE      ZERO,REPORTNO
          MOVE      SP70,TMOTFRST
          MOVE      SP70,TMOTNEXT
          MOVE      SP70,TEMPLATE
          MOVE      SP70,ICDRDDTE
          MOVE      ZERO,ICDRDVER
          MOVE      ZERO,CATACTIV
.
XMLRED20  DISPLAY   *R,"<";                  * Log File Heart Beat
          MOVE      SP70,WEBPID           * Process ID
          MOVE      SP70,USERID           * User
          READ      WEBFIFO1;ANS,WEBPID,USERID;         * Fifo Read
          DISPLAY   ">"                   * Log File Heart Beat
          MATCH     STXXPING,ANS          * Check for Ping
          GOTO      XMLRED91 IF EQUAL     * Yes
          MATCH     STXXEXIT,ANS          * Check for Exit
          GOTO      XMLRED92 IF EQUAL     * Yes
          MATCH     STXX,ANS              * Wait for Start of Message Sync
          GOTO      XMLRED20 IF NOT EQUAL * Loop No Start of Message Sync
.
          MOVE      ZERO,NHIREAD          * NHI Record Not Read
.
          CALL      IBACLOCK               * Clock Current Date
          CLOCK     TIME,CTIMEIS
.
          CALL      GETQRY00              * Get HTTP Request Details
.
.
.         *** Note: USERID from the FIFO is not reliable from v10.04  
.
          PACK      KEY80,LOGINAME,SP256
          CALL      RDWBSE7
          IF        OVRCD=0
            MOVE      WBSEUID,USERID
            GOTO      XMLRED40
          ENDIF
.         
.         try again in lower case
.         
XMLRED30  PACK      KEY80,LOGINAME
          REPLACE   LOWUPP,KEY80          * xlate to lower case
          CALL      RDWBSE7
          IF        OVRCD=0
            MOVE      WBSEUID,USERID
          ENDIF
.
.
XMLRED40  COMPARE   "999",REPORTNO        * End Server Process Report Option 999
          GOTO      XMLRED92 IF EQUAL 
          MOVE      ZERO,EXIT
          GOTO      XMLRED99     * Time Out Performed in Session
.
. Ping Return End of Document
.
XMLRED91  CALL      CLOSHTML   * End of Document
          GOTO      XMLRED10
.
. Shutdown Service
.
XMLRED92  CLOSE     HTMLFILE,DELETE
          CLOSE     WEBFIFO1
          MOVE      ONE,EXITFLAG
          CHAIN     "SHUTDOWN"
.
XMLRED99  BEGIN
.
          RETURN
.------------------------------------------------------------
. Invalid visit for user based on current hospital logged in to
.------------------------------------------------------------
WINENQ00  CALL      OPENHTML
          BRANCH    EXIT,WINENQ99
.
          REP       " +",ADMISSNO
          REP       " +",URNUMBER
.
          PACK      REDIRECT,SP70,SP70
          CLEAR     REDIRECT
          APPEND    "patweb98.pbl?reportno=01&template=001",REDIRECT
          APPEND    "&urnumber=",REDIRECT
          APPEND    URNUMBER,REDIRECT
          APPEND    "&invadmno=",REDIRECT
          APPEND    ADMISSNO,REDIRECT
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#" ":
                             "src=#"../js/Standard.js#"></script>":
                             "<script type=#"text/javascript#" ":
                             "src=#"../js/Custom.js#"></script>":
                             "<script type=#"text/javascript#"> {":
                             "getTop().content.location.href=#"",REDIRECT,"#"}":
                             "</script>"
          CALL      CLOSHTML

          REP       "+ ",ADMISSNO
          REP       "+ ",URNUMBER
.

          CALL      CLOSHTML     * End of Document
.
WINENQ99  RETURN
+
.------------------------------------------------------------
.  Redirect to REDIRECT URL
.------------------------------------------------------------
RDIREC00  CALL      OPENHTML
          BRANCH    EXIT,RDIREC99
          BRANCH    FHIRTYPE,RDIREC10
          WRITE     HTMLFILE;"<html><head>":
                             "<script type=#"text/javascript#">{":
                             " location.href=#"",REDIRECT,"#";":
                             "}</script>":
                             "</head>":
                             "<body></body></html>"
          CALL      CLOSHTML
          GOTO      RDIREC99
RDIREC10  CALL      FHRSUC00
RDIREC99  RETURN
.------------------------------------------------------------
. FHIR Success Outcome
.------------------------------------------------------------
FHRSUC00  STRIP     WBTPFIL
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
          MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
          PACK      TEMPLFIL,SP70,SP70
          PACK      KEY13,PRGID,KEY2,KEY3
          CALL      RDWBTP1
          IF        OVRCD=1
            MOVE      SP70,WBTPFIL
          ENDIF
.
          WRITE     HTMLFILE;*+,"{ #"resourceType#": #"OperationOutcome#",":
                    " #"extension#": [":
                    " { #"url#":#"%BASEURL/extension/webPASService#"":
                        " ,#"valueString#":#"",PRGID,SP1,PRGNAM,SP1,VERSION,"#"},":
                    " { #"url#":#"%BASEURL/extension/webPASTemplate#"":
                        " ,#"valueString#":#"%TEMPLATEHOME/",WBTPFIL," %TEMPLATEVERSION#"}":
                    " ],":
                    " #"id#": #"allok#", ":
                    "  #"text#": { ":
                    "    #"status#": #"additional#", ":
                    "    #"div#": #"<div>All Ok</div>#" ":
                    "  }, ":
                    "  #"issue#": [ ":
                    "    { ":
                    "      #"severity#": #"information#", ":
                    "      #"code#": #"informational#", ":
                    "      #"details#": { ":
                    "        #"text#": #"All Ok#"":
                    "  } } ] }"  

          CALL      CLOSHTML     * End of Document
          RETURN
.------------------------------------------------------------
. FHIR Error Outcome
.------------------------------------------------------------
FHRERR00  STRIP     WEBTITLE
.
          MOVE      REPORTNO,F2
          MOVE      F2,KEY2
          REP       " 0",KEY2
          MOVE      TEMPLATE,F3
          MOVE      F3,KEY3
          REP       " 0",KEY3
          PACK      TEMPLFIL,SP70,SP70
          PACK      KEY13,PRGID,KEY2,KEY3
          CALL      RDWBTP1
          IF        OVRCD=1
            MOVE      SP70,WBTPFIL
          ENDIF
.
          STRIP     WBTPFIL
          WRITE     HTMLFILE;*+,"{ #"resourceType#": #"OperationOutcome#",":
                    " #"extension#": [":
                    " { #"url#":#"%BASEURL/extension/webPASService#"":
                        " ,#"valueString#":#"",PRGID,SP1,PRGNAM,SP1,VERSION,"#"},":
                    " { #"url#":#"%BASEURL/extension/webPASTemplate#"":
                        " ,#"valueString#":#"%TEMPLATEHOME/",WBTPFIL," %TEMPLATEVERSION#"}":
                    " ],":
                    "  #"text#": { ":
                    "    #"div#": #"<div>",WEBTITLE,"</div>#" ":
                    "  }, ":
                    "  #"issue#": [ ":
                    "    { ":
                    "      #"severity#": #"error#", ":
                    "      #"code#": #"invalid#", ":
                    "      #"details#": { ":
                    "        #"text#": #"",WEBTITLE,"#"":
                    "  } } ] }"  

          CALL      CLOSHTML     * End of Document
          RETURN
.------------------------------------------------------------
.  Redirect URL that will redirect the map content frame
.  and reload to map menu frame
.------------------------------------------------------------
RFRESH00  CALL      OPENHTML
          BRANCH    EXIT,RFRESH99
          WRITE     HTMLFILE;"<html><head>":
                             "<script type=#"text/javascript#">{":
                             "if (parent.menu.document.readyState==#"complete#") {":
                             " parent.menu.location.reload(); }":
                             "setTimeout(function(){location.href=#"",REDIRECT,"#";},200);":
                             "}</script>":
                             "</head>":
                             "<body></body></html>"
.
          CALL      CLOSHTML
RFRESH99  RETURN
.------------------------------------------------------------
. Redirect Page -Javascipt for 2 sec delay Showing Record Saved
.------------------------------------------------------------
RSAVED00  CALL      OPENHTML
          BRANCH    EXIT,RSAVED99
          WRITE     HTMLFILE;"<html><head>":
                             "<script type='text/javascript'>{":
                             "setTimeout(function(){location.href=#"",REDIRECT,"#";},200);":
                             "}</script>":
                             "</head>":
                             "<body><h3 style='text-align:center';>Record Saved</h3>":
                             "</body></html>"
          CALL      CLOSHTML
RSAVED99  RETURN
.
.------------------------------------------------------------
. Set Oracle Application Client Action to Program ID
.------------------------------------------------------------
ORCACT00  MOVE      ZERO,DBTYPE   * Assume Oracle Database Type
          GETENV    "QISNAME",KEY20
          PACK      KEY20,KEY20,SP70
          MATCH     KEY20,SP70
          IF        @EQUAL
            MOVE      ONE,DBTYPE  * QISNAME Not Set so CISAM Database Type
          ENDIF
          BRANCH    DBTYPE,ORCACT99
          PACK      KEY32,PRGID,SP1,VERSION,SP1,PRGNAM,SP70
          CLEAR     SQLEXECC
          APPEND    "BEGIN ",SQLEXECC
          APPEND    " DBMS_APPLICATION_INFO.SET_ACTION('",SQLEXECC
          APPEND    KEY32,SQLEXECC
          APPEND    "');",SQLEXECC
          APPEND    " END;",SQLEXECC
          RESET     SQLEXECC
          QEXIMM    SQLEXECC
ORCACT99  RETURN
.
.-----------------------------------------------------------------------
. Set Oracle Application Client Information to User Security ID and Name
.-----------------------------------------------------------------------
ORCCLI00  BRANCH    DBTYPE,ORACLI99
          PACK      KEY64,WBSEUID,WBSENAM,SP70
          CLEAR     SQLEXECC
          APPEND    "BEGIN ",SQLEXECC
          APPEND    " DBMS_APPLICATION_INFO.SET_CLIENT_INFO('",SQLEXECC
          APPEND    KEY64,SQLEXECC
          APPEND    "');",SQLEXECC
          APPEND    " END;",SQLEXECC
          RESET     SQLEXECC
          QEXIMM    SQLEXECC
ORACLI99  RETURN
.
.******************************************************************************
.                               SEXDSC00
.                         get sex description
.         Requires DSEXCHAR as input
.         Returns  DSEXDESC    8 character description
.                  DSEXNO      Form 1 field for 1=M 2=F 3=I 4=R 9=U
.******************************************************************************
.
SEXDSC00  MOVE      NINE,DSEXNO             * default 9
.
          PACK      KEY5,ANSG,SP1,DSEXCHAR,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,SEXDSC90
.
          MOVE      ZERO,F1
          MOVE      TCINDC1,F1
          MOVE      F1,DSEXNO
.
          PACK      DSEXDESC,TDESC,SP70
          GOTO      SEXDSC99
.
SEXDSC90  MOVE      "Invalid Sex",DSEXDESC
.
SEXDSC99  RETURN
+
.******************************************************************************
. get sex details from Category G for long description, short dsecription,
. indicator 1 and associate field 1
.
. Input : DSEXCHAR   from IBACOMM
. Output: DSEXLDES   Long description
.         DSEXSDES   Short description
.         DSEXASC1   Associate field 1
.         DSEXIND1   Indicator 1
.******************************************************************************
.
SEXDSP00  MOVE      SP70,DSEXLDES
          MOVE      SP70,DSEXSDES
          MOVE      SP70,DSEXASC1
          MOVE      SP70,DSEXIND1
.
          MATCH     SP70,DSEXCHAR
          GOTO      SEXDSP99 IF EQUAL
.
          PACK      KEY5,ANSG,SP1,DSEXCHAR,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,SEXDSP90
.
          PACK      DSEXLDES,PTCDLDES,SP70
          PACK      DSEXSDES,TDESC,SP70
          MOVE      TCINDC1,DSEXIND1
.
          PACK      DSEXASC1,PTCDASC1,SP70
          MATCH     SP70,DSEXASC1
          IF        @EQUAL
            MOVE      DSEXCHAR,DSEXASC1
          ENDIF
.
          GOTO      SEXDSP99
.
SEXDSP90  MOVE      "Invalid Sex",DSEXLDES
.
SEXDSP99  RETURN
+
.------------------------------------------------------------
.  Output common system parameter value from comparaf
.  Usage: %STANDARD.01.045.AAAAAAAA
.         AAAAAAAA = 8 character parameter name
.         Required IBCNMHOS to have been read
.------------------------------------------------------------
          DEFPROC   COMPARVL
.
          INC       COMPARFD/INC
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      COMPARA1,"comparaf"          * File create during live
          TRAPCLR   IO                           * V11.00 release
          BRANCH    OVRCD,COMPAR99
.
COMPAR00  COMPARE   ZERO,IBCNMHOS                * Not multi hospital
          GOTO      COMPAR50 IF EQUAL
.
          PACK      KEY10,USERID,SP70
          CALL      RDWBSE1                      * Read user id
          BRANCH    OVRCD,COMPAR50
.
          MATCH     SP70,WBSEHOSP                * Users hospital blank
          GOTO      COMPAR50 IF EQUAL
.
          PACK      KEY11,WBSEHOSP,D8,SP70
          CALL      RDCMPAR1                     * Read hospital parameter
          BRANCH    OVRCD,COMPAR50               * Try defalut with no hospital
.
          MATCH     "0",CMPAACTV
          GOTO      COMPAR80 IF EQUAL            * found active data
.
COMPAR50  PACK      KEY11,SP3,D8,SP70
          CALL      RDCMPAR1                     * Read default system parameter
          BRANCH    OVRCD,COMPAR90
.
          MATCH     "0",CMPAACTV
          GOTO      COMPAR80 IF EQUAL            * found active data
.
          GOTO      COMPAR90
.
COMPAR80  STRIP     CMPAVALU
          WRITE     HTMLFILE;*+,CMPAVALU;        * Parameter Value
.
COMPAR90  CLOSE     COMPARA1
          GOTO      COMPAR99
.
          INC       IBAOVRIO/INC
          INC       COMPARIO/INC
.
COMPAR99  ENDPROC
