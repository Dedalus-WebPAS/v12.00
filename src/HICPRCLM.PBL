. *----------------------------------------------------------------------
. * Include Name  :   HICPRCLM.PBL
. *
. * Function      :   Function to print HIC Claim 
. *
. * Mods          :   
. *                   V10.14.01 12/07/2019  Ebon Clements  TSK 0877982
. *                             Updated key length for RSPRDO3 KEY28 to KEY32
. *                   V10.05.01 21/10/2014  Ebon Clements  CAR 268970
. *                            Change to use OUTCLIFD index 1 instead of index 2
. *                   V9.04.07 08/09/2005 J.Tan  CAR 66205
. *                            Mods printing an extra line on first page
. *                   V9.04.06 25/08/2005 J.Tan  CAR 71442
. *                            Mods primary doctor of Medical Practice as the
. *                            payee provider
. *                   V9.04.05 17/08/2005 J.Tan  CAR 71442
. *                            Fixed printing missing provider number
. *                   V9.04.04 12/08/2005 Davin  CAR 71442
. *                            Added printing of payee provider
. *                            Changed 'doctor' to 'service provider'
. *                   V9.04.03 01/08/2005 J.Tan  CAR 69062
. *                            Changed claim index 7 to include voucher no
. *                   V9.04.02 07/07/2005 J.Tan
. *                            Fixed free format ref provider no
. *                   V9.04.01 01/06/2005 J.Tan
. *                            Fixed printing ref provider no
. *                   V9.04.00 19/04/2005 J.Tan
. *                            Created include
. *
. * Parameter     :   BATCHKEY  
. * Variables     :   CLMCOUNT - FORM5
. *                   REFPROV  - DIM20
. *                   FORM1
. *----------------------------------------------------------------------
.
HICPRCLM  MOVE      ZERO,CPAGENO
PRHC0000  MOVE      ZERO,EXIT
          MOVE      ZERO,CLMCOUNT
          MOVE      ZERO,HCBTCAMT            * initialise batch total
.
          MOVE      BATCHKEY,KEY33           * read batch file
          CALL      RDHCBTC1
          BRANCH    OVRCD,PRHC5000
.
          CALL      OUTFIL00       * Check Correct Files Are Open & Valid Site
          CALL      PRHD0000                 * Print header
.
          PACK      KEY15,HCBTBTCH,SP70      * get all claims for the batch
          CALL      RSHCCLM7
PRHC1000  CALL      RKHCCLM7
          BRANCH    OVRCD,PRHC5000
          MATCH     HCBTBTCH,HCCLBTCH        * Same Batch number?
          GOTO      PRHC5000 IF NOT EQUAL
.
          PACK      KEY16,HCCLVISN,SP70      * check link referral
          CALL      RSALLNK2
          CALL      RKALLNK2
          BRANCH    OVRCD,PRHC1000
          MATCH     HCCLVISN,ALLNLNKV
          GOTO      PRHC1000 IF NOT EQUAL 
.
          PACK      KEY8,ALLNVISN,SP70       * Patient Referral Details
          CALL      RDALREF1
          BRANCH    OVRCD,PRHC1000           * missing referral record
          CALL      PROV0000                 * Get ref provider
.
          ADD       ONE,CLMCOUNT
          UNPACK    HCCLDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          MOVE      HCCLURNO,KEY8            * read master record
          CALL      RDMAST1
          BRANCH    OVRCD,PRHC1000
          CALL      RDPMPX21
          BRANCH    OVRCD,PRHC1000
.
          COMPARE   "70",CLNO
          CALL      PRHD0000 IF NOT LESS
.
          UNPACK    HCCLDATE,CCENT,CYEAR,CMON,CDAY    * claim date
          CALL      PACDATE
          PRINT     *1,CLMCOUNT,*6,CPDATE,*16,PURNO;
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY    * dob
          CALL      PACDATE
.
          MOVE      TWO,PACFRMT
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          CALL      PACNAME                  * set patient name
          MOVE      PACFNAME,KEY20
.
          PRINT     *26,KEY20,*48,CPDATE,*60,PMEDI;
.
          MATCH     SP70,PMPXMEDC
          IF        !@EQUAL
            UNPACK    PMPXMEDC,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            PRINT     *72,CPDATE;
          ENDIF
.
          MOVE      ZERO,FORM1
          PACK      KEY18,HCCLCLMN,HCCLVISN,SP70
          CALL      RSHCCIT1
PRHC2000  CALL      RKHCCIT1
          BRANCH    OVRCD,PRHC3000
.
          MATCH     HCCLCLMN,HCCICLMN          * Same claim number
          GOTO      PRHC3000 IF NOT EQUAL
.
          MATCH     HCCLVISN,HCCIVISN          * Same visit number
          GOTO      PRHC3000 IF NOT EQUAL
.
          UNPACK    HCCIIDES,KEY25
          PRINT     *82,HCCIITMN,*92,KEY25,*118,HCCICAMT
          MOVE      ONE,FORM1
          ADD       ONE,CLNO
          GOTO      PRHC2000
.
PRHC3000  IF        FORM1=0
            PRINT     *N;
            ADD       ONE,CLNO
          ENDIF
.
          PRINT     *1,"Ref.Provider: ",REFPROV,*35,"Start Date: ";
.
          MATCH     SP70,ALRERTYP           * No referral type
          GOTO      PRHC3200 IF EQUAL
          UNPACK    ALRERDAT,CCENT,CYEAR,CMON,CDAY     * referral date
          CALL      PACDATE
.
          PRINT     *48,CPDATE:
                    *60,"Period: ";
          PACK      KEY5,ANSR,ANSI,ALRERTYP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PRHC3200
          COMPARE   ZERO,TCFORM6            * No days for expiry warning
          GOTO      PRHC3200 IF EQUAL
          DIV       "30",TCFORM6
          PRINT     *68,TCFORM6," month(s)";
.
PRHC3200  PRINT     *N
          GOTO      PRHC1000
.
.         Print end of report
.
PRHC5000  PRINT     *1,"Total Entried: ",CLMCOUNT:
                    *N,*1,"Total for this Claim: ",HCBTCAMT
.
PRHC9999  RETURN
.
.***************************************************************************
.*                              PROV0000                                   *
.*        Get the referring provider from either the referring HCP/Practice*
.*        (if not blank), or the free format referring HCP field.          *
.***************************************************************************
PROV0000  MOVE      SP70,REFPROV
          MATCH     SP10,ALRERHCP                * referring HCP blank ?
          IF        !@EQUAL
            MATCH     SP10,ALRERHCR              * no - ref. HCP Practice blank?
            IF        !@EQUAL
              PACK      KEY20,ALRERHCP,ALRERHCR  * no
              CALL      RDPMHCL1                 * HCP link record found ?
              BRANCH    OVRCD,PROV8000           * no - get free format HCP
.
              MATCH     SP10,PMHLPRV1
              IF        !@EQUAL
                MOVE      PMHLPRV1,REFPROV
                GOTO      PROV9999
              ENDIF
.
              MATCH     SP10,PMHLPRV2
              IF        !@EQUAL
                MOVE      PMHLPRV2,REFPROV
                GOTO      PROV9999
              ENDIF
.
              MATCH     SP10,PMHLPRV3
              IF        !@EQUAL
                MOVE      PMHLPRV3,REFPROV
                GOTO      PROV9999
              ENDIF
.
              MATCH     SP10,PMHLPRV4
              IF        !@EQUAL
                MOVE      PMHLPRV4,REFPROV
                GOTO      PROV9999
              ENDIF
.
              MATCH     SP10,PMHLPRV5
              IF        !@EQUAL
                MOVE      PMHLPRV5,REFPROV
                GOTO      PROV9999
              ENDIF
            ELSE
              PACK      KEY10,ALRERHCP,SP70
              CALL      RDPMHCP1
              BRANCH    OVRCD,PROV8000
              MATCH     SP10,PMHCPRV1            * HCP Provider number
              GOTO      PROV8000 IF EQUAL
.
              MOVE      PMHCPRV1,REFPROV 
              GOTO      PROV9999
            ENDIF
          ENDIF
.
          MATCH     SP70,ALREHCPP
          IF        !@EQUAL
            MOVE      ALREHCPP,REFPROV
            GOTO      PROV9999
          ENDIF
.
.         Can't find a provider number.
.
PROV8000  MOVE      "Provider no missing.",REFPROV
PROV9999  RETURN
+
.***************************************************************************
.*                              PRHD0000                                   *
.*           Remove all current items already claimed (but not batched)    *
.***************************************************************************
PRHD0000  COMPARE  ZERO,CPAGENO              * First page ?
          GOTO     PRHD1000 IF EQUAL         * Yes. Don't print underlines
          CALL     UND132                    * Print the last line of prev page
.
PRHD1000  ADD      ONE,CPAGENO
          CALL     IBACLOCK
          MOVE     CCC,CCENT
          MOVE     CYY,CYEAR
          MOVE     CMM,CMON
          MOVE     CDD,CDAY
          CALL     PACDATE
          MOVE     CPCDATE,CDATE
          PRINT    *F,PRGID,*40,CNAME,*116,"DATE : ",CDATE:
                   *N,VERSION,*40,"Medicare Consolidation Report":
                   *116,"TIME :   ",CTIMEIS:
                   *N,*116,"PAGE :        ",CPAGENO:
                   *N,*116,"Claim: ",HCBTBTCH
.
.         Default Service provider from clinic id
.
          OPEN      PRIDOCT3,"pridoctf"
          MOVE      SP70,OCADESC
          PACK      KEY20,HCBTSITE,HCBTCLID,Z70
          CALL      RDSCLIA1
          CALL      RDPCLIA1                *To get Clinic ID Description
          BRANCH    OVRCD,PRHD2000
.
          MATCH     HCBTSITE,OCASITE
          GOTO      PRHD2000 IF NOT EQUAL       * default to clinic desc.
.
          MATCH     HCBTCLID,OCACLIN
          GOTO      PRHD3000 IF EQUAL       * default to clinic desc.
.
PRHD2000  MOVE      SP70,OCADESC
.
PRHD3000  PACK      KEY32,HCBTPSRV,SP70
          CALL      RSPRDO3
          CALL      RKPRDO3
          BRANCH    OVRCD,PRHD4000
.
          MATCH     HCBTPSRV,PRDOPROV         * Same provider number?
          GOTO      PRHD4000 IF NOT EQUAL
.
.         Get doctor name
.
          PACK      KEY10,PRDODOCT,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,PRHD4000
.
          MOVE      "1",PACFRMT
          MOVE      PMHCTITL,PACTITLE
          MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          CALL      PACNAME
          MOVE      SP70,OCADESC
          MOVE      PACFNAME,OCADESC
.
PRHD4000  MATCH     HCBTPYEE,HCBTPSRV
          GOTO      PRHD5000 IF NOT EQUAL
.
          MOVE      OCADESC,PRPRDESC 
          GOTO      PRHD6000
.
.         Get payee description from primary doctor of  medical practice
.
PRHD5000  MOVE      SP70,PRPRDESC
          PACK      KEY6,HCBTMPRA,SP70
          CALL      RDPRPR1                 * get medical practice
          BRANCH    OVRCD,PRHD6000
.
          MATCH     SP70,PRPRPDOC
          GOTO      PRHD6000 IF EQUAL
.
          PACK      KEY10,PRPRPDOC,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,PRHD6000
.
          MOVE      "1",PACFRMT
          MOVE      PMHCTITL,PACTITLE
          MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          CALL      PACNAME
          MOVE      PACFNAME,PRPRDESC
.
PRHD6000  PRINT    *1,"Payee Provider  : ",PRPRDESC," - ",HCBTPYEE:
                   *N,*1,"Service Provider: ",OCADESC," - ",HCBTPSRV:
                   *N,*N,*1,"Line",*6,"Date",*16,"Code":
                   *26,"Patient",*48,"DOB",*60,"Medicare ##":
                   *72,"Exp.Date":
                   *82,"Item",*92,"Item Description",*127,"Amount"
          CALL     UND132
          MOVE     NINE,CLNO
PRHD9999  RETURN
.
.*******************************************************************************
.                     Check Correct Files are Open for System
.*******************************************************************************
OUTFIL00  MOVE      HCBTSITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,OUTFIL99
          MATCH     OSTFILE,CFILEPRE         * Save Current File Prefix
          IF        !@EQUAL
            MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
            CALL      OPNOUT00               * Open Outpatient Files
          ENDIF
OUTFIL99  RETURN
+
.
