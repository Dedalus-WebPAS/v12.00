.------------------------------------------------------------
.  File          :  PMSVKIPR.PBL
.
.  Function      :  Update Keyword Index File for transfer source Table
.
.  Parameters    :  PTVACODE - Key to transfer source table
.
.  Modifications :  V10.01.01 01/03/2011 Ebon Clements CAR 207295
.                             Created include
.  Notes
.  -----
.  This Skeleton Routine is to Create a Procedure to Update
.  a Key Word Index Table that can be used for searching 
.  Masterfiles. The procedure should be called from the 
.  Add, Update and Delete functions in the Masterfile 
.  Maintenance Function.
.  
.  A Key Word Index Table is Defined in the following Manner
.                              
.  PMVKCODE  DIM       5       * Transfer Destination Code
.  PMVKKKWD  DIM       30      * Key Word
.  PMVKSPAR  DIM       25      * Spare
.
.  Index 1   PMVKCODE, PMVKKKWD
.  Index 2   PMVKKKWD, PMVKCODE
.
.  To convert this Procedure the following should be performed
.    Global Change PAT to the System ID eg :%s/PAT/PAT/g
.    Global Change CKI to the File   ID eg :%s/CKI/CKI/g
.    Global Change PTDK to the IO Call ID eg :%s/PTDK/PTDK/g
.    Modify the KEYINDEX defintion to combined Code Field Lengths eg 6
.    Global Change KEY21 to the Key for the Key Word Table eg KEY21
.    Change xxxxxxx to the Code Fields eg DCODEhh
.    Change the PVGE0000 routine to call the VADBW000 for each
.    string of key words to be indexed for the Coded Field
.    eg MOVE    DSNAME,KEYWORDS
.       CALL    VADBW000
.       MOVE    DGNAME,KEYWORDS
.       CALL    VADBW000
.       MOVE    TDESC,KEYWORDS
.       CALL    VADBW000
.------------------------------------------------------------
          DEFPROC   PMSVKIUP
.
          INC       PMSVKIFD/INC
.
KEYINDEX  DIM       5         * Code Index in Table
KEYWORDS  DIM       60        * String Containing the Key Words to be Indexed
SIXTY     FORM      "60"
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
.
.------------------------------------------------------------
. Generate Word Index
.------------------------------------------------------------
PVGE0000  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     PMSVKIA1,"pmsvkiaf"
          TRAPCLR  IO
          BRANCH   OVRCD,PVGE9999
.
          MOVE     PTVACODE,KEYINDEX
          CALL     VKYCL000           * Remove Current Key Word Index
          BRANCH   EXIT,PVGE9999
.
          MOVE     PTVACODE,KEY5      * Validate Record on File
          CALL     RDPTVAD1
          BRANCH   OVRCD,PVGE1900     * If Not then it Must be a Delete/Clear
.
          MOVE     PTVACODE,KEYINDEX
          MOVE     PTVADESC,KEYWORDS
          CALL     VADBW000
.
PVGE1900  CLOSE    PMSVKIA1
.
PVGE9999  GOTO     PMSVKIEP
.------------------------------------------------------------
. Clear Current Key Words
.------------------------------------------------------------
VKYCL000  PACK     KEY35,KEYINDEX,SP70
          CALL     RSPMVKI1
          CALL     RKPMVKI1
          BRANCH   OVRCD,VKYCL999
.
          MATCH    KEYINDEX,PMVKCODE
          GOTO     VKYCL999 IF NOT EQUAL
.
          PACK     KEY35,PMVKCODE,PMVKKKWD,SP70
          CALL     DEPMVKI1
          GOTO     VKYCL000
.
VKYCL999  RETURN
.-------------------------------------------------
. Determine Words to Index
.-------------------------------------------------
VADBW000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      VADBW000 IF NOT EOS
            GOTO      VADBW999               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWORDS,SP30      * reset form pointer
          MOVE      KEY60,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      VADBW100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Save this Word
.
VADBW100  MOVE      KEYWORDS,KEY30
          PACK      KEY30,KEY30,SP70
          REP       UPPLOW,KEY30             * Always Upper Case
          PACK      KEY35,KEYINDEX,KEY30
          UNPACK    KEY35,PMVKCODE,PMVKKKWD
          CALL      RDPMVKI1
          IF        OVRCD=1
            CALL      WRPMVKI1
          ENDIF
          GOTO      VADBW190
.
.         Check for any more words
.
VADBW190  SETLPTR   KEYWORDS,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY60,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY60,KEYWORDS
          GOTO      VADBW000
.
VADBW999  RETURN
.
          INC       PMSVKIIO/INC
          INC       IBAOVRIO/INC
.
PMSVKIEP  ENDPROC                          * End of Procedure
.
