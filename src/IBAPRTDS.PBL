.************************************************************************
.*                                                                      *
.*                               IBAPRTDS                               *
.*                                                                      *
.*     INCLUDE to display all printer codes on file                     *
.*                                                                      *
.*     Author      : Graeme Williams                                    *
.*                                                                      *
.*     Date        : 12/04/90                                           *
.*                                                                      *
.*     File Needed : CATMESA1    (Dietary Messages file)                *
.*                                                                      *
.************************************************************************
IBAPRTDS  MOVE      SP3,KEY3
          CALL      RDSPRTA1
          CALL      RDKPRTA1
          BRANCH    OVRCD,PRTDS400
.
. ------  display Heading and then loop over entries  ------
.
          DISPLAY   *P1:3,*EF,*P15:4,*V2LON,*ULON,"PRINTER CODES"
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
.
          CALL      RDSPRTA1
.
PRTDS100  CALL      RDKPRTA1
          BRANCH    OVRCD,PRTDS500
.
. ------  Check for a full screen  ------
.
          COMPARE   "70",CCOL
          GOTO      PRTDS300 IF LESS
.
.         We have a full screen. Ask them what they want to do
.
PRTDS200  DISPLAY   *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue, ":
                    "(",*V2LON,"F",*HOFF,")irst Screen, ":
                    "(",*V2LON,"N",*HOFF,")ext Screen : ";
.
PRTDS210  KEYIN     *P44:24,*DV,UNDLN1,*P44:24,*V2LON,ANS;
.
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          MATCH     SP1,ANS
          GOTO      PRTDS220 IF EQUAL
.
          REP       UPPLOW,ANS
.
          CMATCH    ANSN,ANS
          GOTO      PRTDS220 IF EQUAL
          CMATCH    ANSC,ANS
          GOTO      PRTDS500 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      IBAPRTDS IF EQUAL
          BEEP
          GOTO      PRTDS210
.
PRTDS220  MOVE      ONE,CCOL
          MOVE      FIVE,CVERT
          DISPLAY   *PCCOL:CVERT,*EF;
.
. ------  Display this printer  ------
.
PRTDS300  DISPLAY   *PCCOL:CVERT,*V2LON,PRTCODE,SP2,*HOFF,PRTDESC;
.
.         Get the next display position
.
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      PRTDS100 IF LESS
.
          MOVE      FIVE,CVERT
          ADD       TWENTY7,CCOL
          GOTO      PRTDS100
.
. ------  if missing Dietary Message code show code missing  ------
.
PRTDS400  DISPLAY   *P1:24,*B:
                    "No printer codes on file ";
          CALL      HITENTER
          MOVE      ONE,OVRCD
          GOTO      PRTDS999
.
. ------  end of codes  ------
.
PRTDS500  MOVE      ZERO,OVRCD
.
PRTDS999  RETURN
