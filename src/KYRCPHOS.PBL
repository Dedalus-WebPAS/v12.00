.*********************************************************************
.*  KYRCPHOS.PBL  :  Keyin a Hospital code                           *
.*                                                                   *
.*  Author        :  Lofty the Big Gay     11/05/94                  *
.*                                                                   *
.*  Paramaters    :  MRCNNOHS - How many hospitals (IBAMRT98)        *
.*                   HDNAMEx  - Hospital code                        *
.*                   HDDESCx  - Hospital description                 *
.*                   HDPATHx  - Hospital DPATH                       *
.*                   HLEF,HTOP- Top left hand co-ords of box         *
.*                                                                   *
.*  Returns       :  EXIT = 0 - All is OK,   Hosp selected = FORM1   *
.*                          1 - Exit/Hospital not selected           *
.*                          2 - Not using multi hospitals            *
.*                                                                   *
.*  Mods          :  19/04/1998  Steve Armstrong   STH               *
.*                   Mods for 6 databases                            *
.*                                                                   *
.*********************************************************************
KYRCPHOS  PACK      XXDPATH,SP30,SP30,SP30,SP30
.
          IF        MRCNNOHS=0
            MOVE      TWO,EXIT              * not using multi hospitals
            GOTO      KHOS9999
          ENDIF
.
. make sure box will fit in screen
.
          IF        HLEF>=43
            MOVE      "42",HLEF
          ENDIF
          IF        HTOP>=15
            MOVE     "14",HTOP
          ENDIF
.
          ASSIGN    (HLEF+36),HRIG                * get other co-ords of box
          ASSIGN    (HTOP+9),HBOT
.
          CALL      GETSCR00                      * save screen
.
          ASSIGN    (HTOP+8),PHTOP                * get other positions on scren
          ASSIGN    (HLEF+1),PHLEF
.
          BOXCLR    HLEF,HTOP,HRIG,HBOT           * clear box
          BOX       16,HLEF,HTOP,HRIG,HBOT        * draw box
.
          ASSIGN    (ONE+HTOP),CVERT
          DISPLAY   *PPHLEF:CVERT,*V2LON,*ULON,"Cnt"
          ADD       FOUR,PHLEF
          DISPLAY   *PPHLEF:CVERT,*V2LON,*ULON,"Hospital                      "
          SUB       FOUR,PHLEF
          ADD       ONE,CVERT
          MOVE      ONE,HOSPCNT
          WHILE     HOSPCNT <= 6
            LOAD      KEY4,HOSPCNT,HDNAME1,HDNAME2,HDNAME3,HDNAME4,HDNAME5,HDNAME6
            LOAD      KEY30,HOSPCNT,HDDESC1,HDDESC2,HDDESC3,HDDESC4,HDDESC5,HDDESC6
            MATCH     SP4,KEY4
            IF        !@EQUAL
              DISPLAY   *PPHLEF:CVERT,*V2LON,HOSPCNT,*HOFF,". ",KEY30
              ADD       ONE,CVERT
            ENDIF
            ADD       ONE,HOSPCNT
          DO 
.
KHOS1000  KEYIN     *PPHLEF:PHTOP,*DV,SP30,*PPHLEF:PHTOP," Select item, (":
                    *V2LON,"E",*HOFF,")xit ? ":
                    *V2LON,ANS
          REP       UPPLOW,ANS
          MOVE      ONE,EXIT
          MATCH     ANSE,ANS
          GOTO      KHOS9000 IF EQUAL       * exit
.
          TYPE      ANS
          GOTO      KHOS2000 IF EQUAL       * number entered
.
KHOS1500  BEEP
          GOTO      KHOS1000
.
KHOS2000  MOVE      ZERO,FORM1
          MOVE      ANS,FORM1
          LOAD      KEY4,FORM1,HDNAME1,HDNAME2,HDNAME3,HDNAME4,HDNAME5,HDNAME6
          LOAD      HOSPNAME,FORM1,HDDESC1,HDDESC2,HDDESC3,HDDESC4,HDDESC5,HDDESC6
          LOAD      XXDPATH,FORM1,HDPATH1,HDPATH2,HDPATH3,HDPATH4,HDPATH5,HDPATH6
          MATCH     SP30,XXDPATH
          GOTO      KHOS1500 IF EQUAL
          CALL      PUTSCR00
.
.......   CALL      DHHN0000                * display hospital name
.
KHOS8000  MOVE      ZERO,EXIT
          GOTO      KHOS9999
.
KHOS9000  CALL      PUTSCR00
          MOVE      ONE,EXIT
.
KHOS9999  RETURN
+
.*********************************************************************
.         display the hospital name in highlight
.*********************************************************************
DHHN0000  STRIP     HOSPNAME
          MOVELPTR  HOSPNAME,FORM2
          ADD       TWO,FORM2
          ASSIGN    ((80-FORM2)/2),CCOL
          DISPLAY   *P1:7,*EF,*PCCOL:7,*V2LON,*HON,SP1,*+,HOSPNAME,SP1
.
DHHN9999  RETURN

