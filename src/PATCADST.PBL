.*********************************************************************
.*                                                                   *
.*   Program :    PATCADST.PBL                                       *
.*                                                                   *
.*   Function:    Input patients current address details             *
.*                                                                   *
.*   Author  :    Karin Peak   14/09/1990                            *
.*                                                                   *
.*   Comments:                                                       *
.*                                                                   *
.*   Returns :    PATCACAN     =  1 if cancel selected               *
.*                             =  0 if cancel not selected           *
.*                                                                   *
.*   Modif'ns:                                                       * 
.*********************************************************************
.*        V12.00.01 15.05.2025  DA Sarkies     TSK 0955096i          *
.*                  Updated for Alpha-Numeric Visit Number           *
.*********************************************************************
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.*        V5.08.01  22/08/2000  Tonii                                *
.*                  PSEX now accepts "U" & "I", "Unknown" and        *
.*                  "Indeter", as patient sex description            *
.*********************************************************************
.*                  04/10/1999  Andrew Peel  SRF 632649              *
.*                              Fixed pre-admission screens          *
.*                  09/07/1999  Caleb Sharman  SRF 644740            *
.*                              Mods changing patient data           *
.                   08/10/1998  Glenn Berry      5.07 changes
.*********************************************************************
.*                V5.01.01  13/12/90  DIG                            *
.*                          Changed display to clear screen from     *
.*                          line 4 so as to not delete alert info    *
.*                                                                   *
.*********************************************************************
.
PATCADST  MOVE      ZERO,PATCACAN
.
PATCA100  CALL      DSPPD000             * display patients basic details screen
          CALL      DATPD000             * display patients basic data screen
          CALL      SCRNN000             * display the screen format
.
          MOVE      ZERO,PATCACAD        * set update flags
.
          OPEN      PATCA1A1,"patca1af"
.
          MATCH     ZEROVISN,AADMNO
          GOTO      PATCA500 IF EQUAL
.
          MOVE      AADMNO,KEY8
          CALL      RDCADD1
          BRANCH    OVRCD,PATCA500
.
.------ modify an existing patient ----
.
          MOVE      ZERO,PATCAFLG
          CALL      DISPP000             * display patient details
          MOVE      ONE,PATCASFL
          CALL      SLECT000             * select item, post, cancel
          GOTO      PATCA999
.
.----- insert a new patient ----
.
PATCA500  MOVE      ZERO,PATCAFLG
          CALL      CLRRR000             * clear variables
          CALL      ISERT000             * add new patient details
          BRANCH    EXIT,PATCA100
          MOVE      ZERO,PATCASFL
          CALL      SLECT000             * select item, post, cancel
          GOTO      PATCA999
.
PATCA999  CLOSE     PATCA1A1
          RETURN
+
.**********************************************************************
.*                        DSPPD000                                    *
.*                Displays basic patients details screen              *
.**********************************************************************
.
DSPPD000  DISPLAY   *P1:4,*EF,"U/R Number   :",*P27:4,"Name :":
                    *P27:5,"Sex  :",*P43:5,"Born :",*P62:5,"Age  :" ;
          RETURN
+
.**********************************************************************
.*                        DATPD000                                    *
.*                Displays basic patients data screen                 *
.**********************************************************************
.
DATPD000  UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PATCABDT
.
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,PATCASEX
.* ******************************************   End of Changes         *C-49016
.
DATPD100  MOVE      SP1,ANS
          MOVE      PGNAME,ANS
          PACK      PATCANAM,ANS,DOT,PSNAME
.
          DISPLAY   *P16:4,*V2LON,PURNO:
                    *P34:4,PATCANAM:
                    *P34:5,PATCASEX:
                    *P50:5,PATCABDT:
                    *P69:5,PSAGE
DATPD999  RETURN
+
.**********************************************************************
.*                        SCRNN000                                    *
.*                Displays the screen format                          *
.**********************************************************************
.
SCRNN000  DISPLAY   *P1:7,*EF:
                    *P1:7,*V2LON," 1. ",*HOFF,"Current Address : ":
                    *P1:8,*V2LON," 2. ",*HOFF,"                : ":
                    *P1:9,*V2LON," 3. ",*HOFF,"                : ":
                    *P1:10,*V2LON," 4. ",*HOFF,"Post Code       : ":
                    *P1:11,*V2LON," 5. ",*HOFF,"LGA Code        : ":
                    *P1:12,*V2LON," 6. ",*HOFF,"Phone (Home)    : ":
                    *P1:13,*V2LON," 7. ",*HOFF,"Phone (Bus)     : " 
          RETURN
+
.*********************************************************************
.*                         DISPP000                                  *
.*             Display the contents of a patient record              *
.*********************************************************************
.
DISPP000  DISPLAY   *P23:7,*V2LON,PTCAADD1:
                    *P23:8,PTCAADD2:
                    *P23:9,PTCASUB:              
                    *P23:10,PTCAPCOD:                 
                    *P23:11,PTCALGA:                  
                    *P23:12,PTCAHPH:
                    *P23:13,PTCABPH                
          RETURN
.
.**********************************************************************
.*                        ISERT000                                    *
.*            Insert a new patients details                           *
.**********************************************************************
.
.
.----  This routine simply uses CCITEM as a counter and ---
.----  loops through the keyins by calling the modify   ---
.----  routine.                                         ---
.
ISERT000  MOVE      ZERO,CCITEM
.
ISERT100  ADD       ONE,CCITEM
.
          COMPARE   CCITEM,PATCAMAX      * test if counter > number of
          GOTO      ISERT999 IF LESS       fields on the screen
.
          BRANCH    PATCAFLG,ISERT999
.
          CALL      MDDFY000
          BRANCH    EXIT,ISERT999        * exitchar selected
.
          GOTO      ISERT100
.
ISERT999  RETURN
+
.**********************************************************************
.*                        SLECT000                                    *
.*              Select an item post or cancel                         *
.**********************************************************************
.
SLECT000  CALL      MAINQST
.
          COMPARE   "-1",CCITEM
          GOTO      SLECT100 IF NOT EQUAL   * cancel selected
          MOVE      ONE,PATCACAN
          GOTO      SLECT999
.
SLECT100  COMPARE   ZERO,CCITEM          * post selected
          GOTO      SLECT500 IF EQUAL
.
          CALL      MDDFY000
          BRANCH    EXIT,SLECT999       * exitchar selected
          GOTO      SLECT000
.
SLECT500  BRANCH    PATCASFL,SLECT700
          GOTO      SLECT999
.
.---- update an existing U/R number ----
.
SLECT700  DISPLAY   *P1:24,*EL,*V2LON,*BLINKON,"**** Posting Changes ****"
.          CALL      UPCAD000
.
SLECT999  RETURN
+
.**********************************************************************
.*                        MDDFY000                                    *
.*              Modify a new patients details                         *
.**********************************************************************
.
MDDFY000  COMPARE   CCITEM,PATCAMAX      * test if number <= to number
          GOTO      MDDFY005 IF LESS       of fields on the screen
.
MDDFY002  BRANCH    CCITEM,MDDFY010,MDDFY020,MDDFY030,MDDFY040,MDDFY050:
                           MDDFY060,MDDFY070
.
MDDFY005  BEEP
          GOTO      MDDFY999
.
MDDFY010  CALL      KCAD1000
          GOTO      MDDFY999
.
MDDFY020  CALL      KCAD2000
          GOTO      MDDFY999
.
MDDFY030  CALL      KCSUB000
          GOTO      MDDFY999
.
MDDFY040  CALL      KCPST000
          GOTO      MDDFY999
.
MDDFY050  CALL      KCLGA000 
          GOTO      MDDFY999
.
MDDFY060  CALL      KCHPH000 
          GOTO      MDDFY999
.
MDDFY070  CALL      KCBPH000
.
MDDFY999  RETURN
+
.*****************************************************************
.*                       KCAD1000                                *
.*               Keyin the current address one                   *
.*****************************************************************
.
KCAD1000  MOVE      ONE,PATCACAD
          MOVE      ZERO,EXIT   
.
          KEYIN     *P23:7,*DV,UNDLN20,*DV,UNDLN5:
                    *P23:7,*V2LON,PATCAAD1:
                    *P23:7,*DV,PATCAAD1
.
          ENDSET    PATCAAD1
          APPEND    SP30,PATCAAD1
          RESET     PATCAAD1
.
.         If enter is hit for current address line one the fields will 
.         default to the patients home address etc.
.
          MATCH     PATCAAD1,SP30
          GOTO      KCAD1999 IF NOT EQUAL
.
          MOVE      ONE,PATCAFLG
.
          MOVE      PADD1,PATCAAD1
          MOVE      PADD2,PATCAAD2
          MOVE      PSUBRB,PATCASUB
          MOVE      PPOST,PATCAPST 
          MOVE      PTMXLGA,PATCALGA 
          MOVE      PTELEP,PATCAHPH
          MOVE      PTELEB,PATCABPH
.
          DISPLAY   *P23:7,*V2LON,PATCAAD1:
                    *P23:8,PATCAAD2:
                    *P23:9,PATCASUB:              
                    *P23:10,PATCAPST:                 
                    *P23:11,PATCALGA:                 
                    *P23:12,PATCAHPH:
                    *P23:13,PATCABPH                
KCAD1999  RETURN
+
.*****************************************************************
.*                       KCAD2000                                *
.*                Keyin the current address two                  *
.*****************************************************************
.
KCAD2000  MOVE      ONE,PATCACAD
          MOVE      ZERO,EXIT   
.
          KEYIN     *P23:8,*DV,UNDLN20,*DV,UNDLN5:
                    *P23:8,*V2LON,PATCAAD2:
                    *P23:8,*DV,PATCAAD2
.
          RETURN
+
.*****************************************************************
.*                         KCSUB000                              *
.*                  Keyin current suburb                         *
.*****************************************************************
.
KCSUB000  MOVE      ONE,PATCACAD
          MOVE      ZERO,EXIT
.
          KEYIN     *P23:9,*DV,UNDLN15:
                    *P23:9,*V2LON,PATCASUB:
                    *P23:9,*DV,PATCASUB
.
          RETURN 
+
.*****************************************************************
.*                       KCPST000                                *
.*                Keyin current postcode                         *
.*****************************************************************
.
KCPST000  MOVE      ONE,PATCACAD
          MOVE      ZERO,EXIT
.
          KEYIN     *P23:10,*DV,UNDLN4:
                    *P23:10,*V2LON,PATCAPST:
                    *P23:10,*DV,PATCAPST
.
          RETURN 
+
.*****************************************************************
.*                       KCLGA000                                *
.*                    Keyin LGA Code                             *
.*****************************************************************
.
KCLGA000  MOVE      ONE,PATCACAD
          MOVE      ZERO,EXIT
.
          KEYIN     *P23:11,*DV,UNDLN4:
                    *P23:11,*V2LON,PATCALGA:
                    *P23:11,*DV,PATCALGA
.
          RETURN 
+
.*****************************************************************
.*                       KCHPH000                                *
.*               Keyin current home phone number                 *
.*****************************************************************
.
KCHPH000  MOVE      ONE,PATCACAD
.
          KEYIN     *P23:12,*DV,UNDLN12:
                    *P23:12,*V2LON,PATCAHPH:
                    *P23:12,*DV,PATCAHPH
.
          RETURN
+
.*****************************************************************
.*                       KCBPH000                                *
.*              Keyin current business phone number              *
.*****************************************************************
.
KCBPH000  MOVE      ONE,PATCACAD
.
          KEYIN     *P23:13,*DV,UNDLN12:            
                    *P23:13,*V2LON,PATCABPH:
                    *P23:13,*DV,PATCABPH
          RETURN
+
.**********************************************************************
.*                        CLRRR000                                    *
.*                Clear record variables                              *
.**********************************************************************
.
CLRRR000  CLEAR     PATCAAD1  
          CLEAR     PATCAAD2    
          CLEAR     PATCASUB   
          CLEAR     PATCAPST
          CLEAR     PATCALGA
          CLEAR     PATCAHPH     
          CLEAR     PATCABPH   
          RETURN
+
.**********************************************************************
.*                        PTUPD000                                    *
.*                Post or update current address file                 *
.**********************************************************************
.
PTUPD000  COMPARE   ZERO,PATCACAD
          GOTO      PTUPD999 IF EQUAL
.
          OPEN      PATCA1A1,"patca1af"
.
          MOVE      AADMNO,KEY8
          CALL      RDCADD1
          BRANCH    OVRCD,PTUPD500
.
          MOVE      PATCAAD1,PTCAADD1
          MOVE      PATCAAD2,PTCAADD2
          MOVE      PATCASUB,PTCASUB
          MOVE      PATCAPST,PTCAPCOD
          MOVE      PATCALGA,PTCALGA
          MOVE      PATCAHPH,PTCAHPH
          MOVE      PATCABPH,PTCABPH
.
          CALL      UPCADD1
          GOTO      PTUPD999
.
PTUPD500  MOVE      AADMNO,PTCAADMN
          MOVE      PATCAAD1,PTCAADD1
          MOVE      PATCAAD2,PTCAADD2
          MOVE      PATCASUB,PTCASUB
          MOVE      PATCAPST,PTCAPCOD
          MOVE      PATCALGA,PTCALGA
          MOVE      PATCAHPH,PTCAHPH
          MOVE      PATCABPH,PTCABPH
.
          MOVE      AADMNO,KEY8
          CALL      WRCADD1
.
PTUPD999  CLOSE     PATCA1A1
          MOVE      ZERO,PATCACAD
          RETURN    
+
