.------------------------------------------------------------
.  File          :  PATKLGPR
.
.  Function      :  Update Keyword Index File for LGA Codes 
.
.  Parameters    :  LGACODE
.
.  Modifications :  
.
.------------------------------------------------------------
          DEFPROC   PATKLGUP 
.
          INC       PATKLGFD/INC
.
KEYINDEX  DIM       9         * Code Index in Table
KEYWORDS  DIM       60        * String Containing the Key Words to be Indexed
SIXTY     FORM      "60"
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
.------------------------------------------------------------
. Generate Word Index
.------------------------------------------------------------
GENR0000  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     PATKLGA1,"patklgaf"
          TRAPCLR  IO
          BRANCH   OVRCD,GENR9999
.
          MOVE     LGACODE,KEYINDEX
          CALL     CLEAR000           * Remove Current Key Word Index
          BRANCH   EXIT,GENR9999
.
          MOVE     LGACODE,KEY4       * Validate Record on File
          CALL     RDLGAA1 
          BRANCH   OVRCD,GENR1900     * If Not then it Must be a Delete/Clear
.
.   Insert Code to Call BWORD000 function for each string to be indexed 
.   The string should be moved into the variable KEYWORDS

          MOVE     LGACODE,KEYINDEX
          MOVE     LGACODE,KEYWORDS
          CALL     BWORD000
.
          PACK     KEYWORDS,LGADESC,SP70
          CALL     BWORD000
.
GENR1900  CLOSE    PATKLGA1
.
GENR9999  GOTO     PATKLGEP
.------------------------------------------------------------
. Clear Current Key Words
.------------------------------------------------------------
CLEAR000  PACK     KEY19,KEYINDEX,SP70
          CALL     RSPTKLG1
          CALL     RKPTKLG1
          BRANCH   OVRCD,CLEAR999
          MATCH    KEYINDEX,PTLGCODE
          GOTO     CLEAR999 IF NOT EQUAL
.
          PACK     KEY19,PTLGCODE,PTLGKWRD,SP70
          CALL     DEPTKLG1
          GOTO     CLEAR000
.
CLEAR999  RETURN
.-------------------------------------------------
. Determine Words to Index
.-------------------------------------------------
BWORD000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      BWORD000 IF NOT EOS
            GOTO      BWORD999               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWORDS,SP30      * reset form pointer
          MOVE      KEY60,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      BWORD100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Save this Word
.
BWORD100  MOVE      KEYWORDS,KEY15
          PACK      KEY15,KEY15,SP70
          REP       UPPLOW,KEY15             * Always Upper Case
          PACK      KEY19,KEYINDEX,KEY15
          UNPACK    KEY19,PTLGCODE,PTLGKWRD
          CALL      RDPTKLG1
          IF        OVRCD=1
            CALL      WRPTKLG1
          ENDIF
          GOTO      BWORD190
.
.         Check for any more words
.
BWORD190  SETLPTR   KEYWORDS,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY60,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY60,KEYWORDS
          GOTO      BWORD000
.
BWORD999  RETURN
.
          INC       PATKLGIO/INC
          INC       IBAOVRIO/INC
.
PATKLGEP  ENDPROC                          * End of Procedure
.
