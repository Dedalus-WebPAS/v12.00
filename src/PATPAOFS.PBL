.******************************************************************************
.* System         : Patient and Outpatient and A&E                            *
.*                                                                            *
.* Include Name   : PATPAOFS.PBL                                              *
.*                                                                            *
.* Function       : To set up the values to allow alerts to be printed on     *
.*                   I.D. sheets.  This will allow up to 10 alerts to be      *
.*                   printed.  An alert can be printed if TCINDC3 = "P" for   *
.*                   the required alert CODE.                                 *
.*                                                                            *
.* Author         : Justin Coates      26/10/1993                             *
.*                                                                            *
.* Parameters     : FORM5         which alert number to print                 *
.*                                 1, 2, 3 = Alert  1 Code, Desc, HDP         *
.*                                 4, 5, 6 = Alert  2 Code, Desc, HDP         *
.*                                 7, 8, 9 = Alert  3 Code, Desc, HDP         *
.*                                10,11,12 = Alert  4 Code, Desc, HDP         *
.*                                13,14,15 = Alert  5 Code, Desc, HDP         *
.*                                16,17,18 = Alert  6 Code, Desc, HDP         *
.*                                19,20,21 = Alert  7 Code, Desc, HDP         *
.*                                22,23,24 = Alert  8 Code, Desc, HDP         *
.*                                25,26,27 = Alert  9 Code, Desc, HDP         *
.*                                28,29,30 = Alert 10 Code, Desc, HDP         *
.*                                                                            *
.*----------------------------------------------------------------------------*
.* Modifications  :                                                           *
.*----------------------------------------------------------------------------*
.* V11.03.01 08/09/2023  Thanh T.           TSK 0912156                       *
.*           Changed GCATP000 to select only active alert when ind 3 = 'A'    *
.*           of H1-H9 category.                                               *
.* V10.03.01 29/11/2011  Jill Parkinson CAR 249362                            *
.*           Changed alert key                                                *
.*  V9.05.01 24/03/2006 Peter Vela CAR 61299                                  *
.*           Increased field no size from DIM3 to DIM5                        *
.******************************************************************************
.
          DEFPROC   PATPAOFS
.
          INC       PATALRFD/INC
.
PRNTALR   FORM      2             * the alert number to print
.
. ------- determine if printing a code or description or hdp equiv -------
.
PAOFS000  OPEN      PATALRT1,"patalrtf"
          BRANCH    FORM5,PAOFS100,PAOFS400,PAOFS700:      * alert 1
                          PAOFS100,PAOFS400,PAOFS700:      * alert 2
                          PAOFS100,PAOFS400,PAOFS700:      * alert 3
                          PAOFS100,PAOFS400,PAOFS700:      * alert 4
                          PAOFS100,PAOFS400,PAOFS700:      * alert 5
                          PAOFS100,PAOFS400,PAOFS700:      * alert 6
                          PAOFS100,PAOFS400,PAOFS700:      * alert 7
                          PAOFS100,PAOFS400,PAOFS700:      * alert 8
                          PAOFS100,PAOFS400,PAOFS700:      * alert 9
                          PAOFS100,PAOFS400,PAOFS700       * alert 10
          GOTO      PAOFS999
.
. ------- print an alert code -------
.
PAOFS100  ASSIGN    (FORM5+2)/3,FORM2       * get the alert number
          CALL      GCATP000                * get the correct alert to print
          PACK      VARIABLE,ACODE,SP30,SP30,SP30
          GOTO      PAOFS999
.
. ------- print an alert description -------
.
PAOFS400  ASSIGN    (FORM5+1)/3,FORM2       * get the alert number
          CALL      GCATP000                * get the correct alert to print
          PACK      VARIABLE,TDESC,SP30,SP30,SP30
          GOTO      PAOFS999
.
. ------- print an alert hdp  -------
.
PAOFS700  ASSIGN    FORM5/3,FORM2           * get the alert number
          CALL      GCATP000                * get the correct alert to print
          PACK      VARIABLE,THCSCOD,SP30,SP30,SP30
          GOTO      PAOFS999
+
.*********************************************************************
.         loop over alerts to get the correct one to print
.         Para's  : FORM2         the alert number to print (1-10)
.*********************************************************************
GCATP000  MOVE      FORM2,PRNTALR           * the alert to print
          MOVE      ZERO,FORM2              * clear counter
          PACK      KEY16,PURNO,SP70
          CALL      RSPTALR1
.
GCATP100  CALL      RKPTALR1
          BRANCH    OVRCD,GCATP900          * patient has no alerts
          MATCH     PURNO,PTALURNO
          GOTO      GCATP900 IF NOT EQUAL   * patient has no alerts
.
          PACK      KEY5,PTALCATG,PTALCODE
          CALL      RDCODE1
          BRANCH    OVRCD,GCATP100          * invalid alert code
.
          MATCH     ANSP,TCINDC3
          GOTO      GCATP500 IF EQUAL       * Printable alert
.
          MATCH     ANSA,TCINDC3            * Check active alert
          GOTO      GCATP100 IF NOT EQUAL
.
. Check active alert
.
          MATCH     SP70,PTALEDAT
          GOTO      GCATP300 IF EQUAL       * no alert inactive date
.
          MATCH     PTALEDAT,CURRDATE
          GOTO      GCATP100 IF NOT LESS    * alert end date passed ?
.
GCATP300  MATCH     SP70,PTALDTIN
          GOTO      GCATP500 IF EQUAL       * no alert inactive date
.
          MATCH     PTALDTIN,CURRDATE
          GOTO      GCATP100 IF NOT LESS    * alert inactive date passed ?
.
GCATP500  ADD       ONE,FORM2               * add to number of printable alerts
          COMPARE   FORM2,PRNTALR
          GOTO      GCATP100 IF NOT EQUAL   * not the required printable alert
          GOTO      GCATP999                * have got the alert to print
.
GCATP900  MOVE      SP3,ACODE
          MOVE      SP20,TDESC
          MOVE      SP4,THCSCOD
.
GCATP999  RETURN
+
          INC       IBAOVRIO/INC
          INC       PATALRIO/INC
.
PAOFS999  ENDPROC
+
