.************************************************************************
.*  Procedure: DGCLIRAD  -  Send an O/P New Referral (A05)              *
.*                                                                      *
.*  Author   : Steve Armstrong  (26/02/2009)   CAR 177234               *
.*                                                                      *
.*  Requires :                                                          *
.*                                                                      *
.*  Mods     :                                                          *
.*                                                                      *
.************************************************************************
.*  V11.04.01 09/01/2024 Thanh T           TSK 0936263                  *
.*            Added OPEN/CLOSE PMSQPXA1 index                           *
.************************************************************************
.*  V10.06.01 12/03/2015  Steve Armstrong  CAR 314108                   *
.*            Removed definition of PTCGDATE as PATCGPFD is no longer   *
.*            in use.                                                   *
.************************************************************************
.*  V10.03.01 17/04/2013  Davin S          CAR 280425                   *
.*            Changed IBCNUCIS - value 1 or greater will write trigger  *
.************************************************************************
.*  V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                  *
.*            Added setting of H7CITRID & H7CIINCL                      *
.************************************************************************
          DEFPROC   DGCLIRAD
.
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
.         INC       OUTRF1FD/INC
          INC       OUTRQUFD/INC
          INC       PMSQPTFD/INC
.
CISMFLAG  FORM      1             * not used but required for compile
IBAMFLAG  FORM      1             * not used but required for compile
.
MESSAGID  DIM       20
.
DGCLI000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,THIRTY2;*126,OTCNA05R
.
          MATCH     "1",IBCNUCIS                 * using CIS interface ?
          GOTO      DGCLI999 IF LESS             * no - finished
.
.         Check if we are sending any messages.
.
          COMPARE   ONE,OTCNA05R                 * sending A05 message ?
          GOTO      DGCLI999 IF NOT EQUAL        * no - finished
.
.         Check if the sub-type code (Cat CQ) has the indicator set
.         to exclude HL7 messages
.
          MATCH     SP6,OTRLCTYP                 * clinic type blank ?
          GOTO      DGCLI999 IF EQUAL            * yes - finished
.
          MATCH     SP6,OTRLSITE                 * site blank ?
          GOTO      DGCLI999 IF EQUAL            * yes - finished
.
.         Open the clinic type file and get the sub-type (Cat CQ).
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OUTSITA1,"outsitaf"          * site file found ?
          TRAPCLR   IO
          BRANCH    OVRCD,DGCLI999               * no - finished
.
          MOVE      OTRLSITE,KEY6
          CALL      RDSITA1                      * site found ?
          BRANCH    OVRCD,DGCLI999               * no - finished
.
          MATCH     SP3,OSTFILE                  * blank O/P file prefix ?
          GOTO      DGCLI999 IF EQUAL            * yes - finished
.
          PACK      CFNAME,OSTFILE,FILCTYA1
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OUTCTYA1,CFNAME              * clinic type file found ?
          TRAPCLR   IO
          BRANCH    OVRCD,DGCLI999               * no - finished
.
          PACK      KEY12,OTRLSITE,OTRLCTYP
          CALL      RDCTYA1                      * clinic type found ?
          BRANCH    OVRCD,DGCLI999               * no - finished
.
          MATCH     SP3,OCTVACS                  * sub-type blank ?
          GOTO      DGCLI999 IF EQUAL            * yes - finished
.
          PACK      KEY5,ANSC,ANSQ,OCTVACS
          CALL      RDCODE1                      * code on file ?
          BRANCH    OVRCD,DGCLI999               * no - finished
.
          MATCH     ANSX,TCINDC1                 * exclude flag set ?
          GOTO      DGCLI999 IF EQUAL            * yes - finished
.
.         Open the relevant holding tables
.
          OPEN      HL7CISA1,"hl7cisaf"
          OPEN      PMSQPTA1,"pmsqptaf"
          OPEN      PMSQPXA1,"pmsqpxaf"
          OPEN      OUTRQUA1,"outrquaf"
.
          CALL      DGMESSID                     * get the unique message id
.
.         Write a record to pmsqptaf (holding patient details)
.
          MOVE      MESSAGID,PMQPMESI
          MOVE      SP8,PMQPOURN
          PACK      PMQPSPAR,SP30,SP30,SP20
          CALL      WRPMQPT1
.
.         Write a record to outrquaf (holding O/P referral details)
.
          MOVE      MESSAGID,OTRQMESG
          MOVE      SP20,OTRQSPAR
          CALL      WROTRQU1
.
          MOVE      "A05",H7CIMETP
          MOVE      HL7TRGID,H7CITRID
          MOVE      HL7INCLD,H7CIINCL
          CALL      WRCIS000                     * write hl7cisaf record
.
          CLOSE     HL7CISA1
          CLOSE     PMSQPTA1
          CLOSE     PMSQPXA1
          CLOSE     OUTRQUA1
.
DGCLI999  GOTO      DGCLIEND                     * end procedure
.
.         I/O's go in here
.
          INC       BRHLCOMN
          INC       CLPMSQPX
.
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       IBAOVRIO/INC
          INC       OUTRQUIO/INC
          INC       PMSQPTIO/INC
.
DGCLIEND  ENDPROC
