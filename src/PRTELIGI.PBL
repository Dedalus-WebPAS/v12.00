PRTELIGI
.
.        VERSION  :
.
.         V10.14.01 20/03/2019  J.Tan          TSK 0857392
.                   Changed RCP Transaction count from DIM3 to DIM5
.         V10.08.04 20/10/2016  J.Tan          TSK 0827903
.                   Mods DELG2000 - chk refund deposit for released receipt only
.         V10.08.03 05/10/2016  J.Tan          TSK 0826826
.                   Mods checking for refund deposit
.         V10.08.02 16/09/2016  J.Tan          TSK 0298257
.                   Mods to print Miscellaneous item
.         V10.08.01 13/09/2016  J.Tan          TSK 0825795
.                   Mods to print Sameday excess for patient admitted today
.                   and ASTAY=0
.         V10.07.03 09/03/2016  J.Tan          CAR 0319403
.                   Fixed printing Overnight/Daycase excess
.         V10.07.02 07/03/2016  J.Tan          CAR 0814361
.                   Changed var.name for bed days BPDAYS/BSDAYS/BCDAYS
.         V10.07.01 31/12/2015  Peter Vela     CAR 0807988
.                   Added excess validation in PELG0000
.         V10.06.00 27/05/2015  J.Tan          CAR 276452
.                   Create a common include to print Eligibility Check

.------------------------------------------------------------
.         Print Eligibility Check
.------------------------------------------------------------
PELG0000  MATCH     "1",PTCNGPHR
          IF        @EQUAL
            CALL      PRPH0000           * Printing total PHX,PHP
          ENDIF
.
          MATCH     ADATE,DDATE
          IF        @EQUAL
            MOVE      ZERO,CPAYPRIV
            MOVE      ZERO,CPAYSHRD
            MOVE      ZERO,BPDAYS        * Number of days in private
            MOVE      ZERO,BSDAYS        * Number of days in shared
            MOVE      ZERO,BCDAYS        * Number of days in CCU,ICU,SCN,HDU
          ELSE
            CALL      GBDY0000           * Check for invoiced accomm.bed days
          ENDIF
.
          MOVE      PTELPPUT,MAXCOPAY    * Max copayment $ amount
          MOVE      ZERO,SUMCOPAY        * Sum of copayment $ amount
.
.
.         Printing Co-payment for private
.
          MOVE      CPAYPRIV,FORM12P2
          IF        PTELPPUT<>0
            IF        CPAYPRIV>PTELPPUT
              MOVE      PTELPPUT,FORM12P2
            ENDIF
          ENDIF
          IF        MAXCOPAY<>0
            IF        SUMCOPAY>=MAXCOPAY
              MOVE      ZERO,FORM12P2                   * Max reached
            ELSE
              IF (SUMCOPAY+FORM12P2)>MAXCOPAY
                ASSIGN    MAXCOPAY-SUMCOPAY,FORM12P2
                ADD       FORM12P2,SUMCOPAY
              ELSE
                ADD       FORM12P2,SUMCOPAY
              ENDIF
            ENDIF
          ENDIF
.
          COMPARE   MAXLINE,COUNT                   * Check For A Full Page
          CALL      TAIL IF NOT LESS
.
          MOVE      PTELPPRI,FORM62
          IF        PTCNINVL=25
            PRINT     *19,"Co-payment required - Private":
                      *49,FORM62;
          ELSE
            PRINT     *23,"Co-payment required - Private":
                      *53,FORM62;
          ENDIF
.
          MOVE      PTELPPUT,FORM62
....      PRINT     *62,FORM62;
.
          MOVE      FORM12P2,FORM62
          IF        PTCNINVL=25
            MOVE      BPDAYS,F3
            PRINT     *60,F3,*72,FORM62
          ELSE
            PRINT     *71,FORM62
          ENDIF
          ADD       FORM12P2,TDBSAMNT
          ADD       ONE,COUNT
.
.         Printing Co-payment for shared
.
          MOVE      CPAYSHRD,FORM12P2
          IF        PTELPSUT<>0
            IF        CPAYSHRD>PTELPSUT
              MOVE      PTELPSUT,FORM12P2
            ENDIF
          ENDIF
          IF        MAXCOPAY<>0
            IF        SUMCOPAY>=MAXCOPAY
              MOVE      ZERO,FORM12P2                   * Max reached
            ELSE
              IF (SUMCOPAY+FORM12P2)>MAXCOPAY
                ASSIGN    MAXCOPAY-SUMCOPAY,FORM12P2
                ADD       FORM12P2,SUMCOPAY
              ELSE
                ADD       FORM12P2,SUMCOPAY
              ENDIF
            ENDIF
          ENDIF
.
          COMPARE   MAXLINE,COUNT                   * Check For A Full Page
          CALL      TAIL IF NOT LESS
.
          MOVE      PTELPSHA,FORM62
          IF        PTCNINVL=25
            PRINT     *19,"Co-payment required - Shared":
                      *49,FORM62;
          ELSE
            PRINT     *23,"Co-payment required - Shared":
                      *53,FORM62;
          ENDIF
.
          MOVE      PTELPSUT,FORM62
.....     PRINT     *62,FORM62;
.
          MOVE      FORM12P2,FORM62
          IF        PTCNINVL=25
            MOVE      BSDAYS,F3
            PRINT     *60,F3,*72,FORM62
          ELSE
            PRINT     *71,FORM62
          ENDIF
.
          ADD       FORM12P2,TDBSAMNT
          ADD       ONE,COUNT
.
.         Printing Co-payment for sameday
.
          MOVE      ZERO,FORM12P2
          MATCH     DDATE,ADATE
          IF        @EQUAL
            MOVE      PTELPSDY,FORM12P2
            IF        PTELPSDU<>0
              IF        PTELPSDY>PTELPSDU
                MOVE      PTELPSDU,FORM12P2
              ENDIF
            ENDIF
          ENDIF
          IF        MAXCOPAY<>0
            IF        SUMCOPAY>=MAXCOPAY
              MOVE      ZERO,FORM12P2                   * Max reached
            ELSE
              IF (SUMCOPAY+FORM12P2)>MAXCOPAY
                ASSIGN    MAXCOPAY-SUMCOPAY,FORM12P2
                ADD       FORM12P2,SUMCOPAY
              ELSE
                ADD       FORM12P2,SUMCOPAY
              ENDIF
            ENDIF
          ENDIF
.
          COMPARE   MAXLINE,COUNT                   * Check For A Full Page
          CALL      TAIL IF NOT LESS
.
          MOVE      PTELPSDY,FORM62
          IF        PTCNINVL=25
            PRINT     *19,"Co-payment required - Same day":
                      *49,FORM62;
          ELSE
            PRINT     *23,"Co-payment required - Same day":
                      *53,FORM62;
          ENDIF
.
          MOVE      PTELPSDU,FORM62
....      PRINT     *62,FORM62;
.
          MOVE      FORM12P2,FORM62
          IF        PTCNINVL=25
            MATCH     ADATE,DDATE
            IF        @EQUAL
              MOVE      ONE,F3
              PRINT     *60,F3,*72,FORM62
            ELSE
              PRINT     *72,FORM62
            ENDIF
          ELSE
            PRINT     *71,FORM62
          ENDIF
.
          ADD       FORM12P2,TDBSAMNT
          ADD       ONE,COUNT
.
.         Printing Co-payment for CCU,ICU,SCN,HDU
.
          MOVE      CPAYCCUH,FORM12P2
          IF        PTELPCUT<>0
            IF        CPAYCCUH>PTELPCUT
              MOVE      PTELPCUT,FORM12P2
            ENDIF
          ENDIF
.
          COMPARE   MAXLINE,COUNT                   * Check For A Full Page
          CALL      TAIL IF NOT LESS
.
          MOVE      PTELPCCU,FORM62
          IF        PTCNINVL=25
            PRINT     *19,"Co-payment req-CCU,ICU,SCN,HDU":
                      *49,FORM62;
          ELSE
            PRINT     *23,"Co-payment req-CCU,ICU,SCN,HDU":
                      *53,FORM62;
          ENDIF
.
          MOVE      FORM12P2,FORM62
          IF        PTCNINVL=25
            MOVE      BCDAYS,F3
            PRINT     *60,F3,*72,FORM62
          ELSE
            PRINT     *71,FORM62
          ENDIF
.
          ADD       FORM12P2,TDBSAMNT
          ADD       ONE,COUNT
.
.         Printing Co-payment for hospital
.
          COMPARE   MAXLINE,COUNT                   * Check For A Full Page
          CALL      TAIL IF NOT LESS
.
          MOVE      PTELPHSP,FORM62
          IF        PTCNINVL=25
            PRINT     *19,"Co-payment required - Hospital":
                      *49,FORM62;
          ELSE
            PRINT     *23,"Co-payment required - Hospital":
                      *53,FORM62;
          ENDIF
.
.         MOVE      PTELPHSU,FORM62
.         IF        PTCNINVL=25
.           PRINT     *63,FORM62;
.         ELSE
.           PRINT     *62,FORM62;
.         ENDIF
.
          MOVE      PTELPHSP,FORM12P2
.         MOVE      ASTAY,F3              * default to Expected LOS
          IF        DYCHRGED=0
            MOVE      ONE,F3              * treat LOS as 1
          ELSE
            MOVE      DYCHRGED,F3         * use the actual LOS
          ENDIF
          MULT      F3,FORM12P2
          IF        PTELPHSU<>0
            IF        FORM12P2>PTELPHSU
              MOVE      PTELPHSU,FORM12P2
            ENDIF
          ENDIF
          MOVE      FORM12P2,FORM62
          IF        PTCNINVL=25
            PRINT     *60,F3,*72,FORM62
          ELSE
            PRINT     *71,FORM62
          ENDIF
          ADD       FORM12P2,TDBSAMNT
          ADD       ONE,COUNT
.
.         Printing Co-payment for theatre
.
          COMPARE   MAXLINE,COUNT                   * Check For A Full Page
          CALL      TAIL IF NOT LESS
.
          MOVE      PTELPTHE,FORM62
          IF        PTCNINVL=25
            PRINT     *19,"Co-payment required - Theatre":
                      *49,FORM62;
          ELSE
            PRINT     *23,"Co-payment required - Theatre":
                      *53,FORM62;
          ENDIF
.
          MOVE      PTELPTUT,FORM62
....      PRINT     *62,FORM62;
.
          MOVE      PTELPTHE,FORM62
          IF        PTCNINVL=25
            PRINT     *72,FORM62
          ELSE
            PRINT     *71,FORM62
          ENDIF
          ADD       PTELPTHE,TDBSAMNT
          ADD       ONE,COUNT
.
.         Printing Sundry Misc. if required
.
          MATCH     "1",PTCNEMDB
          GOTO      PELG0800 IF NOT EQUAL
.
          PACK      PTELMDES,PTELMDES,SP70
          MATCH     SP70,PTELMDES
          GOTO      PELG0800 IF EQUAL
.
          MATCH     "1",EXCLMISC
          GOTO      PELG0800 IF EQUAL               * Exclude Misc.
.
          COMPARE   MAXLINE,COUNT                   * Check For A Full Page
          CALL      TAIL IF NOT LESS
.
          UNPACK    PTELMDES,KEY30
          MOVE      PTELMAMT,FORM62
          IF        PTCNINVL=25
            PRINT     *19,KEY30:
                      *49,FORM62:
                      *72,FORM62
          ELSE
            PRINT     *23,KEY30:
                      *53,FORM62:
                      *71,FORM62
          ENDIF
          ADD       PTELMAMT,TDBSAMNT
          ADD       ONE,COUNT
.
.         Printing Excess
.
PELG0800  COMPARE   MAXLINE,COUNT                   * Check For A Full Page
          CALL      TAIL IF NOT LESS
.
          IF        ASTAT=3
            MATCH     ADATE,DDATE
            GOTO      PELG0900 IF EQUAL
          ENDIF
.
.         If patient is admitted today and Expected LOS is zero, print
.         Excess for Sameday
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          MATCH     CURRDATE,ADATE
          IF        @EQUAL
            COMPARE   ZERO,ASTAY
            GOTO      PELG0900 IF EQUAL
          ENDIF
.
          MOVE      PTELXCSS,FORM62
          IF        PTCNINVL=25
            PRINT     *19,"Excess - Overnight":
                      *49,FORM62:
                      *72,FORM62
          ELSE
            PRINT     *23,"Excess - Overnight":
                      *53,FORM62:
                      *71,FORM62
          ENDIF
          ADD       PTELXCSS,TDBSAMNT
          GOTO      PELG1000
.
PELG0900  MOVE      PTELXCSD,FORM62
          IF        PTCNINVL=25
            PRINT     *19,"Excess - Same Day":
                      *49,FORM62:
                      *72,FORM62
          ELSE
            PRINT     *23,"Excess - Same Day":
                      *53,FORM62:
                      *71,FORM62
          ENDIF
          ADD       PTELXCSD,TDBSAMNT
.
PELG1000  ADD       ONE,COUNT
.
.         Printing Out of Pocket Expenses
.
          COMPARE   MAXLINE,COUNT                   * Check For A Full Page
          CALL      TAIL IF NOT LESS
.
          MOVE      TDBSAMNT,FORM62
          IF        PTCNINVL=25
            PRINT     *19,"Out of Pocket Expenses Total":
                      *72,FORM62
          ELSE
            PRINT     *23,"Out of Pocket Expenses Total":
                      *71,FORM62
          ENDIF
          ADD       ONE,COUNT
.
.         Check for any payments
.
          COMPARE   MAXLINE,COUNT                   * Check For A Full Page
          CALL      TAIL IF NOT LESS
.
          PACK      KEY25,AADMNO,SP70
          CALL      RSRCDTE6
PELG2000  CALL      RKRCDTE6
          BRANCH    OVRCD,PELG9999
.
          MATCH     DAADMNO,RCDTVIST
          GOTO      PELG9999 IF NOT EQUAL
.
          MATCH     SP70,RCDTINVN          * Invoice number must be blank
          GOTO      PELG2000 IF NOT EQUAL
.
          PACK      KEY12,RCDTRECN
          CALL      RDRCBNK1
          BRANCH    OVRCD,PELG2000
          MATCH     "1",RCBNSTAT            * check if cancelled
          GOTO      PELG2000 IF EQUAL
.
          PACK      KEY17,RCDTRECN,RCDTTCNT
          CALL      RDCMDEP1
          IF        OVRCD=0
            MATCH     SP70,CMDERRFN
            GOTO      PELG2000 IF NOT EQUAL   * Refunded
          ENDIF
.
          MOVE      "Deposit ",TDESC
          MATCH     SP70,RCDTDPTY
          IF        !@EQUAL
            PACK      KEY5,ANSD,ANSP,RCDTDPTY,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              CLEAR     TDESC
              APPEND    "Invalid Deposit Type",TDESC
              APPEND    RCDTDPTY,TDESC
              APPEND    SP70,TDESC
              RESET     TDESC
            ENDIF
          ENDIF
          CLEAR     KEY45
          APPEND    TDESC,KEY45
          APPEND    " RECEIPT ## ",KEY45
          APPEND    RCDTRECN,KEY45
          APPEND    SP70,KEY45
          RESET     KEY45
.
          MULT      "-1",RCDTAMNT
          MOVE      RCDTAMNT,FORM62
          ADD       FORM62,TDBSAMNT
.
          IF        PTCNINVL=25
            PRINT     *19,KEY45:
                      *72,FORM62
          ELSE
            PRINT     *23,KEY45:
                      *71,FORM62
          ENDIF
          ADD       ONE,COUNT
          GOTO      PELG2000
.
PELG9999  RETURN
+
.------------------------------------------------------------------------------
.   Calculate invoiced accommodation bed days for Private and Shared Co-payment
.------------------------------------------------------------------------------
GBDY0000  MOVE      "1",DIM1             * Accommodation record
          PACK      KEY18,ADMISSNO,DIM1,SP70
          CALL      RDSDTRN3
GBDY1000  CALL      RDKDTRN3
          BRANCH    OVRCD,GBDY9999
          MATCH     ADMISSNO,DTADMNO
          GOTO      GBDY9999 IF NOT EQUAL
          MATCH     DTRECTYP,DIM1
          GOTO      GBDY9999 IF NOT EQUAL * Not accommodation record
.
          MATCH     "1",PTDTCRST
          GOTO      GBDY1000 IF EQUAL     * Credit note
.
          COMPARE   ZERO,TNODAYS
          GOTO      GBDY1000 IF EQUAL
.
          MOVE      PTDTBTYP,STRBTYP
          MOVE      TNODAYS,NODAYS
          CALL      SCPAY0000             * from DBSCATBI (setup co-payment)
          GOTO      GBDY1000
GBDY9999  RETURN
+

.------------------------------------------------------------
.         Printing Total of PHX and PHP Pharmacy items
.------------------------------------------------------------
PRPH0000  IF        PHXIAMNT<>0
            MOVE      MAXLINE,FORM2
            SUB       ONE,FORM2
            COMPARE   FORM2,COUNT                   * Check For A Full Page
            CALL      TAIL IF NOT LESS
            MOVE      PHXIAMNT,FORM62
            PRINT     *N,*23,"PHX Total";
            IF        PTCNINVL=25
              PRINT     *72,FORM62
            ELSE
              PRINT     *71,FORM62
            ENDIF
            ADD       TWO,COUNT
          ENDIF
          IF        PHPIAMNT<>0
            MOVE      MAXLINE,FORM2
            SUB       ONE,FORM2
            COMPARE   FORM2,COUNT                   * Check For A Full Page
            CALL      TAIL IF NOT LESS
            MOVE      PHPIAMNT,FORM62
            PRINT     *23,"PHP Total";
            IF        PTCNINVL=25
              PRINT     *72,FORM62,*N
            ELSE
              PRINT     *71,FORM62,*N
            ENDIF
            ADD       TWO,COUNT
          ENDIF
PRPH9999  RETURN
+
