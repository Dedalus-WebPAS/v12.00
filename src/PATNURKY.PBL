.******************************************************************************
.*  Include Name  : PATNURKY.PBL                                              *
.*  Description   : Keyin of nursing station codes                            *
.*  Author        : Ian Rutt                                                  *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF3 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYINODE - is keyin mode                                  *
.*                                                                            *
.*  Returns       : NSTAT    - Keyed in code                                  *
.*                  NSDESC    - Description                                   *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.******************************************************************************
PATNURKY
PTNRKY00  MATCH     SP3,CKYIDEF3                  * using a default ?
          GOTO      PTNRKY10 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN2:      * no default used
                    *PECOL:EVERT,*V2LON,NSTAT:
                    *PECOL:EVERT,*DV,NSTAT
          GOTO      PTNRKY20
.
PTNRKY10  MOVE      CKYIDEF3,NSTAT                 * default
          KEYIN     *PECOL:EVERT,*DV,NSTAT:     * default used
                    *PECOL:EVERT,*V2LON,*RV,NSTAT:
                    *PECOL:EVERT,*DV,NSTAT
.
.         check what was entered
.
PTNRKY20  CALL      NURCK000
          BRANCH    EXIT,PTNRKY25,PTNRKY31,PTNRKY41,PTNRKY95
.                        "?"      valid    nothing  exitchar
          GOTO      PTNRKY00
.
.         a "?" was input
.
PTNRKY25  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
PTNRKY26  CALL      PATNURDS
.
.         now ask for code while leaving displayed codes on screen
.
PTNRKY27  MATCH     SP3,CKYIDEF3                  * using a default
          GOTO      PTNRKY28 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Nursing Station Code : ":
                    *P24:24,*DV,UNDLN2:      * no default used
                    *P24:24,*V2LON,NSTAT:
                    *P24:24,*DV,NSTAT
          GOTO      PTNRKY29
.
PTNRKY28  MOVE      CKYIDEF3,NSTAT
          KEYIN     *P1:24,*EL,*DV,"Nursing Station Code : ":
                    *P24:24,*DV,NSTAT:     * default used
                    *P24:24,*V2LON,*RV,NSTAT:
                    *P24:24,*DV,NSTAT
.
.         repeat the validation of the code
.         check what was entered
.
PTNRKY29  CALL      NURCK000
          BRANCH    EXIT,PTNRKY26,PTNRKY30,PTNRKY40,PTNRKY90
.                        "?"      valid    nothing  exitchar
.
          GOTO      PTNRKY27
.
.         a valid code was entered 
. 
PTNRKY30  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,NSTAT
.
PTNRKY31  MOVE      FALSE,EXIT                    * valid input
          GOTO      PTNRKY99 
.
.         nothing was input
.
PTNRKY40  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,NSTAT
.
PTNRKY41  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      PTNRKY99 
.
.         EXITCHAR input
.
PTNRKY90  CALL      FRESCR00                * free the screen saved
.
PTNRKY95  MOVE      TWO,EXIT                * EXITCHAR
.
PTNRKY99  RETURN
+
.*********************************************************************
.*                  NURCK000                    Called by : PATCODKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
NURCK000  ENDSET    NSTAT
          APPEND    SP3,NSTAT
          RESET     NSTAT
.
          MATCH     EXITCHAR,NSTAT
          GOTO      NURCK600 IF EQUAL       * exit char
.
          MATCH     SP3,NSTAT
          GOTO      NURCK700 IF EQUAL       * nothing
.
          MATCH     QUEST,NSTAT
          GOTO      NURCK800 IF EQUAL       * ? mark
.
.         a code was entered so validate
. 
NURCK500  PACK      KEY6,NSTAT,SP6
          CALL      RDNURS1
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,NURCK900          * invalid code
          ELSE
            COMPARE   ZERO,OVRCD   
            GOTO      NURCK900 IF EQUAL
          ENDIF  
.
          MOVE      TWO,EXIT                * valid code
          GOTO      NURCK999
.
.         exitchar entered
.
NURCK600  MOVE      FOUR,EXIT
          MOVE      SP3,NSTAT
          MOVE      SP20,NSDESC              * clear desc field
          GOTO      NURCK999
.
.         nothing entered
.
NURCK700  BRANCH    CKYIMAND,NURCK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP3,NSTAT
          MOVE      SP20,NSDESC              * clear desc field
          GOTO      NURCK999
.
.         "?" entered
.
NURCK800  MOVE      ONE,EXIT
          GOTO      NURCK999
.
.         invalid code entered
.
NURCK900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
.
          IF        CKYIMODE = 0
            DISPLAY   *P1:24,*EL,*B,"Nursing Stations Invalid Code. ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      NURCK999
.
.         field is mandatory
.
NURCK950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is Mandatory. ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
NURCK999  RETURN
.
          INCLUDE   PATNURDS
+
