. **************************************************************
. *              I N C L U D E  S U M M A R Y                  *
. *              -------------  -------------                  *
. *                                                            *
. *     INCLUDE NAME:     RHIHDAYS                             *
. *                                                            *
. *     FUNCTION:         CALCULATE HOSPITAL IN THE HOME DAYS  *
. *                                                            *
. *     V12.00.01 23/05/2025  Ebon Clements   TSK 0955096      *
. *               Alphanueric visit number changes             *
. *     V9.03.02    10/10/2003 Henry Son CAR 44242             *
. *                       Fix Calc. of Hosptital home days.    *
. *     V9.03.01    12/08/2003 Henry Son                       *
. *                       Copy of NHOSPDAY but only add up     *
. *                       Hospital in the Home days.           *
. *                                                            *
. **************************************************************
.
.                READ TRANSFER FILE
.
RHIHDAYS
HIHD0000  MOVE      SP8,HIHSTDTE
          MOVE      ZERO,HIHLOS
          MOVE      ZERO,CALDAYS
.
          COMPARE   THREE,ASTAT
          GOTO      HIHD0100 IF NOT EQUAL
.
          MATCH     ADATE,DDATE
          GOTO      HIHD0100 IF NOT EQUAL
.
          MATCH     FROMDATE,ADATE
          RETURN    IF LESS
.
          MATCH     ADATE,TODATE
          RETURN    IF LESS
.
          MOVE      ZERO,HIHLOS        * Sameday patient HIHLOS =0 days
          RETURN
.
HIHD0100  PACK      KEY30 WITH AADMNO,SP20
          CALL      RDSTRAN2
HIHD0200  CALL      RDKTRAN2
          BRANCH    OVRCD OF HIHD0800
.
          MATCH     TADMN,AADMNO
          GOTO      HIHD0800 IF NOT EQUAL
.
          MATCH     FROMDATE,TDATE     * Transfer before fromdate?
          GOTO      HIHD0200 IF LESS
          MATCH     TDATE,TODATE       * Transfer after todate?
          GOTO      HIHD0800 IF LESS
.
          MATCH     "A",TMOVE                                          *I-44242
          GOTO      HIHD0300 IF EQUAL                                  *I-44242
.
          MATCH     "C",TMOVE                                          *I-44242
          GOTO      HIHD0300 IF EQUAL                                  *I-44242
.
          MATCH     "R",TMOVE                                          *I-44242
          GOTO      HIHD0300 IF EQUAL                                  *I-44242
.
......    L- leave and D-discharge fall through                        *I-44242
.
          MATCH     SP70,HIHSTDTE                                      *I-44242
          IF        !@EQUAL
            DAYSEP    HIHSTDTE,TDATE,CALDAYS                           *I-44242
            ADD       CALDAYS,HIHLOS     * increment hihLOS days       *I-44242
            MOVE      SP8,HIHSTDTE       * reset from hih date         *I-44242
          ENDIF                                                        *I-44242
          GOTO      HIHD0200                                           *I-44242
.
HIHD0300  CALL      BDTP0000         * Get Bed Type                    *C-44242
          IF        HIHIND=1
            MATCH   SP70,HIHSTDTE    * have we got a hospital home xfer?
            IF      @EQUAL
              MOVE     TDATE,HIHSTDTE
            ENDIF
          ELSE
            MATCH    SP70,HIHSTDTE
            IF       !@EQUAL
              DAYSEP   HIHSTDTE,TDATE,CALDAYS
              ADD      CALDAYS,HIHLOS     * increment hihLOS days
              MOVE     SP8,HIHSTDTE       * reset from hih date
            ENDIF
          ENDIF
.
          GOTO      HIHD0200
.
HIHD0800  MATCH    SP70,HIHSTDTE        * hih Date no "return" date
          IF       !@EQUAL
            DAYSEP   HIHSTDTE,TODATE,CALDAYS
            ADD      CALDAYS,HIHLOS     * increment hihLOS days
            MOVE     SP8,HIHSTDTE       * reset from hih date
          ENDIF
.
HIHD9999  RETURN                    
+
.**************************************************************************
.*  Determine the Bed Type of the transfer.                               *
.**************************************************************************
BDTP0000  MOVE      ZERO,HIHIND
          PACK      KEY5,ANSB,ANST,TRBTYP
          CALL      RDCODE1                 * Read a codes file record
          BRANCH    OVRCD,BDTP9999
.
          MATCH     ANSH,TCINDC1            * is it Hosptial in the home.
          IF        @EQUAL
            MOVE      ONE,HIHIND
          ELSE
            MOVE      ZERO,HIHIND
          ENDIF
.
BDTP9999  RETURN
