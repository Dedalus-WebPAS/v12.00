.******************************************************************************
.*                                                                            *
.*  PRIINVTL - PRI Invoice Tail Fields                                        *
.*                                                                            *
.* &&&&& Make sure the file ibavaraf is updated with the new variables &&&&&  *
.*                                                                            *
.* Modifications  :                                                           *
.*       V11.04.01  09/07/2024  J.Tan            TSK 0934944                  *
.*                  Mod to check Referral Provider no from a linked Practice  *
.*       V11.03.01  02/10/2023  Nikitha B        TSK 0927699                  *
.*                  Added CAPPRVNO and PTCNHABN for Hospital Approval Number  *
.*                  and  ABN Number for single and multi hospital.            *
.*       V11.02.01  24/01/2022  Jacob Jackson    TSK 0864505                  *
.*                  Added preferred name option fields -                      *
.*                  PRPI1180, PRPI1190, PRPI1200, PRPI1210, PRPI1220          *
.*       V11.01.01  23/08/2021  Davin            TSK 0897318                  *
.*                  Added field 51 - Adjustment Total (PRPIT910)              *
.*----------------------------------------------------------------------------*
.*       V11.00.01  06/04/2020  Thanh T          TSK 0879163                  *
.*                  Added PRPIT900 for sex long description                   *
.*----------------------------------------------------------------------------*
.*       V10.13.01  31/07/2018  J.Tan            TSK 0848506                  *
.*                  Changed PP Doctor from DIM6 to DIM10                      *
.*       V10.12.01  26/04/2018  Peter Vela     TSK 0852811                    *
.*                  Added Referring HCP Linked Practice Provider Number       *
.*       V10.10.01  10/05/2017  Tracey Nguyen  TSK 0837929                    *
.*                  Fixed I*C error message on PATMESG1 at PRPIT810 & PRPIT820*
.*       V10.04.01  24/01/2013  Patrick Adair  CAR 265844                     *
.*                  Add new fields for PRA State and Full Address             *
.*       V10.03.02  18/07/2012  Don Nguyen     CAR 264561                     *
.*                  Cleared BPAYHOSP before calling GBCRN000                  *
.*       V10.03.01  29/11/2011  J.Tan          CAR 254776                     *
.*                  Added variables for PRFA and Debtor full address          *
.*       V10.02.01  04/04/2011  Jill Parkinson   CAR 239574                   *
.*                  Removed open of ibaprntf                                  *
.*       V10.00.03  14/09/2010  Davin            CAR 229590                   *
.*                  Use prcninvn to read priinvof not the cgi invoicen        *
.*       V10.00.02  26/08/2010  Davin            CAR 226963                   *
.*                  Added referral date from prihreff (variable 46)           *
.*       V10.00.01  24/08/2010  Peter Vela       CAR 221500                   *
.*                  Added read to priinvof before read to prihreaf            *
.*        V9.12.01  03/09/2009  Peter Vela       CAR 194715                   *
.*                  Added variables from prihreaf                             *
.*        V9.11.01  19/03/2009  Davin S          CAR 178015                   *
.*                  Added field 33 for BPAY Customer Reference No. (Generated)*
.******************************************************************************
.*        V9.10.01  01/12/2008  Steve Armstrong  CAR 184702                   *
.*                  Added Service Doctor ABN and formatted name (variables    *
.*                  31 & 32 respectively)                                     *
.******************************************************************************
.*        V9.05.02  17/03/2006  Peter Vela    CAR 95780                       *
.*                  Added EMG mobile contact numbers                          *
.*        V9.05.01  27/03/2006  Peter Vela    CAR 61299                       *
.*                  Increased field no size from DIM3 to DIM5                 *
.******************************************************************************
.*        V9.03.05  06/07/2004  J.Tan         CAR 51112                       *
.*                  Mods outstanding amount to include non-banked deposit     *
.*        V9.03.04  25/05/2004  J.Tan         CAR 50111                       *
.*                  Fixed not checking for VSCANPMI                           *
.*        V9.03.03  29/03/2004  J.Tan   CAR 44116                             *
.*                  Added credit note amount to the outstanding amount        *
.*        V9.03.02  02/03/2004  Lina Vo       CAR 47921                       *
.*                  Removed use of PRIDBTFD & IO.                             *
.*        V9.03.01  26/02/2004  J.Tan                                         *
.*                  Mods not to Close patmesgf                                *
.******************************************************************************
.*        V9.02.04  21/10/2002  J.Tan                                         *
.*                  Fixed outstanding balance                                 *
.*        V9.02.03  28/08/2002  J.Tan                                         *
.*                  Fixed outstanding balance not include GST as invoice total*
.*                  already includes GST                                      *
.*        V9.02.02  02/07/2002  Dean Cramer                                   *
.*                  Add message lines 1 & 2                                   *
.*        V9.02.01  14/12/2001  Dean Cramer                                   *
.*                  Mods for web                                              *
.******************************************************************************
.*        V5.08.01  14/08/2000  Davin  SRF 4106                               *
.*                  Added multipage variable for numeric fields               *
.*        V5.08.00  07/06/2000  Davin                                         *
.*                  Ported from 607                                           * .****************************************************************************** .
.******************************************************************************
.*        Lineup Print for Invoice Tail Fields                                *
.******************************************************************************
.
LINUPPIT  MATCH     "IBARSH",PGM
          GOTO      LINUPPT1 IF EQUAL
.
          IF        WEBFLAG=0
            CALL      OPNPRINT                 * open spool file
          ENDIF
LINUPPT1  MOVE      ONE,CURRROW              * Initialise current row counter
          IF        IBDTROW < 1
            GOTO      PRILT999               * don't template anything
          ENDIF
.
PRILT000  COMPARE   CURRROW,IBDTROW          * Current row up to required row ?
          GOTO      PRILT200 IF EQUAL        * Yes - print data
          PRINT     *N;                      * Else print 1 extra line
          ADD       ONE,CURRROW              * Increment counter
          GOTO      PRILT000                 * Check counter again
.
. ------- Read through the details file -------
.
PRILT100  CALL      RKIBDET1                 * Read next detail record
          BRANCH    OVRCD,PRILT900           * No more records - pad out form
.
          MATCH     PRCNPRIT,IBDTSCOD        * Same form ?
          GOTO      PRILT900 IF NOT EQUAL    * No - pad out rest of form
.
PRILT110  COMPARE   CURRROW,IBDTROW          * Are we at required row ?
          GOTO      PRILT200 IF EQUAL        * Yes - print data
          PRINT     *N;                      * Else pad out form to req row
          ADD       ONE,CURRROW              * One more row printed
          GOTO      PRILT110                 * Check row count
.
. ------- Print actual data lines -------
.
PRILT200  BRANCH    IBDTINDC,PRILT300:       * Text field
                             PRILT400        * Data field
.
. ------- Print text field -------
.
PRILT300  PRINT     *IBDTCOL,IBDTTXFL;       * Print data
          MOVE      IBDTROW,CURRROW          * Row printed is current row
          GOTO      PRILT100                 * Read next record
.
. ------- Print data field -------
.
PRILT400  MOVE      IBDTTXFL,DIM5            * Data number 1st 3 chars of field
          MOVE      DIM5,FORM5               * Move to a FORM field for BRANCH
.
          BRANCH    FORM5,PRILT450,PRILT450,PRILT450,PRILT470,PRILT450:
                          PRILT450,PRILT450,PRILT450,PRILT450,PRILT480:    10
                          PRILT480,PRILT450,PRILT450,PRILT450,PRILT480:
                          PRILT480,PRILT450,PRILT450,PRILT450,PRILT450:    20
                          PRILT450,PRILT450,PRILT480,PRILT450,PRILT490:
                          PRILT550,PRILT550,PRILT450,PRILT450,PRILT450:    30
                          PRILT450,PRILT450,PRILT450,PRILT450,PRILT450:
                          PRILT550,PRILT550,PRILT450,PRILT450,PRILT450:    40
                          PRILT450,PRILT450,PRILT450,PRILT450,PRILT450
.
PRILT450  PACK      VARIABLE,X10,X10,X10,X10,X10,X10,X10,X10,X10,X10,X10,X10,X10,X10,X10
          GOTO      PRILT600
.
PRILT460  MOVE      "99:99:99",VARIABLE          * Time variables
          GOTO      PRILT600
.
PRILT470  IF        IBDTPLEN = 10
            MOVE      "99/99/9999",VARIABLE      * Date with century variables
          ELSE
            MOVE      "99/99/99",VARIABLE        * Date variables
          ENDIF
          GOTO      PRILT600
.
PRILT480  MOVE      "9999999999999999999999999",VARIABLE    * Form Field
          GOTO      PRILT600
.
PRILT490  MOVE      "9",VARIABLE                 * Indicator
          GOTO      PRILT600
.
PRILT520  CLEAR     VARIABLE                     * Address
          APPEND    X10,VARIABLE
          APPEND    X10,VARIABLE
          APPEND    X5,VARIABLE
          APPEND    COMMA,VARIABLE
          APPEND    X10,VARIABLE
          APPEND    X10,VARIABLE
          APPEND    X5,VARIABLE
          APPEND    COMMA,VARIABLE
          APPEND    " 9999",VARIABLE
          RESET     VARIABLE
          GOTO      PRILT600
.
PRILT530  MOVE      "99",VARIABLE            * Year
          GOTO      PRILT600
.
PRILT540  MOVE      "999999.99",VARIABLE     * Daily rate
          GOTO      PRILT600
.
PRILT550  PACK      VARIABLE,X10,X10,X10,X10,X10,X10,X10
          GOTO      PRILT600
.
.
. ------- Actual data print routine -------
.
PRILT600  RESET     VARIABLE,IBDTPLEN        * Reset pointer to required length
          LENSET    VARIABLE                 * Push logical length there
          RESET     VARIABLE                 * Reset form pointer to start
          PRINT     *IBDTCOL,*+,VARIABLE;    * Only print data between FP & LL
          MOVE      IBDTROW,CURRROW          * Move row number to one printed
          GOTO      PRILT100                 * Read next record
.
. ------- Finished all data for this form - pad out rest of form -------
.
PRILT900  COMPARE   CURRROW,IBTHLENG         * End of form ?
          GOTO      PRILT999 IF EQUAL        * Yes - exit
          PRINT     *N;                      * Otherwise pad out 1 more line
          ADD       ONE,CURRROW              * 1 more line printed
          GOTO      PRILT900                 * Test again
.
PRILT999  MOVE      FALSE,EXIT               * Set exit flag
          MATCH     "IBARSH",PGM
          RETURN    IF EQUAL
          IF        WEBFLAG=0
            CALL      CLSPRINT                 * close the spool file
          ENDIF
          RETURN                             * Finished
+
.*******************************************************************************
.*        Actual Print Routine for PRI Invoice Tail Fields                     *
.*******************************************************************************
.
PRTPRITL  MATCH     "IBARSH",PGM
          GOTO      PRTPRIT1 IF EQUAL
.
          IF        WEBFLAG=0
            CALL      OPNPRINT                 * open spool file
          ENDIF
PRTPRIT1  MOVE      ONE,CURRROW              * Initialise current row counter
          IF        IBDTROW < 1
            GOTO      PRPT9999               * don't template anything
          ENDIF
.
PRPIT000  COMPARE   CURRROW,IBDTROW          * Current row up to required row ?
          GOTO      PRPIT200 IF EQUAL        * Yes - print data
          PRINT     *N;                      * Else print 1 extra line
          ADD       ONE,CURRROW              * Increment counter
          GOTO      PRPIT000                 * Check counter again
.
. ------- Read through the details file -------
.
PRPIT100  CALL      RKIBDET1                 * Read next detail record
          BRANCH    OVRCD,PRPT8000           * No more records - pad out form
.
          MATCH     PRCNPRIT,IBDTSCOD        * Same form ?
          GOTO      PRPT8000 IF NOT EQUAL    * No - pad out rest of form
.
PRPIT110  COMPARE   CURRROW,IBDTROW          * Are we at the required row ?
          GOTO      PRPIT200 IF EQUAL        * Yes - print data
          PRINT     *N;                      * Else pad out to required row
          ADD       ONE,CURRROW              * One more row printed
          GOTO      PRPIT110                 * Check row count
.
. ------- Print actual data lines -------
.
PRPIT200  BRANCH    IBDTINDC,PRPIT300:       * Text field
                             PRPIT400        * Data field
.
. ------- Print text field -------
.
PRPIT300  RESET     IBDTTXFL,IBDTPLEN
          LENSET    IBDTTXFL
          RESET     IBDTTXFL
          PRINT     *IBDTCOL,*+,IBDTTXFL;    * Print data
          MOVE      IBDTROW,CURRROW          * Row printed is current row
          GOTO      PRPIT100                 * Read next record
.
. ------- Print data field -------
.
PRPIT400  MOVE      IBDTTXFL,DIM5            * Data number 1st 3 chars of field
          MOVE      DIM5,FORM5               * Move to a FORM field for BRANCH
.
          PACK      VARIABLE,SP30,SP30,SP30,SP30,SP10  * Initialise variable
          BRANCH    FORM5,PRPIT410,PRPIT420,PRPIT430,PRPIT440,PRPIT450:
                          PRPIT460,PRPIT470,PRPIT480,PRPIT490,PRPIT500:   10
                          PRPIT510,PRPIT520,PRPIT530,PRPIT540,PRPIT550:
                          PRPIT560,PRPIT570,PRPIT580,PRPIT590,PRPIT600:   20
                          PRPIT610,PRPIT620,PRPIT630,PRPIT640,PRPIT650:
                          PRPIT660,PRPIT670,PRPIT680,PRPIT690,PRPIT700:   30
                          PRPIT710,PRPIT720,PRPIT730,PRPIT740,PRPIT750:
                          PRPIT760,PRPIT770,PRPIT780,PRPIT790,PRPIT800:   40
                          PRPIT810,PRPIT820,PRPIT830,PRPIT840,PRPIT850:
                          PRPIT860,PRPIT870,PRPIT880,PRPIT890,PRPIT900:   50
                          PRPIT910,PRPIT920,PRPIT930,PRPIT940,PRPIT950:   
                          PRPIT960,PRPIT970,PRPIT980,PRPIT981,PRPIT982
.
PRPIT410  MOVE      SP70,VARIABLE
          IF        MPGEFLAG <> 1
            MOVE      PMLINE1,VARIABLE         * Invoice Message Line 1
          ENDIF
          GOTO      PRPT5000
.
PRPIT420  MOVE      SP70,VARIABLE
          IF        MPGEFLAG <> 1
            MOVE      PMLINE2,VARIABLE         * Invoice Message Line 2
          ENDIF
          GOTO      PRPT5000
.
PRPIT430  MOVE      CNAME,VARIABLE             * Hospital Name
          GOTO      PRPT5000
.
PRPIT440  CALL      IBACLOCK                   * Current Date
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
          CALL      PACDATE
          IF        IBDTPLEN = 10
            MOVE      CPCDATE,VARIABLE
          ELSE
            MOVE      CPDATE,VARIABLE
          ENDIF
          GOTO      PRPT5000
. 
PRPIT450  MOVE      PRCNADD1,VARIABLE            * Hosp. Postal Address 1
          GOTO      PRPT5000
.
PRPIT460  MOVE      PRCNADD2,VARIABLE            * Hosp. Postal Address 2
          GOTO      PRPT5000
.
PRPIT470  MOVE      PRCNADD3,VARIABLE            * Hosp. Postal Address 3
          GOTO      PRPT5000
.
PRPIT480  MOVE      PRCNPOST,VARIABLE            * Hosp. Postcode
          GOTO      PRPT5000
.
PRPIT490  MOVE      SHORTDOC,VARIABLE            * Doctor Title & Surname
          GOTO      PRPT5000
.
PRPIT500  READ      CONTROLF,ZERO;*70,IBCNABNN   * ABN Number
          MOVE      IBCNABNN,VARIABLE
          GOTO      PRPT5000
.
PRPIT510  MOVE      PRCNINVN,VARIABLE            * Invoice Number
          MOVE      EIGHT,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      PRPT6000
.
PRPIT520  MOVE      VDEBNAME,VARIABLE            * Formatted Debtors Name
          GOTO      PRPT5000
.
PRPIT530  MOVE      VDEBTOR,VARIABLE             * Debtor Number
          GOTO      PRPT5000
.
PRPIT540  MOVE      PRCNNAME,VARIABLE            * Clinic Name on Invoice
          GOTO      PRPT5000
.
.>>>> PRPIT550  PACK      KEY22,PRDTINVN,SP30
.>>>>           CALL      RSPRDT4
.>>>> PRPIT551  CALL      RKPRDT4
.>>>>           BRANCH    OVRCD,PRPIT558
.>>>> .
.>>>>           MATCH     PINVNO,TREF
.>>>>           GOTO      PRPIT558 IF NOT EQUAL
.>>>> .
.>>>>           ADD       PTDTAMTP,FORM7P2
.>>>>           GOTO      PRPIT551
.>>>> .
.>>>> PRPIT558  MOVE      FORM7P2,PINVPAID
.>>>> .
PRPIT550  COMPARE   ONE,MPGEFLAG
          GOTO      PRPT4500 IF EQUAL
.
          MOVE      PINVPAID,VARIABLE            * Total Amount Paid
          MOVE      TEN2,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      PRPT6000
.
PRPIT560  COMPARE   ONE,MPGEFLAG
          GOTO      PRPT4500 IF EQUAL
.
          MOVE      PRINITOT,VARIABLE            * Invoice Total
          MOVE      TEN1,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      PRPT6000
.
PRPIT570 
.         IF        VSCANPMI = 1
            MOVE      "U",VARIABLE               * Patient Type (PMI)
.         ELSE
.           MOVE      "D",VARIABLE               * Patient Type (PRI)
.         ENDIF
          GOTO      PRPT5000
.
PRPIT580  MOVE      VPRANAME,VARIABLE            * PRA Name
          GOTO      PRPT5000
.
PRPIT590  MOVE      TMPRADD1,VARIABLE            * PRA Address line 1
          GOTO      PRPT5000
.
PRPIT600  MOVE      TMPRADD2,VARIABLE            * PRA Address line 2
          GOTO      PRPT5000
.
PRPIT610  MOVE      TMPRADD3,VARIABLE            * PRA Address line 3
          GOTO      PRPT5000
.
PRPIT620  MOVE      TMPRPOST,VARIABLE            * PRA Postcode
          GOTO      PRPT5000
.
PRPIT630  COMPARE   ONE,MPGEFLAG
          GOTO      PRPT4500 IF EQUAL
.
.         ASSIGN    (PRINITOT+PRINPAMT+PRINHAMT+PRINIAMT+PRINMAMT+PRINVAMT+PRINOTHA+PRINJAMT+PRINGSTA+PRINGSTJ),PINVTOT          * Amount outstanding
          ASSIGN    (PRINITOT+PRINPAMT+PRINHAMT+PRINIAMT+PRINMAMT+PRINVAMT+PRINOTHA+PRINJAMT+PRINCNTT+PRINGSTJ),PINVTOT          * Amount outstanding
          SUB       NBNKDEPS,PINVTOT
          MOVE      PINVTOT,VARIABLE
          MOVE      TEN2,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      PRPT6000
.
PRPIT640  MOVE      SP70,VARIABLE              * Debtor's Name (titl/givn/surn)
.         BRANCH    VSCANPMI,PRPIT645
.
.         Get debtor name
.
...          MOVE      SP30,PRDBSNAM
...          MOVE      SP30,PRDBGNAM
...          MOVE      SP5,PRDBTITL
...          MOVE      VDEBTOR,KEY8
...          CALL      RDPRDB1
...          BRANCH    OVRCD,PRPIT649
.
...          MOVE      PRDBSNAM,PACSNAME
...          MOVE      PRDBGNAM,PACGNAME
...          MOVE      PRDBTITL,PACTITLE
.         MOVE      "Unknown Debtor",PACSNAME
.         MOVE      SP70,PACGNAME
.         MOVE      SP70,PACTITLE
.         GOTO      PRPIT648
.
.         Get PMI name
.
PRPIT645  OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          MOVE      SP30,PSNAME
          MOVE      SP30,PGNAME
          MOVE      SP5,PTITL
          MOVE      VDEBTOR,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,PRPIT649
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
.
PRPIT648  MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,VARIABLE
PRPIT649  GOTO      PRPT5000
.
PRPIT650  MOVE      PSEX,VARIABLE            * Patients Sex
          GOTO      PRPT5000
.
PRPIT660  OPEN      PATMESG1,"patmesgf"  
          MOVE      MESSCODE,KEY3
          CALL      RDMESG1                 * read the message file for line 1
          BRANCH    OVRCD,PRPIT665
.
          MOVE      PMLINE1,VARIABLE
          GOTO      PRPT5000
.
PRPIT665  PACK      PMLINE1 WITH SP30,SP30,SP5
          GOTO      PRPT5000
.
PRPIT670  OPEN      PATMESG1,"patmesgf"  
          MOVE      MESSCODE,KEY3
          CALL      RDMESG1                 * read the message file for line 1
          BRANCH    OVRCD,PRPIT675
.
          MOVE      PMLINE2,VARIABLE
          GOTO      PRPT5000
.
PRPIT675  PACK      PMLINE2 WITH SP30,SP30,SP5
          GOTO      PRPT5000
.
PRPIT680  MOVE      PMPXKMOB,VARIABLE
          GOTO      PRPT5000
.
PRPIT690  MOVE      PMPXCMOB,VARIABLE
          GOTO      PRPT5000
.
PRPIT700  MOVE      PMPXRMOB,VARIABLE
          GOTO      PRPT5000
.
PRPIT710  MOVE      SP70,VARIABLE
          PACK      KEY22,PRINPRAC,PRINDOCT,PRINCLAM,TMPPIND
          CALL      RDPRDO1
          IF        OVRCD = 0
            MOVE      PRDOABNN,VARIABLE          * Service Doctor ABN
          ENDIF
          GOTO      PRPT5000
.
PRPIT720  MOVE      VSVDCNAM,VARIABLE            * Formatted Service Doc. Name
          GOTO      PRPT5000
.
PRPIT730  MOVE      PRINNUMB,BPAYINVN
          MOVE      SP10,BPAYHOSP          * private; doesn't have Hospital ID
          CALL      GBCRN000                    * Generate BPAY CRN
          MOVE      BPAYREFN,VARIABLE
          GOTO      PRPT5000
.
PRPIT740  MOVE      SP70,VARIABLE
          PACK      KEY8,PRCNINVN,SP70
          CALL      RDPRIN1
          BRANCH    OVRCD,PRPT5000
.
          PACK      KEY27,DPRINUNQ,PRINPRAC,PRINDOCT,TMPPIND,SP70
          CALL      RDPRHR1
          IF        OVRCD = 0
            MATCH     SP70,PRHRREFD
            IF        !@EQUAL & !@EOS
              PACK      KEY10,PRHRREFD,SP70
              CALL      RDPMHCP1
              IF        OVRCD = 0
                MOVE      PMHCTITL,VARIABLE
              ENDIF
            ENDIF
          ENDIF
          GOTO      PRPT5000
.
PRPIT750  MOVE      SP70,VARIABLE
          PACK      KEY8,PRCNINVN,SP70
          CALL      RDPRIN1
          BRANCH    OVRCD,PRPT5000
.
          PACK      KEY27,DPRINUNQ,PRINPRAC,PRINDOCT,TMPPIND,SP70
          CALL      RDPRHR1
          IF        OVRCD = 0
            MATCH     SP70,PRHRREFD
            IF        !@EQUAL & !@EOS
              PACK      KEY10,PRHRREFD,SP70
              CALL      RDPMHCP1
              IF        OVRCD = 0
                MOVE      PMHCGNAM,VARIABLE
              ENDIF
            ENDIF
          ENDIF
          GOTO      PRPT5000
.
PRPIT760  MOVE      SP70,VARIABLE
          PACK      KEY8,PRCNINVN,SP70
          CALL      RDPRIN1
          BRANCH    OVRCD,PRPT5000
.
          PACK      KEY27,DPRINUNQ,PRINPRAC,PRINDOCT,TMPPIND,SP70
          CALL      RDPRHR1
          IF        OVRCD = 0
            MATCH     SP70,PRHRREFD
            IF        !@EQUAL & !@EOS
              PACK      KEY10,PRHRREFD,SP70
              CALL      RDPMHCP1
              IF        OVRCD = 0
                MOVE      PMHCSNAM,VARIABLE
              ENDIF
            ENDIF
          ENDIF
          GOTO      PRPT5000
.
PRPIT770  MOVE      SP70,VARIABLE
          PACK      KEY8,PRCNINVN,SP70
          CALL      RDPRIN1
          BRANCH    OVRCD,PRPT5000
.
          PACK      KEY27,DPRINUNQ,PRINPRAC,PRINDOCT,TMPPIND,SP70
          CALL      RDPRHR1
          BRANCH    OVRCD,PRPT5000
.
          MATCH     SP70,PRHRREFD
          IF        !@EQUAL & !@EOS
            OPEN      PMSHCLA1,"pmshclaf"
            PACK      KEY20,PRHRREFD,PRHRHCPP,SP70
            CALL      RDPMHCL1
            IF        OVRCD=0
              MATCH     SP70,PMHLPRV1
              IF        !@EQUAL
                MOVE      PMHLPRV1,VARIABLE    * provider from a Linked Practice
                GOTO      PRPT5000
              ENDIF
            ENDIF
            PACK      KEY10,PRHRREFD,SP70
            CALL      RDPMHCP1
            IF        OVRCD = 0
              MOVE      PMHCPRV1,VARIABLE
            ENDIF
          ENDIF
          GOTO      PRPT5000
.
PRPIT780  MOVE      SP70,VARIABLE
          PACK      KEY8,PRCNINVN,SP70
          CALL      RDPRIN1
          BRANCH    OVRCD,PRPT5000
.
          PACK      KEY27,DPRINUNQ,PRINPRAC,PRINDOCT,TMPPIND,SP70
          CALL      RDPRHR1
          IF        OVRCD = 0
            MOVE      PRHRREFT,VARIABLE
          ENDIF
          GOTO      PRPT5000
.
PRPIT790  MOVE      SP70,VARIABLE
          PACK      KEY8,PRCNINVN,SP70
          CALL      RDPRIN1
          BRANCH    OVRCD,PRPT5000
.
          PACK      KEY27,DPRINUNQ,PRINPRAC,PRINDOCT,TMPPIND,SP70
          CALL      RDPRHR1
          IF        OVRCD = 0
            MOVE      PRHRRFPD,VARIABLE
          ENDIF
          GOTO      PRPT5000
.
PRPIT800  MOVE      SP70,VARIABLE
          PACK      KEY8,PRCNINVN,SP70
          CALL      RDPRIN1
          BRANCH    OVRCD,PRPT5000
.
          PACK      KEY27,DPRINUNQ,PRINPRAC,PRINDOCT,TMPPIND,SP70
          CALL      RDPRHR1
          IF        OVRCD = 0
            MATCH     SP70,PRHRRFPD
            IF        !@EQUAL & !@EOS
              PACK      KEY5,ANSR,ANSF,PRHRRFPD,SP70
              CALL      RDCODE1
              IF        OVRCD=0
                MOVE      TDESC,VARIABLE
              ENDIF
            ENDIF
          ENDIF
          GOTO      PRPT5000
.
PRPIT810  MOVE      SP70,VARIABLE
          PACK      KEY8,PRCNINVN,SP70
          CALL      RDPRIN1
          BRANCH    OVRCD,PRPT5000
.
          PACK      KEY27,DPRINUNQ,PRINPRAC,PRINDOCT,TMPPIND,SP70
          CALL      RDPRHR1
          IF        OVRCD = 0
            MATCH     SP70,PRHRMESS
            IF        !@EQUAL & !@EOS
              OPEN      PATMESG1,"patmesgf"
              PACK      KEY3,PRHRMESS,SP70
              CALL      RDMESG1
              IF        OVRCD=0
                MOVE      PMLINE1,VARIABLE
              ENDIF
            ENDIF
          ENDIF
          GOTO      PRPT5000
.
PRPIT820  MOVE      SP70,VARIABLE
          PACK      KEY8,PRCNINVN,SP70
          CALL      RDPRIN1
          BRANCH    OVRCD,PRPT5000
.
          PACK      KEY27,DPRINUNQ,PRINPRAC,PRINDOCT,TMPPIND,SP70
          CALL      RDPRHR1
          IF        OVRCD = 0
            MATCH     SP70,PRHRMESS
            IF        !@EQUAL & !@EOS
              OPEN      PATMESG1,"patmesgf"
              PACK      KEY3,PRHRMESS,SP70
              CALL      RDMESG1
              IF        OVRCD=0
                MOVE      PMLINE2,VARIABLE
              ENDIF
            ENDIF
          ENDIF
          GOTO      PRPT5000
.
PRPIT830  MOVE      SP70,VARIABLE
          PACK      KEY8,PRCNINVN,SP70
          CALL      RDPRIN1
          BRANCH    OVRCD,PRPT5000
.
          PACK      KEY27,DPRINUNQ,PRINPRAC,PRINDOCT,TMPPIND,SP70
          CALL      RDPRHR1
          IF        OVRCD = 0
            MOVE      PRHRBULK,VARIABLE
          ENDIF
          GOTO      PRPT5000
.
PRPIT840  MOVE      SP70,VARIABLE
          PACK      KEY8,PRCNINVN,SP70
          CALL      RDPRIN1
          BRANCH    OVRCD,PRPT5000
.
          PACK      KEY27,DPRINUNQ,PRINPRAC,PRINDOCT,TMPPIND,SP70
          CALL      RDPRHR1
          IF        OVRCD = 0
            MATCH     SP70,PRHRBULK
            IF        !@EQUAL & !@EOS
              PACK      KEY5,ANSG,ANSB,PRHRBULK,SP70
              CALL      RDCODE1
              IF        OVRCD=0
                MOVE      TDESC,VARIABLE
              ENDIF
            ENDIF
          ENDIF
          GOTO      PRPT5000
.
PRPIT850  MOVE      SP70,VARIABLE
          PACK      KEY8,PRCNINVN,SP70
          CALL      RDPRIN1
          BRANCH    OVRCD,PRPT5000
.
          PACK      KEY27,DPRINUNQ,PRINPRAC,PRINDOCT,TMPPIND,SP70
          CALL      RDPRHR1
          IF        OVRCD = 0
            MOVE      PRHROCOD,VARIABLE
          ENDIF
          GOTO      PRPT5000
.
PRPIT860  MOVE      SP70,VARIABLE
          PACK      KEY8,PRCNINVN,SP70
          CALL      RDPRIN1
          BRANCH    OVRCD,PRPT5000
.
          PACK      KEY27,DPRINUNQ,PRINPRAC,PRINDOCT,TMPPIND,SP70
          CALL      RDPRHR1
          IF        OVRCD = 0
            UNPACK    PRHRRDAT,CCENT,CYEAR,CMON,CDAY  * Referral date
            CALL      PACDATE
            IF        IBDTPLEN = 10
              MOVE      CPCDATE,VARIABLE
            ELSE
              MOVE      CPDATE,VARIABLE
            ENDIF
          ENDIF
          GOTO      PRPT5000
.
PRPIT870  MOVE      TMPRADD4,VARIABLE            * PRA Address line 4 - state
          GOTO      PRPT5000
.
PRPIT880  CALL      SPRD0000                          * PRA Full address
          GOTO      PRPT5000
.
PRPIT890  MOVE      SP70,VARIABLE
          PACK      KEY8,PRCNINVN,SP70
          CALL      RDPRIN1
          BRANCH    OVRCD,PRPT5000
.
          PACK      KEY27,DPRINUNQ,PRINPRAC,PRINDOCT,TMPPIND,SP70
          CALL      RDPRHR1
          IF        OVRCD = 0
            MATCH     SP70,PRHRREFD
            IF        !@EQUAL & !@EOS
.             PACK      KEY10,PRHRREFD,SP70
.             CALL      RDPMHCP1
.             IF        OVRCD = 0
.               MOVE      PMHCPRV1,VARIABLE
.             ENDIF
.
              PACK      KEY20,PRHRREFD,SP70
              CALL      RSPMHCL1
              CALL      RKPMHCL1                * read hcp link table
              BRANCH    OVRCD,PRPT5000
.
              MATCH     PMHLHCPC,PRHRREFD
              GOTO      PRPT5000 IF NOT EQUAL
.
              MOVE      PMHLPRV1,VARIABLE
.
            ENDIF
          ENDIF
          GOTO      PRPT5000
.	  
.----- format patient sex long description ------	  
.	  
PRPIT900  MOVE      PSEX,DSEXCHAR	  
          CALL      SEXDSP00	      
          MOVE      DSEXLDES,VARIABLE        * Get sex long description		
          GOTO      PRPT5000	      
.
PRPIT910  COMPARE   ONE,MPGEFLAG
          GOTO      PRPT4500 IF EQUAL
.
          MOVE      PRINCNTT,ADJTOTAL        * Credit Note Total
          ADD       PRINJAMT,ADJTOTAL        * Journal Amount
          ADD       PRINGSTJ,ADJTOTAL        * GST Journal Amount
          MOVE      ADJTOTAL,VARIABLE        * Adjustment Total
          MOVE      TEN2,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      PRPT6000
.
PRPIT920  PACK      VARIABLE,PMPXPFSN,SP70   * Preferred Surname
          GOTO      PRPT5000
.
PRPIT930  PACK      VARIABLE,PMPXPFGN,SP70   * Preferred Given Name
          GOTO      PRPT5000
.
PRPIT940  PACK      VARIABLE,PSNAME,SP70     * Pref Surname instead of Actual SN
          MATCH     "1",PTCNPFSN
          IF        @EQUAL
            MATCH     SP70,PMPXPFSN
            IF        !@EQUAL & !@EOS
              PACK      VARIABLE,PMPXPFSN,SP70,SP10
            ENDIF
          ENDIF
          GOTO      PRPT5000
.
PRPIT950  PACK      VARIABLE,PGNAME,SP70     * Pref GN instead of Actual GN
          MATCH     "1",PTCNPFGN
          IF        @EQUAL
            MATCH     SP70,PMPXPFGN
            IF        !@EQUAL & !@EOS
              PACK      VARIABLE,PMPXPFGN,SP70,SP10
            ENDIF
          ENDIF
          GOTO      PRPT5000
.
PRPIT960  PACK      VARIABLE,PTMASNAM,SP70   * Pref Surname instead of 40char SN
          MATCH     "1",PTCNPFSN
          IF        @EQUAL
            MATCH     SP70,PMPXPFSN
            IF        !@EQUAL & !@EOS
              PACK      VARIABLE,PMPXPFSN,SP70,SP10
            ENDIF
          ENDIF
          GOTO      PRPT5000
.
PRPIT970  PACK      VARIABLE,CAPPRVNO,SP70   * Hospital Provider Approval Number
          GOTO      PRPT5000
.
PRPIT980  PACK      VARIABLE,PTCNHABN,SP70   * Hospital ABN Number
          GOTO      PRPT5000
.
PRPIT981  MOVE      SP70,VARIABLE
          PACK      KEY27,DPRINUNQ,PRINPRAC,PRINDOCT,TMPPIND,SP70
          CALL      RDPRHR1
          IF        OVRCD = 0
            MATCH     SP6,PRHRPRAC                       * practice blank ?
            IF        !@EQUAL
              PACK      KEY6,PRHRPRAC,SP70
              CALL      RDPRPR1                          * practice on file ?
              IF        OVRCD = 0
                MATCH     SP3,PRPRREGI                   * register blank ?
                IF        !@EQUAL
                  PACK      KEY3,PRPRREGI,SP70
                  CALL      RDREGA1                      * register on file ?
                  IF        OVRCD = 0
                    MATCH     SP3,REGHOSP                * hosp file blank ?
                    IF        !@EQUAL
                      PACK      KEY3,REGHOSP,SP70
                      CALL      RDPTHSP1                 * hospital on file ?
                      IF        OVRCD = 0
                        PACK      VARIABLE,PTHSAPPR,SP70 * MultiHosp ApprovalNum
                        GOTO      PRPT5000
                      ENDIF
                    ENDIF
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          GOTO      PRPT5000
.
PRPIT982  MOVE      SP70,VARIABLE
          PACK      KEY27,DPRINUNQ,PRINPRAC,PRINDOCT,TMPPIND,SP70
          CALL      RDPRHR1
          IF        OVRCD = 0
            MATCH     SP6,PRHRPRAC                        * practice blank ?
            IF        !@EQUAL
              PACK      KEY6,PRHRPRAC,SP70
              CALL      RDPRPR1                           * practice on file ?
              IF        OVRCD = 0
                MATCH     SP3,PRPRREGI                    * register blank ?
                IF        !@EQUAL
                  PACK      KEY3,PRPRREGI,SP70
                  CALL      RDREGA1                       * register on file ?
                  IF        OVRCD = 0
                    MATCH     SP3,REGHOSP                 * hosp file blank ?
                    IF        !@EQUAL
                      PACK      KEY3,REGHOSP,SP70
                      CALL      RDPTHSP1                  * hospital on file ?
                      IF        OVRCD = 0
                        PACK      VARIABLE,PTHSABNN,SP70  * MultiHosp ABN Number
                        GOTO      PRPT5000
                      ENDIF
                    ENDIF
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          GOTO      PRPT5000
.
PRPT4500  MOVE      "C/FWD",VARIABLE         * Indicator for multi-page
          GOTO      PRPT5000
.
. ------- Format the variable -------
.
PRPT5000  RESET     VARIABLE,IBDTPLEN
          LENSET    VARIABLE
          RESET     VARIABLE
          PRINT     *IBDTCOL,*+,VARIABLE;
          GOTO      PRPIT100                 * Read next record
.
. ------- Formatting for Form fields and right justified variables -------
.
PRPT6000  COMPARE   ZERO,PRNTSTRT          * Don't bump if printing full length
          GOTO      PRPT7000 IF EQUAL
.
          BUMP      VARIABLE,PRNTSTRT      * bump to starting position
.
. ------- Print the variable -------
.
PRPT7000  PRINT     *IBDTCOL,*+,VARIABLE;
          GOTO      PRPIT100                 * Read next record
.
.------ end of invoice reached ------
.
PRPT8000  MOVE      FALSE,EXIT
.
.------ print new-lines until the end of page is reached ------
.
PRPT9000  COMPARE   CURRROW,IBTHLENG          * test page length
          GOTO      PRPT9999 IF EQUAL
.
          PRINT     *N;
          ADD       ONE,CURRROW
          GOTO      PRPT9000
.
PRPT9999  MOVE      FALSE,EXIT               * Set exit flag
          MATCH     "IBARSH",PGM
          RETURN    IF EQUAL
          IF        WEBFLAG=0
            CALL      CLSPRINT                 * close the spool file
          ENDIF
          RETURN                             * Finished
+
