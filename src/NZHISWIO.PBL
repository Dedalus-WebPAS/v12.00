.*****************************************************************************
.*  Include  :  NZHISWIO.PBL                                                 *
.*                                                                           *
.*  Function :  This include contains the routines required to access        *
.*              the NZ National Database for the Web Servers.                *
.*                                                                           *
.*  Includes :  NZHISIFD.INC - data variables used by this include           *
.*                                                                           *
.*  Routines :  CONNECT  -  Start connection                                 *
.*              DISCNNCT -  Terminate connection                             *
.*              CHKHCU00 -  Get Nat. HCU Details Verify they Match Local Sys.*
.*              GETHCU   -  Get details for NHI No.                          *
.*              SRHHCU   -  Start a surname search                           *
.*              NXTHCU   -  Continue surname search                          *
.*              ENDSRH   -  Terminate surname search                         *
.*              GETAKA   -  Get Aliases for NHI No.                          *
.*              GETHCE   -  Get Visits for NHI No.                           *
.*              GETDNR   -  Get Donor and contact detail                     *
.*              GETMWS   -  Get Medical Warnings                             *
.*                                                                           *
.*  Mod's    :                                                               *
.*****************************************************************************
.*        V11.04.01 28/10/2024  Ebon Clements  TSK 0948906                   *
.*                  Ignore space in AE2034 Unrecoverable read error format   *
.*                  scan in GETMWS                                          *
.*        V11.02.01 28/07/2022 Jill Parkinson Task 0920166                   *
.*                  Added check for deceased on nhi but not on local         *
.*        V10.15.02 20/02/2020  Peter Vela     TSK 0887845                   *
.*                  Added new warning message for different first given names*
.*        V10.15.01 25/11/2019  Ebon Clements  TSK 0860142                   *
.*                  Added AE2034 Unrecoverable read error check to GETMWS    *
.*****************************************************************************
.*        V10.14.02 30/05/2019  Thanh T.       TSK 0872539                   *
.*                  Added UPCPAT00 to uppercase the patient names            *
.*        V10.14.01 07/02/2019  Ebon Clements  TSK 0856330                   *
.*                  Added alert end date test to CHKMED00                    *
.*****************************************************************************
.*        V10.13.01 30/01/2019  Ebon Clements  TSK 0856330                   *
.*                  Added alert end date test to CHKALR00                    *
.*        V10.12.01 12/07/2018  Jill Parkinson CAR 0859770                   *
.*                  Added use of F1 in DELMED00 so A31 is only sent if       *
.*                  there is a change                                        *
.*        V10.06.02 26/08/2015  Jill Parkinson CAR 318680                    *
.*                  Added UPCADD00 to uppercase both ministry and local      *  
.*                  address fields before comparing them                     *  
.*        V10.06.01 24/08/2015  Jill Parkinson CAR 320905                    *
.*                  Corrected to use KEY7 not KEY8 for medical warnings over *
.*                  15                                                       *
.*        V10.05.01 11/11/2014  Jill Parkinson CAR 306745                    *
.*                  Added trap around WRPTMWS1 to stop I*D if double clicked *
.*        V10.03.05 26/09/2013  Jill Parkinson CAR 291691                    *
.*                  Moved clear of TOTALMWS to start of SETMED00 to stop     *
.*                  loop if more than 15 medical warnings are returned       *
.*        V10.03.04 18/07/2013  Jill Parkinson CAR 288525 & 286748           *
.*                  Moved call to DELMED00 from before SETMED20 to be after  *
.*                  the GETMWS call.  Previously, if GETMWS failed, all      *
.*                  patmwsaf records were already deleted                    *
.*                  Also, added RAPTMWS before call to WRPTMWS1              *
.*        V10.03.03 16/05/2012  Steve Armstrong  CAR 256322                  *
.*                  Added DGCLICUP triggers for Medical Warnings             *
.*        V10.03.02 02/02/2012 Jill Parkinson CAR 259481                     *
.*                  Changed SETMED00 to move NHMANMPI to NZIBNMPI            *
.*        V10.03.01 29/11/2011 Jill Parkinson CAR 249362                     *
.*                  Changed key to alert table                               *
.*****************************************************************************
.*        V10.01.01 11/10/2009 Jill Parkinson CAR 208479                     *
.*                  Changed SETMED20 to use merged NMPI number if required   *
.*****************************************************************************
.*        V9.12.01  22/10/2009 Jill Parkinson CAR 208479                     *
.*                  Changed to set PTMXSIN1 to blank not zero when no alerts *
.*****************************************************************************
.*        V9.10.01  01/07/2008 Jill Habasque CAR 162660                      *
.*                  Added CHKALR00                                           *
.*****************************************************************************
.*        V9.08.01  28/09/2006 Ebon Clements CAR 107243                      *
.*                  Added Deleted alert functionality                        *
.*****************************************************************************
.*        V9.04.08  18/11/2005 Jill Habasque CAR 78533                       *
.*                  Added CHKMED00 to update for PTMXSIN1                    *
.*        V9.04.07  21/03/2005 Ebon Clements CAR 58443                       *
.*                  Changed program to allow for PTMXSIN1 = 2 (S)security    *
.*                  patient alerts                                           *
.*****************************************************************************
.*        V9.03.03  23/02/2004  Jill Habasque SRF 47775                      *
.*                  Added differences warning                                *
.*        V9.03.02  24/10/2003  Jill Habasque SRF 43783                      *
.*                  Split read of fifo into two (errors, then fields)        *
.*        V9.03.01  10/10/2003  Jill Habasque SRF 43985                      *
.*                  Added VALHCU00                                           *
.*****************************************************************************
.*        V9.02.02  21/03/2003  Jill Habasque SRF 36883                      *
.*                  Added length and timeout on all fifo reads               *
.*        V9.02.01  22/11/2002  Jill Habasque SRF 33485                      *
.*                  Added merge warning                                      *
.*****************************************************************************
.*        V5.05.01  08/01/1998  Steve Armstrong   SRF 643903                 *
.*                  Mods to use NZFORM3B instead if NZFORM2A                 *
.*****************************************************************************
.*        V5.03.00  07/07/1995  B.G.Ackland       SRF 640445                 *
.*                  Read nhimasaf after fork to IBANHI01                     *
.*        V5.03.01  01/08/1995  ROD               SRF 610917                 *
.*                  Fixed I*C on 2nd index nhimasaf                          *
.*        V5.02.02  31/05/1995    Justin Coates  SRF 135427                  *
.*                  if cant connect to nat, allow processing to continue     *
.*                   (return exit = 0 from kurnha00)                         *
.*        V5.02.01  21/03/1995  Graeme Williams   SRF 640150                 *
.*                  Restore CNAME after fork to IBANHI01 if CNAME was changed*
.*                                                                           *
.*****************************************************************************\
.
.************************************************************************
.*  CONNECT  : Start Connection between IBA & NZ National database      *
.************************************************************************
CONNECT   READ      CONTROLF,TWENTY1;*139,PTCNNHID,*191,PTCNNHIF
          READ      CONTROLF,HUND09;*177,PTCNNHTM
.
. open the file we want to read from
.
          PACK      KEY12,NZIBRFIL,PORT,DOTPIP     * build reply FIFO
          REP       " 0",KEY12
.
          STRIP     PTCNNHID
          PACK      FULLPATH,PTCNNHID,KEY12
          PREP      NZRFIFA1,FULLPATH              * create reply FIFO
.
. now open file we want to write to
.
          STRIP     PTCNNHID
          STRIP     PTCNNHIF
          PACK      FULLPATH,PTCNNHID,PTCNNHIF,DOTPIP
          CLOSE     NZWFIFA1
          MOVE      ZERO,OVRCD                    * valid open
          TRAP      OVERCOND IF IO
          OPEN      NZWFIFA1,FULLPATH
          TRAPCLR   IO
.
          MOVE      ZERO,EXIT
          IF        OVRCD=1
            MOVE      "Can't open link file to National system. ",WEBTITLE
            CALL      WEBERR00
            MOVE      ONE,EXIT
          ENDIF
.
CONCT999  RETURN
.*************************************************************************
.*  DISCNNCT  :  Terminate Connection between IBA & NZ National database *
.*************************************************************************
DISCNNCT  CLOSE     NZWFIFA1                      * close write FIFO
          CLOSE     NZRFIFA1,DELETE               * close & delete reply FIFO
          RETURN
.--------------------------------------------------------------------------
. CHKHCU00 - Read Patient & Verify Patient Matches on Local System.
.          - Requires Local Details to have been read from NHIMASFD.
.          0 : NZIBNMPI - NMPI No. 
.          EXIT = 0 Read OK Continue
.          EXIT = 1 Can't Read 
.          EXIT = 2 Abort
.--------------------------------------------------------------------------
CHKHCU00 MOVE      NHMANMPI,NZIBNMPI
         CALL      GETHCU               * Read Patient Details 
         BRANCH    OVRCD,CHKHCU80       * Ensure Correct Patient
.
         MOVE      ZERO,EXIT            * Patient Verified Ok
.
         MATCH     ANSM,NZIBMERG        * Check if patient is merged
         GOTO      CHKHCU70 IF EQUAL
. 
         CALL      UPCPAT00             * Uppercase all name fields
.
         MATCH     NZIBSURN,NHMASURN    * Verify Patient has Correct Surname
         GOTO      CHKHCU85 IF NOT EQUAL
         MATCH     NZIBGIV1,NHMAGIV1    * Verify Patient has Correct 1st Name
         GOTO      CHKHCU50 IF EQUAL
.
         PACK      KEY5,ANSG,SP1,NHMASEX,SP70
         CALL      RDCODE1
         IF        OVRCD<>1
           MATCH     NZIBSEX,THCSCOD      * Verify Patient Sex
           GOTO      CHKHCU90 IF NOT EQUAL
         ELSE
           MATCH     NZIBSEX,NHMASEX      * Verify Patient Sex
           GOTO      CHKHCU90 IF NOT EQUAL
         ENDIF
         MATCH     NZIBDOB,NHMADOB      * Verify Patient Date of Birth
         GOTO      CHKHCU95 IF NOT EQUAL
.
         GOTO      CHKHCU76
.
CHKHCU50 MATCH    NHMADOB,NZIBDOB
         GOTO     CHKHCU75 IF NOT EQUAL
         PACK     KEY5,ANSG,SP1,NHMASEX,SP70
         CALL     RDCODE1
         IF       OVRCD<>1
           MATCH     NZIBSEX,THCSCOD      * Verify Patient Sex
           GOTO      CHKHCU75 IF NOT EQUAL
         ELSE
           MATCH     NZIBSEX,NHMASEX      * Verify Patient Sex
           GOTO      CHKHCU75 IF NOT EQUAL
         ENDIF
.
         MATCH    NHMAGIV2,NZIBGIV2
         GOTO     CHKHCU75 IF NOT EQUAL
         MATCH    NHMAGIV3,NZIBGIV3
         GOTO     CHKHCU75 IF NOT EQUAL
         MATCH    NHMAPREI,NZIBPRNM
         GOTO     CHKHCU75 IF NOT EQUAL
.
         CALL     UPCADD00  * Uppercase all address fields
.
         MATCH    NHMAADD1,NZIBADD1
         GOTO     CHKHCU75 IF NOT EQUAL
         MATCH    NHMAADD2,NZIBADD2
         GOTO     CHKHCU75 IF NOT EQUAL
         MATCH    NHMAADD3,NZIBSUBR
         GOTO     CHKHCU75 IF NOT EQUAL
         MATCH    NHMAADD4,NZIBCITY
         GOTO     CHKHCU75 IF NOT EQUAL
         MATCH    NHMAADD5,NZIBCTRY
         GOTO     CHKHCU75 IF NOT EQUAL
         MATCH    NHMADOMC,NZIBDOMC
         GOTO     CHKHCU75 IF NOT EQUAL
         MATCH    NHMARES,NZIBRESI
         GOTO     CHKHCU75 IF NOT EQUAL
         MATCH    NHMAETH,NZIBETHN
         GOTO     CHKHCU75 IF NOT EQUAL
         MATCH    NHMAETH2,NZIBETH2
         GOTO     CHKHCU75 IF NOT EQUAL
         MATCH    NHMAETH3,NZIBETH3
         GOTO     CHKHCU75 IF NOT EQUAL
.
. 0920166 only check death if a value on nhi (may be deceased on local and
. not yet updated on the national yet)
         MATCH     SP70,NZIBDDTH
         IF        !@EQUAL
           MATCH     NZIBDDTH,NHMADDTH     * Verify Patient Date of Death
           GOTO      CHKHCU75 IF NOT EQUAL
         ENDIF
.
         GOTO      CHKHCU99 
.
CHKHCU70 CLEAR     KEY40
         APPEND    "HCU ID has been merged to",KEY40
         APPEND    NZIBALID,KEY40
         RESET     KEY40
         WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                   "alert(#"",KEY40,"#");}":
                   "</script>",012
         GOTO      CHKHCU99
.
CHKHCU75 MOVE      "National Details Differ",KEY40
         CALL      NHIWAR00
         GOTO      CHKHCU99
.
CHKHCU76 MOVE      "National details are different to local",KEY40
         CALL      NHIWAR00
         GOTO      CHKHCU99
.
CHKHCU80 CALL      NZHISWAR
         WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                   "alert(#"",WEBTITLE,"#");}":
                   "</script>",012
.        MOVE      ONE,EXIT
         GOTO      CHKHCU99
.
CHKHCU85 MOVE      "Surname Does Not Match ",KEY40
         CALL      NHIERR00
         GOTO      CHKHCU99
.
CHKHCU90 MOVE      "1st Given Name & Gender Do Not Match",KEY40
         CALL      NHIERR00
         GOTO      CHKHCU99
.
CHKHCU95 MOVE      "1st Given Name & DOB Do Not Match.",KEY40
         CALL      NHIERR00
         GOTO      CHKHCU99
.
CHKHCU99 RETURN
+
.--------------------------------------------------------------------------
. UPCADD00 - Upper Case all NHI address fields before comparison 
.--------------------------------------------------------------------------
UPCADD00  REP       UPPLOW,NHMAADD1
          REP       UPPLOW,NZIBADD1
          REP       UPPLOW,NHMAADD2
          REP       UPPLOW,NZIBADD2
          REP       UPPLOW,NHMAADD3
          REP       UPPLOW,NZIBSUBR
          REP       UPPLOW,NHMAADD4
          REP       UPPLOW,NZIBCITY
          REP       UPPLOW,NHMAADD5
          REP       UPPLOW,NZIBCTRY
.
UPCADD99  RETURN
+
.--------------------------------------------------------------------------
. UPCPAT00 - Upper Case all patient names
.--------------------------------------------------------------------------
UPCPAT00  REP       UPPLOW,NZIBSURN
          REP       UPPLOW,NZIBGIV1
          REP       UPPLOW,NZIBGIV2
          REP       UPPLOW,NZIBGIV3
          REP       UPPLOW,NZIBPRNM
.
UPCPAT99  RETURN
.
.--------------------------------------------------------------------------
. VALHCU00 - Read Patient & Check if merged
.          EXIT = 0 Read OK Continue
.          EXIT = 1 Can't Read
.          EXIT = 2 Abort
.--------------------------------------------------------------------------
VALHCU00 MOVE      NHMANMPI,NZIBNMPI
         CALL      GETHCU               * Read Patient Details
         BRANCH    OVRCD,VALHCU80       * Ensure Correct Patient
.
         MOVE      ZERO,EXIT            * Patient Verified Ok
.
         MATCH     ANSM,NZIBMERG        * Check if patient is merged
         GOTO      VALHCU70 IF EQUAL
.
         GOTO      VALHCU99
.
VALHCU70 CLEAR     KEY40
         APPEND    "HCU ID has been merged to",KEY40
         APPEND    NZIBALID,KEY40
         RESET     KEY40
         WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                   "alert(#"",KEY40,"#");}":
                   "</script>",012
         GOTO      VALHCU99
.
VALHCU80 CALL      NZHISWAR
         WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                   "alert(#"",WEBTITLE,"#");}":
                   "</script>",012
.        MOVE      ONE,EXIT
         GOTO      VALHCU99
.
VALHCU99 RETURN
.------------------------------------------------------------
. Output HTML Override Confirm Message
.------------------------------------------------------------
WEBOVR00  REP       " +",ADMISSNO
          REP       " +",URNUMBER
          CLEAR     REDIRECT
          APPEND    "nhiweb99.pbl?",REDIRECT
          APPEND    "&reportno=",REDIRECT
          APPEND    "01",REDIRECT
          APPEND    "&template=",REDIRECT
          APPEND    "002",REDIRECT
          APPEND    "&urnumber=",REDIRECT
          APPEND    URNUMBER,REDIRECT
          APPEND    "&admissno=",REDIRECT
          APPEND    ADMISSNO,REDIRECT
          RESET     REDIRECT
.
          REP       "+ ",URNUMBER
          REP       "+ ",ADMISSNO
.
          WRITE     HTMLFILE;"<script type=#"text/javascript#"> {":
                   "  var undefined;":
                   "  if (confirm(#"",WEBTITLE,"\n\nClick Ok if you want to":
                   " update the details#")) {;":
                      " location.href=#"",REDIRECT,"#";}":
                      "}":
                   "</script>",012
.
          GOTO      WEBOVR99
.
WEBOVR95  DISPLAY   "*** Fifo Not Found ***"
.
WEBOVR99  RETURN
.------------------------------------------------------------
. Display Critical Warning - Called by CHKHCU00
.------------------------------------------------------------
NHIERR00  CLEAR     WEBTITLE
          APPEND    "Critical Data Mismatch with NHI/MWS NHI No ",WEBTITLE
          APPEND    NHMANMPI,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBOVR00
          RETURN
.------------------------------------------------------------
. Display differences Warning - Called by CHKHCU00
.------------------------------------------------------------
NHIWAR00  CLEAR     WEBTITLE
          APPEND    "Warning: National details are different to local",WEBTITLE
          RESET     WEBTITLE
          CALL      WEBOVR00
          RETURN
.---------------------------------------------
. Display Standard Error Message
.---------------------------------------------
NZHISERR  CLEAR     WEBTITLE
          APPEND    "NHI Error No ",WEBTITLE
          APPEND    NZIBERRN,WEBTITLE
          APPEND    " - ",WEBTITLE
          APPEND    NZIBERRD,WEBTITLE
          RESET     WEBTITLE
          CALL      WEBERR00
          RETURN
.---------------------------------------------
. Display Standard Warning Message
.---------------------------------------------
NZHISWAR  CLEAR     WEBTITLE
          APPEND    "NHI Error No ",WEBTITLE
          APPEND    NZIBERRN,WEBTITLE
          APPEND    " - ",WEBTITLE
          APPEND    NZIBERRD,WEBTITLE
          RESET     WEBTITLE
          RETURN
.************************************************************************
.*  GETHCU  :  Get the PMI details for a NHI Number                     *
.*             Parameters : NZIBNMPI - NMPI No.                         *
.************************************************************************
GETHCU    MOVE      ZERO,NZHISWRN                 * Medical Warning Flag
          MOVE      "3 ",NZIBACTN                 * action identifier
          CALL      FIFHD000                      * set up header details
.
          MOVE     "38",WRLNGTH                  * FIFO record length (write)
          WRITE    NZWFIFA1;NZIBSTX,NZIBACTN,NZIBUSER,NZIBVDU,NZIBRFIF,NZIBNMPI;
.
. now get the reply 
.
          MOVE      "86",RDLNGTH                * FIFO record length (reply)
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBAPPC,NZIBERRN,NZIBERRD;
.
          MOVE      ZERO,OVRCD                  * assume valid read
          MOVE      ONE,NZHISWRN                * Read Ok & Warning Set
          MATCH     "AA",NZIBAPPC               * valid read?
          IF        !@EQUAL
            MOVE      ZERO,NZHISWRN             * Read Failed
            MOVE      ONE,OVRCD                 * error
            GOTO      GTHCU999
          ENDIF
.
          MOVE      "288",RDLNGTH                * FIFO record length (reply)
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBSURN,NZIBGIV1,NZIBGIV2:
                           NZIBGIV3,NZIBPRNM:
                           NZIBADD1,NZIBADD2,NZIBSUBR,NZIBCITY,NZIBCTRY:
                           NZIBDOB,NZIBDDTH,NZIBDOMC,NZIBRESI,NZIBETHN:
                           NZIBETH2,NZIBETH3,NZIBSEX,NZIBALNM,NZIBMWDN:
                           NZIBMWST,NZIBMERG,NZIBALID,NZIBLAST;
.
          MOVE      ZERO,OVRCD                  * assume valid read
          MOVE      ONE,NZHISWRN                * Read Ok & Warning Set
          MATCH     "AA",NZIBAPPC               * valid read?
          IF        !@EQUAL
            MOVE      ZERO,NZHISWRN             * Read Failed
            MOVE      ONE,OVRCD                 * error
          ELSE
            MATCH     SP1,NZIBMWST
            IF        @EQUAL
              MOVE      TWO,NZHISWRN            * Read Ok & No Warning
            ENDIF
          ENDIF
.
GTHCU999  RETURN
.************************************************************************
.*  GETHCE  :  Get list of visits on National database for an NHI Number*
.************************************************************************
GETHCE    MOVE      ZERO,OVRCD
          MOVE      "11",NZIBACTN                 * action identifier
          CALL      FIFHD000                      * set up header details
.
          MOVE      "62",WRLNGTH                  * FIFO record length (write)
          WRITE     NZWFIFA1;NZIBSTX,NZIBACTN,NZIBUSER,NZIBVDU,NZIBRFIF:
                             NZIBNMPI,NZIBCPTR,NZIBMXRP;
.
. now get the reply (firstly checking for an error)
.
          MOVE      "86",RDLNGTH
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBAPPC,NZIBERRN,NZIBERRD;
.
          MATCH     "AA",NZIBAPPC                 * valid read?
          GOTO      GTHCE900 IF NOT EQUAL         * error
.
          MOVE      "28",RDLNGTH
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBMRGS,NZIBNMWF,NZIBNMWR:
                             NZIBCPTR;
.
          MOVE      ONE,NZIBCNT
          MOVE      "106",RDLNGTH
          SQUEEZE   NZIBNMWR
          MOVE      NZIBNMWR,FORM2
          WHILE     NZIBCNT <= FORM2
            READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZARSTDT[NZIBCNT]:
                                       NZARENDT[NZIBCNT],NZAREVND[NZIBCNT]:
                                       NZARRPTF[NZIBCNT];
            ADD       ONE,NZIBCNT
          DO
.
          MOVE      ZERO,OVRCD                    * valid read
          GOTO      GTHCE999
.
GTHCE900  MOVE      ONE,OVRCD                     * an error was encountered
.
GTHCE999  RETURN
.*****************************************************************************
.*  GETDNR  :  Get the Donor and Contact details for a patient from Nat. Sys *
.*****************************************************************************
GETDNR    MOVE      "12",NZIBACTN                 * action identifier
          CALL      FIFHD000                      * set up header details
.
          MOVE      "38",WRLNGTH                  * FIFO record length (write)
          WRITE     NZWFIFA1;NZIBSTX,NZIBACTN,NZIBUSER,NZIBVDU,NZIBRFIF:
                             NZIBNMPI;
.
. now get the reply
.
          MOVE      "86",RDLNGTH                  * FIFO record length (reply)
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBAPPC,NZIBERRN,NZIBERRD;
.
          MOVE      ZERO,OVRCD                  * assume valid read
          MATCH     "AA",NZIBAPPC               * valid read?
          IF        !@EQUAL
            MOVE      ONE,OVRCD                 * error
            GOTO      DNHCU999
          ENDIF
.
          MOVE      "311",RDLNGTH                  * FIFO record length (reply)
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBSTSU,NZIBPCSN,NZIBPCG1:
                          NZIBPCG2,NZIBPCG3:
                          NZIBPRNM,NZIBPCA1,NZIBPCA2,NZIBPCSB,NZIBPCTW,NZIBPCCT:
                          NZIBWKPH,NZIBAHPH,NZIBRELC,NZIBDTLU;
.
DNHCU999  RETURN
+
.************************************************************************
.*  FIFHD000  :  Set up Header details for Write FIFO                   *
.************************************************************************
FIFHD000  CLOCK     PID,NZIBUSER                  * process Id/User
          PACK      NZIBVDU,PASSCODE,SLASH,PORT   * VDU Id
          REP       " 0",NZIBVDU
          PACK      NZIBRFIF,NZIBRFIL,PORT        * reply fifo name
          REP       " 0",NZIBRFIF
.
FIFHD999  RETURN
.------------------------------------------------------------------------------
. Routine to Fork to IBANHI01 to Register or Change a Patients Details
.
. ERROR will contain :  "01xxxxxxxx", where xxxxxxxx = U/R Number
.                   or  "02xxxxxxxx", where xxxxxxxx = Zero U/R Number
.                   or  "03xxxxxxxx", where xxxxxxxx = NMPI Number
.                   or  Spaces (Run from menu)
.
.            01 means that an NHI field has been selected in another program
.            02 means that the Register option on National Search was selected
.            03 means that a National Number was selected not on Local system
.
. Read in Returned Details from the Fork to NHI Screens
. EXIT=0 Record Posted  EXIT=1 Cancelled
.
.------------------------------------------------------------------------------
CALNHI00  MOVE      ZERO,HLEF
          CALL      GETSCR00                * Save Screen
          MOVE      ONE,NZHISMOD
          FORK      "IBANHI01",ANS
          PACK      ERROR,SP20              * Clear Error Field
          CALL      PUTSCR00                * Put Screen
.
          MOVE      "PATNZI",KEY6
          PACK      KEY8,KEY6,PORT
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          oPEN      FILNHIAF,KEY8
          TRAPCLR   IO
          BRANCH    OVRCD,CALNHI99
          READ      FILNHIAF,SEQ;NZHISMOD,PURNO,PSNAME,PGNAME,PSEX:
                                 PBDATE,PADD1,PADD2,PSUBRB,PADD4,PCEASE:
                                 PDECDTE,PPOST
.
          OPEN      NHIMASA2,"nhimasaf"
          MOVE      PURNO,CPPURNO
          MOVE      PURNO,KEY8
          CALL      RDNHMAS2          * Refresh NHI Variables for Display
          CLOSE     FILNHIAF,DELETE
CALNHI99  RETURN
.*****************************************************************************
.*  NZDSP000  :  Display records/matches found                               *
.*****************************************************************************
.NZDSP000  MOVE      "D",FNTN
.          PROC      NZGET000                * call subr in proc to do the job
.          RETURN
.*****************************************************************************
.*  NZDUP000  :  Display records/matches found After New Dup Error           *
.*****************************************************************************
NZDUP000  MOVE      "N",FNTN
          PROC      NZGET000                * call subr in proc to do the job
          RETURN
.******************************************************************************
.* DISNE000 - Display the list of National Episodes                           *
.******************************************************************************
DISNE000  DISPLAY   *P1:8,*EF:
                    *P1:8,*V2LON,*ULON,"Start     ":
                    *P12:8,"End       ":
                    *P23:8,"Reporting Facility  "
.
          MOVE      ZERO,COUNT
          MOVE      NINE,CVERT
.
DISNE100  ADD       ONE,COUNT
.
          MOVE      NZIBNMWR,FORM2
          COMPARE   COUNT,FORM2                  * Check for number returned
          GOTO      DISNE800 IF LESS
.
. ------- check if we need a new screen -------------------------
.
          COMPARE   CVERT,TWENTY2                * Check for number returned
          IF        @LESS
            CALL      EXITM000                   * Exit and Search More
            COMPARE   ZERO,EXIT                  * exit
            GOTO      DISNE999 IF EQUAL
.
            MOVE      NINE,CVERT
            DISPLAY   *P1:CVERT,*EF
          ENDIF
.
          UNPACK    NZARSTDT[COUNT],CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          DISPLAY   *P1:CVERT,CPCDATE
.
          UNPACK    NZARENDT[COUNT],CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          DISPLAY   *P12:CVERT,CPCDATE:
                    *P23:CVERT,NZARRPTF[COUNT]
          DISPLAY   *H02,NZAREVND[COUNT]
.
          ADD       "2",CVERT
.
          GOTO      DISNE100
.
DISNE800  COMPARE   FORM3,FORM3Z
          IF        @EQUAL
            CALL      EXITT000                   * Exit Only Display
          ELSE
            CALL      EXITM000                   * Exit and Search More
          ENDIF
.
DISNE999  RETURN
.
.******************************************************************************
.* EXITT000 - Display (E)xit only prompt                                      *
.******************************************************************************
EXITT000  MOVE      FALSE,EXIT
          DISPLAY   *P1:24,*EL,"(":
                    *V2LON,ANSE,*HOFF:
                    ")xit ? ";
.
EXITT100  KEYIN     *P10:24,*V2LON,ANS;
.
          REP       UPPLOW,ANS
          MATCH     ANS,ANSE
          IF        !@EQUAL
            BEEP
            GOTO      EXITT100
          ENDIF
.
EXITT999  RETURN
.
.******************************************************************************
.* EXITM000 - Display (E)xit and (M)ore Options                               *
.******************************************************************************
EXITM000  MOVE      FALSE,EXIT
.
          DISPLAY   *P1:24,*EL,"(":
                    *V2LON,ANSE,*HOFF:
                    ")xit, (":
                    *V2LON,ANSM,*HOFF:
                    ")ore ? ";
.
EXITM100  KEYIN     *P18:24,*V2LON,ANS;
.
          MOVE      FALSE,EXIT
          REP       UPPLOW,ANS
          MATCH     ANS,ANSE
          IF        @EQUAL
            GOTO      EXITM999
          ENDIF
.
          MATCH     ANS,ANSM
          IF        @EQUAL
            MOVE      TRUE,EXIT
            GOTO      EXITM999
          ENDIF
.
          BEEP
          GOTO      EXITM100
.
EXITM999  RETURN
.
+
.************************************************************************
.*  PROC  NZGET  :  Contains subroutines to search, load & retrieve from*
.*                  New Zealand National system.......to save data space* 
.************************************************************************
          DEFPROC   NZGET000
.
NZDOB     DIM       8
NZGIVEN   DIM       25[15]
NZADDR    DIM       67[15]
.
LISTNMPI  DIM       7[8]     * NMPI No on Screen
NZARNMPI  DIM       7[15]    * NMPI No.
NZARSURN  DIM       25[15]   * Surname/Family name
NZARGIV1  DIM       20[15]   * Given Name 1
NZARGIV2  DIM       20[15]   * Given Name 2
NZARGIV3  DIM       20[15]   * Given Name 3
NZARPRNM  DIM       1[15]    * preferred name indicator
NZARDOB   DIM       8[15]    * Date of Birth
NZARSEX   DIM       1[15]    * Sex/Gender
.
NUMTOT    FORM      3
NUMCUR    FORM      3
.
.
DETFN000
.          MATCH     "L",FNTN
.          IF        @EQUAL
.            CALL      LDHCU000
.            GOTO      DETFN999
.          ENDIF 
.
.         MATCH     "D",FNTN
.         IF        @EQUAL
.           CALL      NZDSP000
.           GOTO      DETFN999
.         ENDIF
.
          MATCH     "N",FNTN
          IF        @EQUAL
            CALL      NZDUP000
            GOTO      DETFN999
          ENDIF
.
DETFN999  GOTO      NZGET999
.************************************************************************
.*  SRHHCU  :  Initiate a Surname search on the National database       *
.************************************************************************
SRHHCU    MOVE      "5 ",NZIBACTN                 * action identifier
          CALL      FIFHD000                      * set up header details
.
          MOVE      "294",WRLNGTH                 * FIFO record length (write)
          WRITE     NZWFIFA1;NZIBSTX,NZIBACTN,NZIBUSER,NZIBVDU,NZIBRFIF:
                          NZIBSTYP,NZIBSTIM,NZIBRLIM:
                          NZIBSURN,NZIBGIV1,NZIBGIV2,NZIBGIV3,NZIBPRNM,NZIBNSTY:
                          NZIBADD1,NZIBADD2,NZIBSUBR,NZIBCITY,NZIBCTRY:
                          NZIBASTY,NZIBMGEN,NZIBMDOB,NZIBMAGE,NZIBMPAG;
.
. now get the reply (firstly checking for an error)
.
          MOVE      "86",RDLNGTH                * FIFO record length (reply)
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBAPPC,NZIBERRN,NZIBERRD;
.
          MATCH     "AA",NZIBAPPC                 * valid read?
          GOTO      SRHCU900 IF NOT EQUAL         * error
.
          MOVE      "30",RDLNGTH                * FIFO record length (reply)
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBNMFN,NZIBNMRE,NZIBNMRP:
                             NZIBCPTR;
.
          MOVE      "280",RDLNGTH                * FIFO record length (reply)
          MOVE      ONE,NZIBCNT
.         
          SQUEEZE   NZIBNMRP
          MOVE      NZIBNMRP,FORM2
          WHILE     NZIBCNT <= FORM2
.
            READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZARNMPI[NZIBCNT]:
                               NZARSURN[NZIBCNT]:
                               NZIBGIV1,NZIBGIV2:
                               NZIBGIV3,NZIBPRNM:
                               NZIBADD1,NZIBADD2:
                               NZIBSUBR,NZIBCITY:
                               NZIBCTRY,NZARDOB[NZIBCNT]:
                               NZIBDDTH,NZIBDOMC:
                               NZIBRESI,NZIBETHN,NZIBETH2,NZIBETH3:
                               NZARSEX[NZIBCNT],NZIBALNM:
                               NZIBMWDN,NZIBMWST:
                               NZIBMERG;
            CALL      NZFGS000                    * format given name
            CALL      NZFAS000                    * format address
            ADD       ONE,NZIBCNT
          DO
.
          MOVE      ZERO,OVRCD                    * valid read
          GOTO      SRHCU999
.
SRHCU900  MOVE      ONE,OVRCD                     * an error was encountered
.
SRHCU999  RETURN
+
.************************************************************************
.*  NXTHCU  :  Continue a surname search on the National database       *
.************************************************************************
NXTHCU    MOVE      "6 ",NZIBACTN                 * action identifier
          CALL      FIFHD000                      * set up header details
.
          MOVE      "59",WRLNGTH                  * FIFO record length (write)
          WRITE     NZWFIFA1;NZIBSTX,NZIBACTN,NZIBUSER,NZIBVDU,NZIBRFIF:
                             NZIBCPTR,NZIBSTIM,NZIBRLIM,NZIBTSRC;
.
. now get the reply (firstly checking for an error)
.
          MOVE      "86",RDLNGTH                * FIFO record length (reply)
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBAPPC,NZIBERRN,NZIBERRD;
.
          MATCH     "AA",NZIBAPPC                * valid read?
          GOTO      NXHCU900 IF NOT EQUAL        * error
.
          MOVE      "30",RDLNGTH                * FIFO record length (reply)
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBNMFN,NZIBNMRE,NZIBNMRP:
                             NZIBCPTR;
.
          MOVE      "280",RDLNGTH                * FIFO record length (reply)
          MOVE      ONE,NZIBCNT
          SQUEEZE   NZIBNMRP
          MOVE      NZIBNMRP,FORM2
          WHILE     NZIBCNT <= FORM2
            READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZARNMPI[NZIBCNT]:
                               NZARSURN[NZIBCNT]:
                               NZIBGIV1,NZIBGIV2:
                               NZIBGIV3,NZIBPRNM:
                               NZIBADD1,NZIBADD2:
                               NZIBSUBR,NZIBCITY:
                               NZIBCTRY,NZARDOB[NZIBCNT]:
                               NZIBDDTH,NZIBDOMC:
                               NZIBRESI,NZIBETHN,NZIBETH2,NZIBETH3:
                               NZARSEX[NZIBCNT],NZIBALNM:
                               NZIBMWDN,NZIBMWST:
                               NZIBMERG;
            CALL      NZFGS000                    * format given name
            CALL      NZFAS000                    * format address
            ADD       ONE,NZIBCNT
          DO
.
          MOVE      ZERO,OVRCD                    * valid read
          GOTO      NXHCU999
.
NXHCU900  MOVE      ONE,OVRCD                     * an error was encountered
.
NXHCU999  RETURN
.*****************************************************************************
.*  NZFRM000  :  format data for display                                     *
.*****************************************************************************
NZFRM000  UNPACK    NZARDOB[TOTALCNT],CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,NZDOB                  * formatted date of birth
.
NZFRM999  RETURN
+
.************************************************************************
.* NZFGS000 :  Format the given name returned from a search             *
.************************************************************************
.
. put as many given names as will fit into NZGIVEN[NZIBCNT]
.
NZFGS000  STRIP     NZIBGIV1
          MOVELPTR  NZIBGIV1,NZIBFRA2
          STRIP     NZIBGIV2
          MOVELPTR  NZIBGIV2,NZIBFRB2
          STRIP     NZIBGIV3
          MOVELPTR  NZIBGIV3,NZIBFRC2
.
          IF        (NZIBFRA2+NZIBFRB2) > 24
            PACK      NZGIVEN[NZIBCNT],NZIBGIV1,SP30
          ELSE
            IF        (NZIBFRA2+NZIBFRB2+NZIBFRC2) > 23
              PACK      NZGIVEN[NZIBCNT],NZIBGIV1,SP1,NZIBGIV2,SP30
            ELSE
              PACK      NZGIVEN[NZIBCNT],NZIBGIV1,SP1,NZIBGIV2,SP1,NZIBGIV3:
                        SP30
            ENDIF
          ENDIF
          RETURN
+
.************************************************************************
.* NZFAS000 :  Format the address returned from a search                *
.************************************************************************
.
. format the address
.
NZFAS000  PACK       NZADDR[NZIBCNT],NZIBADD1
          STRIP      NZADDR[NZIBCNT]
.
          MATCH      SP20,NZIBADD2
          IF         !@EQUAL
            ENDSET     NZADDR[NZIBCNT]
            APPEND     NZCOMMA,NZADDR[NZIBCNT]
            APPEND     NZIBADD2,NZADDR[NZIBCNT]
            RESET      NZADDR[NZIBCNT]
            STRIP      NZADDR[NZIBCNT]
          ENDIF
.
          MATCH      SP20,NZIBSUBR
          IF         !@EQUAL
            ENDSET     NZADDR[NZIBCNT]
            APPEND     NZCOMMA,NZADDR[NZIBCNT]
            APPEND     NZIBSUBR,NZADDR[NZIBCNT]
            RESET      NZADDR[NZIBCNT]
            STRIP      NZADDR[NZIBCNT]
          ENDIF
.
          MATCH      SP20,NZIBCITY
          IF         !@EQUAL
            ENDSET     NZADDR[NZIBCNT]
            APPEND     NZCOMMA,NZADDR[NZIBCNT]
            APPEND     NZIBCITY,NZADDR[NZIBCNT]
            RESET      NZADDR[NZIBCNT]
            STRIP      NZADDR[NZIBCNT]
          ENDIF
.
          MATCH      SP20,NZIBCTRY
          IF         !@EQUAL
            ENDSET     NZADDR[NZIBCNT]
            APPEND     NZCOMMA,NZADDR[NZIBCNT]
            APPEND     NZIBCTRY,NZADDR[NZIBCNT]
            RESET      NZADDR[NZIBCNT]
          ENDIF
.
          MATCH      SP20,NZIBDOMC
          IF         !@EQUAL
            ENDSET     NZADDR[NZIBCNT]
            APPEND     NZCOMMA,NZADDR[NZIBCNT]
            APPEND     NZIBDOMC,NZADDR[NZIBCNT]
            RESET      NZADDR[NZIBCNT]
          ENDIF
.
          RETURN
+
.************************************************************************
.*  ENDSRH  :  Terminate a search on the National database              *
.************************************************************************
ENDSRH    MOVE      "Y",NZIBTSRC                  * Terminate Search = Yes
          CALL      NXTHCU
          RETURN
+
.*****************************************************************************
.
NZGET999  ENDPROC
.-----------------------------------------------------------------------------
. Procedure to Setup Medical Warning Temp File from National NHI/MWS System.
.
. Note Before Calling this Routine
. A Record from the File NHIMASFD Must have been read.
. A Connect to the national system must have been peformed.
.
.-----------------------------------------------------------------------------
.
           DEFPROC   SETMED00
.
. the following fields repeat upto 15 times
.
NZARSEVR   DIM       1[15]    * Severity code
NZARDTON   DIM       8[15]    * Date of Onset
NZARWTXT   DIM       70[15]   * MW Text description
NZARMARC   DIM       8[15]    * MARC approval date
NZARWDTU   DIM       8[15]    * MW Date of Last Update
NZARFACC   DIM       4[15]    * Facility code
NZARDOCC   DIM       5[15]    * MW Doctor code
NZARSYSI   DIM       2[15]    * MW Coding System identifier
NZARMWCD   DIM       6[15]    * MW Code
NZARENTM   DIM       14[15]   * MW Entry Time
.
.------------------------------------------------------------
. Setup File for Medical Warnings
.------------------------------------------------------------
SETMED00  MOVE     NHMANMPI,KEY7
          MOVE     ZERO,TOTALMWS   * MHI/MWS System.
          MOVE     NHMANMPI,NZIBNMPI
          CALL     GETHCU
          BRANCH   OVRCD,SETMED98,SETMED99
.
SETMED10  MATCH    SP1,NZIBMWST
          GOTO     SETMED90 IF EQUAL
.                                  * Access to NHI/MWS Should be OK so delete
SETMED20  PACK     NZIBALID,NZIBALID,SP70
          PACK     KEY7,NZIBNMPI,SP70
          MATCH    SP70,NZIBALID
          IF       !@EQUAL
            PACK     NZIBNMPI,NZIBALID,SP70
          ENDIF
          CALL     GETMWS          * Read in group of Medical Warnings from
          PACK     NZIBNMPI,KEY7,SP70
          BRANCH   OVRCD,SETMED95  * NHI/MWS. OVRCD=ERROR so Exit
.
          CALL     DELMED00        * current medical warnings and reload from
.
SETMED30  SQUEEZE  NZIBNMWR        * Number of Warnings Returned in this Group
          MOVE     NZIBNMWR,FORM2  * as a 2 character field. 
          COMPARE  ZERO,FORM2
          GOTO     SETMED90 IF EQUAL
.
          CALL     ADDMED00
          CALL     CHKMED00
.
          SQUEEZE  NZIBNMWR        * Number of Warnings Returned in this Group
          MOVE     NZIBNMWR,FORM2 
          ADD      FORM2,TOTALMWS  * Accumulated Total Returned
          SQUEEZE  NZIBNMWF        * Total on NHI/MWS 
          MOVE     NZIBNMWF,FORM3  
          IF       TOTALMWS<FORM3
            CALL     GETMWS          * Read in group of Medical Warnings from
            PACK     NZIBNMPI,KEY7,SP70
            BRANCH   OVRCD,SETMED95  * NHI/MWS. OVRCD=ERROR so Exit
            GOTO     SETMED30      * Keep going until all returned
          ENDIF
          MOVE     ZERO,EXIT       * So Just Delete All and Exit
          GOTO     SETMED99
.
SETMED90  CALL     DELMED00        * Indicator for NO Medical Warnings Set
          COMPARE  ONE,F1          * something was changed in DELMED00
          IF       @EQUAL
            CALL     PMIGTNID        * broadcast A31 message
            MOVE     NMPNUMB,PTNINMPI
            MOVE     TWO,HL7TRGID
            MOVE     "NZHISWIO",HL7INCLD
            PROC     DGCLICUP
          ENDIF
.
          MOVE     ZERO,EXIT       * So Just Delete All and Exit
          CALL     CHKMED00
          CALL     CHKALR00
          GOTO     SETMED99
.
SETMED95  CALL     NZHISERR        * Display Error from Medical Warning Read
SETMED98  MOVE     ONE,EXIT        * Error Display in CHKHCU00 Set Exit
          GOTO     SETMED99
.
SETMED99  GOTO     ENDSETMW
.------------------------------------
. Add Medical Warning Read to File
.------------------------------------
ADDMED00  MOVE      ZERO,COUNT
          MOVE      ZERO,PTMWCNT
          SQUEEZE   NZIBNMWR
          MOVE      NZIBNMWR,FORM2
ADDMED10  ADD       ONE,COUNT
          IF        COUNT>FORM2
            GOTO      ADDMED90
          ENDIF
          PACK      PTMWCLSS,NZARSEVR[COUNT],SP6
          MOVE      NHMAURNO,PTMWURNO           * U/R Number
ADDMED20  ADD       ONE,PTMWCNT
          PACK      KEY14,PTMWURNO,PTMWCLSS,PTMWCNT
          CALL      RDPTMWS1
          COMPARE   ZERO,OVRCD
          GOTO      ADDMED20 IF EQUAL
          MOVE      ZERO,PTMWSYS                  * initialise system of coding
          MOVE      NZARDTON[COUNT],PTMWDATE      * date of onset
          MOVE      NZARWTXT[COUNT],PTMWTEXT      * text
          MOVE      NZARMARC[COUNT],PTMWMARC      * MARC Approval date
          PACK      PTMWHOSP,NZARFACC[COUNT],SP30 * Facility who reported warnng
          MOVE      NZARDOCC[COUNT],PTMWDOCT      * Doctor code
          MOVE      NZARSYSI[COUNT],PTMWSYS       * Coding system being used
          MOVE      NZARMWCD[COUNT],PTMWCODE      * Code for Warning (Danger)
          MOVE      NZARENTM[COUNT],PTMWLUPD      * Date/Time entered on NZHIS
          PACK      KEY14,PTMWURNO,PTMWCLSS,PTMWCNT
          CALL      RAPTMWS1
          IF        OVRCD=1
            TRAP      OVERCOND IF IO
            CALL      WRPTMWS1
            TRAPCLR   IO
          ENDIF
.
          GOTO      ADDMED10
.
ADDMED90  CALL      PMIGTNID                      * broadcast A31 message
          MOVE      NMPNUMB,PTNINMPI
          MOVE      ONE,HL7TRGID
          MOVE      "NZHISWIO",HL7INCLD
          PROC      DGCLICUP
.
ADDMED99  RETURN
.**************************************************************************
.*                               CHKALR00              Called by: PROC0000*
.*                                                                        *
.**************************************************************************
CHKALR00  MOVE      NHMAURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,CHKALR99
.
          MOVE      PTMXSIN1,DIM1
.
          PACK      KEY16,NHMAURNO,SP70
          CALL      RSPTALR1
CHKALR10  CALL      RKPTALR1
          BRANCH    OVRCD,CHKALR90
.
          MATCH     NHMAURNO,PTALURNO
          GOTO      CHKALR90 IF NOT EQUAL
.
          MATCH     SP70,PTALEDAT
          GOTO      CHKALR10 IF NOT EQUAL        * show alert as inactive
.
          PACK      KEY5,PTALCATG,PTALCODE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CHKALR10
.
          REP       "S2 1",TCINDC4
.
          MATCH     "2",TCINDC4        * check for security alert, if yes stop
          IF        @EQUAL
            PACK      PTMXSIN1,TCINDC4
            GOTO      CHKALR90
          ENDIF
.
          MATCH     PTMXSIN1,TCINDC4
          IF        @LESS
            PACK      PTMXSIN1,TCINDC4
          ENDIF
.
          MATCH     SP70,PTMXSIN1
          IF        @EQUAL
            PACK      PTMXSIN1,TCINDC4
          ENDIF
.
          GOTO      CHKALR10
.
CHKALR90  MATCH     PTMXSIN1,DIM1
          IF        !@EQUAL
            CALL      UPMAST1
          ENDIF
.
CHKALR99  RETURN
+
.------------------------------------------------------------
. Delete Medical Warnings From System
. Returns F1=0 nothing changed
. Returns F1=1 changed - continue and send message
.------------------------------------------------------------
DELMED00  MOVE      ZERO,F1       * nothing changed yet
          PACK      KEY14,NHMAURNO,SP20
.
DELMED10  CALL      RSPTMWS1
          CALL      RKPTMWS1
          BRANCH    OVRCD,DELMED99
          MATCH     NHMAURNO,PTMWURNO
          GOTO      DELMED99 IF NOT EQUAL
          PACK      KEY14,PTMWURNO,PTMWCLSS,PTMWCNT
          CALL      DEPTMWS1
.
          MOVE      ONE,F1       * something has changed
          GOTO      DELMED10
.
DELMED99  RETURN
+
.------------------------------------------------------------
. Update master file if no medical warnings
.------------------------------------------------------------
.---Update master if all alerts for the patient are deleted---
CHKMED00  MOVE      URNUMBER,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,CHKMED99
.         
          MOVE      PTMXSIN1,DIM1            * No alerts
          MOVE      SP70,PTMXSIN1
.         
          PACK      KEY16,URNUMBER,SP70
          CALL      RSPTALR1
CHKMED20  CALL      RKPTALR1
          BRANCH    OVRCD,CHKMED25          * No alerts
.         
          MATCH     PTALURNO,URNUMBER       * Same ur
          GOTO      CHKMED25 IF NOT EQUAL   
.
          MATCH     SP70,PTALEDAT
          GOTO      CHKMED20 IF NOT EQUAL        * show alert as inactive
.         
          PACK      KEY5,PTALCATG,PTALCODE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CHKMED20
.
          REP       "S2 1",TCINDC4
.
          MATCH     "2",TCINDC4
          IF        @EQUAL
            PACK      PTMXSIN1,TCINDC4       * Yes security alert for this ur
            GOTO      CHKMED40
          ENDIF
.
          MATCH     PTMXSIN1,TCINDC4
          IF        @LESS
            PACK      PTMXSIN1,TCINDC4
          ENDIF
.
          MATCH     SP70,PTMXSIN1
          IF        @EQUAL
            PACK      PTMXSIN1,TCINDC4
          ENDIF
.
          GOTO      CHKMED20
.
CHKMED25  MATCH     SP70,PTMXSIN1
          GOTO      CHKMED40 IF NOT EQUAL
.
          PACK      KEY14,URNUMBER,SP70
          CALL      RSPTMWS1
          CALL      RKPTMWS1
          BRANCH    OVRCD,CHKMED40          * No alerts
.
          MATCH     PTMWURNO,URNUMBER       * Same ur
          GOTO      CHKMED40 IF NOT EQUAL
.
          MOVE      "1",PTMXSIN1            * Yes alers for this ur
.
CHKMED40  BRANCH    CAUDA1,CHKMED60         * Using alert audits
.
          MATCH     "0",PTMXSIN1            * Any current alerts
          IF        !@EQUAL
            MATCH     " ",PTMXSIN1          * Any current alerts
            GOTO      CHKMED60 IF NOT EQUAL
          ENDIF
.
          PACK      KEY27,URNUMBER,SP70
          CALL      ASPTALR2
CHKMED45  CALL      AKPTALR2
          BRANCH    OVRCD,CHKMED60
.
          MATCH     URNUMBER,PTALURNO
          GOTO      CHKMED60 IF NOT EQUAL
.
          MATCH     ANSD,PTALAUDR           * Delete
          GOTO      CHKMED45 IF NOT EQUAL
.
          MOVE      "3",PTMXSIN1            * Yes there are deleted alerts
.
CHKMED60  MATCH     PTMXSIN1,DIM1
          IF        !@EQUAL
            CALL      UPMAST1
          ENDIF
.
.
CHKMED99  RETURN
+
.************************************************************************
.*  GETMWS  :  Get list of Medical Warnings for a patient from Nat. Sys *
.************************************************************************
GETMWS    MOVE      "10",NZIBACTN                 * action identifier
          CALL      FIFHD000                      * set up header details
.
          MOVE      "62",WRLNGTH                  * FIFO record length (write)
          WRITE     NZWFIFA1;NZIBSTX,NZIBACTN,NZIBUSER,NZIBVDU,NZIBRFIF:
                             NZIBNMPI,NZIBCPTR,NZIBMWRP;
.
. now get the reply (firstly checking for an error)
.
          MOVE      "86",RDLNGTH
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBAPPC,NZIBERRN,NZIBERRD;
.
          MATCH     "AA",NZIBAPPC                 * valid read?
          GOTO      GTMWS900 IF NOT EQUAL         * error
.
          MOVE      "29",RDLNGTH
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBMWDN,NZIBMRGS,NZIBNMWF:
                             NZIBNMWR,NZIBCPTR;
.
          MOVE      ONE,NZIBCNT
          SQUEEZE   NZIBNMWR
          MOVE      NZIBNMWR,FORM2
          MOVE      "126",RDLNGTH
          WHILE     NZIBCNT <= FORM2
            READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZARSEVR[NZIBCNT]:
                               NZARDTON[NZIBCNT]:
                               NZARWTXT[NZIBCNT],NZARMARC[NZIBCNT]:
                               NZARWDTU[NZIBCNT],NZARFACC[NZIBCNT]:
                               NZARDOCC[NZIBCNT],NZARSYSI[NZIBCNT]:
                               NZARMWCD[NZIBCNT],NZARENTM[NZIBCNT];
.
            PACK      KEY120,NZARSEVR[NZIBCNT]:
                             NZARDTON[NZIBCNT]:
                             NZARWTXT[NZIBCNT],NZARMARC[NZIBCNT]:
                             NZARWDTU[NZIBCNT],NZARFACC[NZIBCNT]:
                             NZARDOCC[NZIBCNT],NZARSYSI[NZIBCNT]:
                             NZARMWCD[NZIBCNT],NZARENTM[NZIBCNT],SP70,SP70
.
            SCAN      "AE2034",KEY120
            IF        @EQUAL
              RESET     KEY120
              SCAN      "Unrecoverable",KEY120
              IF        @EQUAL
                MOVE      "2034",NZIBERRN
                MOVE      "Unrecoverable read error - GETMWS",NZIBERRD
                RESET     KEY120
                GOTO      GTMWS900
              ENDIF
            ENDIF
            RESET     KEY120
.
            ADD       ONE,NZIBCNT
          DO
.
GTMWS800  MOVE      ZERO,OVRCD                    * valid read
          GOTO      GTMWS999
.
GTMWS900  MOVE      ONE,OVRCD                     * an error was encountered
.
GTMWS999  RETURN
.
ENDSETMW  ENDPROC
.
.----------------------------------------------------------------------
. Procedure to Load Alias Details into the a file for modifications.
.----------------------------------------------------------------------
          DEFPROC   SETALI00
.
NZARSURN  DIM       25[15]   * Surname/Family name
NZARGIV1  DIM       20[15]   * Given Name 1
NZARGIV2  DIM       20[15]   * Given Name 2
NZARGIV3  DIM       20[15]   * Given Name 3
NZARPRNM  DIM       1[15]    * preferred name indicator
.
.------------------------------------------------------------
. Setup File for Alias
.------------------------------------------------------------
SETALI00  CALL     DELALI00     * Delete Records from Temp File
          MATCH    ANSY,NZIBALNM
          GOTO     SETALI90 IF NOT EQUAL
          MOVE     ZERO,TOTALALI
          MOVE     ZERO,F3
          MOVE     F3,TMALLIN
.
SETALI20  CALL     GETAKA
          BRANCH   OVRCD,SETALI90
.
          SQUEEZE  NZIBNARP
          MOVE     NZIBNARP,FORM3
          COMPARE  ZERO,FORM3
          GOTO     SETALI90 IF EQUAL
.
          CALL     ADDALI00
.
          SQUEEZE  NZIBNMRE
          MOVE     NZIBNMRE,FORM3
          COMPARE  ZERO,FORM3
          GOTO     SETALI20 IF NOT EQUAL
.
SETALI90  MOVE     ZERO,EXIT
          GOTO     SETALI99
.
SETALI95  MOVE     ONE,EXIT
          GOTO     SETALI99
.
SETALI99  GOTO     ENDSETAL
.------------------------------------
. Add Alias
.------------------------------------
ADDALI00  MOVE      ZERO,COUNT
          SQUEEZE   NZIBNARP
          MOVE      NZIBNARP,FORM3
          MOVE      TMALLIN,F3
.
ADDALI10  ADD       ONE,COUNT
          IF        COUNT>FORM3
            GOTO      ADDALI99
          ENDIF
          MOVE      NHMAURNO,TMALURNO           * U/R Number
.
ADDALI20  ADD       ONE,F3
          MOVE      F3,TMALLIN
          PACK      KEY11,TMALURNO,TMALLIN
          CALL      RDTMAL1
          COMPARE   ZERO,OVRCD
          GOTO      ADDALI20 IF EQUAL
          MOVE      NZARSURN[COUNT],TMALSUR
          MOVE      NZARGIV1[COUNT],TMALGV1
          MOVE      NZARGIV2[COUNT],TMALGV2
          MOVE      NZARGIV3[COUNT],TMALGV3
          MOVE      NZARPRNM[COUNT],TMALPRE
          CALL      WRTMAL1
          GOTO      ADDALI10
.
ADDALI99  RETURN
.------------------------------------------------------------
. Delete Alias From System
.------------------------------------------------------------
DELALI00  PACK      KEY11,NHMAURNO,SP20
DELALI10  CALL      RSTMAL1
          CALL      RKTMAL1
          BRANCH    OVRCD,DELALI99
          MATCH     NHMAURNO,TMALURNO
          GOTO      DELALI99 IF NOT EQUAL
          PACK      KEY11,TMALURNO,TMALLIN
          CALL      DETMAL1
          GOTO      DELALI10
.
DELALI99  RETURN
.------------------------------------------------------------------------
. GETAKA  :  Get a list of aliases for a NHI Number
.------------------------------------------------------------------------
GETAKA    MOVE      "9 ",NZIBACTN                 * action identifier
          CALL      FIFHD000                      * set up header details
.
          MOVE      "62",WRLNGTH
          WRITE     NZWFIFA1;NZIBSTX,NZIBACTN,NZIBUSER,NZIBVDU,NZIBRFIF:
                             NZIBNMPI,NZIBCPTR,NZIBMXRP;
.
. now get the reply (firstly checking for an error)
.
          MOVE      "86",RDLNGTH
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBAPPC,NZIBERRN,NZIBERRD;
.
          MATCH     "AA",NZIBAPPC               * valid read?
          GOTO      AKHCU900 IF NOT EQUAL         * error
.
          MOVE      "30",RDLNGTH
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBNMFN,NZIBNMRE,NZIBNARP:
                             NZIBCPTR;
.
          MOVE      "86",RDLNGTH
          MOVE      ONE,NZIBCNT
          SQUEEZE   NZIBNARP
          MOVE      NZIBNARP,FORM3
          WHILE     NZIBCNT <= FORM3
            READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZARSURN[NZIBCNT]:
                               NZARGIV1[NZIBCNT]:
                               NZARGIV2[NZIBCNT],NZARGIV3[NZIBCNT]:
                               NZARPRNM[NZIBCNT];
            ADD       ONE,NZIBCNT
          DO
.
          MOVE      ZERO,OVRCD                    * valid read
          GOTO      AKHCU999
.
AKHCU900  MOVE      ONE,OVRCD                     * an error was encountered
.
AKHCU999  RETURN
.
ENDSETAL  ENDPROC
.
