.------------------------------------------------------------
. Function          : Procedure for Seach of 
.                      Fill in description of Search
.
. Parameters        : CKEYSUSP    1=Show Suspended 
.                   : CKEYORDR    0=Key Word Search, 1=Alphabetic Search
.                      Fill in Other Parameters Required
.
. Includes Required :
.                      Fill in Includes Required
. Open Tables Req   :
.                      Fill in Tables that Must be Open
. Returns Fields    : OVRCD    0=Valid Record Selected, 1=No Record Selected
.                     Record Read from xxxxxxx Table
.                      Fill in Other Return Values
.
. Modifications :
. V11.00.01 04/03/2020  J.Tan          TSK 0838262
.           Added Effective from and to date to MBS Item file
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Routine to Seach for
.  a coded field via either a Key Word Index or Alphabetical
.  Index and output a ? Selection List.
.  
.  A Key Word Index Table is Defined in the following Manner
.
.  patikiaf
.  PATIKIA1
.  PATIKIA2    
.  
.  PTIKITM     DIM       xx    * Code Field 
.  PTIKKWD     DIM       15    * Key Word
.
.  Index 1   PTIKITM, PTIKKWD
.  Index 2   PTIKKWD, PTIKxxxx
.
.To convert this Procedure the following should be performed in this sequence
.    Global Change patitemn to name of Alphabetic Search Table   eg patdo1af
.    Global Change PATITEM3 to index of Alphabetic Search Table  eg PATDO1A2
.    Global Change RDITEM3  to the Read on Alpha Search Index    eg RDDOCT2
.    Global Change RDKITEM3 to the Read Next Alpha Search Index  eg RDKDOCT2
.    Global Change RDPITEM3 to the Read Prev Alpha Search Index  eg RDPDOCT2
.    Global Change RDSITEM3 to the Read Post Alpha Search Index  eg RDSDOCT2
.    Global Change ITEMNO   to the Code Field from the Table     eg DCODE
.    Global Change IDESC    to the Name Field from the Table     eg DSNAME
.    Global Change KEY57    to the Key for the Aplha Index       eg KEY26
.    Global Change patikiaf to name of Key Word Table            eg patdkiaf
.    Global Change PATIKIA1 to index 1 of Key Word Table         eg PATDKIA1
.    Global Change PATIKIA2 to index 2 of Key Word Table         eg PATDKIA2
.    Global Change PTIKITM  to Key Word Index Code Field         eg PTDKDOC
.    Global Change PTIKKWD  to Key Word Index Word Field         eg PTDKKWD
.    Global Change HPTIKA   to Routine Label                     eg HPTDKA00
.    Global Change KEY24    to the Key needed for Key Word Table eg KEY21
.    Global Change KEY9     to the Key for the Code Field        eg KEY6
.    Global Change RDITEM1  to the Read on Master Table          eg RDDOCT1
.    Global Change PTIK     to the IO Call ID for the Key Words  eg PTDK
.    Global Change  PTITACTV to the field to Indicate Inactive Code eg DRSTAT
.
.    Modify List Display to Suit
.
.    Note : vi Global change command   :%s/FromString/ToString/g
.  
.  A Alphabetic Index on a Master Table is Defined in the following Manner
.                              
.  ITEMNO    DIM       xx    * Code Field 
.  IDESC     DIM       xx    * Name or Description Field
.
.------------------------------------------------------------
           DEFPROC   HPTIKA00
.
           INC       STDHLPDF
           INC       STDKWSDF
           INC       PATIKIFD/INC
.
SAVKEY24  DIM       24       * Key Word Table Index 
SAVKEY9   DIM       9        * Code Length
CATEGORY  DIM       3
.
.------------------------------------------------------------
. Search 
.------------------------------------------------------------
HPTIKA00  MOVE      CKEYSUSP,SHOWSUSP
          MOVE      CKEYORDR,SEARCHTY
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATIKIA1,"patikiaf"
          TRAPCLR   IO
          IF        OVRCD=1
            BEEP
            MOVE      "ERROR: Search Table Missing (patikiaf)- ",DISPMSG
            CALL      MESSAGE1
            GOTO      HPTIKAZZ
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATITEM3,"patitemn"
          TRAPCLR   IO
          IF        OVRCD=1
            BEEP
            MOVE      "ERROR: Search Table Missing (patitemn)- ",DISPMSG
            CALL      MESSAGE1
            GOTO      HPTIKAZZ
          ENDIF
.
          OPEN      PATIKIA2,"patikiaf"
          MOVE      ZERO,CSCRSIZ
.
          MOVE      "08",HTOP
          MOVE      "24",HBOT
          MOVE      "01",HLEF
          MOVE      "80",HRIG
.
HPTIKA10  CALL      KWINT000
          BRANCH    EXIT,HPTIKA99
          MOVE      STDCOL00,STDCOL01
          ADD       " 5",STDCOL01
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON,"MBS Item"        
          MOVE      STDCOL01,STDCOL02
          ADD       "10",STDCOL02
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"Description"
          MOVE      STDCOL02,STDCOL03
          ADD       "41",STDCOL03
.
.        Change "Desciption" and "Code" to the correct names
.        Add any addition columns.
.
          CALL      SHWFLG00                * Display Flags
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
HPTIKA20  MOVE      HTOP,MINITEM
          ADD       ONE,MINITEM
          MOVE      MINITEM,STDROW
          IF        SHMGORSC=1  
            DISPLAY   *PSTDCOL03:STDROW,*V2LON,*ULON,"Misc. Group    ",*HOFF
          ELSE   
            DISPLAY   *PSTDCOL03:STDROW,*V2LON,*ULON,"Suggested Class",*HOFF
          ENDIF
.
          IF        SEARCHTY=1
            CALL      ALPSRH00              * Alphabetic Search
          ELSE
            CALL      KEYSRH00              * Key Word Search
          ENDIF
          BRANCH    DOAGAIN,HPTIKA10,HPTIKA20
.
          PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
.
HPTIKA99  CLOSE     PATIKIA1
          CLOSE     PATIKIA2
          GOTO      HPTIKAZZ           * End Procedure
.------------------------------------------------------------
. Perform Key Word Search
.------------------------------------------------------------
KEYSRH00  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    STDROW+1,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-13,FLAGCOL      
          DISPLAY   *PFLAGCOL:FLAGROW,*V2LON,*ULON,"Enter Key Words for Search"
          ASSIGN    STDROW+3,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-30,FLAGCOL
          KEYIN     *PFLAGCOL:FLAGROW,*V2LON,KEYWRD00
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ENDSET    KEYWRD00
          APPEND    SP70,KEYWRD00
          RESET     KEYWRD00
          CALL      GETWRD00     * Break Words into Seprate Fields
.
KEYSRH09  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          MOVE      SP70,PTIKITM 
.
          STRIP     KEYWRD01
          PACK      KEY24,KEYWRD01,SP70
          CALL      RSPTIK2 
KEYSRH10  CALL      RKPTIK2 
          BRANCH    OVRCD,KEYSRH15
.
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH10,KEYSRH15
.
          ADD       ONE,SELITMNO
          ADD       ONE,STDROW
          PACK      KEY24,PTIKKWD,PTIKITM
          STORE     KEY24,SELITMNO,ST1,ST2,ST3,ST4,ST5:                           
                                 ST6,ST7,ST8,ST9,ST10:
                                 ST11,ST12,ST13,ST14,ST15:
                                 ST16,ST17
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,SELITMNO,DOT;
          IF        CHECKSUS<>0
            DISPLAY   *V2LON,*BLINKON,"*"
          ENDIF
          DISPLAY   *PSTDCOL01:STDROW,PTIKITM
          MOVE      IDESC,KEY40
          DISPLAY   *PSTDCOL02:STDROW,KEY40
.
          IF        SHMGORSC<>0
            MOVE      "FI",CATEGORY
            MOVE      IMISGRP,KEY3
          ELSE
            MOVE      "A ",CATEGORY
            MOVE      ICLSS,KEY3
          ENDIF
.
          CALL      GETCOD00
          DISPLAY   *PSTDCOL03:STDROW,TDESC
.
          COMPARE   MAXITEM,SELITMNO
          GOTO      KEYSRH10 IF NOT EQUAL
.
KEYSRH15  MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
.
KEYSRH20  CALL      KWSEL000
          BRANCH    EXIT,KEYSRH30,KEYSRH35,KEYSRH40,KEYSRH45,KEYSRH98:
                                  KEYSRH91,KEYSRH00,KEYSRH70
.                        Next     Last     Home     End      Expand/Reduce
.                                 Exit     Search   Flags
          GOTO      KEYSRH92
.
KEYSRH30  CALL      RKPTIK2 
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH30,KEYSRH85
.
          PACK      KEY24,PTIKKWD,PTIKITM,SP70                                  
          CALL      RPPTIK2 
          MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      KEYSRH10
.
KEYSRH35  MOVE      ST1,KEY24
          CALL      RDPTIK2 
.
KEYSRH36  CALL      RPPTIK2 
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH36,KEYSRH85
          PACK      KEY24,PTIKKWD,PTIKITM,SP70                                  
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
.
KEYSRH37  CALL      RPPTIK2 
          BRANCH    OVRCD,KEYSRH38
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH37,KEYSRH38
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      KEYSRH37 IF NOT EQUAL
.
KEYSRH38  MOVE      ZERO,SELITMNO
          GOTO      KEYSRH10
.
KEYSRH40  MOVE      ST1,KEY24
          CALL      RDPTIK2 
KEYSRH41  CALL      RPPTIK2 
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH41,KEYSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      KEYSRH09
.
KEYSRH45  CALL      RKPTIK2 
          BRANCH    OVRCD,KEYSRH85
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH45,KEYSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,SELITMNO
          PACK      KEY24,KEYWRD01,Z70                                          
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
          CALL      RSPTIK2 
.
KEYSRH46  CALL      RPPTIK2 
          BRANCH    OVRCD,KEYSRH47
          CALL      CHREC000
          BRANCH    OVRCD,KEYSRH46,KEYSRH47
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      KEYSRH46 IF NOT EQUAL
.
KEYSRH47  MOVE      ZERO,SELITMNO
          GOTO      KEYSRH10
.
KEYSRH70  CALL      FLAGS000
          MOVE      TWO,DOAGAIN
          GOTO      KEYSRH99
.
KEYSRH85  LOAD      KEY24,SELITMNO,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY24,PTIKKWD,PTIKITM
          CALL      RDPTIK2 
.
KEYSRH90  BEEP
          GOTO      KEYSRH20
.
KEYSRH91  MOVE      SP70,KEY24
          MOVE      SP70,PTIKITM 
          MOVE      SP70,PTIKKWD 
          MOVE      ONE,OVRCD
          GOTO      KEYSRH96
.
KEYSRH92  MOVE      SP70,KEY24
          MOVE      SP70,PTIKITM 
          LOAD      KEY24,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY24,PTIKKWD,PTIKITM
          PACK      KEY17,PTIKITM,SP70
          CALL      PATITMRD
.
KEYSRH96  MOVE      ZERO,DOAGAIN
          GOTO      KEYSRH99
.
KEYSRH98  MOVE      ONE,DOAGAIN
.
KEYSRH99  RETURN
.------------------------------------------------------------
. Perform Alphabetic Search
.------------------------------------------------------------
ALPSRH00  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    STDROW+1,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-10,FLAGCOL
          DISPLAY   *PFLAGCOL:FLAGROW,*V2LON,*ULON,"Enter Name for Search"
          ASSIGN    STDROW+3,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-30,FLAGCOL
          KEYIN     *PFLAGCOL:FLAGROW,*V2LON,KEYWRD00
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ENDSET    KEYWRD00
          APPEND    SP70,KEYWRD00
          RESET     KEYWRD00
.
ALPSRH09  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          MOVE      SP70,ITEMNO 
.
          STRIP     KEYWRD00
          PACK      KEY57,KEYWRD00,SP70
          CALL      RDSITEM3 
ALPSRH10  CALL      RDKITEM3 
          BRANCH    OVRCD,ALPSRH15
.
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH10,ALPSRH15
.
          ADD       ONE,SELITMNO
          ADD       ONE,STDROW
          PACK      KEY57,IDESC,ITEMNO,PTITEFDT,SP70
          STORE     KEY57,SELITMNO,ST1,ST2,ST3,ST4,ST5:                         
                                 ST6,ST7,ST8,ST9,ST10:
                                 ST11,ST12,ST13,ST14,ST15:
                                 ST16,ST17
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,SELITMNO,DOT;
          IF        CHECKSUS<>0
            DISPLAY   *V2LON,*BLINKON,"*"
          ENDIF
          DISPLAY   *PSTDCOL01:STDROW,PTIKITM
          MOVE      IDESC,KEY40
          DISPLAY   *PSTDCOL02:STDROW,KEY40
.
          IF        SHMGORSC<>0
            MOVE      "FI",CATEGORY
            MOVE      IMISGRP,KEY3
          ELSE
            MOVE      "A ",CATEGORY
            MOVE      ICLSS,KEY3
          ENDIF
.
          CALL      GETCOD00
          DISPLAY   *PSTDCOL03:STDROW,TDESC
.       
          COMPARE   MAXITEM,SELITMNO
          GOTO      ALPSRH10 IF NOT EQUAL
.
ALPSRH15  MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
.
ALPSRH20  CALL      KWSEL000
          BRANCH    EXIT,ALPSRH30,ALPSRH35,ALPSRH40,ALPSRH45,ALPSRH98:
                                  ALPSRH91,ALPSRH00,ALPSRH70
.                        Next     Last     Home     End      Expand/Reduce
.                                 Exit     Search   Flags
          GOTO      ALPSRH92
.
ALPSRH30  CALL      RDKITEM3 
          BRANCH    OVRCD,ALPSRH85
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH30,ALPSRH85
.
          PACK      KEY57,IDESC,ITEMNO,PTITEFDT,SP70
          CALL      RDPITEM3 
          MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      ALPSRH10
.
ALPSRH35  PACK      KEY57,ST1,SP70
          CALL      RDITEM3 
.
ALPSRH36  CALL      RDPITEM3 
          BRANCH    OVRCD,ALPSRH85
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH36,ALPSRH85
          PACK      KEY57,IDESC,ITEMNO,PTITEFDT,SP70
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
.
ALPSRH37  CALL      RDPITEM3 
          BRANCH    OVRCD,ALPSRH38
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH37,ALPSRH38
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      ALPSRH37 IF NOT EQUAL
.
ALPSRH38  MOVE      ZERO,SELITMNO
          GOTO      ALPSRH10
.
ALPSRH40  MOVE      ST1,KEY57
          CALL      RDITEM3 
ALPSRH41  CALL      RDPITEM3 
          BRANCH    OVRCD,ALPSRH85
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH41,ALPSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      ALPSRH09
.
ALPSRH45  CALL      RDKITEM3 
          BRANCH    OVRCD,ALPSRH85
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH45,ALPSRH85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,SELITMNO
          PACK      KEY57,KEYWRD01,Z70                                          
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
          CALL      RDSITEM3 
.
ALPSRH46  CALL      RDPITEM3 
          BRANCH    OVRCD,ALPSRH47
          CALL      CHREC000
          BRANCH    OVRCD,ALPSRH46,ALPSRH47
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      ALPSRH46 IF NOT EQUAL
.
ALPSRH47  MOVE      ZERO,SELITMNO
          GOTO      ALPSRH10
.
ALPSRH70  CALL      FLAGS000
          MOVE      TWO,DOAGAIN
          GOTO      ALPSRH99
.
ALPSRH85  LOAD      KEY57,SELITMNO,ST1,ST2,ST3,ST4,ST5: 
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY57,IDESC,ITEMNO,PTITEFDT                             
          CALL      RDITEM3 
.
ALPSRH90  BEEP
          GOTO      ALPSRH20
.
ALPSRH91  MOVE      SP70,ITEMNO 
          MOVE      SP70,KEY57
          MOVE      SP70,IDESC
          MOVE      ONE,OVRCD
          GOTO      ALPSRH96
.
ALPSRH92  MOVE      SP70,KEY57
          MOVE      SP70,ITEMNO 
          LOAD      KEY57,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY57,IDESC,ITEMNO,PTITEFDT
          PACK      KEY17,ITEMNO,PTITEFDT,SP70
          CALL      PATITMRD
.
ALPSRH96  MOVE      ZERO,DOAGAIN
          GOTO      ALPSRH99
.
ALPSRH98  MOVE      ONE,DOAGAIN
.
ALPSRH99  RETURN
.------------------------------------------------------------
. Alter Flags
.------------------------------------------------------------
FLAGS000  BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    MINITEM+2,F2
FLAGS110  MOVE      "N",ANS
          KEYIN     *PSTDCOL02:F2,"Show Inactive (",*V2LON,"Y",*HOFF,"/":
                                  *V2LON,"N",*HOFF,")    : ",*V2LON,*RV,ANS
          REP       UPPLOW,ANS
          MOVE      ONE,SHOWSUSP
          MATCH     "Y",ANS
          GOTO      FLAGS190 IF EQUAL
          MATCH     "N",ANS
          GOTO      FLAGS110 IF NOT EQUAL
          MOVE      ZERO,SHOWSUSP
FLAGS190  ASSIGN    MINITEM+3,F2
FLAGS210  MOVE      "K",ANS
          KEYIN     *PSTDCOL02:F2,*V2LON,"K",*HOFF,"ey Word or ":
                                  *V2LON,"A",*HOFF,"lphabetic Search : ":
                                  *V2LON,*RV,ANS
          REP       UPPLOW,ANS
          MOVE      ZERO,SEARCHTY
          MATCH     "K",ANS
          GOTO      FLAGS290 IF EQUAL
          MATCH     "A",ANS
          GOTO      FLAGS210 IF NOT EQUAL
          MOVE      ONE,SEARCHTY
FLAGS290  ASSIGN    MINITEM+4,F2
FLAGS300  MOVE      "S",ANS
          KEYIN     *PSTDCOL02:F2,"Show ",*V2LON,"S",*HOFF,"uggested Class or ":
                                  *V2LON,"M",*HOFF,"isc. Group : ":
                                  *V2LON,*RV,ANS
          REP       UPPLOW,ANS
          MOVE      ZERO,SHMGORSC
          MATCH     "S",ANS
          GOTO      FLAGS900 IF EQUAL
          MATCH     "M",ANS
          GOTO      FLAGS300 IF NOT EQUAL
          MOVE      ONE,SHMGORSC
          
FLAGS900  CALL      SHWFLG00 
          RETURN 
.------------------------------------------------------------
. Display Flag Settings
.------------------------------------------------------------
SHWFLG00  CLEAR     FLAGDESC
          IF        SHOWSUSP=1
            APPEND    "Show Inactive, ",FLAGDESC
          ELSE
            APPEND    "Hide Inactive, ",FLAGDESC
          ENDIF
.
          IF        SEARCHTY=1
            APPEND    "Alphabetic Search, ",FLAGDESC
          ELSE
            APPEND    "Key Word Search, ",FLAGDESC
          ENDIF
.
          IF        SHMGORSC=1
            APPEND    "Show Misc. Group",FLAGDESC
          ELSE
            APPEND    "Show Suggested Class",FLAGDESC
          ENDIF
          RESET     FLAGDESC
.
          ADD       ONE,HLEF
          SUB       ONE,HRIG
          HLINE     *G37,HBOT,HLEF,HRIG
          SUB       ONE,HLEF
          ADD       ONE,HRIG
          ASSIGN    HBOT,FLAGROW
          MOVELPTR  FLAGDESC,F2
          ADD       "9",F2
          ASSIGN    ((HRIG-HLEF)/2)-(F2/2),FLAGCOL
          MOVELPTR  FLAGDESC,F2
          SFORMAT   DISPVAR,F2
          MOVE      FLAGDESC,DISPVAR
          DISPLAY   *PFLAGCOL:FLAGROW,*V2LON,*ULON," Flags: ",DISPVAR,SP1
          SFORMAT   DISPVAR,127
.
          RETURN
.------------------------------------------------------------
. Check Record
.------------------------------------------------------------
CHREC000  BRANCH    SEARCHTY,CHREC100
          MATCH     KEYWRD01,PTIKKWD
          GOTO      CHREC920 IF NOT EQUAL                                       
          IF        KEYWRDNO>1
            PACK      SAVKEY24,PTIKKWD,PTIKITM
            CALL      MCHWRD00
            BRANCH    EXIT,CHREC910
            MOVE      SAVKEY24,KEY24
            CALL      RDPTIK2
          ENDIF
.
          PACK      KEY17,PTIKITM,SP70
          CALL      PATITMRD
          BRANCH    OVRCD,CHREC910
          GOTO      CHREC800
.
CHREC100  MATCH     KEYWRD00,IDESC
          GOTO      CHREC920 IF NOT EQUAL                                       
.
.    check following code for inactive indicator from master table

CHREC800  
..        MATCH     "I",PTITACTV
..          IF        @EQUAL
..            MOVE      ONE,CHECKSUS
..          ELSE
          MOVE      ZERO,CHECKSUS           * assume PTITACTV active
..          ENDIF
          BRANCH    SHOWSUSP,CHREC900

..          MATCH     "I",PTITACTV
..          GOTO      CHREC910 IF EQUAL

.
CHREC900  MOVE      ZERO,OVRCD
          GOTO      CHREC999
CHREC910  MOVE      ONE,OVRCD
          GOTO      CHREC999
CHREC920  MOVE      TWO,OVRCD
CHREC999  RETURN
.------------------------------------------------------------
. Check for Matching Words
.------------------------------------------------------------
MCHWRD00  PACK      SAVKEY9,PTIKITM
          MOVE      ONE,F2
          MOVE      ZERO,EXIT
.
MCHWRD10  COMPARE   KEYWRDNO,F2
          GOTO      MCHWRD99 IF EQUAL
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY24,SAVKEY9,KEYWRDXX
          CALL      RDPTIK1
          COMPARE   ZERO,OVRCD
          GOTO      MCHWRD10 IF EQUAL
.
          CALL      RKPTIK1
          BRANCH    OVRCD,MCHWRD90
          PACK      KEY9,PTIKITM,SP70
          MATCH     SAVKEY9,KEY9 
          GOTO      MCHWRD90 IF NOT EQUAL
.
          STRIP     KEYWRDXX
          MATCH     PTIKKWD,KEYWRDXX
          GOTO      MCHWRD90 IF NOT EQUAL
          GOTO      MCHWRD10
.
MCHWRD90  MOVE      ONE,EXIT
MCHWRD99  RETURN
.
.------------------------------------------------------------
. Get Code Description
.------------------------------------------------------------
GETCOD00  MOVE      SP70,TDESC
          MATCH     SP70,KEY3
          GOTO      GETCOD99 IF EQUAL
          PACK      KEY5,CATEGORY,KEY3
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      SP70,TDESC
          ENDIF
GETCOD99  RETURN
.
          INC       PATITMRD
          INC       PATIKIIO/INC
          INC       IBAOVRIO/INC
          INC       STDKWSCD
.
HPTIKAZZ  ENDPROC   
