.FEESESTM.PBL
.         Function : Routine to calculate Fees Estimation with Multiple 
.                    bed types and Length of stays
.
.         GFEH0000 - Generate IFC for SJOG,HEA and Cabrini (PATWEB78)
.                  - Generate Fees Estimate for SJOG (PATWEB76)
.                    (SJOG Fees Estimate has same layout as their IFC
.
.         GFES0000 - Generate Fees Estimate for HEA,Cabrini,Standard (PATWEB76)
.
.         SJOG and HEA templates have 'Basic/Default Cover' rate - USEBASIC
.         SJOG Fees Estimate & IFC have same layout (IFC - PMSFESFD)
._______________________________________________________________________________
.
.         Modification:
.
.  V12.00.01 14/05/2025  Ebon Clements  TSK 0955096
.            Alphanueric visit number changes
.  V11.05.01 19/12/2024 J.Tan           TSK 0955163
.            Added a routine for checking DVA concession card (dvacolor)
.  V11.04.07 28/10/2024 Alina Bhari     TSK 0948172
.            Calculated bed fee from no of day instead of expected length of
.            stay when informed financial consent form layout parameter is set
.            to formate 2(SJGHC)-FESC0000
.  V11.04.06 23/10/2024 J.Tan           TSK 0942917
.            Mod GSCL0000 - not to initialise ATYPE for Using Basic cover
.  V11.04.05 15/10/2024 J.Tan           TSK 0951331
.            Added Misc.item field to HEA IFC Eligibility Check page
.  V11.04.04 20/09/2024 J.Tan           TSK 0942917
.            Fixed SJOG Accommodation Fees Estimate
.  V11.04.03 21/08/2024 J.Tan           TSK 0942917
.            Fixed SJOG IFC MBS items from Booking
.  V11.04.02 16/05/2024  J.Tan     TSK 0942917
.            Mod SJOG IFC - Added GST Accommodaton and save items to pmseliaf
.  V11.04.01 08/03/2024  J.Tan     TSK 0919335
.            Mod checking for HF History file
.  V11.04.02 22/11/2023 J.Tan    TSK 0939335
.            Mod validating misc.item to check for date (GMCHRG00)
.  V11.04.01 21/11/2023 J.Tan    TSK 0940129
.            Mod SJOG to update suggested Admission type of the highest MBS
.  V11.03.06 09/08/2023 J.Tan    TSK 0935667
.            Added TRAP prior writing to pmsfesaf file
.  V11.03.05 16/05/2023 J.Tan    TSK 0926482
.            Fixed HEA perdiem using basic cover
.  V11.03.04 08/03/2023 J.Tan    TSK 0926482
.            Zero HF amount for HEA Casepayment Fees Estimate with Basic cover
.            08/03/2023 J.Tan    TSK 0929361
.            Mod check ovrcd=1 before write to pmsfedaf
.  V11.03.03 12/12/2022 J.Tan    TSK 0926482
.            Fixed HEA Fees Estimate Use basic cover rate
.  V11.03,02 09.12.2022 DA Sarkies  TSK 0927204
.            Inserted Escapes to prevent possible I*D errors
.  V11.03.01 06/12/2022 J.Tan    TSK 0926482
.            Fixed HEA Fees Estimate Use basic cover rate
.  V11.02.05 05/09/2022 J.Tan    TSK 0924383
.            Mod GFEH1000 to read admission file
.  V11.02.04 05/09/2022 J.Tan    TSK 0924085
.            Mod WRFS0000 to set SJOG GST Accommodation for private bedtype
.  V11.02.03 13/07/2022 J.Tan    TSK 0900069
.            Mod HEA Fees Estimate to use Default/Basic Cover Rate
.  V11.02.02 07/07/2022 J.Tan    TSK 0921542
.            Fixed Keyin amount for Misc.item (for HEA)
.  V11.02.01 05/07/2022 J.Tan    TSK 0921542
.            Fixed I*D on pmsfesaf
.            05/07/2022 J.Tan    TSK 0921364
.            Fixed removing tempfile CRET0000
.  V11.01.07 04/04/2022 J.Tan    TSK 0910723
.            Mod checking for zero of MISAMT (SJOG)
.  V11.02.06 11/02/2022 J.Tan    TSK 0910723
.            Mod for SJOG IFC
.  V11.01.05 14/10/2021  J.Tan          Task 0910723
.            Added UPDATEST displaying items (Preview Form for SJOG Eligibility)
.  V11.01.04 07/09/2021  J.Tan          Task 0910723
.            Fixed Bedtype for USEBASIC where there is NO Private/Shared Add on.
.  V11.01.03 07/09/2021  J.Tan          Task 0910723
.            Mod CASP6000 to check for USEBASIC where there is NO Private/Shared
.            charges setup
.  V11.01.02 19/08/2021  J.Tan          Task 0910460
.            Fixed GCSOV00 where there is No Daily and Additional charges setup
.  V11.01.01 01/07/2021  J.Tan          Task 0885959
.            Mod for Fees Estimate Standard format
.  V11.00.02 11/02/2021  Jill Parkinson Task 0892480
.            Added read of cat BP for hea sameday to use private or shared
.  V11.00.01 03/03/2020  J.Tan          TSK 0838262
.            Added Effective from and to date to MBS Item file
.  V10.15.01 20/01/2020  J.Tan          TSK 0026834
.            Mod for SJOG Fees Estimate ASTAY to use Casepayment LOS
.  V10.14.01 15/11/2019  J.Tan          TSK 0826834
.            Mod for SJOG IFC/Fees Estimate
.  V10.11.01 20/12/2017  J.Tan          TSK 0844301
.            Added Extra MBS and Misc.Items to Fees Estimates
.  V10.09.01 05/01/2017  J.Tan        Task 0830638
.            Mods GCSOV000 - added one day to FORM5A to determine Low Outlier
.  V10.08.05 03/05/2016  J.Tan        Task 0313842
.            Fixed Fees Estimate to check for default rates for additional rate
.  V10.08.04 03/05/2016  J.Tan        Task 0313842
.            Fixed Fees Estimate to check for default rates
.  V10.08.03 29/04/2016  J.Tan        Task 0313842
.            Fixed default basic cover rate
.  V10.08.02 13/04/2016  J.Tan        Task 0313842
.            Added Expected casemix LOS to SJOG IFC/Fees Estimate
.  V10.08.01 21/03/2016  J.Tan        Task 0213842
.            Fixed SJOG IFC Using Basic cover default rates
.  V10.07.03 23/02/2016  J.Tan        CAR 0814431
.            Fixed HEA IFC for Daycase sugg.Class NOT to use patsgcaf
.  V10.07.02 11/02/2016  J.Tan        CAR 0323465
.            Fixed HEA IFC for EPM based contracts
.  V10.07.01 04/01/2016  J.Tan        CAR 0313842
.            Mods for SJGHC IFC
.  V10.06.04 20/08/2015  J.Tan        CAR 316980
.            Fixed HEA IFC for Daycase adm.type Suggested class.
.  V10.06.03 11/08/2015  J.Tan        CAR 316980
.            Fixed HEA IFC for Daycase Theatre fees (TFES0000)
.  V10.06.02 09/07/2015  J.Tan        CAR 316980
.            Mods changes for HEA IFC
.  V10.06.01 30/04/2015  J.Tan        CAR 314005
.            Mods further update for HEA IFC 
.  V10.05.04 11/02/2015  J.Tan        CAR 310816
.            Mods to HEA IFC Theatre for Casemix=Y but No Casemixcode to
.            display Perdiem theatre fees
.  V10.05.03 01/10/2014  J.Tan        CAR 253233
.            Mods to print/display bed type with TCFORM6=1 only
.  V10.05.02 23/09/2014  J.Tan        CAR 253233
.            Mods to use PTCDNHCP=2 for Shared bed type
.  V10.05.01 12/09/2014  J.Tan        CAR 253233
.            Added Healthscope layout
.  V10.04.01 29/05/2014  J.Tan        CAR 301639
.            Mods to erase tempfile at end of processing
.  V10.04.01 29/05/2014  J.Tan        CAR 301639
.            Mods to erase tempfile at end of processing
.  V10.03.04 06/11/2013  J.Tan        CAR 251004
.            Fixed updating Adm.type(pmfdatyp) for Suggested classification
.  V10.03.03 25/07/2013  J.Tan        CAR 251004
.            Added PMSFD029 - Adm.type used for Accommodation Charge
.  V10.03.02 18/05/2012  J.Tan        CAR 250988
.            Fixed calculating Lumpsum GST amount
.  V10.03.01 24/04/2012  J.Tan        LanDesk 500543
.            Added Other Fees
.  V10.01.02 14/12/2010  Mike Laming  CAR 233046
.            Mods to "patmchgf" - replaced read logic with PATMCHRD
.            13/01/2011  J.Tan        CAR 233048
.            Mods Theatre fees file to use health fund table type
.  V10.01.01 01/12/2010  J.Tan        CAR 233034
.            Mods bed fees file to use health fund table type
.  V10.00.03 19/08/2010  Davin        CAR 228437
.            Fixed casemix funding to check for defaults
.  V10.00.02 10/08/2010  Davin        CAR 221046
.            If misc charge amount is zero, check for keyed in amount from
.            screen (cmsc0000)
.  V10.00.01 30/04/2010 J.Tan         CAR 220887
.            Mods checking for Active/Inactive of Misc.charge
.  V9.12.02  08/10/2009  Davin         CAR 206358
.            Remove 'Miscellaneous' (pmfimisc) from total payable on admission
.  V9.12.01  03/09/2009  J.Tan CAR 205303
.            Added Casemix Contract ID
.  V9.11.01  14/04/2009  Davin         CAR 182901
.            Remove Assistant Gap Amount (pmfiagam) from total payable on
.            admission (pmfitpoa)
.            Add 'Miscellaneous' (pmfimisc) to total payable on admission
.            Add 'Physiotherapy Subsequent Visits' (pmfiphys) to Expected
.            Cost of Surgery (pmfiecos)
.  V9.10.07  02/10/2008  Davin         CAR 179991
.            Don't include splints/brace field (pmfisplb) in total payable on
.            admission (pmfitpoa)
.  V9.10.06  22/09/2008  Davin         CAR 177361
.            Mods to set daycase flag (dcflag) if LOS=0 for fees information
.  V9.10.05  18/08/2008  J.Tan         CAR 176322
.            Mods to initialise FORM2 in GBFE0000 (Get bed type for a bedtype)
.  V9.10.04  18/08/2008  Ebon Clements CAR 174917
.            Add assistant gap amount to amount payable on admission
.            for uninsured patients - fees information - WFINF000
.            Add educational DVD to amount payable on admission - WFINF000
.  V9.10.03  03/07/2008  Ebon Clements CAR 160213
.            Added update functionality to Cabrini Fees Estimation
.  V9.10.02  15/05/2008  Jill Habasque CAR 161873
.            Added pmfiphca and pmfiedva to pmfiecos
.  V9.10.01  14/05/2008  J.Tan         CAR 164036
.            Fixed getting bed fees for daycase
.  V9.09.03  15/04/2008  J.Tan         CAR 157585
.            Mods to set zero bed fees if no bed fees setup
.            15/04/2008  J.Tan         CAR 164036
.            Mods to print others bed types with Assc.no=1
.  V9.09.02  04/02/2008  Davin         CAR 153384
.            Removed call to print fees info (prtfin00) - moved to patweb76
.  V9.09.01  14/01/2008  Davin         CAR 153384
.            Added fees information functionality for SportsMedSA (gfinf000)
.
.------------------------------------------------------------
.         Generate Fees Estimate
.------------------------------------------------------------
GFES0000  IF        UPDATEST=1
            CALL      DFES0000                   * Delete existing estimate    
            GOTO      GFES1000                   * before recalculation
          ENDIF
.
          MOVE      " 95",PRXCODE                * set sector for locking
          CALL      GETSLK00                     * lock sector
          READ      CONTROLF,NINTY5;*153,PTCNQUOT
          ADD       ONE,PTCNQUOT
          WRITAB    CONTROLF,NINTY5;*153,PTCNQUOT
          CALL      RELSLK00                     * release lock
.
          SUB       ONE,PTCNQUOT          * get quote number
.
GFES1000  OPEN      IBAGEDA1,"ibagedaf"
          OPEN      PATCTFA2,"patctfaf"
          OPEN      PATLSDA1,"patlsdaf"
          OPEN      PATASDA1,"patasdaf"
          OPEN      PATBFEE1,"patbfeef"
          OPEN      PMSFENA1,"pmsfenaf"
          READ      CONTROLF,TWENTY;*193,PTCNDCLM
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          IF        IBCNMHOS=1
            OPEN      PATHSPA1,"pathspaf"
            MOVE      WBSEHOSP,KEY3
            CALL      RDPTHSP1
            IF        OVRCD=0
              MOVE      PTHSDCLM,PTCNDCLM     * default claim code charging
            ENDIF
          ENDIF
.
          MOVE      URNUMBER,PMFDURNO
.
          IF        UPDATEST=1
            MOVE      QUOTENUM,PMFDQUOT       * Update existing fee estimate
          ELSE
            MOVE      PTCNQUOT,PMFDQUOT       * Generate new fee estimate
          ENDIF
.
          CALL      UPPMFD00                * Update fields from cgi-var
.
          PACK      PMFDCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMFDCDAT
          CLOCK     TIME,PMFDCTIM  
          MOVE      USERID,PMFDCUID
.
          PACK      KEY12,PMFDQUOT,SP20
          CALL      RAPMFED1
          IF        OVRCD=1
            CALL      WRPMFED1           * write detail record
          ELSE
            CALL      UPPMFED1           * record already exists
          ENDIF
.
          PACK      KEY12,PMFDQUOT,SP20
          MOVE      PMFDQUOT,PMFNQUOT
          MOVE      PTITL,PMFNTITL
          MOVE      PSNAME,PMFNSNAM
          MOVE      PGNAME,PMFNGNAM
          MOVE      PADD1,PMFNADD1
          MOVE      PADD2,PMFNADD2
          MOVE      PSUBRB,PMFNSUBR
          MOVE      PPOST,PMFNPOST
          MOVE      PBDATE,PMFNBDAT
          MOVE      PTELEP,PMFNPHON
.
          CALL      RAPMFEN1
          IF        OVRCD=1
            CALL      WRPMFEN1           * write detail record
          ELSE
            CALL      UPPMFEN1           * record already exists
          ENDIF
.
          CALL      PFES0000           * Process fees estimation
          MOVE      ZERO,EXIT
GFES9999  RETURN
+
.------------------------------------------------------------
.         Process Fees estimation
.------------------------------------------------------------
PFES0000  MOVE      ZERO,BASICFLG
          MOVE      SP70,USEBASIC
          MOVE      ZERO,FEESTYPE
          MATCH     "1",PTCNIFCL
          GOTO      PFES1000 IF EQUAL   * HEA
          MATCH     "2",PTCNIFCL
          GOTO      PFES1200 IF NOT EQUAL
.
PFES1000  MOVE      PMFDUBAS,USEBASIC
          REP       " 0",USEBASIC
.
PFES1200  READ      CONTROLF,HUND20;*244,PTCNCWCL
.
          CALL      CCMX0000             * Check for Casemix Patient
          CALL      CMSC0000             * Calculate Misc.charge
          CALL      GPPR0000             * Save Pharmacy/Prosthetic charge
.
          IF        CMXFLAG=1
            CALL      CTHF0000           * Get casemix theatre fees
            CALL      CTHE0000           * Generate theatre fees
          ELSE
            CALL      NTHF0000           * Get non-casemix theatre fees
            CALL      GTHE0000           * Generate theatre fees
          ENDIF
.
          MATCH     "1",PMSFD029
          GOTO      PFES2000 IF EQUAL    * Use keyin adm.type for charging
.
          CALL      GSCL0000             * Get suggested classif. (cat A)
.
PFES2000  IF        CMXFLAG=1
            MATCH     "1",PTCNIFCL
            IF        @EQUAL
              MATCH     "1",USEBASIC
              IF        @EQUAL
                CALL      CBTP0000             * Get special bed type
              ENDIF
            ENDIF
            CALL      MCBD0000           * Generate casemix bed fees
          ELSE
            CALL      MNBD0000           * Generate Bed fee 
          ENDIF
          MOVE      ZERO,EXIT
.
          CALL      KILL0000                * deleting temporary file
.
PFES9999  RETURN
+
.******************************************************************************
.*                                  CCMX0000                                  *
.*                          Check If Casemix Patient                          *
.******************************************************************************
CCMX0000  MOVE      ZERO,DCFLAG
          MOVE      ZERO,FORM1
          MOVE      SP70,CONTRCID           * Initialise Contract Id
.
          IF        PMFDNONS=0
            MOVE      ONE,DCFLAG            * Daycase
          ENDIF
.
          MATCH     SP9,PMFDCMCD
          GOTO      CCMX9000 IF EQUAL       * Non casemix patient
.
          PACK      KEY18,PMFDCLTY,PMFDHFUN,PMFDCMCD,SP70
          MOVE      PMFDADAT,CEFFDATE       * Admission date
          CALL      VALCMXFN                * Validate casemix funding
          BRANCH    EXIT,CCMX9000
.
          MOVE      PTCMCNID,CONTRCID       * Save the contract id
          MOVE      ONE,CMXFLAG             * Casemix flag - using casemix
          GOTO      CCMX9999
.
CCMX9000  MOVE      ZERO,CMXFLAG            * Casemix flag - not using casemix
          GOTO      CCMX9999
.
CCMX9999  RETURN
+
.******************************************************************************
.*                                  GSCL0000                                  *
.*                     Get suggested classification (cat A)                   *
.******************************************************************************
GSCL0000  PACK      KEY18,SP20
          CALL      RSTM0000
          CALL      RKTM0000                * Read first tempfile record
          BRANCH    OVRCD,GSCL9999
.
          MATCH     "PATWEB76",PRGID
          IF        @EQUAL
            MOVE      SP70,ATYPE            * initialise for Fees Estimate
          ENDIF
          COMPARE   ONE,PTCNAUAT
          GOTO      GSCL9999 IF NOT EQUAL
.
          PACK      KEY17,KEY9,PMFDADAT
          CALL      PATITMRD                * If not in 'patsgcaf'
          BRANCH    OVRCD,GSCL9999
.
          MOVE      SP70,DIM3
          MOVE      ICLSS,DIM3          * Get suggested class
          IF        PMFDNONS=0
            MOVE      PTITDCSC,DIM3
          ENDIF
          MATCH     SP70,DIM3
          IF        !@EQUAL
            MOVE      DIM3,ATYPE
          ENDIF
.
          COMPARE   ONE,PTSGCOPT            * Using suggested class.?
          GOTO      GSCL8000 IF NOT EQUAL
.
          OPEN      PATSGCA1,"patsgcaf"
          PACK      KEY18,PMFDCLTY,PMFDHFUN,KEY9,SP20
          CALL      RDPTSGC1                * Get suggested adm. type
          COMPARE   ZERO,OVRCD
          GOTO      GSCL2000 IF EQUAL
.
          MATCH     SP70,PMFDHFUN
          GOTO      GSCL8000 IF EQUAL
.
          MOVE      SP70,PTSGCLMN           * blank claim
          PACK      KEY18,PTSGCLMN,PMFDHFUN,KEY9,SP20
          CALL      RDPTSGC1                * Get suggested adm. type
          COMPARE   ZERO,OVRCD
          GOTO      GSCL2000 IF EQUAL
.
          MOVE      SP70,PTSGFUND           * blank health fund
          PACK      KEY18,PMFDCLTY,PTSGFUND,KEY9,SP20
          CALL      RDPTSGC1                * Get suggested adm. type
          BRANCH    OVRCD,GSCL8000
.
GSCL2000  MATCH     SP70,PTSGCLSS
          IF        !@EQUAL
            MOVE      PTSGCLSS,ATYPE
          ENDIF
.
GSCL8000  MATCH     SP70,ATYPE
          GOTO      GSCL9999 IF EQUAL
          MOVE      ATYPE,PMFDATYP
.
          PACK      KEY12,PMFDQUOT,SP20
          CALL      RAPMFED1
          IF        OVRCD=0
            CALL      UPPMFED1           * record already exists
          ENDIF
.
GSCL9999  RETURN
+
.******************************************************************************
.*                                  CTHF0000                                  *
.*                          Get Casemix Theatre Fee                           *
.******************************************************************************
CTHF0000  CALL      CREA0000                * Create tempfile
          OPEN      TTFILE,FNAMEC
.
          MOVE      ZERO,BOOKFLAG
          MOVE      ZERO,COUNTER
.
          MOVE      ZERO,TCFORM6
          PACK      KEY5,ANSC,ANSL,PMFDCLTY
          CALL      RDCODE1                 * Read the codes file
          BRANCH    OVRCD,CTHF9999
.
          MOVE      TCFORM6,HFBAND
          MATCH     SP6,PMFDHFUN
          IF        !@EQUAL
            PACK      KEY14,PMFDHFUN,ZERO8
            CALL      RDFUND1               * Read the health fund file
            BRANCH    OVRCD,CTHF9999
.
            MOVE      "03",KEY2     * check HF history for Theatre charging ind.
            PACK      KEY17,PMFDHFUN,KEY2,PMFDADAT,SP70
            CALL      PATDDHRD              * Check HF History file
          ENDIF
          MOVE      HFBAND,THCFLAG
.
          MOVE      SP10,PTHFBCAT
          MOVE      SP10,PTCTTABT
          MATCH     SP6,PMFDHFUN
          IF        !@EQUAL
            PACK      KEY14 WITH PMFDHFUN,PMFDHFTB,SP70
            CALL      RDFUND1
            MOVE      PTHFBCAT,PTCTTABT       * health fund table type
          ENDIF
.
. ----- Get the item details -----
.
CTHF1000  MOVE      ZERO,FORM2
          COMPARE   "60",COUNTER
          GOTO      CTHF9999 IF EQUAL       * End of array
.
          ADD       ONE,COUNTER
          MOVE      COUNTER,MBSCOUNT
          MOVE      SP10,KEY9
          PACK      KEY9,THEFEE[MBSCOUNT],SP70
.
          MATCH     Z70,KEY9
          GOTO      CTHF1000 IF EQUAL       * Next item
          MATCH     SP9,KEY9
          GOTO      CTHF1000 IF EQUAL       * Next item
.
CTHF2000  PACK      KEY17,KEY9,PMFDADAT
          CALL      PATITMRD                * Read the item file
          BRANCH    OVRCD,CTHF1000
.
          MOVE      SP5,KEY3
          LOAD      KEY3,THCFLAG,IBAND1,IBAND2,IBAND3,IBAND4,IBAND5:
                                 IBAND6,IBAND7,IBAND8,IBAND9,IBAND10:
                                 IBAND11,IBAND12,IBAND13,IBAND14,IBAND15:
                                 IBAND16
.
          MOVE      ONE,FORM2               * Default to first sequence number
          MOVE      ZERO,ERRFLAG
          CALL      CSTF0000                * Get casemix theatre fee
.         BRANCH    EXIT,CTHF1000
.
. ----- write to tempfile in MBS amount order -----
.
          MOVE      PTCTDPAT,FORM82
          ADD       PTCTDREB,FORM82

          MOVE      KEY3,XBAND
          MOVE      ONE,XQUAN
.
          MOVE      COUNTER,MBSCOUNT
          MOVE      THEQUA[MBSCOUNT],XQUAN
          MOVE      GSTAPL[MBSCOUNT],XGSTA
          MOVE      GSTCOD[MBSCOUNT],XGSTC
          MOVE      GSTTKY[MBSCOUNT],XKGST
.
.         if GST applicable is unknown, check if GST Code is populated
.
          MATCH     "2",XGSTA
          IF        @EQUAL
            MATCH     SP70,XGSTC
            IF        !@EQUAL
              MOVE       "1",XGSTA         * GST Applicable
            ENDIF
          ENDIF
.
          MOVE      SP70,XBTYP             * Theatre fee from Booking flag
          MATCH     "PATWEB78",PRGID
          IF        @EQUAL
            MATCH     "0",ITMTYP[MBSCOUNT]
            IF        @EQUAL
              MOVE      "1",XBTYP
            ENDIF
          ENDIF
.
          CALL      WRTM0000             * write to tempfile
          GOTO      CTHF1000
.
CTHF9999  RETURN
+
.******************************************************************************
.                        Process Casemix theatre fee
.******************************************************************************
CTHE0000  MOVE      ZERO,COUNTER
          MOVE      SP20,KEY18
.
          MATCH     "PATWEB78",PRGID
          GOTO      CTHE1000 IF NOT EQUAL
.         
.         initialise variables used to store fields to pmseliaf file
.
          MATCH     "2",PTCNIFCL
          IF        @EQUAL
            MOVE      ONE,F3
            WHILE     F3 <= 60
              MOVE      SP70,ITMTYP[F3]   * record type
              MOVE      SP70,THEFEE[F3]   * Theatre Fee Code
              MOVE      ZERO,THEQUA[F3]   * No of Items
              MOVE      SP70,GSTTKY[F3]   * Keyin GST 1=Yes,
              MOVE      SP70,GSTTAP[F3]   * GST Payable 1=Yes, 2=No
              MOVE      SP70,GSTTCD[F3]   * GST Code
              ADD       ONE,F3
            DO
          ENDIF
.
CTHE1000  CALL      RSTM0000
CTHE2000  ADD       ONE,COUNTER
          CALL      RKTM0000
          BRANCH    OVRCD,CTHE9999
.
          MATCH     SP70,KEY9
          GOTO      CTHE2000 IF EQUAL
.
          MOVE      COUNTER,FORM2             * Sequence number
          MOVE      XBAND,KEY3
          CALL      CSTF0000                * Get casemix theatre fee
.         BRANCH    EXIT,CTHE2000
.
          ADD       PTCTDREB,PTCTDPAT
          MOVE      PTCTDPAT,BFPAT            * Pat + HF portion
          MOVE      PTCTDREB,BFHF
.
          MOVE      XQUAN,FORM3               * Quantity
          MOVE      ZERO,FORM12P2             * GST Amount
          MATCH     "1",XGSTA
          IF        @EQUAL
            MOVE      XGSTC,KEY6
            CALL      AGST0000
            MOVE      PTCTDPAT,FORM12P2
            MULT      IBGEAMNT,FORM12P2
            DIV       "100.00",FORM12P2
          ENDIF
.
          MATCH     "PATWEB78",PRGID
          GOTO      CTHE8000 IF NOT EQUAL
.
.       save quantity and GST fields to variables used to write to pmseliaf file
.
          MATCH     "2",PTCNIFCL
          IF        @EQUAL
            MOVE      "1",ITMTYP[COUNTER]            * MBS item
            MATCH     "1",XBTYP
            IF        @EQUAL
              MOVE      "0",ITMTYP[COUNTER]          * MBS Item from Booking
            ENDIF
            MOVE      KEY9,THEFEE[COUNTER]
            MOVE      XQUAN,THEQUA[COUNTER]
            MOVE      XGSTA,GSTTAP[COUNTER]
            MOVE      XGSTC,GSTTCD[COUNTER]
            MOVE      XKGST,GSTTKY[COUNTER]
.
            MOVE      XGSTA,GSTAPL[COUNTER]
            MOVE      XGSTC,GSTCOD[COUNTER]
          ENDIF
.
. ------  Write theatre fee details -----
.
CTHE8000  CALL      WRTF0000                * write theatre fees to pmsfesaf
.
          GOTO      CTHE2000
.
CTHE9999  RETURN
+
.******************************************************************************
.*                       Get casemix theatre fee                              *
.******************************************************************************
CSTF0000  MOVE      ZERO,EXIT
          MOVE      ZERO,FORM1
          PACK      KEY33,CONTRCID,PMFDCLTY,PMFDHFUN,PTCTTABT:
                          KEY3,SP9,DCFLAG,FORM2
CSTF4500  CALL      RDPTCTF2                * Read the DRG/MBS theatre fee file
          COMPARE   ZERO,OVRCD
          GOTO      CSTF9999 IF EQUAL       * Valid record found, print details
.
. ----- Combination not setup, try with different defaults -----
.
          ADD       ONE,FORM1
          BRANCH    FORM1,CSTF5000,CSTF5500
          GOTO      CSTF7000
.
. ----- Try combination without health fund code -----
.
CSTF5000  PACK      KEY33,CONTRCID,PMFDCLTY,SP6,SP3,KEY3,SP9,DCFLAG,FORM2
          GOTO      CSTF4500
.
. ----- Try combination with zero claim type & no health code -----
.
CSTF5500  PACK      KEY33,CONTRCID,PTCNDCLM,SP6,SP3,KEY3,SP9,DCFLAG,FORM2
          GOTO      CSTF4500
.
. ----- Error -----
.
CSTF7000  BRANCH    ERRFLAG,CSTF9999
.         DISPLAY   *P1:24,*EL,*B,"Unable to get theatre fee details for ":
.                   *V2LON,KEY9,*HOFF,".  ";
.         CALL      HITENTER
          MOVE      ONE,EXIT
.
          MOVE      ZERO,PTCTDPAT
          MOVE      ZERO,PTCTDREB
.
CSTF9999  RETURN
+
.******************************************************************************
.*                                  NTHF0000                                  *
.*                        Get Non Casemix Theatre Fee                         *
.******************************************************************************
NTHF0000  CALL      CREA0000                * Create tempfile
          OPEN      TTFILE,FNAMEC
.
          MOVE      ZERO,BOOKFLAG
          MOVE      ZERO,COUNTER
.
. ----- Get the item details -----
.
NTHF1000  MOVE      ZERO,FORM2
          COMPARE   "60",COUNTER
          GOTO      NTHF9999 IF EQUAL        * End of array
.
          ADD       ONE,COUNTER
          MOVE      COUNTER,MBSCOUNT
          MOVE      SP10,KEY9
          PACK      KEY9,THEFEE[MBSCOUNT],SP70
.
          MATCH     Z70,KEY9
          GOTO      NTHF1000 IF EQUAL       * try next item
          MATCH     SP9,KEY9
          GOTO      NTHF1000 IF EQUAL       * try next item
.
NTHF2000  PACK      KEY17,KEY9,PMFDADAT
          CALL      PATITMRD                * Read the item file
          BRANCH    OVRCD,NTHF1000
.
          MOVE      SP70,KEY1
          IF        DCFLAG=1
            MOVE      "D",KEY1
          ENDIF
.
. ----- Get the default theatre classification -----
.
          MOVE      ZERO,CNTRCEFR
          MOVE      PMFDADAT,CEFFDATE
          MOVE      PMFDADAT,TSRVDATE       * Service date
          PACK      KEY18,PMFDCLTY,PMFDHFUN,PMFDHFTB,KEY1,SP20
          CALL      GETTFEES
.
. ----- write to tempfile in MBS amount order -----
.
NTHF8000  MOVE      TFPAT1,FORM82
          ADD       TFHF1,FORM82
          MOVE      SP10,XBAND
          MOVE      ONE,XQUAN
.
          MOVE      COUNTER,MBSCOUNT
          MOVE      THEQUA[MBSCOUNT],XQUAN
          MOVE      GSTAPL[MBSCOUNT],XGSTA
          MOVE      GSTCOD[MBSCOUNT],XGSTC
          MOVE      GSTTKY[MBSCOUNT],XKGST
.
.         if GST applicable is unknown, check if GST Code is populated
.
          MATCH     "2",XGSTA
          IF        @EQUAL
            MATCH     SP70,XGSTC
            IF        !@EQUAL
              MOVE       "1",XGSTA         * GST Applicable
            ENDIF
          ENDIF
.
          MOVE      SP70,XBTYP          * Flag for Theatre item NOT from Booking
          MATCH     "PATWEB78",PRGID
          IF        @EQUAL
            MATCH     "0",ITMTYP[MBSCOUNT]
            IF        @EQUAL
              MOVE      "1",XBTYP        * from booking
            ENDIF
          ENDIF
.
          CALL      WRTM0000             * write to tempfile
          GOTO      NTHF1000
.
NTHF9999  RETURN
+
.******************************************************************************
.*                                  GTHE0000                                  *
.*                    Generate  Theatre Fee                                   *
.******************************************************************************
GTHE0000  MATCH     "PATWEB78",PRGID
          GOTO       GTHE1000 IF NOT EQUAL
.
.         initialise variables used to store fields to pmseliaf file
.
          MATCH     "2",PTCNIFCL
          IF        @EQUAL
            MOVE      ONE,F3
            WHILE     F3 <= 60
              MOVE      SP70,ITMTYP[F3]   * record type
              MOVE      SP70,THEFEE[F3]   * Theatre Fee Code
              MOVE      ZERO,THEQUA[F3]   * No of Items
              MOVE      SP70,GSTTKY[F3]   * Keyin GST 1=Yes,
              MOVE      SP70,GSTTAP[F3]   * GST Payable 1=Yes, 2=No
              MOVE      SP70,GSTTCD[F3]   * GST Code
              ADD       ONE,F3
            DO
          ENDIF
.
. ------  Read through tempfile -----
.
GTHE1000  MOVE      ZERO,COUNTER
          MOVE      SP20,KEY18
          CALL      RSTM0000
.
GTHE2000  ADD       ONE,COUNTER
          CALL      RKTM0000
          BRANCH    OVRCD,GTHE9999
.
          MATCH     SP70,KEY9
          GOTO      GTHE2000 IF EQUAL
.
          MOVE      ZERO,BFPAT
          LOAD      BFPAT,COUNTER,TFPAT1,TFPAT2,TFPAT3,TFPAT4,TFPAT5,TFPAT6
.
          MOVE      ZERO,BFHF
          LOAD      BFHF,COUNTER,TFHF1,TFHF2,TFHF3,TFHF4,TFHF5,TFHF6
.
          IF        COUNTER > 6
            IF        PTCNMBSF = 0
              MOVE      TFPAT6,BFPAT
              MOVE      TFHF6,BFHF
            ENDIF
          ENDIF
.
          ADD       BFHF,BFPAT
.
          MOVE      XQUAN,FORM3               * Quantity
          MOVE      ZERO,FORM12P2             * GST Amount
          MATCH     "1",XGSTA
          IF        @EQUAL
            MOVE      XGSTC,KEY6
            CALL      AGST0000
            MOVE      BFPAT,FORM12P2
            MULT      IBGEAMNT,FORM12P2
            DIV       "100.00",FORM12P2
          ENDIF
.
          MATCH     "1",XBTYP
          IF        @EQUAL
            PACK      OMBSCODE[COUNTER],KEY9,SP70    * MBS Order
          ENDIF
.
.       save quantity and GST fields to variables used to write to pmseliaf file
.
          MATCH     "PATWEB78",PRGID
          GOTO      GTHE8000 IF NOT EQUAL
.
          MATCH     "2",PTCNIFCL
          IF        @EQUAL
            MOVE      "1",ITMTYP[COUNTER]            * MBS Item
            MATCH     "1",XBTYP
            IF        @EQUAL
              MOVE      "0",ITMTYP[COUNTER]          * MBS Item from Booking
            ENDIF
.
            MOVE      KEY9,THEFEE[COUNTER]
            MOVE      XQUAN,THEQUA[COUNTER]
            MOVE      XGSTA,GSTTAP[COUNTER]
            MOVE      XGSTC,GSTTCD[COUNTER]
            MOVE      XKGST,GSTTKY[COUNTER]
.
            MOVE      XGSTA,GSTAPL[COUNTER]
            MOVE      XGSTC,GSTCOD[COUNTER]
.
.         SJOG - if Casemix code populated, there will be No Theatre fee
.
            MATCH     SP70,PTELCMXC
            IF        !@EQUAL
              MOVE      ZERO,BFPAT
              MOVE      ZERO,BFHF
              MOVE      ZERO,FORM12P2
            ENDIF
          ENDIF
.
. ------  Print theatre fee details -----
.
GTHE8000  CALL      WRTF0000                * write theatre fees to pmsfesaf
.
          GOTO      GTHE2000
.
GTHE9999  RETURN
+
.******************************************************************************
.*                                  GPPR0000              Called by: ML0000   *
.*                     Write Pharmacy & Prosthetic to pmsfesaf file           *
.******************************************************************************
GPPR0000  MATCH     "1",PTCNIFCL              * HEA Fees Estimate layout
          GOTO      GPPR2000 IF EQUAL
.
          IF        PROSTHET<>0
.           MOVE      PROSTHET,BFPAT
.           MOVE      ZERO,BFHF
.           CALL      WRTF0000                * write Prosthetic to pmsfesaf
.
            MOVE      PROSTHET,MPATPOR
            MOVE      ZERO,MHFPOR
            MOVE      "Prosthet.",KEY9
            MOVE      ONE,FORM3               * Quantity
            MOVE      ZERO,FORM12P2           * No GST
            MOVE      SP70,XGSTA
            MOVE      SP70,XGSTC
            MOVE      "6",KEY1                * Miscellaneous charge record type
.
            MATCH     "2",PTCNIFCL
            IF        @EQUAL
              MATCH     "PATWEB76",PRGID
              IF        @EQUAL
                MOVE      "8",KEY1    * As misc.item
              ENDIF
            ENDIF
.
            CALL      WRMF0000                * write Prosthetic to pmsfesaf
          ENDIF
.
          IF        PHARMACY<>0
            MOVE      "Pharmacy ",KEY9
            MOVE      PHARMACY,MPATPOR
            MOVE      ZERO,MHFPOR
            MOVE      ONE,FORM3               * Quantity
            MOVE      ZERO,FORM12P2           * No GST
            MOVE      SP70,XGSTA
            MOVE      SP70,XGSTC
            MOVE      "6",KEY1                * Miscellaneous charge record type
.
            MATCH     "2",PTCNIFCL
            IF        @EQUAL
              MATCH     "PATWEB76",PRGID
              IF        @EQUAL
                MOVE      "8",KEY1    * As misc.item
              ENDIF
            ENDIF
.
            CALL      WRMF0000                * write Pharmacy to pmsfesaf
          ENDIF
.
          IF        CONSUMAB<>0
            MOVE      CONSUMAB,MPATPOR
            MOVE      ZERO,MHFPOR
            MOVE      "Consumab.",KEY9
            MOVE      ONE,FORM3               * Quantity
            MOVE      ZERO,FORM12P2           * No GST
            MOVE      SP70,XGSTA
            MOVE      SP70,XGSTC
            MOVE      "8",KEY1                * Consumables record type
            CALL      WRMF0000                * write Prosthetic to pmsfesaf
          ENDIF
          GOTO      GPPR8000
.
.         HEA Fees Estimate layout has discount field
.
GPPR2000  IF        DISCOUNT<>0
            MOVE      DISCOUNT,MPATPOR
            MOVE      ZERO,MHFPOR
            MOVE      "Discount ",KEY9
            MOVE      ONE,FORM3               * Quantity
            MOVE      ZERO,FORM12P2           * No GST
            MOVE      SP70,XGSTA
            MOVE      SP70,XGSTC
            MOVE      "9",KEY1                * Consumables record type
            CALL      WRMF0000                * write Prosthetic to pmsfesaf
          ENDIF
.
          IF        PROSTHET<>0 | PROSTREB<>0
            MOVE      PROSTHET,MPATPOR
            ADD       PROSTREB,MPATPOR        * total
            MOVE      PROSTREB,MHFPOR
            MOVE      "Prosthet.",KEY9
            MOVE      ONE,FORM3               * Quantity
            MOVE      ZERO,FORM12P2           * No GST
            MOVE      SP70,XGSTA
            MOVE      SP70,XGSTC
            MOVE      "6",KEY1                * Miscellaneous charge record type
            CALL      WRMF0000                * write Prosthetic to pmsfesaf
          ENDIF
.
          IF        PHARMACY<>0 | PHARMREB<>0
            MOVE      "Pharmacy ",KEY9
            MOVE      PHARMACY,MPATPOR
            ADD       PHARMREB,MPATPOR        * total
            MOVE      PHARMREB,MHFPOR
            MOVE      ONE,FORM3               * Quantity
            MOVE      ZERO,FORM12P2           * No GST
            MOVE      SP70,XGSTA
            MOVE      SP70,XGSTC
            MOVE      "6",KEY1                * Miscellaneous charge record type
            CALL      WRMF0000                * write Prosthetic to pmsfesaf
          ENDIF
.
          IF        CONSUMAB<>0 | CONSMREB<>0
            MOVE      CONSUMAB,MPATPOR
            ADD       CONSMREB,MPATPOR        * total
            MOVE      CONSMREB,MHFPOR
            MOVE      "Consumab.",KEY9
            MOVE      ONE,FORM3               * Quantity
            MOVE      ZERO,FORM12P2           * No GST
            MOVE      SP70,XGSTA
            MOVE      SP70,XGSTC
            MOVE      "8",KEY1                * Consumables record type
            CALL      WRMF0000                * write Prosthetic to pmsfesaf
          ENDIF
.
.         Other Fees
.
GPPR8000  MOVE      "Surg.Gap ",KEY9          * Surgeon Gap Amount
          MOVE      SURGAPAM,MPATPOR
          CALL      SFEE0000                  * set fields for others fees
.
          MOVE      "Asst.Gap ",KEY9          * Assistant Gap Amount
          MOVE      ASSGAPAM,MPATPOR
          CALL      SFEE0000                  * set fields for others fees
.
          MOVE      "Anaes.Gap",KEY9          * Anaesthetist Gap Amount
          MOVE      ANAGAPAM,MPATPOR
          CALL      SFEE0000                  * set fields for others fees
.
          MOVE      "ICU Inten",KEY9          * ICU Intensivists
          MOVE      ICUINTEN,MPATPOR
          CALL      SFEE0000                  * set fields for others fees
.
          MOVE      "Phys.Init",KEY9          * Physio Initial Visit
          MOVE      PHYSINIT,MPATPOR
          CALL      SFEE0000                  * set fields for others fees
.
          MOVE      "Phys.Subs",KEY9          * Physio Subs Visit
          MOVE      PHYSSUBS,MPATPOR
          CALL      SFEE0000                  * set fields for others fees
.
          MOVE      "Radiology",KEY9          * Radiology
          MOVE      RADIOLGY,MPATPOR
          CALL      SFEE0000                  * set fields for others fees
.
          MOVE      "Pathology",KEY9          * Pathology
          MOVE      PATHOLGY,MPATPOR
          CALL      SFEE0000                  * set fields for others fees
.
          MOVE      "Cosmetic ",KEY9          * Cosmetic Out of Pocket
          MOVE      COSMETIC,MPATPOR
          CALL      SFEE0000                  * set fields for others fees
.
GPPR9999  RETURN
+
.******************************************************************************
.           Set fields for Other fees
.******************************************************************************
SFEE0000    MOVE      ZERO,MHFPOR
            MOVE      ONE,FORM3               * Quantity
            MOVE      ZERO,FORM12P2           * No GST
            MOVE      SP70,XGSTA
            MOVE      SP70,XGSTC
            MOVE      "7",KEY1                * Other Fee record type
            CALL      WRMF0000                * write record to pmsfesaf
SFEE9999    RETURN
+
.******************************************************************************
.*                                  WRTF0000              Called by: ML0000   *
.*                  Write theatre fees to pmsfesaf                            *
.******************************************************************************
WRTF0000  MOVE      ZERO,FORM5
          PACK      KEY17,PMFDQUOT,Z70
          CALL      RSPMFES1
          CALL      RPPMFES1
          BRANCH    OVRCD,WRTF2000
.
          MATCH     PMFDQUOT,PMFSQUOT
          GOTO      WRTF2000 IF NOT EQUAL
          MOVE      PMFSUNIQ,FORM5
.
WRTF2000  MOVE      URNUMBER,PMFSURNO
          MOVE      PMFDQUOT,PMFSQUOT
          ADD       ONE,FORM5
          MOVE      FORM5,PMFSUNIQ
          MOVE      SP70,PMFSBTYP
.
.
          MOVE      THREE,PMFSRTYP
          MATCH     "2",PTCNIFCL
          IF        @EQUAL
            MOVE      FIVE,PMFSRTYP       * SJOG Fees estimate using IFC layout
            MATCH     "PATWEB78",PRGID
            IF        @EQUAL
              MATCH     "1",XBTYP
              IF        @EQUAL
                MOVE      "XXX",PMFSBTYP  * Flag for Items from booking
              ENDIF
            ENDIF
          ENDIF
          MOVE      ZERO,PMFSLUMH
          MOVE      ZERO,PMFSLUMF
          MOVE      ZERO,PMFSDAFR
          MOVE      ZERO,PMFSDAYT
          MOVE      ZERO,PMFSDAYH
          MOVE      ZERO,PMFSDAHF
          MOVE      KEY9,PMFSTHIT
          MOVE      BFPAT,PMFSTHEH
          MOVE      BFHF,PMFSTHEF
          IF        FORM3=0
            MOVE      ONE,FORM3
          ENDIF
          MOVE      FORM3,PMFSQUAN
          MOVE      FORM12P2,PMFSGSTA
          MOVE      XGSTA,PMFSGSTP
          MOVE      XGSTC,PMFSGSTC
.
          PACK      KEY17,PMFSQUOT,PMFSUNIQ
          CALL      RAPMFES1
          COMPARE   ZERO,OVRCD
          GOTO      WRTF2000 IF EQUAL
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRPMFES1
          TRAPCLR   IO
.
WRTF9999  RETURN
+
.******************************************************************************
.*                                  MCBD0000              Called by: ML0000   *
.*                        Generate Casemix Bed Fee with multiple bed types    *
.******************************************************************************
MCBD0000  MOVE      ONE,FORM4
.
          COMPARE   ONE,DCFLAG
          GOTO      MCBD1000 IF NOT EQUAL   * Overnight stay
.
. ----- Daycase -----
.
          MOVE      PMFDBDTY,SAVBTYP
          MOVE      ZERO,FORM5A
          CALL      GCDAY000                * Get casemix daycase rate
          GOTO      MCBD9999
.
. ----- Overnight stay -----
.
MCBD1000  MOVE      ZERO,FORM1              * Addition charge flag only
          MOVE      PMFDBDTY,SAVBTYP
          MOVE      PMFDNONS,FORM5A
          CALL      GCSOV000                * Get casemix overnight rate
.
          IF        PMFDNDS2<>0
            MOVE      ONE,FORM1
            MOVE      PMFDBDT2,SAVBTYP
            MOVE      PMFDNDS2,FORM5A
            CALL      GCSOV000                * Get casemix overnight rate
          ENDIF
.
          IF        PMFDNDS3<>0
            MOVE      ONE,FORM1
            MOVE      PMFDBDT3,SAVBTYP
            MOVE      PMFDNDS3,FORM5A
            CALL      GCSOV000                * Get casemix overnight rate
          ENDIF
.
          IF        PMFDNDS4<>0
            MOVE      ONE,FORM1
            MOVE      PMFDBDT4,SAVBTYP
            MOVE      PMFDNDS4,FORM5A
            CALL      GCSOV000                * Get casemix overnight rate
          ENDIF
.
          IF        PMFDNDS5<>0
            MOVE      ONE,FORM1
            MOVE      PMFDBDT5,SAVBTYP
            MOVE      PMFDNDS5,FORM5A
            CALL      GCSOV000                * Get casemix overnight rate
          ENDIF
.
.         Do all the other bed types for daily rate only
.
MCBD2000  MOVE      "BT",KEY2
          PACK      KEY5,KEY2,SP70
          CALL      RDSCODE1
MCBD4000  CALL      RDKCODE1
          BRANCH    OVRCD,MCBD9999
          MATCH     KEY2,TCODE
          GOTO      MCBD9999 IF NOT EQUAL
          MATCH     "I",PTCOACTV
          GOTO      MCBD4000 IF EQUAL      * In-active
.
          IF        IBCNMHOS=1
            MATCH    "1",PTCDHOSS
            IF       @EQUAL
              PACK     KEY8,WBSEHOSP,TCODE,ACODE,SP70
              CALL     RDMLCOD1
              BRANCH   OVRCD,MCBD4000
            ENDIF
          ENDIF
.
          COMPARE   ONE,TCFORM6
          GOTO      MCBD4000 IF NOT EQUAL  * not a valid fees estimates bedtype
.
          CALL      CBTYP000               * Check if it's one of the selected
          BRANCH    EXIT,MCBD4000          * yes, skip it
.
          MOVE      ONE,FORM1
          MOVE      ACODE,SAVBTYP
          MOVE      ZERO,FORM5A
          CALL      GCSOV000                * Get casemix overnight rate
          GOTO      MCBD4000
.
MCBD9999  RETURN
+
.******************************************************************************
.         Get casemix overnight rate
.******************************************************************************
GCSOV000  MOVE      ZERO,SAVEND
.
          BRANCH    FORM1,GCSOV500       * Additional charge only
.
          MOVE      FORM5A,SAVNODAY
.
          MOVE      PMFDNONS,FORM5A
          ADD       PMFDNDS2,FORM5A
          ADD       PMFDNDS3,FORM5A
          ADD       PMFDNDS4,FORM5A
          ADD       PMFDNDS5,FORM5A       * total number of days
          ADD       ONE,FORM5A
          MOVE      SP70,DIM27A
.
          PACK      KEY27,CONTRCID,PMFDCLTY,PMFDHFUN,PTCTTABT,PMFDCMCD
          CALL      RDPTHLF1                * Read the lumpsum overnight file
          IF        OVRCD=1
            PACK      KEY27,CONTRCID,PMFDCLTY,SP6,SP3,PMFDCMCD
            CALL      RDPTHLF1              * Read the lumpsum overnight file
            IF        OVRCD=1
              PACK      KEY27,CONTRCID,PTCNDCLM,SP6,SP3,PMFDCMCD
              CALL      RDPTHLF1            * Read the lumpsum overnight file
              BRANCH    OVRCD,GCSOV300
            ENDIF
          ENDIF
.
          MOVE      KEY27,DIM27A
          COMPARE   FORM5A,PTHLCOFF
          GOTO      GCSOV280 IF LESS        * Outlier cut off day < ngts of stay
.
. ----- Read the Low outlier file -----
.
GCSOV200  PACK      DIM27,DIM27A,SP70
          PACK      KEY31,DIM27A,SP70
          CALL      RSPTLDF1                * Position on & read the daily fee
GCSOV220  CALL      RKPTLDF1                  outliers file
          BRANCH    OVRCD,GCSOV400
.
          PACK      KEY27,PTLDCNID,PTLDCLMN,PTLDHFND,PTLDTABT,PTLDCASM,SP30
          MATCH     DIM27,KEY27
          GOTO      GCSOV400 IF NOT EQUAL   * Different codes
.
          MOVE      PTLDDPAT,BFPAT
          ADD       PTLDDREB,BFPAT
          MOVE      PTLDDREB,BFHF
.
          MOVE      ZERO,FEESTYPE
          CALL      HBAS00000               * Check HEA Basic rate
.
          MOVE      SP70,PMFSBTYP           * no bed type for outlier
          MOVE      PTLDEDAY,BFENDDY
          CALL      WRDC0000                * write daily charges to pmsfesaf
.
          IF        PTLDEDAY<FORM5A
            MOVE      PTLDEDAY,SAVEND
            GOTO      GCSOV220              * Get next stepdown fee
          ENDIF
          MOVE      ZERO,SAVEND
          GOTO      GCSOV400
.
.         High outlier
.
GCSOV280  SUB       ONE,FORM5A
.
          MOVE      PTHLLPAT,BFPAT
          ADD       PTHLLREB,BFPAT
          MOVE      PTHLLREB,BFHF
.
          MOVE      ZERO,FEESTYPE
          CALL      HBAS00000               * Check HEA Basic rate
.
          CALL      WRLS0000                * write lumpsum to pmsfesaf
.
          PACK      DIM27,DIM27A,SP70
          PACK      KEY31,DIM27,SP70
.
          GOTO      GCSOV320
.
. ----- Read the inlier file -----
.
GCSOV300  PACK      DIM27,CONTRCID,PMFDCLTY,PMFDHFUN,PTCTTABT,PMFDCMCD,SP30
          PACK      KEY31,DIM27,SP30
.
GCSOV320  CALL      RSPTHDF1                * Position on & read the daily fee
GCSOV340  CALL      RKPTHDF1                  inliers file
          BRANCH    OVRCD,GCSOV380
.
          PACK      KEY27,PTHDCNID,PTHDCLMN,PTHDHFND,PTHDTABT,PTHDCASM,SP70
          MATCH     DIM27,KEY27
          GOTO      GCSOV380 IF NOT EQUAL   * Different codes
.
          MOVE      KEY27,DIM27A
.
          MOVE      PTHDDPAT,BFPAT
          ADD       PTHDDREB,BFPAT
          MOVE      PTHDDREB,BFHF
.
          MOVE      ZERO,FEESTYPE
          CALL      HBAS00000            * Check HEA Basic rate
.
          MOVE      SP70,PMFSBTYP           * no bed type for Inlier
          MOVE      PTHDEDAY,BFENDDY
          CALL      WRDC0000                * write daily charge to pmsfesaf
.
          IF        PTHDEDAY<FORM5A
            MOVE      PTHDEDAY,SAVEND
            GOTO      GCSOV340              * Get next stepdown fee
          ENDIF
          MOVE      ZERO,SAVEND
          GOTO      GCSOV400
.
GCSOV380  PACK      DIM27A,CONTRCID,PMFDCLTY,PMFDHFUN,PTCTTABT:
                           PMFDCMCD,SP30
.
GCSOV400  MOVE      SAVNODAY,FORM5A         * restore the number of days
.
. ----- Print the additional charges -----
.
.
GCSOV500  PACK      DIM30,DIM27A,SAVBTYP,SP30
          PACK      KEY34,DIM27A,SAVBTYP,SP30
          CALL      RSPTAFE1                * Position on & read the additional
GCSOV600  CALL      RKPTAFE1                  charges overnight file
          BRANCH    OVRCD,GCSOV700
.
          PACK      KEY30,PTFECNID,PTFECLMN,PTFEHFND,PTFETABT:
                          PTFECASM,PTFEBTYP,SP70
          MATCH     DIM30,KEY30
          GOTO      GCSOV700 IF NOT EQUAL   * Different codes
.
          MOVE      PTFEDPAT,BFPAT
          ADD       PTFEDREB,BFPAT
          MOVE      PTFEDREB,BFHF
.
          MOVE      ONE,FEESTYPE            * Flag for additional rate
          CALL      HBAS00000               * Check HEA Basic rate
.
          MOVE      SAVBTYP,PMFSBTYP         * bed type
          MOVE      PTFEEDAY,BFENDDY
          CALL      WRDC0000                 * write daily charge to pmsfesaf
.
          IF        PTFEEDAY<FORM5A
            MOVE      PTFEEDAY,SAVEND
            GOTO      GCSOV600              * Get next stepdown fee
          ENDIF
          MOVE      ZERO,SAVEND
          GOTO      GCSOV900
.
GCSOV700  MOVE      ZERO,BFPAT
          MOVE      ZERO,BFHF
          MOVE      SAVBTYP,PMFSBTYP         * bed type
          MOVE      PTFEEDAY,BFENDDY
          CALL      WRDC0000                 * write daily charge to pmsfesaf
.
GCSOV900  MOVE      ZERO,EXIT
GCSOV999  RETURN
+
.******************************************************************************
.         Get Basic rate for HEA Fees Estimate (Rebate amount from bed fees)
.         - C/P Lumpsum and Inlier/Outlier use Shared bedtype
.         - C/P Private bedtype will have zero Health fund rebate
.         - Other Bed types use the actual bedtype (including per diem)
.
.         FEESTYPE = 0 for CP Lumpsum and Inlier/Outlier 
.         FEESTYPE = 1 for CP Additional rate (zero for Private bedtype)
.         FEESTYPE = 2 for per diem
. 
.******************************************************************************
HBAS0000  MATCH     "1",PTCNIFCL
          GOTO      HBAS9999 IF NOT EQUAL
          MATCH     "1",USEBASIC
          GOTO      HBAS9999 IF NOT EQUAL
.
          BRANCH    FEESTYPE,HBAS2000,HBAS5000  
.
.         For Lumpsum and Inlier use Shared bed type
.
          MOVE      SHRBTYP,SBEDTYPE   * Shared bed type
          GOTO      HBAS6000
.
HBAS2000  MATCH     SAVBTYP,PRVBTYP
          IF        @EQUAL
            MOVE      ZERO,BASICHF     * Private bed will have zero Rebate
            MOVE      ONE,BASICFLG
            GOTO      HBAS9999
          ENDIF
.
HBAS5000  MOVE      SAVBTYP,SBEDTYPE     * bed type
.
HBAS6000  MOVE      BFHF,F12P2
          MOVE      BFPAT,FORM12P2
          MOVE      BFENDDY,FORM3B
.
          CALL      CWBFE000             * Get Basic Cover bed fees rates
          MOVE      F12P2,BFHF
          MOVE      FORM12P2,BFPAT       * restore Lumpsum + Inlier amount
          MOVE      FORM3B,BFENDDY
HBAS9999  RETURN
+
.******************************************************************************
.         Get casemix day case rate
.******************************************************************************
GCDAY000  PACK      KEY27,CONTRCID,PMFDCLTY,PMFDHFUN,PTCTTABT,PMFDCMCD
          CALL      RDPTLSD1                * Read the lumpsum daycase file
          IF        OVRCD=1
            PACK      KEY27,CONTRCID,PMFDCLTY,SP6,SP3,PMFDCMCD
            CALL      RDPTLSD1              * Read the lumpsum daycase file
            IF        OVRCD=1
              PACK      KEY27,CONTRCID,PTCNDCLM,SP6,SP3,PMFDCMCD
              CALL      RDPTLSD1             * Read the lumpsum daycase file
              BRANCH    OVRCD,GCDAY200
            ENDIF
          ENDIF
.
          MOVE      PTLSLPAT,BFPAT
          ADD       PTLSLREB,BFPAT
          MOVE      PTLSLREB,BFHF
.
          MOVE      ZERO,FEESTYPE           * flag for lumpsum
          CALL      HBAS00000               * Check HEA Basic rate
.
          CALL      WRLS0000                * write lumpsum to pmsfesaf
.
GCDAY200  PACK      KEY30,CONTRCID,PMFDCLTY,PMFDHFUN,PTCTTABT,PMFDCMCD,SAVBTYP
          CALL      RDPTASD1                * Read the addition charge d/c file
          IF        OVRCD=1
            PACK      KEY30,CONTRCID,PMFDCLTY,SP6,SP3,PMFDCMCD,SAVBTYP
            CALL      RDPTASD1              * Read the addition charge d/c file
            IF        OVRCD=1
              PACK      KEY30,CONTRCID,PTCNDCLM,SP6,SP3,PMFDCMCD,SAVBTYP
              CALL      RDPTASD1            * Read the addition charge d/c file
              BRANCH    OVRCD,GCDAY999
            ENDIF
          ENDIF
.
          MOVE      PTADDPAT,BFPAT
          ADD       PTADDREB,BFPAT
          MOVE      PTADDREB,BFHF
.
          MOVE      ONE,FEESTYPE            * Flag for additional rate
          CALL      HBAS00000               * Check HEA Basic rate
.
          MOVE      SAVBTYP,PMFSBTYP        * bed type
          MOVE      ZERO,BFENDDY
          CALL      WRDC0000                * write daily charge to pmsfesaf
.
GCDAY9999 RETURN
+
.******************************************************************************
.*                                  CMSC0000                                  *
.*                          Get Miscellaneous charge                          *
.******************************************************************************
CMSC0000  MOVE      ZERO,COUNTER
          MOVE      ZERO,XMCCNT
          IF        CMXFLAG=1
            CALL      CRET0000                * Create tempfile
            OPEN      MCFILE,FNAMED
          ENDIF
.
CMSC1000  COMPARE   "99",COUNTER
          GOTO      CMSC9000 IF EQUAL
.
          ADD       ONE,COUNTER
          MOVE      COUNTER,MBSCOUNT
          MOVE      SP10,KEY9
          PACK      KEY9,MISCHG[MBSCOUNT],SP70
.
          MATCH     Z70,KEY9
          GOTO      CMSC1000 IF EQUAL
          MATCH     SP70,KEY9
          GOTO      CMSC1000 IF EQUAL
.
          MOVE      KEY9,MCHARGE
.
          MATCH     "1",PTCNIFCL
          GOTO      CMSC1200 IF EQUAL
          MATCH     "2",PTCNIFCL
          GOTO      CMSC1400 IF NOT EQUAL    * NOT SJOG Format
.
CMSC1200  CALL      GMCHRG00                * Get misc.charge
          BRANCH    EXIT,CMSC2000
.
          MATCH     "Y",PTMCKREB
          GOTO      CMSC1600 IF NOT EQUAL    * not a keyin item amount
.
          MATCH     Z70,MISAMT[MBSCOUNT]
          GOTO      CMSC1600 IF EQUAL
.
          MOVE      ZERO,FORM12P2
          MOVE      MISAMT[MBSCOUNT],FORM12P2
          COMPARE   ZERO,FORM12P2
          GOTO      CMSC1600 IF EQUAL
.
          MOVE      ZERO,FORM12P2
          MOVE      ZERO,MPATPOR
          PACK      KEY15,MISAMT[MBSCOUNT]
          STRIP     KEY15
          MOVE      KEY15,MPATPOR
          MOVE      ZERO,MHFPOR
          GOTO      CMSC1600
.
CMSC1400  CALL      GMCHRG00                * Get misc.charge
          BRANCH    EXIT,CMSC2000
.
CMSC1600  COMPARE   ONE,CMXFLAG
          GOTO      CMSC4000 IF NOT EQUAL
          COMPARE   ZERO,PTMCCFCY
          GOTO      CMSC2000 IF EQUAL       * No charge on all items
.
          PACK      KEY9,MCHARGE
          CALL      RDTEMP1
          IF        OVRCD = 1
            MOVE      MCHARGE,XCHARGE
            MOVE      ONE,XMCCNT
            CALL      WRTEMP1
          ELSE
            ADD       ONE,XMCCNT
            CALL      UPTEMP1
          ENDIF
.
          COMPARE   PTMCCFCY,XMCCNT            * charge from item number ?
          GOTO      CMSC2000 IF LESS           * no charge
          GOTO      CMSC4000
.
CMSC2000  MOVE      ZERO,MPATPOR                 * no charge
          MOVE      ZERO,MHFPOR
.
          MATCH     "1",PTCNIFCL
          GOTO      CMSC2200 IF EQUAL
.
          MATCH     "2",PTCNIFCL
          GOTO      CMSC4000 IF NOT EQUAL
.
CMSC2200  MATCH     "PATWEB78",PRGID
          GOTO      CMSC4000 IF NOT EQUAL
.
          MOVE      ZERO,MISAMT[MBSCOUNT]      * to store on pmseliaf
.
CMSC4000  MOVE      MISQUA[MBSCOUNT],FORM3
          ADD       MHFPOR,MPATPOR
.
          MOVE      SP70,KEY1
          MOVE      SP70,XGSTA
          MOVE      SP70,XGSTC
          MOVE      GSTAPM[MBSCOUNT],KEY1
.
          MOVE      ZERO,FORM12P2
          MATCH     "1",KEY1
          IF        @EQUAL
            MOVE      GSTCOM[MBSCOUNT],KEY6
            CALL      AGST0000
            MOVE      MPATPOR,FORM12P2
            MULT      IBGEAMNT,FORM12P2
            DIV       "100.00",FORM12P2
            MOVE      ONE,XGSTA
            MOVE      KEY6,XGSTC
          ENDIF
.
          MATCH     "1",PTCNIFCL
          IF        @EQUAL
            MATCH     "PATWEB78",PRGID
            GOTO      CMSC8000 IF EQUAL
          ENDIF
          MATCH     "2",PTCNIFCL
          GOTO      CMSC8000 IF EQUAL       * SJOG Format
.
. If amount is zero, check for keyed in amount from screen
.
          COMPARE   ZERO,MPATPOR
          IF        @EQUAL
            MOVE      MISAMT[MBSCOUNT],MPATPOR
          ENDIF
.
. ------  write Miscellaneous charge details -----
.
CMSC8000  MOVE      "6",KEY1                * Miscellaneous charge record type
.
          MATCH     "1",PTCNIFCL
          IF        @EQUAL
            MATCH     "PATWEB78",PRGID
            GOTO      CMSC8200 IF EQUAL     * HEA IFC has a Misc.item
          ENDIF
.
          MATCH     "2",PTCNIFCL
          GOTO      CMSC8900 IF NOT EQUAL
.
CMSC8200  MOVE      "8",KEY1              * SJOG Fees Est. using IFC layout
.
CMSC8900  CALL      WRMF0000
          GOTO      CMSC1000
.
CMSC9000  IF        CMXFLAG=1
            CALL      KILM0000              * deleting temporary file
          ENDIF
CMSC9999  RETURN
+
.******************************************************************************
.*                                  WRMF0000              Called by: ML0000   *
.*                  Write misc charge fees to pmsfesaf                        *
.******************************************************************************
WRMF0000  MOVE      ZERO,FORM5
          PACK      KEY17,PMFDQUOT,Z70
          CALL      RSPMFES1
          CALL      RPPMFES1
          BRANCH    OVRCD,WRMF2000
.
          MATCH     PMFDQUOT,PMFSQUOT
          GOTO      WRMF2000 IF NOT EQUAL
.
          MOVE      PMFSUNIQ,FORM5
.
WRMF2000  MOVE      URNUMBER,PMFSURNO
          MOVE      PMFDQUOT,PMFSQUOT
.
. -----   write misc charge fee records -----
.
          MOVE      KEY1,PMFSRTYP
          MOVE      ZERO,PMFSLUMH
          MOVE      ZERO,PMFSLUMF
          MOVE      ZERO,PMFSDAFR
          MOVE      ZERO,PMFSDAYT
          MOVE      ZERO,PMFSDAYH
          MOVE      ZERO,PMFSDAHF
          MOVE      KEY9,PMFSTHIT
          MOVE      MPATPOR,PMFSTHEH
          MOVE      MHFPOR,PMFSTHEF
          MOVE      FORM3,PMFSQUAN
          MOVE      SP70,PMFSBTYP
          MOVE      FORM12P2,PMFSGSTA
          MOVE      XGSTA,PMFSGSTP
          MOVE      XGSTC,PMFSGSTC
.
WRMF4000  ADD       ONE,FORM5
          MOVE      FORM5,PMFSUNIQ
.
          PACK      KEY17,PMFSQUOT,PMFSUNIQ
          CALL      RAPMFES1
          COMPARE   ZERO,OVRCD
          GOTO      WRMF4000 IF EQUAL
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRPMFES1
          TRAPCLR   IO
.
WRMF9999  RETURN
+
.------------------------------------------------------------
. Get the appropriate miscellaneous charges record
.------------------------------------------------------------
GMCHRG00  MOVE      ZERO,EXIT
.
          MOVE      CURRDATE,D8
          PACK      PMSFD004,PMSFD004,SP70
          MATCH     Z70,PMSFD004
          GOTO      GMCHRG10 IF EQUAL
.
          MATCH     SP70,PMSFD004
          IF        !@EQUAL
            MOVE      PMSFD004,D8      * admission date
          ENDIF
.
GMCHRG10  REP       " 0",D8
.
          UNPACK    SP20,PTHFBCAT      * change "H/F Table" to "Table Type"
          PACK      KEY14,PMFDHFUN,PMFDHFTB,SP20
          CALL      RDFUND1            * "Table Type code" now in PTHFBCAT
.
          MOVE      MCHARGE,KEY9            * (MCHARGE re-use in PATMCHRD)
          PACK      KEY29,PMFDCLTY,PMFDHFUN,PTHFBCAT,KEY9,D8,SP10
          CALL      PATMCHRD                * read Misc.Charges file 
          COMPARE   ZERO,EXIT
          GOTO      GMCHRG99 IF EQUAL
.
.         No record with health fund details - try a blank health fund
.
          PACK      KEY29,PMFDCLTY,SP6,SP3,KEY9,D8,SP10
          CALL      PATMCHRD                * read Misc.Charges file 
          COMPARE   ZERO,EXIT
          GOTO      GMCHRG99 IF EQUAL
.
.         No record with health fund details - last chance, try default claim
.
          PACK      KEY29,PTCNDCLM,SP6,SP3,KEY9,D8,SP10
          CALL      PATMCHRD                * read Misc.Charges file 
.
GMCHRG99  RETURN
+
.******************************************************************************
.*                                  MNBD0000                                  *
.*                      Generate Non Casemix Bed Fee with multiple bed types  *
.******************************************************************************
MNBD0000  MOVE      ZERO,FORM1
          MOVE      ZERO,FORM2
          MOVE      ONE,FORM4
          MOVE      PMFDBDTY,SAVBTYP
          MOVE      PMFDNONS,FORM5A
          CALL      GBFE0000               * get bed fee for bed type 1
          BRANCH    DCFLAG,MNBD2000        * Daycase can only have one bedtype
.
          MOVE      ONE,FORM1
          IF        PMFDNDS2<>0
            MOVE      PMFDBDT2,SAVBTYP
            MOVE      PMFDNDS2,FORM5A
            CALL      GBFE0000               * get bed fee for bed type 2
          ENDIF
.
          IF        PMFDNDS3<>0
            MOVE      PMFDBDT3,SAVBTYP
            MOVE      PMFDNDS3,FORM5A
            CALL      GBFE0000               * get bed fee for bed type 3
          ENDIF
.
          IF        PMFDNDS4<>0
            MOVE      PMFDBDT4,SAVBTYP
            MOVE      PMFDNDS4,FORM5A
            CALL      GBFE0000               * get bed fee for bed type 4
          ENDIF
.
          IF        PMFDNDS5<>0
            MOVE      PMFDBDT5,SAVBTYP
            MOVE      PMFDNDS5,FORM5A
            CALL      GBFE0000               * get bed fee for bed type 5
          ENDIF
.
.         Do all the other bed types for daily rate only
.
MNBD2000  MOVE      "BT",KEY2
          PACK      KEY5,KEY2,SP70
          CALL      RDSCODE1
MNBD4000  CALL      RDKCODE1
          BRANCH    OVRCD,MNBD9999
          MATCH     KEY2,TCODE
          GOTO      MNBD9999 IF NOT EQUAL
          MATCH     "I",PTCOACTV
          GOTO      MNBD4000 IF EQUAL      * In-active
.
          IF        IBCNMHOS=1
            MATCH    "1",PTCDHOSS
            IF       @EQUAL
              PACK     KEY8,WBSEHOSP,TCODE,ACODE,SP70
              CALL     RDMLCOD1
              BRANCH   OVRCD,MNBD4000
            ENDIF
          ENDIF
.
          COMPARE   ONE,TCFORM6
          GOTO      MNBD4000 IF NOT EQUAL  * not a valid fees estimates bedtype
.
          CALL      CBTYP000               * Check if it's one of the selected
          BRANCH    EXIT,MNBD4000          * yes, skip it
.
          MOVE      ONE,FORM1
          MOVE      ACODE,SAVBTYP
          MOVE      ZERO,FORM5A
          CALL      GBFE0000               * get bed fee for a bed type 
          GOTO      MNBD4000
.
MNBD9999  RETURN
+
.******************************************************************************
.         Check if the bed type is one of the selected bed type
.******************************************************************************
CBTYP000  MOVE      ZERO,EXIT
          MATCH     ACODE,PMFDBDTY           * bed type 1
          GOTO      CBTYP900 IF EQUAL
.
          MATCH     ACODE,PMFDBDT2           * bed type 2
          IF        @EQUAL
            COMPARE   ZERO,PMFDNDS2
            GOTO      CBTYP900 IF NOT EQUAL
            GOTO      CBTYP999
          ENDIF
.
          MATCH     ACODE,PMFDBDT3           * bed type 3
          IF        @EQUAL
            COMPARE   ZERO,PMFDNDS3
            GOTO      CBTYP900 IF NOT EQUAL
            GOTO      CBTYP999
          ENDIF
.
          MATCH     ACODE,PMFDBDT4           * bed type 4
          IF        @EQUAL
            COMPARE   ZERO,PMFDNDS4
            GOTO      CBTYP900 IF NOT EQUAL
            GOTO      CBTYP999
          ENDIF
.
          MATCH     ACODE,PMFDBDT5           * bed type 5
          IF        @EQUAL
            COMPARE   ZERO,PMFDNDS3
            GOTO      CBTYP900 IF NOT EQUAL
          ENDIF
          GOTO      CBTYP999
.
CBTYP900  MOVE      ONE,EXIT
CBTYP999  RETURN
+
.******************************************************************************
.         Get a bed fee for a bed type
.******************************************************************************
GBFE0000  MOVE      ZERO,FORM2
          MOVE      ZERO,SAVEND
          PACK      KEY23,PMFDCLTY,PMFDHFUN,PTHFBCAT,PMFDATYP,SAVBTYP,SP70
.
          COMPARE   NINE,CFEETYP
          GOTO      GBFE1000 IF EQUAL      * Johns Hopkins bed fees
.
          PACK      KEY9,PMFDCLTY,PMFDHFUN,SP70
          IF        ASTAT=0
            MOVE      TWO,ASTAT            * Set to current status
          ENDIF
          CALL      GETCNEFF               * Get Contract Effective From
          BRANCH    EXIT,GBFE7000
.
          MOVE      SP70,PTHFBCAT
          PACK      KEY14,PMFDHFUN,PMFDHFTB,SP10
          CALL      RDFUND1
          PACK      KEY18,PMFDCLTY,PMFDHFUN,PTHFBCAT,PMFDATYP,SAVBTYP,SP70
.
GBFE1000  IF        CFEETYP=5
            OPEN      JHSBFEA1,"jhsbfeaf"
            PACK      KEY26,KEY23,SP70
            CALL      RSJHBFE1              * Position on & read the bed fee
          ELSE
            PACK      KEY27,KEY18,SP30
            CALL      RDSBFEE1              * Position on & read the bed fee
          ENDIF
GBFE2000  IF        CFEETYP=5
            CALL      RKJHBFE1
            BRANCH    OVRCD,GBFE3000
            PACK      DIM23,BFCLM,BFHFUND,BFHFTAB,BFATYPE,BFBTYPE,SP70
            MATCH     KEY23,DIM23
            GOTO      GBFE3000 IF NOT EQUAL
          ELSE
            CALL      RDKBFEE1
            BRANCH    OVRCD,GBFE3000
            PACK      DIM18,BFCLM,BFHFUND,PTBFHTYP,BFATYPE,BFBTYPE,SP70
            MATCH     KEY18,DIM18
            GOTO      GBFE3000 IF NOT EQUAL
.
            MOVE      PMFDADAT,CEFFDATE       * Use admission date
            MOVE      PTBFCNID,KEY6           * Contract ID
            CALL      VALCONTR                * Validate Contract ID
            BRANCH    EXIT,GBFE2000           * not valid
            MOVE      PTBFCNID,CONTRCID       * Contract ID
          ENDIF
.
          GOTO      GBFE6000                * Same bed fee rate, print
.
GBFE3000  ADD       ONE,FORM2
          BRANCH    FORM2,GBFE4000,GBFE5000
.
.         DISPLAY   *P1:24,*EL,*B,"No bed fee detail found.  Default not set ":
.                                 "up.  ";
.         CALL      HITENTER
          GOTO      GBFE7000
.
GBFE4000  PACK      KEY18,PMFDCLTY,SP6,SP3,PMFDATYP,SAVBTYP,SP70
          PACK      KEY23,PMFDCLTY,SP6,SP8,PMFDATYP,SAVBTYP,SP70
          GOTO      GBFE1000
.
GBFE5000  PACK      KEY18,PTCNDCLM,SP6,SP3,PMFDATYP,SAVBTYP,SP70
          PACK      KEY23,PTCNDCLM,SP6,SP8,PMFDATYP,SAVBTYP,SP70
          GOTO      GBFE1000
.
GBFE6000  COMPARE   ZERO,BFENDDY
          GOTO      GBFE8000 IF NOT EQUAL
.
          BRANCH    DCFLAG,GBFE8000         * day case rate
          GOTO      GBFE2000
.
GBFE7000  MOVE      ZERO,BFPAT
          MOVE      ZERO,BFHF
.
GBFE8000  ADD       BFHF,BFPAT              * Total daily rate
.
          PACK      SAVKEY27,BFCLM,BFHFUND,PTBFHTYP,BFATYPE,BFBTYPE:
                             PTBFCNID,BFENDDY,SP70
          MOVE      KEY18,SAVKEY18
.
.         HEA Fees Estimate - check Basic cover rate using Shared bedtype
.
          MOVE      TWO,FEESTYPE            * Flag for perdiem rate
          CALL      HBAS00000               * Check HEA Basic rate
.
          MOVE      SAVBTYP,PMFSBTYP        * bed type
          CALL      WRDC0000                * write accommodation charges
          COMPARE   FIVE,CFEETYP
          GOTO      GBFE9999 IF EQUAL
.
          IF        BFENDDY<FORM5A
            MOVE      BFENDDY,SAVEND
            MOVE      SAVKEY27,KEY27
            MOVE      SAVKEY18,KEY18
            CALL      RDSBFEE1
            GOTO      GBFE2000              * Get next stepdown fee
          ENDIF
          MOVE      ZERO,SAVEND
GBFE9999  RETURN
+
.******************************************************************************
.*                                  WRDC0000                                  *
.*                  Write the daily charges to pmsfesaf                       *
.******************************************************************************
WRDC0000  MOVE      PMFSBTYP,KEY3           * Save bed type
.
          MOVE      ZERO,FORM5
          PACK      KEY17,PMFDQUOT,Z70
          CALL      RSPMFES1
          CALL      RPPMFES1
          BRANCH    OVRCD,WRDC2000
.
          MATCH     PMFDQUOT,PMFSQUOT
          GOTO      WRDC2000 IF NOT EQUAL
.
          MOVE      PMFSUNIQ,FORM5
.
WRDC2000  MOVE      URNUMBER,PMFSURNO
          MOVE      PMFDQUOT,PMFSQUOT
          ADD       ONE,FORM5
          MOVE      FORM5,PMFSUNIQ
.
. -----   write bed fees as Daily charge  -----
.
          MOVE      KEY3,PMFSBTYP           * Bed type
          MOVE      TWO,PMFSRTYP
          MOVE      ZERO,PMFSLUMH
          MOVE      ZERO,PMFSLUMF
          MOVE      FORM5A,PMFSDAFR
          IF        FORM5A>BFENDDY
            MOVE      BFENDDY,PMFSDAFR
          ENDIF
          SUB       SAVEND,PMFSDAFR
.
          MOVE      ZERO,PMFSDAYT
          MOVE      BFPAT,PMFSDAYH           * patient + rebate portion
          MOVE      BFHF,PMFSDAHF
.
.         Zero HF amount for HEA Casepayment Fees Estimate with Basic cover
.
          MATCH     "1",PTCNIFCL
          IF        @EQUAL
            MATCH     "1",USEBASIC
            IF        @EQUAL
              IF        BASICFLG=1
                IF        CMXFLAG=1
                  MOVE      ZERO,PMFSDAHF
                ELSE
                  MOVE      BASICHF,PMFSDAHF   * Use Basic cover HF portion
                ENDIF
                MOVE      ZERO,BASICFLG
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      SP70,PMFSTHIT
          MOVE      ZERO,PMFSTHEH
          MOVE      ZERO,PMFSTHEF
          MOVE      ZERO,PMFSQUAN
          MOVE      ZERO,PMFSGSTA
          MOVE      SP70,PMFSGSTP
          MOVE      SP70,PMFSGSTC
.
          COMPARE   ONE,PMFDAGST             * check the admission GST
          GOTO      WRDC8000 IF NOT EQUAL    * no GST accommodation
.
          MOVE      PTCNAGST,KEY6
          CALL      AGST0000
          MOVE      PMFSDAYH,PMFSGSTA
          MULT      IBGEAMNT,PMFSGSTA
          DIV       "100.00",PMFSGSTA
.
WRDC8000  PACK      KEY17,PMFSQUOT,PMFSUNIQ
          CALL      RAPMFES1
          COMPARE   ZERO,OVRCD
          GOTO      WRDC2000 IF EQUAL
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRPMFES1
          TRAPCLR   IO
.
          GOTO      WRDC9999
.
WRDC9999  RETURN
+
.******************************************************************************
.*                                  WRLS0000                                  *
.*                  Write the lumpsum to pmsfesaf                             *
.******************************************************************************
WRLS0000  MOVE      ZERO,FORM5
          PACK      KEY17,PMFDQUOT,Z70
          CALL      RSPMFES1
          CALL      RPPMFES1
          BRANCH    OVRCD,WRLS2000
.
          MATCH     PMFDQUOT,PMFSQUOT
          GOTO      WRLS2000 IF NOT EQUAL
.
          MOVE      PMFSUNIQ,FORM5
.
WRLS2000  MOVE      URNUMBER,PMFSURNO
          MOVE      PMFDQUOT,PMFSQUOT
          ADD       ONE,FORM5
          MOVE      FORM5,PMFSUNIQ
.
. -----   write Lumpsum charge  -----
.
          MOVE      ONE,PMFSRTYP
          MOVE      BFPAT,PMFSLUMH
          MOVE      BFHF,PMFSLUMF
.
.         HEA Casepayment Fees Estimate with Basic cover
.
          MATCH     "1",PTCNIFCL
          IF        @EQUAL
            MATCH     "1",USEBASIC
            IF        @EQUAL
              IF        BASICFLG=1
                MOVE      BASICHF,PMFSLUMF     * Use Basic cover HF portion
                MOVE      ZERO,BASICFLG
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      ZERO,PMFSDAFR
          MOVE      ZERO,PMFSDAYT
          MOVE      ZERO,PMFSDAYH 
          MOVE      ZERO,PMFSDAHF
          MOVE      PMFDCMCD,PMFSTHIT
          MOVE      ZERO,PMFSTHEH
          MOVE      ZERO,PMFSTHEF
          MOVE      ZERO,PMFSQUAN
          MOVE      SP70,PMFSBTYP         * bed type
          MOVE      ZERO,PMFSGSTA
          MOVE      SP70,PMFSGSTP
          MOVE      SP70,PMFSGSTC
.
          COMPARE   ONE,PMFDAGST             * check the admission GST
          GOTO      WRLS8000 IF NOT EQUAL    * no GST accommodation
.
          MOVE      PTCNAGST,KEY6
          CALL      AGST0000
          MOVE      PMFSLUMH,PMFSGSTA
          MULT      IBGEAMNT,PMFSGSTA
          DIV       "100.00",PMFSGSTA
.
WRLS8000  PACK      KEY17,PMFSQUOT,PMFSUNIQ
          CALL      RAPMFES1
          COMPARE   ZERO,OVRCD
          GOTO      WRLS2000 IF EQUAL
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRPMFES1
          TRAPCLR   IO
.
          GOTO      WRLS9999
.
WRLS9999  RETURN
+
.******************************************************************************
. ------  Get Accommodation/Item GST -----
.******************************************************************************
AGST0000  MOVE     ZERO,IBGEAMNT            * Initialise GST percentage amount
.
          OPEN     IBAGEDA1,"ibagedaf"
          MOVE     ZERO,IBGEAMNT             * zero GST percentage
          PACK     KEY8,CCC,CYY,CMM,CDD
          REP      " 0",KEY8
          PACK     KEY14,KEY6,KEY8
          CALL     RDIBGE1
          COMPARE  ZERO,OVRCD
          GOTO     AGST9999 IF EQUAL         * found GST amount for current date
.
          CALL     RPIBGE1                   * read previous record
          BRANCH   OVRCD,AGST8000
.
          MATCH    KEY6,IBGECODE          * Same code ?
          GOTO     AGST9999 IF EQUAL
.
AGST8000  MOVE     ZERO,IBGEAMNT
.
AGST9999  RETURN
+
.******************************************************************************
. ------  Write to tempfile in MBS amount order ------
.******************************************************************************
WRTM0000  MOVE      "99999999.99",F82
          SUB       FORM82,F82
.
          MOVE      "9999",XMBS
          SUB       IAMT,XMBS
.
          MOVE      SP10,DXMBS
          MOVE      F82,DXAMT
          MOVE      XMBS,DXMBS
          MOVE      ZERO,FORM1
          MOVE      COUNTER,FORM2
          MOVE      FORM2,DUNIQ
.
          PACK      KEY18,DXAMT,DXMBS,DUNIQ
          WRITE     TTFILE,KEY18;DXAMT,DXMBS,DUNIQ,KEY9,XBAND:
                                 TFPAT1,TFPAT2,TFPAT3,TFPAT4,TFPAT5,TFPAT6:
                                 TFHF1,TFHF2,TFHF3,TFHF4,TFHF5,TFHF6:
                                 XQUAN,XGSTA,XGSTC,XBTYP:
                                 XKGST
WRTM9999  RETURN
.
RSTM0000  RESET     KEY18
          READ      TTFILE,KEY18;;
          RETURN
.
RKTM0000  MOVE      ZERO,OVRCD
          READKS    TTFILE;DXAMT,DXMBS,DUNIQ,KEY9,XBAND:
                                 TFPAT1,TFPAT2,TFPAT3,TFPAT4,TFPAT5,TFPAT6:
                                 TFHF1,TFHF2,TFHF3,TFHF4,TFHF5,TFHF6:
                                 XQUAN,XGSTA,XGSTC,XBTYP:
                                 XKGST
          GOTO      OVERCOND IF OVER
          RETURN
.
DETM0000  MOVE      ZERO,OVRCD
          RESET     KEY18
          DELETE    TTFILE,KEY18
          RETURN
+
.------------------------------------------------------------------------------
.* Temp file IO
.------------------------------------------------------------------------------
RSTEMP1   RESET     KEY9
          READ      MCFILE,KEY9;;
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    MCFILE;XCHARGE,XMCCNT
          GOTO      OVERCOND IF OVER
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          READ      MCFILE,KEY9;XCHARGE,XMCCNT
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   MOVE      ZERO,OVRCD
          WRITE     MCFILE,KEY9;XCHARGE,XMCCNT
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   MOVE      ZERO,OVRCD
          UPDATE    MCFILE;XCHARGE,XMCCNT
          GOTO      OVERCOND IF OVER
          RETURN
.
DETEMP1   RESET     KEY9
          DELETE    MCFILE,KEY9
          RETURN
.
.******************************************************************************
.*                       Deleting an indexed file                             *
.******************************************************************************
KILL0000  CLOSE     TTFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEC,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
.
KILM0000  CLOSE     MCFILE
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMED,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
KILM9999  RETURN
+
.******************************************************************************
.*                       Creating an indexed file                             *
.******************************************************************************
CREA0000  CALL      KILL0000                * deleting temporary file
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEC,CMDLINE
          APPEND    " 150 U1-18",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      TTFILE,FNAMEC
          PACK      KEY18,SP70
          CALL      RSTM0000
CREA2000  CALL      RKTM0000
          BRANCH    OVRCD,CREA9999
.
          PACK      KEY18,DXAMT,DXMBS,DUNIQ,SP70
          MATCH     SP70,KEY18
          GOTO      CREA9999 IF EQUAL
          CALL      DETM0000
          GOTO      CREA2000
.
CREA9999  RETURN
.
.******************************************************************************
.*                       Creating an indexed file for Casemix misc.items      *
.******************************************************************************
CRET0000  CALL      KILM0000                * deleting temporary file
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMED,CMDLINE
          APPEND    " 14 U1-9",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          OPEN      MCFILE,FNAMED
          PACK      KEY9,SP70
          CALL      RSTEMP1
CRET2000  CALL      RKTEMP1
          BRANCH    OVRCD,CRET9999
.
          PACK      KEY9,XCHARGE,XMCCNT,SP70
          MATCH     SP70,KEY9
          GOTO      CRET9999 IF EQUAL
          CALL      DETEMP1
          GOTO      CRET2000
.
CRET9999  RETURN
.
+
.------------------------------------------------------------
.         Generate Fees Information
.------------------------------------------------------------
GFINF000  MOVE      " 95",PRXCODE                * set sector for locking
          CALL      GETSLK00                     * lock sector
          READ      CONTROLF,NINTY5;*153,PTCNQUOT
          ADD       ONE,PTCNQUOT
          WRITAB    CONTROLF,NINTY5;*153,PTCNQUOT
          CALL      RELSLK00                     * release lock
.         
          SUB       ONE,PTCNQUOT          * get quote number
.
          OPEN      IBAGEDA1,"ibagedaf"
          OPEN      PATCTFA2,"patctfaf"
          OPEN      PATLSDA1,"patlsdaf"
          OPEN      PATASDA1,"patasdaf"
          OPEN      PATBFEE1,"patbfeef"
          OPEN      PMSFENA1,"pmsfenaf"
          READ      CONTROLF,TWENTY;*193,PTCNDCLM
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          IF        IBCNMHOS=1
            OPEN      PATHSPA1,"pathspaf"
            MOVE      WBSEHOSP,KEY3
            CALL      RDPTHSP1
            IF        OVRCD=0
              MOVE      PTHSDCLM,PTCNDCLM     * default claim code charging
            ENDIF
          ENDIF
.
          CALL      CLPMFI00              * clear file variables
.
          MOVE      PTCNQUOT,PMFIFINO
          MOVE      URNUMBER,PMFIURNO
          MOVE      ADMISSNO,PMFIADMN
.
          CALL      MVPMFI00              * Update fields from cgi-vars
.
          PACK      KEY12,PMFIFINO,SP20
          MOVE      PMFIFINO,PMFNQUOT
          MOVE      PTITL,PMFNTITL
          MOVE      PSNAME,PMFNSNAM
          MOVE      PGNAME,PMFNGNAM
          MOVE      PADD1,PMFNADD1
          MOVE      PADD2,PMFNADD2
          MOVE      PSUBRB,PMFNSUBR
          MOVE      PPOST,PMFNPOST
.
          CALL      RAPMFEN1
          IF        OVRCD=1
            CALL      WRPMFEN1           * write detail record
          ELSE
            CALL      UPPMFEN1           * record already exists
          ENDIF
.
          CALL      PFINF000             * Process fees information
GFINF999  RETURN
+
.------------------------------------------------------------
.         Process Fees Information
.------------------------------------------------------------
PFINF000  CALL      CMISC000             * Calculate Misc.charge
.
          CALL      NTHFS000             * Get non-casemix theatre fees
          CALL      GTHEF000             * Generate theatre fees
.
          CALL      GSCLS000             * Get suggested classif. (cat A)
.
          CALL      MNBDF000             * Generate Bed fee 
.
          CALL      WFINF000             * Write Fees Information record
.
          CALL      KILL0000                * deleting temporary file
.
PFINF999  RETURN
+
.******************************************************************************
.*                                  GSCLS000                                  *
.*                     Get suggested classification (cat A)                   *
.******************************************************************************
GSCLS000  PACK      KEY18,SP20
          CALL      RSTM0000
          CALL      RKTM0000                * Read first tempfile record
          BRANCH    OVRCD,GSCLS999
.
          MOVE      SP70,ATYPE
          COMPARE   ONE,PTCNAUAT
          GOTO      GSCLS999 IF NOT EQUAL
.
          PACK      KEY17,KEY9,ADATE,SP70
          CALL      PATITMRD                * If not in 'patsgcaf'
          BRANCH    OVRCD,GSCLS999
.
          MOVE      ICLSS,ATYPE          * Get suggested class
          MOVE      PMFISTAY,FORM3A
          IF        FORM3A=0
            MOVE      PTITDCSC,ATYPE
          ENDIF
.
          COMPARE   ONE,PTSGCOPT            * Using suggested class.?
          GOTO      GSCLS800 IF NOT EQUAL
.
          OPEN      PATSGCA1,"patsgcaf"
          PACK      KEY18,PMFICLTY,PMFIHFND,KEY9,SP20
          CALL      RDPTSGC1                * Get suggested adm. type
          COMPARE   ZERO,OVRCD
          GOTO      GSCLS200 IF EQUAL
.
          MATCH     SP70,PMFIHFND
          GOTO      GSCLS800 IF EQUAL
.
          MOVE      SP70,PTSGCLMN           * blank claim
          PACK      KEY18,PTSGCLMN,PMFIHFND,KEY9,SP20
          CALL      RDPTSGC1                * Get suggested adm. type
          COMPARE   ZERO,OVRCD
          GOTO      GSCLS200 IF EQUAL
.
          MOVE      SP70,PTSGFUND           * blank health fund
          PACK      KEY18,PMFICLTY,PTSGFUND,KEY9,SP20
          CALL      RDPTSGC1                * Get suggested adm. type
          BRANCH    OVRCD,GSCLS800
.
GSCLS200  MOVE      PTSGCLSS,ATYPE
.
GSCLS800  MATCH     SP70,ATYPE
          GOTO      GSCLS999 IF EQUAL
          MOVE      ATYPE,PMFIATYP
GSCLS999  RETURN
+
.******************************************************************************
.*                                  NTHFS000                                  *
.*                        Get Non Casemix Theatre Fee                         *
.******************************************************************************
NTHFS000  CALL      CREA0000                * Create tempfile
          OPEN      TTFILE,FNAMEC
          MOVE      ZERO,COUNTER
.
. ----- Get the item details -----
.
NTHFS100  MOVE      ZERO,FORM2
          MOVE      SP70,KEY1
          IF        DCFLAG=1
            MOVE      "D",KEY1
          ENDIF
          ADD       ONE,COUNTER
          MOVE      COUNTER,MBSCOUNT
          MOVE      SP10,KEY9
          MOVE      THEFEE[MBSCOUNT],KEY9
.
          MATCH     SP9,KEY9
          GOTO      NTHFS999 IF EQUAL       * Blank item, exit
          MATCH     Z70,KEY9
          GOTO      NTHFS999 IF EQUAL       * Blank item, exit
.
          PACK      KEY17,KEY9,PMFIADAT
          CALL      PATITMRD                * Read the item file
          BRANCH    OVRCD,NTHFS999
.
. ----- Get the default theatre classification -----
.
          MOVE      ZERO,CNTRCEFR
          MOVE      PMFIADAT,CEFFDATE
          MOVE      PMFIADAT,TSRVDATE       * Service date
          PACK      KEY18,PMFICLTY,PMFIHFND,PMFIHFTB,KEY1,SP20
          CALL      GETTFEES
.
. ----- write to tempfile in MBS amount order -----
.
          MOVE      TFPAT1,FORM82
          ADD       TFHF1,FORM82
          MOVE      SP10,XBAND
          MOVE      ONE,XQUAN
.
          MOVE      COUNTER,MBSCOUNT
          MOVE      THEQUA[MBSCOUNT],XQUAN
          MOVE      GSTAPL[MBSCOUNT],XGSTA
          MOVE      GSTCOD[MBSCOUNT],XGSTC
          MOVE      GSTTKY[MBSCOUNT],XKGST
          MOVE      SP70,XBTYP              * Theatre fee from Booking flag
.
          CALL      WRTM0000                * write to tempfile
          GOTO      NTHFS100
.
NTHFS999  RETURN
+
.******************************************************************************
.*                                  GTHEF000                                  *
.*                    Generate  Theatre Fee                                   *
.******************************************************************************
GTHEF000  
.
. ------  Read through tempfile -----
.
          MOVE      ZERO,TEMPETCP
          MOVE      ZERO,TEMPETCH
          MOVE      ZERO,COUNTER
          MOVE      SP20,KEY18
          CALL      RSTM0000
.
GTHEF200  ADD       ONE,COUNTER
          CALL      RKTM0000
          BRANCH    OVRCD,GTHEF999
.
          MATCH     SP70,KEY9
          GOTO      GTHEF200 IF EQUAL
.
          MOVE      ZERO,BFPAT
          LOAD      BFPAT,COUNTER,TFPAT1,TFPAT2,TFPAT3,TFPAT4,TFPAT5,TFPAT6
.
          MOVE      ZERO,BFHF
          LOAD      BFHF,COUNTER,TFHF1,TFHF2,TFHF3,TFHF4,TFHF5,TFHF6
.>>>>     ADD       BFHF,BFPAT
.
          MOVE      XQUAN,FORM3               * Quantity
          MOVE      ZERO,FORM12P2             * GST Amount
          MATCH     "1",XGSTA
          IF        @EQUAL
            MOVE      XGSTC,KEY6
            CALL      AGST0000
            ASSIGN    (BFPAT+BFHF),FORM12P2
            MULT      IBGEAMNT,FORM12P2
            DIV       "100.00",FORM12P2
          ENDIF
.
. ------  Print theatre fee details -----
.
          STORE     KEY9,COUNTER,PMFITHF1,PMFITHF2,PMFITHF3:
                                 PMFITHF4,PMFITHF5,PMFITHF6
          STORE     FORM3,COUNTER,PMFITHQ1,PMFITHQ2,PMFITHQ3:
                                 PMFITHQ4,PMFITHQ5,PMFITHQ6
          STORE     FORM12P2,COUNTER,PMFITHG1,PMFITHG2,PMFITHG3:
                                 PMFITHG4,PMFITHG5,PMFITHG6
          ASSIGN    (FORM3 * (BFPAT+FORM12P2)),TEMPETCP
          ASSIGN    (FORM3 * BFHF),TEMPETCH
.
          ADD       TEMPETCP,PMFIETCP
          ADD       TEMPETCH,PMFIETCH
.
          COMPARE   SIX,COUNTER
          GOTO      GTHEF200 IF LESS        * < 6 items, get next
.
GTHEF999  RETURN
+
.******************************************************************************
.*                                  CMISC000                                  *
.*                          Get Miscellaneous charge                          *
.******************************************************************************
CMISC000  MOVE      ZERO,COUNTER
          MOVE      ZERO,TEMPEMCP
          MOVE      ZERO,TEMPEMCH
          MOVE      ZERO,DCFLAG
          MOVE      PMFISTAY,FORM3A
          IF        FORM3A=0
            MOVE      ONE,DCFLAG            * Daycase
          ENDIF
.
CMISC100  COMPARE   THREE,COUNTER
          GOTO      CMISC999 IF EQUAL
.
          ADD       ONE,COUNTER
          MOVE      COUNTER,MBSCOUNT
          MOVE      SP10,KEY9
          MOVE      MISCHG[MBSCOUNT],KEY9
.
          MATCH     Z70,KEY9
          GOTO      CMISC100 IF EQUAL
          MATCH     SP70,KEY9
          GOTO      CMISC100 IF EQUAL
.
          MOVE      KEY9,MCHARGE
          CALL      GMISCC00                * Get misc.charge
          IF        EXIT=1
            MOVE      ZERO,MPATPOR          * no charge
            MOVE      ZERO,MHFPOR
          ENDIF
.
CMISC400  MOVE      MISQUA[MBSCOUNT],FORM3
.>>>>     ADD       MHFPOR,MPATPOR
.
          MOVE      SP70,KEY1
          MOVE      GSTAPM[MBSCOUNT],KEY1
.
          MOVE      ZERO,FORM12P2
          MATCH     "1",KEY1
          IF        @EQUAL
            MOVE      GSTCOM[MBSCOUNT],KEY6
            CALL      AGST0000
            ASSIGN    (MPATPOR+MHFPOR),FORM12P2
            MULT      IBGEAMNT,FORM12P2
            DIV       "100.00",FORM12P2
          ENDIF
.
. ------  store Miscellaneous charge details -----
.
          STORE     KEY9,COUNTER,PMFIMCC1,PMFIMCC2,PMFIMCC3
          STORE     FORM3,COUNTER,PMFIQUA1,PMFIQUA2,PMFIQUA3
          STORE     FORM12P2,COUNTER,PMFIITG1,PMFIITG2,PMFIITG3
          ASSIGN    (FORM3 * (MPATPOR+FORM12P2)),TEMPEMCP
          ASSIGN    (FORM3 * MHFPOR),TEMPEMCH
.
          ADD       TEMPEMCP,PMFIEMCP
          ADD       TEMPEMCH,PMFIEMCH
.
          GOTO      CMISC100
.
CMISC999  RETURN
+
.------------------------------------------------------------
. Get the appropriate miscellaneous charges record
.------------------------------------------------------------
GMISCC00  MOVE      ZERO,EXIT
          MOVE      MCHARGE,KEY9            * (MCHARGE re-use in PATMCHRD)
.
          UNPACK    SP20,PTHFBCAT      * change "H/F Table" to "Table Type"
          PACK      KEY14,PMFIHFND,PMFIHFTB,SP20
          CALL      RDFUND1            * "Table Type code" now in PTHFBCAT
.
          PACK      KEY29,PMFICLTY,PMFIHFND,PTHFBCAT,KEY9,CURRDATE,SP10
          CALL      PATMCHRD                * find Miscellaneous Charge Item
          COMPARE   ZERO,EXIT
          GOTO      GMISCC99 IF EQUAL
.
.         No record with health fund detials - try a blank health fund
.
GMISCC10  PACK      KEY29,PMFICLTY,SP8,SP3,PTHFBCAT,KEY9,CURRDATE,SP10
          CALL      PATMCHRD              * find Miscellaneous Charge Item
          COMPARE   ZERO,EXIT
          GOTO      GMISCC99 IF EQUAL
.
.         No record with health fund detials - last chance, try default claim
.
GMISCC20  PACK      KEY29,PTCNDCLM,SP8,SP3,PTHFBCAT,KEY9,CURRDATE,SP10
          CALL      PATMCHRD              * find Miscellaneous Charge Item
.
.                 * EXIT is set - 0=OK, 1=invalid, inactive or not in date range
GMISCC99  RETURN
+
.******************************************************************************
.*                                  MNBDF000                                  *
.*                      Generate Non Casemix Bed Fee                          *
.******************************************************************************
MNBDF000  MOVE      ZERO,FORM1
          MOVE      ZERO,FORM2
          MOVE      ONE,FORM4
          MATCH     SP70,PMFIBTYP
          GOTO      MNBDF999 IF EQUAL
.
          MOVE      PMFIBTYP,SAVBTYP
          MOVE      PMFISTAY,FORM3A
          IF        DCFLAG=1
            MOVE      ONE,FORM3A           * daycase
          ENDIF
          CALL      GBFET000               * get bed fee for bed type 1
          BRANCH    DCFLAG,MNBDF999        * Daycase can only have one bedtype
.
          MOVE      ONE,FORM1
.
MNBDF999  RETURN
+
.******************************************************************************
.         Get a bed fee for a bed type
.******************************************************************************
GBFET000  MOVE      ZERO,FORM2
          PACK      KEY23,PMFICLTY,PMFIHFND,PMFIHFTB,PMFIATYP,SAVBTYP,SP70
.
          COMPARE   NINE,CFEETYP
          GOTO      GBFET100 IF EQUAL      * Johns Hopkins bed fees
.
          PACK      KEY9,PMFICLTY,PMFIHFND,SP70
          IF        ASTAT=0
            MOVE      TWO,ASTAT            * Set to current status
          ENDIF
          CALL      GETCNEFF               * Get Contract Effective From
          BRANCH    EXIT,GBFET700
.
          MOVE      SP70,PTHFBCAT
          PACK      KEY14,PMFIHFND,PMFIHFTB,SP10
          CALL      RDFUND1
          PACK      KEY18,PMFICLTY,PMFIHFND,PTHFBCAT,PMFIATYP,SAVBTYP,SP70
.
GBFET100  IF        CFEETYP=5
            OPEN      JHSBFEA1,"jhsbfeaf"
            PACK      KEY26,KEY23,SP70
            CALL      RSJHBFE1              * Position on & read the bed fee
          ELSE
            PACK      KEY27,KEY18,SP70
            CALL      RDSBFEE1              * Position on & read the bed fee
          ENDIF
GBFET200  IF        CFEETYP=5
            CALL      RKJHBFE1
            BRANCH    OVRCD,GBFET300
            PACK      DIM23,BFCLM,BFHFUND,BFHFTAB,BFATYPE,BFBTYPE,SP70
            MATCH     KEY23,DIM23
            GOTO      GBFET300 IF NOT EQUAL
          ELSE
            CALL      RDKBFEE1
            BRANCH    OVRCD,GBFET300
            PACK      DIM18,BFCLM,BFHFUND,PTBFHTYP,BFATYPE,BFBTYPE,SP70
            MATCH     KEY18,DIM18
            GOTO      GBFET300 IF NOT EQUAL
.
            MOVE      PMFIADAT,CEFFDATE       * Use admission date
            MOVE      PTBFCNID,KEY6           * Contract ID
            CALL      VALCONTR                * Validate Contract ID
            BRANCH    EXIT,GBFET200           * not valid
            MOVE      PTBFCNID,CONTRCID       * Contract ID
          ENDIF
.
          GOTO      GBFET600                * Same bed fee rate, print
.
GBFET300  ADD       ONE,FORM2
          BRANCH    FORM2,GBFET400,GBFET500
.
.         DISPLAY   *P1:24,*EL,*B,"No bed fee detail found.  Default not set ":
.                                 "up.  ";
          GOTO      GBFET700
.
GBFET400  PACK      KEY18,PMFICLTY,SP6,SP3,PMFIATYP,SAVBTYP,SP70
          PACK      KEY23,PMFICLTY,SP6,SP8,PMFIATYP,SAVBTYP,SP70
          GOTO      GBFET100
.
GBFET500  PACK      KEY18,PTCNDCLM,SP6,SP3,PMFIATYP,SAVBTYP,SP70
          PACK      KEY23,PTCNDCLM,SP6,SP8,PMFIATYP,SAVBTYP,SP70
          GOTO      GBFET100
.
GBFET600  COMPARE   ZERO,BFENDDY
          GOTO      GBFET800 IF NOT EQUAL
.
          IF        FORM1=0
            BRANCH    DCFLAG,GBFET800       * day case rate
          ENDIF
          GOTO      GBFET200
.
GBFET700  MOVE      ZERO,BFPAT
          MOVE      ZERO,BFHF
.
GBFET800  MOVE      SAVBTYP,PMFIBTYP        * bed type
.>>>>     ADD       BFHF,BFPAT              * Total daily rate
.
. ------  store accommodation charges -----
.
          CALL      SACCM000                * write accommodation charges
.
GBFET999  RETURN
+
.******************************************************************************
.*                                  SACCM000                                  *
.*                         store accommodation charges                        *
.******************************************************************************
SACCM000  MOVE      ZERO,FORM12P2
.
          MATCH     "1",PMFIAGST             * check the accommodation GST
          IF        @EQUAL 
            MOVE      PTCNAGST,KEY6
            CALL      AGST0000
            ASSIGN    (BFPAT+BFHF),FORM12P2      * apply GST to total
            MULT      IBGEAMNT,FORM12P2
            DIV       "100.00",FORM12P2
          ENDIF
.
          ASSIGN    (FORM3A * (BFPAT+FORM12P2)),PMFIEACP   * patient portion
          ASSIGN    (FORM3A * BFHF),PMFIEACH               * rebate portion
.
SACCM999  RETURN
+
.******************************************************************************
.*                                  WFINF000                                  *
.*                    Write fees information to pmsfidaf                      *
.******************************************************************************
WFINF000  MOVE      ZERO,FORM3
.>>>>     PACK      KEY15,PMFIFINO,Z70
.>>>>     CALL      RSPMFID1
.>>>>     CALL      RPPMFID1
.>>>>     BRANCH    OVRCD,WRINF100
.         
.>>>>     MOVE      PMFIUNIQ,FORM3
.         
WRINF100  ADD       ONE,FORM3
          MOVE      FORM3,PMFIUNIQ
.
          ASSIGN    (PMFISGAM+PMFIAGAM+PMFIANGA+PMFIPHYI+PMFIPHYS+PMFIXRAY+PMFISPLB+PMFIMISC+PMFIPHAR+PMFIPROS+PMFIHEXC+PMFIEACP+PMFIETCP+PMFIPHCA+PMFIEDVA),PMFIECOS
.
          ASSIGN    (PMFISGAM+PMFIPROS+PMFIHEXC+PMFIEACP+PMFIETCP+PMFIEDVA),PMFITPOA
.
          MATCH     SP70,PMFICLTY
          GOTO      WFINF200 IF EQUAL
.
          PACK      KEY5,ANSC,ANSL,PMFICLTY,SP70
          CALL      RDCODE1                 * Read the codes file
          BRANCH    OVRCD,WFINF200
.
          MATCH    "10",THCSCOD
          IF       !@EQUAL
            MATCH    "11",THCSCOD           * Uninsured
            GOTO     WFINF200 IF NOT EQUAL
          ENDIF
.
.>>>>>    ADD       PMFIAGAM,PMFITPOA
.
WFINF200  CALL      IBACLOCK
          PACK      PMFICDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMFICDAT
          CLOCK     TIME,CTIMEIS
          MOVE      CTIMEIS,PMFICTIM
          MOVE      USERID,PMFICUID
.
          PACK      KEY15,PMFIFINO,PMFIUNIQ
          CALL      RAPMFID1
          IF        OVRCD=1
            CALL      WRPMFID1
          ENDIF
.
.>>>>     CALL      PRTFIN00                * print fee information form
.
WFINF999  RETURN
+
.******************************************************************************
.*                                  PRTFIN00                                  *
.*                    print fee information form                              *
.******************************************************************************
PRTFIN00  MOVE      ZERO,F3
          MOVE      ZERO,COUNT
PRTFIN10  ADD       ONE,COUNT
          IF        COUNT<=95
            MATCH     SP70,STATNARR[COUNT]
            GOTO      PRTFIN99 IF EQUAL
            MOVE      SCHDPARR[COUNT],SCHDPRNT   * Array of selected Printer
            MOVE      SCHDCARR[COUNT],SCHDCOPY   * Number of Copies
            MOVE      STATNARR[COUNT],STATNCOD   * Stationery Code
            CALL      ADDPRN00
            CALL      UPDEFP00
            GOTO      PRTFIN10
          ENDIF
.
PRTFIN99  RETURN
+
.******************************************************************************
.* Delete all fee estimation details for this quote number                    *
.******************************************************************************
DFES0000  PACK      KEY12,QUOTENUM,SP70
          CALL      DEPMFED1
.
          PACK      KEY12,QUOTENUM,SP70
          CALL      DEPMFEN1
.
DFES1000  PACK      KEY17,QUOTENUM,SP70
          CALL      RSPMFES1
          CALL      RKPMFES1
          BRANCH    OVRCD,DFES9999
.
          MOVE      QUOTENUM,DIM12
          MATCH     PMFSQUOT,DIM12
          GOTO      DFES9999 IF NOT EQUAL
.
          PACK      KEY17,PMFSQUOT,PMFSUNIQ,SP70
          CALL      DEPMFES1
.
          GOTO      DFES1000
.
DFES9999  RETURN
+
.******************************************************************************
.         Setup pmsfedaf fields
.******************************************************************************
SFED0000  MOVE      AURNO,PMFDURNO        * U/R Number
          MOVE      SP70,PMFDNONS         * No of days
          MOVE      ADATE,PMFDADAT        * Admission date
          MOVE      AFUNDH,PMFDHFUN       * Health Fund
          MOVE      AFNDTB,PMFDHFTB       * Health Fund table
          MOVE      ZERO,PMFDAGST         * Accommodation GST
.
          MATCH     "2",PTCNIFCL
          GOTO      SFED1000 IF NOT EQUAL * Not SJOG
.
.         SJOG IFC use the GST Accommodation from Admission
.
          MATCH     "Y",PTMIGSTA
          IF        @EQUAL
            MOVE      "1",PMFDAGST        * Accommodation GST
          ENDIF
.
          MOVE      ADMISSNO,DIM8VISN
          MOVE      DIM8VISN,KEY8
          CALL      RDPTELG1
          IF        OVRCD=0
            PACK      PTMIPCMX,PTELCMXC,SP70
            MATCH     SP70,PTMIPCMX       * Provisional Casemix code
            IF        !@EQUAL
              PACK      PTELECLS,PTELECLS,SP70
              SQUEEZE   PTELECLS
              MOVE      PTELECLS,ASTAY    * Use the keyin Exp.LOS
              MOVE      ASTAY,PMFDNONS
            ENDIF
          ENDIF
.
SFED1000  MOVE      PTMIPCMX,PMFDCMCD     * Casemix code
.
          MOVE      ACLAIM,PMFDCLTY       * Claim type
          MOVE      SP70,PMFDBDTY         * Bed type
          MOVE      ATYPE,PMFDATYP        * Admissin type
          UNPACK    SP70,PMFDBDT2,PMFDBDT3,PMFDBDT4,PMFDBDT5  * Bed type
          MOVE      ZERO,PMFDNDS2         * No of days stay
          MOVE      ZERO,PMFDNDS3         * No of days stay
          MOVE      ZERO,PMFDNDS4         * No of days stay
          MOVE      ZERO,PMFDNDS5         * No of days stay
          MOVE      ZERO,PMFDXCSS         * Excess
          MOVE      SP70,PMFDEXPD         * Expiry date
          MOVE      SP70,PMFDDOCT         * Doctor Code
          MOVE      SP70,PMFDACHR         * Use keyin Suggested class
          PACK      PMFDSPAR,SP70
.
SFED9999  RETURN
+
.-----------------------------------------------------------------
.         Set admission fields from Fees Estimate screen
.-----------------------------------------------------------------
FESC0000  MOVE      SP70,USEBASIC
          MOVE      PMFDUBAS,USEBASIC
          REP       " 0",USEBASIC
.
          CALL      CLPATMIS
.
          MOVE      ZERO,ASTAY
          IF        PMFDNONS=0
            MOVE      ONE,DCFLAG            * Daycase
          ENDIF
          MOVE      PMFDNONS,ASTAY
.
          MOVE      PMFDURNO,AURNO
          MOVE      PMFDADAT,ADATE
          MOVE      PMFDADAT,ADATE        * Admission date
          MOVE      PMFDHFUN,AFUNDH       * Health Fund
          MOVE      PMFDHFTB,AFNDTB       * Health Fund table
.
          MOVE      PMFDCMCD,PTMIPCMX     * Casemix code
          MATCH     SP70,PMFDCMCD
          IF        !@EQUAL
            OPEN      PATCMCA1,"patcmcaf"
            MOVE      PMFDCMCD,KEY9
            CALL      RDPTCMC1
            IF        OVRCD=0
              MATCH     "2",PTCNIFCL
              IF        !@EQUAL
                MOVE      PTCAELOS,ASTAY
              ENDIF   
            ENDIF
          ENDIF
.
          MOVE      PMFDCLTY,ACLAIM       * Claim type
          MOVE      PMFDATYP,ATYPE        * Admissin type
.
FESC9999  RETURN
+
.--------------------------------------------------------------------------
.         Process Informed Financial Consent for Healthscope/SJGHC layout
.         PTCNIFCL=1 for Healthscope and PTCNIFCL=2 for SJGHC
.
.         Note: HEA IFC does not have Basic/Default cover (only SJGHC)
.               HEA Fees Estimate has Basic/Default cover
.--------------------------------------------------------------------------
GFEH0000  OPEN      PATCTFA2,"patctfaf"
          OPEN      PATLSDA1,"patlsdaf"
          OPEN      PATASDA1,"patasdaf"
          OPEN      PATBFEE1,"patbfeef"
          OPEN      PMSFENA1,"pmsfenaf"
          READ      CONTROLF,TWENTY;*193,PTCNDCLM
          READ      CONTROLF,NINTY8;*130,PTCNAGST
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          IF        IBCNMHOS=1
            OPEN      PATHSPA1,"pathspaf"
            MOVE      WBSEHOSP,KEY3
            CALL      RDPTHSP1
            IF        OVRCD=0
              MOVE      PTHSDCLM,PTCNDCLM     * default claim code charging
            ENDIF
          ENDIF
.
          MATCH     "2",PTCNIFCL
          GOTO      GFEH1000 IF NOT EQUAL        * not using SJOGH layout
.
          MATCH     "PATWEB76",PRGID
          GOTO      GFEH1000 IF NOT EQUAL        * Not from Fees Estimate
.
.         SJOGH layout - Generate IFC from Fees Estimates
.
          IF        UPDATEST=1
            CALL      DFES0000                 * Delete existing estimate
            GOTO      GFEH0200
          ENDIF
.
          MOVE      " 95",PRXCODE                * set sector for locking
          CALL      GETSLK00                     * lock sector
          READ      CONTROLF,NINTY5;*153,PTCNQUOT
          ADD       ONE,PTCNQUOT
          WRITAB    CONTROLF,NINTY5;*153,PTCNQUOT
          CALL      RELSLK00                     * release lock
.
          SUB       ONE,PTCNQUOT          * get quote number
.
GFEH0200  MOVE      URNUMBER,PMFDURNO
.
          IF        UPDATEST=1
            MOVE      QUOTENUM,PMFDQUOT       * Update existing fee estimate
          ELSE
            MOVE      PTCNQUOT,PMFDQUOT       * Generate new fee estimate
          ENDIF
.
          CALL      UPPMFD00                * Update fields from cgi-var
.
          CALL      FESC0000                * Set admission fields for IFC
          GOTO      GFEH2000
.
.         Generate IFC from Eligibility Check
.
GFEH1000  MOVE      ADMISSNO,DIM8VISN          * use Admission no as Quote no.
          MOVE      DIM8VISN,KEY8
          CALL      RDMISS1
          IF        OVRCD=1
            CALL      CLPATMIS                 * Clear fields
          ENDIF
.
          MOVE      FORM8,QUOTENUM
          MOVE      QUOTENUM,KEY12
          REP       " 9",KEY12
          MOVE      KEY12,QUOTENUM
          MOVE      QUOTENUM,PMFDQUOT
.
          MATCH     "2",PTCNIFCL
          IF        @EQUAL
            BRANCH    UPDATEST,GFEH9999
          ENDIF
.
          CALL      DFES0000                   * Delete existing estimate    
.
          MOVE      URNUMBER,PMFDURNO
          CALL      SFED0000                   * Setup pmsfedaf fields
.
          MOVE      QUOTENUM,PMFDQUOT
.
GFEH2000  PACK      PMFDCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMFDCDAT
          CLOCK     TIME,PMFDCTIM
          MOVE      USERID,PMFDCUID
.
          PACK      KEY12,PMFDQUOT,SP20
          CALL      RAPMFED1
          IF        OVRCD=1
            CALL      WRPMFED1           * write detail record
          ELSE
            CALL      UPPMFED1           * record already exists
          ENDIF
.
          PACK      KEY12,PMFDQUOT,SP20
          MOVE      PMFDQUOT,PMFNQUOT
          MOVE      PTITL,PMFNTITL
          MOVE      PSNAME,PMFNSNAM
          MOVE      PGNAME,PMFNGNAM
          MOVE      PADD1,PMFNADD1
          MOVE      PADD2,PMFNADD2
          MOVE      PSUBRB,PMFNSUBR
          MOVE      PPOST,PMFNPOST
          MOVE      PBDATE,PMFNBDAT
          MOVE      PTELEP,PMFNPHON
.
          CALL      RAPMFEN1
          IF        OVRCD=1
            CALL      WRPMFEN1           * write detail record
          ELSE
            CALL      UPPMFEN1           * record already exists
          ENDIF
.
          MATCH     "2",PTCNIFCL
          IF        !@EQUAL
            MOVE      SP70,USEBASIC      * HEA 'IFC' not using BasicCover rate
          ENDIF
.
          CALL      HFES0000             * Process fees estimation
          MOVE      ZERO,EXIT
.
GFEH9999  RETURN
+
.--------------------------------------------------------------------------
.         Fees Estimate for Healthsc and SJGHC layout (also SJGHC IFC)
.--------------------------------------------------------------------------
HFES0000  MOVE      ZERO,EPMFLAG
          MOVE      ZERO,DCFLAG           * Overnight
          MOVE      ZERO,BASICFLG
          MOVE      ZERO,BOOKFLAG
.
          IF        ASTAY=0
            MOVE      ONE,DCFLAG            * Same day
          ENDIF
.
          MOVE      SP70,CASMXKEY
          MOVE      ZERO,CMXFLG
.
          MATCH     "2",PTCNIFCL
          IF        !@EQUAL
            MATCH     "Y",PTMICMXP          * Casepayment (Y/N)
            GOTO      HFES1000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,PTMIPCMX         * Provisional Casemix code
          GOTO      HFES1000 IF EQUAL     * blank
.
          MATCH     "2",PTCNIFCL
          GOTO      HFES0200 IF NOT EQUAL
          MATCH     "PATWEB78",PRGID
          GOTO      HFES0200 IF NOT EQUAL
.
.         SJOG - if Casemix is populated from Eligibilty check, NO TheatreeFee
.                (it was generating using per diem theatre fee,which would have 
.                 no fees setup anyway)
.
          CALL      PTHE0000              * Process Theatre/Misc.
          MOVE      ONE,BOOKFLAG
.
.         SJOG Casepayment
.         Fees Estimate is using the Keyin Adm.type
.         IFC is using the Adm.type (suggested class.) of first keyin MBS,
.             if no Theatre fee entered, use the first code Cat A Ind16=O
.
          MOVE      SP70,KEY9
          IF        MBSINDEX<>0
            PACK      KEY9,FMBSCODE[1],SP70  * use theatre fee from booking
          ENDIF
.
          MATCH     SP70,KEY9
          IF        @EQUAL
            PACK      KEY9,THEFEE[1],SP70  * First keyin Theatre fee
          ENDIF
.
          MATCH     SP70,KEY9
          IF        @EQUAL
            MOVE      KEY9,AMBS
          ENDIF
          CALL      DATY0000               * Get default Adm.Type Ind16=O
          MOVE      ATYPE,PMFDATYP
.
.         Check for Equitable Payment Mode (EPM) Health Fund
.
HFES0200  MATCH     SP70,PMFDHFUN
          IF        !@EQUAL
            MOVE      "0000    ",KEY8
            PACK      KEY14,PMFDHFUN,KEY8,SP10
            CALL      RDFUND1
            IF        OVRCD=0
              MOVE      "w0",KEY2
              PACK      KEY5,KEY2,PTHFOCGR
              CALL      RDCODE1
              IF        OVRCD=0
                MATCH     "E",TCINDC1
                IF        @EQUAL
                  MOVE      ONE,EPMFLAG   * EPM based contracts
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
.         Generate Casepayment
.
          MOVE      ONE,CMXFLG
          CALL      CPAY0000              * Get Casepayment Accommodation fees
          GOTO      HFES3000
.
.         Generate Bed fees (per diem)
.
HFES1000  MATCH     "2",PTCNIFCL
          GOTO      HFES1400 IF NOT EQUAL
.
.
.         SJOG :
.         Fees Estimate is using the Keyin Adm.type
.         IFC is using the Adm.type (suggested class.) of first keyin MBS,
.             if no Theatre fee entered, use the first code Cat A Ind16=O
.
          MATCH     "PATWEB78",PRGID
          GOTO      HFES1400 IF NOT EQUAL
.
          MOVE      SP70,KEY9
          IF        MBSINDEX<>0
            PACK      KEY9,OMBSCODE[1],SP70  * use theatre fee from booking
          ENDIF
.
          MATCH     SP70,KEY9
          IF        @EQUAL
            MATCH     Z70,THEFEE[1]
            IF        !@EQUAL
              PACK      KEY9,THEFEE[1],SP70  * First keyin Theatre fee
            ENDIF
          ENDIF
.
          MATCH     SP70,KEY9
          IF        !@EQUAL
            MOVE      KEY9,AMBS
          ENDIF
.
          CALL      DATY0000               * Get default Adm.Type Ind16=O
          MOVE      ATYPE,PMFDATYP
.
.         HEA
.
HFES1400  MATCH    SP70,AMBS
          IF       !@EQUAL
            CALL      SCBF0000            * Bed fees using Suggested Class.
          ELSE
.
.           Generate Theatre fees before Bed fees, to get items sorted
.           for the suggested classification
.
            MATCH     "2",PTCNIFCL
            IF        @EQUAL
              MATCH     "PATWEB76",PRGID
              IF        @EQUAL
                IF        BOOKFLAG=0
                  CALL      PTHE0000              * Process Theatre/Misc.
                  MOVE      ONE,BOOKFLAG
                  MOVE      PMFDATYP,ATYPE      * Same day Admission type
                ENDIF
              ENDIF
            ENDIF
.
            MOVE      ATYPE,SAVATYPE      * Same day Admission type
            CALL      ATBF0000            * Bed fees using Admission type
          ENDIF
.
.         Theatre/Procedure
.
HFES3000  MATCH     "2",PTCNIFCL
          IF        !@EQUAL
            MATCH     "Y",PTMICMXP          * Casemixpayment (Y/N)
            GOTO      HFES3400 IF NOT EQUAL
          ENDIF
.
.         SJOG has multiple keyin Theatre and Misc.items
.
          MATCH     "2",PTCNIFCL
          GOTO      HFES3200 IF NOT EQUAL
.
          IF        BOOKFLAG=0
            CALL      PTHE0000              * Process Theatre/Misc.
          ENDIF
          MOVE      ZERO,BOOKFLAG
.
          MATCH     "PATWEB76",PRGID
          IF        @EQUAL
            CALL      GPPR0000              * process Pharmacy/Prosthet.
            GOTO      HFES9999              * NO IFC for Fees Estimate
          ENDIF
.
          GOTO      HFES4000
.
.         Fees Estimate layout
.
HFES3200  MATCH     SP70,AMBS
          IF        !@EQUAL
            MATCH     SP70,PTMIPCMX
            IF        @EQUAL
              CALL      TFES0000          * Theatre fees
              GOTO      HFES4000
            ELSE
              CALL      CASF0000          * Casemix Theatre fees
              GOTO      HFES4000
            ENDIF
          ENDIF
          GOTO       HFES3600
.
HFES3400  MATCH     SP70,AMBS
          IF        !@EQUAL
            CALL      TFES0000          * Theatre fees
            GOTO      HFES4000
          ENDIF
.
HFES3600  MOVE      SP70,DIM9
          MOVE      FIVE,FORM1A          * Record type=5 for Theatre/procedure
          MOVE      SP70,KEY3             * blank bed type
          MOVE      ZERO,BFPAT
          MOVE      ZERO,BFHF
          MOVE      SP70,CONTRCID
          CALL      WRFS0000              * write to pmsfesaf
.
.         Process IFC Other/Consumables/Prosthetic
.
HFES4000  MATCH     "1",PTCNIFCL
          IF        @EQUAL
            MATCH     "PATWEB78",PRGID
            IF        @EQUAL
              CALL      CMSC0000           * Calculate Misc.charge
            ENDIF
          ENDIF
.
          MOVE      SP70,PTHFBCAT
          PACK      KEY14,AFUNDH,AFNDTB,SP10
          CALL      RDFUND1
.
          MATCH     "2",PTCNIFCL
          IF        !@EQUAL
            MATCH     "Y",PTMICMXP          * Casemixpayment (Y/N)
            GOTO      HFES6000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,PTMIPCMX
          IF        !@EQUAL
            MOVE      "1",KEY1                   * Record type
            PACK      DIM10,KEY1,PTMIPCMX,SP70   * use Casemix code
            GOTO      HFES8000
          ENDIF
.
HFES6000  MATCH     SP70,AMBS
          IF        !@EQUAL
            MOVE      "2",KEY1                 * Record type
            PACK      DIM10,KEY1,AMBS,KEY9     * use provision MBS
            GOTO      HFES8000
          ENDIF
.
          MOVE      "3",KEY1                     * Record type
          PACK      DIM10,KEY1,ATYPE,SP70        * use admission type
.
HFES8000  MATCH     "2",PTCNIFCL
          GOTO      HFES9999 IF EQUAL            * SJOG has no IFC theatre
.
          CALL      RIFC0000                     * Get IFC Charges
HFES9999  RETURN
+
.------------------------------------------------------------
.         Get default Adm.type for IFC
.------------------------------------------------------------
DATY0000  MOVE      "A ",KEY2
          PACK      KEY5,KEY2,SP70
          CALL      RDSCODE1
DATY1000  CALL      RDKCODE1
          BRANCH    OVRCD,DATY9000
.
          MATCH     "A ",TCODE
          GOTO      DATY9000 IF NOT EQUAL
.
          MATCH     "I",PTCOACTV
          GOTO      DATY1000 IF EQUAL      * In-active
.
          IF        ASTAY=0
            MATCH     "S",TCINDC16
            GOTO      DATY1000 IF NOT EQUAL
          ELSE
            MATCH     "O",TCINDC16
            GOTO      DATY1000 IF NOT EQUAL
          ENDIF
          MOVE      ACODE,ATYPE
          MOVE      ZERO,EXIT
          GOTO      DATY9999
.
DATY9000  MOVE      ONE,EXIT
DATY9999  RETURN
+
.------------------------------------------------------------
.         Process Theatre and Misc. for SJOG Fees estimate
.------------------------------------------------------------
PTHE0000  CALL      CMSC0000             * Calculate Misc.charge
.
          IF        CMXFLAG=1
            CALL      CTHF0000           * Get casemix theatre fees
            CALL      CTHE0000           * Generate theatre fees
          ELSE
            CALL      NTHF0000           * Get non-casemix theatre fees
            CALL      GTHE0000           * Generate theatre fees
          ENDIF
.
.        Only Fees Estimate template will have field for 
.        Use keyin adm.type for charging ?
.
          PACK      PMSFD029,PMSFD029,SP70
          MATCH     "1",PMSFD029
          IF        !@EQUAL
            CALL      GSCL0000             * Get suggested classif. (cat A)
          ENDIF
.
PTHE9999  RETURN
+
.------------------------------------------------------------
.         Checking for any Pharmacy or Prosthesis
.------------------------------------------------------------
RIFC0000  MOVE      ZERO,F1
          MOVE      ZERO,FORM1A
          MOVE      ZERO,IFCFLAG
          OPEN      PATIFCA1,"patifcaf"
          PACK      DIM12,ACLAIM,AFUNDH,PTHFBCAT,SP70
RIFC1000  PACK      KEY25,DIM12,DIM10,SP70
          CALL      RSPTIFC1
RIFC2000  CALL      RKPTIFC1
          BRANCH    OVRCD,RIFC3000
.
          PACK      KEY12,PTFCCLTY,PTFCFUND,PTFCHTAB,SP70
          MATCH     DIM12,KEY12
          GOTO      RIFC3000 IF NOT EQUAL
.
          PACK      KEY10,PTFCRTYP,PTFCCMAT,SP70
          MATCH     DIM10,KEY10           * checking casemix code/MBS/Adm.type
          GOTO      RIFC3000 IF NOT EQUAL
.
          MOVE      "ce",KEY2
          PACK      KEY5,KEY2,PTFCCTYP
          CALL      RDCODE1
          BRANCH    OVRCD,RIFC2000
.
          BRANCH    IFCFLAG,RIFC2100,RIFC9999
          GOTO      RIFC2200
.
RIFC2100  COMPARE   SIX,FORM1A
          GOTO      RIFC2400 IF EQUAL    * done 'Other Pharmacy' already
.
.         Look for 'Other Pharmacy'
.
          MATCH     "O",TCINDC1
          GOTO      RIFC2000 IF NOT EQUAL
          GOTO      RIFC2300
.
.
RIFC2200  MATCH     "O",TCINDC1
          GOTO      RIFC2400 IF NOT EQUAL
.
.         Other Pharmacy
.
RIFC2300  MOVE      SIX,FORM1A          * Record type=6 for IFC Other
          ADD       ONE,IFCFLAG
          MOVE      ZERO,F1
          PACK      DIM12,ACLAIM,AFUNDH,PTHFBCAT,SP70
          GOTO      RIFC2800
.
RIFC2400  MATCH     "P",TCINDC1
          GOTO      RIFC2000 IF NOT EQUAL
.
.         Prosthesis
.
          MOVE      SEVEN,FORM1A          * Record type=7 for IFC Prosthesis
          ADD       ONE,IFCFLAG
          MOVE      ZERO,F1
          PACK      DIM12,ACLAIM,AFUNDH,PTHFBCAT,SP70
.
RIFC2800  MOVE      SP70,KEY3             * blank bed type
.
          MOVE      PTFCPAMT,BFPAT
          ADD       PTFCRAMT,BFPAT
          MOVE      PTFCRAMT,BFHF
.
          MOVE      SP70,CONTRCID
          PACK      DIM9,PTFCCTYP,SP70    * Charge type
          CALL      WRFS0000              * write to pmsfesaf
.
.         Check if both Other Phamacy and Prosthesis charges have been found
.
          COMPARE   TWO,IFCFLAG
          GOTO      RIFC1000 IF NOT EQUAL
.
          GOTO      RIFC9999
.
RIFC3000  ADD       ONE,F1
          BRANCH    F1,RIFC4000,RIFC5000,RIFC6000
          GOTO      RIFC9999
.
RIFC4000  PACK      DIM12,ACLAIM,SP70    * blank health fund
          GOTO      RIFC1000
.
RIFC5000  PACK      DIM12,SP3,AFUNDH,PTHFBCAT,SP70    * blank claim code
          GOTO      RIFC1000
.
RIFC6000  PACK      DIM12,SP70           * blank claim code/health fund
          GOTO      RIFC1000
.
RIFC9999  RETURN
+
.------------------------------------------------------------
.         Get Theatre/Procedure for Casepayment
.------------------------------------------------------------
CASF0000  MOVE      ZERO,EXIT
          MOVE      ZERO,FORM1
.
          MOVE      ZERO,TCFORM6
          PACK      KEY5,ANSC,ANSL,PMFDCLTY
          CALL      RDCODE1                 * Read the codes file
          BRANCH    OVRCD,CASF9999
.
          MOVE      TCFORM6,HFBAND
          MATCH     SP6,PMFDHFUN
          IF        !@EQUAL
            PACK      KEY14,PMFDHFUN,ZERO8
            CALL      RDFUND1               * Read the health fund file
            BRANCH    OVRCD,CASF9999
.
            MOVE      "03",KEY2     * check HF history for Theatre charging ind.
            PACK      KEY17,PMFDHFUN,KEY2,PMFDADAT,SP70
            CALL      PATDDHRD              * Check HF History file
          ENDIF
          MOVE      HFBAND,THCFLAG
.
          MOVE      SP10,PTHFBCAT
          MOVE      SP10,PTCTTABT
          MATCH     SP6,PMFDHFUN
          IF        !@EQUAL
            PACK      KEY14 WITH PMFDHFUN,PMFDHFTB,SP70
            CALL      RDFUND1
            MOVE      PTHFBCAT,PTCTTABT       * health fund table type
          ENDIF
.
          MOVE      AMBS,KEY9
          PACK      KEY17,KEY9,PMFDADAT,SP70
          CALL      PATITMRD                * Read the item file
          BRANCH    OVRCD,CASF9999
.
          MOVE      SP5,KEY3
          LOAD      KEY3,THCFLAG,IBAND1,IBAND2,IBAND3,IBAND4,IBAND5:
                                 IBAND6,IBAND7,IBAND8,IBAND9,IBAND10:
                                 IBAND11,IBAND12,IBAND13,IBAND14,IBAND15:
                                 IBAND16
.
          MOVE      ONE,FORM2               * Default to first sequence number
          MOVE      ZERO,ERRFLAG
.
          MOVE      ZERO,KEY1               * SJOG using Overnight rate
.         MATCH     "2",PTCNIFCL
.         IF        !@EQUAL
            MOVE      DCFLAG,KEY1
.         ENDIF
          PACK      KEY33,CONTRCID,PMFDCLTY,PMFDHFUN,PTCTTABT:
                          KEY3,SP9,KEY1,FORM2
CASF1000  CALL      RDPTCTF2                * Read the DRG/MBS theatre fee file
          COMPARE   ZERO,OVRCD
          GOTO      CASF8000 IF EQUAL       * Valid record found, print details
.
. ----- Combination not setup, try with different defaults -----
.
          ADD       ONE,FORM1
          BRANCH    FORM1,CASF5000,CASF5500
          MOVE      ZERO,PTCTDPAT
          MOVE      ZERO,PTCTDREB
          GOTO      CASF8000
.
. ----- Try combination without health fund code -----
.
CASF5000  PACK      KEY33,CONTRCID,PMFDCLTY,SP6,SP3,KEY3,SP9,KEY1,FORM2
          GOTO      CASF1000
.
. ----- Try combination with zero claim type & no health code -----
.
CASF5500  PACK      KEY33,CONTRCID,PTCNDCLM,SP6,SP3,KEY3,SP9,KEY1,FORM2
          GOTO      CASF1000
.
CASF8000  MOVE      FIVE,FORM1A          * Record type=5 for Theatre/procedure
          MOVE      SP70,KEY3             * blank bed type
          MOVE      PTCTDPAT,BFPAT
          ADD       PTCTDREB,BFPAT
          MOVE      PTCTDREB,BFHF
          MOVE      AMBS,DIM9             * Provisional MBS
          CALL      WRFS0000              * write to pmsfesaf
.
CASF9999  RETURN
+
.------------------------------------------------------------
.         Get Theatre/Procedure
.------------------------------------------------------------
TFES0000  MOVE      SP70,KEY1
.
          IF        ASTAY=0
            MOVE      "D",KEY1              * Daycase
          ENDIF
.
          MOVE      AMBS,KEY9
          PACK      KEY17,KEY9,PMFDADAT
          CALL      PATITMRD                * Read the item file
          BRANCH    OVRCD,TFES9999
.
. ----- Get the default theatre classification -----
.
          MOVE      ZERO,CNTRCEFR
          MOVE      PMFDADAT,CEFFDATE
          MOVE      PMFDADAT,TSRVDATE       * Service date
          PACK      KEY18,PMFDCLTY,PMFDHFUN,PMFDHFTB,KEY1,SP20
          CALL      GETTFEES
          BRANCH    EXIT,TFES9999
.
          MOVE      TFPAT1,FORM82
          ADD       TFHF1,FORM82
.
          MOVE      FIVE,FORM1A          * Record type=5 for Theatre/procedure
          MOVE      TFPAT1,BFPAT            * patient portion
          ADD       TFHF1,BFPAT             * Health fund rebate
.
          MOVE      TFHF1,BFHF              * Health fund rebate
          MOVE      SP70,KEY3               * blank bed type
          MOVE      AMBS,DIM9             * Provisional MBS
          MOVE      MBSCONTR,CONTRCID
          CALL      WRFS0000                * write to pmsfesaf

TFES9999  RETURN
+
.------------------------------------------------------------
.         Get Accommodation for Casepayment
.------------------------------------------------------------
CPAY0000  MOVE      SP10,PTHFBCAT
          MOVE      SP10,PTCTTABT
          MATCH     SP6,PMFDHFUN
          IF        !@EQUAL
            PACK      KEY14 WITH PMFDHFUN,PMFDHFTB,SP70
            CALL      RDFUND1
            MOVE      PTHFBCAT,PTCTTABT       * health fund table type
          ENDIF
.
          MOVE      SP70,CONTRCID           * Initialise Contract Id
          PACK      KEY18,PMFDCLTY,PMFDHFUN,PMFDCMCD,SP70
          MOVE      PMFDADAT,CEFFDATE       * Admission date
          CALL      VALCMXFN                * Validate casemix funding
          BRANCH    EXIT,CPAY9000
.
          IF        ASTAY=0
            MOVE      ONE,FORM1A            * Record type=1 for Sameday
            MATCH     "2",PTCNIFCL
            IF        @EQUAL
              MOVE      TWO,FORM1A          * Record type=2 for Sameday
            ENDIF
            CALL      CPSD0000              * Casepayment sameday
            GOTO      CPAY9999
          ENDIF
.
CPAY8200  CALL      CASP0000                * Overnight lumpsum + inlier
          GOTO      CPAY9999
.
CPAY9000  MOVE      ONE,EXIT
CPAY9999  RETURN
+
.---------------------------------------------------------------------
.         Casepayment Same day
.---------------------------------------------------------------------
CPSD0000  MOVE      ZERO,BFPAT
          MOVE      ZERO,BFHF
.
          OPEN      PATCMXA2,"patcmxaf"
.
          PACK      KEY24,CONTRCID,PMFDCLTY,PMFDHFUN,PMFDCMCD
          CALL      RDPTCMX2
          COMPARE   ZERO,OVRCD
          GOTO      CPSD1000 IF EQUAL
.
          PACK      KEY24,CONTRCID,PMFDCLTY,SP6,PMFDCMCD
          CALL      RDPTCMX2
          COMPARE   ZERO,OVRCD
          GOTO      CPSD1000 IF EQUAL
.
          PACK      KEY24,CONTRCID,PTCNDCLM,SP6,PMFDCMCD
          CALL      RDPTCMX2
.
.         Get lumpsum amount
.
CPSD1000  PACK      KEY27,CONTRCID,PMFDCLTY,PMFDHFUN,PTCTTABT,PMFDCMCD
          CALL      RDPTLSD1                * Read the lumpsum daycase file
          COMPARE   ZERO,OVRCD
          GOTO      CPSD4000 IF EQUAL
.
          PACK      KEY27,CONTRCID,PMFDCLTY,SP6,SP3,PMFDCMCD
          CALL      RDPTLSD1                * Read the lumpsum daycase file
          COMPARE   ZERO,OVRCD
          GOTO      CPSD4000 IF EQUAL
.
          PACK      KEY27,CONTRCID,PTCNDCLM,SP6,SP3,PMFDCMCD
          CALL      RDPTLSD1                * Read the lumpsum daycase file
          BRANCH    OVRCD,CPSD8000
.
CPSD4000  MOVE      PTLSLPAT,BFPAT          * patient portion
          ADD       PTLSLREB,BFPAT
          MOVE      PTLSLREB,BFHF           * Health fund rebate
.
          MATCH     "2",PTCNIFCL
          GOTO      CPSD6000 IF NOT EQUAL   * HEA IFC doesn't have Basic Cover
.
          MATCH     "1",USEBASIC
          IF        @EQUAL
            MOVE      BFHF,F12P2
            MOVE      BFPAT,FORM12P2
            CALL      CBTP0000             * Get special bed type
            MOVE      PRVBTYP,SBEDTYPE     * use private bed
            CALL      CWBFE000             * Get Basic Cover bed fees rates
            MOVE      F12P2,BFHF
            MOVE      FORM12P2,BFPAT       * restore Lumpsum + Inlier amount
          ENDIF
.
CPSD6000  MOVE      SP70,KEY3               * blank bed type
          MOVE      SP70,DIM9               * blank Theatre charge/MBS
          CALL      WRFS0000                * write to pmsfesaf
          GOTO      CPSD9999
.
.         Set up a zero Same day rate
.
CPSD8000  MOVE      ZERO,BFPAT              * patient portion
          MOVE      ZERO,BFHF               * Health fund rebate
          MOVE      SP70,KEY3               * blank bed type
          MOVE      SP70,DIM9               * blank Theatre charge/MBS
          CALL      WRFS0000                * write to pmsfesaf
.
CPSD9999  RETURN
+
.---------------------------------------------------------------------
.         Casepayment Overnight Lumpsum + Inlier + Additional charges
.---------------------------------------------------------------------
CASP0000  MOVE      ZERO,BFPAT
          MOVE      ZERO,BFHF
          MOVE      ZERO,EPMPATAM
          MOVE      ZERO,EPMREBAM
.
          PACK      KEY27,CONTRCID,PMFDCLTY,PMFDHFUN,PTCTTABT,PMFDCMCD
          CALL      RDPTHLF1                * Read the lumpsum overnight file
          COMPARE   ZERO,OVRCD
          GOTO      CASP1000 IF EQUAL
.
          PACK      KEY27,CONTRCID,PMFDCLTY,SP6,SP3,PMFDCMCD
          CALL      RDPTHLF1                * Read the lumpsum overnight file
          COMPARE   ZERO,OVRCD
          GOTO      CASP1000 IF EQUAL
.
          PACK      KEY27,CONTRCID,PTCNDCLM,SP6,SP3,PMFDCMCD
          CALL      RDPTHLF1                * Read the lumpsum overnight file
          BRANCH    OVRCD,CASP2010
.
CASP1000  MOVE      KEY27,CASMXKEY          * save casemix key
.
          MOVE      PTHLLPAT,BFPAT          * Lumpsum overnight
          ADD       PTHLLREB,BFPAT          * Patient + Rebate portion
          MOVE      PTHLLREB,BFHF           * Rebate portion
.
CASP2010  IF        EPMFLAG=1
            MOVE      BFPAT,EPMPATAM        * EPM contract amount
          ENDIF
.
.         MATCH     "2",PTCNIFCL
.         IF        @EQUAL
.           COMPARE   ZERO,ASTAY
.           GOTO      CASP2020 IF EQUAL
.         ENDIF
          DIV       ASTAY,BFPAT             * Daily charge for lumpsum
.
CASP2020  IF        EPMFLAG=1
            MOVE      BFHF,EPMREBAM         * EPM contract amount
          ENDIF
.         MATCH     "2",PTCNIFCL
.         IF        @EQUAL
.           COMPARE   ZERO,ASTAY
.           GOTO      CASP2040 IF EQUAL
.         ENDIF
          DIV       ASTAY,BFHF              * Daily charge for lumpsum
.
. ----- Read the inlier file -----
.
CASP2040  MOVE      ZERO,F1
          PACK      CASMXKEY,CASMXKEY,SP70
          MATCH     SP70,CASMXKEY
          IF        !@EQUAL
            PACK      KEY27,CASMXKEY,PMFDCMCD,SP70
            PACK      KEY31,KEY27,SP70
          ELSE
            PACK      KEY27,CONTRCID,PMFDCLTY,PMFDHFUN,PTCTTABT,PMFDCMCD,SP30
            PACK      KEY31,CONTRCID,PMFDCLTY,PMFDHFUN,PTCTTABT,PMFDCMCD,SP30
          ENDIF
CASP2100  CALL      RSPTHDF1                * Position on & read the daily fee
          CALL      RKPTHDF1                  inliers file
          BRANCH    OVRCD,CASP2200
.
          PACK      DIM27,PTHDCNID,PTHDCLMN,PTHDHFND,PTHDTABT,PTHDCASM
          MATCH     DIM27,KEY27
          GOTO      CASP4000 IF EQUAL
.
CASP2200  MATCH     SP70,CASMXKEY
          GOTO      CASP6000 IF NOT EQUAL
.
          ADD       ONE,F1
          BRANCH    F1,CASP3100,CASP3200
          GOTO      CASP6000
.
CASP3100  PACK      KEY31,CONTRCID,PMFDCLTY,SP6,SP3,PMFDCMCD,SP30
          PACK      KEY27,CONTRCID,PMFDCLTY,SP6,SP3,PMFDCMCD,SP30
          GOTO      CASP2100
.
CASP3200  PACK      KEY31,CONTRCID,PTCNDCLM,SP6,SP3,PMFDCMCD,SP30
          PACK      KEY27,CONTRCID,PTCNDCLM,SP6,SP3,PMFDCMCD,SP30
          GOTO      CASP2100
.
CASP4000  MOVE      KEY27,CASMXKEY          * save casemix key
          ADD       PTHDDPAT,BFPAT          * add Inlier to Lumpsum
.
          IF        EPMFLAG=1
            CALL      OINL0000              * Get Outlier for EPM Contract
            GOTO      CASP5000
          ENDIF
.
CASP5000  ADD       PTHDDREB,BFPAT
          ADD       PTHDDREB,BFHF
.
CASP6000  MOVE      BFPAT,TEMPEMCP          * save Lumpsum + Inlier
          MOVE      BFHF,TEMPEMCH           * save Lumpsum + Inlier
.
          IF        EPMFLAG=1
            DIV       ASTAY,EPMPATAM           * divided by LOS
            DIV       ASTAY,EPMREBAM
          ENDIF
.
          CALL      CPAD0000                * Overnight Add on for all bed types
          BRANCH    FORM3A,CASP7000
.
.         Need to do Shared room line - no shared rate from patient contract
.
          MOVE      SAVBTYP,KEY3            * Private bed type
          MOVE      ONE,FORM1A              * Record type=1 Shared room
          IF        EPMFLAG=1
            MOVE      EPMPATAM,BFPAT        * Lumpsum + Stepdown Inlier
            MOVE      EPMREBAM,BFHF
          ELSE
            MOVE      TEMPEMCP,BFPAT        * Lumpsum + Inlier amount
            MOVE      TEMPEMCH,BFHF         * Lumpsum + Inlier amount
          ENDIF
.
          MATCH     "1",USEBASIC
          IF        @EQUAL
            MOVE      SAVBTYP,SBEDTYPE
            MOVE      BFHF,F12P2
            MOVE      BFPAT,FORM12P2
            CALL      CWBFE000             * Get Basic Cover bed fees rates
            MOVE      F12P2,BFHF
            MOVE      FORM12P2,BFPAT       * restore Lumpsum + Inlier amount
          ENDIF
.
          MOVE      SAVBTYP,KEY3            * Private bed type
          MOVE      SP70,DIM9               * blank Theatre charge/MBS
          CALL      WRFS0000                * write to pmsfesaf
.
.
CASP7000  BRANCH    FORM2A,CASP8000         * Private room has been done
.
.         Need to do Private room line - no private rate from patient contract
.
          MOVE      PRVBTYP,KEY3            * Private bed type
          MOVE      TWO,FORM1A              * Record type=2 for Private room
          IF        EPMFLAG=1
            MOVE      EPMPATAM,BFPAT        * Lumpsum + Stepdown Inlier
            MOVE      EPMREBAM,BFHF
          ELSE
            MOVE      TEMPEMCP,BFPAT        * Lumpsum + Inlier amount
            MOVE      TEMPEMCH,BFHF         * Lumpsum + Inlier amount
          ENDIF
.
          MATCH     "1",USEBASIC
          IF        @EQUAL
            MOVE      PRVBTYP,SBEDTYPE
            MOVE      BFHF,F12P2
            MOVE      BFPAT,FORM12P2
            CALL      CWBFE000             * Get Basic Cover bed fees rates
            MOVE      F12P2,BFHF
            MOVE      FORM12P2,BFPAT       * restore Lumpsum + Inlier amount
          ENDIF
.
          CALL      CDVA0000              * Check for DVA patients
.
          MOVE      PRVBTYP,KEY3            * Private bed type
          MOVE      SP70,DIM9               * blank Theatre charge/MBS
          CALL      WRFS0000                * write to pmsfesaf
.
.         Need to do a Daycase lumpsum rate line for Overnight patient
.
CASP8000  
.         MOVE      ZERO,FORM1A             * Record type=0 for Daycase rate
.         CALL      CPSD0000                * Casepayment sameday
CASP9999  RETURN
+
.---------------------------------------------------------------------------
.         Casepayment Overnight Additional charges for all bed types
.---------------------------------------------------------------------------
CPAD0000  CALL      CBTP0000                * Get special bed type
.
          MOVE      ZERO,F1
          MOVE      ZERO,FORM3A             * flag for finding shared room
          MOVE      ZERO,FORM2A             * flag for finding private room
.
          MOVE      ZERO,FORM1
          PACK      CASMXKEY,CASMXKEY,SP70
          MATCH     SP70,CASMXKEY
          IF        !@EQUAL
            PACK      KEY27,CASMXKEY,PMFDCMCD,SP70
            PACK      KEY34,CASMXKEY,PMFDCMCD,SP70
          ELSE
            PACK      KEY27,CONTRCID,PMFDCLTY,PMFDHFUN,PTCTTABT,PMFDCMCD,SP30
            PACK      KEY34,CONTRCID,PMFDCLTY,PMFDHFUN,PTCTTABT,PMFDCMCD,SP30
          ENDIF
          MOVE      KEY27,SAVKEY27
.
CPAD1000  CALL      RSPTAFE1                * Position on & read the additional
          CALL      RKPTAFE1                * charges overnight file
          BRANCH    OVRCD,CPAD2000
.
          PACK      DIM27,PTFECNID,PTFECLMN,PTFEHFND,PTFETABT,PTFECASM
          MATCH     DIM27,KEY27
          GOTO      CPAD6000 IF EQUAL
.
CPAD2000  MATCH     SP70,CASMXKEY
          GOTO      CPAD9999 IF NOT EQUAL
.
          ADD       ONE,F1
          BRANCH    F1,CPAD2100,CPAD2200
          GOTO      CPAD9999
.
CPAD2100  PACK      KEY34,CONTRCID,PMFDCLTY,SP6,SP3,PMFDCMCD,SP30
          PACK      KEY27,CONTRCID,PMFDCLTY,SP6,SP3,PMFDCMCD,SP30
          GOTO      CPAD1000
.
CPAD2200  PACK      KEY34,CONTRCID,PTCNDCLM,SP6,SP3,PMFDCMCD,SP30
          PACK      KEY27,CONTRCID,PTCNDCLM,SP6,SP3,PMFDCMCD,SP30
          GOTO      CPAD1000
.
CPAD6000  COMPARE   ONE,EPMFLAG
          GOTO      CPAD6120 IF NOT EQUAL   * Not EPM Contract
.
          MATCH     SP70,SAVBTYP
          IF        !@EQUAL
            MATCH     PTFEBTYP,SAVBTYP      * Shared room
            GOTO      CPAD6100 IF EQUAL
          ENDIF
.
          MATCH     SP70,PRVBTYP
          IF        !@EQUAL
            MATCH     PTFEBTYP,PRVBTYP      * Private room
            GOTO      CPAD6100 IF EQUAL
          ENDIF
          GOTO      CPAD6120
.
CPAD6100  MOVE      EPMPATAM,BFPAT          * stepdown inlier
          MOVE      EPMREBAM,BFHF
          GOTO      CPAD6130
.
CPAD6120  MOVE      TEMPEMCP,BFPAT          * Lumpsum + Inlier amount
          MOVE      TEMPEMCH,BFHF           * Lumpsum + Inlier amount
.
CPAD6130  ADD       PTFEDPAT,BFPAT        * Add additional charge for a bedtyp
          ADD       PTFEDREB,BFPAT        * Add additional charge for a bedtyp
          ADD       PTFEDREB,BFHF         * Add additional charge for a bedtyp
.
          MOVE      PTFEBTYP,SBEDTYPE
.
          MATCH     "1",USEBASIC
          IF        @EQUAL
            MOVE      BFHF,F12P2
            MOVE      BFPAT,FORM12P2
            CALL      CWBFE000             * Get Basic Cover bed fees rates
            MOVE      F12P2,BFHF
            MOVE      FORM12P2,BFPAT       * restore Lumpsum + Inlier amount
          ENDIF
.
          MOVE      SBEDTYPE,KEY3           * bed type
          PACK      KEY5,ANSB,ANST,KEY3
          CALL      RDCODE1
          BRANCH    OVRCD,CPAD8000
.
          MATCH     "I",PTCOACTV
          GOTO      CPAD8000 IF EQUAL      * In-active
.
          COMPARE   ONE,TCFORM6
          GOTO      CPAD6200 IF EQUAL      * a valid estimates bedtype
          COMPARE   TWO,TCFORM6
          GOTO      CPAD8000 IF NOT EQUAL  * not valid fees estimates bedtype
.
.         Bed types are ordered as Shared,Private, ICU and SCN
.
CPAD6200  MATCH     SP70,SAVBTYP
          IF        !@EQUAL
            MATCH     SBEDTYPE,SAVBTYP      * Shared room
            IF        @EQUAL
              MOVE      ONE,FORM1A
              MOVE      SP70,SAVBTYP
              MOVE      ONE,FORM3A          * flag for shared room
              GOTO      CPAD7000
            ENDIF
          ENDIF
.
          MATCH     SP70,PRVBTYP
          IF        !@EQUAL
            MATCH     SBEDTYPE,PRVBTYP      * Private room
            IF        @EQUAL
              MOVE      TWO,FORM1A
              MOVE      SP70,PRVBTYP
              CALL      CDVA0000            * Check for DVA patients
              MOVE      ONE,FORM2A          * flag for Private room
              GOTO      CPAD7000
            ENDIF
          ENDIF
.
          MATCH     SP70,ICUBTYP
          IF        !@EQUAL
            MATCH     SBEDTYPE,ICUBTYP      * ICU Bed type
            IF        @EQUAL
              MOVE      TEMPEMCP,BFPAT      * Lumpsum + Inlier amount
              MOVE      TEMPEMCH,BFHF       * Lumpsum + Inlier amount
              CALL      HRAT0000            * Get Highest rate if required
              MOVE      THREE,FORM1A
              MOVE      SP70,ICUBTYP
              GOTO      CPAD7000
            ENDIF
          ENDIF
.
          MATCH     SP70,SCNBTYP
          IF        !@EQUAL
            MATCH     SBEDTYPE,SCNBTYP      * SCN Bed type
            IF        @EQUAL
              MOVE      TEMPEMCP,BFPAT      * Lumpsum + Inlier amount
              MOVE      TEMPEMCH,BFHF       * Lumpsum + Inlier amount
              CALL      HRAT0000            * Get Highest rate if required
              MOVE      THREE,FORM1A
              MOVE      SP70,SCNBTYP
              GOTO      CPAD7000
            ENDIF
          ENDIF
.
          MOVE      FOUR,FORM1A             * Other bed type
          MOVE      TEMPEMCP,BFPAT          * Lumpsum + Inlier amount
          MOVE      TEMPEMCH,BFHF           * Lumpsum + Inlier amount
          CALL      HRAT0000                * Get Highest rate if required
.
CPAD7000  MOVE      SP70,DIM9               * blank Theatre charge/MBS
          CALL      WRFS0000                * write to pmsfesaf
.
CPAD8000  MOVE      SAVKEY27,KEY27
          PACK      KEY34,KEY27,SBEDTYPE,Z70
          GOTO      CPAD1000
.
CPAD9999  RETURN
+
.---------------------------------------------------------------------------
.         Get Outlier amount for EPM Contract
.---------------------------------------------------------------------------
OINL0000  MOVE      ZERO,COUNTER
          MOVE      ASTAY,FORM4A
.
          COMPARE   ONE,PTHDDTYP
          GOTO      OINL2000 IF NOT EQUAL
.
          MOVE      PTHDEDAY,COUNTER
          SUB       PTHDEDAY,FORM4A
.
OINL1000  CALL      RKPTHDF1                 * inliers file
          BRANCH    OVRCD,OINL9999
.
          PACK      DIM27,PTHDCNID,PTHDCLMN,PTHDHFND,PTHDTABT,PTHDCASM
          MATCH     DIM27,KEY27
          GOTO      OINL9999 IF NOT EQUAL
.
          COMPARE   TWO,PTHDDTYP
          GOTO      OINL1000 IF NOT EQUAL    * not an Outlier Day type
.
OINL2000  COMPARE   ASTAY,PTHDEDAY
          GOTO      OINL4000 IF NOT LESS
.
          MOVE      ZERO,FORM4
          MOVE      PTHDEDAY,FORM4
.
          MOVE      ASTAY,FORM4A
          SUB       PTHDEDAY,FORM4A              * left over LOS
.
          SUB       COUNTER,FORM4
          IF        PTHDEDAY>0
            ADD       PTHDDREB,PTHDDPAT
            MULT      FORM4,PTHDDPAT
            ADD       PTHDDPAT,EPMPATAM
.
            MULT      FORM4,PTHDDREB
            ADD       PTHDDREB,EPMREBAM
            COMPARE   FORM4A,ZERO
            GOTO      OINL9999 IF NOT LESS
          ENDIF
          GOTO       OINL1000
.
OINL4000  ADD       PTHDDREB,PTHDDPAT
          MULT      FORM4A,PTHDDPAT
          ADD       PTHDDPAT,EPMPATAM          * patient portion
.
          MULT      FORM4A,PTHDDREB
          ADD       PTHDDREB,EPMREBAM          * rebate portion
.
OINL9999  RETURN
+
.------------------------------------------------------------------
.         Get highest rate from all the stepdown for the bed type
.------------------------------------------------------------------
HRAT0000  COMPARE   ONE,CMXFLG
          GOTO      HRAT9999 IF NOT EQUAL   * Not Casepayment
.
          MOVE      "BT",KEY2
          PACK      KEY5,KEY2,KEY3          * bed type
          CALL      RDCODE1
          BRANCH    OVRCD,HRAT9999
.
          MATCH     "I",PTCOACTV
          GOTO      HRAT9999 IF EQUAL      * In-active
.
          COMPARE   TWO,TCFORM6
          GOTO      HRAT9999 IF NOT EQUAL  * not a valid fees estimates bedtype
.
          MOVE      ZERO,TFORM12A
          MOVE      ZERO,F1
.
          PACK      KEY34,PTFECNID,PTFECLMN,PTFEHFND,PTFETABT,PTFECASM:
                          PTFEBTYP,SP70
          UNPACK    KEY34,KEY27
          CALL      RSPTAFE1                * Position on & read the additional
HRAT1000  CALL      RKPTAFE1                  charges overnight file
          BRANCH    OVRCD,HRAT2000
.
          PACK      DIM27,PTFECNID,PTFECLMN,PTFEHFND,PTFETABT,PTFECASM
          MATCH     DIM27,KEY27
          GOTO      HRAT2000 IF NOT EQUAL
          MATCH     PTFEBTYP,KEY3
          GOTO      HRAT2000 IF NOT EQUAL
.
          MOVE      PTFEDPAT,FORM12P2
          ADD       PTFEDREB,FORM12P2
          COMPARE   TFORM12A,FORM12P2
          GOTO      HRAT1000 IF LESS
.
          MOVE      FORM12P2,TFORM12A       * Highest rate
          PACK      KEY34,PTFECNID,PTFECLMN,PTFEHFND,PTFETABT,PTFECASM:
                          PTFEBTYP,DPTFEEDA,SP70     * save current rate
          MOVE      ONE,F1
          GOTO      HRAT1000
.
HRAT2000  COMPARE   ZERO,F1
          GOTO      HRAT9000 IF EQUAL
.
          CALL      RDPTAFE1                 * read the highest rate record
          BRANCH    OVRCD,HRAT9000
.
          ADD       PTFEDPAT,BFPAT    * Highest stepdown for the bed type
          ADD       PTFEDREB,BFPAT    * Highest stepdown for the bed type
          ADD       PTFEDREB,BFHF     * Highest stepdown for the bed type
.
HRAT9000  MOVE      KEY3,PTFEBTYP     * Restore bed type
HRAT9999  RETURN
+
.--------------------------------------------------------------
.         Get Accommodation for Bed fees using Suggested Class.
.--------------------------------------------------------------
SCBF0000  MOVE      ATYPE,SAVATYPE          * Same day suggested class
          PACK      KEY9,AMBS
.
          COMPARE   ZERO,ASTAY
          GOTO      SCBF8000 IF EQUAL       * Daycase doesn't use AMBS sug.class
.
          PACK      KEY17,KEY9,ADATE
          CALL      PATITMRD
          BRANCH    OVRCD,SCBF1000
.
          MATCH     SP70,ICLSS
          IF        !@EQUAL
            MOVE      ICLSS,ATYPE            * Overnight Suggested Class.
          ENDIF
.
SCBF1000  COMPARE   ONE,PTSGCOPT            * Using suggested class.?
          GOTO      SCBF8000 IF NOT EQUAL
.
          OPEN      PATSGCA1,"patsgcaf"
          PACK      KEY18,PMFDCLTY,PMFDHFUN,KEY9,SP20
          CALL      RDPTSGC1                * Get suggested adm. type
          COMPARE   ZERO,OVRCD
          GOTO      SCBF2000 IF EQUAL
.
          MATCH     SP70,PMFDHFUN
          GOTO      SCBF8000 IF EQUAL
.
          MOVE      SP70,PTSGCLMN           * blank claim
          PACK      KEY18,PTSGCLMN,PMFDHFUN,KEY9,SP20
          CALL      RDPTSGC1                * Get suggested adm. type
          COMPARE   ZERO,OVRCD
          GOTO      SCBF2000 IF EQUAL
.
          MOVE      SP70,PTSGFUND           * blank health fund
          PACK      KEY18,PMFDCLTY,PTSGFUND,KEY9,SP20
          CALL      RDPTSGC1                * Get suggested adm. type
          BRANCH    OVRCD,SCBF8000
.
SCBF2000  MATCH     SP70,PTSGCLSS
          IF        !@EQUAL
            MOVE      PTSGCLSS,ATYPE
            MOVE      PTSGCLSS,SAVATYPE
            MOVE      ATYPE,PMFDATYP
          ENDIF
.
SCBF8000  CALL      ATBF0000                * Get Accommodation for Sugg.class.
SCBF9999  RETURN
+
.------------------------------------------------------------
.         Get Accommodation for Bed fees using Adm.Type
.------------------------------------------------------------
ATBF0000  CALL      CBTP0000                * Get special bed type
.
          MOVE      ZERO,FORM1C
          MOVE      ZERO,FORM3A             * flag for finding shared room
          MOVE      ZERO,FORM2A             * flag for finding private room
          CALL      BFES0000                * get bed fees
.
.         if shared room rate was not setup, write a record with zero amount
.
          MOVE      PMFDATYP,DIM9       * store atype in Theatre charge/MBS
          IF        FORM3A=0
            MATCH     SP70,SAVBTYP
            IF        !@EQUAL
              MOVE      "1",FORM1A          * shared bed record type
              MOVE      SAVBTYP,KEY3        * bed type
              MOVE      ZERO,BFPAT          * Patient portion
              MOVE      ZERO,BFHF           * Rebate portion
              CALL      WRFS0000            * write to pmsfesaf
            ENDIF
          ENDIF
.
.         if private room rate was not setup, write a record with zero amount
.
          IF        FORM2A=0
            MATCH     SP70,PRVBTYP
            IF        !@EQUAL
              MOVE      "2",FORM1A          * private bed record type
              MOVE      PRVBTYP,KEY3        * bed type
              MOVE      ZERO,BFPAT          * Patient portion
              MOVE      ZERO,BFHF           * Rebate portion
              CALL      WRFS0000            * write to pmsfesaf
            ENDIF
          ENDIF
.
          MATCH     "2",PTCNIFCL
          GOTO      ATBF9999 IF EQUAL       * SJOG
.
.         Get 'Daycase Shared room rate' for Overnight
.
          IF        DCFLAG=0
            MOVE      SAVBTYP,KEY3          * bed type
            MOVE      ONE,FORM1C            * flag to get daycase shared room
            CALL      BFES0000              * get bed fees
            IF        EXIT=1
              MOVE      "0",FORM1A          * sameday record type
              MOVE      SAVBTYP,KEY3        * bed type
              MOVE      ZERO,BFPAT          * Patient portion
              MOVE      ZERO,BFHF           * Rebate portion
              CALL      CDVA0000            * Check for DVA patients
              CALL      WRFS0000            * write to pmsfesaf
            ENDIF
          ENDIF
.
ATBF9999  RETURN
+
.------------------------------------------------------------
.         Get special bed type for Shared room, ICU/SCN
.         HCP Equivalent 1- Private, 2-Shared 3-ICU/CCU/SCN         
.------------------------------------------------------------
CBTP0000  MOVE      SP70,SAVBTYP
          MOVE      SP70,PRVBTYP
          MOVE      SP70,ICUBTYP
          MOVE      SP70,SCNBTYP
          MOVE      SP70,SHRBTYP
          MOVE      "BT",KEY2
          PACK      KEY5,KEY2,SP70
          CALL      RDSCODE1
CBTP1000  CALL      RDKCODE1
          BRANCH    OVRCD,CBTP9999
.
          MATCH     "BT",TCODE
          GOTO      CBTP9999 IF NOT EQUAL
.
          MATCH     "I",PTCOACTV
          GOTO      CBTP1000 IF EQUAL      * In-active
.
          COMPARE   ONE,TCFORM6
          GOTO      CBTP1200 IF EQUAL      * a valid fees estimates bedtype
.
          COMPARE   TWO,TCFORM6
          GOTO      CBTP1000 IF NOT EQUAL  * not a valid bedtype
.
CBTP1200  MATCH     "3",PTCDNHCP
          GOTO      CBTP2000 IF NOT EQUAL
.
          COMPARE   TWO,TCFORM6
          GOTO      CBTP1400 IF EQUAL
          COMPARE   ONE,TCFORM6
          GOTO      CBTP8000 IF NOT EQUAL  * not a valid bedtype
.
CBTP1400  MATCH     SP70,ICUBTYP
          IF        @EQUAL
            MOVE      ACODE,ICUBTYP              * ICU Bed type
          ELSE
            MATCH     SP70,SCNBTYP
            IF        @EQUAL
              MOVE      ACODE,SCNBTYP            * SCN Bed type
            ENDIF
          ENDIF
          GOTO      CBTP8000
.
CBTP2000  MATCH     "2",PTCDNHCP
          GOTO      CBTP3000 IF NOT EQUAL        * not a shared room
.
          COMPARE   ONE,TCFORM6
          GOTO      CBTP8000 IF NOT EQUAL        * not a valid bedtype
.
          MATCH     SP70,SAVBTYP
          IF        @EQUAL
            MOVE      ACODE,SAVBTYP              * shared room
            MOVE      ACODE,SHRBTYP
          ENDIF
          GOTO      CBTP8000
.
CBTP3000  MATCH     "1",PTCDNHCP
          GOTO      CBTP8000 IF NOT EQUAL        * not a private room
.
          COMPARE   ONE,TCFORM6
          GOTO      CBTP8000 IF NOT EQUAL        * not a valid bedtype
.
          MATCH     SP70,PRVBTYP
          IF        @EQUAL
            MOVE      ACODE,PRVBTYP              * Private Bed type
          ENDIF
.
CBTP8000  MATCH     SP70,SAVBTYP
          GOTO      CBTP1000 IF EQUAL
          MATCH     SP70,PRVBTYP
          GOTO      CBTP1000 IF EQUAL
          MATCH     SP70,ICUBTYP
          GOTO      CBTP1000 IF EQUAL
          MATCH     SP70,SCNBTYP
          GOTO      CBTP1000 IF EQUAL
.
CBTP9999  RETURN
+
.------------------------------------------------------------
.         Get bed fees
.------------------------------------------------------------
BFES0000  PACK      KEY9,PMFDCLTY,PMFDHFUN,SP70
          CALL      GETCNEFF               * Get Contract Effective From
          BRANCH    EXIT,BFES9999
.
          MOVE      ATYPE,PMFDATYP         * Overnight Suggested atype
          IF        ASTAY=0 | FORM1C=1
            MOVE      SAVATYPE,PMFDATYP        * Daycase Suggested atype
          ENDIF
.
          MOVE      SP70,KEY12
          MOVE      ZERO,FORM2
          MOVE      SP70,PTHFBCAT
          PACK      KEY14,PMFDHFUN,PMFDHFTB,SP10
          CALL      RDFUND1
.
          IF        FORM1C=1
            UNPACK    SAVKEY15,KEY12
            PACK      SAVKEY15,KEY12,PMFDATYP,SP70      * use Daycase Sugg.Atype
            PACK      KEY18,SAVKEY15,SAVBTYP,SP70
            MOVE      THREE,FORM2           * to Stop looking for default
          ELSE
            PACK      KEY18,PMFDCLTY,PMFDHFUN,PTHFBCAT,PMFDATYP,SP70
            UNPACK    KEY18,SAVKEY15
          ENDIF
BFES1000  PACK      KEY27,KEY18,SP30
BFES1200  CALL      RDSBFEE1              * Position on & read the bed fee
BFES2000  CALL      RDKBFEE1
          BRANCH    OVRCD,BFES3000
          IF        FORM1C=1
            PACK      DIM18,BFCLM,BFHFUND,PTBFHTYP,BFATYPE,BFBTYPE,SP70
          ELSE
            PACK      DIM18,BFCLM,BFHFUND,PTBFHTYP,BFATYPE,SP70
          ENDIF
          MATCH     KEY18,DIM18
          GOTO      BFES3000 IF NOT EQUAL
.
.         Get daycase shared room rate for Overnight patient
.
          IF        FORM1C=1
            COMPARE   ZERO,BFENDDY
            GOTO      BFES2000 IF NOT EQUAL     * Not a day case rate
            GOTO      BFES2200
          ENDIF
.
          IF        DCFLAG=1
            COMPARE   ZERO,BFENDDY
            GOTO      BFES2000 IF NOT EQUAL   * Not a day case rate
          ELSE
            COMPARE   ZERO,BFENDDY
            GOTO      BFES2000 IF EQUAL       * Skip daycase rate
          ENDIF
.
BFES2200  IF        FORM2=3
            MATCH     CONTRCID,PTBFCNID
            GOTO      BFES2000 IF NOT EQUAL * different contract
          ELSE
            MOVE      PMFDADAT,CEFFDATE       * Use admission date
            MOVE      PTBFCNID,KEY6           * Contract ID
            CALL      VALCONTR                * Validate Contract ID
            BRANCH    EXIT,BFES2000           * not valid
            MOVE      PTBFCNID,CONTRCID       * Contract ID
          ENDIF
          GOTO      BFES8000                * bed fee rate
.
BFES3000  ADD       ONE,FORM2
          BRANCH    FORM2,BFES4000,BFES5000
          MOVE      ONE,EXIT
          GOTO      BFES9999
.
BFES4000  PACK      KEY18,PMFDCLTY,SP6,SP3,PMFDATYP,SP70
          UNPACK    KEY18,SAVKEY15
          GOTO      BFES1000
.
BFES5000  PACK      KEY18,PTCNDCLM,SP6,SP3,PMFDATYP,SP70
          UNPACK    KEY18,SAVKEY15
          GOTO      BFES1000
.
BFES8000  MOVE      BFBTYPE,SBEDTYPE
.
          MOVE      "BT",KEY2
          PACK      KEY5,KEY2,SBEDTYPE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,BFES9000
.
          MATCH     "I",PTCOACTV
          GOTO      BFES9000 IF EQUAL
.
          COMPARE   ONE,TCFORM6
          GOTO      BFES8100 IF EQUAL
          COMPARE   TWO,TCFORM6
          GOTO      BFES9000 IF NOT EQUAL  *not a valid fees estimates bedtype
.
BFES8100  ADD       BFHF,BFPAT
.
          MATCH     "1",USEBASIC
          IF        @EQUAL
            MOVE      BFHF,F12P2
            MOVE      BFPAT,FORM12P2
            CALL      CWBFE000               * Get Basic Cover bed fees rates
            MOVE      F12P2,BFHF
            MOVE      FORM12P2,BFPAT         * restore Lumpsum + Inlier amount
          ENDIF
.
. ** hea use preferred accom if entered 0892480
          MATCH     "1",PTCNIFCL
          IF        @EQUAL
            IF        DCFLAG=1
              MATCH     SP70,PMVXPACC
              IF        !@EQUAL
                PACK      KEY5,ANSB,ANSP,PMVXPACC,SP70
                CALL      RDCODE1
                IF        OVRCD<>1
                  MATCH     "1",TCINDC1
                  IF        @EQUAL
                    MATCH     PRVBTYP,SBEDTYPE
                    GOTO      BFES8300 IF EQUAL       * Private bed type
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SAVBTYP,SBEDTYPE
          GOTO      BFES8200 IF EQUAL       * Shared bed type
.
.         SJOG has Private fees for Daycase
.
          IF        FORM1C=0
            MATCH     "2",PTCNIFCL
            IF        @EQUAL
              IF        DCFLAG=1
                MATCH     PRVBTYP,SBEDTYPE
                GOTO      BFES8300 IF EQUAL       * Private bed type
              ENDIF
            ENDIF
            BRANCH    DCFLAG,BFES9000       * Daycase only has 'Sameday' row
          ENDIF
.
BFES8120  MATCH     PRVBTYP,SBEDTYPE
          GOTO      BFES8300 IF EQUAL       * Private bed type
.
          MATCH     ICUBTYP,SBEDTYPE
          GOTO      BFES8400 IF EQUAL       * ICU bed type
          MATCH     SCNBTYP,SBEDTYPE
          GOTO      BFES8400 IF EQUAL       * SCN bed type
.
          MOVE      FOUR,FORM1A           * for other bed types
          GOTO      BFES8900
.
.         FORM1C is flag to get Daycase rate shared room for Overnight patient
.
BFES8200  IF        FORM1C=1
            MOVE      ZERO,FORM1A           * Record type=0 for Daycase rate
            MOVE      SBEDTYPE,KEY3         * bed type
            MOVE      PMFDATYP,DIM9         * store atype in Theatre charge/MBS
            CALL      WRFS0000              * write to pmsfesaf
            GOTO      BFES9999
          ENDIF
.
          MOVE      ONE,FORM1A            * Record type=1 for Shared room
          MOVE      ONE,FORM3A            * flag for shared room
          GOTO      BFES8900
.
BFES8300  MOVE      TWO,FORM1A            * Record type=2 for Private room
          MOVE      ONE,FORM2A            * flag for private room
          CALL      CDVA0000              * Check for DVA patients
          GOTO      BFES8900
.
BFES8400  MOVE      THREE,FORM1A          * Record type=3 for ICU/SCN
.
BFES8900  MOVE      SBEDTYPE,KEY3         * bed type
          MOVE      PMFDATYP,DIM9         * store atype in Theatre charge/MBS
          CALL      WRFS0000              * write to pmsfesaf
.
BFES9000  MOVE      THREE,FORM2           * to Stop looking for default
          PACK      KEY27,SAVKEY15,SBEDTYPE,Z70
          PACK      KEY18,SAVKEY15,SP70
          GOTO      BFES1200              * look for next bed type
.
BFES9999  RETURN
+
.******************************************************************************
.         Get Basic Cover Bed fees
.******************************************************************************
CWBFE000  READ      CONTROLF,HUND20;*244,PTCNCWCL
.
          MOVE      ZERO,BASICHF
          MOVE      ONE,BASICFLG
          PACK      KEY9,PTCNCWCL,PMFDHFUN,SP70
          CALL      GETCNEFF               * Get Contract Effective From
          BRANCH    EXIT,CWBFE900
.
          MOVE      SP70,KEY12
          MOVE      ZERO,BFECOUNT
.
          PACK      KEY18,PTCNCWCL,PMFDHFUN,PTHFBCAT,PMFDATYP,SBEDTYPE,SP70
CWBFE100  PACK      KEY27,KEY18,SP30
CWBFE120  CALL      RDSBFEE1              * Position on & read the bed fee
CWBFE200  CALL      RDKBFEE1
          BRANCH    OVRCD,CWBFE300
.
          PACK      DIM18,BFCLM,BFHFUND,PTBFHTYP,BFATYPE,BFBTYPE,SP70
          MATCH     KEY18,DIM18
          GOTO      CWBFE300 IF NOT EQUAL
.
          MATCH     BFBTYPE,SBEDTYPE
          GOTO      CWBFE300 IF NOT EQUAL
.
          IF        DCFLAG=1
            COMPARE   ZERO,BFENDDY
            GOTO      CWBFE200 IF NOT EQUAL     * Not a day case rate
          ELSE
            COMPARE   ZERO,BFENDDY
            GOTO      CWBFE200 IF EQUAL         * Skip daycase rate
          ENDIF
.
CWBFE220  MOVE      PMFDADAT,CEFFDATE       * Use admission date
          MOVE      PTBFCNID,KEY6           * Contract ID
          CALL      VALCONTR                * Validate Contract ID
          BRANCH    EXIT,CWBFE200           * not valid
          MOVE      BFHF,BASICHF
.
.         Check if the Basic Cover Rebate is greater than Total Estimate cost
.
          IF        BASICHF>FORM12P2
            MOVE      FORM12P2,BASICHF        * use the total estimate
          ENDIF
          GOTO      CWBFE999                  * bed fee rate
.
CWBFE300  ADD       ONE,BFECOUNT
          BRANCH    BFECOUNT,CWBFE400
          GOTO      CWBFE900
.
CWBFE400  PACK      KEY18,PTCNCWCL,SP6,SP3,PMFDATYP,SBEDTYPE,SP70
          GOTO      CWBFE100
.
CWBFE900  MOVE      ZERO,BASICHF
CWBFE999  RETURN
+
.******************************************************************************
.         Check for DVA Patient
.******************************************************************************
CDVA0000  
          MATCH     "PATWEB78",PRGID
          GOTO      CDVA1000 IF NOT EQUAL
.
          MOVE      ADMISSNO,DIM8VISN
          MOVE      DIM8VISN,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,CDVA9999
.
          MATCH     "2",PTCNIFCL
          GOTO      CDVA1000 IF NOT EQUAL        * Not SJOG
.
.
          CALL      RDPTELG1
          IF        OVRCD=0
            PACK      PTMIPCMX,PTELCMXC,SP70
            MATCH     SP70,PTMIPCMX         * Provisional Casemix code
            IF        !@EQUAL
              PACK      PTELECLS,PTELECLS,SP70
              SQUEEZE   PTELECLS
              MOVE      PTELECLS,ASTAY    * Use the keyin Exp.LOS
            ENDIF
          ELSE
            MOVE      ZERO,PTELPPRI
          ENDIF
          GOTO      CDVA9999
.
CDVA1000  PACK      KEY5,ANSC,ANSL,ACLAIM        * check for DVA indicator
          CALL      RDCODE1
          IF        OVRCD = 0
            MOVE      ZERO,FORM1
            WHILE     FORM1 < 5
              ADD       ONE,FORM1
              LOAD      ANS,FORM1,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
              MATCH     ANSV,ANS
              GOTO      CDVA2000 IF EQUAL
            DO
          ENDIF
          GOTO      CDVA9999
.
CDVA2000  CALL      RDPTELG1
          IF        OVRCD=0
            ADD       PTELPPRI,BFPAT      * Add Private Room Co-payment to Priv.
          ENDIF
CDVA9999  RETURN
+
.******************************************************************************
.*                                  WRFS0000                                  *
.*                  Write record to pmsfesaf file for HEA layout              *
.******************************************************************************
WRFS0000  MOVE      ZERO,FORM5
          PACK      KEY17,PMFDQUOT,Z70
          CALL      RSPMFES1
          CALL      RPPMFES1
          BRANCH    OVRCD,WRFS2000
.
          MATCH     PMFDQUOT,PMFSQUOT
          GOTO      WRFS2000 IF NOT EQUAL
.
          MOVE      PMFSUNIQ,FORM5
.
WRFS2000  MOVE      URNUMBER,PMFSURNO
          MOVE      PMFDQUOT,PMFSQUOT
.
          MOVE      FORM1A,PMFSRTYP
          MOVE      ZERO,PMFSLUMH
          MOVE      ZERO,PMFSLUMF
          MOVE      ZERO,PMFSDAFR
          MOVE      ZERO,PMFSDAYT
          MOVE      BFPAT,PMFSDAYH
          MOVE      BFHF,PMFSDAHF
.
          MATCH     "1",USEBASIC
          IF        @EQUAL
            IF        BASICFLG=1
              MOVE      BASICHF,PMFSDAHF     * Use Basic cover HF portion
              MOVE      ZERO,BASICFLG
            ENDIF
          ENDIF
.
          MOVE      DIM9,PMFSTHIT
          MOVE      ZERO,PMFSTHEH
          MOVE      ZERO,PMFSTHEF
          MOVE      ASTAY,PMFSQUAN        * LOS
          MOVE      KEY3,PMFSBTYP         * bed type
          MOVE      ZERO,PMFSGSTA
.
.         Store Casemix and Contract id in GST fields (temporary)
.
          MOVE      CMXFLG,PMFSGSTP       * casepayment flag
          PACK      PMFSGSTC,CONTRCID,SP70
.
          MATCH     "2",PTCNIFCL
          GOTO      WRFS8000 IF NOT EQUAL
.
.         SJOG Fees Estimate is using IFC layout
.
          MATCH     "PATWEB76",PRGID
          GOTO      WRFS6000 IF EQUAL
.
.         SJOG IFC is using Admission GST Code
.
          COMPARE   FIVE,PMFSRTYP
          GOTO      WRFS7000 IF LESS              * Accommondation records
.
          GOTO      WRFS8000
.
.         SJOG Fees Estimate Accommodation GST only for Private bedtype
.
WRFS6000  COMPARE   TWO,PMFSRTYP
          GOTO      WRFS8000 IF NOT EQUAL
.
WRFS7000  IF        PMFDAGST=1
            MOVE      ASTAY,PMFSDAFR        * LOS
            MOVE      PTCNAGST,KEY6
            CALL      AGST0000
            MOVE      PMFSDAYH,PMFSGSTA
            MULT      IBGEAMNT,PMFSGSTA
            DIV       "100.00",PMFSGSTA
            MOVE      "1",PMFSGSTP
            MOVE      PTCNAGST,PMFSGSTC
          ENDIF
.
WRFS8000  ADD       ONE,FORM5
          MOVE      FORM5,PMFSUNIQ
.
          PACK      KEY17,PMFSQUOT,PMFSUNIQ
          CALL      RAPMFES1
          COMPARE   ZERO,OVRCD
          GOTO      WRFS8000 IF EQUAL
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRPMFES1
          TRAPCLR   IO
.
WRFS9999  RETURN
.
.-----------------------------------------------------------
.         Clear eligibility link fields
.-----------------------------------------------------------
CLRAEL00  UNPACK    SP70,PMAEVISN
          UNPACK    SP70,PMAEQUOT
          UNPACK    SP70,PMAESPAR
.
CLRAEL99  RETURN
+

.------------------------------------------------------------
.          Check for Veterans Affairs concession card
.------------------------------------------------------------
VETC0000  CALL      IBACLOCK
          PACK      KEY8,CCC,CYY,CMM,CDD  * current date
          REP       " 0",KEY8
          MOVE      SP70,KEY1
.
          PACK      KEY19,PURNO,SP70
          CALL      RSPMCCD1           * Read concession card table
VETC1000  CALL      RKPMCCD1
          BRANCH    OVRCD,VETC9000
.
          MATCH     PURNO,PMCDURNO     * Check same UR
          GOTO      VETC9000 IF NOT EQUAL
.
          MATCH     SP70,PMCDEXDT
          IF        !@EQUAL & !@EOS
            MATCH     KEY8,PMCDEXDT     * check if exp. date is >= current date
            IF        @LESS
              GOTO      VETC1000
            ENDIF
          ENDIF
.
          MATCH     SP70,PMCDDVAC
          GOTO      VETC1000 IF EQUAL
.
          PACK      KEY5,ANSD,ANSX,PMCDDVAC,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,VETC1000
          MATCH     ANSW,THCSCOD
          GOTO      VETC2000 IF EQUAL       * White DVA card color
          MATCH     ANSG,THCSCOD
          GOTO      VETC1000 IF NOT EQUAL   * NOT Gold DVA card color
.
VETC2000  UNPACK    THCSCOD,KEY1
.
VETC9000  WRITE     HTMLFILE;KEY1;
VETC9999  RETURN
+

