+
.******************************************************************************
.
.         PATWRDDS.PBL
.         ------------
.         This include displays Wards only from the Ward Bed File
.
.     Modifications: 28/11/89 Graeme Williams
.                    Added the parameter CNEWDS to determine if the include
.                    is to use the new standards for "?" routines
.                    11/06/93 J.Tan   SRF 121660
.                    Mods to check for active wards
.
.------------------------------------------------------------------------------
.         Display Heading, and then loop over entries
.------------------------------------------------------------------------------
.
PATWRDDS  DISPLAY   *P1:3,*EF,*P20:4,*V2LON,*ULON,"WARDS"
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
.
          PACK      KEY6,SP6
          CALL      RDSWARD3
PWDS1000  CALL      RDKWARD3
          BRANCH    OVRCD,PWDS2000
.
          MATCH     SP3,WBED                 * Still only wards ?
          GOTO      PWDS2000 IF NOT EQUAL    * No - exit
.
          BRANCH    WACTIVE,PWDS1000         * Ignore non-active wards
.
          DISPLAY   *PCCOL:CVERT,*V2LON,WWARD,SP1,*HOFF,WBDESC
.
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      PWDS1000 IF LESS
.
          MOVE      FIVE,CVERT
          ADD       "40",CCOL
          COMPARE   "70",CCOL
          GOTO      PWDS1000 IF LESS
.
.         Check for new standards question, or old question
.
          BRANCH    CNEWDS,DSWRD200
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSE,ANS
          GOTO      PWDS3000 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATWRDDS IF EQUAL
.
          GOTO      DSWRD400
.
.         Use the new standards question
.
DSWRD200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS 
          CMATCH    ANSC,ANS
          GOTO      PWDS3000 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATWRDDS IF EQUAL
.
DSWRD400  MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
          GOTO      PWDS1000
.
.         End of codes
.
PWDS2000  BRANCH    CNEWDS,PWDS3000
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
.
PWDS3000  MOVE      ZERO,OVRCD
          RETURN
