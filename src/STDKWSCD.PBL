.------------------------------------------------------------
. Routine   : KWINT000
.
. Function  : Initialisation for Help Screen Display.
.              - Save Screen
.              - Display Box Details
.
. Called by : All Computer Generated Help Screens
.------------------------------------------------------------
KWINT000  MOVE      ZERO,EXIT
          MOVE      ONE,CSEARCH
.
          CALL      GETSCR00
          BRANCH    EXIT,KWINT999
.
          MOVE      HTOP,CTOP
          MOVE      HLEF,CLEF
          MOVE      HBOT,CBOT
          MOVE      HRIG,CRIG
.
          ADD       TWO,CTOP       * Cxxx Clip Box Size
          ADD       ONE,CLEF
          SUB       THREE,CBOT
          SUB       ONE,CRIG
          MOVE      HBOT,MAXITEM
          SUB       HTOP,MAXITEM
          SUB       "4",MAXITEM
.
          SUSPEND
.
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          ADD       ONE,CBOT
          DISPLAY   *PHRIG:CBOT,*G34,*PHLEF:CBOT,*G33;
          HLINE     *G37,CBOT,CLEF,CRIG
          ADD       ONE,CBOT
.
          CLIP      1,1,CRIG,24    * Start Inside of Box Displays
.
          DISPLAY   *PCLEF:CBOT,"e",*V2LON,"X",*HOFF,"it, ex":
                                    *V2LON,"P",*HOFF,"and, ":
                                    *V2LON,"U",*HOFF,"p, ":
                                    *V2LON,"D",*HOFF,"own, ":
                                    *V2LON,"S",*HOFF,"earch, ":
                                    *V2LON,"H",*HOFF,"ome, ":
                                    *V2LON,"E",*HOFF,"nd, ":
                                    *V2LON,"R",*HOFF,"educe, ":
                                    *V2LON,"F",*HOFF,"lags ";
.exit, expand, up, down, search, home, end, reduce, view, Flags
          SUB       TWO,CBOT
.
          MOVE      HTOP,MINITEM
          ADD       ONE,MINITEM
          MOVE      MINITEM,STDROW
.
          MOVE      HLEF,STDCOL00
          ADD       ONE,STDCOL00
          DISPLAY   *PSTDCOL00:STDROW,*V2LON,*ULON,"Item"
.
KWINT999  RETURN
.------------------------------------------------------------
. Function    : Select Option Function for Help Screens
.
. Called by   : All Computer Generated Help Screens
.
. Exit Values : 0 = Item Selected      
.               1 = Page Up
.               2 = Page Down            
.               3 = Home
.               4 = End      
.               5 = Display Screen from start
.               6 = Exit
.               7 = Search
.               8 = View
.------------------------------------------------------------
KWSEL000  MOVE      ZERO,CCITEM
          MOVE      SP2,CCITEMD
.
          KEYIN     *PCLEF:CBOT,*NOEDIT,*+,*EOFF,ANS
          GOTO      KWSEL300 IF PAGEDOWN
          GOTO      KWSEL350 IF PAGEUP
          GOTO      KWSEL400 IF HOME
          GOTO      KWSEL450 IF END
          GOTO      KWSEL500 IF RIGHT
          GOTO      KWSEL550 IF LEFT 
          GOTO      KWSEL600 IF UP
          GOTO      KWSEL650 IF DOWN
          RESET     ANS
          GOTO      KWSEL250 IF EOS
          TYPE      ANS
          GOTO      KWSEL220 IF NOT EQUAL
          MOVE      ANS,CCITEM
.
          MOVE      SELITMNO,F3
          DIV       TEN,F3
          COMPARE   CCITEM,F3          * Less than ten items on screen 
          GOTO      KWSEL210 IF LESS   * so no need to enter second character
.
          KEYIN     *PCLEF:CBOT,*NOEDIT,*+,*EOFF,ANS
          RESET     ANS
          GOTO      KWSEL210 IF EOS
          MULT      TEN,CCITEM
          MOVE      ANS,F1
          ADD       F1,CCITEM
.
KWSEL210  
          COMPARE   ONE,CCITEM
          GOTO      KWSEL900 IF LESS       * Invalid Entry
          COMPARE   CCITEM,SELITMNO
          GOTO      KWSEL900 IF LESS       * Invalid Entry
          MOVE      ZERO,EXIT
          GOTO      KWSEL999               * Item Selected
.
KWSEL220 
          REP       UPPLOW,ANS
          MATCH     SP70,ANS           * Next Item
          GOTO      KWSEL650 IF EQUAL  
          MATCH     "X",ANS             * Exit
          GOTO      KWSEL910 IF EQUAL
          MATCH     "D",ANS             * Page Down
          GOTO      KWSEL300 IF EQUAL
          MATCH     "U",ANS             * Page Up
          GOTO      KWSEL350 IF EQUAL
          MATCH     "H",ANS             * Home
          GOTO      KWSEL400 IF EQUAL
          MATCH     "E",ANS             * End
          GOTO      KWSEL450 IF EQUAL
          MATCH     "S",ANS             * Search
          GOTO      KWSEL480 IF EQUAL
          MATCH     "P",ANS             * Left  (Expand Display)
          GOTO      KWSEL500 IF EQUAL
          MATCH     "R",ANS             * Right (Reduce Display)
          GOTO      KWSEL550 IF EQUAL
          MATCH     "F",ANS             * View Creditor
          GOTO      KWSEL750 IF EQUAL
          GOTO      KWSEL900               * Invalid Entry
.
KWSEL250  
          MOVE      DITEM,CCITEM
          COMPARE   CCITEM,SELITMNO
          GOTO      KWSEL900 IF LESS       * Invalid Entry
          MOVE      ZERO,EXIT
          GOTO      KWSEL999               * Item Selected
.         
. Page Down
.
KWSEL300  MOVE      ONE,EXIT
          GOTO      KWSEL999
.
. Page Up
.
KWSEL350  MOVE      TWO,EXIT
          GOTO      KWSEL999
.
. Home
.
KWSEL400  MOVE      THREE,EXIT
          GOTO      KWSEL999
.
. End
.
KWSEL450  MOVE      FOUR,EXIT
          GOTO      KWSEL999
.
. Search
.
KWSEL480  MOVE      SEVEN,EXIT
          GOTO      KWSEL999
.
. Expand Display
.
KWSEL500  BRANCH    CSCRSIZ,KWSEL900
          MOVE      ONE,CSCRSIZ
          SUSPEND
          CALL      PUTSCR00
          MOVE      HTOP,STOP     * Save Screen Size
          MOVE      HLEF,SLEF
          MOVE      HBOT,SBOT
          MOVE      HRIG,SRIG
          MOVE      "3",HTOP      * Full Screen Display Size
          MOVE      "1",HLEF
          MOVE      "24",HBOT
          MOVE      "80",HRIG
          MOVE      FIVE,EXIT     * Display Help Screen from Start
          GOTO      KWSEL999
.
. Reduce Display
.
KWSEL550  COMPARE   ZERO,CSCRSIZ
          GOTO      KWSEL900 IF EQUAL
          MOVE      ZERO,CSCRSIZ
          CALL      PUTSCR00
          MOVE      STOP,HTOP     * Restore Saved Screen Size
          MOVE      SLEF,HLEF
          MOVE      SBOT,HBOT
          MOVE      SRIG,HRIG
          MOVE      FIVE,EXIT     * Display Help Screen from Start
          GOTO      KWSEL999
.
. Up Arrow. Highlight Last Item
.
KWSEL600  DISPLAY   *PSTDCOL00:DROW,*V2LON,SP1,DITEM,DOT
          COMPARE   DITEM,ONE
          GOTO      KWSEL610 IF NOT EQUAL
          COMPARE   SELITMNO,ZERO
          GOTO      KWSEL620 IF EQUAL
          MOVE      SELITMNO,DITEM
          MOVE      CTOP,DROW
          ADD       SELITMNO,DROW  
          ADD       ONE,DITEM
.
KWSEL610  SUB       ONE,DITEM
          SUB       ONE,DROW
KWSEL620  DISPLAY   *PSTDCOL00:DROW,*HON,*V2LON,SP1,DITEM,DOT
          GOTO      KWSEL000
.
. Down Arrow. Highlight Next Item
.
KWSEL650  DISPLAY   *PSTDCOL00:DROW,*V2LON,SP1,DITEM,DOT
          COMPARE   DITEM,SELITMNO
          GOTO      KWSEL660 IF NOT EQUAL
          MOVE      ZERO,DITEM
          MOVE      CTOP,DROW
          SUB       ONE,DROW
.
KWSEL660  COMPARE   SELITMNO,ZERO
          GOTO      KWSEL670 IF EQUAL
          ADD       ONE,DITEM
          ADD       ONE,DROW
KWSEL670  DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
          GOTO      KWSEL000
.
KWSEL750  MOVE      EIGHT,EXIT
          GOTO      KWSEL999               * Item Selected
.
KWSEL900  BEEP
          GOTO      KWSEL000
.
KWSEL910  MOVE      SIX,EXIT
.
KWSEL999  RETURN
.------------------------------------------------------------
. Break Keywords into Words
.------------------------------------------------------------
GETWRD00  MOVE      ZERO,KEYWRDNO
          MOVE      SP70,KEYWRD01
          MOVE      SP70,KEYWRD02
          MOVE      SP70,KEYWRD03
          MOVE      SP70,KEYWRD04
          MOVE      SP70,KEYWRD05
          MOVE      SP70,KEYWRD06
          MOVE      SP70,KEYWRD07
          MOVE      SP70,KEYWRD08
          MOVE      SP70,KEYWRD09
          MOVE      SP70,KEYWRD10
GETWRD10  CMATCH    SP1,KEYWRD00             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWRD00
            GOTO      GETWRD10 IF NOT EOS
            GOTO      GETWRD99               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWRD00,SP30        * reset form pointer
          MOVE      KEY60,KEYWRD00
.
          SCAN      SP1,KEYWRD00               * Locate the first blank
          GOTO      GETWRD20 IF NOT EQUAL
          BUMP      KEYWRD00,-1              * go back 1 to end of name
          MOVEFPTR  KEYWRD00,CCITEM          * another handly form field
          RESET     KEYWRD00                 * reset the form pointer
          SETLPTR   KEYWRD00,CCITEM          * set logical length to end of name
.
.         Save this Word
.
GETWRD20  MOVE      KEYWRD00,KEY15           
          ADD       ONE,KEYWRDNO
          REP       UPPLOW,KEY15
          STORE     KEY15,KEYWRDNO,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                                   KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10 
          GOTO      GETWRD19
.
.         Check for any more words
.
GETWRD19  SETLPTR   KEYWRD00,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWRD00,CCITEM        * reset to this point
          PACK      KEY60,KEYWRD00,SP30    * reset form pointer
          MOVE      KEY60,KEYWRD00
.
          GOTO      GETWRD10
.
GETWRD99  RETURN
.
