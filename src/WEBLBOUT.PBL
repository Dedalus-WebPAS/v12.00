.******************************************************************************
.* LABELOUT - This is a proc to display the variables painted for outpatient  *
.*            Labels.                                                         *
.* PARAMETERS  : This procedure requires that the BOKA and BOKB records have  *
.*               been read. The proc will read the Clinic Master and other    *
.*               files required to print the variables.                       *
.*                                                                            *
.*               KEY28 = Key to BOKA File.                                    *
.*               FORM1 = Label Type                                           *
.*                       1 = Mailing Label                                    *
.*                       2 = Appointment Label                                *
.*                       3 = Booking Label                                    *
.*                       4 = GP Label                                         *
.*                                                                            *
.* RETURNS     : EXIT = 0       screen painted label and was printed          *
.*               EXIT = 1       not screen painted label (CCITEM=no labels)   *
.*               EXIT = 2       dont print the labels                         *
.*                                                                            *
.* Modifications : 
.******************************************************************************
.* V10.11.01 23/08/2017  Thanh T.       TSK 0821710                           * 
.*           Audit Amission/outpatient visit comments                         *
.******************************************************************************
.*        V10.02.02 01/07/2011  Ebon Clements CAR 242246                      *
.*                  Added read of slot template header                        *
.*        V10.02.01 09/06/2011  Ebon Clements CAR 239530                      *
.*                  Added print extra contact functionality                   *
.*        V9.09.01  12/07/2007  Peter Vela    CAR 142913                      *
.*                  Added call to GCCSJG00                                    *
.*        V9.08.01  27/11/2006  Ebon Clements CAR 125800                      *
.*                  Added read on pmsvx1af.                                   *
.******************************************************************************
.*        V9.06.01  23/05/2006  Ebon Clements CAR 792776                      *
.*                  Print postal/pmi address on carer label if carer details  *
.*                  blank                                                     *
.******************************************************************************
.*        V9.05.00  27/03/2006  Ebon Clements CAR 79277                       *
.*                  Added carer contact details                               *
.*        V9.04.05  12/09/2005  Mike Laming   CAR 62952                       *
.*                  Change to use Clinic Description as at the Booking Date   *
.*        V9.04.04  16/08/2005  Ebon Clements  CAR 58448                      *
.*                    Removed Change - Moved labels one column to the right   *
.*        V9.04.03  10/08/2005  Peter Vela     CAR 58448                      *
.*                    Moved labels one column to the right                    *
.*        V9.04.02  13/05/2005  Peter Vela     CAR 61776                      *
.*                    Fixed barcode printing                                  *
.*        V9.04.01  15/03/2005  Mike Laming    CAR 58448                      *
.*                    Change printing of Outpatient Labels to fix WebSpooler  *
.*                    problem of adding "Cr/Lf" when variables are too long   *
.*                    for print space provided                                *
.*        V9.03.01  17/02/2004  Ebon Clements           CAR 47525             *
.*                    Changed INIT000 to work out casekeys from the           *
.*                    admission number                                        *
.*        16/01/2004  Peter Vela                        CAR 37624             *
.*                    Cater for null CASEKEYS                                 *
.*        28/09/2000  Dean Cramer                                             *
.*                    Recomplied for DXPATMAS                                 *
.*                    Changed for SLA code in PATMX1FD                        *
.*        06/04/1998  Nick Read                                               *
.*                    Mods to print multiple screen painted labels across the *
.*                    page based on parameters HOLPRNT, HOLSIZE and HOLPAGE   *
.*                                                                            *
.*                 20/06/1997  Steve Armstrong          SRF 643002            *
.*                             Fixed bug in printing screen painted labels    *
.*                 28.03.1996 B.G.Ackland                                     *
.*                            Added Option for Multiple Patient Print with    * 
.*                            CPAGENO set to 1 for No Questions Asked         *
.*                                                                            *
.*                 16/05/1996 Justin Coates                                   *
.*                 set up CCITEM as the number of labels for non s/p labels   * 
.*                 23/05/1997  Steve Armstrong    SRF 642902                  *
.*                 Added open on IBAPRNT1                                     *
.******************************************************************************
          DEFPROC   LABELOUT
.
          INC       WEBBOOKV/INC
          INC       IBAPASFD/INC
          INC       NHIETHFD/INC
          INC       PATICDFD/INC
.
. ------- variables required -------
.
DONTASK   FORM      1
CODEDC    INIT      "DC"
CODEDA    INIT      "DC"
CODEEP    INIT      "EP"
CODEH1    INIT      "H1"
CODEH2    INIT      "H2"
CODEH3    INIT      "H3"
CODEO1    INIT      "O1"
CODEO2    INIT      "O2"
CODEO3    INIT      "O3"
CODEO4    INIT      "O4"
CODEM     INIT      "M "
CODEP     INIT      "P "
CODEP2    INIT      "P2"
CODEP3    INIT      "P3"
CODEP4    INIT      "P4"
CODEP5    INIT      "P5"
CODEP6    INIT      "P6"
CODER     INIT      "R "
CODEU2    INIT      "U2"
CODEU3    INIT      "U3"
CODEV6    INIT      "V6"
CODEV7    INIT      "V7"
CODEV8    INIT      "V8"
CYRSFDTE  DIM       8
CYRSTDTE  DIM       8
CYRSYEAR  FORM      3
...PRGID     DIM       8
PROCONUM  DIM       8
SAVKEY28  DIM       28
SAVLBTYP  FORM      1
NOTSCPT   FORM      1         * 1 = not screen painted
SITE      DIM       6
SP100     INIT      "                                                      ":
                    "                                              "
SP80      INIT      "                                  ":
                    "                                              "
+
.*********************************************************************
MAIN0000  UNPACK    LABELTYP,PRGID,SCRID
.
          CALL      INIT0000                     * Initialise proc
          BRANCH    EXIT,MAIN9999
.
          CALL      DSPBGRND                       * Display the label
          CALL      DSPFIELD                       * Display Fields on the label
.
MAIN1000  MOVE      HOLPAGE,LABCOS        * no of labels across page
          MOVE      HOLSIZE,LABSIZE       * Label Size               
          MOVE      ZERO,EXIT               * accept the number of labels
.
.-------  Print the label  -------
.
MAIN1100  CALL      OPNPRINT
          MOVE      ONE,FORM3
          MOVE      NOLABEL,LABPRINT
.
          IF        LABSIZE=1
            MOVE       "37",TABPOS2
            MOVE       "73",TABPOS3
          ENDIF
.
          IF        LABSIZE=2
            MOVE       "42",TABPOS2
            MOVE       "83",TABPOS3
          ENDIF
.
.         get the label screen size
.
          MOVE      EIGHT,SCMATOP
          MOVE      TWENTY2,SCMABOT
          PACK      KEY10,PRGID,SCRID    
          CALL      RDSCMA1
MAIN4000  MOVE      SCMATOP,CVERT             * start at top of screen
.
MAIN5000  COMPARE   TWENTY3,CVERT
          GOTO      MAIN6000 IF NOT LESS
.
..          GETVAR    PRINTVAR,ATT,ONE,CVERT
          MOVE      LABELDAT[CVERT],PRINTVAR
          MOVE      LABELATT[CVERT],ATT
          STRIP     PRINTVAR
          STRIP     ATT
.
. --------- stop if no background ---------------------------------------
.
          PACK      KEY12,PRGID,SCRID,CVERT,SP20
          CALL      RDSCSB1                 * positional read
          IF        OVRCD=1
            COMPARE   SCMATOP,CVERT
            GOTO      MAIN8000 IF EQUAL
            GOTO      MAIN6000
          ENDIF
.
          REP       BARCODE,ATT             * change any barcoded fields
.
.         if using TBC_7, then compile with a printvar
.        
          IFGE      $$VER,7
              IF      LABCOS>1
                  IF      LABCOS=2
                      PRINT   *1;           * print 2 across the page
                      PRINTVAR   PRINTVAR,ATT;                         *C-61776
                      PRINT   *TABPOS2;
                      PRINTVAR   PRINTVAR,ATT;                         *C-61776
                      PRINT   *1
                  ELSE 
                      PRINT   *1;           * print 3 across the page
                      PRINTVAR   PRINTVAR,ATT;                         *C-61776
                      PRINT   *TABPOS2;
                      PRINTVAR   PRINTVAR,ATT;                         *C-61776
                      PRINT   *TABPOS3;
                      PRINTVAR   PRINTVAR,ATT;                         *C-61776
                      PRINT   *1
                  ENDIF
              ELSE
                  PRINTVAR  PRINTVAR,ATT;
                  PRINT     *1
              ENDIF
          XIF
.
.         if using TBC_6, then compile with a print
.        
          IFEQ      $$VER,6
              IF        LABCOS>1
                  IF        LABCOS=2
                      PRINT     PRINTVAR;     * print 2 across the page
                      PRINT     *TABPOS2;
                      PRINT     PRINTVAR; 
                      PRINT     *N;
                  ELSE
                      PRINT     PRINTVAR;     * print 3 across the page
                      PRINT     *TABPOS2;
                      PRINT     PRINTVAR; 
                      PRINT     *TABPOS3;
                      PRINT     PRINTVAR; 
                      PRINT     *N;
                  ENDIF
              ELSE
                  PRINT     PRINTVAR
              ENDIF
            PRINT     *1,PRINTVAR
          XIF
          ADD       ONE,CVERT
.
          GOTO      MAIN5000
.
MAIN6000  SUB       LABCOS,LABPRINT
          IF        LABPRINT>0
              IF        LABCOS>LABPRINT
                   MOVE   LABPRINT,LABCOS
              ENDIF
              GOTO      MAIN4000
          ENDIF
.
          CALL      CLSPRINT
.
          MOVE      ZERO,EXIT               * labels printed
          GOTO      MAIN9999
.
MAIN8000  MOVE      ONE,EXIT                * labels are not screen painted
          GOTO      MAIN9999
.
MAIN9000  CALL      PUTSCR00
MAIN9500  MOVE      TWO,EXIT                * dont print
          GOTO      MAIN9999
.
MAIN9999  GOTO      LABOUT99
+
.*********************************************************************
.         initilisation
.*********************************************************************
INIT0000  CALL      CLRARR00               * Clear Print Array
          MOVE      ZERO,EXIT
          MOVE      FORM1,SAVLBTYP
          MOVE      CPAGENO,DONTASK         * 1 = dont ask number of labels
.
...          MOVE      "LABELOUT",PRGID
.
          MATCH     SP70,CASEKEYS
.         GOTO      INIT0100 IF NOT EQUAL   * I CAR 37624
          GOTO      INIT0005 IF EQUAL
          GOTO      INIT0005 IF EOS
          GOTO      INIT0100
.
INIT0005  MOVE      ADMISSNO,KEY8
          CALL      RDBOKB1
          BRANCH    OVRCD,INIT9300
.
          PACK      KEY36,ADMISSNO,SP70
          CALL      RDSBOKA6
          CALL      RDKBOKA6
          BRANCH    OVRCD,INIT9300
.
          MATCH     ADMISSNO,DOBAOUTN
          GOTO      INIT9300 IF NOT EQUAL
.
          PACK      CASEKEYS,OBASITE,OBACLIN,OBADATE,OBASTRT,OBASLOT,SP70
.
          MOVE      ADMISSNO,KEY8           * C CAR 37624
          CALL      RDVISA1
          IF        OVRCD=1
            CALL      CLPATVIS
          ENDIF
.
          MOVE      ADMISSNO,KEY8
          CALL      RDPMVX11
          BRANCH    OVRCD,INIT9400
.
INIT0100  MOVE      CASEKEYS,SAVKEY28
          MOVE      CASEKEYS,KEY28
          MOVE      CASEKEYS,SITE
.
          PACK      KEY6,SITE,SP10
          CALL      RDSITA1                                * Get Site
          BRANCH    OVRCD,INIT9000                         * No Site
. 
.-------  Open and read outbokaf  ------
.
          MOVE      SAVKEY28,KEY28
          CALL      RDBOKA1
          BRANCH    OVRCD,INIT9050
.
.-------  Open and read outma1af  ------
.
          PACK      KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT
          CALL      RDMASA1
          BRANCH    OVRCD,INIT9100
.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT,SP70
          CALL      RDSESA1
          BRANCH    OVRCD,INIT9100
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT,SP70
          CALL      RDOTHED1
          BRANCH    OVRCD,INIT9100
.
.-------  Open and read outbobaf  ------
.
          PACK      KEY8,OBAOUTNO,SP10
          CALL      RDBOKB1
          BRANCH    OVRCD,INIT9150
.
.-------  Open and read outbocaf  ------
.
          PACK      KEY8,OBAOUTNO,SP10
          CALL      RDOTBOC1
.
.-------  Open and read outdiaaf ------
.
          MOVE      SP9,OPDICODE        * Default diagnosis code
          PACK      OPDIDESC,SP30,SP20  * Default diagnosis code
          MOVE      OBAOUTNO,KEY8       * Key for the diag file
          CALL      RDDIAG1             * Get the diagnosis record
.
.-------  Open and read outcliaf  ------
.
.******************************************** start of changes         *C-62952
....      PACK      KEY20,OBASITE,OBACLIN,Z70
....      CALL      RDSCLIA1
....      CALL      RDPCLIA1
.                                           * use OCADESC at Booking date
          PACK      KEY20,OBASITE,OBACLIN,OBADATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OBASITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OBACLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.********************************************   end of changes         *C-62952
          BRANCH    OVRCD,INIT9200
.
.-------  Open and read outctyaf  ------
.
          PACK      KEY12,OBASITE,OBACTYP
          CALL      RDCTYA1
          BRANCH    OVRCD,INIT9250
.
.-------  Open and read patvisaf  ------
.
          PACK      KEY8,OBAURNO,SP10
          CALL      RDMAST1
          BRANCH    OVRCD,INIT9350
.
          PACK      KEY8,OBAURNO,SP10
          CALL      RDPMPX21
          BRANCH    OVRCD,INIT9350
.
          CALL      GCCSJG00
.
          PACK      PTHSNAME,SP30,SP30
          PACK      KEY3,OTMAHOSP 
          CALL      RDPTHSP1
.
.         set the PMI GP details
.
          MOVE      SP30,PMHCSNAM
          PACK      KEY10,PMPXRHC1,SP70
          CALL      RDPMHCP1
.
          MOVE      SP70,PMHCADR1
          PACK      KEY10,PMPXRH1G,SP70  * GP Practice code
          PACK      KEY12,KEY10,PMPXR1GC,SP70  * GP Practice code
          CALL      RDPMHCG1
.
          READ      CONTROLF,TWENTY1;*138,PTCNNHII
.
          IF        PTCNNHII = 1
            MOVE      SP10,NHMARES
            MOVE      SP10,NHMAETH
            MOVE      SP30,NMPNUMB 
            MOVE      OBAURNO,KEY8
            CALL      RDNHMAS2
          ELSE
            MOVE      SP20,NMPNUMB
            PACK      KEY10,CHOSINDX,OBAURNO,SP10 
            CALL      RDPTNID1
            MOVE      PTNINMPI,NMPNUMB
          ENDIF 
.
          READ      CONTROLF,TWENTY;*196,PTCNUCRD
.
          IF        PTCNUCRD = 1
            MOVE       SP20,PTVKCARD
            MOVE       SP10,PTVKCEXP
            MOVE       SP10,PTVKCXMP
            MOVE       SP10,PTVKCOMP
            MOVE       SP10,PTVKFMLY
            PACK      KEY8,OBAOUTNO,SP10
            CALL      RDPTVKC1
          ENDIF
.
          UNPACK    SP100,PKNAME
          UNPACK    SP100,PKADD1,PKADD2 
          UNPACK    SP100,PKADD4,PKSUBR,PKPOST,PKTELEP 
          UNPACK    SP100,PKTELEB,PKRELP
          PACK      KEY8,OBAOUTNO,SP10
          CALL      RDPTRES1
          GOTO      INIT9999
.
INIT9000  CLEAR     WEBTITLE 
          APPEND    "Invalid Site Code  - ",WEBTITLE
          APPEND    KEY6,WEBTITLE
          RESET     WEBTITLE
          GOTO      INIT9995
INIT9050  CLEAR     WEBTITLE 
          MOVE      "Invalid Booking Details -",WEBTITLE
          APPEND    KEY28,WEBTITLE
          RESET     WEBTITLE
          GOTO      INIT9995
INIT9100  CLEAR     WEBTITLE 
          APPEND    "Invalid Clinic Master Details -",WEBTITLE
          APPEND    KEY18,WEBTITLE
          RESET     WEBTITLE
          GOTO      INIT9995
INIT9150  CLEAR     WEBTITLE 
          APPEND    "Invalid Booking Details- ",WEBTITLE
          APPEND    KEY8,WEBTITLE
          RESET     WEBTITLE
          GOTO      INIT9995
INIT9200  CLEAR     WEBTITLE 
          APPEND    "Invalid Clinic ID - ",WEBTITLE
          APPEND    KEY6,WEBTITLE
          RESET     WEBTITLE
          GOTO      INIT9995
INIT9250  CLEAR     WEBTITLE 
          APPEND    "Invalid Clinic Type - ",WEBTITLE
          APPEND    KEY6,WEBTITLE
          RESET     WEBTITLE
          GOTO      INIT9995
INIT9300  CLEAR     WEBTITLE 
          APPEND    "Can't Find Case for Booking - ",WEBTITLE
          RESET     WEBTITLE
          GOTO      INIT9995
INIT9350  CLEAR     WEBTITLE 
          APPEND    "Invalid PMI Details - ",WEBTITLE
          APPEND    KEY8,WEBTITLE
          RESET     WEBTITLE
          GOTO      INIT9995
INIT9400  CLEAR     WEBTITLE
          APPEND    "Can't find visit extension record - ",WEBTITLE
          APPEND    KEY8,WEBTITLE
          RESET     WEBTITLE
          GOTO      INIT9995
INIT9995  CALL      WEBERR00
          MOVE      ONE,EXIT
INIT9999  RETURN
.*****************************************************************
.*        GCLI0000  Get Label Screen ID      Called By :         *
.*****************************************************************
GCLI0000  MOVE      ZERO,EXIT
          PACK      KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT
          CALL      RDMASA1
          BRANCH    OVRCD,GCLI9000
.
          LOAD      SCRID,SAVLBTYP,OTMASPMM,OTMASAPL,OTMASBKL,OTMASGPM
          GOTO      GCLI9999
.
GCLI9000  MOVE      "Clinic Master Not Found. ",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
GCLI9999  RETURN
. ****************************************************************************
. * HCMN0000 : Test if there any visit comments in the comments file         *
. ****************************************************************************
HCMN0000  MOVE      ONE,EXIT                 * Assume we don't have comments
          MOVE      ZERO,OVRCD               * Assume file will exist
.
          MOVE      PVIBILL,KEY8             * Key for start of comment
          PACK      KEY17,KEY8,VSCMTTYP,Z70
.
          CALL      RSVSMDT1
HCMN0200  CALL      RPVSMDT1
          BRANCH    OVRCD,HCMN9999
.
          MATCH     KEY8,VSMDVISN
          GOTO      HCMN9999 IF NOT EQUAL
.
          MATCH     VSCMTTYP,VSMDTYPE
          GOTO      HCMN9999 IF NOT EQUAL
.
          MATCH     "1",VSMDDELT          * is comment deleted
          GOTO      HCMN0200 IF EQUAL
.
          MOVE      ZERO,EXIT                * We have comments
.
HCMN9999  RETURN
.
+
.*****************************************************************
.*        DISPVARS  Display Items            Called By :         *
.*****************************************************************
.
DISPVARS  MOVE      SCPSITM,FIELDNO
          MOVE      SCPSLEN,LLCNT
.
          COMPARE   "500",FIELDNO         * If item more than 500 use 
          GOTO      DVARS100 IF LESS      * Outpatient Details else use
          SUB       "500",FIELDNO         * PMI
.
          MOVE      FIELDNO,SCPSITM
          CALL      DXOUTDET
          GOTO      DVARS999
.
DVARS100  CALL      DXPATMAS
.
DVARS999  RETURN
.
.------------------------------------------------------------
. Clear Printing Array
.------------------------------------------------------------
CLRARR00  MOVE      SP80,LABELDAT[1]
          MOVE      SP80,LABELDAT[2]
          MOVE      SP80,LABELDAT[3]
          MOVE      SP80,LABELDAT[4]
          MOVE      SP80,LABELDAT[5]
          MOVE      SP80,LABELDAT[6]
          MOVE      SP80,LABELDAT[7]
          MOVE      SP80,LABELDAT[8]
          MOVE      SP80,LABELDAT[9]
          MOVE      SP80,LABELDAT[10]
          MOVE      SP80,LABELDAT[11]
          MOVE      SP80,LABELDAT[12]
          MOVE      SP80,LABELDAT[13]
          MOVE      SP80,LABELDAT[14]
          MOVE      SP80,LABELDAT[15]
          MOVE      SP80,LABELDAT[16]
          MOVE      SP80,LABELDAT[17]
          MOVE      SP80,LABELDAT[18]
          MOVE      SP80,LABELDAT[19]
          MOVE      SP80,LABELDAT[20]
          MOVE      SP80,LABELDAT[21]
          MOVE      SP80,LABELDAT[22]
          MOVE      SP80,LABELDAT[23]
          MOVE      SP80,LABELDAT[24]
.
          MOVE      SP80,LABELATT[1]
          MOVE      SP80,LABELATT[2]
          MOVE      SP80,LABELATT[3]
          MOVE      SP80,LABELATT[4]
          MOVE      SP80,LABELATT[5]
          MOVE      SP80,LABELATT[6]
          MOVE      SP80,LABELATT[7]
          MOVE      SP80,LABELATT[8]
          MOVE      SP80,LABELATT[9]
          MOVE      SP80,LABELATT[10]
          MOVE      SP80,LABELATT[11]
          MOVE      SP80,LABELATT[12]
          MOVE      SP80,LABELATT[13]
          MOVE      SP80,LABELATT[14]
          MOVE      SP80,LABELATT[15]
          MOVE      SP80,LABELATT[16]
          MOVE      SP80,LABELATT[17]
          MOVE      SP80,LABELATT[18]
          MOVE      SP80,LABELATT[19]
          MOVE      SP80,LABELATT[20]
          MOVE      SP80,LABELATT[21]
          MOVE      SP80,LABELATT[22]
          MOVE      SP80,LABELATT[23]
          MOVE      SP80,LABELATT[24]
.
          RETURN
+
          INC       AGECHK
          INC       CALCYRS
          INC       DSPFIELD
          INC       DXOUTDET
          INC       DXPATCOD
          INC       DXPATDOC
          INC       DXPATMAS
          INC       IBAOVRIO/INC
          INC       IBAPASIO/INC
          INC       NHIETHIO/INC
          INC       OUTCTYIO/INC
          INC       PATICDIO/INC
          INC       PMIGTNID
.
LABOUT99  ENDPROC
