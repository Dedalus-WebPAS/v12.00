.*****************************************************************************
.*    Program      : PABXSERV                                                *
.*    Description  : Connect/Disconnect an Extension (PABX)                  *
.*                                                                           *
.*    Author       : ROD                                                     *
.*    Date         : 20/12/96                                                *
.*                                                                           *
.*    Called From  : IBAPAT31, IBABIL01, IBAPAT34, IBASYS64                  *
.*                                                                           *
.*    Reqd Vars    : PABXVARS.INC                                            *
.*                                                                           *
.*    Parameters   : EXTENSN -  Extension Number                             *
.*                   SERVICE -  E=Enable, D=Disable                          *
.*                                                                           *
.*    Modifications:                                                         *
.*****************************************************************************
PABXSERV  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*52,IBCNPBXI
.
. are we using this feature?
.
          MATCH     SP9,IBCNPBXI 
          GOTO      PBXSRV99 IF EQUAL
.
. get extension information
.
          OPEN      IBAEXTA1,"ibaextaf"
          PACK      KEY4,EXTENSN
          CALL      RDIBEXT1
          BRANCH    OVRCD,PBXSRV99
.
. if ward only extension, then dont connect/disconnect extension
.
          COMPARE   FOUR,IBEXETYP
          GOTO      PBXSRV99 IF EQUAL
.
. get HDP Equivalents to pass to script file
.
          MOVE      SP4,THCSCOD
          PACK      KEY5,ANSX,ANSV,IBEXPTYP
          CALL      RDCODE1
          MOVE      THCSCOD,PHONHCOD                 * phone type
.
          MOVE      SP4,THCSCOD
          PACK      KEY5,ANSX,ANSW,IBEXLEVL
          CALL      RDCODE1
          MOVE      THCSCOD,LEVLHCOD                 * Enable Access Level
.
          MOVE      SP4,THCSCOD
          PACK      KEY5,ANSX,ANSW,IBEXLVLD
          CALL      RDCODE1
          MOVE      THCSCOD,LVLDHCOD                 * Disable Access Level
.
          MOVE      SP4,THCSCOD
          PACK      KEY5,ANSX,ANSX,IBEXSRVL
          CALL      RDCODE1
          MOVE      THCSCOD,SRVLHCOD                 * Service Level
.
. if either of Phone Type or Level are blank, then dont send either
.
          MATCH     SP4,PHONHCOD
          IF        @EQUAL
            MOVE      SP4,LVLDHCOD
            MOVE      SP4,LEVLHCOD
            MOVE      SP4,SRVLHCOD
          ENDIF
.
          MATCH     SP4,LEVLHCOD
          IF        @EQUAL
            MATCH     SP4,LVLDHCOD
            IF        @EQUAL
              MOVE      SP4,PHONHCOD
              MOVE      SP4,SRVLHCOD
            ENDIF
          ENDIF
.
          MATCH     SP4,SRVLHCOD
          IF        @EQUAL
            MOVE      SP4,PHONHCOD
            MOVE      SP4,LEVLHCOD
            MOVE      SP4,LVLDHCOD
          ENDIF
.
          IF        IBEXTENN=0
            MOVE      SP4,PHONHCOD
            MOVE      SP4,LEVLHCOD
            MOVE      SP4,SRVLHCOD
          ENDIF
.
.    write appropriate access level depending on enable or disable
.
          MOVE      LVLDHCOD,ACSSLEVL
          MATCH     ANSE,SERVICE
          IF        @EQUAL
            MOVE      LEVLHCOD,ACSSLEVL
          ENDIF
.
. write to script file
.
          PACK      CMDLINE,SCRPTFIL,SP1,SERVICE,SP1,EXTENSN,SP1,PHONHCOD:
                            SP1,ACSSLEVL,SP1,SRVLHCOD,SP1,IBEXTENN
.
          EXECUTE   CMDLINE,TASKID         
.
PBXSRV99  RETURN
