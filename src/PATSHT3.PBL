.*****************************************************************************
. Program : PATSHT3
.  
. Modifications :
. V10.10.01  17/03/2017  Thanh T.              TSK 0832066
.            Changed DVA PREPAT/PMPXDVAC fields to use the new
.            concession card PMCDCNUM/PMCDDVAC fields     
.  V9.08.01  08/06/2007  Mike Laming           CAR 125736
.            Changes to PATDO1af Address lines - change DSADD3 to
.            DSADD4 and DSADD2 to DSADD3 (new DSADD2 still to go in)  
.  V5.08.01  06/06/2000  Jill Habasque                                 
.            Changed from key10 to key14 for the health fund file      
.            08/10/1998  Glenn Berry      5.07 changes
.*****************************************************************************
.  V5.01     14/03/89 J.Tan
.            Mods HOA ID Sheet.                SRF 5378
.  V5.02     22/03/89 J.Tan
.            Add form feed at the beginning of form
.  V5.03     24/05/89 J.Tan
.            Initialize discharge time.        SRF 100724
.
.         THIS INCLUDE PRINTS THE PATIENT SUMMARY (IDENTIFICATION) DETAILS
.
.         Layout 1 - H.O.A.
.
.        IDENTIFICATION VARIABLES
.
. DIM22    DIM      22
. ADOCDESC DIM      22
. RDOCTEL  DIM      12
.
. A8DATE   DIM      8
. B8DATE   DIM      8
. D8DATE   DIM      8
. XDAY     DIM      2
. XMON     DIM      2
. XYEAR    DIM      2
. MARITAL1 DIM      20
. RELIGN   DIM      20
. ADMSRC   DIM      20
. COUNTRY1 DIM      20
. XAGE     DIM      3
.
.        LINEUP PRINT
.
LINUPID3
        CALL      OPNPRINT
        PRINT     *F,*1,"Hospital",*39,"U/R Number",*52,"Adm.Number":
                  *N:                                               (LINE1)
                  *1,X30,X5,*39,"999999":
                  *52,"999999":
                  *N,*N:                                            (LINE3)
                  *1,"Surname",*23,"Given Name(s)":
                  *49,"Title",*56,"Sex":
                  *61,"Date of Birth",*76,"Age":
                  *N:                                               (LINE4)
                  *1,X20,*23,X20,X5:
                  *49,X4,*57,X1,*62,"99/99/99":
                  *76,"999":
                  *N,*N:                                            (LINE6)
                  *1,"Marital Status",*23,"Address",*49,"Telephone":
                  *N:                                               (LINE7)
                  *1,X20,*23,X20,X5:
                  *50,"(H)",*54,X10,X2:
                  *N:                                               (LINE8)
                  *23,X20,X5,*50,"(B)":
                  *54,X10,X2:
                  *N:                                               (LINE9)
                  *23,X15,*40,X4:
                  *N,*N:                                            (LINE11)
                  *1,"Religion",*23,"Allergies",*33,"Smoker":
                  *41,"Pension Number",*57,"Repat.Number":
                  *N:                                               (LINE12)
                  *1,X20,*25,X3,*34,X3:
                  *41,X10,X1,*57,X10:
                  *N,*N:                                            (LINE14)
                  *1,"Occupation",*23,"Country of Birth":
                  *45,"Language Used at Home":
                  *N:                                               (LINE15)
                  *1,X10,X4,*23,X20:
                  *45,X3,SP1,X20:
                  *N,*N:                                            (LINE17)
                  *1,"Claim Type",*23,"Health Fund",*55,"Table":
                  *64,"Membership Number":
                  *N:                                               (LINE18)
                  *1,X20:
                  *23,X30:
                  *55,X4,*64,X10:
                  *N,*N:                                            (LINE20)
                  *1,"Person to Notify",*23,"Relatioship":
                  *41,"Attending Doctor":
                  *N:                                               (LINE21)
                  *1,X20,*23,X10:
                  *41,X20,X2:
                  *N:                                               (LINE22)
                  *1,X20,X5,X1:
                  *41,X30:
                  *N:                                               (LINE23)
                  *1,X20,X5:
                  *41,X30:
                  *N:                                               (LINE24)
                  *1,X15,*15,X4:
                  *41,X30,*70,X4:
                  *N:                                               (LINE25)
                  *1,"Phone:",*8,"(H) XXXXXXXXXXXX":
                  *41,"Phone: XXXXXXXXXXXX":
                  *N:                                               (LINE26)
                  *8,"(B) XXXXXXXXXXXX":
                  *N:                                               (LINE27)
                  *41,"Referring Doctor",*68,"Phone":
                  *N:                                               (LINE28)
                  *41,X20,X2,*68,X10,X2:
                  *N,*N:                                            (LINE30)
                  *1,"Admission Date",*17,"Admission Time":
                  *33,"Admission Source":
                  *N:                                               (LINE31)
                  *4,"99/99/99",*21,"99:99:99",*33,X20:
                  *N,*N:                                            (LINE33)
                  *1,"Provisional Diagnosis":
                  *N:                                               (LINE34)
                  *1,X30,X20:
                  *N,*N:                                            (LINE36)
                  *1,"Discharge Date",*17,"Discharge Time":
                  *33,"Discharge Status",*55,"Discharge Destination":
                  *N:                                               (LINE34)
                  *4,"99/99/99",*21,"99:99:99",*33,X20:
                  *55,X15,X4:
                  *N,*N,*N,*N,*N,*N,*N,*N,*N:
                  *N,*N,*N,*N,*N,*N,*N,*N,*N:
                  *N,*N,*N,*N,*N,*N,*N,*N,*N:
                  *N
.
        CALL      CLSPRINT
        RETURN
.
.       SETUP ALL VARIABLES FOR PRINT
.
SETLID3 CALL            LABELAGE            * format age
. 
        UNPACK      ADATE,XCENT,XYEAR,XMON,XDAY
        PACK            A8DATE WITH XDAY,SLASH,XMON,SLASH,XYEAR
.
        UNPACK      PBDATE,XCENT,XYEAR,XMON,XDAY
        PACK            B8DATE WITH XDAY,SLASH,XMON,SLASH,XYEAR
.
        MOVE            SP10,D8DATE
        MOVE            SP10,DTIME
        MOVE            SP20,DSCSTAT
        MOVE            SP20,DSCDEST
.
        COMPARE         THREE,ASTAT
        GOTO            GETRDOC IF NOT EQUAL
.
.       Get discharges info
.
        MOVE            AADMNO,KEY8
        CALL            RDDSCH1
        BRANCH          OVRCD OF GETRDOC
.
        UNPACK      DDATE,XCENT,XYEAR,XMON,XDAY
        PACK            D8DATE WITH XDAY,SLASH,XMON,SLASH,XYEAR
.
.       Discharged status
.
        MOVE            SP20,TDESC
        PACK            KEY5 WITH CODED,DSTAT
        CALL            RDCODE1
        MOVE            TDESC,DSCSTAT
.
.       Discharged destination
.
        MOVE            SP20,TDESC
        PACK            KEY5 WITH CODEDD,DDEST
        CALL            RDCODE1
        MOVE            TDESC,DSCDEST
.
.       REFERRING DOCTOR INFO
.
GETRDOC MOVE            SP20,RDOCTEL
        BRANCH          CRDRTYP OF GETATD
.
        MOVE            ADOCTR,KEY6
        CALL            GETDOC1
        MOVE            DIM22,ARDRNAM
        MOVE            DTELES,RDOCTEL
.
.       ATTENDING DOCTOR INFO
.
GETATD  MOVE            ADOCTA,KEY6
        CALL            GETDOC1
        MOVE            DIM22,ADOCDESC
.
.       MARITAL DESCRIPTION
.
        MOVE            SP20,TDESC
        MOVE            SP20,MARITAL1
.
        MATCH           SP3,PMSTAT
        GOTO            NOMART IF EQUAL
.
        PACK            KEY5 WITH CODEM,PMSTAT
        CALL            RDCODE1
        MOVE            TDESC,MARITAL1
.
.       RELIGION DESCRIPTION
.
NOMART  MOVE            SP20,TDESC
        MOVE            SP20,RELIGN
.
        MATCH           SP3,PREG
        GOTO            NOREG IF EQUAL
.
        PACK            KEY5 WITH CODER,PREG
        CALL            RDCODE1
        MOVE            TDESC,RELIGN
.
.       ADMITTED SOURCE DESCRIPTION
.
NOREG   MOVE            SP20,TDESC
        MOVE            SP20,ADMSRC
        MATCH           SP3,ASRCE
        GOTO            NOASRC IF EQUAL
.
        PACK            KEY5 WITH CODES,ASRCE
        CALL            RDCODE1
        MOVE            TDESC,ADMSRC
.
.       COUNTRY DESCRIPTION
.
NOASRC  MOVE            SP20,TDESC
        MOVE            SP20,COUNTRY1
.
        MATCH           SP3,PCONT
        GOTO            NOCNTRY IF EQUAL
.
        PACK            KEY5 WITH CODEC,PCONT
        CALL            RDCODE1
        MOVE            TDESC,COUNTRY1
.
.       Language used at home
.
NOCNTRY MOVE            SP20,TDESC
        MOVE            SP20,U1DESC
.
        MATCH           SP3,AUSR1
        GOTO            NOLANG IF EQUAL
.
        PACK            KEY5 WITH CODEU1,AUSR1
        CALL            RDCODE1
        MOVE            TDESC,U1DESC
.
.       Allergies
.
NOLANG  MOVE            "UNK",DALERG
        LOAD            DALERG FROM AALERG OF DYES,DNO
.
.       Smokes
.
        MOVE            "UNK",DSMOKE
        LOAD            DSMOKE FROM PSMOK OF DYES,DNO
.
.       Claim type
.
        MOVE            SP20,TDESC
        PACK            KEY5 WITH CODECL,ACLAIM
        CALL            RDCODE1
        MOVE            TDESC,CLDESC
.
.       Health Fund
.
        PACK            HFNAME WITH SP20,SP10
        MOVE            "0000",HFTABL
        PACK            KEY14 WITH AFUNDH,HFTABL,SP10
        CALL            RDFUND1
.
ENDCLM1 RETURN
.
.
.      GET DOCTOR RECORD...... SET KEY6
.
GETDOC1 MOVE            SP20,DGNAME
        MOVE            SP20,DSNAME
        MOVE            SP12,DTELES
        MOVE            SP30,DSADD1
        MOVE            SP30,DSADD2
        MOVE            SP30,DSADD3
        MOVE            SP30,DSADD4
        MOVE            SP30,DSPOST
        MOVE            SP30,DIM22
.
        CALL            RDDOCT1
.
        MATCH           SP20,DSNAME
        RETURN          IF EQUAL
.
        MOVE            DGNAME,ANS
        CLEAR           DIM22
        MATCH           SP1,ANS
        GOTO            APPDSN1 IF EQUAL
        APPEND          ANS,DIM22
        APPEND          DOT,DIM22
APPDSN1 APPEND          DSNAME,DIM22
        APPEND          SP2,DIM22
        RESET           DIM22
        RETURN
.
.        PRINT PATIENT ID
.
PRTSHT3  
        CALL      OPNPRINT
.  
        MOVE      THREE,DIM1
        CALL      GCCARD00
.
        PRINT     *F,*1,"Hospital",*39,"U/R Number",*52,"Adm.Number":
                  *N:                                               (LINE1)
                  *1,CNAME,*39,AURNO,*52,AADMNO:
                  *N,*N:                                            (LINE3)
                  *1,"Surname",*23,"Given Name(s)":
                  *49,"Title",*56,"Sex":
                  *61,"Date of Birth",*76,"Age":
                  *N:                                               (LINE4)
                  *1,PSNAME,*23,PGNAME,*49,PTITL,*57,PSEX,*62,B8DATE:
                  *76,XAGE:
                  *N,*N:                                            (LINE6)
                  *1,"Marital Status",*23,"Address",*49,"Telephone":
                  *N:                                               (LINE7)
                  *1,MARITAL1,*23,PADD1,*50,"(H)",*54,PTELEP:
                  *N:                                               (LINE8)
                  *23,PADD2,*50,"(B)",*54,PTELEB:
                  *N:                                               (LINE9)
                  *23,PSUBRB,*40,PPOST:
                  *N,*N:                                            (LINE11)
                  *1,"Religion",*23,"Allergies",*33,"Smoker":
                  *41,"Pension Number",*57,"Repat.Number":
                  *N:                                               (LINE12)
                  *1,RELIGN,*25,DALERG,*34,DSMOKE:
                  *41,PPENNO,*57,PMCDCNUM:
                  *N,*N:                                            (LINE14)
                  *1,"Occupation",*23,"Country of Birth":
                  *45,"Language Used at Home":
                  *N:                                               (LINE15)
                  *1,POCCUP,*23,COUNTRY1,*45,AUSR1," ",U1DESC:
                  *N,*N:                                            (LINE17)
                  *1,"Claim Type",*23,"Health Fund",*55,"Table":
                  *64,"Membership Number":
                  *N:                                               (LINE18)
                  *1,CLDESC,*23,HFNAME,*55,AFNDTB,*64,AFNDMM:
                  *N,*N:                                            (LINE20)
                  *1,"Person to Notify",*23,"Relatioship":
                  *41,"Attending Doctor":
                  *N:                                               (LINE21)
                  *1,PNKNAME,*23,PNKRELP,*41,ADOCDESC:
                  *N:                                               (LINE22)
                  *1,PNKADD1,*41,DSADD1:
                  *N:                                               (LINE23)
                  *1,PNKADD2,*41,DSADD3:
                  *N:                                               (LINE24)
                  *1,PNKSUBR,*15,PNKPOST,*41,DSADD4,*70,DSPOST:
                  *N:                                               (LINE25)
                  *1,"Phone:",*8,"(H) ",PNKTELP:
                  *41,"Phone: ",DTELES:
                  *N:                                               (LINE26)
                  *8,"(B) ",PNKTELB:
                  *N:                                               (LINE27)
                  *41,"Referring Doctor",*68,"Phone":
                  *N:                                               (LINE28)
                  *41,ARDRNAM,*68,RDOCTEL:
                  *N,*N:                                            (LINE30)
                  *1,"Admission Date",*17,"Admission Time":
                  *33,"Admission Source":
                  *N:                                               (LINE31)
                  *4,A8DATE,*21,ATIME,*33,ADMSRC:
                  *N,*N:                                            (LINE33)
                  *1,"Provisional Diagnosis":
                  *N:                                               (LINE34)
                  *1,ADIAG1," ",ADIAG2:
                  *N,*N:                                            (LINE36)
                  *1,"Discharge Date",*17,"Discharge Time":
                  *33,"Discharge Status",*55,"Discharge Destination":
                  *N:                                               (LINE34)
                  *4,D8DATE,*21,DTIME,*33,DSCSTAT,*55,DSCDEST:
                  *N,*N,*N,*N,*N,*N,*N,*N,*N:
                  *N,*N,*N,*N,*N,*N,*N,*N,*N:
                  *N,*N,*N,*N,*N,*N,*N,*N,*N:
                  *N
.
        CALL      CLSPRINT
        RETURN
.
