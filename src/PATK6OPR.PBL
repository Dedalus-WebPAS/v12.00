.******************************************************************************
.*  File          :  PATK6OPR.PBL                                             *
.*                                                                            *
.*  Function      :  Update Keyword Index File for ICD10 Ed.6 Operation Codes *
.*                                                                            *
.*  Parameters    :  OPCODE,ODESC                                             *
.*                                                                            *
.*  Modifications :                                                           *
.*                                                                            *
.         V9.09.00  19/03/2008 Mike Laming     CAR 163918  HDP2008            *
.                   Created this procedure based on PATK5OPR.PBL              *
.******************************************************************************
.  Notes
.  -----
.  This Skeleton Routine is to Create a Procedure to Update
.  a Key Word Index Table that can be used for searching 
.  Masterfiles. The procedure should be called from the 
.  Add, Update and Delete functions in the Masterfile 
.  Maintenance Function.
.  
.  A Key Word Index Table is Defined in the following Manner
.                              
.  PTCOITM    DIM       xx    * Code Field Segment 1
.  PTCOITM    DIM       xx    * Code Field Segment n
.  PTCOKWD    DIM       15    * Key Word
.  PTCOSPA    DIM       10    * Spare
.
.  Index 1   PTCOITM, PTCOITM, PTCOKWD
.  Index 2   PTCOKWD, PTCOITM, PTCOITM
.
.  To convert this Procedure the following should be performed
.    Global Change PAT to the System ID eg :%s/PAT/PAT/g
.    Global Change OIC to the File   ID eg :%s/OIC/DKI/g
.    Global Change PTOI to the IO Call ID eg :%s/PTOI/PTDK/g
.    Modify the KEYINDEX defintion to combined Code Field Lengths eg 6
.    Global Change KEY9 to the Key for the Master File Table eg KEY6 
.    Global Change KEY24 to the Key for the Key Word Table eg KEY21
.    Change xxxxxxx to the Code Fields eg DCODEhh
.    Change the GENR0000 routine to call the BWORD000 for each
.    string of key words to be indexed for the Coded Field
.    eg MOVE    DSNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    DGNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    TDESC,KEYWORDS
.       CALL    BWORD000
.------------------------------------------------------------
          DEFPROC   PATK6OUP
.
          INC       PATKCOFD/INC
.
KEYINDEX  DIM       9         * Code Index in Table
KEYWORDS  DIM       60        * String Containing the Key Words to be Indexed
SIXTY     FORM      "60"
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
.------------------------------------------------------------
. Generate Word Index
.------------------------------------------------------------
GENR0000  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     PATK6OA1,"patkco6f"
          TRAPCLR  IO
          BRANCH   OVRCD,GENR9999
.
          MOVE     OPCODE,KEYINDEX
          CALL     CLEAR000        * Remove Current Key Word Index
          BRANCH   EXIT,GENR9999
.
          MOVE     OPCODE,KEY9     * Validate Record on File
          CALL     RDPT16O1        * ICD10 Ed.5 Procedure File
          BRANCH   OVRCD,GENR1900  * If Not then it Must be a Delete/Clear
.
          MOVE     OPCODE,KEYINDEX
          MOVE     OPCODE,KEYWORDS
          CALL     BWORD000
.
          MOVE     ODESC,KEYWORDS
          CALL     BWORD000
.
GENR1900  CLOSE    PATK6OA1
.
GENR9999  GOTO     PATKCIEP
.------------------------------------------------------------
. Clear Current Key Words
.------------------------------------------------------------
CLEAR000  PACK     KEY24,KEYINDEX,SP70
          CALL     RSPT6O1
          CALL     RKPT6O1
          BRANCH   OVRCD,CLEAR999
          MATCH    KEYINDEX,PTCOITM
          GOTO     CLEAR999 IF NOT EQUAL
.
          PACK     KEY24,PTCOITM,PTCOKWD,SP70
          CALL     DEPT6O1
          GOTO     CLEAR000
.
CLEAR999  RETURN
.-------------------------------------------------
. Determine Words to Index
.-------------------------------------------------
BWORD000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      BWORD000 IF NOT EOS
            GOTO      BWORD999               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWORDS,SP30      * reset form pointer
          MOVE      KEY60,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      BWORD100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Save this Word
.
BWORD100  MOVE      KEYWORDS,KEY15
          PACK      KEY15,KEY15,SP70
          REP       UPPLOW,KEY15             * Always Upper Case
          PACK      KEY24,KEYINDEX,KEY15
          UNPACK    KEY24,PTCOITM,PTCOKWD
          CALL      RDPT6O1
          IF        OVRCD=1
            CALL      WRPT6O1
          ENDIF
          GOTO      BWORD190
.
.         Check for any more words
.
BWORD190  SETLPTR   KEYWORDS,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY60,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY60,KEYWORDS
          GOTO      BWORD000
.
BWORD999  RETURN
.
          INC       PATKCOIO/INC
          INC       IBAOVRIO/INC
.
PATKCIEP  ENDPROC                          * End of Procedure
.
