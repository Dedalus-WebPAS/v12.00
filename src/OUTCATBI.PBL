+
.
.        OUTCATBI :
.
.        This include produces the body of the next invoice for the current
.        outpatient. PROGFLAG is used to determine if the invoice is to be
.        displayed, printed, or just have the total calculated.
.
.        include. The main includes where these may be found (other than
.        the I/O includes) are :
.
.               PATCALFN : Include to update the Fees Invoiced Statistics
.
.               OUTINVS  : Include that controls the printing of Invoices
.
.               PATINVTn : Include that contains the code that is dependent
.                          on the invoice layout (eg. Heading and Tail of
.                          the invoice, any right-hand-side, etc).
.
.---------------------------------------------------------------------------
. V12.00.01 19/05/2025  Ebon Clements  TSK 0955096
.           Alphanueric visit number changes
. V11.05.03 21/02/2025  J.Tan   TSK 0955356
.           Added FININVN,FINADMN,FINURNO
. V11.05.02 18/02/2025  Peter Vela     TSK 0939466
.           Eclipse Webservices DVAW 
.           Modified assignment of Distance in kms  
.           Added validation of DVAWBILL
. V11.05.01 27/11/2024  Thanh T        TSK 0939466
.           Added Distance in kms field
.----------------------------------------------------------------------------
.         V11.02.01 03/03/2022  J.Tan           TSK 0902652
.                   Mod for Patient Invoice new functionality (PTCNPPIN=1)
.         V11.01.01 18/02/2021  J.Tan           TSK 0888639
.                   Added PMMITMNO - Theatre Team no
.         V11.00.01 24/06/2020  J.Tan          TSK 0886178
.                   Mod to write to patinpaf - invoice pending details
.         V10.15.01 29/11/2019  J.Tan          TSK 0857392
.                   Mod for IBAHMS58 - ABF Comparison report
.         V10.14.05 16/10/2019  Peter Vela     TSK 0882906
.                   Fixed I*C for OUTBB1 ABF
.         V10.14.04 15/10/2019  J.Tan          TSK 0857392
.                   Mod Checking for Tier 2 for ABF invoice
.         V10.14.03 26/09/2019  J.Tan          TSK 0857392
.                   Mod for ABF Outpatient invoice
.         V10.14.02 28/06/2019  J.Tan          TSK 0857392
.                   Mod for ABF invoice
.         V10.14.01 04/02/2019  Peter Vela     TSK 0867807
.                   Added name property to column in OLINE300 Eclipse DBS
.         V10.13.09 15/01/2018  Peter Vela     TSK 0867807
.                   Added After care Override Indicator, Time Duration
.                   to MVDO0000
.         V10.13.08 11/01/2019  Peter Vela     TSK 0867807
.                   Added Multi Proc, Duplicate Override, No of Pat, SD and LSPN
.                   to MVDO0000
.         V10.13.07 11/01/2019  Peter Vela     TSK 0867807
.                   Added Restrictive override code and Service Text to
.                   MVDO0000
.         V10.13.06 05/12/2018  Peter Vela     TSK 0859743
.                   Added functionality for Theatre Items in MVDO0000
.         V10.13.05 26/10/2018  Thanh T        TSK 0859743
.                   Added BULKBILL chekcing
.         V10.13.04 25/10/2018  Thanh T        TSK 0859743
.                   Added Bulk Billing functionality
.         V10.13.03 23/10/2018  Tracey Nguyen  TSK 0859743
.                   Added PMMIHOSI - Hopsital Indicator on PMSMTIFD
.                   23/10/2018  Thanh T        TSK 0859743
.                   Changed OLINE300 to cater for bulk billing to display
.                   maintenace icon for extra details to be updated.      
.         V10.13.02 19/09/2018  J.Tan          TSK 0863727
.                   Added Restrictive Override code
.         V10.13.01 21/08/2017  J.Tan          TSK 0859742
.                   Added ECLIPSE new fields from pmsmtiaf
.---------------------------------------------------------------------------
.         V10.11.01 31/08/2017  J.Tan          TSK 0837479
.                   Added PMMICTYP - Consumption Type
.         V10.08.01 11/07/2016  J.Tan          CAR 0822042
.                   Fixed displaying Johns Hopkins outpatient invoice
.         V10.07.01 21/12/2015  J.Tan          CAR 0303942
.                   Mods to setup Payment code
.         V10.05.01 04/08/2014  Peter Vela   CAR 302164
.                   Added PMMIRBRS - Rebate Reason CAT Rr
.         V10.04.01 17/01/2014  J.Tan        CAR 295008
.                   Mods to set PMMIDTCR,PMMITMCR,PMMIIDCR
.                   20/01/2014  J.Tan        CAR 249828
.                   Added PMMIPMBS - Patient MBS Team and Counter
.         V10.03.03 25/07/2012  J.Tan          CAR 266612
.                   Fixed variables for printing item quantity and rates
.         V10.03.02 01/05/2012 J.Tan           CAR 237245
.                   Mods JH invoice to print rounding -ve amt in bracket
.         V10.03.01 21/03/2012 J.Tan         CAR 262115
.                   Mods to display/print Quantity
.         V10.02.02 04/11/2011 J.Tan           CAR 237245
.                   Mods Johns Hopkins invoice to print rounding amount
.         V10.02.01 21/04/2011 J.Tan           CAR 237245
.                   Mods for Johns Hopkins Insurance Providers
.         V10.01.01 15/12/2010  Mike Laming  CAR 233046
.                   Mods to Misc.Charges PATMCHFD - incl. new routine PATMCHRD
.         V10.00.03 23/09/2010 J.Tan         CAR 230664
.                   Mods for HEC invoice layout - PTCNINVL=18
.         V10.00.02 18/08/2010 J.Tan         CAR 222031
.                   Mods to set PMMIDUPD and PMMITUPD
.         V10.00.01 30/04/2010 J.Tan         CAR 220887
.                   Mods checking for Active/Inactive of Misc.charge
.         V9.11.01  31/03/2009  J.Tan        CAR 192772        
.                   Mods for Johns Hopkins invoice layout
.         V9.10.01  21/05/2008  J.Tan        CAR 167702        
.                   Mods for Cabrini invoice layout
.         V9.08.11  07/05/2007  Peter Vela     CAR SJOG ED Billing
.                   Initialised new pmsmtiaf variables
.         V9.08.10  02/05/2007  J.Tan          CAR 132276
.                   Mods for SX GST to use same as JH 
.         V9.08.09  05/03/2007  J.Tan          CAR 130462
.                   Mods for JH GST
.         V9.08.08  20/12/2006  J.Tan          CAR 128578
.                   Fixed Rounding        
.         V9.08.07  18/12/2006  J.Tan          CAR 128578
.                   Fixed Outpatient GST amount for Next invoice
.         V9.08.06  12/12/2006  J.Tan          CAR 127726
.                   Mods to JH GST Discount
.         V9.08.05  11/12/2006  J.Tan          CAR 127646
.                   Added Reference column to JH Details invoice
.         V9.08.04  05/12/2006  J.Tan          CAR 127092
.                   Mods printing Misc.group in bold for JH Invoices
.         V9.08.03  04/12/2006  J.Tan          CAR 126763
.                   Added quantity column to JH Detailed invoice
.         V9.08.02  27/11/2006  J.Tan          CAR 125987
.                   Fixed displaying JH Transaction date
.         V9.08.01  25/10/2006  J.Tan          CAR 122541
.                   Mods to use alternate item Group (Catg FN) for JH invoice
.         V9.07.01  28/07/2006 J.Tan  CAR 103365
.                   Mods for Johns Hopkins invoice
.         V9.04.06  29/09/2005 J.Tan  CAR 77671
.                   Fixed print invoice amounts
.         V9.04.05  30/12/2004 J.Tan  CAR 55977
.                   Fixed outpatient for Calvary layout (16)
.         V9.04.04  11/11/2004 J.Tan  CAR 55024
.                   Fixed outpatient SVHM invoice layout
.         V9.04.03  14/10/2004 J.Tan  CAR 54312
.                   Fixed outpatient SVHM invoice layout
.         V9.04.02  05/10/2004  J.Tan   CAR 53798
.                   Mods default charging claim code for multi hospital       
.         V9.04.01  20/08/2004 J.Tan  CAR 52835
.                   Setup hospital ID for patfinsf
.         V9.03.11  01/07/2004 J.Tan  CAR 51112
.                   Fixed displaying unrelease non banked deposit
.         V9.03.10  11/06/2004 J.Tan  CAR 50035
.                   Mods to set mechanical vent var from pmsmtiaf
.         V9.03.09  13/05/2004 J.Tan  CAR 49827
.                   Mods to display non-banked deposit
.         V9.03.08  28/04/2004 J.Tan  CAR 36504
.                   Fixed displaying cash on the next invoice screen
.         V9.03.07  03/02/2004 J.Tan  CAR 46269
.                   Mods for deleting items from next invoice screen.
.         V9.03.06  18/12/2003  J.Tan    CAR 44093
.                   Mods to use comdepaf for deposit
.         V9.03.05  20/10/2003  J.Tan    CAR 44172
.                   Mods to read misc.theatre item file (pmsmitaf)
.         V9.03.04  17/09/2003  Lina Vo        CAR 40497                      
.                   Changed index and read of patmchgf to include effective  
.                   date.
.         V9.03.03  30/07/2003  J.Tan  
.                   Fixed WEB print invoice
.         V9.03.02  10/11/2003  J.Tan  
.                   Fixed WEB outpatient total invoice
.         V9.03.01  30/05/2003  J.Tan  CAR 39514
.                   Fixed checking web server
.         V9.02.05  08/04/2003  J.Tan
.                   Mods for WEB billing
.         V9.02.04  06/11/2002  Dean Cramer 
.                   Mods for SVM format  
.         V9.02.03  25/06/2002  Dean Cramer 
.                   Mods for RCH format  
.         V9.02.02  04/06/2002  J.Tan
.                   Mods to remove tail18 
.         V9.02.01  22/05/2002  J.Tan     
.                   Mods for casepayment and added parameter not to print 
.                   claim details
.         V9.02.00  07/05/2002  J.Tan     
.                   Changes for RCH and STV
.         V5.09.B01 05/01/2001  Greg Horvat
.                   Replaced certain variables with KEY?? variables cause
.                   OUT66 couldnt compile
.         V5.08.04  19/07/2000  Dean Cramer   SRF 6710
.                   Alter key size and pack variables for code size
.                   increase in patmchgf.
.         V5.08.03  18/07/2000  Davin
.                   Print total amount at 71 not 69
.         V5.08.02  27/06/2000  J.TaN
.                   Recompiled for PATCALF - to set FGSTFLAG 
.         V5.08.01  29/05/2000  Caleb Sharman  SRF 639169 
.                   Mods to NEXTO1
.         V6.07.01  16/05/2000  J.Tan
.                   Mods to print invoice on landscape
.         V5.07.00  15/10/1998  Jim Stathopoulos
.                   507 Changes
.         V5.05.01  14/10/1997  Steve Armstrong    SRF 643393                  
.                   Removed invoice message for THC                            
.         V5.04.01  15/07/1996  Greg Horvat       SRF 616143
.                   Added new invoice layout for MBF - layout 17
.
.        V5.00  13/02/89 J.Tan
.               Reposition record after updating DTRO as Invoice no.changed
.        V5.01.01  25/08/89 Graeme Williams
.                  Added GST
.        V5.01.02  23/03/92 J.Tan   SRF 113203
.                  Fixed printing invoice amount
.        V5.01.03  07/04/92 J.Tan   SRF 114007
.                  Fixed the FINDATE
.        V5.01.04  12/06/92 Graeme Williams        SRF 114865
.                  Changes for 9 character misc items
.        V5.01.05  06/11/92 Graeme Williams
.                  Print PRTITCOD instead of OTRECEPT. This is so Hong Kong
.                  can print the rate, while other hospitals can print the
.                  reference. This required a change to all PROCOUTI routines
.                  as well (only PATINVT8 at the time of the change).
.        V5.01.06  03/08/1994  Rob Leonard  Quote 8064
.                  Changes in printing of body layout for Taranaki.
.        V5.02.01  02/03/1995  Greg Horvat      SRF 134196  Quote 8277
.                  Changed fees invoiced to print miscell. charges by claim
.                  code
.
.---------------------------------------------------------------------------
.
.        IF PROGFLAG = 1  THEN WORK OUT THE AMOUNT TO BE INVOICED
.
.        IF PROGFLAG = 2  THEN DISPLAY NEXT INVOICE
.
.        IF PROGFLAG = 3  THEN PRINT NEXT INVOICE
.
.        IF PROGFLAG = 4  THEN UPDATE PATFINS WITH AMOUNTS TO BE INVOICED
.
.        IPATFLAG=2 for Print Patient Invoice Using new Patient Inv.(PTCNPPIN=1)
.
.        PREBFLAG=1 for Print Rebate Invoice (PTCNPPIN=1)
.
.--------------------------------------------------------------------------
.
.         This is the main section of this include - it does the actual
.         work of calculating the body of the invoice
.
OUTTBI   
OLINE000 MOVE      ZERO,GROSSTOT
         MOVE      ZERO,NETTOT
         MOVE      ZERO,TOTGST
         MOVE      ZERO,LINEGST
         MOVE      ZERO,HFTHEA
         MOVE      ZERO,HFMISC
         MOVE      ONE,NUMINVS              * assume one invoice needed
         MOVE      ZERO,XGROSTOT
         MOVE      ZERO,XTOTGST
         MOVE      ZERO,XLINEGST
         MOVE      ZERO,XLINETOT
         MOVE      ZERO,TOTDISC
         MOVE      ZERO,ABFFLAG
         MOVE      ZERO,PRINTABF
         MOVE      ZERO,PPAYABLE            * Patient Portion
         MOVE      ZERO,TOTPGST
         MOVE      ZERO,PATLNTOT
         MOVE      ZERO,PATLNGST
         MOVE      ZERO,PREBFLAG
         MOVE      OBAOUTNO,DOBAOUTN
.
         OPEN      CONTROLF,"controlf"
         READ      CONTROLF,HUND03;*210,PTCNRDWN
         READ      CONTROLF,HUND09;*247,PTCNROUN
         READ      CONTROLF,TEN;*240,PTCNINVN
         READ      CONTROLF,HUND29;*89,PTCNPPIN
         MOVE      PTCNINVN,MAXLINE
.
         MATCH     "1",PTCNPPIN      * New Patient Invoice functionality
         IF        @EQUAL
           OPEN      OUTHTRA1,"outhtraf"
           CALL      CPTO0000        * Check if we are printing Rebate Invoice
         ENDIF
.
         MOVE      SP70,PMVXMHOS
         OPEN      PMSVX1A1,"pmsvx1af"
         PACK      KEY8,OBAOUTNO
         CALL      RDPMVX11
.
.         Check if Using ABF
.
          MOVE      ZERO,PATFEE              * Total patient amount
          MOVE      ZERO,HFAMT               * Total rebate amount
.
          READ      CONTROLF,HUND28;*105,PTCNUABF
          MATCH     "1",PTCNUABF
          GOTO      OLINE100 IF NOT EQUAL
.
          PACK      CFNAME WITH OSTFILE,FILBB1A1
          CLOSE     OUTBB1A1
          CALL      OPOTBB11
.
          PACK      KEY8,OBAOUTNO
          CALL      RDBOKB1
          BRANCH    OVRCD,OLINE100
.
          PACK      KEY5,ANSC,ANSL,OBACOMP
          CALL      RDCODE1
          BRANCH    OVRCD,OLINE100
.
          MATCH     "A",TCINDC2          * ABF Funding ?
          GOTO      OLINE100 IF NOT EQUAL
.
          MATCH     SP70,OTBBTCOD
          GOTO      OLINE100 IF EQUAL         * Blank Tier 2
.
          PACK      KEY5,ANSN,ANSC,OTBBTCOD
          CALL      RDCODE1
          BRANCH    OVRCD,OLINE100
.
          MATCH     "A",TCINDC1
          GOTO      OLINE100 IF NOT EQUAL     
.
          MOVE      ONE,ABFFLAG              * set ABF flag
.
.     PROGFLAG=1 (Calc.to be invoiced),PRABFINV=1 - ABF Charge
.                (Calc.to be invoiced),PRABFINV=2 - Item Charge only
.                (Calc.to be invoiced),PRABFINV=0 - ABF + Item chrgs
.
.     PROGFLAG=2 (Display Invoice) - ABF + Items charges
.
.     PROGFLAG=3 (Print Invoice) - PRABFINV=0 for Item charges only
.     PROGFLAG=3 (Print Invoice) - PRABFINV=1 for ABF charge
.     PROGFLAG=4 (Update FI) - ABF + Item charges
.
.     If we are not displaying invoice (progflag<>2), check for ABF charges flag
.
          IF        PROGFLAG=1 | PROGFLAG=3
            IF        PROGFLAG=1
              COMPARE   TWO,PRABFINV
              GOTO      OLINE100 IF EQUAL    * Calculate Item charges only
.
              IF        PRABFINV=1
                CALL      ABFS0000           * Check if ABF invoice raised
                MOVE      ONE,ABFFLAG        * set ABF flag back for non-disc.
                GOTO      OLINE640
              ENDIF
            ELSE
              COMPARE   ONE,PRABFINV
              GOTO      OLINE100 IF NOT EQUAL  * Print Item charges only
            ENDIF
          ENDIF
.
         CALL      ABFS0000                  * Check if ABF invoice raised
.
.        Restore invoice fields for PROGFLAG=3 - Print invoice
.
         IF        PROGFLAG=3
           MOVE      OBAOUTNO,PINVADM
           MOVE      OBAURNO,PINVUR
           MOVE      CNEXTINV,PINVNO
           PACK      PINVDTE WITH CCC,CYY,CMM,CDD
           REP       " 0",PINVDTE
           MOVE      TWO,PINVSYS
           MOVE      OBASITE,PINVSITE
         ENDIF
.
         MOVE      ONE,ABFFLAG               * set ABF flag back for non-dischg.
.
OLINE100 IF        CFEETYP=9 | CFEETYP=5
           GOTO      JHINV000                  * Johns Hopkins/SX
         ENDIF
.
.         Printing Rebate invoice, get item details from outhtraf file
.
          MOVE      OBAOUTNO,DOTNUMB
          IF        PREBFLAG=1
           CALL      ORINV0000
          ENDIF
.
.        Search for any items to be printed
.
         MOVE      OBAOUTNO,OTNUMB
         PACK      KEY14,OBAOUTNO,SP70
         CALL      RSPMMTI1
OLINE200 CALL      RKPMMTI1
         BRANCH    OVRCD,OLINE640
.
         MOVE      PMMITRAN,SAVTRNNO

         MATCH     OBAOUTNO,PMMIVISN
         GOTO      OLINE640 IF NOT EQUAL
.
.      If we are printing ABF invoice, do not include Item record
.
         IF        ABFFLAG=1
           MATCH     SP70,PMMIITEM
           IF        @EQUAL
             PACK      KEY14,PMMIVISN,PMMITRAN
             CALL      DEPMMTI1        * Delete Outpatient charge for ABF
             GOTO      OLINE2000
           ENDIF
.
           IF        PROGFLAG=1 | PROGFLAG=3
             COMPARE   ONE,PRABFINV
             GOTO      OLINE200 IF EQUAL
           ENDIF
         ENDIF
.
         CALL      MVDO0000              * move variables pmsmtiaf to dtrof
.
         MOVE      ZERO,PATLNTOT
         MOVE      ZERO,PATLNGST
.
         MOVE      OTPATPOR,PATLNTOT
         IF        OTSERVS<>0
           MULT      OTSERVS,PATLNTOT
         ENDIF
.
.         Check if multiple invoices are required. This code will set NUMINVS
.         to "2" if this record requires two invoices, and will leave NUMINVS
.         unchanged otherwise.
.
          LOAD      NUMINVS,OTNINVS,NUMINVS,TWO
.
.         Print Patient Invoice :
.         if no of invoice = 1, skip item with rebate portion
.         if no of invoice = 2, skip item with zero patient portion
.
          IF        IPATFLAG=2
            LOAD      IPASS,OTNINVS,ZERO,ONE
            IF        OTNINVS=2
              COMPARE   ZERO,OTPATPOR
              GOTO      OLINE200 IF EQUAL
            ELSE
              COMPARE   ZERO,OTREBPOR
              GOTO      OLINE200 IF NOT EQUAL
            ENDIF
          ENDIF
.
.
.         Check what pass we are doing.
.                Pass 0 = Only one invoice -> all items will appear
.                Pass 1 = Patient portion only
.                Pass 2 = Rebate portion only
.
          MOVE      ZERO,LINEGST
          BRANCH    IPASS,OLINE210,OLINE250
.
.         Process this miscellaneous item
.
          MOVE      OTPATAMT,LINETOT
          ADD       LINETOT,GROSSTOT
          ADD       OTREBPOR,HFMISC
.
          GOTO      OLINE290
.
.         Patient portion only wanted
.
OLINE210  MATCH     "1",PTCNPPIN
          IF        !@EQUAL
            COMPARE   ZERO,OTPATPOR
            GOTO      OLINE200 IF EQUAL        * no patient portion -> ignore
          ENDIF
.
          MOVE      OTPATPOR,LINETOT
          ADD       LINETOT,GROSSTOT
.
          GOTO      OLINE290
.
.         Rebate portion only wanted.
.
OLINE250  COMPARE   ZERO,OTPATPOR
          GOTO      OLINE270 IF EQUAL        * no patient portion -> print
.
          COMPARE   ZERO,OTREBPOR
          GOTO      OLINE200 IF EQUAL        * no rebate portion -> ignore
.
OLINE270  MOVE      OTREBPOR,LINETOT
          ADD       LINETOT,GROSSTOT
          ADD       OTREBPOR,HFMISC
.
          MOVE      ZERO,LINEGST             * No GST, for Rebate portion
          MOVE      ZERO,PATLNGST
.
.
.         Process this item
.
OLINE290 IF        IBCNUGST=2 & OTDTGSTA=1
            CALL      OGST0000               * Get Item GST rate
            MOVE      LINETOT,LINEGST
            MULT      IBGEAMNT,LINEGST     * GST amount
            DIV       "100.00",LINEGST     * Divide by 100 to get wanted fig.
            ADD       LINEGST,TOTGST
.
            IF        PATLNTOT<>0
              MOVE      PATLNTOT,GSTWORK
              MULT      IBGEAMNT,GSTWORK      * Item GST Amount
              DIV       "100.00",GSTWORK
              MOVE      GSTWORK,PATLNGST
            ENDIF
          ENDIF
.
          IF        INVDFLAG=1
            MOVE      SP70,KEY2
            MOVE      PMMIITEM,KEY9
            MOVE      LINETOT,FORM12D2
            CALL      WOUT0000              * Write to invoice details file
          ENDIF
.
          ADD       PATLNTOT,PPAYABLE
          ADD       PATLNGST,PPAYABLE
          ADD       PATLNGST,TOTPGST
.
         BRANCH    PROGFLAG OF OLINE200,OLINE300,OLINE500,OLINE620
.
.        Display miscellaneous item
.
OLINE300 UNPACK    OTDDATE WITH XCENT,XYEAR,XMON,XDAY
         MOVE      XDAY,TFDAY
         MOVE      XMON,TFMONTH
         MOVE      XYEAR,TFYEAR
         MOVE      XCENT,TFCENT
.
         PACK      KEY10,TFDAY,SLASH,TFMONTH,SLASH,TFCENT,TFYEAR
         REP       " 0",KEY10
.
         MOVE     XMON,F2
         LOAD     MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG,NSEP:
                             NOCT,NNOV,NDEC
.
         IF       IPATFLAG=2
           MOVE     PATLNTOT,LINETOT
           MOVE     PATLNGST,LINEGST
         ENDIF
.
         MOVE     LINETOT,TOTAMNT
         ADD      LINEGST,TOTAMNT
.
         IF       PMMISERV=0
           MOVE     ONE,PMMISERV
         ENDIF
.
         MOVE     ZERO,EXIT
         MATCH    "1",PTCNPPIN
         IF       @EQUAL
           IF       IPATFLAG<>2
             CALL      CPAO0000       * Check if Patient invoice raised
.                                     * if return EXIT =1 patient inv.raised
           ENDIF
         ENDIF
.
.      EXIT=1  Patient invoice has been raised for this item,
.      disable Delete item option
.
. check if it is bulk billing
.
         MATCH    "1",BULKBILL
         IF       !@EQUAL
           MATCH    "1",DVAWBILL
           GOTO     OLINE360 IF NOT EQUAL
         ENDIF
.
         WRITE    HTMLFILE;"<tr>":
                  "<td>":
                  "<dbsdate value='",OTDDATE,"'></dbsdate>":
                  "<dbsitem value='",OTITEMNO,"'></dbsitem>":
                  XDAY,SP1,MTHNAM3,SP1,XCENT,XYEAR:
                  "</td>";
.
.       If Patient invoice has been raised for this item, disable Delete item
.
        IF       EXIT=1
          WRITE    HTMLFILE;"<td>&nbsp;</td>":
                            "<td>&nbps;</td>";
          GOTO     OLINE350
        ENDIF
.
        WRITE     HTMLFILE;"<td><img src=../images/EditIcon.gif":
                  " class=ListIcon onclick='UpdateItem(#"":
                  OBAOUTNO,"#",#"",PMMITRAN,"#")'>":
                  OTDDESC,"</td>":
                  "<td><img src=../images/DeleteIcon.gif ":
                  " class=ListIcon onclick='DeleteItem(#"":
                  OBAOUTNO,"#",#"",PMMITRAN,"#")'>":
                  OTITEMNO,"</td>";
.
OLINE350  WRITE   HTMLFILE;"<td>&nbsp",OTRECEPT,"</td>":
                  "<td>",PMMISERV,"</td>":
                  "<td align=right>",LINETOT,"</td>":
                  "<td align=right>",LINEGST,"</td>":
                  "<td align=right>",TOTAMNT,"</td>":
                  "</tr>";
          MOVE     ZERO,EXIT
          GOTO     OLINE200
.
.        Not bulk billing
.
OLINE360  WRITE   HTMLFILE;"<tr>":
                  "<td>&nbsp",XDAY,SP1,MTHNAM3,SP1,XCENT,XYEAR,"</td>":
                  "<td>&nbsp",OTDDESC,"</td>";
.
.       If Patient invoice has been raised for this item, disable Delete item
.
          IF       EXIT=1
            WRITE    HTMLFILE;"<td>&nbsp;</td>";
            GOTO     OLINE370
          ENDIF
.
          WRITE  HTMLFILE;"<td><img src=../images/DeleteIcon.gif ":
                  " class=ListIcon onclick='DeleteItem(#"":
                  OBAOUTNO,"#",#"",PMMITRAN,"#")'>":
                  OTITEMNO,"</td>";
.
OLINE370  WRITE   HTMLFILE;"<td>&nbsp",OTRECEPT,"</td>":
                  "<td>",PMMISERV,"</td>":
                  "<td align=right>",LINETOT,"</td>":
                  "<td align=right>",LINEGST,"</td>":
                  "<td align=right>",TOTAMNT,"</td>":
                  "</tr>";
          MOVE     ZERO,EXIT
          GOTO     OLINE200
.
.        Print miscellaneous item
.
OLINE500 MOVE      LINETOT,XLINETOT
         MOVE      LINEGST,XLINEGST
.
.
.        Do any miscellaneous processing
.
         MOVE      OTSERVS,NODAYS
         MOVE      OTPATPOR,LSTRATE
         IF        IPATFLAG<>2
           ADD       OTREBPOR,LSTRATE
         ENDIF
         MOVE      OTDDATE,DFDATE
         MOVE      OTITEMNO,KEY9
         MOVE      OTDDESC,DIM30
         MOVE      TWO,RECFLAG
.
.         the following fields are used for printing (PRLN000)
.
         MOVE      OTITEMNO,PMMIITEM
         PACK      PMMIDESC,OTDDESC,SP70
         PACK      PMMIDES2,SP70,SP70
         MOVE      OTSERVS,PMMISERV
         MOVE      SP70,PMMIINCT
         MOVE      "3",PMMIRTYP
         LOAD      PMMIRTYP,OTRECTYP,THREE,FIVE,SIX,FOUR,SEVEN,TWO,NINE
         MOVE      SP70,PMMIMVBR
         CALL      PRLN0000                    * print item
.
.         Check if we need to update the number of visits that are charged
.
          IF        (OTCNFTYP = 1) & (OTPATPOR <> 0.00)
.
.           The visit charge record will be the only one with a blank item no.
.
            MATCH     SP9,OTITEMNO
            IF        @EQUAL
              PROC      OPKWC000             * update the visits charged
            ENDIF
          ENDIF
.
.         If we are generating multiple invoices, and this item contains both
.         a patient and rebate portion, then we need to split into two items,
.         one for each portion
.
          MOVE      OTREBPOR,SAVHF     * save rebate portion
          BRANCH    IPASS,OLINE510
          GOTO      OLINE580
.
.         We have an item during the patient portion invoice. Check if it
.         has a rebate portion as well
.
OLINE510  COMPARE   ZERO,OTREBPOR
          GOTO      OLINE580 IF EQUAL
.
          COMPARE   TWO,IPATFLAG
          GOTO      OLINE570 IF EQUAL     * patient inv using New functionality
.
.         Split this item into two records. Start by making a new item for
.         rebate portion
.
          MOVE      ZERO,OTPATPOR            * no patient portion on new item
          MOVE      OTREBPOR,OTPATAMT        * rebate portion is entire amount
          MOVE      ZERO,OTDTGSTM            * no GST amount
.
          MOVE      OTREBPOR,PMMIRBAT        * rebate portion
          MOVE      ZERO,PMMIAMTP            * no patient portion on new item
          MOVE      OTREBPOR,PMMIAMTT        * rebate portion is entire amount
          MOVE      ZERO,PMMIGSTM            * no GST amount
          MOVE      "0",PMMIMVBR             * Mechanical ventilation billing
.
          CALL      NXTTRAN1                 * generate a new transaction no.
          MOVE      OTTRANS,PMMITRAN
          MOVE      SP70,PMMICNTR
          MOVE      SP70,PMMISUNQ
          MOVE      SP70,PMMIINCT
          MOVE      SP70,PMMISUBN
          MOVE      SP70,PMMIEDAD
          MOVE      SP70,PMMISVTM
          MOVE      SP70,PMMIDUPD
          MOVE      SP70,PMMITUPD
          MOVE      SP70,PMMIWUSR
          PACK      PMMIDTCR,CCC,CYY,CMM,CDD
          REP       " 0",PMMIDTCR
          CALL      IBACLOCK
          MOVE      CTIMEIS,PMMITMCR
          MOVE      PASSCODE,PMMIIDCR
          MOVE      SP70,PMMITMNO
          MOVE      SP70,PMMIPMBS
          MOVE      SP70,PMMIRBRS
          MOVE      SP70,PMMICTYP
          PACK      PMMIACOI,SP70       * After Care Override Indicator
          PACK      PMMIDSOV,SP70       * Duplicate Service Override
          PACK      PMMISTXT,SP70       * Service Text
          PACK      PMMILSPN,SP70       * Location Specific Practice No(LSPN)
          PACK      PMMIMPOV,SP70       * Multi Procedure Override
          PACK      PMMINMPT,SP70       * Number of Patients Seen
          PACK      PMMISDCD,SP70       * Self Deem Code (Cat Sd)
          PACK      PMMITDUR,SP70       * Time Duration (Mins)
          PACK      PMMIROVR,SP70       * Restricted Override Code (Cat Ro)
          PACK      PMMIHOSI,SP70       * Hospital Indicator         *T0859743
          MOVE      SP70,PMMIDKSM       * Distance in kms 
          PACK      PMMISPAR,SP70,SP70
          PACK      KEY14,OTNUMB,OTTRANS,SP70
          CALL      WRPMMTI1                 * post new item
.
          MOVE      SAVTRNNO,OTTRANS         * restore the transaction number
.
OLINE570  MOVE      ZERO,OTREBPOR            * no rebate portion on this item
          MOVE      OTPATPOR,OTPATAMT        * patient portion is entire amount
.
.        Add the invoice number to the DTRAN record
.
OLINE580 MOVE      CNEXTINV,OTINVNO
         MOVE      ONE,OTINVPRT
         MOVE      ZERO,OTDTGSTM
         IF        IBCNUGST=2
           MOVE      LINEGST,OTDTGSTM        * GST amount
         ENDIF
         PACK      OTDTEFFD,CCC,CYY,CMM,CDD
         REP       " 0",OTDTEFFD
         MOVE      SP70,OTDTPCOD
.
OLINE582 CALL      NXTTRAN1
         PACK      KEY22,OTNUMB,OTINVNO,OTTRANS
         CALL      RDADTRO1
         COMPARE   ZERO,OVRCD
         GOTO      OLINE582 IF EQUAL
         CALL      WRDTRO1
.
         COMPARE   TWO,IPATFLAG
         GOTO      OLINE600 IF NOT EQUAL
.
         COMPARE   ZERO,SAVHF
         GOTO      OLINE600 IF EQUAL    * No rebate portion
.
.        If we are printing New Patient Invoice functionality, write the Rebate
.        portion to outhtraf file (HF debtor transaction)
.
          CALL      CLOUTHTR
          CALL      MOHT0000            * Move DTR fields to outhtraf
.
          MOVE      CNEXTINV,OTHTPINV    * Patient invoice number
          MOVE      OTTRANS,OTHTPTRN     * Transaction number
.
          MOVE      SP70,OTHTINVN
          MOVE      OTTRANS,OTHTTRAN
.
          MOVE      SAVHF,OTHTRPOR       * Restore rebate portion
          MOVE      ZERO,OTHTPPOR        * zero patient portion
          MOVE      SAVHF,OTHTAMTT       * Rebate portion is full amount
          MULT      OTHTSERV,OTHTAMTT
.
          MOVE      ZERO,OTHTGSTM
          IF        IBCNUGST=2
            CALL      OGST0000               * Get Item GST rate
            MOVE      OTHTAMTT,OTHTGSTM
            MULT      IBGEAMNT,OTHTGSTM    * GST amount
            DIV       "100.00",OTHTGSTM    * Divide by 100 to get wanted fig.
          ENDIF
          GOTO      OLINE586
.
OLINE584  CALL      NXTTRAN1      * Get the next transaction number
          MOVE      OTTRANS,OTHTTRAN
.
OLINE586  PACK      KEY22,OTHTNUMB,OTHTINVN,OTHTTRAN
          CALL      RAOTHTR1
          COMPARE   ZERO,OVRCD
          GOTO      OLINE584 IF EQUAL
.
          CALL      WROTHTR1
.
OLINE600 PACK      KEY14,OTNUMB,SAVTRNNO
         CALL      DEPMMTI1
.
         CALL      RSPMMTI1
.
.        Update the Fees Invoiced Summary file
.
OLINE620 MOVE      OTPATAMT,FINAMT
         MOVE      "A",FINTYPE
.
         MATCH     SP9,OTITEMNO              * standard charge ?
         GOTO      OLINE630 IF NOT EQUAL      * no, post to normal FINCODE
.
.        For the standard charge, check if we are posting to the clinic id
.
         BRANCH    OTCNCFEE,OLINE632
.
.        Using misc group code
.
OLINE630  MOVE      SP3,OBACOMP
          PACK      KEY8,OTNUMB
          CALL      RDBOKB1
.
          MOVE      "M",ANS
          IF        PTCNMFEE=1
            MATCH     OTMISGRP,SP3
            IF        @EQUAL
              PACK      FINCODE,ANSM,OBACOMP,OTITEMNO,SP20
.                               Misc. Charge, Claim Code, Item Code
            ELSE
              PACK      FINCODE,ANSM,OBACOMP,OTMISGRP,SP20
.                               Misc. Charge, Claim Code, Group Code
            ENDIF
          ELSE
            PACK      FINCODE,ANSM,OTMISGRP,OTITEMNO,SP20
.                             Misc. Charge, Group Code, Item Code
          ENDIF
          GOTO      OLINE634
.
.        Using clinic id
.
OLINE632 PACK      FINCODE,ANSA,PVIDOCT,SP20
.
.        Post to the fees invoiced file
.
OLINE634 UNPACK    SP70,FININVN,FINADMN,FINURNO
         IF        PROGFLAG=3
           PACK      FININVN,CNEXTINV,SP70
           MOVE      OBAOUTNO,FINADMN
           MOVE      OBAURNO,FINURNO
         ENDIF
.
         MOVE      PINVDTE,FINDATE       * Invoice date
         REP       " 0",FINDATE
         MOVE      TWO,FINSYS
         MOVE      OBASITE,FINSITE
         MOVE      ZERO,FGSTFLAG
         MOVE      PMVXMHOS,FINHOSP
         CALL      PATCALF
.
         IF        IBCNUGST=2
           IF        LINEGST<>0
             MOVE      LINEGST,FINAMT
             MOVE      ONE,FGSTFLAG
             MOVE      PMVXMHOS,FINHOSP
             CALL      PATCALF
           ENDIF
         ENDIF
.
         GOTO      OLINE200
.
.        End of this invoice's details
.
OLINE640 MOVE      GROSSTOT TO NETTOT
         ADD       TOTGST,NETTOT
.
         MOVE      GROSSTOT,XGROSTOT
         MOVE      TOTGST,XTOTGST
.
.        Calculate the Goods & Services tax if needed
.
         COMPARE   ONE,IBCNUGST
         CALL      GTGSO000 IF EQUAL         * Calculate GST
.
         BRANCH    PROGFLAG OF OLINE900,OLINE740,OLINE700,OLINE800
.
.        Printing invoice. Check if GST needs to be added
.
OLINE700 COMPARE   ONE,IBCNUGST
         CALL      ADGSO000 IF EQUAL         * Add GST to invoice
.
.        Check if claim details are to be printed
.
         OPEN      CONTROLF,"controlf"
         READ      CONTROLF,HUND05;*189,PTCNPRCL
         IF        PTCNPRCL<>0
           CALL      PRTCLMS
         ENDIF
.
.        Display invoice. Check if GST needs to be added
.
OLINE740 COMPARE   ONE,IBCNUGST
         CALL      DSGSO000 IF EQUAL         * Display GST on invoice
.
.        Check if there is room for the invoice total
.
         GOTO      OLINE900
.
.        Just updating the fee's invoiced file. Add GST if needed
.
OLINE800 COMPARE   ONE,IBCNUGST
         CALL      ADGSO800 IF EQUAL         * Add GST to fee's invoiced file
.
OLINE900  BRANCH    PROGFLAG OF OLINE990,OLINE920,OLINE980,OLINE990
.
.        Display invoice total and check if there was any cash received
.
OLINE920 MOVE     GROSSTOT,TOTAMNT
         ADD      TOTGST,TOTAMNT
.
         WRITE    HTMLFILE;"<tr>":
                  "<td>&nbsp;</td>":
                  "<td><b> Invoice Total </b></td>":
                  "<td>&nbsp;</td>":
                  "<td>&nbsp;</td>":
                  "<td>&nbsp;</td>";
.
         IF       IPATFLAG=2
           MOVE      PPAYABLE,FORM12P2
           SUB       TOTPGST,FORM12P2
.
           WRITE    HTMLFILE;"<td align=right>",FORM12P2,"</td>":
                    "<td align=right>",TOTPGST,"</td>":
                    "<td align=right>",PPAYABLE,"</td>":
                    "</tr>";
          MOVE      PPAYABLE,NETTOT
.
         ELSE
           WRITE    HTMLFILE;"<td align=right>",GROSSTOT,"</td>":
                    "<td align=right>",TOTGST,"</td>":
                    "<td align=right>",TOTAMNT,"</td>":
                    "</tr>";
         ENDIF
.
         COMPARE   ZERO,CASHFLAG
         GOTO      OLINE960 IF EQUAL
.
.        Print Cash on WEB page
.
         PACK      KEY8,CSCENT,CSYEAR,CSMON,CSDAY
         REP       " 0",KEY8
         UNPACK    KEY8,XCENT,XYEAR,XMON,XDAY
         MOVE      XMON,F2
         LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG,NSEP:
                              NOCT,NNOV,NDEC
         MOVE      SP70,KEY30
         MOVE      "PAYMENT RECEIVED WITH THANKS",KEY30
.
         MULT      "-1",CSAMT
         WRITE     HTMLFILE;"<tr>":
                   "<td>",XDAY,SP1,MTHNAM3,SP1,XCENT,XYEAR,"</td>":
                   "<td>",KEY30,"</td>":
                   "<td>&nbsp;</td>":
                   "<td>&nbsp;</td>":
                   "<td>&nbsp;</td>":
                   "<td>&nbsp;</td>":
                   "<td>&nbsp;</td>":
                   "<td align=right>",CSAMT,"</td>":
                   "</tr>";
         MULT      "-1",CSAMT
         SUB       CSAMT FROM NETTOT
.
.         Checking for non bank deposit
.
OLINE960  COMPARE    ZERO,NBNKAMNT
          GOTO       OLINE970 IF EQUAL
.
          MULT      "-1",NBNKAMNT
          WRITE     HTMLFILE;"<tr><td>&nbsp;</td>":
                             "<td>Non-Banked Deposit</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td align=right>&nbsp",NBNKAMNT,"</td>":
                             "<tr>"
          MULT      "-1",NBNKAMNT
          SUB       NBNKAMNT FROM NETTOT
.
.        Display balance payable
.
OLINE970   WRITE     HTMLFILE;"<tr>":
                   "<td>&nbsp;</td>":
                   "<td><b> Balance Payable </b></td>":
                   "<td>&nbsp;</td>":
                   "<td>&nbsp;</td>":
                   "<td>&nbsp;</td>":
                   "<td>&nbsp;</td>":
                   "<td>&nbsp;</td>":
                   "<td align=right>",NETTOT,"</td>":
                   "</tr>";
.
         WRITE     HTMLFILE;"<input type=hidden name=ppayable ":
                                "value='",PPAYABLE,"'>"
         RETURN
.
.        Print the invoice total
.
OLINE980 MOVE      GROSSTOT,XGROSTOT
         MOVE      TOTGST,XTOTGST
.
         MOVE      "Invoice Total",IVTOTDES
         MOVE      THREE,RECFLAG
         CALL      PRLN0000              * Print invoice total
.
.        Print the tail end of the invoice
.
         CALL      ENDPRT00
         GOTO      OLINE999
.
.        Add GST to invoice amount
.
OLINE990 COMPARE   ONE,IBCNUGST
         RETURN    IF NOT EQUAL
.
         ADD       GSTAMNT,NETTOT
         ADD       GSTAMNT,GROSSTOT
         MOVE      GROSSTOT,XGROSTOT
OLINE999 RETURN
+
.*******************************************************************************
.        Write to Invoice detail file
.*******************************************************************************
WOUT0000  CALL      CLPATINP              * clear patinpaf vars
.
          MOVE      KEY2,PTIPRTYP         * record type
          MATCH     SP70,PTIPRTYP
          GOTO      WOUT1000 IF NOT EQUAL * must be ABF Charge
.
          MATCH     SP70,PMMIMGRP
          IF        !@EQUAL
            MOVE      PMMIMGRP,PTIPBAND     * Item Group (Cat FI)
            PACK      KEY5,ANSF,ANSI,PTIPBAND
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TCINDC2,PTIPMHDP  * misc. charge group (Cat.FI, Ind.2)
            ENDIF
          ENDIF
          CALL      OMITM000             * misc. item record
.
          MATCH     "2",PMMIRTYP
          IF        @EQUAL
            MOVE      "2",PTIPTTYP       * Theatre
          ENDIF
          MATCH     "3",PMMIRTYP
          IF        @EQUAL
            MOVE      "3",PTIPTTYP       * Miscellaneous item
          ENDIF
          MOVE      PMMIRBAT,PTIPFREB    * estimated fund rebate
.
          GOTO      WOUT4000
.
WOUT1000  MOVE      ONE,PTIPTRNN         * ABF charge
          MOVE      "4",PTIPTTYP
          MOVE      FORM12D2,PTIPGREV
.
WOUT4000  MOVE      OBAOUTNO,PTIPADMN       * admission number
          MOVE      OBAURNO,PTIPURNO        * u/r number
          MOVE      OBACOMP,PTIPCLAM        * claim type
          MOVE      OTBBFUND,PTIPHFND       * health fund
          MOVE      OTBBTBLE,PTIPFTBL       * health fund table
          MOVE      OBADATE,PTIPADAT        * visit Date
          MOVE      OBADOCT,PTIPADOC        * Attending Doctor Code
          MOVE      OTBBRFGP,PTIPRDOC       * Referring Doctor
          PACK      PTIPITEM,KEY9,SP70      * Item code
.
          CALL      IBACLOCK
          PACK      PTIPCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTIPCDAT           * date record created
          MOVE      CTIMEIS,PTIPCTIM
          REP       " 0",PTIPCTIM           * time record created
          PACK      PASSCODE,PASSCODE,SP70
          MATCH     SP70,PASSCODE
          IF        !@EQUAL
            MOVE      PASSCODE,PTIPOPER     * operator id
          ELSE
            MOVE      "CMDL",PTIPOPER
          ENDIF
          IF        UGSTFLAG=1
            MOVE      "U",PTIPGSTU          * Unknown GST
          ENDIF
.
          PACK      KEY16,PTIPADMN,PTIPRTYP,PTIPTRNN
          CALL      WRPTINP1
.
WOUT9999  RETURN
+
.******************************************************************************
.*                                  OMITM000                                  *
.*                      Set up Item fields to patinpaf                        *
.******************************************************************************
OMITM000  MATCH     ANSA,PTIPMHDP
          IF        @EQUAL
            PACK      PTIPRTYP,ANSA,SP3     * record type
            MOVE      FORM12D2,PTIPAREV     * accommodation revenue
            ADD       ONE,ACCCOUNT
            MOVE      ACCCOUNT,PTIPTRNN     * transaction number
            GOTO      OMITM999
          ENDIF
.
          MATCH     ANSI,PTIPMHDP
          IF        @EQUAL
            PACK      PTIPRTYP,ANSI,SP3     * record type
            MOVE      FORM12D2,PTIPIREV     * ICU/CCU revenue
            ADD       ONE,ICUCOUNT
            MOVE      ICUCOUNT,PTIPTRNN     * transaction number
            GOTO      OMITM999
          ENDIF
.
          MATCH     ANST,PTIPMHDP
          IF        @EQUAL
            PACK      PTIPRTYP,ANST,SP3     * record type
            MOVE      FORM12D2,PTIPTREV     * theatre revenue
            ADD       ONE,THTCOUNT
            MOVE      THTCOUNT,PTIPTRNN     * transaction number
            GOTO      OMITM999
          ENDIF
.
          MATCH     ANSP,PTIPMHDP
          IF        @EQUAL
            PACK      PTIPRTYP,ANSP,SP3     * record type
            MOVE      LINETOT,PTIPPREV      * prosthesis revenue
            ADD       ONE,PRSCOUNT
            MOVE      PRSCOUNT,PTIPTRNN     * transaction number
            GOTO      OMITM999
          ENDIF
.
          MATCH     ANSD,PTIPMHDP
          IF        @EQUAL
            PACK      PTIPRTYP,ANSD,SP3     * record type
            MOVE      LINETOT,PTIPHREV      * pharmacy revenue
            ADD       ONE,PHRCOUNT
            MOVE      PHRCOUNT,PTIPTRNN     * transaction number
            GOTO      OMITM999
          ENDIF
.
          MATCH     ANSL,PTIPMHDP
          IF        @EQUAL
            PACK      PTIPRTYP,ANSL,SP3     * record type
            MOVE      LINETOT,PTIPLREV      * labour ward revenue
            ADD       ONE,LBWCOUNT
            MOVE      LBWCOUNT,PTIPTRNN     * transaction number
            GOTO      OMITM999
          ENDIF
.
          PACK      PTIPRTYP,ANSM,SP3       * record type
          MOVE      LINETOT,PTIPMREV        * miscellaneous charges revenue
          ADD       ONE,MISCOUNT
          MOVE      MISCOUNT,PTIPTRNN       * transaction number
.
OMITM999  RETURN
+
.******************************************************************************
.         Check if ABF Invoice has been raised
.******************************************************************************
ABFS0000  MATCH     "IBAHMS58",PRGID
          GOTO      ABFS2000 IF EQUAL        * Calc.ABF charges for comparison
.
          OPEN      PATINVR3,"patinvrf"
          PACK      KEY16,OBAOUTNO,SP70
          CALL      RSPTINV3
ABFS1000  CALL      RKPTINV3
          BRANCH    OVRCD,ABFS2000
.
          MATCH     PINVADM,OBAOUTNO
          GOTO      ABFS2000 IF NOT EQUAL
.
          REP       " 0",PTINCRST
          MATCH     "0",PTINCRST
          GOTO      ABFS1000 IF NOT EQUAL  * Credit note
.
          COMPARE   THREE,PTINCMIX
          GOTO      ABFS9000 IF EQUAL      * found an ABF Invoice
          GOTO      ABFS1000
.
ABFS2000  PROC      ABFOTINV               * Outpatient ABF
.
ABFS9000  MOVE      ZERO,EXIT
          GOTO      ABFS99999
.
ABFS9100  MOVE      ONE,EXIT
          GOTO      ABFS99999
.
ABFS9999  RETURN
+
.******************************************************************************
.        Johns Hopkins Invoice
.******************************************************************************
JHINV000 MOVE      ZERO,MISCTOT
         MOVE      ZERO,MISCGST
         MOVE      ZERO,TOTAMNT
         MOVE      ZERO,GSTAMNT
         MOVE      ZERO,GROSSTOT
         MOVE      SP70,SAVGCOD
         MOVE      "38",MAXLINE             * default to 38 for SX
         IF        CFEETYP=9
           MOVE      "30",MAXLINE           * JH
         ENDIF
         OPEN      PMSMTIA4,"pmsmtiaf"
.
         MOVE      ZERO,COMPFLG
         MOVE      OBAOUTNO,OTNUMB
         PACK      KEY18,OBAOUTNO,SP70
         CALL      RSPMMTI4 
JHINV100 CALL      RKPMMTI4
         BRANCH    OVRCD,JHINV5000
.         
         MATCH     OBAOUTNO,PMMIVISN
         GOTO      JHINV500 IF NOT EQUAL
.
         MOVE      PMMITRAN,SAVTRNNO
         CALL      MVDO0000              * move variables pmsmtiaf to dtrof
.
         MATCH     "3",PMMIRTYP
         GOTO      JHINV180 IF NOT EQUAL   * not Misc.Item, must be MBS item
.
         COMPARE   ZERO,COMPFLG
         GOTO      JHINV180 IF EQUAL      * first record
.
         MATCH     SP70,PMMIMGRP
         GOTO      JHINV180 IF EQUAL
.
         MATCH     SAVGCOD,PMMIMGRP
         GOTO      JHINV180 IF EQUAL
.
.        Display/Print total item for the group
.
         MOVE      SP70,TDESC
         MATCH     SP70,SAVGCOD
         IF        @EQUAL
           MOVE      "No Group Code",TDESC
         ELSE
           IF        CFEETYP=9
             PACK      KEY5,ANSF,ANSN,SAVGCOD,SP70
           ELSE
             PACK      KEY5,ANSF,ANSI,SAVGCOD,SP70
           ENDIF
           CALL      RDCODE1
           IF        OVRCD=1
             CLEAR     TDESC
             APPEND    "Invalid Group",TDESC
             APPEND    SAVGCOD,TDESC
             APPEND    SP70,TDESC
             RESET     TDESC
           ENDIF
         ENDIF
.
         ADD       MISCTOT,TOTAMNT
         ADD       MISCGST,GSTAMNT
.
         BRANCH    PROGFLAG,JHINV160,JHINV120,JHINV140
         GOTO      JHINV160
.
JHINV120 MOVE      MISCTOT,FORM12P2
         ADD       MISCGST,FORM12P2
         IF        INVTYPE=1
           WRITE     HTMLFILE;"<tr><td>&nbsp;</td>":
                              "<td>",TDESC,"</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td align=right>&nbsp;</td>":
                              "<td align=right>",MISCTOT,"</td>":
                              "<td>&nbsp;</td>":
                              "<td align=right>",FORM12P2,"</td>":
                              "</tr>"
         ELSE
           WRITE     HTMLFILE;"<tr><td>&nbsp;</td>":
                              "<td><b>",TDESC,"</b></td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td align=right>",FORM12P2,"</td>":
                              "</tr>"
          ENDIF
          GOTO      JHINV160
.
.         Print the Item category description
.
JHINV140  COMPARE   MAXLINE,COUNT
          CALL      TAIL IF NOT LESS
.
          MOVE      MISCTOT,FORM7P2
          IF        INVTYPE=1
            PRINT     *3,ESCCHR,CHARG,TDESC,ESCCHR,CHARH:
                      *58,FORM7P2
          ELSE
            PRINT     *3,ESCCHR,CHARG,TDESC,ESCCHR,CHARH:
                      *71,FORM7P2
          ENDIF
          ADD       ONE,COUNT
.
JHINV160  MOVE      ZERO,MISCTOT
          MOVE      ZERO,MISCGST
.
.         Process this miscellaneous item
.
JHINV180 MATCH     "7",PMMIRTYP
         IF        @EQUAL
            MOVE      OTPATAMT,LINETOT
            MOVE      OTDTGSTM,LINEGST
            MOVE      "5",OTRECTYP          * Discount
            GOTO      JHINV190
          ENDIF
.
         MOVE      OTPATAMT,LINETOT
         ADD       LINETOT,GROSSTOT
.
         MOVE      ZERO,LINEGST
         IF        IBCNUGST=2 & OTDTGSTA=1
           CALL      OGST0000               * Get Item GST rate
           MOVE      LINETOT,LINEGST
           MULT      IBGEAMNT,LINEGST     * GST amount
           DIV       "100.00",LINEGST     * Divide by 100 to get wanted fig.
         ENDIF
.
         ADD       LINETOT,MISCTOT
         ADD       LINEGST,MISCGST
         MOVE      PMMIMGRP,SAVGCOD
         MOVE      ONE,COMPFLG
.
.         Process this item
.
JHINV190 BRANCH    PROGFLAG OF JHINV100,JHINV200,JHINV240,JHINV400
.
.        Display miscellaneous item
.
JHINV200 MATCH    "7",PMMIRTYP
         GOTO     JHINV100 IF EQUAL       * display discount later
.
         BRANCH   INVTYPE,JHINV100            * Print item by group
.
         UNPACK   PMMITDAT,XCENT,XYEAR,XMON,XDAY
         REP      " 0",XDAY
         MOVE     XMON,F2
         LOAD     MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG,NSEP:
                             NOCT,NNOV,NDEC
.
         WRITE    HTMLFILE;"<tr>":
                  "<td>&nbsp",XDAY,SP1,MTHNAM3,SP1,XCENT,XYEAR,"</td>":
                  "<td>&nbsp",OTDDESC,"</td>";
         IF       INVTYPE=2
           WRITE     HTMLFILE;"<td>&nbsp;",OTRECEPT,"</td>";
         ENDIF
         WRITE    HTMLFILE;"<td><img src=../images/DeleteIcon.gif ":
                  " class=ListIcon onclick='DeleteItem(#"":
                  OBAOUTNO,"#",#"",PMMITRAN,"#")'>":
                  OTITEMNO,"</td>";
.
         IF        PMMISERV<>0
           MOVE      PMMIAMTP,FORM12D2
           ADD       PMMIRBAT,FORM12D2
           WRITE     HTMLFILE;"<td>",PMMISERV,"</td>":
                              "<td align=right>",FORM12D2,"</td>";
         ELSE
           WRITE     HTMLFILE;"<td>&nbsp;</td>":
                              "<td>&nbsp;</td>";
         ENDIF
.
         WRITE    HTMLFILE;"<td align=right>",LINETOT,"</td>":
                           "<td>&nbsp;</td>":
                           "<td align=right>",LINETOT,"</td>":
                           "<td>&nbsp;</td>":
                           "</tr>";
         GOTO      JHINV100
.
.        Print miscellaneous item
.
JHINV240 MATCH     "3",PMMIRTYP
         GOTO      JHINV250 IF NOT EQUAL
.
         BRANCH    INVTYPE,JHINV300      * Print items by group
.
JHINV250 MATCH     "7",PMMIRTYP
         GOTO      JHINV300 IF EQUAL     * Print discount later
.
         COMPARE   MAXLINE,COUNT
         CALL      TAIL IF NOT LESS
.
         UNPACK    OTDDESC,KEY25
         STRIP     OTDDESC
         MOVE      LINETOT,FORM7P2
         UNPACK    OTDDATE,CCENT,CYEAR,CMON,CDAY
         CALL      PACDATE
         PRINT     *3,OTITEMNO:
                   *13,CPCDATE:
                   *25,KEY25:
                   *52,OTSERVS:
                   *57,FORM7P2 
         ADD       ONE,COUNT
.
.        Add the invoice number to the DTRAN record
.
JHINV300 MOVE      CNEXTINV,OTINVNO
         MOVE      ONE,OTINVPRT
         MOVE      ONE,OTDTGSTA
         MOVE      PTCNAGST,OTDTGSTC
         MOVE      ZERO,OTDTGSTM
         CALL      OGST0000               * Get Item GST rate
         MOVE      OTPATAMT,OTDTGSTM
         MULT      IBGEAMNT,OTDTGSTM
         DIV       "100.00",OTDTGSTM
         PACK      OTDTEFFD,CCC,CYY,CMM,CDD
         REP       " 0",OTDTEFFD
         MOVE      SP70,OTDTPCOD
.
JHINV320 CALL      NXTTRAN1
         PACK      KEY22,OTNUMB,OTINVNO,OTTRANS
         CALL      RDADTRO1
         COMPARE   ZERO,OVRCD
         GOTO      JHINV320 IF EQUAL
         CALL      WRDTRO1
.
         PACK      KEY14,OTNUMB,SAVTRNNO
         CALL      DEPMMTI1
         CALL      RSPMMTI1
.
.        Update the Fees Invoiced Summary file
.
JHINV400 MOVE      OTPATAMT,FINAMT
         MOVE      "A",FINTYPE
         MOVE      "M",ANS
.
.        group by misc group code
.
          MOVE      SP3,OBACOMP
          PACK      KEY8,OTNUMB
          CALL      RDBOKB1
.
          MOVE      SP70,KEY3
          UNPACK    OTBATCHN,KEY3
.
          MATCH     "7",PMMIRTYP
          IF        @EQUAL
            MULT      "-1",FINAMT        * discount
            MOVE      OTMISGRP,KEY3
          ENDIF
          IF        PTCNMFEE=1
            MATCH     KEY3,SP3
            IF        @EQUAL
              PACK      FINCODE,ANSM,OBACOMP,OTITEMNO,SP20
.                               Misc. Charge, Claim Code, Item Code
            ELSE
              PACK      FINCODE,ANSM,OBACOMP,KEY3,SP20
.                               Misc. Charge, Claim Code, Group Code
            ENDIF
          ELSE
            PACK      FINCODE,ANSM,KEY3,OTITEMNO,SP20
.                             Misc. Charge, Group Code, Item Code
          ENDIF
.
.        Post to the fees invoiced file
.
         UNPACK    SP70,FININVN,FINADMN,FINURNO
         IF        PROGFLAG=3
           PACK      FININVN,CNEXTINV,SP70
           MOVE      OBAOUTNO,FINADMN
           MOVE      OBAURNO,FINURNO
         ENDIF
.
         MOVE      PINVDTE,FINDATE       * Invoice date
         REP       " 0",FINDATE
         MOVE      TWO,FINSYS
         MOVE      OBASITE,FINSITE
         MOVE      ZERO,FGSTFLAG
         MOVE      PMVXMHOS,FINHOSP
         CALL      PATCALF
.
         GOTO      JHINV100
.
.        End of this invoice's details
.
JHINV500 
         COMPARE   ZERO,COMPFLG
         GOTO      JHINV600 IF EQUAL
.
         MOVE      SP70,TDESC
         MATCH     SP70,SAVGCOD
         IF        @EQUAL
           MOVE      "No Group Code",TDESC
         ELSE
           IF        CFEETYP=9
             PACK      KEY5,ANSF,ANSN,SAVGCOD,SP70
           ELSE
             PACK      KEY5,ANSF,ANSI,SAVGCOD,SP70
           ENDIF
           CALL      RDCODE1
           IF        OVRCD=1
             CLEAR     TDESC
             APPEND    "Invalid Group ",TDESC
             APPEND    SAVGCOD,TDESC
             APPEND    SP70,TDESC
             RESET     TDESC
           ENDIF
         ENDIF
.
         ADD       MISCTOT,TOTAMNT
         ADD       MISCGST,GSTAMNT
.
         BRANCH    PROGFLAG,JHINV600,JHINV520,JHINV530,JHINV600
.
JHINV520 IF        INVTYPE=1
           WRITE     HTMLFILE;"<tr><td>&nbsp;</td>":
                              "<td>",TDESC,"</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td align=right>&nbsp;</td>":
                              "<td align=right>",MISCTOT,"</td>":
                              "<td>&nbsp;</td>":
                              "<td align=right>",MISCTOT,"</td>":
                              "</tr>"
           MOVE      ZERO,MISCTOT
           MOVE      ZERO,MISCGST
         ELSE
           WRITE     HTMLFILE;"<tr><td>&nbsp;</td>":
                              "<td><b>",TDESC,"</b></td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td align=right>&nbsp;</td>":
                              "<td align=right>&nbsp;</td>":
                              "<td align=right>&nbsp;</td>":
                              "<td align=right>&nbsp;</td>":
                              "<td align=right>",MISCTOT,"</td>":
                              "</tr>"
         ENDIF
         GOTO      JHINV600
.
JHINV530 COMPARE   MAXLINE,COUNT
         CALL      TAIL IF NOT LESS
.
         MOVE      MISCTOT,FORM7P2
         IF        INVTYPE=1
            PRINT     *3,ESCCHR,CHARG,TDESC,ESCCHR,CHARH:
                      *58,FORM7P2         * Summary
          ELSE
            PRINT     *3,ESCCHR,CHARG,TDESC,ESCCHR,CHARH:
                      *71,FORM7P2         * Details
          ENDIF
          ADD       ONE,COUNT
.
          MOVE      MAXLINE,FORM2
          SUB       ONE,FORM2
          COMPARE   FORM2,COUNT
          CALL      TAIL IF NOT LESS
.
          MOVE      TOTAMNT,FORM7P2
          PRINT     *N,*3,"Total Hospital Charges",*70,FORM7P2
          ADD       TWO,COUNT
.
          CALL      PRDIS000              * Print discount if any
.
          MOVE      MAXLINE,FORM2
          SUB       ONE,FORM2
          COMPARE   FORM2,COUNT
          CALL      TAIL IF NOT LESS
.
          CALL      CLGST0000             * Calculate GST amount
          MOVE      GSTAMNT,FORM7P2
          MOVE      GROSSTOT,NETTOT
          ADD       GSTAMNT,NETTOT
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,NINTY8;*130,PTCNAGST
          OPEN      IBAGSTA1,"ibagstaf"
          PACK      KEY6,PTCNAGST,SP70
          CALL      RDIBGS1
          IF        OVRCD=1
            MOVE      "Add GST                     ",IBGSDESC
          ENDIF
.
          PRINT     *3,"Add ",IBGSDESC,*70,FORM7P2:
                    *N,*70,"----------"
          ADD       TWO,COUNT
.
JHINV580  MOVE      MAXLINE,FORM2
          SUB       ONE,FORM2
          COMPARE   FORM2,COUNT
          CALL      TAIL IF NOT LESS
.
          MOVE      TOTAMNT,FORM12P2
          ADD       GSTAMNT,FORM12P2
          MOVE      FORM12P2,FORM7P2
          PRINT     *3,"TOTAL HOSPITAL BILL (INCLUSIVE OF GST)",*70,FORM7P2:
                    *N
          ADD       TWO,COUNT
.
          MOVE      TOTAMNT,FORM7P2
          ADD       GSTAMNT,FORM7P2
.
          MOVE      FORM7P2,FORM12P2
          CALL      GRND0000              * Work out the rounding adjustment
          COMPARE   FORM7P2,FORM12P2
          GOTO      JHINV600 IF EQUAL     * no rounding
.
          SUB       FORM7P2,FORM12P2
          MOVE      FORM12P2,FORM7P2
          IF        FORM7P2<>0
            COMPARE   MAXLINE,COUNT
            CALL      TAIL IF NOT LESS
            CALL      FRND0000                * Format rounding amount
.
            MOVE      FORM12P2,FORM7P2
            IF        FORM7P2<0
              PRINT     *3,"Rounding",*69,"(",KEY10,")"
            ELSE
              PRINT     *3,"Rounding",*70,KEY10
            ENDIF
            ADD       ONE,COUNT       
          ENDIF
.
JHINV600 MOVE      GROSSTOT,NETTOT
         BRANCH    PROGFLAG OF JHINV624,JHINV620,JHINV700
         GOTO      JHINV999
.
JHINV620 MOVE     GROSSTOT,TOTAMNT
         ADD      GSTAMNT,TOTAMNT
         WRITE    HTMLFILE;"<tr>":
                  "<td>&nbsp;</td>":
                  "<td><b>Total Charges</b></td>";
         IF        INVTYPE=2
           WRITE     HTMLFILE;"<td>&nbsp;</td>";
         ENDIF
.
         WRITE    HTMLFILE;"<td>&nbsp;</td>":
                  "<td>&nbsp;</td>":
                  "<td>&nbsp;</td>":
                  "<td align=right>",GROSSTOT,"</td>":
                  "<td>&nbsp;</td>";
         IF        INVTYPE=2
           WRITE     HTMLFILE;"<td>&nbsp;</td>";
         ENDIF
         WRITE    HTMLFILE;"<td align=right>",TOTAMNT,"</td>":
                  "</tr>";
.
JHINV624 CALL     DSDIS000              * Display discount if any
         COMPARE  TWO,PROGFLAG
         GOTO     JHINV625 IF NOT EQUAL    * not displaying invoice
.
         CALL     CLGST0000                * Calculate GST
         MOVE     GROSSTOT,TOTAMNT
         ADD      GSTAMNT,TOTAMNT
.
         MOVE     GROSSTOT,NETTOT
         ADD      GSTAMNT,NETTOT
         WRITE    HTMLFILE;"<tr bgcolor=#CCCCCC><td>&nbsp;</td>":
                  "<td><b>Invoice Total</b></td>";
.
         IF        INVTYPE=2
           WRITE     HTMLFILE;"<td>&nbsp;</td>";
         ENDIF
         WRITE    HTMLFILE;"<td>&nbsp;</td>":
                           "<td>&nbsp;</td>":
                           "<td>&nbsp;</td>":
                           "<td align=right>",GROSSTOT,"</td>":
                           "<td align=right>",GSTAMNT,"</td>";
         IF        INVTYPE=2
           WRITE     HTMLFILE;"<td>&nbsp;</td>";
         ENDIF
         WRITE    HTMLFILE;"<td align=right>",TOTAMNT,"</td>":
                  "</tr>";
.
JHINV625 BRANCH    PROGFLAG,JHINV999,JHINV626
         GOTO      JHINV999
.
JHINV626 COMPARE   ZERO,CASHFLAG
         GOTO      JHINV630 IF NOT EQUAL
         COMPARE   ZERO,NBNKAMNT
         GOTO      JHINV680 IF EQUAL
.
.        Print Cash on WEB page
.
JHINV630 ADD       NBNKAMNT,CSAMT
         MULT      "-1",CSAMT
         MOVE      "PAYMENT RECEIVED WITH THANKS",KEY30
.
         IF        CASHFLAG=1
           PACK      KEY8,CSCENT,CSYEAR,CSMON,CSDAY
           REP       " 0",KEY8
           UNPACK    KEY8,XCENT,XYEAR,XMON,XDAY
           MOVE      XMON,F2
           LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG,NSEP:
                              NOCT,NNOV,NDEC
           WRITE     HTMLFILE;"<tr>":
                     "<td>",XDAY,SP1,MTHNAM3,SP1,XCENT,XYEAR,"</td>";
         ELSE
           WRITE     HTMLFILE;"<tr><td>&nbsp;</td>";
         ENDIF
.
         WRITE     HTMLFILE;"<td>",KEY30,"</td>";
.
         IF        INVTYPE=2
           WRITE     HTMLFILE;"<td>&nbsp;</td>":
                              "<td>&nbsp;</td>";
         ENDIF
         WRITE     HTMLFILE;"<td>&nbsp;</td>":
                            "<td>&nbsp;</td>":
                            "<td>&nbsp;</td>":
                            "<td>&nbsp;</td>":
                            "<td>&nbsp;</td>";
.
         WRITE     HTMLFILE;"<td align=right>",CSAMT,"</td>":
                   "</tr>";
         MULT      "-1",CSAMT
         SUB       CSAMT FROM NETTOT
.
.        Display balance payable
.
JHINV680 MOVE      NETTOT,FORM12P2
         CALL      GRND0000                 * Work out the rounding adjustment
.
         WRITE     HTMLFILE;"<tr bgcolor=#CCCCCC><td>&nbsp;</td>":
                   "<td><b> Balance Payable </b></td>";
.
         IF        INVTYPE=2
           WRITE     HTMLFILE;"<td>&nbsp;</td>":
                              "<td>&nbsp;</td>";
         ENDIF
         WRITE     HTMLFILE;"<td>&nbsp;</td>":
                            "<td>&nbsp;</td>":
                            "<td>&nbsp;</td>":
                            "<td>&nbsp;</td>":
                            "<td>&nbsp;</td>";
.
         WRITE     HTMLFILE;"<td align=right>",FORM12P2,"</td>":
                   "</tr>";
.
         GOTO      JHINV999
.
JHINV700 MOVE      CSAMT,CASHTOT
         ADD       NBNKAMNT,CASHTOT
.
         COMPARE   ZERO,CASHTOT
         GOTO      JHINV800 IF EQUAL
.
         COMPARE   FORM2,COUNT
         CALL      TAIL IF NOT LESS
         MOVE      CASHTOT,FORM7P2
         MULT      "-1",FORM7P2
         PRINT     *3,"Total Payments",*70,FORM7P2
         ADD       ONE,COUNT
.
JHINV800 MOVE      TOTAMNT,NETTOT
         ADD       GSTAMNT,NETTOT
         SUB       CASHTOT,NETTOT
         MOVE      GSTAMNT,TOTGST
         MOVE      NETTOT,FORM7P2
.
.        Check if the invoice total need to be rounded
.
         MATCH     SP70,PTCNRDWN
         GOTO      JHINV900 IF EQUAL        * Not set
.
         MOVE      NETTOT,FORM12P2
         CALL      GRND0000                 * Work out the rounding adjustment
         COMPARE   FORM12P2,NETTOT
         GOTO      JHINV900 IF EQUAL
         MOVE      FORM12P2,FORM7P2
.
         SUB       NETTOT,FORM12P2
         CALL      WADJ0000                 * Write rounding adj.to dtr file
         MOVE      FORM12P2,ROUNDAMT
         ADD       FORM12P2,GROSSTOT
.
         MOVE      FORM12P2,FINAMT
         MULT      "-1",FINAMT              * Rounding adjustment
.
         MOVE      "A",FINTYPE
         MOVE      "M",ANS
         IF        PTCNMFEE = 1
           PACK      FINCODE,ANSM,OBACOMP,PTCNROUN,SP70
         ELSE
           PACK      FINCODE WITH ANS,PTCNROUN,SP70
         ENDIF
.
         UNPACK    SP70,FININVN,FINADMN,FINURNO
         IF        PROGFLAG=3
           PACK      FININVN,CNEXTINV,SP70
           MOVE      OBAOUTNO,FINADMN
           MOVE      OBAURNO,FINURNO
         ENDIF
.
         MOVE      PINVDTE,FINDATE       * Invoice date
         REP       " 0",FINDATE
         MOVE      TWO,FINSYS
         MOVE      OBASITE,FINSITE
         MOVE      ZERO,FGSTFLAG
         MOVE      PMVXMHOS,FINHOSP
         CALL      PATCALF
.
JHINV900 MOVE      MAXLINE,FORM2
         SUB       "4",FORM2
         COMPARE   FORM2,COUNT               * Check For A Full Page
         CALL      TAIL IF NOT LESS
         PRINT     *70,"----------":
                   *N,*3,"BALANCE PAYABLE",*70,FORM7P2:
                   *N,*70,"==========":
                 *N,*N,*3,"P L E A S E  P A Y  T H I S  A M O U N T",*70,FORM7P2
         ADD       FIVE,COUNT
.
         PACK      KEY19,OBAOUTNO,SP70
         CALL      PRTIPROV                 * Print insurance provider
.
         WHILE     COUNT<MAXLINE
           PRINT     *N;
           ADD       ONE,COUNT
         DO
.
         CALL      SINVT000                 * set up tail stationery code
         MOVE      ZERO,MPGEFLAG            * indicate single/last page
         CALL      PRTINVTL                 * print templated invoice tail
.
JHINV999 RETURN
+
.**********************************************************************
.         format rounding amount
.**********************************************************************
FRND0000  UNPACK    SP70,KEY10,KEY7,KEY1,KEY2
.
          IF        FORM7P2<0
            MULT      "-1",FORM7P2
          ENDIF
.
          MOVE      FORM7P2,KEY10
          UNPACK    KEY10,KEY7,KEY1,KEY2
          MATCH     SP70,KEY7
          IF        @EQUAL
            MOVE      "      0",KEY7
          ENDIF
          PACK      KEY10,KEY7,KEY1,KEY2,SP70
.
FRND9999  RETURN
+
.*******************************************************************************
.         Calculate GST Amount
.*******************************************************************************
CLGST000  MOVE      ZERO,TOTGST
          MOVE      ZERO,GSTAMNT
.
          OPEN     CONTROLF,"controlf"
          READ     CONTROLF,NINTY8;*130,PTCNAGST
          MOVE     ZERO,IBGEAMNT            * Initialise GST percentage amount
          OPEN     IBAGEDA1,"ibagedaf"
          PACK     KEY8,CCC,CYY,CMM,CDD
          REP      " 0",KEY8
          PACK     KEY14,PTCNAGST,KEY8
          CALL     RDIBGE1
          COMPARE  ZERO,OVRCD
          GOTO     CLGST100 IF EQUAL         * found GST amount for current date
.
          CALL     RPIBGE1                   * read previous record
          BRANCH   OVRCD,CLGST999
.
          MATCH    PTCNAGST,IBGECODE         * Same code ?
          GOTO     CLGST999 IF NOT EQUAL
.
.         Calculate total GST 
.
CLGST100  MOVE      GROSSTOT,FORM12P2
          MULT      IBGEAMNT,FORM12P2    * GST amount
          DIV       "100.00",FORM12P2   * Divide by 100 to get wanted fig.
          MOVE      FORM12P2,TOTGST
          COMPARE   ZERO,TOTGST
          GOTO      CLGST999 IF EQUAL   * No GST
.
          MOVE      TOTGST,GSTAMNT
          COMPARE   THREE,PROGFLAG
          GOTO      CLGST9999 IF NOT EQUAL    * Not printing invoice
.
          PACK      FININVN,CNEXTINV,SP70
          MOVE      OBAOUTNO,FINADMN
          MOVE      OBAURNO,FINURNO

          MOVE      ONE,FGSTFLAG
          MOVE      "A",FINTYPE
          PACK      FINCODE WITH ANSA,SP70
          MOVE      PINVDTE,FINDATE
          REP       " 0",FINDATE
          MOVE      TWO,FINSYS
          MOVE      OBASITE,FINSITE
          MOVE      PMVXMHOS,FINHOSP
          MOVE      TOTGST,FINAMT
          CALL      PATCALF
.
CLGST999  RETURN
+
.******************************************************************************
.         Print Discount for Johns Hopkins only
.******************************************************************************
PRDIS000  MOVE      OBAOUTNO,OTNUMB
          MOVE      CNEXTINV,KEY8
.
          PACK      CFNAME,OSTFILE,FILDTRO5
          CLOSE     OUTDTRO5
          OPEN      OUTDTRO5,CFNAME
.
          PACK      KEY26,OBAOUTNO,KEY8,FIVE,SP20
          CALL      RDSDTRO5
PRDIS100  CALL      RDKDTRO5
          BRANCH    OVRCD,PRDIS999
.
          MATCH     OTINVNO,KEY8
          GOTO      PRDIS999 IF NOT EQUAL   * different invoice
.
          MATCH     OTNUMB,OBAOUTNO
          GOTO      PRDIS999 IF NOT EQUAL   * different admission
.
          COMPARE   "5",OTRECTYP
          GOTO      PRDIS999 IF NOT EQUAL
.
          ADD       OTPATAMT,GROSSTOT
          ADD       OTPATAMT,TOTDISC
          ADD       OTDTGSTM,TOTGST
          ADD       OTDTGSTM,GSTAMNT
          MOVE      OTPATAMT,LINETOT     * Discount
          ADD       LINETOT,TOTAMNT
          MOVE      ZERO,LINEGST
.
          COMPARE   MAXLINE,COUNT
          CALL      TAIL IF NOT LESS
.
          MOVE      "Discount",TDESC
          PACK      KEY5,ANSF,ANSI,OTMISGRP,SP70
          CALL      RDCODE1
.
          MOVE      LINETOT,FORM7P2
          PRINT     *3,TDESC,*70,FORM7P2    * Details
          ADD       ONE,COUNT
          GOTO      PRDIS100
.
PRDIS999  RETURN
+
.******************************************************************************
.         Display Discount
.******************************************************************************
DSDIS000  OPEN      PMSMTIA4,"pmsmtiaf"
          MOVE      OBAOUTNO,OTNUMB
          PACK      KEY18,OBAOUTNO,SEVEN,SP20
          CALL      RSPMMTI4
DSDIS100  CALL      RKPMMTI4
          BRANCH    OVRCD,DSDIS999
.
          MATCH     OTNUMB,OBAOUTNO 
          GOTO      DSDIS999 IF NOT EQUAL
.
          MATCH     "7",PMMIRTYP
          GOTO      DSDIS999 IF NOT EQUAL
.
          ADD       PMMIAMTT,GROSSTOT
          ADD       PMMIAMTT,TOTDISC
          ADD       PMMIGSTM,TOTGST
          ADD       PMMIGSTM,GSTAMNT
          MOVE      PMMIAMTT,LINETOT     * Discount
          MOVE      LINETOT,FORM12P2
.
          ADD       PMMIGSTM,LINETOT
          ADD       LINETOT,NETTOT
          ADD       PMMIAMTT,TOTAMNT
.
          COMPARE   TWO,PROGFLAG
          GOTO      DSDIS100 IF NOT EQUAL    * not displaying invoice
.
          UNPACK    PMMITDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,FORM2
          LOAD      MTHNAM3,FORM2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG:
                                       NSEP,NOCT,NNOV,NDEC
          PACK      KEY4,CCENT,CYEAR
          REP       " 0",KEY4
.
          WRITE     HTMLFILE;"<tr><td>",CDAY,SP1,MTHNAM3,SP1,KEY4,"</td>":
                            "<td>",PMMIDESC,"</td>";
          IF        INVTYPE=2
            WRITE     HTMLFILE;"<td>&nbsp;</td>";
          ENDIF
.
          WRITE     HTMLFILE;"<td><img src=../images/DeleteIcon.gif ":
                           " class=ListIcon onclick='DeleteItem(#"":
                           OBAOUTNO,"#",#"",PMMITRAN,"#")'>":
                           OTITEMNO,"</td>";
.         IF        INVTYPE=2
            WRITE     HTMLFILE;"<td>&nbsp;</td>";
.         ENDIF
.
          WRITE     HTMLFILE;"<td>&nbsp;</td>":
                             "<td align=right>",FORM12P2,"</td>":
                             "<td align=right>&nbsp;",PMMIGSTM,"</td>";
.
          ADD       PMMIGSTM,FORM12P2
          BRANCH    INVTYPE,DSDIS300           * Summary
          WRITE     HTMLFILE;"<td>&nbsp;</td>":
                             "<td align=right>",FORM12P2,"</td></tr>"
          GOTO      DSDIS100
.
DSDIS300  WRITE     HTMLFILE;"<td align=right>",FORM12P2,"</td></tr>"
          GOTO      DSDIS100
.
DSDIS999  RETURN
+
.******************************************************************************
.        Workout the rounding amount
.******************************************************************************
GRND0000 MOVE      FORM12P2,KEY15
         UNPACK    KEY15,KEY14,KEY1
         MOVE      ZERO,FORM1
         MOVE      ZERO,FORM12D2
         MOVE      KEY1,FORM1
         COMPARE   ZERO,FORM1
         GOTO      GRND9999 IF EQUAL
.
         MATCH     "05",PTCNRDWN
         GOTO      GRND5000 IF NOT EQUAL
.
.        5 cent rounding
.
         BRANCH    FORM1,GRND8000,GRND8000:
                         GRND8000,GRND8000,GRND1000,GRND1000,GRND1000:
                         GRND1000,GRND1000
.
GRND1000 MOVE      FIVE,FORM12D2
         GOTO      GRND8000
.
GRND2000 ADD       TEN,FORM12D2
         GOTO      GRND8000
.
.        10 cent rounding
.
GRND5000 MATCH     "10",PTCNRDWN
         GOTO      GRND9999 IF NOT EQUAL
.
         BRANCH    FORM1,GRND8000,GRND8000,GRND8000,GRND8000,GRND8000:
                         GRND8000,GRND8000,GRND8000,GRND8000,GRND7000
.
GRND7000 MOVE      TEN,FORM12D2
.
GRND8000 SUB       FORM1,FORM12D2
         DIV       "100",FORM12D2
         ADD       FORM12D2,FORM12P2
         GOTO      GRND9999
.
GRND9999 RETURN
+
.******************************************************************************
.* WADJ0000 : Write to DTR for Rounding Adjustment - Johns Hopkins
.******************************************************************************
WADJ0000 CALL      NXTTRAN1
         MOVE      CNEXTINV,TREF
         MOVE      OTTRANS,PMMITRAN
         PACK      KEY22 WITH OBAOUTNO,TREF,OTTRANS 
         CALL      RDADTRO1
         COMPARE   ZERO,OVRCD
         GOTO      WADJ0000 IF EQUAL     * Try the next transaction number
.
         MOVE      OBAOUTNO,OTNUMB
         PACK      OTDDATE,CCC,CYY,CMM,CDD
         REP       " 0",OTDDATE
         MOVE      FORM12P2,OTPATAMT
         MOVE      FORM12P2,OTPATPOR
         MOVE      "5",OTRECTYP
         MOVE      PTCNROUN,OTMISGRP     * Rounding category
         MOVE      CNEXTINV,OTINVNO
         MOVE      ONE,OTINVPRT
         MOVE      "ROUNDING           ",OTDDESC
         UNPACK    SP70,OTITEMNO,OTTYPE,OTRECEPT
         UNPACK    SP70,OTDEPTYP,OTBATCHN,OTDTGSTC,OTDTCRST
         MOVE      ZERO,OTPAYTYP
         MOVE      ZERO,OTREBPOR
         MOVE      ZERO,OTNINVS
         MOVE      ZERO,OTSERVS 
         MOVE      ZERO,OTDTGSTA
         MOVE      ZERO,OTDTGSTM
         MOVE      OTDDATE,OTDTEFFD
         MOVE      ONE,OTINVPRT
         MOVE      SP70,OTDTPCOD
.
         PACK      KEY22,OTNUMB,OTINVNO,OTTRANS
         CALL      WRDTRO1
.
WADJ9999 RETURN
+
.******************************************************************************
.* GTGSO000 : Calculate the GST amount for the current invoice                *
.*                                                                            *
.* CGSTPERC is a percentage figure, such as 12.5 %. To use this figure, we    *
.* will need to divide by 100 at some point. The formula we are using is      *
.*           GROSSTOT * CGSTPERC / 100.00                                     *
.******************************************************************************
GTGSO000 MOVE      GROSSTOT,GSTWORK          * Move gross total to work var
         MULT      CGSTPERC,GSTWORK          * Multiply work var by percentage
         DIV       "100.00",GSTWORK          * Divide by 100 to get wanted fig.
         MOVE      GSTWORK,GSTAMNT           * Move work var to proper variable
GTGSO999 RETURN
.******************************************************************************
.* DSGSO000 : Display the GST amount on the screen, after a subtotal          *
.******************************************************************************
DSGSO000 COMPARE   ZERO,GSTAMNT              * Is there any GST to display ?
         GOTO      DSGSO999 IF EQUAL         * No. Don't do anything
.
.        Get the description of the GST item, and display on the screen
.
         MOVE      "GOODS & SERVICES TAX          ",MDESC
         CALL      OCLM0000        * Check Hospital claim code charging
         PACK      KEY29,PTCNDCLM,SP6,SP3,CGSTITEM,CURRDATE,SP10
         CALL      PATMCHRD
.
         MOVE      GSTAMNT,LINETOT           * Set up for display
.
.        Increment the invoice amount
.
         ADD       LINETOT,GROSSTOT
         ADD       LINETOT,NETTOT
.
DSGSO999 RETURN
.*******************************************************************************
.         Get the default charging claim code for multi hospital 
.*******************************************************************************
OCLM0000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          IF        IBCNMHOS=1
            OPEN      PATHSPA1,"pathspaf"
            MOVE      PMVXMHOS,KEY3
            CALL      RDPTHSP1
            IF        OVRCD=0
              MOVE      PTHSDCLM,PTCNDCLM     * default claim code charging
            ENDIF
          ENDIF
OCLM9999  RETURN
.******************************************************************************
.* ADGSO000 : Print the GST amount on the invoice, and post to dtran file     *
.* ADGSO800 : Just post amount to the Fee's invoiced file                     *
.******************************************************************************
ADGSO000 MOVE      GROSSTOT,XGROSTOT
         MOVE      LINETOT,XLINETOT
         COMPARE   ZERO,GSTAMNT              * Is there any GST to print ?
         GOTO      ADGSO999 IF EQUAL         * No. Don't do anything
.
         IF        IBCNUGST=2 & PTCNINVB=1
           PRINT     *54,"SUB-TOTAL",*119,XGROSTOT;
         ELSE
           PRINT     *54,"SUB-TOTAL",*71,XGROSTOT;
         ENDIF
         CALL      PEOL0000
.
.        Get the description of the GST item, and print on the invoice
.
         MOVE      "GOODS & SERVICES TAX          ",MDESC
         CALL      OCLM0000        * Check Hospital claim code charging
         PACK      KEY29,PTCNDCLM,SP6,SP3,CGSTITEM,CURRDATE,SP10
         CALL      PATMCHRD
.
         MOVE      GSTAMNT,LINETOT           * Set up for print
         MOVE      LINETOT,XLINETOT
.
         IF        IBCNUGST=2 & PTCNINVB=1
           PRINT     *1,CDATE,*18,MDESC,*119,XLINETOT;
         ELSE
           PRINT     *1,CDATE,*18,MDESC,*71,XLINETOT;
         ENDIF
         CALL      PEOL0000      * Print the right hand side of the invoice
.
.        Post this GST item to the dtran file
.
ADGSO300 MOVE      OBAOUTNO,OTNUMB
         MOVE      CNEXTINV,OTINVNO
         MOVE      OBAOUTNO,KEY8
         CALL      TRVISA1
         MOVE      PVITRAN,OTTRANS
         MOVE      MDESC,OTDDESC
         MOVE      GSTAMNT,OTPATAMT
         PACK      OTDDATE,CCC,CYY,CMM,CDD
         REP       " 0",OTDDATE
         PACK      OTITEMNO,CGSTITEM,SP20
         MOVE      "DB",OTTYPE
         MOVE      ZERO,OTPAYTYP
         PACK      OTRECEPT,SP20
         MOVE      ONE,OTINVPRT
         MOVE      ONE,OTRECTYP
         MOVE      GSTAMNT,OTPATPOR
         MOVE      ZERO,OTREBPOR
         MOVE      SP3,OTMISGRP
         MOVE      SP3,OTDEPTYP
         MOVE      SP8,OTBATCHN
         MOVE      ZERO,OTDTGSTA
         MOVE      SP10,OTDTGSTC
         MOVE      ZERO,OTDTGSTM
         PACK      OTDTEFFD,CCC,CYY,CMM,CDD
         REP       " 0",OTDTEFFD
         MOVE      SP70,OTDTPCOD
.
         PACK      KEY22,OTNUMB,OTINVNO,OTTRANS
         CALL      WRDTRO1
.
.        Add GST to the invoice total
.
         ADD       GSTAMNT,GROSSTOT
         ADD       GSTAMNT,NETTOT
         MOVE      GROSSTOT,XGROSTOT
.
.        Update the Fee's invoiced file with GST
.
ADGSO800 MOVE      GSTAMNT,FINAMT
         MOVE      ANSA,FINTYPE
.
         MOVE      SP3,OBACOMP
         PACK      KEY8,OTNUMB
         CALL      RDBOKB1
.
         IF        PTCNMFEE=1
           PACK      FINCODE,ANSM,OBACOMP,CGSTITEM,SP20
.                            Misc. Charge, Claim Code, Item Code
         ELSE
           PACK      FINCODE,ANSM,SP3,CGSTITEM,SP20
.                            Misc. Charge, Group Code, Item Code
         ENDIF
.
         UNPACK    SP70,FININVN,FINADMN,FINURNO
         IF        PROGFLAG=3
           PACK      FININVN,CNEXTINV,SP70
           MOVE      OBAOUTNO,FINADMN
           MOVE      OBAURNO,FINURNO
         ENDIF
.
         MOVE      PINVDTE,FINDATE   * Invoice date
         REP       " 0",FINDATE
         MOVE      TWO,FINSYS
         MOVE      OBASITE,FINSITE
         MOVE      ZERO,FGSTFLAG
         MOVE      PMVXMHOS,FINHOSP
         CALL      PATCALF
.
ADGSO999 RETURN
+
.******************************************************************************
.* OGST0000 : Get percentage of Item GST                                      *
.******************************************************************************
OGST0000  MOVE     ZERO,IBGEAMNT            * Initialise GST percentage amount
          COMPARE  THREE,IBCNUGST
          GOTO     OGST6000 IF EQUAL
          COMPARE  TWO,IBCNUGST
          GOTO     OGST9999 IF NOT EQUAL    * not using Aust.GST
.
          BRANCH   OTDTGSTA,OGST6000        * GST payable
          GOTO     OGST9000                 * GST free
.
OGST6000  OPEN     IBAGEDA1,"ibagedaf"
          MOVE     ZERO,IBGEAMNT             * zero GST percentage
          PACK     KEY8,CCC,CYY,CMM,CDD
          REP      " 0",KEY8
          PACK     KEY14,OTDTGSTC,KEY8
          CALL     RDIBGE1
          COMPARE  ZERO,OVRCD
          GOTO     OGST9000 IF EQUAL         * found GST amount for current date
.
          CALL     RPIBGE1                   * read previous record
          BRANCH   OVRCD,OGST8000
.
          MATCH    OTDTGSTC,IBGECODE         * Same code ?
          GOTO     OGST9000 IF EQUAL
.
OGST8000  MOVE     ZERO,IBGEAMNT
.
OGST9000  CLOSE    IBAGEDA1
OGST9999  RETURN
+
. *****************************************************************************
. * MVDO0000 : move fields from pmsmtiaf to outdtrof file                     *
. *****************************************************************************
MVDO0000  MOVE      PMMIVISN,OTNUMB
          MOVE      PMMIVISN,DOTNUMB
          MOVE      "00000000",OTINVNO
          MOVE      PMMITRAN,OTTRANS
          MOVE      PMMIDESC,OTDDESC
          MOVE      PMMIAMTT,OTPATAMT
          MOVE      PMMITDAT,OTDDATE
          MOVE      PMMIITEM,OTITEMNO
          MOVE      "DB",OTTYPE
          MOVE      ZERO,OTPAYTYP
          MOVE      PMMIREFN,OTRECEPT
          MOVE      ZERO,OTINVPRT
          MOVE      ONE,OTRECTYP
.
          MATCH     "2",PMMIRTYP
          IF        @EQUAL
            MOVE      SIX,OTRECTYP
          ENDIF
.
          MOVE      PMMIMGRP,OTMISGRP
.
          IF        OTRECTYP=SIX
            MOVE      PMMISUBN,OTDEPTYP
          ELSE
            MOVE      SP10,OTDEPTYP
          ENDIF

          MOVE      PMMIBTCH,OTBATCHN
          MOVE      PMMIAMTP,OTPATPOR
          MOVE      PMMIRBAT,OTREBPOR
          MOVE      "1",OTNINVS
          IF        IPATFLAG=2
              MOVE      PMMIINVN,OTNINVS
          ENDIF
          MOVE      ONE,OTSERVS
          IF        PMMISERV<>0
            MOVE      PMMISERV,OTSERVS
          ENDIF
          MOVE      PMMIGSTA,OTDTGSTA
          MOVE      PMMIGSTC,OTDTGSTC
          MOVE      PMMIGSTM,OTDTGSTM
          MOVE      PMMIROVR,OTDTNCAT
          MOVE      PMMISTXT,OTDTSRTX
          MOVE      PMMIMPOV,OTDTMPRO
          MOVE      PMMIDSOV,OTDTDSRO
          MOVE      PMMINMPT,OTDTNPAT
          MOVE      PMMISDCD,OTDTSFDM
          MOVE      PMMILSPN,OTDTLSPN
          MOVE      PMMIACOI,OTDTACAO
          MOVE      PMMITDUR,OTDTTDUR
          UNPACK    SP70,OTDTEFFD,OTDTCRST
.
          MOVE      PMMIDKSM,OTDTDSKM
          MOVE      SP70,OTSPARE 
.
MVDO9999  RETURN
+
. *****************************************************************************
. * MRHT0000 : Move DTR fields to HF debtor transaction (outhtraf) file       *
. *****************************************************************************
MOHT0000  MOVE      DOTNUMB,OTHTNUMB       * Admission Number
          MOVE      DOTTRANS,OTHTTRAN      * Transaction Number
          MOVE      OTDDATE,OTHTDATE       * Date
          MOVE      OTITEMNO,OTHTITEM      * Item Number
          MOVE      OTDDESC,OTHTDESC       * Description
          MOVE      "1",OTHTIPRT           * Patient Invoice printed(0=No,1=Yes)
          MOVE      DOTRECTY,OTHTRTYP      * Record Type
          MOVE      OTSERVS,OTHTSERV       * Number of Services
          MOVE      OTMISGRP,OTHTMGRP      * Misc.Group (Cat FI)
          MOVE      OTDTGSTA,OTHTGSTA      * Is GST applicable to this time ?
          MOVE      OTDTGSTC,OTHTGSTC      * GST Payable code
          MOVE      OTNINVS,OTHTNINV       * No of invoice to be printed

MOHT9999  RETURN
+
. *****************************************************************************
. * MODT0000 : Move field from outhtraf file to DTR for printing              *
. *****************************************************************************
MODT0000  MOVE      OTHTNUMB,OTNUMB   *  Outpatient Number
          MOVE      OTHTINVN,OTINVNO  *  Rebate Invoice Number
          MOVE      OTHTTRAN,OTTRANS  *  Health Fund Transaction Number
          MOVE      OTHTDESC,OTDDESC  *  Description
          MOVE      OTHTAMTT,OTPATAMT *  Total Amount
          MOVE      OTHTDATE,OTDDATE  *  Date (ccmmyydd)
          MOVE      OTHTITEM,OTITEMNO *  Item Number
          MOVE      OTHTTTYP,OTTYPE   *  Transaction Type
          MOVE      OTHTPTYP,OTPAYTYP *  Payment Method
          MOVE      OTHTRECP,OTRECEPT *  Receipt Number
          MOVE      OTHTIPRT,OTINVPRT *  Invoice Printed (0=No, 1=Yes)
          MOVE      OTHTRTYP,OTRECTYP *  Record Type
          MOVE      OTHTMGRP,OTMISGRP *  Miscellaneous Group (Cat FI)
          MOVE      OTHTDTYP,OTDEPTYP *  Deposit Type
          MOVE      OTHTBATC,OTBATCHN *  Batch Number for Misc.Items
          MOVE      OTHTPPOR,OTPATPOR *  Patient Portion
          MOVE      OTHTRPOR,OTREBPOR *  Rebate Portion
          MOVE      OTHTNINV,OTNINVS  *  No of Invoices to be printed
          MOVE      OTHTSERV,OTSERVS  *  Number of services
          MOVE      OTHTGSTA,OTDTGSTA *  Is GST Applicable to this Item ?
          MOVE      OTHTGSTC,OTDTGSTC *  GST Payable Code
          MOVE      OTHTGSTM,OTDTGSTM *  GST Amount
          MOVE      OTHTEFFD,OTDTEFFD *  Date Item assigned to Fees Inv.
          MOVE      OTHTCRST,OTDTCRST *  Credit Note Status
          MOVE      OTHTBTCH,OTDTBTCH *  Batch details (NZ Private Extract)
          MOVE      OTHTPCOD,OTDTPCOD *  Payment Code (Cat Py)
          MOVE      OTHTACAO,OTDTACAO *  After Care Override
          MOVE      OTHTDSRO,OTDTDSRO *  Duplicate Service Override
          MOVE      OTHTSRTX,OTDTSRTX *  Service Text
          MOVE      OTHTLSPN,OTDTLSPN *  LSPN Value
          MOVE      OTHTMPRO,OTDTMPRO *  Multiple Procedure Override
          MOVE      OTHTNPAT,OTDTNPAT *  Number of Patients
          MOVE      OTHTSFDM,OTDTSFDM *  Self Deem Code (cat Sd)
          MOVE      OTHTTDUR,OTDTTDUR *  Time Duration
          MOVE      OTHTNCAT,OTDTNCAT *  Restricted Override Code
.
MODT9999  RETURN
+
. *****************************************************************************
. Check if Patient invoice has been raised (ie record exist in HF Debtors)
. *****************************************************************************
CPTO0000  MOVE      ZERO,PREBFLAG
          COMPARE   TWO,IPATFLAG
          GOTO      CPTO9999 IF EQUAL          * printing Patient invoice
.
.         Check if there is a rebate invoice to be printed
.
          PACK      KEY22 WITH DOBAOUTN,SP70
          CALL      RSOTHTR1
CPTO1000  CALL      RKOTHTR1
          BRANCH    OVRCD,CPTO9999
.
          MATCH     DOBAOUTN,OTHTNUMB
          GOTO      CPTO9999 IF NOT EQUAL
.
          MATCH     SP70,OTHTINVN              * Blank Rebate Invoice no?
          GOTO      CPTO9999 IF NOT EQUAL
.
.         Rebate invoice has not been raised, check if the patient invoice
.         has been credited
.
          OPEN      PATINVR1,"patinvrf"
          MOVE      OTHTPINV,KEY8
          CALL      RDINV1
          BRANCH    OVRCD,CPTO1000
.
          MATCH     "1",PTINCRST
          GOTO      CPTO1000 IF EQUAL     * fully credited
.
.         found a Patient invoice with Rebate invoice NOT raised yet
.
          MOVE      ONE,PREBFLAG          * flag for printing Rebate patient
.
CPTO9999  RETURN
+
. *****************************************************************************
.         Check if this item has the Patient invoice raised
. *****************************************************************************
CPAO0000  PACK      KEY22 WITH DOTNUMB,SP70
          CALL      RSOTHTR1
CPAO1000  CALL      RKOTHTR1
          BRANCH    OVRCD,CPAO9000
.
          MATCH     DOTNUMB,OTHTNUMB
          GOTO      CPAO9000 IF NOT EQUAL
.
          MATCH     SP70,OTHTINVN           * Blank HF invoice ?
          GOTO      CPAO9000 IF NOT EQUAL
.
          MATCH     OTHTITEM,PMMIITEM
          GOTO      CPAO1000 IF NOT EQUAL
.
          MATCH     OTHTTRAN,PMMITRAN
          GOTO      CPAO1000 IF NOT EQUAL
.
.         We have found the item that has blank health fund invoice
.
          MOVE      ONE,EXIT
          GOTO      CPAO9999
.
CPAO9000  MOVE      ZERO,EXIT
CPAO9999  RETURN
+

. *****************************************************************************
.         Printing rebate invoice
. *****************************************************************************
ORINV000  PACK      KEY22 WITH DOTNUMB,SP70
ORINV100  CALL      RSOTHTR1
ORINV200  CALL      RKOTHTR1
          BRANCH    OVRCD,ORINV999
.
          MATCH     DOTNUMB,OTHTNUMB
          GOTO      ORINV999 IF NOT EQUAL
.
          MATCH     SP70,OTHTINVN              * Blank Rebate Invoice no?
          GOTO      ORINV999 IF NOT EQUAL
.
.         Check if the Patient invoice has been credited
.
          MOVE      OTHTPINV,KEY8
          CALL      RDINV1
          BRANCH    OVRCD,ORINV200
.
          MATCH     "1",PTINCRST
          GOTO      ORINV200 IF EQUAL          * fully credited
.
          CALL      MODT0000                * move outhtraf to outdtraf file
.
          MOVE      OTHTAMTT,LINETOT
          ADD       LINETOT,GROSSTOT
          ADD       OTHTAMTT,HFMISC
.
          MOVE      OTHTGSTM,LINEGST
          IF        IBCNUGST=2
            ADD       LINEGST,TOTGST
          ENDIF
.
.         Process this item
.
          IF        INVDFLAG=1
            MOVE      SP70,KEY2
            MOVE      OTHTITEM,KEY9
            MOVE      LINETOT,FORM12D2
            CALL      WOUT0000              * Write to invoice details file
          ENDIF
.
          PACK      SKEY22,OTHTNUMB,OTHTINVN,OTHTTRAN,SP70
.
.         process Rebate invoice

          BRANCH    PROGFLAG OF ORINV200,ORINV300,ORINV500,ORINV620
.
.        Display miscellaneous item
.
ORINV300  UNPACK    OTDDATE WITH XCENT,XYEAR,XMON,XDAY
          MOVE      XDAY,TFDAY
          MOVE      XMON,TFMONTH
          MOVE      XYEAR,TFYEAR
          MOVE      XCENT,TFCENT
.
          PACK      KEY10,TFDAY,SLASH,TFMONTH,SLASH,TFCENT,TFYEAR
          REP       " 0",KEY10
.
          MOVE     XMON,F2
          LOAD     MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG,NSEP:
                              NOCT,NNOV,NDEC
.
          MOVE     LINETOT,TOTAMNT
          ADD      LINEGST,TOTAMNT
.
          IF       PMMISERV=0
            MOVE     ONE,PMMISERV
          ENDIF
.
. check if it is bulk billing
.
          MATCH    "1",BULKBILL
          IF       !@EQUAL
            MATCH    "1",DVAWBILL
            GOTO     ORINV320 IF NOT EQUAL
          ENDIF
.
          WRITE    HTMLFILE;"<tr>":
                   "<td>":
                   "<dbsdate value='",OTDDATE,"'></dbsdate>":
                   "<dbsitem value='",OTITEMNO,"'></dbsitem>":
                   XDAY,SP1,MTHNAM3,SP1,XCENT,XYEAR:
                   "</td>":
                   "<td><img src=../images/EditIcon.gif":
                   " class=ListIcon onclick='UpdateItem(#"":
                   OBAOUTNO,"#",#"",PMMITRAN,"#")'>":
                   OTDDESC,"</td>":
                   "<td>",OTITEMNO,"</td>":
                   "<td>&nbsp",OTRECEPT,"</td>":
                   "<td>",PMMISERV,"</td>":
                   "<td align=right>",LINETOT,"</td>":
                   "<td align=right>",LINEGST,"</td>":
                   "<td align=right>",TOTAMNT,"</td>":
                   "</tr>";
          GOTO     ORINV200
.
ORINV320  WRITE    HTMLFILE;"<tr>":
                   "<td>&nbsp",XDAY,SP1,MTHNAM3,SP1,XCENT,XYEAR,"</td>":
                   "<td>&nbsp",OTDDESC,"</td>":
                   "<td>",OTITEMNO,"</td>":
                   "<td>&nbsp",OTRECEPT,"</td>":
                   "<td>",PMMISERV,"</td>":
                   "<td align=right>",LINETOT,"</td>":
                   "<td align=right>",LINEGST,"</td>":
                   "<td align=right>",TOTAMNT,"</td>":
                   "</tr>";
          GOTO      ORINV200
.
.        Print miscellaneous item
.
ORINV500  MOVE      LINETOT,XLINETOT
          MOVE      LINEGST,XLINEGST
.
.        Do any miscellaneous processing
.
          MOVE      OTSERVS,NODAYS
          MOVE      OTPATPOR,LSTRATE
          ADD       OTREBPOR,LSTRATE
          MOVE      OTDDATE,DFDATE
          MOVE      OTITEMNO,KEY9
          MOVE      OTDDESC,DIM30
          MOVE      TWO,RECFLAG
.
.         the following fields are used for printing (PRLN000)
.
          MOVE      OTITEMNO,PMMIITEM
          PACK      PMMIDESC,OTDDESC,SP70
          PACK      PMMIDES2,SP70,SP70
          MOVE      OTSERVS,PMMISERV
          MOVE      SP70,PMMIINCT
          MOVE      "3",PMMIRTYP
          LOAD      PMMIRTYP,OTRECTYP,THREE,FIVE,SIX,FOUR,SEVEN,TWO,NINE
          MOVE      SP70,PMMIMVBR
.
          CALL      PRLN0000               * Print item
          ADD       LINEGST,LINETOT        * add GST back
.
.        write record to DTR
.
          MOVE      CNEXTINV,OTINVNO
          MOVE      ONE,OTINVPRT
          MOVE      ZERO,OTDTGSTM
          IF        IBCNUGST=2
            MOVE      LINEGST,OTDTGSTM        * GST amount
          ENDIF
          PACK      OTDTEFFD,CCC,CYY,CMM,CDD
          REP       " 0",OTDTEFFD
          MOVE      SP70,OTDTPCOD
.
ORINV582  CALL      NXTTRAN1
          PACK      KEY22,OTNUMB,OTINVNO,OTTRANS
          CALL      RDADTRO1
          COMPARE   ZERO,OVRCD
          GOTO      ORINV582 IF EQUAL
          CALL      WRDTRO1
.
.         update invoice and transaction number on outhtraf
.
          MOVE      OTTRANS,OTHTTRAN
          MOVE      CNEXTINV,OTHTINVN        * Update Rebate invoice number
          CALL      UPOTHTR1
.
.        Update the Fees Invoiced Summary file
.
ORINV620  MOVE      OTPATAMT,FINAMT
          MOVE      "A",FINTYPE
.
          MATCH     SP9,OTITEMNO              * standard charge ?
          GOTO      ORINV630 IF NOT EQUAL      * no, post to normal FINCODE
.
.        For the standard charge, check if we are posting to the clinic id
.
          BRANCH    OTCNCFEE,ORINV650
.
.        Using misc group code
.
ORINV630  MOVE      SP3,OBACOMP
          PACK      KEY8,OTNUMB
          CALL      RDBOKB1
.
          MOVE      "M",ANS
          IF        PTCNMFEE=1
            MATCH     OTMISGRP,SP3
            IF        @EQUAL
              PACK      FINCODE,ANSM,OBACOMP,OTITEMNO,SP20
.                               Misc. Charge, Claim Code, Item Code
            ELSE
              PACK      FINCODE,ANSM,OBACOMP,OTMISGRP,SP20
.                               Misc. Charge, Claim Code, Group Code
            ENDIF
          ELSE
            PACK      FINCODE,ANSM,OTMISGRP,OTITEMNO,SP20
.                             Misc. Charge, Group Code, Item Code
          ENDIF
          GOTO      ORINV680
.
.        Using clinic id
.
ORINV650  PACK      FINCODE,ANSA,PVIDOCT,SP20
.
.        Post to the fees invoiced file
.
ORINV680  UNPACK    SP70,FININVN,FINADMN,FINURNO
          IF        PROGFLAG=3
            PACK      FININVN,CNEXTINV,SP70
            MOVE      OBAOUTNO,FINADMN
            MOVE      OBAURNO,FINURNO
          ENDIF
.
          MOVE      PINVDTE,FINDATE       * Invoice date
          REP       " 0",FINDATE
          MOVE      TWO,FINSYS
          MOVE      OBASITE,FINSITE
          MOVE      ZERO,FGSTFLAG
          MOVE      PMVXMHOS,FINHOSP
          CALL      PATCALF
.
          IF        IBCNUGST=2
           IF        LINEGST<>0
             MOVE      LINEGST,FINAMT
             MOVE      ONE,FGSTFLAG
             MOVE      PMVXMHOS,FINHOSP
             CALL      PATCALF
           ENDIF
          ENDIF
.
          IF        PROGFLAG=3
            MOVE      SKEY22,KEY22
            GOTO      ORINV100                 * printing rebate invoice
          ENDIF
          GOTO      ORINV200
.
ORINV9999 RETURN
+
. *****************************************************************************
. * OPKWC000 : Update the number of days charged in the PATKWCFD file         *
. *****************************************************************************
.
          DEFPROC   OPKWC000
.
          INC       PATKWCFD/INC
          INC       PATVKCFD/INC
.
KWCFIL    INIT      "patkwc"
.
OPKWC000  CALL      CHKCD000                 * check the card number
          BRANCH    EXIT,OPKWC999            * no card, or invalid card
.
          CALL      OPNKW000                 * open the appropriate file
          BRANCH    EXIT,OPKWC999            * file not found
.
.         Check if we have a card number
.
          MATCH     SP20,PTVKCARD            * do we have a card number ?
          GOTO      OPKWC100 IF EQUAL        * no, treat as a special case
.
.         Key details if card number is known
.
          MOVE      PTVKCARD,PTKWCARD        * card number
          MOVE      SP8,PTKWURNO             * no U/R number
          GOTO      OPKWC300
.
.         Key details if card number is not known
.
OPKWC100  MOVE      SP20,PTKWCARD            * card number
          MOVE      OBAURNO,PTKWURNO         * U/R number
.
.         Read in the card file details if possible
.
OPKWC300  PACK      KEY28,PTKWCARD,PTKWURNO  * key for card details
          CALL      RDPTKWC1                 * get the card details
          BRANCH    OVRCD,OPKWC500           * no record on file for card
.
          ADD       ONE,PTKWVISI             * no. of outpatient visits
          CALL      UPPTKWC1
          GOTO      OPKWC999
.
.         Write a new record to the kiwi card file
.
OPKWC500  MOVE      ONE,PTKWVISI             * no. of outpatient visits
          MOVE      ZERO,PTKWDAYS            * no. of inpatient days
.
          CALL      WRPTKWC1
.
OPKWC999  GOTO      ENDOPKWC
.
. *****************************************************************************
. * CHKCD000 : Validate card number.                                          *
. *****************************************************************************
.
CHKCD000  MOVE      OTNUMB,KEY8
          OPEN      PATVKCA1,"patvkcaf"
          CALL      RDPTVKC1
          CLOSE     PATVKCA1
          BRANCH    OVRCD,CHKCD900           * no card details
.
          MATCH     SP20,PTVKCARD            * no card number ?
          GOTO      CHKCD800 IF EQUAL        * no, still want to post details
.
          CMATCH    ANSC,PTVKCARD            * community service card ?
          GOTO      CHKCD900 IF NOT EQUAL    * no, don't post to PATKWCFD
.
CHKCD800  MOVE      ZERO,EXIT
          GOTO      CHKCD999
.
.         Invalid card details
.
CHKCD900  MOVE      ONE,EXIT
.
CHKCD999  RETURN
+
. *****************************************************************************
. * OPNKW000 : Open the appropriate kiwicard file                             *
. *****************************************************************************
.
OPNKW000  UNPACK    OTDDATE,CCENT,CYEAR,KEY4
.
          MATCH     PTCNBCUT,KEY4
          IF        ! @LESS
            MOVE      CYEAR,FORM2            * year of kiwicard
          ELSE
            MOVE      CYEAR,FORM2
            SUB       ONE,FORM2              * calculate start of kiwicard year
            IF        FORM2 < 0
              MOVE     "99",FORM2            * allow for the year 2000
            ENDIF
          ENDIF
.
          PACK      CFNAME,KWCFIL,FORM2      * name of kiwicard file
          REP       " 0",CFNAME
.
          MOVE      ZERO,OVRCD               * assume file exists
          TRAP     OVERCOND IF IO            * trap for error openning file
          OPEN     PATKWCA1,CFNAME           * open kiwicard file
          TRAPCLR  IO
.
          BRANCH   OVRCD,OPNKW900            * open failed
.
          MOVE     ZERO,EXIT
          GOTO     OPNKW999
.
OPNKW900  MOVE     ONE,EXIT
.
OPNKW999  RETURN
.
          INC       PATKWCIO/INC
          INC       PATVKCIO/INC
          INC       IBAOVRIO/INC
.
ENDOPKWC  ENDPROC
.
          INC       ABFOTINV
          INC       CLOUTHTR              * Clear Outpat HF debtors transaction
.
