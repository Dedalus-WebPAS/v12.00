.******************************************************************************
. File          :  PATKXDPR.PBL
.
. Function      :  Update Keyword Index File for ICD10 Ed.10 Diagnosis Codes
.
. Parameters    :  DPCODE
.
. Modifications :  
.
. V10.12.01  09/05/2018  Richa Phogat    TSK 0848449
.            Updated GENR0000 by replacing DDESC with PT0DDSC2, 
.            Added new variables
. V10.10.00  08/03/2017  Davin            TSK 0833093
.            Created this procedure based on PATK9DPR.PBL             
.******************************************************************************
.  Notes
.  -----
.  This Skeleton Routine is to Create a Procedure to Update
.  a Key Word Index Table that can be used for searching 
.  Masterfiles. The procedure should be called from the 
.  Add, Update and Delete functions in the Masterfile 
.  Maintenance Function.
.  
.  A Key Word Index Table is Defined in the following Manner
.                              
.  PTCDITM    DIM       xx    * Code Field Segment 1
.  PTCDITM    DIM       xx    * Code Field Segment n
.  PTCDKWD    DIM       15    * Key Word
.  PTCDSPA    DIM       10    * Spare
.
.  Index 1   PTCDITM, PTCDITM, PTCDKWD
.  Index 2   PTCDKWD, PTCDITM, PTCDITM
.
.  To convert this Procedure the following should be performed
.    Global Change PAT to the System ID eg :%s/PAT/PAT/g
.    Global Change OIC to the File   ID eg :%s/OIC/DKI/g
.    Global Change PTOI to the IO Call ID eg :%s/PTOI/PTDK/g
.    Modify the KEYINDEX defintion to combined Code Field Lengths eg 6
.    Global Change KEY9 to the Key for the Master File Table eg KEY6 
.    Global Change KEY24 to the Key for the Key Word Table eg KEY21
.    Change xxxxxxx to the Code Fields eg DCODEhh
.    Change the GENR0000 routine to call the BWORD000 for each
.    string of key words to be indexed for the Coded Field
.    eg MOVE    DSNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    DGNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    TDESC,KEYWORDS
.       CALL    BWORD000
.------------------------------------------------------------
          DEFPROC   PATKXDUP
.
          INC       PATKCDFD/INC
.
KEY200    DIM       200
KEYINDEX  DIM       9         * Code Index in Table
KEYWORDS  DIM       200       * String Containing the Key Words to be Indexed
TWOHUND   FORM      "200"
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
.------------------------------------------------------------
. Generate Word Index
.------------------------------------------------------------
GENR0000  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     PATKXDA1,"patkcdxf"
          TRAPCLR  IO
          BRANCH   OVRCD,GENR9999
.
          MOVE     DPCODE,KEYINDEX
          CALL     CLEAR000           * Remove Current Key Word Index
          BRANCH   EXIT,GENR9999
.
          MOVE     DPCODE,KEY9        * Validate Record on File
          CALL     RDPT1XD1           * ICD10 Ed.10 Disease File
          BRANCH   OVRCD,GENR1900     * If Not then it Must be a Delete/Clear
.
          MOVE     DPCODE,KEYINDEX
          MOVE     DPCODE,KEYWORDS
          CALL     BWORD000
.
          MOVE     PT0DDSC2,KEYWORDS
          CALL     BWORD000
.
GENR1900  CLOSE    PATKXDA1
.
GENR9999  GOTO     PATKCIEP
.------------------------------------------------------------
. Clear Current Key Words
.------------------------------------------------------------
CLEAR000  PACK     KEY24,KEYINDEX,SP70
          CALL     RSPTXD1
          CALL     RKPTXD1
          BRANCH   OVRCD,CLEAR999
          MATCH    KEYINDEX,PTCDITM
          GOTO     CLEAR999 IF NOT EQUAL
.
          PACK     KEY24,PTCDITM,PTCDKWD,SP70
          CALL     DEPTXD1                   * delete record
          GOTO     CLEAR000
.
CLEAR999  RETURN
.-------------------------------------------------
. Determine Words to Index
.-------------------------------------------------
BWORD000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      BWORD000 IF NOT EOS
            GOTO      BWORD999               * entire name if blank
          ENDIF
          PACK      KEY200,KEYWORDS,SP30     * reset form pointer
          MOVE      KEY200,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      BWORD100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Save this Word
.
BWORD100  MOVE      KEYWORDS,KEY15
          PACK      KEY15,KEY15,SP70
          REP       UPPLOW,KEY15             * Always Upper Case
          PACK      KEY24,KEYINDEX,KEY15
          UNPACK    KEY24,PTCDITM,PTCDKWD
          CALL      RDPTXD1
          IF        OVRCD=1
            CALL      WRPTXD1
          ENDIF
          GOTO      BWORD190
.
.         Check for any more words
.
BWORD190  SETLPTR   KEYWORDS,TWOHUND       * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY200,KEYWORDS,SP30   * reset form pointer
          MOVE      KEY200,KEYWORDS
          GOTO      BWORD000
.
BWORD999  RETURN
.
          INC       PATKCDIO/INC
          INC       IBAOVRIO/INC
.
PATKCIEP  ENDPROC                          * End of Procedure
.
