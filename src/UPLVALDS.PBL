.------------------------------------------------------------------------------
. Uploading data validation common sections
.------------------------------------------------------------------------------
. Modifications  : 
.------------------------------------------------------------------------------
. V11.04.01 22/02/2024  Thanh T        TSK 0923560
.           Added CNBLEN00 section
. V11.03.00 23/10/2023  Thanh T        TSK 0928468
.           Initial Version.
.------------------------------------------------------------------------------
. Validate a character field total size
. Requires:  VINFIELD - Charcater field
.            VFLDNAME - Field Name
.            VINTLEN  - Character length
. Returns:   VERRFLAG - 1 if an error in the validation has occurred and set
.                         in the NERR0000 section of the calling program
.            VERRDESC - Description of error when an error has occurred
.------------------------------------------------------------------------------
CCHLEN00  MOVE      ZERO,VERRFLAG
          STRIP     VINFIELD
          MOVE      VINFIELD,DIM100
          MATCH     SP70,DIM100                * blank number ?
          IF        @EQUAL | @EOS
            GOTO      CCHLEN99
          ENDIF
.
          MOVELPTR  DIM100,F3
          IF        F3 > VINTLEN
            PACK      VFLDNAM1,LBRACK,VINTLEN,RBRACK
            SQUEEZE   VFLDNAM1
            PACK      VERRDESC,CCMAXLEN,SP1,VFLDNAM1
            CALL      NERR0000
            GOTO      CCHLEN99
          ENDIF
.
CCHLEN99  RETURN
.
.------------------------------------------------------------------------------
. Validate a numeric field size - No negative sign
. Requires:  VINFIELF - Number field
.            VFLDNAME - Field Name
.            VINTLEN  - Field length
. Returns:   VERRFLAG - 1 if an error in the validation has occurred and set
.                         in the NERR0000 section of the calling program
.            VERRDESC - Description of error when an error is occurred
.------------------------------------------------------------------------------
CNBLEN00  STRIP     VINFIELF
          TYPE      VINFIELF
          IF        !@EQUAL
            PACK      VFLDNAM1,LBRACK,VINFIELF,RBRACK
            PACK      VERRDESC,CNOTNUM,SP1,VFLDNAM1
            CALL      NERR0000
            GOTO      CNBLEN99
          ENDIF
.
CNBLEN20  MOVELPTR  VINFIELF,F3
          IF        F3 > VINTLEN
            PACK      VFLDNAM1,LBRACK,VINTLEN,RBRACK
            SQUEEZE   VFLDNAM1
            PACK      VERRDESC,CTMAXLEN,SP1,VFLDNAM1
            CALL      NERR0000
            GOTO      CNBLEN99
          ENDIF
.
CNBLEN99  RETURN
.
.------------------------------------------------------------------------------
. Validate a numeric field size 3.2 - No negative sign
. Requires:  VINFIELF - Number/Decimal field 
.            VFLDNAME - Field Name
. Returns:   VERRFLAG - 1 if an error in the validation has occurred and set
.                         in the NERR0000 section of the calling program
.            VERRDESC - Description of error when an error is occurred
.------------------------------------------------------------------------------
C32NUM00  STRIP     VINFIELF
          TYPE      VINFIELF
          IF        !@EQUAL
            PACK      VFLDNAM1,LBRACK,VINFIELF,RBRACK
            PACK      VERRDESC,CNOTNUM,SP1,VFLDNAM1
            CALL      NERR0000
            GOTO      C32NUM99
          ENDIF
.
C32NUM20  MOVE      3,VINTLEN
          MOVE      2,VDECLEN
          MOVE      0,VNEGALLW
.
          CALL      NUMDC000
.
C32NUM99  RETURN
.
.------------------------------------------------------------------------------
. Validate a numeric field size 3.2 with negative sign
. Requires:  VINFIELF - Number/Decimal field
.            VFLDNAME - Field Name
. Returns:   VERRFLAG - 1 if an error in the validation has occurred and set
.                         in the NERR0000 section of the calling program
.            VERRDESC - Description of error when an error is occurred
.------------------------------------------------------------------------------
C32NUS00  STRIP     VINFIELF
          TYPE      VINFIELF
          IF        !@EQUAL
            PACK      VFLDNAM1,LBRACK,VINFIELF,RBRACK
            PACK      VERRDESC,CNOTNUM,SP1,VFLDNAM1
            CALL      NERR0000
            GOTO      C32NUS99
          ENDIF
.
C32NUS20  MOVE      3,VINTLEN
          MOVE      2,VDECLEN
          MOVE      1,VNEGALLW
.
          CALL      NUMDC000
.
C32NUS99  RETURN
.
.------------------------------------------------------------------------------
. Validate a numeric with decimal field
. Requires:  VINFIELF - Number/Decimal field
.            VFLDNAME - Field Name
.            VINTLEN  - Integer length
.            VDECLEN  - Decimal length
.            VNEGALLW - 1 (Yes), 0 (No)
. Returns:   VERRFLAG - 1 if an error in the validation has occurred and set
.                         in the NERR0000 section of the calling program
.            VERRDESC - Description of error when an error is occurred
.------------------------------------------------------------------------------
NUMDC000  MOVE      ZERO,VERRFLAG
          STRIP     VINFIELF
          MOVE      VINFIELF,WORKFLD
          MATCH     SP70,WORKFLD               * blank number ?
          IF        @EQUAL | @EOS
            GOTO      NUMDC999
          ENDIF
.
          COMPARE   ZERO,VNEGALLW
          IF        @EQUAL
            ASSIGN    VINTLEN+VDECLEN+1,VTOTLEN
          ELSE
            ASSIGN    VINTLEN+VDECLEN+2,VTOTLEN 
          ENDIF
          MOVELPTR  WORKFLD,FORM2
          IF        FORM2 > VTOTLEN
            PACK      VFLDNAM1,LBRACK,VINFIELF,RBRACK
            PACK      VERRDESC,CTMAXLEN,SP1,VFLDNAM1
            CALL      NERR0000
            GOTO      NUMDC999
          ENDIF
.
NUMDC200  SCAN      ".",WORKFLD
          IF        @EQUAL
            BUMP      WORKFLD
            MOVEFPTR  WORKFLD,FORM2
            MOVE      WORKFLD,WORKFLDD
            SUB       TWO,FORM2
            MOVE      VINFIELF,WORKFLD
            SETLPTR   WORKFLD,FORM2
            MOVE      WORKFLD,WORKFLDI
          ELSE
            MOVE      WORKFLD,WORKFLDI
            MOVE      SP70,WORKFLDD
          ENDIF
.
          SQUEEZE   WORKFLDI
          MOVELPTR  WORKFLDI,FORM2
          IF        FORM2 > VINTLEN
            MOVE      "Integer Length",VERRDESC
            CALL      NERR0000
            GOTO      NUMDC999
          ENDIF
.
          SQUEEZE   WORKFLDD
          MOVELPTR  WORKFLDD,FORM2
          IF        FORM2 > VDECLEN
            MOVE      "Decimal Length",VERRDESC
            CALL      NERR0000
            GOTO      NUMDC999
          ENDIF
.
          COMPARE   ZERO,VNEGALLW
          IF        @EQUAL
            SCAN      HYPHEN,WORKFLD
            IF        @EQUAL
              MOVE      "Negative SIGN Not Allowed",VERRDESC
              CALL      NERR0000
              GOTO      NUMDC999
            ENDIF
          ENDIF
.
NUMDC999  RETURN
.
.------------------------------------------------------------------------------
. Validate a date field
.
. Requires:  TEMPDATE - Date in format ccyymmdd
.            VFLDNAME  - Date Field Name
. Returns:   VERRFLAG - 1 if an error in the validation has occurred and set
.                         in the NERR0000 section of the calling program
.            VERRDESC - Description of error when an error is occurred
.------------------------------------------------------------------------------
VDAT0000  MOVE      ZERO,VERRFLAG
          MATCH     SP8,TEMPDATE                 * blank date ?
          GOTO      VDAT9999 IF EQUAL            * yes
.
. Validate that the date has been fully populated
.
          SQUEEZE   TEMPDATE
          MOVELPTR  TEMPDATE,FORM1
          IF        FORM1 <> 8
            MOVE      "Insufficient digits",VERRDESC
            CALL      NERR0000
            GOTO      VDAT9999
          ENDIF
.
. Validate that the date is numeric
.
VDAT0100  TYPE      TEMPDATE
          IF        !@EQUAL
            MOVE      "Not numeric",VERRDESC
            CALL      NERR0000
            GOTO      VDAT9999
          ENDIF
.
          UNPACK    TEMPDATE,DIM2C,DIM2Y,DIM2M,DIM2D
.
. Validate the century is >= 18 and <= current century
.
          MOVE      DIM2C,FORM2
          IF        FORM2 < 18 | FORM2 > CURRCENT
            MOVE      "Century not valid",VERRDESC
            CALL      NERR0000
            GOTO      VDAT9999
          ENDIF
.
. Validate the month
.
          MOVE      DIM2M,FORM2
          IF        FORM2 < 1 | FORM2 > 12
            MOVE      "Month not valid",VERRDESC
            CALL      NERR0000
            GOTO      VDAT9999
          ENDIF
.
. Validate the day is:
.   < 32 for January, March, May, July, August, October & December
.   < 31 for April, June, September & November
.   < 29 for February, except in a leap year where it is < 30
.
          BRANCH    FORM2,VDAT1000:              * Jan
                          VDAT3000:              * Feb
                          VDAT1000:              * Mar
                          VDAT2000:              * Apr
                          VDAT1000:              * May
                          VDAT2000:              * Jun
                          VDAT1000:              * Jul
                          VDAT1000:              * Aug
                          VDAT2000:              * Sep
                          VDAT1000:              * Oct
                          VDAT2000:              * Nov
                          VDAT1000               * Dec
.
. Validate days for month of 31 days
.
VDAT1000  MOVE      DIM2D,FORM2
          IF        FORM2 < 1 | FORM2 > 31
            GOTO      VDAT3900
          ENDIF
          GOTO      VDAT9999
.
. Validate days for month of 30 days
.
VDAT2000  MOVE      DIM2D,FORM2
          IF        FORM2 < 1 | FORM2 > 30
            GOTO      VDAT3900
          ENDIF
          GOTO      VDAT9999
.
. Validate days for February
. A leap year is one where:
.  1. Every year divisible by 4 is a leap year.
.  2. But every year divisible by 100 is NOT a leap year
.  3. Unless the year is also divisible by 400, then it is still a
.     leap year.
.
VDAT3000  MOVE      DIM2D,FORM2
          PACK      DIM4,DIM2C,DIM2Y
          MOVE      DIM4,FORM4
          IF        (FORM4%4) = 0
            IF        (FORM4%100) = 0
              IF        (FORM4%400) = 0
                GOTO      VDAT3200
              ENDIF
            ELSE
              GOTO      VDAT3200
            ENDIF
          ENDIF
.
. Check for normal Feb days
.
VDAT3100  IF        FORM2 < 1 | FORM2 > 28
            GOTO      VDAT3900
          ENDIF
          GOTO      VDAT9999
.
. Check for leap year Feb days
.
VDAT3200  IF        FORM2 < 1 | FORM2 > 29
            GOTO      VDAT3900
          ENDIF
          GOTO      VDAT9999
.
VDAT3900  MOVE      "Day not valid",VERRDESC
          CALL      NERR0000
          GOTO      VDAT9999
.
VDAT9999  RETURN
.
.------------------------------------------------------------------------------
. Validate a time field
.
. Requires:  TEMPTIME - Time in format hh:mm:ss
.            VFLDNAME  - Time Field Name
. Returns:   VERRFLAG - 1 if an error in the validation has occurred and set
.                         in the NERR0000 section of the calling program
.            VERRDESC - Description of error when an error is occurred
.------------------------------------------------------------------------------
VTIM0000  MOVE      ZERO,VERRFLAG
          MATCH     SP8,TEMPTIME                 * blank time ?
          GOTO      VTIM9999 IF EQUAL            * yes
.
VTIM0100  UNPACK    TEMPTIME,DIMHH,DIMSEP1,DIMMM,DIMSEP2,DIMSS
.
          MATCH     COLON,DIMSEP1
          IF        !@EQUAL
            MOVE      "Incorrect format",VERRDESC
            CALL      NERR0000
            GOTO      VTIM9999
          ENDIF
.
          MATCH     COLON,DIMSEP2
          IF        !@EQUAL
            MOVE      "Incorrect format",VERRDESC
            CALL      NERR0000
            GOTO      VTIM9999
          ENDIF
.
          TYPE      DIMHH
          IF        !@EQUAL
            MOVE      "Hour must be numeric",VERRDESC
            CALL      NERR0000
            GOTO      VTIM9999
          ELSE
            MOVE      DIMHH,FORM2
            IF        FORM2 > 23
              MOVE      "Hour must between 0 and 23",VERRDESC
              CALL      NERR0000
              GOTO      VTIM9999
            ENDIF
          ENDIF
.
          TYPE      DIMMM
          IF        !@EQUAL
            MOVE      "Minute must be numeric",VERRDESC
            CALL      NERR0000
            GOTO      VTIM9999
          ELSE
            MOVE      DIMMM,FORM2
            IF        FORM2 > 59
              MOVE      "Minute must between 0 and 59",VERRDESC
              CALL      NERR0000
              GOTO      VTIM9999
            ENDIF
          ENDIF
.
          TYPE      DIMSS
          IF        !@EQUAL
            MOVE      "Second must be numeric",VERRDESC
            CALL      NERR0000
            GOTO      VTIM9999
          ELSE
            MOVE      DIMSS,FORM2
            IF        FORM2 > 59
              MOVE      "Second must between 0 and 59",VERRDESC
              CALL      NERR0000
              GOTO      VTIM9999
            ENDIF
          ENDIF
.
VTIM9999  RETURN
