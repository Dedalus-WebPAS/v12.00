. *----------------------------------------------------------------------
. * Include Name  :   ALLITVTM.PBL
. *
. * Function      :   CGI variables for ALLITVTM - allitvaf.dat
. *
. * Includes Needed : 
. *                   
. * Modifications   :
. *                   V9.11.01 16/10/2008 Peter Vela CAR 179155
. *                            Fixed redirect in NEXTIV00 and PREVIV00
. *                   V9.08.01 30/03/2007 Jill Habasque  
. *                            Created include
. *----------------------------------------------------------------------
.
.------------------------------------------------------------------------
.   Clear Parameters for Allied Health ITV Table
.------------------------------------------------------------------------
.
CPALIV00  MOVE      Z70,ALITV001
          MOVE      Z70,ALITV002
          MOVE      Z70,ALITV003
          MOVE      Z70,ALITV004
          MOVE      Z70,ALITV005
.
CPALIV99  RETURN
.
.------------------------------------------------------------
. Add fields for allied health ITV Table
.------------------------------------------------------------
ALIV0000  BRANCH    FLDITMNO,ALIV0100,ALIV0200,ALIV0300,ALIV0400,ALIV0500:
                             ALIV0600,ALIV0700,ALIV0800,ALIV0900,ALIV1000:
                             ALIV1100,ALIV1200,ALIV1300,ALIV1400
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      ALIV9999
.
.
ALIV0100  WRITE     HTMLFILE;ALIVVISN;
          GOTO      ALIV9999
.
ALIV0200  WRITE     HTMLFILE;ALIVENCT;
          GOTO      ALIV9999
.
ALIV0300  WRITE     HTMLFILE;ALIVICNT;
          GOTO      ALIV9999
.
ALIV0400  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      KEY1,F1
          BRANCH    F1,ALIV0410
.
          WRITE     HTMLFILE;ALIVCODE;
          GOTO      ALIV9999
.
ALIV0410  PACK      KEY9,ALIVCODE,SP70
          CALL      RDALINC1
          BRANCH    OVRCD,ALIV9999
.
          WRITE     HTMLFILE;ALINDESC;
          GOTO      ALIV9999
.
ALIV0500  WRITE     HTMLFILE;ALIVDURA;
          GOTO      ALIV9999
.
ALIV0600  MATCH     ALIVCDAT,SP70
          GOTO      ALIV9999 IF EQUAL
          UNPACK    ALIVCDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      ALIV9999
.
ALIV0700  WRITE     HTMLFILE;ALIVCTIM; 
          GOTO      ALIV9999
.
ALIV0800  MATCH     SP70,ALIVCUID
          GOTO      ALIV9999 IF EQUAL
          PACK      KEY10,ALIVCUID
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      ALIVCUID,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM;
          GOTO      ALIV9999
.
ALIV0900  MATCH     ALIVUDAT,SP70
          GOTO      ALIV9999 IF EQUAL
          UNPACK    ALIVUDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      ALIV9999
.
ALIV1000  WRITE     HTMLFILE;ALIVUTIM;
          GOTO      ALIV9999
.
ALIV1100  MATCH     SP70,ALIVUUID
          GOTO      ALIV9999 IF EQUAL
          PACK      KEY10,ALIVUUID
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      ALIVUUID,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM;
          GOTO      ALIV9999
.
ALIV1200  CALL      IVTAB000
          GOTO      ALIV9999
.
ALIV1300  CALL      NEXTIV00
          GOTO      ALIV9999
.
ALIV1400  CALL      PREVIV00
          GOTO      ALIV9999
.
ALIV9999  RETURN
.
.------------------------------------------------------------
. Table of Allied Health Intervention Details
.------------------------------------------------------------
IVTAB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      "10",NORECORD       * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
          CALL      IVHED000                * Display ITV heading
.
          UNPACK    ENCOUTKY,ADMISSNO,ALIVENCT
.
          PACK      KEY24,ENCOUTKY,STARTKEY,SP70
          CALL      RSALITV1
          CALL      RPALITV1
IVTAB100  CALL      RKALITV1
          BRANCH    OVRCD,IVTAB999
.
          MATCH     ADMISSNO,ALIVVISN
          GOTO      IVTAB999 IF NOT EQUAL
.
          MATCH     ALENENCT,ALIVENCT
          GOTO      IVTAB999 IF NOT EQUAL
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO     IVTAB100
          ENDIF
.
          CALL      IVROW000                * Display ITV Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      IVTAB100 IF NOT EQUAL
.
IVTAB999  RETURN
.
.******************************************************************************
.*                      Display Of Header(Intervention List)                  *
.******************************************************************************
IVHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=12%>":
                               "<b>Intervention Code</b></td>":
                               "<td class=HeadingCell width=18%>":
                               "<b>Description</b></td>":
                               "<td class=HeadingCell width=18%>":
                               "<b>Duration</b></td>":
                             "</tr>"
.
IVHED999  RETURN
.
.------------------------------------------------------------
.  Allied Health Intervention Details (TABLE ROW)
.------------------------------------------------------------
IVROW000   PACK      KEY8,ALIVVISN,SP70
           CALL      RDALREF1
           IF        OVRCD=1
             PACK      ALREDEPT,SP70
           ENDIF
           PACK      KEY12,ALREDEPT,ALIVCODE,SP70
           CALL      RDALINC1
           IF        OVRCD=1
             PACK      ALINDESC,SP70
           ENDIF
.
           REP       " +",ENCOUTKY
           REP       " +",ALIVVISN
           REP       " +",ALIVENCT
           REP       " +",ALIVICNT 
           WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&encoutky=",ENCOUTKY:
                             "&alitv001=",ALIVVISN:
                             "&alitv002=",ALIVENCT:
                             "&alitv003=",ALIVICNT,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             ALIVCODE,"</td>":
                             "<td>",ALINDESC,"&nbsp;</td>":
                             "<td>",ALIVDURA,"&nbsp;</td>":
                             "</tr>"
           REP       "+ ",ENCOUTKY
           REP       "+ ",ALIVVISN
           REP       "+ ",ALIVENCT
           REP       "+ ",ALIVICNT 
.
IVROW999  RETURN
.
.-----------------------------------------------------------
. Link to Next Group
.-----------------------------------------------------------
NEXTIV00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,D3,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",ENCOUTKY
          REP       " +",ALIVICNT
.
          WRITE     HTMLFILE;"allweb03.pbl":
                                 "?reportno=",F2:
                                 "&template=",D3:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&encoutky=",ENCOUTKY:
                                 "&alitv003=",ALIVICNT;
.
          REP       "+ ",ENCOUTKY
          REP       "+ ",ALIVICNT
.
NEXTIV99  RETURN
.
.-------------------------------------------------------------
. Link to Prev Group
.-------------------------------------------------------------
PREVIV00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",ENCOUTKY
          REP       " +",ALIVICNT
.
          WRITE     HTMLFILE;"allweb03.pbl":
                                "?reportno=",F2:
                                "&template=",FLDPARA:
                                "&startnum=",KEY3:
                                "&norecord=",KEY2:
                                "&encoutky=",ENCOUTKY:
                                "&alitv003=",ALIVICNT;
.
          REP       "+ ",ENCOUTKY
          REP       "+ ",ALIVICNT
.
PREVIV99  RETURN
.
.------------------------------------------------------------
.   Set Parameters for allied health ITV table
.------------------------------------------------------------
SPALIV00  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPALIV01,SPALIV02,SPALIV03,SPALIV04,SPALIV05
.
          MOVE      ONE,EXIT
          GOTO      SPALIV99
.
SPALIV01  MOVE      QRYDATA,ALITV001
          GOTO      SPALIV99
.
SPALIV02  MOVE      QRYDATA,ALITV002
          GOTO      SPALIV99
.
SPALIV03  MOVE      QRYDATA,ALITV003
          GOTO      SPALIV99
.
SPALIV04  MOVE      QRYDATA,ALITV004
          GOTO      SPALIV99
.
SPALIV05  MOVE      QRYDATA,ALITV005
          GOTO      SPALIV99
.
SPALIV99  RETURN
.------------------------------------------------------------
.  Update allied health ITV table
.------------------------------------------------------------
MVALIV00  MATCH     ALITV001,Z70
          IF        !@EQUAL
            MOVE      ALITV001,ALIVVISN
          ENDIF
.
          MATCH     ALITV002,Z70
          IF        !@EQUAL
            MOVE      ALITV002,ALIVENCT
          ENDIF
.
          MATCH     ALITV003,Z70
          IF        !@EQUAL
            MOVE      ALITV003,ALIVICNT
          ENDIF
.
          MATCH     ALITV004,Z70
          IF        !@EQUAL
            MOVE      ALITV004,ALIVCODE
          ENDIF
.
          MATCH     ALITV005,Z70
          IF        !@EQUAL
            MOVE      ALITV005,ALIVDURA 
          ENDIF
.
MVALIV99  RETURN
