.******************************************************************************
.*                                                                            *
.*                              PRICALFS                                      *
.*                              --------                                      *
.*                                                                            *
.*    Include to update the Private Practice Financial Summary File           *
.*                                                                            *
.*    AUTHOR           : DIG                                                  *
.*                                                                            *
.*    DATE             : 16/07/91                                             *
.*                                                                            *
.*    VARIABLES NEEDED : FSTAMNT        FORM     12.2                         *
.*                       FSTDATE        DIM        8  (CCYYMMDD)              *
.*                       FSTWORK        FORM     12.2                         *
.*                       FGSTFLAG       FORM     1                            *
.*                                                                            *
.*    FILES NEEDED     : PRIFINFD + IO  (Open the 1st index)                  *
.*                       PATDRGFD + IO                                        *
.*                                                                            *
.*    PARAMETERS       : FSTDATE  -  Date of the transaction                  *
.*                       FSTAMNT  -  Amount of the transaction                *
.*                       PRFNTYPE -  Financial Type (see PRIFINFD)            *
.*                       PRFNCODE -  Finance Code (see PRIFINFD)              *
.*                       PRFNMPRA -  Medical Practice (see PRIFINFD)          *
.*                                                                            *
.*    RETURNS          : EXIT = 1  if the Date Range File is not set up for   *
.*                                 the transaction date or the current date   *
.*                            = 0  if update is successfull                   *
.*                                                                            *
.*    COMMENTS         : This include was copied from PPPCALFN.PBL and then   *
.*                       modified for the new Private Practice system.        *
.*                                                                            *
.*    MODIFICATIONS    :                                                      *
.*        V10.13.01 26/07/2018  J.Tan         TSK 0848506                     *
.*                  Changed PP Doctor from DIM6 to DIM10                      *
.*        V9.12.01  23/07/2009 J.Tan      CAR 200230                          *
.*                  Fixed generating previous year data                       *
.*        V9.11.01  08/01/2009 J.Tan      CAR 178415                          *
.*                  Added Medical Practice to prifinsf                        *
.*        V9.04.01  11/08/2002  J.Tan     CAR 62750                           *
.*                  Mods amount variables to be 12.2                          *
.*        V9.02.03  07/02/2002  J.Tan                                         *
.*                  Recompiled for PRIINVPR - Doctor Income percentage        *
.*       V9.02.02  29.11.2001  B.G.Ackland                                    *
.*                 Remove pi's from Program
.*                                                                            *
.******************************************************************************
PRICALFS  MOVE      FALSE,EXIT
          OPEN      PATDRGA2,"patdrgaf"     * Open the date range file
.
          PACK      KEY14,FSTDATE,SP6       * Position to this date
          CALL      RDSDRGA2                * Get the first record on the date
          CALL      RDKDRGA2                  range file on or after this date
          BRANCH    OVRCD,PRIFS900          
.
          MATCH     DRGFDTE,FSTDATE         * Is date before the from date ?
          GOTO      PRIFS900 IF LESS        * Yes. Date not found in file.
.
          MOVE      DRGYR,PRFNYEAR          
          MOVE      DRGNUM,FORM2
.
          MATCH     ANSA,PRFNTYPE           * invoice date must be current year
          GOTO      PRIFS300 IF EQUAL         so bypass next code
.
.------ Check if this is in a prior financial year ------
.
          PACK      FSTDATE,CCC,CYY,CMM,CDD
          REP       " 0",FSTDATE   
.
          PACK      KEY14,FSTDATE,SP6       * Position to this date
          CALL      RDSDRGA2                * Get the first record on the date
          CALL      RDKDRGA2                  range file on or after this date
          BRANCH    OVRCD,PRIFS900          
.
          MATCH     DRGFDTE,FSTDATE         * Is date before the from date ?
          GOTO      PRIFS900 IF LESS        * Yes. Date not found in file.
.
          MATCH     DRGYR,PRFNYEAR          * see if the years are the same
          GOTO      PRIFS300 IF EQUAL       * Yes. Continue Processing
.
          GOTO      PRIFS200
.
.------ figures need to go into the last year amount field on the ------
.------ financial summary file, for the current financial year ------
.
PRIFS200  MOVE      TEN4,FORM2      
          MOVE      DRGYR,PRFNYEAR 
.
.------ write the data to the financial summary file ------
.
PRIFS300  PACK      KEY39,PRFNYEAR,PRFNTYPE,PRFNCODE,PRFNMPRA,SP70
.
.------ see if the record exists on the financial summary file ------
.
PRIFS400  IF        FGSTFLAG=1
            MOVE      "PRG",PRXCODE   * System Lock Audits
            CALL      GETSLK00        * Get System Lock Audits
            CALL      RDPRFG1                 * Read financial summary file
          ELSE
            MOVE      "PFR",PRXCODE   * System Lock Audits
            CALL      GETSLK00        * Get System Lock Audits
            CALL      RDPRFN1                 * Read financial summary file
          ENDIF
          BRANCH    OVRCD,PRIFS500          
.
          LOAD      FSTWORK,FORM2,PRFNPR01,PRFNPR02,PRFNPR03:
                                  PRFNPR04,PRFNPR05,PRFNPR06:
                                  PRFNPR07,PRFNPR08,PRFNPR09:
                                  PRFNPR10,PRFNPR11,PRFNPR12:
                                  PRFNPR13,PRFNLAST
.
          ADD       FSTAMNT,FSTWORK        
.
          STORE     FSTWORK,FORM2,PRFNPR01,PRFNPR02,PRFNPR03:
                                  PRFNPR04,PRFNPR05,PRFNPR06:
                                  PRFNPR07,PRFNPR08,PRFNPR09:
                                  PRFNPR10,PRFNPR11,PRFNPR12:
                                  PRFNPR13,PRFNLAST
.
          ADD       FSTAMNT,PRFNPERA      
.
          IF        FGSTFLAG=1
            CALL      UPPRFG1                 * Update the record
          ELSE
            CALL      UPPRFN1                 * Update the record
          ENDIF
          CALL      RELSLK00        * Release System Lock Audits
.
          GOTO      PRIFS999            
.
.------ Write a new record with zero amounts and then update the record ------
.
PRIFS500  MOVE      ZERO,PRFNPERA
          MOVE      ZERO,PRFNPR01
          MOVE      ZERO,PRFNPR02
          MOVE      ZERO,PRFNPR03
          MOVE      ZERO,PRFNPR04
          MOVE      ZERO,PRFNPR05
          MOVE      ZERO,PRFNPR06
          MOVE      ZERO,PRFNPR07
          MOVE      ZERO,PRFNPR08
          MOVE      ZERO,PRFNPR09
          MOVE      ZERO,PRFNPR10
          MOVE      ZERO,PRFNPR11
          MOVE      ZERO,PRFNPR12
          MOVE      ZERO,PRFNPR13
          MOVE      ZERO,PRFNLAST
          MOVE      SP5,PRFNSPAR
.
          IF        FGSTFLAG=1
            CALL      WRPRFG1                 * write to the financial summary
          ELSE
            CALL      WRPRFN1                 * write to the financial summary
          ENDIF
          GOTO      PRIFS400
.
.------ The requested date does not exist in the date range file OR ------
.------ the current date does not exist in the date range file ------
.
PRIFS900  UNPACK    FSTDATE,CCENT,CYEAR,CMON,CDAY
.
          CALL      PACDATE                 * pack the date
.
          DISPLAY   *P1:24,*EL,*B,"** Date ",*V2LON,CPCDATE,*HOFF," not found":
                    " in Period Range file. Hit <",*V2LON,"ENTER",*HOFF:
                    "> to continue ";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
          MOVE      TRUE,EXIT
.
.------ Finished update of financial summary file ------
.
PRIFS999  RETURN
+
