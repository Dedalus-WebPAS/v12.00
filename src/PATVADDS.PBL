.******************************************************************************
.*                                                                            *
.*                                PATVADDS                                    *
.*                                --------                                    *
.*                                                                            *
.*    INClude to display all transfer sources in destination description order*
.*                                                                            *
.*    AUTHOR           : Glen Hobby                                           *
.*                                                                            *
.*    DATE             : 16/05/1990                                           *
.*                                                                            *
.*    FILES NEEDED     : PATVADFD, PATVADIO                                   *
.*                                                                            *
.*    MODIFICATIONS    :                                                      *
.*        V5.08.B01 30/03/2000  Greg Horvat                                   *
.*                  Replaced TFSCDESC with KEY30                              *
.*        V5.01.00  21/06/1990  Glen Hobby                                    *
.*                  Upgrade from V5.00 to V5.01                               *
.*        V5.01.01  20/12/1991  i chung           SRF 112242                  *
.*                  Modify to allow search by description                     *
.*        V5.01.02  07/01/93  J.Tan      SRF 118774                           *
.*                  Mods the heading                                          *
.*        V5.01.03  11/03/1993  i chung           SRF 121157                  *
.*                  Fixed incorrect keyin position for description            *
.*        V5.01.04  04/03/94  I.Rutt                                          *
.*                  Lined up headings                                         *
.*        V5.03.01  23/11/1995  Greg Horvat      SRF 612434                   *
.*                  Changed to display the second half & next the screen from *
.*                  line 5 instead of line 9                                  *
.*                                                                            *
.******************************************************************************
PATVADDS  PACK      KEY35,SP30,SP5
          CALL      RSPTVAD2                * check to see if file is empty
          CALL      RKPTVAD2
          BRANCH    OVRCD,PATVA700          * file is empty - display message
.
          DISPLAY   *P1:3,*EF,*P20:4,*V2LON,*ULON:
                    "TRANSFER SOURCE/DESTINATION",*HOFF:
                    *P1:6,"Transfer Source/Destination description : ":
                          "______________________________";
.
          MOVE      SP30,KEY30
          KEYIN     *P43:6,*V2LON,KEY30,*P43:6,*DV,KEY30;
.
PATVA100  MOVE      "5",CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF;
.
          PACK      KEY35,KEY30,SP5
          CALL      RDPTVAD2
.
.         Loop over file
.
PATVA200  CALL      RKPTVAD2
          BRANCH    OVRCD,PATVA800
.
          RESET     KEY30
          GOTO      PATVA300 IF EOS
.
          MATCH     KEY30,SP30
          GOTO      PATVA300 IF EQUAL
.
          MATCH     KEY30,PTVADESC
          GOTO      PATVA800 IF NOT EQUAL
.
PATVA300  DISPLAY   *PCCOL:CVERT,*V2LON,PTVACODE,*HOFF,SP3,PTVADESC;
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      PATVA200 IF LESS
.
          MOVE      "5",CVERT
          ADD       "40",CCOL
          COMPARE   "70",CCOL
          GOTO      PATVA200 IF LESS
.
          DISPLAY   *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue, (",*V2LON,"F":
                    *HOFF,")irst Screen, (",*V2LON,"N",*HOFF,")ext Screen ? ";
.
PATVA400  KEYIN     *P45:24,*EL,*DV,UNDLN1,*P45:24,*V2LON,ANS;
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          MATCH     SP1,ANS
          GOTO      PATVA450 IF EQUAL
.
          REP       UPPLOW,ANS               * Convert to upper case
          DISPLAY   *P45:24,*V2LON,ANS;
.
          MATCH     ANSC,ANS
          GOTO      PATVA900 IF EQUAL        * Continue
          MATCH     ANSF,ANS
          GOTO      PATVA100 IF EQUAL        * First screen
          MATCH     ANSN,ANS
          GOTO      PATVA450 IF EQUAL        * Next screen
          BEEP
          GOTO      PATVA400                 * Incorrect keyin
.
PATVA450  MOVE      "5",CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF;               * Clear screen for next lot of data
          GOTO      PATVA200
.
. ------  if missing Transfer Source code show code missing  ------
.
PATVA700  DISPLAY   *P1:4,*EF,*B,*P20:4,"No Transfer":
                    " Source codes on file",*HOFF,*V2LON,*P1:24;
          CALL      HITENTER
          MOVE      ONE,OVRCD
          GOTO      PATVA999
.
. ------  end of PATVADFD file  ------
.
PATVA800  DISPLAY   *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue, (":
                                   *V2LON,"F",*HOFF,")irst Screen ? ";
.
PATVA810  KEYIN     *P30:24,*DV,UNDLN1,*P30:24,*V2LON,ANS;
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          MATCH     SP1,ANS
          GOTO      PATVA820 IF EQUAL
.
          REP       UPPLOW,ANS               * Convert to upper case
          DISPLAY   *P30:24,*V2LON,ANS;
.
          MATCH     ANSC,ANS
          GOTO      PATVA900 IF EQUAL        * Continue
          MATCH     ANSF,ANS
          GOTO      PATVA100 IF EQUAL        * First screen
.
PATVA820  BEEP
          GOTO      PATVA810                 * Incorrect keyin
.
. ------  "Continue" option  ------
.
PATVA900  MOVE      ZERO,OVRCD
.
PATVA999  RETURN
+
