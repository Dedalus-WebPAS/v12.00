. *****************************************************************************
. * Routine Name  : UNLNKMUL                                                  *
. *                                                                           *
. * Function      : To utilise the multi database search feature.  It will    *
. *                 loop over each of the databases Visit files for the UR    *
. *                 and extract all the UNLINKED visits and display the       * 
. *                 record.                                                   *
. *                                                                           * 
. * Modifications :                                                           *
. *                                                                           *
. *       V12.00.01 30/05/2025  Don Nguyen       TSK 0955096                  *
. *                 Alphanumeric visit number changes                         *
. *       V10.08.01 02/05/2016  Ebon Clements    TSK 0268970                  *
. *                 Change to use OUTCLIFD index 1 instead of index 2         *
. *       V10.00.01 26/07/2010  Ebon Clements    CAR 225970                   *
. *                 Fixed open of outboka6 in MLOUT000                        *
. *       V9.10.04  22/06/2009  Jill Habasque    CAR 179650                   *
. *                 Mods to use outboka6 in MLOUT000                          *
. *       V9.10.03  24/02/2009  Ebon Clements    CAR 189402                   *
. *                          Don't display cancelled ED visits                *
. *       V9.10.02  18/02/2009  Ebon Clements    CAR 184833                   *
. *                          Increased display limit from 50 to 999           *
. *       V9.10.01  04/02/2009  Jill Habasque    CAR 179332                   *
. *                          Mods to move OTRLDIAG to DIAGNSIS                *
. *                 V9.05.B01 20/02/2006  Ebon Clements     CAR 76341         *
. *                          Added referral and encounter file audit          *
. *                 V9.04.00 14/10/2005 Ebon Clements CAR 75461               *
. *                          Fixed display of diagnosis in MLOUT000           *
. *                          Fixed display of clinic/doctor for outpatients   *
. *                 V9.03.04 11/12/2003 Ebon Clements CAR 45549               *
. *                          Mods for OUTRF1FD - change from doctors file     *
. *                          to hcp file                                      *
. *                 V9.03.03 04/08/2003 Jill Habasque SRF 42003               *
. *                           Added match for outpatient site                 *
. *                 V9.03.02 22.07.2003 Jill Habasque CAR 21760               *
. *                          Changed key for bokdiaaf                         *
. *                 V9.03.01 09.07.2003 Jill Habasque                         *
. *                           Mods for multi database                         *
. *                 V9.02.00 09/10/2002 Sylvek Litewka   SRF 22692            *
. *                          Made changes to process (display) Allied Health  *
. *                          Referrals (patvisaf.pvitype=9, pvisyst=2) and    *
. *                          Patient Events (patvisaf.pvitype=9, pvisyst=1)   *
. *                                                                           *
. *                 V5.07.01 04.10.1999 B.G.Ackland                           *
. *                          Changes Made at Taranaki                         *
. *                 V5.07.00 22.09.1999 Pat Dirito                            *
. *                          Created Program - WEB Version of GETVIMUL        *
. *                                                                           *
. *****************************************************************************
          DEFPROC   UNLNKMUL
.
          INC       AAEDE1FD/INC         * Aae details File
          INC       ALLCONFD/INC
          INC       EOCREFFD/INC         * Aae details File
          INC       BOKRC1FD/INC         * Booking File
          INC       EMRVISFD/INC         * Emergency Visit
          INC       OUTBOAFD/INC         * Slots File
          INC       OUTBB1FD/INC         * Booking File
          INC       OUTCLIFD/INC         * Clinic Id File
          INC       OUTCTYFD/INC         * Clinic Type File
          INC       OUTMA1FD/INC         * Outpatient Master File
          INC       OUTRF1FD/INC         * Referrals File
          INC       OUTSITFD/INC         * Site File
          INC       PATHOSFD/INC         * Database File
          INC       PATHSPFD/INC         * Hospital File
          INC       PATMI1FD/INC         * Admission File
          INC       PATVISFD/INC         * Visit File
          INC       PATWR1FD/INC         * Ward File
          INC       WATTR1FD/INC         * Waiting List File
          INC       OUTDIAFD/INC         * Outpatient - Diagnosis File
          INC       BOKDIAFD/INC         * Booking Diagnosis File 
          INC       ALLPRRFD/INC         * Allied Health Problem Codes File
          INC       ALLREFFD/INC         * Allied Health Referal File
          INC       PMSEVTFD/INC         * Patient Event File
          INC       PATCODFD/INC         * Patient Codes File
.-----------------------------------------------------------------------------
. Variable Declarations
.
SAVOSTF   DIM       3
DISSTAT   DIM       1
HOSPNUM   FORM      2
HOSPCNT   FORM      2
MRCNNOHS  FORM      2
LNKCOUNT  FORM      3
DMSHOSPN  FORM      2
CLIHOSPN  FORM      2
DIAHOSPN  FORM      2
DIGHOSPN  FORM      2
.DOCHOSPN  FORM      2
SITHOSPN  FORM      2
MISHOSPN  FORM      2
VISHOSPN  FORM      2
AHRHOSPN  FORM      2
APCHOSPN  FORM      2
PEVHOSPN  FORM      2
PCDHOSPN  FORM      2
.
.-----------------------------------------------------------------------------
UNLNK000  MOVE      "99",DMSHOSPN
          MOVE      "99",CLIHOSPN
          MOVE      "99",DIAHOSPN
          MOVE      "99",DIGHOSPN
.          MOVE      "99",DOCHOSPN
          MOVE      "99",SITHOSPN
          MOVE      "99",MISHOSPN
          MOVE      "99",VISHOSPN
	  MOVE      "99",AHRHOSPN
	  MOVE      "99",APCHOSPN
          MOVE      "99",PEVHOSPN
          MOVE      "99",PCDHOSPN
.         
          OPEN      PATHSPA1,"pathspaf"
          OPEN      CONTROLF,"controlf" 
          READ      CONTROLF,SIXTY;*129,MRCNNOHS  * Number of Data Directories
          READ      CONTROLF,TEN;*244,CHOSPNUM
.
          MOVE      ZERO,LNKCOUNT           * Clear the Counter
          MOVE      CHOSPNUM,HOSPNUM
          MOVE      ZERO,HOSPCNT            * Clear the Counter
          COMPARE   ZERO,MRCNNOHS
          GOTO      UNLNK080 IF EQUAL
.
.         loop over each data directory
.
UNLNK050  ADD       ONE,HOSPCNT             * Increment Hospital Counter
          COMPARE   HOSPCNT,MRCNNOHS
          GOTO      UNLNK999 IF LESS        * Finished Number of Hospitals
.
          MOVE      HOSPCNT,HOSPNUM
.
UNLNK080  PACK      XXDPATH,SP20,SP20,SP20
          LOAD      XXDPATH,HOSPCNT,HDPATH1,HDPATH2,HDPATH3,HDPATH4,HDPATH5,HDPATH6
          IF        HOSPCNT <> ZERO
            MATCH     SP30,XXDPATH
            GOTO      UNLNK050 IF EQUAL       * no DPATH for hospital
          ENDIF
.
          MOVE      "patvisaf",OPFILE
          MOVE      TEN8,FILENUM
          CALL      OMDFN000                * open appropriate visit file
          BRANCH    EXIT,UNLNK700           * no visit file, try O/P file
.
          MOVE      ONE,COUNTR
          STORE     PATH,HOSPCNT,PATH1,PATH2,PATH3,PATH4,PATH5,PATH6
.
UNLNK090  COMPARE   HOSPCNT,COUNTR
          IF        @LESS
            LOAD      OLDPATH,COUNTR,PATH1,PATH2,PATH3,PATH4,PATH5,PATH6
            MATCH     OLDPATH,PATH
            IF        @EQUAL
....          GOTO      UNLNK050    * same path, dont loop thru again
            ENDIF
            ADD       ONE,COUNTR
            GOTO      UNLNK090
          ENDIF
.
          MOVE      ONE,FILENUM
          MOVE      "patmi1af",OPFILE
          CALL      OMDFN000                * open appropriate visit file
          BRANCH    EXIT,UNLNK700           * no visit file, try O/P file
.
          CALL      OPTWD000           * Open the Ward file
          BRANCH    EXIT,UNLNK700      * No Ward file, try O/P file
.
          CALL      OPTDC000                * doctor
          BRANCH    EXIT,UNLNK700           * no visit file, try O/P file
.
          MOVE      "outsitaf",OPFILE       * op site
          MOVE      FORTY,FILENUM
          CALL      OMDFN000
          BRANCH    EXIT,UNLNK700
.
          CALL      OAEDA000           * Open the A&E Details File
          BRANCH    EXIT,UNLNK700      * No A&E file, try O/P file
.
          CALL      OPNAHR00           * Open the Allied Health Referral File
          BRANCH    EXIT,UNLNK700      * No Allied Health file, try O/P file
.
          CALL      OPNAPC00           * Open Allied Health Problem Codes File
          BRANCH    EXIT,UNLNK700      * No AHP Codes file, try O/P file
.
          CALL      OPNPEV00           * Open the Patient Events File
          BRANCH    EXIT,UNLNK700      * No Patient Events file, try O/P file
.
          CALL      OPNPCD00           * Open the Patient Codes File
          BRANCH    EXIT,UNLNK700      * No Patient Codes file, try O/P file
.
          CALL      OREF0000           * Get Outpatient Referral Letters
          CALL      GBOK0000           * Get Future O/P Bookings
          CALL      IPBK0000           * Get Inpatient Bookings
          CALL      WAIT0000           * Get Waiting List Patients
.
          PACK      KEY24,URNUMBER,Z70
          CALL      RDSVISA2
UNLNK100  CALL      RDPVISA2
          BRANCH    OVRCD,UNLNK700
          MATCH     PVIURNO,URNUMBER         * Same U/R No.?
          GOTO      UNLNK700 IF NOT EQUAL
          MATCH     PTCNLEDT,PVIDATE
          GOTO      UNLNK700 IF LESS
.
          IF        LNKCOUNT>998
            GOTO      UNLNK800
          ENDIF
.
          BRANCH    PVITYPE,UNLNK110,UNLNK110,UNLNK110:
                            UNLNK100,UNLNK100,UNLNK110:
                            UNLNK100,UNLNK100,UNLNK110
          GOTO      UNLNK100
.
. ------- make sure the visit is not already linked ----------------
.
UNLNK110  PACK      KEY10,HOSPNUM,PVIBILL,SP10
          CALL      RDECLNK2
          IF        OVRCD = 0
            GOTO      UNLNK100
          ENDIF
.
          IF        PVITYPE=1
            PACK      KEY8,PVIBILL,SP70
            CALL      RDEMVIS1
            BRANCH    OVRCD,UNLNK100
.
            COMPARE   FOUR,EMVISTAT         * Cancelled ED visit
            GOTO      UNLNK100 IF EQUAL
          ENDIF
.
.    Get extra details for each Visit Type and Set VISITYPE Variable
.
          PERFORM   PVITYPE,MLAAE000,MLOUT000,MLINP000:
                            MLDUM000,MLDUM000,MLCOM000:
                            MLDUM000,MLDUM000,MLOTH000
.
.  Move Variables Here - VISITYPE ALREADY SET in above routiunes!!!!! 
.
          CALL      CLREVN00      * Clear Display Variables
.
          MOVE      PVIBILL,EVNTNUM      * Event Number
          MOVE      PVIDATE,EVNTDATE     * Event Date
          MOVE      PVIDOCT,EVNTDOC      * Doctor
          MOVE      SP70,EVNTHCP         * HCP
          MOVE      VISITYPE,EVNVTYPE    * Event Visit Type
          MOVE      DIAGNSIS,EVNTDIAG    * Event Diagnosis
          MOVE      HOSPNUM,EVNTHOSP     * Hospital Unique Code
          MOVE      HOSPCODE,EVNTHSPL    * Hospital Code   
.
          CALL      DISLNK00       * Display Unlinked Record 
          GOTO      UNLNK100       * Get next visit
.
UNLNK700  COMPARE   ZERO,MRCNNOHS
          GOTO      UNLNK050 IF NOT EQUAL   * Using Many Directories
          GOTO      UNLNK999
.
UNLNK800  WRITE     HTMLFILE;"<tr><td align=center colspan=6 bgcolor=##FF0000>":
                             "Too Many Unlinked Events Found":
                             "</td></tr>"
.
UNLNK999  GOTO      ENDUNLNK
.---------------------------------------------------------------------
.   Get the Future OP Bookings
.---------------------------------------------------------------------
GBOK0000  IF        OTCNNSID = 1
            MOVE      "out",OSTFILE   * Non Site Dependant ? control parameter! 
            PACK      OPFILE,OSTFILE,FILBOKA3   * Parameter for OPNFIL00
            GOTO      GBOK2000
          ENDIF
.
          MOVE      "outsitaf",OPFILE       * op site
          MOVE      FORTY1,FILENUM
          CALL      OMDFN000
          BRANCH    EXIT,GBOK9999
.
          PACK      SAVOSTF,SP70
          MOVE      SP6,KEY6
          CALL      RDSSITA3
GBOK1000  BRANCH    OTCNNSID,GBOK9999
          CALL      RDKSITA3
          BRANCH    OVRCD,GBOK9999          * end if file
.
          MATCH     OSTFILE,SAVOSTF
          GOTO      GBOK1000 IF EQUAL
          PACK      SAVOSTF,OSTFILE,SP70
.
          PACK      OPFILE,OSTFILE,FILBOKA3   * Parameter for OPNFIL00
.
GBOK2000  MOVE      SEVEN,FILENUM
          CALL      OMDFN000                * open bookings file
          BRANCH    EXIT,GBOK1000       * File not found
.
          CALL      OOTMA000            * O/P Clinic Master File
          BRANCH    EXIT,GBOK1000
.
          PACK      OPFILE,OSTFILE,FILDIAG1
          MOVE      "69",FILENUM
          CALL      OMDFN000                * open diagnosis file
          BRANCH    EXIT,GBOK1000
.
          PACK      OPFILE,OSTFILE,FILCLIA1
          MOVE      THIRTY6,FILENUM
          CALL      OMDFN000                * op clinic id - index 1
          BRANCH    EXIT,GBOK1000
.
          PACK      KEY36,URNUMBER,PTCNLEDT,SP30,SP10
          CALL      RDSBOKA3
GBOK3000  CALL      RDKBOKA3
          BRANCH    OVRCD,GBOK1000          * no more data
.
          MATCH     URNUMBER,OBAURNO
          GOTO      GBOK1000 IF NOT EQUAL   * no more data
.
          COMPARE   ONE,OBASTAT
          GOTO      GBOK3000 IF NOT EQUAL   * not booked
.
.
. ------- make sure the visit is not already linked ----------------
.
          PACK      KEY10,HOSPNUM,OBAOUTNO,SP10
          CALL      RDECLNK2
          IF        OVRCD = 0
            GOTO      GBOK3000
          ENDIF
.
          MOVE      SP3,OTMAHOSP            * Get Hospital Code
          PACK      KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT
          CALL      RDMASA1
          MOVE      OTMAHOSP,HOSPCODE       * Hospital Code Set 
.
          PACK      OPDIDESC,SP70           * Get the Diagnosis 
          PACK      KEY8,OBAOUTNO,SP70
          CALL      RDDIAG1
          MOVE      OPDIDESC,DIAGNSIS       * Daignosis Set
.
          MOVE      TEN2,VISITYPE        * "O/P Book  "
.
.  Move Variables Here
.
          CALL      CLREVN00      * Clear Display Variables
.
          MOVE      OBAOUTNO,EVNTNUM     * Event Number
          MOVE      OBADATE,EVNTDATE     * Event Date
          MOVE      DIAGNSIS,EVNTDIAG    * Event Diagnosis
          MOVE      VISITYPE,EVNVTYPE    * Event Visit Type
          MOVE      HOSPNUM,EVNTHOSP     * Hospital Unique Code
          MOVE      HOSPCODE,EVNTHSPL    * Hospital Code
.
          MOVE      SP70,EVNTDOC         * Doctor
          MOVE      SP70,EVNTHCP         * HCP
          MOVE      OBACLIN,PVIDOCT
          MOVE      OBASITE,PVISITE
          MOVE      OBADATE,PVIDATE
          CALL      ODOC0000             * Doctor/ Clinic
.
          CALL      DISLNK00             * Display Unlinked Record 
          GOTO      GBOK3000
.
GBOK9999  RETURN
.---------------------------------------------------------------------
.    Get the Outpatient Referrals which aren't booked
.---------------------------------------------------------------------
OREF0000  CALL      OOTRF000
          BRANCH    EXIT,OREF9999
.
          PACK      KEY24,URNUMBER,PTCNLEDT,SP30
          CALL      RSOTRFL2
OREF1000  CALL      RKOTRFL2
          BRANCH    OVRCD,OREF9999          * no more details
          MATCH     URNUMBER,OTRLURNO
          GOTO      OREF9999 IF NOT EQUAL   * no more details
.
. see if the referral is booked or not
.
          MATCH     SP8,OTRLBDTE
          GOTO      OREF1000 IF NOT EQUAL   * referral is booked
.
. ------- Do display if this is the Outpat external referral for this EOC ---
. ------- Don't display if this is an other external referral for this EOC ---
.
          COMPARE   ONE,OTRLTREF              Other referral ?
          GOTO      OREF1000 IF EQUAL         Yes
.
          MOVE      TEN6,VISITYPE
          MOVE      ECRFREFN,KEY8
          MATCH     OTRLOUTN,KEY8
          GOTO      OREF2000 IF EQUAL 
.
          MOVE      OTRLOUTN,KEY8
          CALL      CREF0000  * Checks if Referral is an External Referral (EOC)
          BRANCH    EXIT,OREF1000
          MOVE      TEN1,VISITYPE
.
. ------- make sure the referral is not already linked ----------------
.
OREF2000  PACK      KEY10,HOSPNUM,OTRLOUTN,SP10
          CALL      RDECLNK2
          IF        OVRCD = 0
            GOTO      OREF1000
          ENDIF
.
.  Move Variables Here - VISITYPE SET ABOVE!!!   
.
          CALL      CLREVN00      * Clear Display Variables
.
          MOVE      OTRLOUTN,EVNTNUM     * Event Number
          MOVE      OTRLDATE,EVNTDATE    * Event Date
          MOVE      OTRLDIAG,EVNTDIAG    * Event Diagnosis
          MOVE      SP70,EVNTDOC         * Doctor
          MOVE      OTRLRDOC,EVNTHCP     * HCP
          MOVE      VISITYPE,EVNVTYPE    * Event Visit Type
          MOVE      HOSPNUM,EVNTHOSP     * Hospital Unique Code
          MOVE      SP70,EVNTHSPL        * Hospital Code ????????????
          MOVE      OTRLDIAG,DIAGNSIS
.
          CALL      DISLNK00             * Display Unlinked Record 
          GOTO      OREF1000
.
OREF9999  RETURN
.---------------------------------------------------------------------
.      Get all the waiting list entries (unscheduled)
.---------------------------------------------------------------------
CREF0000  OPEN      EOCREFA1,"eocrefaf"
          OPEN      EOCREFA2,"eocrefaf"
          MOVE      ZERO,EXIT
.
          PACK      KEY13,ECRFURNO,DECRFEPN,SP70
          PACK      KEY15,OTRLURNO,CHOSPNUM,SP30
          CALL      RSECREF2
CREF1000  CALL      RKECREF2
          BRANCH    OVRCD,CREF9999
.
          MATCH     OTRLURNO,ECRFURNO
          GOTO      CREF9999 IF NOT EQUAL
.
          COMPARE   CHOSPNUM,ECRFHOSP
          GOTO      CREF9999 IF NOT EQUAL
.
          MOVE      ECRFREFN,KEY8
          MATCH     OTRLOUTN,KEY8
          GOTO      CREF1000 IF NOT EQUAL
.
          MOVE      ONE,EXIT
.
CREF9999  CALL      RDECREF1
          RETURN
.---------------------------------------------------------------------
.      Get all the waiting list entries (unscheduled)
.---------------------------------------------------------------------
WAIT0000  CALL      OWTWM000                * Open Wiating List File
          BRANCH    EXIT,WAIT9999           * No file
.
          PACK      KEY20,ONE,URNUMBER,SP70
          CALL      RDSTREA2
WAIT1000  CALL      RDKTREA2
          BRANCH    OVRCD,WAIT9999          * No more details
.
          COMPARE   ONE,WMSTAT
          GOTO      WAIT9999 IF NOT EQUAL   * No more details
.
          MATCH     URNUMBER,WMURNO
          GOTO      WAIT9999 IF NOT EQUAL   * No more details
.
          MATCH     PTCNLEDT,WMDATE1
          GOTO      WAIT1000 IF LESS
.
. ------- make sure the visit is not already linked ----------------
.
          PACK      KEY10,HOSPNUM,WMBOOK,SP10
          CALL      RDECLNK2
          IF        OVRCD = 0
            GOTO      WAIT1000
          ENDIF
.
          MOVE      NINE,VISITYPE        * "Waiting   "
.
.  Move Variables Here
.
          CALL      CLREVN00      * Clear Display Variables
.
          MOVE      WMBOOK,EVNTNUM       * Event Number
          MOVE      WMDATE1,EVNTDATE     * Event Date
          MOVE      WMREASON,EVNTDIAG    * Event Diagnosis
          MOVE      WMDOCTOR,EVNTDOC     * Doctor
          MOVE      SP70,EVNTHCP         * HCP
          MOVE      VISITYPE,EVNVTYPE    * Event Visit Type
          MOVE      HOSPNUM,EVNTHOSP     * Hospital Unique Code
          MOVE      WTWMIHSP,EVNTHSPL    * Hospital Code     
.
          CALL      DISLNK00             * Display Ulinked Record 
          GOTO      WAIT1000
.
WAIT9999  RETURN
.-----------------------------------------------------------------------
.         Get the Inpatient Bookings
.-----------------------------------------------------------------------
IPBK0000  CALL      OBKRC000           * Open Booking File
          BRANCH    EXIT,IPBK9999      * No File
.
          MOVE      "bokdiaaf",OPFILE
          MOVE      "68",FILENUM
          CALL      OMDFN000                * open appropriate visit file
.
          PACK      KEY16,URNUMBER,SP8
          CALL      RSBKREC6
IPBK1000  CALL      RKBKREC6
          BRANCH    OVRCD,IPBK9999          * no more details
.
          MATCH     URNUMBER,BKREURNO
          GOTO      IPBK9999 IF NOT EQUAL   * no more details
.
          COMPARE   ZERO,BKRESTAT
          GOTO      IPBK1000 IF NOT EQUAL   * only want bookings
.
          MATCH     PTCNLEDT,BKREEDAT
          GOTO      IPBK1000 IF LESS
.
. ------- make sure the visit is not already linked ----------------
.
          PACK      KEY10,HOSPNUM,BKREBOOK,SP10
          CALL      RDECLNK2
          IF        OVRCD = 0
            GOTO      IPBK1000
          ENDIF
.
.  Move Variables Here
.
          MOVE      SP3,PTWDHOSN         * Get the Hospital Code (from Ward)
          PACK      KEY6,BKREWARD,SP3
          CALL      RDWARD1
          MOVE      PTWDHOSN,HOSPCODE
.
          PACK      BKDIDES1,SP70        * Get the Diagnosis                   
          PACK      KEY18,BKREBOOK,SP70
          CALL      RSBKDIA1
          CALL      RKBKDIA1
          BRANCH    OVRCD,IPBK2000
          MATCH     BKREBOOK,BKDIBOOK
          IF        @EQUAL
            MOVE      BKDIDES1,DIAGNSIS    * Event Diagnosis
          ENDIF
.
IPBK2000  MOVE      TEN,VISITYPE         * "I/P Book  "
.
.  Move Variables Here
.
          CALL      CLREVN00      * Clear Display Variables
.
          MOVE      BKREBOOK,EVNTNUM     * Event Number
          MOVE      BKREEDAT,EVNTDATE    * Event Date
          MOVE      BKREADOC,EVNTDOC     * Doctor
          MOVE      SP70,EVNTHCP         * HCP
          MOVE      VISITYPE,EVNVTYPE    * Event Visit Type
          MOVE      DIAGNSIS,EVNTDIAG    * Event Diagnosis 
          MOVE      HOSPNUM,EVNTHOSP     * Hospital Unique Code
          MOVE      HOSPCODE,EVNTHSPL    * Hospital Code
.
          CALL      DISLNK00             * Display Unlinked Record 
          GOTO      IPBK1000
.
IPBK9999  RETURN
.----------------------------------------------------------------------
.  Writes the Unlinked record to the screen...
.----------------------------------------------------------------------
DISLNK00  UNPACK    EVNTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY11,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          LOAD      VTYPE,EVNVTYPE,MVEVN01,MVEVN02,MVEVN03,MVEVN04,MVEVN05:
                                   MVEVN06,MVEVN07,MVEVN08,MVEVN09,MVEVN10:
                                   MVEVN11,MVEVN12,MVEVN13,MVEVN14,MVEVN15:
                                   MVEVN16,MVEVN17,MVEVN18
.
          MATCH     SP70,EVNTHCP
          GOTO      DISLNK30 IF EQUAL
.
          MOVE      SP70,DOCTOR
          PACK      KEY10,EVNTHCP,SP70  * Get HCP Formatted name
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
            CLEAR     DOCTOR
            APPEND    DOCFNAME,DOCTOR
          ENDIF
          GOTO      DISLNK40
.
DISLNK30  COMPARE   TEN2,EVNVTYPE       * Outpatient booking
          GOTO      DISLNK40 IF EQUAL
.
          COMPARE   TWO,EVNVTYPE        * Outpatient attendance
          GOTO      DISLNK40 IF EQUAL
.
          MOVE      SP70,DOCTOR
          PACK      KEY6,EVNTDOC,SP70   * Get Doctor Formatted name
          CALL      RDDOCT1
          IF        OVRCD = 0
            CALL      DOCNAM00
            CLEAR     DOCTOR
            APPEND    DOCFNAME,DOCTOR
          ELSE
            MOVE      SP70,DOCTOR
          ENDIF
.
DISLNK40  OPEN      PATHSPA1,"pathspaf"
.
          PACK      KEY3,EVNTHSPL,SP70   * Get Hospital
          CALL      RDPTHSP1
          IF        OVRCD = 0
            MOVE      PTHSNAME,KEY25
          ELSE
            MOVE      SP70,KEY25
          ENDIF
.
          MOVE      "#"",KEY1
          PACK      UNLINK,KEY1,EVNTHOSP,EVNTNUM,KEY1,SP70
.
          WRITE     HTMLFILE;"<tr>":
                             "<td nowrap>":
                             "<img src=../images/patdet.gif hspace=4":
                             " border=0 align=absmiddle alt=#"",EVNTNUM,"#"":
                             " onClick='SelectVisit(#"",EVNVTYPE,"#",#"":
                             EVNTNUM,"#",#"",EVNTHOSP,"#")'>":
                             KEY11,"</td>":
                             "<td>",VTYPE,"</td><td>",DIAGNSIS,"</td>":
                             "<td>",DOCTOR,"</td><td>",KEY25,"</td>":
                             "<td align=center><input type=checkbox":
                             " name=unlkevnt value=",UNLINK,"></td>":
                             "</tr>" 
          ADD       ONE,LNKCOUNT
.  
DISLNK99  RETURN
.---------------------------------------------------------------------
.         Clear Display Variables              
.---------------------------------------------------------------------
CLREVN00  MOVE      SP70,EVNTNUM    * Event Number
          MOVE      SP70,EVNTDATE   * Event Date
          MOVE      SP70,EVNTDIAG   * Event Diagnosis
          MOVE      SP70,EVNTDOC    * Doctor/Clinic
          MOVE      SP70,EVNTHCP    * HCP
          MOVE      SP70,EVNTHOSP   * Hospital Unique ID
          MOVE      ZERO,EVNVTYPE   * Event Visit Type
          MOVE      SP70,EVNTHSPL   * Hospital Code
.
CLREVN99  RETURN
.---------------------------------------------------------------------
.         dummy routine for visits types 4,5
.---------------------------------------------------------------------
MLDUM000  MOVE      SP3,HOSPCODE 
          RETURN
.---------------------------------------------------------------------
.         Get The Diagnosis from the A&E Details File                        
.---------------------------------------------------------------------
MLAAE000  MOVE      SP3,HOSPCODE 
          MOVE      ONE,VISITYPE                 
.
          PACK      ADADIAG,SP70
          PACK      KEY8,PVIBILL,SP70
          CALL      RDDETA1
          MOVE      ADADIAG,DIAGNSIS
.
MLAAE999  RETURN
.---------------------------------------------------------------------
.         get the hospital code for outpatients                      
.         Returns : OVRCD = 1     invalid details
.---------------------------------------------------------------------
MLOUT000  MOVE      SP3,HOSPCODE 
          MOVE      TWO,VISITYPE                 * patient type
          MOVE      SP20,EVNTDOC
          MOVE      SP20,EVNTHCP
.
          MOVE      PVISITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,MLOUT999
.
          PACK      OPFILE,OSTFILE,FILBOKA2
          MOVE      "45",FILENUM
          CALL      OMDFN000                * open bookings file
          BRANCH    EXIT,MLOUT999
.
          PACK      OPFILE,OSTFILE,FILBOKA6
          MOVE      "77",FILENUM
          CALL      OMDFN000                * open bookings file
          BRANCH    EXIT,MLOUT999

          CALL      OOTMA000        * op clinic master
          BRANCH    EXIT,MLOUT999
.
          CALL      OOTBB000        * op booking B
          BRANCH    EXIT,MLOUT999
.
          PACK      OPFILE,OSTFILE,FILDIAG1
          MOVE      "69",FILENUM
          CALL      OMDFN000                * open diagnosis file
          BRANCH    EXIT,MLOUT999
.
          PACK      OPFILE,OSTFILE,FILCLIA1
          MOVE      THIRTY6,FILENUM
          CALL      OMDFN000                * op clinic id - index 1
          BRANCH    EXIT,MLOUT999
.
.         get the booking details
.
MLOUT050  MOVE      PVIBILL,KEY8
          CALL      RDBOKB1
          BRANCH    OVRCD,MLOUT999          * no details
.
          PACK      OPDIDESC,SP30,SP30
          PACK      KEY8,PVIBILL,SP10       * diagnosis
          CALL      RDDIAG1
          MOVE      OPDIDESC,DIAGNSIS
.
          PACK      KEY36,PVIBILL,SP70
          CALL      RDSBOKA6
          CALL      RDKBOKA6
          BRANCH    OVRCD,MLOUT999
.
          MATCH     DPVIBILL,DOBAOUTN
          GOTO      MLOUT999 IF NOT EQUAL
.
          PACK      KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT
          CALL      RDMASA1
          BRANCH    OVRCD,MLOUT999          * invalid clinic master
.
          MOVE      OTMAHOSP,HOSPCODE
          CALL      ODOC0000                * Doctor/ Clinic
.
MLOUT999  RETURN
.
.*********************************************************************
. ODOC0000 - get doctor/clinic description
.*********************************************************************
ODOC0000  MOVE      SP6,OCADOCT
          MOVE      SP70,DOCTOR
          MOVE      PVIDOCT,EVNTDOC         * Doctor
.
          PACK      KEY20,PVISITE,PVIDOCT,PVIDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     PVISITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     PVIDOCT,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      SP30,OCADESC
          ENDIF
          MOVE      OCADESC,DOCTOR     * Save the clinic description
          BRANCH    OVRCD,ODOC9999
.
          MATCH     SP6,OCADOCT
          IF        !@EQUAL
            PACK      KEY6,OCADOCT,SP70   * Get Doctor Formatted name
            CALL      RDDOCT1
            IF        OVRCD = 0
              CALL      DOCNAM00
              CLEAR     DOCTOR
              APPEND    DOCFNAME,DOCTOR
              MOVE      OCADOCT,EVNTDOC   * Doctor
            ELSE
              MOVE      SP70,DOCTOR
            ENDIF
          ENDIF
.
ODOC9999  RETURN
.---------------------------------------------------------------------
.         get the hospital code                                   
.         Returns : OVRCD = 1     invalid details
.---------------------------------------------------------------------
MLINP000  MOVE      SP10,HOSPCODE 
.
          MOVE      PVIBILL,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,MLINP999
.
          MOVE      ADIAG1,DIAGNSIS
.
          LOAD      VISITYPE,ASTAT,SEVEN,THREE,FIVE,SIX,FOUR
.
          IF        ASTAT = ONE
            PACK      KEY6,PTMIXWRD,SP10
          ELSE
            PACK      KEY6,AWARD,SP10
          ENDIF
          CALL      RDWARD1
          BRANCH    OVRCD,MLINP999
          MOVE      PTWDHOSN,HOSPCODE
.        
MLINP999  RETURN
.---------------------------------------------------------------------
.         get the hospital code for community health                 
.---------------------------------------------------------------------
MLCOM000  MOVE      SP3,HOSPCODE 
          MOVE      EIGHT,VISITYPE
          MOVE      SP20,DIAGNSIS
          MOVE      SP20,EVNTDOC
          MOVE      SP20,EVNTHCP
MLCOM999  RETURN
.---------------------------------------------------------------------
.         get the diagnosis and visit type for Other types
.         (Patient Events and Allied Health Referrals)
.---------------------------------------------------------------------
MLOTH000  MOVE      SP3,HOSPCODE
          MOVE      ZERO,VISITYPE
          MOVE      SP70,DIAGNSIS
.
          MATCH     " 1",PVISYST            * Is it Patient Event?
          GOTO      MLOTH100 IF EQUAL       * Yes, process Patient Event
.
          MATCH     " 2",PVISYST            * Is it Allied Health Referral?  
          GOTO      MLOTH200 IF EQUAL       * Yes, Process Allied Health Ref
          GOTO      MLOTH999
.
MLOTH100  MOVE      SP70,MVEVN17
          MOVE      TEN7,VISITYPE
          PACK      KEY8,PVIBILL
          CALL      RDPMEVT1
          BRANCH    OVRCD,MLOTH999
.
          MOVE      PMEVHOSN,HOSPCODE
          MOVE      PMEVPPRB,DIAGNSIS
          MATCH     SP3,PMEVEVNT
          IF        !@EQUAL
            MOVE      "EV",KEY2
            PACK      KEY5,KEY2,PMEVEVNT
            CALL      RDCODE1
            BRANCH    OVRCD,MLOTH999
            MOVE      TDESC,MVEVN17
          ENDIF
         
          GOTO      MLOTH999
.
MLOTH200  MOVE      TEN8,VISITYPE
          PACK      KEY8,PVIBILL
          CALL      RDALREF1
          BRANCH    OVRCD,MLOTH999
.
          MOVE      ALREHOSN,HOSPCODE
          PACK      KEY12,ALREDEPT,ALREPRO1
          CALL      RDALPRR1
          BRANCH    OVRCD,MLOTH999
          MOVE      ALPRDESC,DIAGNSIS
.
MLOTH999  RETURN
.---------------------------------------------------------------------
.    Open the ward file
.---------------------------------------------------------------------
OPTWD000  MOVE      "patwr1af",OPFILE
          MOVE      TEN5,FILENUM
          CALL      OMDFN000
.
OPTWD999  RETURN
.*********************************************************************
.         open the waiting list details file
.*********************************************************************
OWTWM000  MOVE      "wattr1af",OPFILE
          MOVE      SIXTY2,FILENUM
          CALL      OMDFN000
OWTWM999  RETURN
+
.---------------------------------------------------------------------
.    Open the A&E Details File
.---------------------------------------------------------------------
OAEDA000  MOVE      "aaede1af",OPFILE
          MOVE      FIVE,FILENUM
          CALL      OMDFN000
.
          MOVE      "emrvisaf",OPFILE
          MOVE      SEVENTY6,FILENUM
          CALL      OMDFN000
.
OAEDA999  RETURN
.*********************************************************************
.         open the outpatient referral file
.*********************************************************************
OOTRF000  MOVE      "outrf1af",OPFILE
          MOVE      SIXTY1,FILENUM
          CALL      OMDFN000
          MOVE      SIXTY5,FILENUM
          CALL      OMDFN000
OOTRF999  RETURN
+
.*********************************************************************
.         open the doctor file
.*********************************************************************
OPTDC000  MOVE      "patdo1af",OPFILE
          MOVE      THIRTY7,FILENUM
          CALL      OMDFN000
OPTDC999  RETURN
.*********************************************************************
.         open the required file
.*********************************************************************
OTRF0000  IF        FILENUM = 1
            CLOSE     PATMI1A1
            OPEN      PATMI1A1,PATH
          ENDIF
          IF        FILENUM = 5
            CLOSE     AAEDE1A1
            OPEN      AAEDE1A1,PATH
          ENDIF
          IF        FILENUM = 6
            CLOSE     OUTBOKA1
            OPEN      OUTBOKA1,PATH
          ENDIF
          IF        FILENUM = 7
            CLOSE     OUTBOKA3
            OPEN      OUTBOKA3,PATH
          ENDIF
          IF        FILENUM = 8
            CLOSE     OUTBB1A1
            OPEN      OUTBB1A1,PATH
          ENDIF
          IF        FILENUM = 15
            CLOSE     PATWR1A1
            OPEN      PATWR1A1,PATH
          ENDIF
          IF        FILENUM = 18
            CLOSE     PATVISA2
            OPEN      PATVISA2,PATH
          ENDIF
          IF        FILENUM = 20
            CLOSE     PATVISA1
            OPEN      PATVISA1,PATH
          ENDIF
          IF        FILENUM = 36
            CLOSE     OUTCLIA1
            OPEN      OUTCLIA1,PATH
          ENDIF
          IF        FILENUM = 37
            CLOSE     PATDO1A1
            OPEN      PATDO1A1,PATH
          ENDIF
          IF        FILENUM = 38
            CLOSE     OUTMA1A1
            OPEN      OUTMA1A1,PATH
          ENDIF
          IF        FILENUM = 40
            CLOSE     OUTSITA1
            OPEN      OUTSITA1,PATH
          ENDIF
          IF        FILENUM = 41
            CLOSE     OUTSITA3
            OPEN      OUTSITA3,PATH
          ENDIF
          IF        FILENUM = 45
            CLOSE     OUTBOKA2
            OPEN      OUTBOKA2,PATH
          ENDIF
          IF        FILENUM = 61
            CLOSE     OUTRF1A1
            OPEN      OUTRF1A1,PATH
          ENDIF
          IF        FILENUM = 62
            CLOSE     WATTR1A2
            OPEN      WATTR1A2,PATH
          ENDIF
          IF        FILENUM = 63
            CLOSE     BOKRC1A6
            OPEN      BOKRC1A6,PATH
          ENDIF
          IF        FILENUM = 65
            CLOSE     OUTRF1A2
            OPEN      OUTRF1A2,PATH
          ENDIF
          IF        FILENUM = 68
            CLOSE     BOKDIAA1
            OPEN      BOKDIAA1,PATH
          ENDIF
          IF        FILENUM = 69
            CLOSE     OUTDIAG1
            OPEN      OUTDIAG1,PATH
          ENDIF
          IF        FILENUM = 71
            CLOSE     BOKRC1A1
            OPEN      BOKRC1A1,PATH
          ENDIF
.
          IF        FILENUM = 72
            CLOSE     ALLREFA1
            OPEN      ALLREFA1,PATH
          ENDIF
.
          IF        FILENUM = 73
            CLOSE     ALLPRRA1
            OPEN      ALLPRRA1,PATH
          ENDIF
.
          IF        FILENUM = 74
            CLOSE     PMSEVTA1
            OPEN      PMSEVTA1,PATH
          ENDIF
.
          IF        FILENUM = 75
            CLOSE     PATCODE1
            OPEN      PATCODE1,PATH
          ENDIF
.
          IF        FILENUM = 76
            CLOSE     EMRVISA1
            OPEN      EMRVISA1,PATH
          ENDIF
.
          IF        FILENUM = 77
            CLOSE     OUTBOKA6
            OPEN      OUTBOKA6,PATH
          ENDIF
.      
OTRF9999  RETURN
.*********************************************************************
.         open the O/P booking file
.*********************************************************************
OOTBB000  PACK      OPFILE,OSTFILE,FILBB1A1
          MOVE      EIGHT,FILENUM
          CALL      OMDFN000
OOTBB999  RETURN
.*********************************************************************
.         open the outpatient master file
.*********************************************************************
OOTMA000  PACK      OPFILE,OSTFILE,FILMA1A1
          MOVE      THIRTY8,FILENUM
          CALL      OMDFN000
OOTMA999  RETURN
.
.*********************************************************************
.         open the booking file
.*********************************************************************
OBKRC000  MOVE      "bokrc1af",OPFILE
          MOVE      SIXTY3,FILENUM
          CALL      OMDFN000
OBKRC999  RETURN
.---------------------------------------------------------------------
.         Open the Allied Health Referral file
.---------------------------------------------------------------------
OPNAHR00  MOVE      "allrefaf",OPFILE
          MOVE      SEVENTY2,FILENUM
          CALL      OMDFN000
.
OPNAHR99  RETURN 
.---------------------------------------------------------------------
.         Open the Allied Health Problem Codes file
.---------------------------------------------------------------------
OPNAPC00  MOVE      "allprraf",OPFILE
          MOVE      SEVENTY3,FILENUM
          CALL      OMDFN000
.
OPNAPC99  RETURN
.---------------------------------------------------------------------
.         Open the Patient Events file
.---------------------------------------------------------------------
OPNPEV00  MOVE      "pmsevtaf",OPFILE
          MOVE      SEVENTY4,FILENUM
          CALL      OMDFN000
.
OPNPEV99  RETURN
.---------------------------------------------------------------------
.         Open the Patient Codes file
.---------------------------------------------------------------------
OPNPCD00  MOVE      "patcodes",OPFILE
          MOVE      SEVENTY5,FILENUM
          CALL      OMDFN000
.
OPNPCD99  RETURN
.----------------------------------------------------------------------
.
          INC       IBAOVRIO/INC
          INC       EOCREFIO/INC
          INC       PATMI1IO/INC
          INC       PATVISIO/INC
          INC       BOKRC1IO/INC
          INC       EMRVISIO/INC
          INC       OUTCLIIO/INC
          INC       OUTCTYIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTMA1IO/INC
          INC       OUTSITIO/INC
          INC       OUTRF1IO/INC
          INC       PATHOSIO/INC
          INC       PATHSPIO/INC
          INC       PATWR1IO/INC
          INC       WATTR1IO/INC
          INC       OUTDIAIO/INC
          INC       BOKDIAIO/INC
          INC       AAEDE1IO/INC
          INC       ALLPRRIO/INC
          INC       ALLREFIO/INC
          INC       PMSEVTIO/INC
          INC       PATCODIO/INC
          INC       PATDPATH
.
ENDUNLNK  ENDPROC
