.************************************************************************
.*  Procedure: DGCLICRG  -  Send PMI Registration Details               *
.*                                                                      *
.*  Author   : Steve Armstrong  (18/04/1997)                            *
.*                                                                      *
.*  Mods     :                                                          *
.*                                                                      *
.************************************************************************
.*  V11.04.01 09/01/2024 Thanh T           TSK 0936263                  *
.*            Added OPEN/CLOSE PMSQPXA1 index                           *
.************************************************************************
.*  V10.03.01 17/04/2013  Davin S          CAR 280425                   *
.*            Changed IBCNUCIS - value 1 or greater will write trigger  *
.*  V10.01.01 06/01/2011  Steve Armstrong   CAR 235689                  *
.*            Mods to check if message triggered by HL7RECVR (to        *
.*            prevent infinite message looping).                        *
.************************************************************************
.*  V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                  *
.*            Added setting of H7CITRID & H7CIINCL                      *
.************************************************************************
.*  V9.04.01  07/07/2005  Davin            CAR 64087                    *
.*            Mods to handle CIS interface.                             *
.*            Mods to write directly to holding tables instead of       *
.*            hl7bmtaf.                                                 *
.************************************************************************
.* V9.02.02   07/11/2001  Davin & MikeL                                 *
.*            Mods to use message holding file                          *
.*            Rename DGCLIREG to DGCLICRG                               *
.* V9.02.01   26/09/2001  Mike Laming                                   *
.*            Add PMI Master Xtrn.2 (PMSPX2) for WEB Based processing   *
.*             (using Compiler Option $$DBASE)                          *
.*                                                                      *
.*            08/07/1999  Nicole Harrington                             *
.*            Added IF statements around display for web programs       *
.*            22/12/1998  Davin                                         *
.*            Y2K mods                                                  *
.*            21/10/1998  Davin                                         *
.*            Mods for 507 changes                                      *
.*            13/10/1997  Steve Armstrong   WHL                         *
.*            Mods for new Master extension file variables              *
.*            (PTMXOPER - PTMXPOST).                                    *
.*            Also added nhi master file variables.                     *
.*            Also fixed birth date from ddmmyyyy to ddmmccyy.          *
.*                                                                      *
.************************************************************************
          DEFPROC   DGCLICRG
.
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
          INC       PMSQPTFD/INC
.
CISMFLAG  FORM      1             * CIS message flag
.                                    0 = don't send HL7 CIS message
.                                    1 = send HL7 CIS message
IBAMFLAG  FORM      1             * IBA proprietary message flag
.                                    0 = don't send IBA proprietary message
.                                    1 = send IBA proprietary message
MESSAGID  DIM       20
.
DGCLI000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,SEVENTY9;*242,PTCNDGRG
          READ      CONTROLF,HUND10;*112,PTCNA28M
.
          MOVE      ZERO,CISMFLAG                * set default interface flag
          MATCH     "1",IBCNUCIS                 * using CIS interface ?
          IF        !@LESS
            COMPARE   ONE,PTCNA28M               * yes - sending A28 ?
            IF        @EQUAL
              MOVE      ONE,CISMFLAG             * yes- set flag for CIS message
            ENDIF
          ENDIF
.
.         If this is PAS-CIS upload we are sending all current admissions to
.         synchronise the PAS and CIS databases, so don't send proprietary
.         message as this would double up on a CAD previously sent.
.
          MATCH     "CISUPLOD",PRGID
          IF        @EQUAL
            MOVE      ZERO,PTCNDGRG
          ENDIF
.
.         Check if we are sending any messages.
.
          IF        PTCNDGRG <> 1 & CISMFLAG <> 1
            GOTO      DGCLI999
          ENDIF
.
          MATCH     "HL7RECVR",PRGID             * trigger from receiver ?
          GOTO      DGCLI999 IF EQUAL            * yes - finished
.
.         We are sending IBA Proprietary and/or CIS HL7 messages, so
.         first open the relevant holding tables
.
          MOVE      ZERO,IBAMFLAG
          IF        PTCNDGRG = 1
            OPEN      HL7INB01,"hl7inbaf"
            MOVE      ONE,IBAMFLAG
          ENDIF
          IF        CISMFLAG = 1
            OPEN      HL7CISA1,"hl7cisaf"
          ENDIF
          OPEN      PMSQPTA1,"pmsqptaf"
          OPEN      PMSQPXA1,"pmsqpxaf"
.
          CALL      DGMESSID                    * Get Message ID, Date & Time
.
.         Write a record to pmsqptaf (holding patient details)
.
          MOVE      MESSAGID,PMQPMESI
          MOVE      SP8,PMQPOURN
          PACK      PMQPSPAR,SP30,SP30,SP20
          CALL      WRPMQPT1
.
          COMPARE   ONE,CISMFLAG                 * sending CIS HL7 A28 message?
          IF        @EQUAL
            MOVE      "A28",H7CIMETP
            MOVE      HL7TRGID,H7CITRID
            MOVE      HL7INCLD,H7CIINCL
            CALL      WRCIS000                   * yes - write hl7cisaf record
          ENDIF
.
          COMPARE   ONE,PTCNDGRG                 * sending proprietary message?
          IF        @EQUAL
            MOVE      "CRG",H7INMETP
            CALL      WRINB000                   * yes - write hl7inbaf record
          ENDIF
.
          CLOSE     HL7INB01
          CLOSE     HL7CISA1
          CLOSE     PMSQPTA1
          CLOSE     PMSQPXA1
.
DGCLI999  GOTO      DGCLIEND                        * end procedure
.
.         I/O Routines
.
          INC       BRHLCOMN
          INC       CLPMSQPX
.
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       IBAOVRIO/INC
          INC       PMSQPTIO/INC
.
DGCLIEND  ENDPROC
+
