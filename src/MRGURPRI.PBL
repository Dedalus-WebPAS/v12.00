.******************************************************************************
.* System         : WEB                                                       *
.*                                                                            *
.* Include Name   : MRGURPRI.PBL                                              *
.*                                                                            *
.* Function       : Merge a U/R for all PRI files                             *
.*                  files which contain a U/R Number.                         *
.*                                                                            *
.* Author         : J.Tan    06/11/2002                                       *
.*                                                                            *
.* Parameters     : OLDURNO            the old urnumber                       *
.*                  NEWURNO            the new urnumber                       *
.*                                                                            *
.* Modifications  :                                                           *
.*       V10.14.01 21/03/2019  J.Tan            TSK 0857392                   *
.*                 Changed RCP Transaction count from DIM3 to DIM5            *
.*       V10.13.01 14/08/2018  J.Tan          TSK 0848506                     *
.*                 Changed PP Doctor from DIM6 to DIM10                       *
.*       V10.12.01 27/04/2018  J.Tan        Task 0845996                      *
.*                 Mod to update CMDEUDAT,CMDEUTIM and CMDEUUID               *
.*       V10.11.01 14/12/2017  Ania P        TSK 0849648                      *
.*                 Fixed I * U on UPPRFA2 call                                *
.*       V10.08.02 17/06/2016  Peter Vela    TSK 0820868                      *
.*                 Changed SAVKEY21 to SAVKEY27 in FXPHDB10                   *
.*       V10.08.01 18/04/2016  Peter Vela    CAR 0294177                      *
.*                 Changed read to prihdb to KEY27                            *
.*       V10.00.02 23/08/2010  J.Tan        CAR 228752                        *
.*                 Mods update Credit notes files (pricnoaf & prifciaf)       *
.*       V10.00.01 19/04/2010  J.Tan        CAR 219670                        *
.*                 Mods to set date/time updated to rcpdteaf file             *
.*       V9.10.02  23/05/2008 J.Tan      CAR 169149                           *
.*                 Added Date and Time invoice created/updated                *
.*       V9.10.01  12/05/2008  J.Tan     CAR 162313                           *
.*                 Added Deposit Status to compdeaf file                      *
.*       22/08/2006 V9.06.01 Jill Habasque CAR 109594                         *
.*                  Added save of invoice number in FXID0000                  *
.*       05/07/2004 V9.03.01 Lina Vo    CAR 50811                             *
.*                  Replaced pridepaf with comdepaf in FXCDEP00.              *
.*                  Added FXRDTE000.                                          *
.*                  V9.02.03 J.Habasque SRF 22697                             *
.*                  Added priaudaf, priauiaf and priauraf                     *
.*                  J.Tan - Fixed to add pridtr records of OLD UR to New UR   *
.*                  V9.02.02 J.Tan                                            *
.*                  Fixed to add prihtr record of OLD UR to New UR            *
.*                  V9.02.01 J.Tan                                            *
.*                  Fixed Updating prihtr file                                *
.******************************************************************************
.
          DEFPROC   MRGURPRI
.
SAVUNIQ   FORM      8
SAVKEY18  DIM       18
SAVKEY21  DIM       21
SAVKEY22  DIM       22
SAVKEY27  DIM       27
SAVKEY62  DIM       62
SAVINVN   DIM       8
Z3        INIT      "zzz"
.
          INC       COMDEPFD/INC
          INC       PRIAUDFD/INC
          INC       PRIAUIFD/INC
          INC       PRIAURFD/INC
          INC       PRICMNFD/INC
          INC       PRIFACFD/INC
          INC       PRIHDBFD/INC
          INC       PRIDTRFD/INC
          INC       PRIINVFD/INC
          INC       PRIHREFD/INC
          INC       PRIHTRFD/INC
          INC       PRIHISFD/INC
          INC       PRIHOLFD/INC
          INC       PRILHIFD/INC
          INC       PRIJRNFD/INC
          INC       RCPDTEFD/INC
          INC       PRICNOFD/INC
          INC       PRIFCIFD/INC
.
          DISPLAY   *P1:24,*EL,"Fixing PRI Files."
.
          CALL      FXPCMN00
          CALL      FXPFAC00
          CALL      FXPHDB00
          CALL      FXPHIS00
	  CALL      FXRDTE00
          CALL      FXCDEP00
          CALL      FXPHOL00
          CALL      FXPREM00
          CALL      FXPINV00
          CALL      FXPDTR00
          CALL      FXPCDA00
          CALL      FXPINA00
          CALL      FXPDRA00
          CALL      FXCRDN00
.
          GOTO      MRGPRI99
+
.*********************************************************************
.        Subroutine to process Private Practice comments file
.*********************************************************************
FXPCMN00  MOVE      ZERO,FORM3
          MOVE      ZERO,EXIT
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PRICMNT1,"pricmntf"
          TRAPCLR   IO
          BRANCH    OVRCD,FXPCMN99
          DISPLAY   *P71:24,*V2LON,"pricmntf"
.
.         Check if the new U/R has got any comments
.
          PACK      KEY13,NEWURNO,SP1,ONE,Z3
          CALL      RSPRCM1
          CALL      RPPRCM1
          BRANCH    OVRCD,FXPCMN10       * no existing comments for new U/R
          MATCH     NEWURNO,PRCMDEBT
          GOTO      FXPCMN10 IF NOT EQUAL
.
          MOVE      ONE,EXIT
          MOVE      PRCMCLNO,FORM3
          ADD       ONE,FORM3
.
.        Loop over the comments file and update each record
.        Move old U/R comments to the end of the new U/R comments
.
FXPCMN10  PACK      KEY13,OLDURNO,SP1,ONE,SP10
          CALL      RSPRCM1
          CALL      RKPRCM1
          BRANCH    OVRCD OF FXPCMN90
.
          MATCH     PRCMDEBT,OLDURNO
          GOTO      FXPCMN90 IF NOT EQUAL
.
          DISPLAY   *P30:24,*EL,"Comments : ",*V2LON,PRCMDEBT,PRCMCLNO;
.
          PACK      PRCMDEBT,NEWURNO
          IF        EXIT = 0
            CALL      UPPRCM1
          ELSE
            PACK      KEY13,PRCMDEBT,SP1,ONE,FORM3
            CALL      WRPRCM1
            ADD       ONE,FORM3
.
            PACK      KEY13,OLDURNO,SP1,ONE,PRCMCLNO
            CALL      DEPRCM1
          ENDIF
          GOTO      FXPCMN10
.
FXPCMN90  CLOSE     PRICMNT1
FXPCMN99  RETURN
+
.        =========================================================
.        Subroutine to process Private Practice future action file
.        =========================================================
FXPFAC00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PRIFACT1,"prifactf"
          OPEN      PRIFACT2,"prifactf"
          TRAPCLR   IO
          BRANCH    OVRCD,FXPFAC99
          DISPLAY   *P71:24,*V2LON,"prifactf"
.
.        Loop over the future action file and update each record
.
FXPFAC10  PACK      KEY18,OLDURNO,SP1,ONE,SP10
          CALL      RSPRFA2
          CALL      RKPRFA2
          BRANCH    OVRCD OF FXPFAC90
.
          MATCH     PRFADEBT,OLDURNO
          GOTO      FXPFAC90 IF NOT EQUAL
.
          DISPLAY   *P30:24,*EL,"Action : ",*V2LON,PRFADATE;
.
          PACK      SAVKEY18,PRFADATE,PRFADEBT,DPRFASCN
          PACK      KEY18,NEWURNO,DPRFASCN,PRFADATE
          CALL      RDPRFA2
          IF        OVRCD=1
            UNPACK    SAVKEY18,PRFADATE,PRFADEBT,DPRFASCN
            PACK      KEY18,PRFADEBT,DPRFASCN,PRFADATE
            CALL      RDPRFA2
            PACK      PRFADEBT,NEWURNO
            CALL      UPPRFA2
          ELSE
            PACK      KEY18,SAVKEY18
            CALL      DEPRFA1
          ENDIF
.
          GOTO      FXPFAC10
.
FXPFAC90  CLOSE     PRIFACT2
FXPFAC99  RETURN
+
.        ==========================================================
.        Subroutine to process Private Practice holding header file
.        ==========================================================
FXPHDB00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PRIHDBT1,"prihdbtf"
          TRAPCLR   IO
          BRANCH    OVRCD,FXPHDB99
          DISPLAY   *P71:24,*V2LON,"prihdbtf"
.
.        Loop over the holding header file and update each record
.
FXPHDB10  PACK      KEY27,OLDURNO,SP1,ONE,SP20
          CALL      RSPRHD1
          CALL      RKPRHD1
          BRANCH    OVRCD OF FXPHDB90
.
          MATCH     PRHDDEBT,OLDURNO
          GOTO      FXPHDB90 IF NOT EQUAL
.
          DISPLAY   *P30:24,*EL,"Header : ",*V2LON,PRHDDEBT;
.
.------ if the same header details exist with the new U/R number, we can't ---
.------ update as this would produce a duplicate key. ------
.------ What we need to do is delete the old header record and update all -----
.------ the old unique identifier details with the new unique identifier ------
.
          PACK      SAVKEY27,PRHDDEBT,PRHDSCAN,PRHDCLAM,PRHDACCD,PRHDHFND
          MOVE      PRHDUNIQ,SAVUNIQ
          PACK      KEY27,NEWURNO,PRHDSCAN,PRHDCLAM,PRHDACCD,PRHDHFND
          CALL      RDPRHD1                 * read the header file
          IF        OVRCD = 1
            MOVE      SAVKEY27,KEY27
            CALL      RDPRHD1               * read the header file
            PACK      PRHDDEBT,NEWURNO
            CALL      UPPRHD1               * update the header file as usual
            GOTO      FXPHDB10
          ELSE
            CALL      FXID0000              * fix this identifier
            MOVE      SAVKEY27,KEY27
            CALL      DEPRHD1               * delete the old header record
            GOTO      FXPHDB10
          ENDIF
.
FXPHDB90  CLOSE     PRIHDBT1
FXPHDB99  RETURN
+
.*********************************************************************
.*                             FXID0000                              *
.*     This routine fixes the problem where the old header record    *
.*     we are updating has the same claim details as the new one     *
.*     which already exists. Basically we take the new identifier    *
.*     and update all the old records with this identifier. This     *
.*     looks like it may become a problem for the PRIHREFD file.     *
.*     If it does then one set of referral details will need to      *
.*     be wiped. This seems to be the only solution.                 *
.*********************************************************************
FXID0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PRIDTRA1,"pridtraf"
          OPEN      PRIINVO3,"priinvof"
          OPEN      PRIHREF1,"prihreff"
          OPEN      PRIHTRA1,"prihtraf"
          TRAPCLR   IO
          BRANCH    OVRCD,FXID9000
          DISPLAY   *P71:24,*V2LON,"pridtraf"
.
.------ fix up the DTR file ------
.
FXID0100  PACK      KEY22,SAVUNIQ,SP20
          CALL      RSPRDT1                 * position on the DTR file
          CALL      RKPRDT1
          BRANCH    OVRCD OF FXID1000
.
          COMPARE   PRDTUNIQ,SAVUNIQ        * compare unique identifiers
          GOTO      FXID1000 IF NOT EQUAL
.
          PACK      SAVINVN,PRDTINVN
          PACK      SAVKEY22,PRDTUNIQ,PRDTINVN,PRDTTRAN
.
          MOVE      PRHDUNIQ,PRDTUNIQ       * New unique number
          PACK      KEY22,PRDTUNIQ,PRDTINVN,Z70
          CALL      RSPRDT1                 * read last record of new uniq
          CALL      RPPRDT1                 * to get the last trans.number
          BRANCH    OVRCD,FXID0400
          COMPARE   PRDTUNIQ,PRHDUNIQ
          GOTO      FXID0400 IF NOT EQUAL   * no record for new unique
.
          MATCH     PRDTINVN,SAVINVN 
          GOTO      FXID0400 IF NOT EQUAL * no record for unique/inv combination
.
          MOVE      PRDTTRAN,FORM3
          ADD       ONE,FORM3
.
          PACK      KEY22,SAVKEY22,SP70     * read old uniq key
          CALL      RDPRDT1
          BRANCH    OVRCD,FXID0100
          MOVE      FORM3,PRDTTRAN
          MOVE      PRHDUNIQ,PRDTUNIQ
          GOTO      FXID0500
.
FXID0400  PACK      KEY22,SAVKEY22,SP70     * old uniq number
          CALL      RDPRDT1
          BRANCH    OVRCD,FXID0100
          MOVE      PRHDUNIQ,PRDTUNIQ
.
FXID0500  CALL      UPPRDT1               * update the DTR file
          GOTO      FXID0100
.
.------ fix up the invoice file ------
.
FXID1000  PACK      KEY18,OLDURNO,SP1,ONE,SP10
          CALL      RSPRIN3
.
.------ read through the invoice file ------
.
FXID1100  CALL      RKPRIN3
          BRANCH    OVRCD OF FXID2000
.
          MATCH     PRINDEBT,OLDURNO          * match U/R numbers
          GOTO      FXID2000 IF NOT EQUAL
.
          COMPARE   ONE,PRINSCAN            * compare scan flags
          GOTO      FXID2000 IF NOT EQUAL
.
          COMPARE   PRINUNIQ,SAVUNIQ        * compare unique identifiers
          GOTO      FXID1100 IF NOT EQUAL
.
          MOVE      PRHDUNIQ,PRINUNIQ
.
          PACK      PRINUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PRINUDAT             * date updated
          CLOCK     TIME,PRINUTIM             * time updated
          MOVE      USERID,PRINUUID           * web user id
          CALL      UPPRIN3                 * update the invoice file
.
          GOTO      FXID1100
.
.------ read through the referral file ------
.
FXID2000  PACK      KEY27,SAVUNIQ,SP20
          CALL      RSPRHR1
          CALL      RKPRHR1
          BRANCH    OVRCD,FXID3000
.
          COMPARE   SAVUNIQ,PRHRUNIQ        * compare unique identifiers
          GOTO      FXID3000 IF NOT EQUAL
.
.------ see if the same record exists with the new details ------
.
          PACK      SAVKEY27,PRHRUNIQ,PRHRPRAC,PRHRDOCT,PRHRPIND
          PACK      KEY27,PRHDUNIQ,PRHRPRAC,PRHRDOCT,PRHRPIND
          CALL      RDPRHR1                 * read the referral file
          IF        OVRCD = 1
            MOVE      SAVKEY27,KEY27
            CALL      RDPRHR1               * read the referral file
.
            MOVE      PRHDUNIQ,PRHRUNIQ
.
            CALL      UPPRHR1               * update the referral file as 
.                                             usual
            GOTO      FXID2000
          ELSE
            MOVE      SAVKEY27,KEY27
.
            CALL      DEPRHR1               * delete the old record
.
            GOTO      FXID2000
          ENDIF
.
.------ read through transaction file and add trans.from old UR to new U/R------
.
FXID3000  PACK      KEY62,SAVUNIQ,SP70      * old uniq number
          CALL      RSPRHT1
          CALL      RKPRHT1
          BRANCH    OVRCD,FXID9000
.
          COMPARE   SAVUNIQ,PRHTUNIQ        * compare unique identifiers
          GOTO      FXID9000 IF NOT EQUAL
.
          PACK      SAVKEY62,PRHTUNIQ,PRHTPRAC,PRHTDOCT:
                          PRHTPIND,PRHTREFD,PRHTRDAT:
                          PRHTFLAG,PRHTITMN,PRHTSUBN,PRHTNUMB
.
          MOVE      PRHDUNIQ,PRHTUNIQ       * new unique number
          PACK      KEY62,PRHTUNIQ,PRHTPRAC,PRHTDOCT:
                          PRHTPIND,PRHTREFD,PRHTRDAT:
                          PRHTFLAG,PRHTITMN,PRHTSUBN,Z70
          CALL      RSPRHT1               * read last record of new uniq
          CALL      RPPRHT1               * to get the last trans.number
          BRANCH    OVRCD,FXID4000
          COMPARE   PRHTUNIQ,PRHDUNIQ
          GOTO      FXID4000 IF NOT EQUAL   * no record for new unique
.
          MOVE      PRHTNUMB,FORM3
          ADD       ONE,FORM3
          PACK      KEY62,SAVKEY62,SP70   * read old uniq key
          CALL      RDPRHT1
          BRANCH    OVRCD,FXID3000
.
          MOVE      FORM3,PRHTNUMB
          MOVE      PRHDUNIQ,PRHTUNIQ
          GOTO      FXID5000
.
FXID4000  PACK      KEY62,SAVKEY62,SP70      * old uniq number
          CALL      RDPRHT1
          BRANCH    OVRCD,FXID3000
          MOVE      PRHDUNIQ,PRHTUNIQ
.
FXID5000  CALL      UPPRHT1                 * update the transaction file
          GOTO      FXID3000
.
FXID9000  CLOSE     PRIDTRA1
          CLOSE     PRIINVO1
          CLOSE     PRIHREF1
          CLOSE     PRIHTRA1
FXID9999  RETURN
+
.        ========================================================
.        Subroutine to process Private Practice test history file
.        ========================================================
FXPHIS00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PRITHIS1,"prithisf"
          TRAPCLR   IO
          BRANCH    OVRCD,FXPHIS99
          DISPLAY   *P71:24,*V2LON,"prithisf"
.
.        Loop over the test history file and update each record
.
FXPHIS10  PACK      KEY37,OLDURNO,SP1,ONE,SP20,SP10
          CALL      RSPRTH1
          CALL      RKPRTH1
          BRANCH    OVRCD OF FXPHIS90
.
          MATCH     PRTHDEBT,OLDURNO
          GOTO      FXPHIS90 IF NOT EQUAL
.
          DISPLAY   *P30:24,*EL,"Test History : ",*V2LON,PRTHDEBT;
.
          PACK      PRTHDEBT,NEWURNO
          CALL      UPPRTH1
          GOTO      FXPHIS10
.
FXPHIS90  CLOSE     PRITHIS1
FXPHIS99  RETURN
+
.        ========================================================
.        Subroutine to process Receipt Details file               
.        ========================================================
FXRDTE00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RCPDTEA5,"rcpdteaf"
          TRAPCLR   IO
          BRANCH    OVRCD,FXRDTE99
          DISPLAY   *P71:24,*V2LON,"rcpdteaf"
.
.        Loop over the receipt detail file and update each record
.
FXRDTE10  PACK      KEY25,OLDURNO,SP30
          CALL      RSRCDTE5
          CALL      RKRCDTE5
          BRANCH    OVRCD OF FXRDTE90
.
          MATCH     RCDTURNO,OLDURNO
          GOTO      FXRDTE90 IF NOT EQUAL
.
          DISPLAY   *P30:24,*EL,"Holding : ",*V2LON,RCDTURNO;
.
          PACK      RCDTURNO,NEWURNO
          PACK      RCDTUDAT,CCC,CYY,CMM,CDD
          REP       " 0",RCDTUDAT           * date updated
          CLOCK     TIME,RCDTUTIM           * time updated
          MOVE      USERID,RCDTUUID         * web user id
          CALL      UPRCDTE5
          GOTO      FXRDTE10
.
FXRDTE90  CLOSE     RCPDTEA5
FXRDTE99  RETURN
+
.        ========================================================
.        Subroutine to process Private Practice Cash deposit file
.        ========================================================
FXCDEP00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      COMDEPA4,"comdepaf"
          TRAPCLR   IO
          BRANCH    OVRCD,FXCDEP99
          DISPLAY   *P71:24,*V2LON,"comdepaf"
.
.        Loop over the cash deposit file and update each record
.
FXCDEP10  MOVE      "0",CMDESTAT
          PACK      KEY33,CMDESTAT,OLDURNO,ONE,SP30
          CALL      RSCMDEP4
          CALL      RKCMDEP4
          BRANCH    OVRCD OF FXCDEP90
.
          MATCH     "0",CMDESTAT             * Deposit held?
          GOTO      FXCDEP90 IF NOT EQUAL
          MATCH     CMDEURNO,OLDURNO
          GOTO      FXCDEP90 IF NOT EQUAL
.
          DISPLAY   *P30:24,*EL,"Holding : ",*V2LON,CMDEURNO;
.
          PACK      CMDEURNO,NEWURNO
.
          PACK      CMDEUDAT,CCC,CYY,CMM,CDD
          REP       " 0",CMDEUDAT           * date updated
          CLOCK     TIME,CMDEUTIM           * time updated
          MOVE      USERID,CMDEUUID         * web user id
          CALL      UPCMDEP4
.
          GOTO      FXCDEP10
.
FXCDEP90  CLOSE     COMDEPA4
FXCDEP99  RETURN
+
.        ===================================================
.        Subroutine to process Private Practice holding file
.        ===================================================
FXPHOL00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PRIHOLA1,"priholdf"
          TRAPCLR   IO
          BRANCH    OVRCD,FXPHOL99
          DISPLAY   *P71:24,*V2LON,"priholdf"
.
.        Loop over the holding file and update each record
.
FXPHOL10  PACK      KEY10,OLDURNO,SP1,ONE,SP10
          CALL      RSPRHO1
          CALL      RKPRHO1
          BRANCH    OVRCD OF FXPHOL90
.
          MATCH     PRHODEBT,OLDURNO
          GOTO      FXPHOL90 IF NOT EQUAL
.
          DISPLAY   *P30:24,*EL,"Holding : ",*V2LON,PRHODEBT;
.
          PACK      PRHODEBT,NEWURNO
          CALL      UPPRHO1
          GOTO      FXPHOL10
.
FXPHOL90  CLOSE     PRIHOLA1
FXPHOL99  RETURN
+
.        ====================================================
.        Subroutine to process Private Practice reminder file
.        ====================================================
FXPREM00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PRILHIS1,"prilhisf"
          TRAPCLR   IO
          BRANCH    OVRCD,FXPREM99
          DISPLAY   *P71:24,*V2LON,"prilhisf"
.
.        Loop over the reminder file and update each record
.
FXPREM10  PACK      KEY21,OLDURNO,SP1,ONE,SP10
          CALL      RSPRLH1
          CALL      RKPRLH1
          BRANCH    OVRCD OF FXPREM90
.
          MATCH     PRLHDEBT,OLDURNO
          GOTO      FXPREM90 IF NOT EQUAL
.
          DISPLAY   *P30:24,*EL,"Reminder : ",*V2LON,PRLHDEBT;
.
          PACK      PRLHDEBT,NEWURNO
          CALL      UPPRLH1
          GOTO      FXPREM10
.
FXPREM90  CLOSE     PRILHIS1
FXPREM99  RETURN
+
.        ===================================================
.        Subroutine to process Private Practice invoice file
.        ===================================================
FXPINV00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PRIINVO3,"priinvof"
          TRAPCLR   IO
          BRANCH    OVRCD,FXPINV99
          DISPLAY   *P71:24,*V2LON,"priinvof"
.
.        Loop over the reminder file and update each record
.
FXPINV10  PACK      KEY18,OLDURNO,SP1,ONE,SP10
          CALL      RSPRIN3
          CALL      RKPRIN3
          BRANCH    OVRCD OF FXPINV90
.
          MATCH     PRINDEBT,OLDURNO
          GOTO      FXPINV90 IF NOT EQUAL
.
          DISPLAY   *P30:24,*EL,"Invoice : ",*V2LON,PRINNUMB;
.
          PACK      PRINDEBT,NEWURNO
          PACK      PRINUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PRINUDAT             * date updated
          CLOCK     TIME,PRINUTIM             * time updated
          MOVE      USERID,PRINUUID           * web user id
          CALL      UPPRIN3
          GOTO      FXPINV10
.
FXPINV90  CLOSE     PRIINVO3
FXPINV99  RETURN
+
.        =======================================================
.        Subroutine to process Private Practice transaction file
.        =======================================================
FXPDTR00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PRIDTRA3,"pridtraf"
          OPEN      PRIJRNL1,"prijrnlf"
          TRAPCLR   IO
          BRANCH    OVRCD,FXPDTR90
          DISPLAY   *P71:24,*V2LON,"pridtraf"
.
.        Loop over the debtors transaction file and update each record
.
FXPDTR10  PACK      KEY24,OLDURNO,SP1,ONE,SP20
          CALL      RSPRDT3
          CALL      RKPRDT3
          BRANCH    OVRCD OF FXPDTR90
.
          MATCH     PRDTDEBT,OLDURNO
          GOTO      FXPDTR90 IF NOT EQUAL
.
          DISPLAY   *P30:24,*EL,"Transaction : ",*V2LON,PRDTINVN,PRDTITMN;
.
          PACK      PRDTDEBT,NEWURNO
          CALL      UPPRDT3
.
.        If this is a journal, then update the journals file as well
.
          COMPARE   THREE,PRDTRTYP
          CALL      FXPJRN00 IF EQUAL
          GOTO      FXPDTR10
.
FXPDTR90  CLOSE     PRIDTRA3
          CLOSE     PRIJRNL1
FXPDTR99  RETURN
+
.        =======================================================
.        Subroutine to fix up the Private Practice journals file
.        =======================================================
FXPJRN00  PACK      KEY35,PRDTTTYP,PRDTSDAT,OLDURNO,PRDTSCAN,PRDTTRAN,PRDTINVN
          CALL      RDPRJR1 
          BRANCH    OVRCD OF ENDJRN 
.
          PACK      PRJRDEBT,NEWURNO
          CALL      UPPRJR1
          DISPLAY   *P30:24,*EL,"Journal : ",*V2LON,PRDTTTYP,PRDTSDAT;
.
ENDJRN    RETURN
+
.        ===================================================
.        Subroutine to process Private Practice cash deposit audit
.        ===================================================
FXPCDA00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PRIAUDA1,"priaudaf"
          TRAPCLR   IO
          BRANCH    OVRCD,FXPCDA99
          DISPLAY   *P71:24,*V2LON,"priaudaf"
.
.        Loop over the reminder file and update each record
.
FXPCDA10  PACK      KEY31,OLDURNO,SP1,ONE,SP70
          CALL      RSPRAD1
          CALL      RKPRAD1
          BRANCH    OVRCD OF FXPCDA90
.
          MATCH     PRADDEBT,OLDURNO
          GOTO      FXPCDA90 IF NOT EQUAL
.
          DISPLAY   *P30:24,*EL,"Receipt Number : ",*V2LON,PRADRECP;
.
          PACK      PRADDEBT,NEWURNO
          CALL      UPPRAD1
          GOTO      FXPCDA10
.
FXPCDA90  CLOSE     PRIAUDA1
FXPCDA99  RETURN
+
.        ===================================================
.        Subroutine to process Private Practice cash invoice audit
.        ===================================================
FXPINA00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PRIAUIA1,"priauiaf"
          TRAPCLR   IO
          BRANCH    OVRCD,FXPINA99
          DISPLAY   *P71:24,*V2LON,"priaudaf"
.
.        Loop over the reminder file and update each record
.
FXPINA10  PACK      KEY18,OLDURNO,SP1,ONE,SP70
          CALL      RSPRAI1
          CALL      RKPRAI1
          BRANCH    OVRCD OF FXPINA90
.
          MATCH     PRAIDEBT,OLDURNO
          GOTO      FXPINA90 IF NOT EQUAL
.
          DISPLAY   *P30:24,*EL,"Invoice Number : ",*V2LON,PRAIINVN;
.
          PACK      PRAIDEBT,NEWURNO
          CALL      UPPRAI1
          GOTO      FXPINA10
.
FXPINA90  CLOSE     PRIAUIA1
FXPINA99  RETURN
+
.        ===================================================
.        Subroutine to process Private Practice deposits refunded audit
.        ===================================================
FXPDRA00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PRIAURA1,"priauraf"
          TRAPCLR   IO
          BRANCH    OVRCD,FXPDRA99
          DISPLAY   *P71:24,*V2LON,"priauraf"
.
.        Loop over the reminder file and update each record
.
FXPDRA10  PACK      KEY13,OLDURNO,SP1,ONE,SP70
          CALL      RSPRAR1
          CALL      RKPRAR1
          BRANCH    OVRCD OF FXPDRA90
.
          MATCH     PRARDEBT,OLDURNO
          GOTO      FXPDRA90 IF NOT EQUAL
.
          DISPLAY   *P30:24,*EL,"Receipt Number : ",*V2LON,PRARRECT;
.
          PACK      PRARDEBT,NEWURNO
          CALL      UPPRAR1
          GOTO      FXPDRA10
.
FXPDRA90  CLOSE     PRIAURA1
FXPDRA99  RETURN
+
.        ===================================================
.        Subroutine to process Private Practice credit notes
.        ===================================================
FXCRDN00  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PRICNOA2,"pricnoaf"
          TRAPCLR   IO
          BRANCH    OVRCD,FXCRDN50
.
          DISPLAY   *P71:24,*V2LON,"pricnoaf"
.
.        Loop over the reminder file and update each record
.
FXCRDN10  PACK      KEY18,OLDURNO,SP70
          CALL      RSPRCNO2
          CALL      RKPRCNO2
          BRANCH    OVRCD OF FXCRDN20
.
          MATCH     PRCODEBT,OLDURNO
          GOTO      FXCRDN20 IF NOT EQUAL
.
          DISPLAY   *P30:24,*EL,"Invoice Number : ",*V2LON,PRCOINVN;
.
          PACK      PRCODEBT,NEWURNO
          CALL      UPPRCNO2
          GOTO      FXCRDN10
.
FXCRDN20  CLOSE     PRICNOA2
.
FXCRDN50  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PRIFCIA1,"prifciaf"
          TRAPCLR   IO
          BRANCH    OVRCD,FXCRDN99
          DISPLAY   *P71:24,*V2LON,"prifciaf"
.
.        Loop over the reminder file and update each record
.
FXCRDN60  PACK      KEY32,OLDURNO,SP70
          CALL      RSPRFCI1
          CALL      RKPRFCI1
          BRANCH    OVRCD OF FXCRDN90
.
          MATCH     PRFCDEBT,OLDURNO
          GOTO      FXCRDN90 IF NOT EQUAL
.
          DISPLAY   *P30:24,*EL,"Invoice Number : ",*V2LON,PRFCINVN;
.
          PACK      PRFCDEBT,NEWURNO
          CALL      UPPRFCI1
          GOTO      FXCRDN60
.
FXCRDN90  CLOSE     PRIFCIA1
FXCRDN99  RETURN
+
          INC       IBAOVRIO/INC
          INC       COMDEPIO/INC
          INC       PRIAUDIO/INC
          INC       PRIAUIIO/INC
          INC       PRIAURIO/INC
          INC       PRICMNIO/INC
          INC       PRIFACIO/INC
          INC       PRIHDBIO/INC
          INC       PRIDTRIO/INC
          INC       PRIINVIO/INC
          INC       PRIHREIO/INC
          INC       PRIHTRIO/INC
          INC       PRIHISIO/INC
          INC       PRIHOLIO/INC
          INC       PRILHIIO/INC
          INC       PRIJRNIO/INC
          INC       RCPDTEIO/INC
          INC       PRICNOIO/INC
          INC       PRIFCIIO/INC
MRGPRI99  ENDPROC
............................................................................
