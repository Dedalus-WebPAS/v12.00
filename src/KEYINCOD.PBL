.******************************************************************************
.*  Include Name  : KEYINCOD.PBL                                              *
.*  Description   : Keyin of a codes file entry in V5.01                      *
.*  Author        : ROD                                                       *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CODE     - category of code                               *
.*                  CKYIDEF3 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                                                                            *
.*  Returns       : ACODE    - Keyed in code                                  *
.*                  TDESC    - Description                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.******************************************************************************
KEYINCOD  
KCODE010  MATCH     SP3,CKYIDEF3                  * using a default ?
          GOTO      KCODE050 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN3:      * no default used
                    *PECOL:EVERT,*V2LON,ACODE:
                    *PECOL:EVERT,*DV,ACODE
          GOTO      KCODE080
.
KCODE050  MOVE      CKYIDEF3,ACODE                 * default
          KEYIN     *PECOL:EVERT,*DV,ACODE:     * default used
                    *PECOL:EVERT,*V2LON,*RV,ACODE:
                    *PECOL:EVERT,*DV,ACODE
.
.         check what was entered
.
KCODE080  CALL      CODCK000
          BRANCH    EXIT,KCODE100,KCODE705,KCODE805,KCODE905
.                        "?"      valid    nothing  exitchar
          GOTO      KCODE010
.
.         a "?" was input
.
KCODE100  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
KCODE105  CALL      PATDSCOD
          BRANCH    CNEWDS,KCODE150
.
          CALL      PUTSCR00                * redisplay screen
          GOTO      KCODE010                * rekey code
.
.         now ask for code while leaving displayed codes on screen
.
KCODE150  MATCH     SP3,CKYIDEF3                  * using a default
          GOTO      KCODE200 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Code    : ":
                    *P11:24,*DV,UNDLN3:      * no default used
                    *P11:24,*V2LON,ACODE:
                    *P11:24,*DV,ACODE
          GOTO      KCODE300
.
KCODE200  MOVE      CKYIDEF3,ACODE
          KEYIN     *P1:24,*EL,*DV,"Code    : ":
                    *P11:24,*DV,ACODE:     * default used
                    *P11:24,*V2LON,*RV,ACODE:
                    *P11:24,*DV,ACODE
.
.         repeat the validation of the code
.         check what was entered
.
KCODE300  CALL      CODCK000
          BRANCH    EXIT,KCODE105,KCODE700,KCODE800,KCODE900
.                        "?"      valid    nothing  exitchar
.
          GOTO      KCODE150
.
.         a valid code was entered 
. 
KCODE700  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE
.
KCODE705  MOVE      FALSE,EXIT                    * valid input
          GOTO      KCODE999 
.
.         nothing was input
.
KCODE800  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE
.
KCODE805  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      KCODE999 
.
.         EXITCHAR input
.
KCODE900  CALL      FRESCR00                * free the screen saved
.
KCODE905  MOVE      TWO,EXIT                * EXITCHAR
.
KCODE999  RETURN
+
.*********************************************************************
.*                  CODCK000                    Called by : KEYINCOD *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
CODCK000  ENDSET    ACODE
          APPEND    SP3,ACODE
          RESET     ACODE
.
          MATCH     EXITCHAR,ACODE
          GOTO      CODCK600 IF EQUAL       * exit char
.
          MATCH     SP3,ACODE
          GOTO      CODCK700 IF EQUAL       * nothing
.
          MATCH     QUEST,ACODE
          GOTO      CODCK800 IF EQUAL       * ? mark
.
.         a code was entered so validate
. 
CODCK500  PACK      KEY5,CODE,ACODE
          CALL      RDCODE1
          BRANCH    OVRCD,CODCK900          * invalid code
.
          MATCH     "I",PTCOACTV            * check code is inactive
          GOTO      CODCK900 IF EQUAL
.
          MOVE      TWO,EXIT                * valid code
          GOTO      CODCK999
.
.         exitchar entered
.
CODCK600  MOVE      FOUR,EXIT
          MOVE      SP3,ACODE
          MOVE      SP20,TDESC              * clear desc field
          GOTO      CODCK999
.
.         nothing entered
.
CODCK700  BRANCH    CKYIMAND,CODCK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP3,ACODE
          MOVE      SP20,TDESC              * clear desc field
          GOTO      CODCK999
.
.         "?" entered
.
CODCK800  MOVE      ONE,EXIT
          GOTO      CODCK999
.
.         invalid code entered
.
CODCK900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*EL,*B,"Invalid code.  ";
          CALL      HITENTER
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      CODCK999
.
.         field is mandatory
.
CODCK950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is mandatory.  ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
CODCK999  RETURN
+
