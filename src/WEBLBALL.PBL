.******************************************************************************
.* LABELALL - This is a proc to display the variables painted for ALL Labels  *
.*                                                                            *
.* PARAMETERS  : This procedure requires that the master file record  has     *
.*               been read.                                                   *
.*               URNUMBER = UR Number                                         *
.*               ADMISSNO = Admission Number                                  *
.*               LABELTYP = Label Type                                        *
.*               USINGELT - Using Eltron printer for labels                   *
.*                      0 = No                                                *
.*                      1 = Yes                                               *
.* RETURNS     : EXIT 0 = Labels Printed / Zero Labels Selected               *
.*                    1 = Not Screen Painted                                  *
.*               NOLABEL = Number of labels to print                          *
.*                                                                            *
.* Modifications :                                                            *
.*                V10.10.01 11/07/2017 Nicole Torrisi TSK 0841026             *
.*                          Added read of NHI details to INIT0000             *
.*                V9.08.00  16/03/2007 Ebon Clements CAR 135528               *
.*                          Created program                                   *
.*                                                                            *
.******************************************************************************
          DEFPROC   LABELALL
.
          INC       IBAPASFD/INC
.
FORM3     FORM      3
.
DIM80     DIM       80
DIM3      DIM       3
DIM6      DIM       6
FIELDNO   FORM      5
PROCADMN  DIM       8
NMPNUMB   DIM       20
NOTSCPT   FORM      1          * not screen painted flag
PAGELIMT  FORM      "55"
.
CYRSFDTE  DIM       8
CYRSTDTE  DIM       8
CYRSYEAR  FORM      3
.
+
MAIN0000  CALL      INIT0000
          BRANCH    CEXIT,MAIN9999          * invalid details
.
          CALL      LABELCOM                * process screen painted labels
.
MAIN9999  GOTO      LABALL99
.******************************************************************************
.*        INIT0000  Initalise routine                                         *
.******************************************************************************
INIT0000  UNPACK    LABELTYP,PRGID,SCRID
.
          READ      CONTROLF,TWENTY1;*138,PTCNNHII
          READ      CONTROLF,HUND06;*181,ALCNLCOS
          READ      CONTROLF,HUND06;*182,ALCNLSIZ
          READ      CONTROLF,EIGHTY8;*1,PTCNPELT
.
          MOVE      PTCNPELT,USINGELT
          MOVE      ALCNLCOS,LABCOS
          MOVE      ALCNLSIZ,LABSIZE
.
.         set the patient details
.
          PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,INIT9100
.
          PACK      KEY8,URNUMBER,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,INIT9100
.
          IF        PTCNNHII = 1
            MOVE      SP10,NHMARES
            MOVE      SP10,NHMAETH
            MOVE      SP30,NMPNUMB
            PACK      KEY8,URNUMBER,SP70
            CALL      RDNHMAS2
          ENDIF
.
.         set the referral/encounter details
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDALREF1
          BRANCH    OVRCD,INIT9200
.
          MOVE      ZERO,CEXIT
          GOTO      INIT9999
.
INIT9100  MOVE      "Invalid U/R Number",WEBTITLE
          CALL      WEBERR00
          MOVE      TWO,EXIT
          MOVE      ONE,CEXIT
          GOTO      INIT9999
.
INIT9200  MOVE      "Invalid Referral/Visit Number",WEBTITLE
          CALL      WEBERR00
          MOVE      TWO,EXIT
          MOVE      ONE,CEXIT
          GOTO      INIT9999
.
INIT9300  MOVE      "Invalid Encounter Number",WEBTITLE
          CALL      WEBERR00
          MOVE      TWO,EXIT
          MOVE      ONE,CEXIT
          GOTO      INIT9999
.
INIT9999  RETURN
+
.*****************************************************************
.*        DISPVARS  Display Items            Called By :         *
.*****************************************************************
DISPVARS  MOVE      SCPSITM,FIELDNO
          MOVE      SCPSLEN,LLCNT
.
          COMPARE   "500",FIELDNO         * If item more than 500 use
          GOTO      DVARS100 IF LESS      * referral Details else use
          SUB       "500",FIELDNO         * PMI
.
          MOVE      FIELDNO,SCPSITM
          CALL      DXALLDET
          GOTO      DVARS999
.
DVARS100  CALL      DXPATMAS
.
DVARS999  RETURN
.
.         I/O includes needed
. 
          INC       AGECHK
          INC       CALCYRS
          INC       CLALLENC
          INC       DXPATCOD
          INC       DXPATMAS
          INC       DXALLDET
.
          INC       DSPFIELD
          INC       IBAOVRIO/INC
          INC       IBAPASIO/INC
          INC       WEBLBCOM    
          INC       PMIGTNID
.
LABALL99  ENDPROC
................................................................................
