.
.         PATINVS.PBL
.         -----------
.
.         This include controls the printing of invoices.
.         All invoice layouts are controlled through this include, the
.         code that is invoice layout dependent being located in the
.         include PATINVTn, where "n" if the invoice layout or PATINVnn
.
.         MODIFICATIONS:
.         
.----------------------------------------------------------------------------
. V12.00.03 23/06/2025  J.Tan           TSK 0961623
.           Added GETDRG for ABF display invoice screen heading
. V12.00.02 20/05/2025  Ebon Clements   TSK 0955096
.           Alphanueric visit number changes
. V12.00.01 15/05/2025 J.Tan    TSK 0960457
.           Mod to set PTDTEFFD for Accommodation
. V11.05.03 25/02/2025 J.Tan    TSK 0956519
.           Mod to check pending item after raising Patient only invoice
. V11.05.02 21/02/2025  J.Tan   TSK 0955356
.           Added FININVN,FINADMN,FINURNO - Financial details
. V11.05.01 29/11/2024  Thanh T        TSK 0939466
.           Added Distance in kms field
.----------------------------------------------------------------------------
.         V11.04.03 27/02/2024 PJ Le Febour 0929044e 
.                   update WBICCUID WBICRCAT WBICCTIM before call WRWBIHC1
.         V11.04.02 02/04/2024 J.Tan        0919569  
.                   Mod to MBS Exploding charges
.         V11.04.01 27/02/2024 PJ Le Febour 0929044db
.                   update WBICUUID WBICUDAT WBICUTIM before call WRWBIHC1
.         V11.03.04 09/12/2023 Alina    TSK 0936086
.                   Closed the bold text in SCRD000             
.         V11.03.03 04/09/2023 J.Tan    TSK 0916870
.                   Fixed displaying ABF Price Weight not set up
.         V11.03.02 20/06/2023 J.Tan    TSK 0923703
.                   Mod checking for ISBLFLAG=0 (independent services)
.         V11.03.01 12/01/2023  J.Tan          TSK 0900145
.                   Mod Win/Loss for each procedure, setup PTDTCPRC 
.         V11.02.02 10/10/2022  Peter Vela         0925622
.                   Removed old Eclipse IHC from Inpatient Invoicing
.         V11.02.01 31/01/2022  Peter Vela     TSK 0902857
.                   Changed WebServices parameter validation to switch
.                   exclusively between WebServices and SA
.         V11.01.07 14/12/2021  Peter Vela     TSK 0902857
.                   Added extra blank keystrokes for IBAWEB04 in SWEBX000
.         V11.01.06 16/06/2021  Peter Vela     TSK 0907637
.                   Added WebServices parameter check for IBAWEB04
.         V11.01.05 06/05/2021  J.Tan          TSK 0863287
.                   Fixed auto reassign debt for PTCNPPIN=0 (RDEI0000)
.         V11.01.04 03/05/2021  J.Tan          TSK 0863287
.                   Mod checking for DBSFLAG Discharge Billing (PTCNPPIN=1)
.         V11.01.03 02/04/2021  J.Tan             TSK 0863287
.                   Mod for New Patient Invoice functionality (PTCNPPIN=1)
.         V11.01.02 23/03/2021  Peter Vela        TSK 0902857
.                   Added SWEBX000
.         V11.01.01 23/02/2021  Peter Vela        TSK 0902857
.                   Added WebServices IHCW functionality
.                   18/02/2021  J.Tan          TSK 0888639
.                   Added PMMITMNO - Theatre Team no
.         V11.00.02 04/06/2020  J.Tan             TSK 0886178
.                   Mod not to apply deposit to ABF Invoice
.         V11.00.01 02/04/2020  J.Tan             TSK 0868813
.                   Changed PKNAME to DIM80
.         V10.15.02 26/11/2019  J.Tan          TSK 0882545
.                   Mod to update PMABADRG
.         V10.15.01 08/10/2019  J.Tan          TSK 0882545
.                   Mod to display msg of Blank Disc.DRG for ABF
.         V10.14.03 23/09/2019  J.Tan          TSK 0881867
.                   Mod ABF Inv.pending to check for zero amount of items
.         V10.14.02 28/06/2019  J.Tan          TSK 0857392
.                   Mod PTINCMIX = 3 for ABF Invoice
.         V10.14.01 21/03/2019  J.Tan            TSK 0857392
.                   Changed RCP Transaction count from DIM3 to DIM5
.         V10.13.02 23/10/2018  Tracey Nguyen   TSK 0859743                     
.                   Added PMMIHOSI - Hopsital Indicator on PMSMTIFD             
.         V10.13.01 21/08/2018  J.Tan        TSK 0859742                       *
.                   Added ECLIPSE new fields from pmsmtiaf                     *
.         V10.12.02 15/06/2018  Ebon Clements  TSK 0846868 
.                   Added exclude claim type from eclipse - ZEROINV
.         V10.12.01 27/04/2018  J.Tan          Task 0845996
.                   Mod to update CMDEUDAT,CMDEUTIM and CMDEUUID
.         V10.11.02 31/08/2017  J.Tan       TSK 0837479
.                   Added PMMICTYP - Consumption Type
.         V10.11.01 10/08/2017  Peter Vela  TSK 0837619
.                   Move PREVTRID to PMECPCTI before WRPMECT1
.         V10.08.01 15/08/2016  J.Tan       TSK 0823517
.                   Mods ZEROINV2 to check for Patient Only Invoice (IPATFLAG)
.         V10.07.01 21/12/2015  J.Tan       CAR 0303942
.                   Mods to setup Payment code
.         V10.06.01 11/09/2015  J.Tan        CAR 317216
.                   Removed SINVT000 and SINVH0000 
.         V10.05.02 10/12/2014  Peter Vela   CAR 309840
.                   Fixed key pack in ZEROINV2 for WRPMECT1
.         V10.05.01 05/08/2014  Peter Vela   CAR 302164
.                   Added PMMIRBRS - Rebate Reason CAT Rr
.         V10.04.02 01/05/2014  J.Tan        CAR 261630
.                   Removed patauc pataum pataui audit files
.         V10.04.01 17/01/2014  J.Tan        CAR 295008
.                   Mods to set PMMIDTCR,PMMITMCR,PMMIIDCR
.                   20/01/2014  J.Tan        CAR 249828
.                   Added PMMIPMBS,PTDTPMBS - Patient MBS Team and Counter
.         V10.03.06 11/12/2013  J.Tan          CAR 292415
.                   Fixed POST0000 updating correct index of rcpdteaf
.         V10.03.05 13/09/2013  J.Tan          CAR 283618
.                   Mods for Independence Services Billing
.         V10.03.04 10/05/2013  J.Tan          CAR 284289
.                   Fixed ALACDTE for Backdated invoice
.         V10.03.03 18/12/2012  J.Tan          CAR 274769
.                   Fixed PINVTYP - Invoice type for split invoice
.         V10.03.02 19/07/2012 J.Tan  CAR 257771
.                   Mods SX invoice to print Theatre duration column
.         V10.03.04 20/03/2012  J.Tan          CAR 262073
.                   Mods Print patient invoice not to remove pending file
.         V10.03.03 14/03/2012  Peter Vela     CAR 261166
.                   Moved SECLX000
.         V10.03.02 30/11/2011  J.Tan          CAR 240262
.                   Fixed printing Discharge billing statement
.         V10.03.01 14/11/2011 J.Tan         CAR 235425
.                   Mods for SX invoice layout
.         V10.02.02 21/04/2011 J.Tan           CAR 237245
.                   Mods for Johns Hopkins Insurance Providers
.         V10.02.01 04/04/2011  Jill Parkinson CAR 239574
.                   Removed open of ibaprntf
.         V10.01.03 28/02/2011  Peter Vela     CAR 233034
.                   Added Raise and Claim functionality for Eclipse
.         V10.01.02 13/01/2011  Mike Laming    CAR 233084
.                   Added new include CLPATINV (Added PTINCNID) in ZEROINV
.         V10.01.01 08/12/2010  Mike Laming    CAR 233046
.                   Mods for PATMCHFD using H/F Table Type (replacing H/F Table)
.         V10.00.07 24/11/2010 Ebon Clements CAR 233156
.                   Added created by to journals
.         V10.00.06 23/09/2010 J.Tan         CAR 230664
.                   Mods for HEC invoice layout - PTCNINVL=18
.         V10.00.05 09/09/2010 J.Tan         CAR 229951
.                   Fixed bug for claim code with indic=P which causing Print 
.                   invoice after the display
.         V10.00.04 25/08/2010 J.Tan         CAR 228192
.                   Added column for quantity on invoice screen
.         V10.00.03 18/08/2010  J.Tan     CAR 222031
.                   Mods to set PMMIDUPD and PMMITUPD
.         V10.00.02 30/04/2010  J.Tan     CAR 220887
.                   Mods checking for Active/Inactive of Misc.charge
.         V10.00.01 19/04/2010  J.Tan        CAR 219670
.                   Mods to set date/time updated to rcpdteaf file
.         V9.12.01  05/10/2009  J.Tan        CAR 191624
.                   Mods to set the Deposit Applied date to Current date
.         V9.12.02  10/06/2009  J.Tan             CAR 197956
.                   Fixed Johns Hopkins backdated invoice date
.                   Mods to setup FGSTFLAG for posting cash (CAR 198611)
.         V9.12.01  15/05/2009  WeeLee Koo        CAR 196722
.                   Re-open PATVISA1,PATFX1A1 and VISPAYA1 that have 
.                   been commented out under CCPS0000
.         V9.11.07  28/04/2009  J.Tan             CAR 195596
.                   Added fields PTDTADPT PTDTADRB
.         V9.11.06  15/04/2009  Steve Armstrong   CAR 194544
.                   Mods for checking for DVA Eclipse claiming in multi-
.                   hospital environment.  If not set for hospital, check
.                   parameter.
.         V9.11.05  01/04/2009  J.Tan             CAR 192704
.                   Fixed field PTDTCCU
.         V9.11.04  10/03/2009  J.Tan            CAR 188102
.                   Added Patient Only Invoice
.         V9.11.03  22/12/2008  Steve Armstrong  CAR 183976
.                   Mods to write to pmsectaf when patfundf.ptfxextr is set to
.                   2 (Eclipse) or when DVA is being sent using Eclipse.
.         V9.11.02  25/11/2008 J.Tan      CAR 183363
.                   Mods to read doctor file for invoice header
.         V9.11.01  30/10/2008 J.Tan      CAR 181263
.                   Mods for printing Discharge Billing Statement
.         V9.10.02  23/05/2008 J.Tan      CAR 169149
.                   Added Date and Time invoice created/updated 
.         V9.10.01  05/05/2008  J.Tan          CAR 162313
.                   Added deposit status to comdepaf file
.         V9.09.03  06/05/2008  J.Tan          CAR 167607
.                   Mods deleting invoice pending for backdated invoice
.         V9.09.02  31/08/2007  J.Tan          CAR 140282
.                   Mods for FFS Win/Loss
.         V9.09.01  26/06/2007  J.Tan          CAR 143630
.                   Fixed checking for First day of period (FRSDAY00)
.         V9.08.13  07/05/2007 Peter Vela CAR SJOG ED Billing
.                   Initialised new pmsmtiaf variables
.         V9.08.12  15/03/2007  J.Tan          CAR 136149
.                   Mods to use default claim code from system parameter
.         V9.08.11  14/03/2007  J.Tan          CAR 135853
.                   Added comments for Mike's changes for JH PRFA format
.         V9.08.10  22/02/2007 Mike Laming   CAR 119436
.                   Added comments for Mike's changes for JH PRFA format
.         V9.08.10  22/02/2007 Mike Laming   CAR 119436
.                   Add PROC FLAGDEBT (PMSOSDTM) for Outstanding Debt Alert ind.
.         V9.08.09  08/02/07 J.Tan    CAR 120001
.                   Mods for NZ Billing
.         V9.08.08  11/01/2007  Mike Laming    CAR 130313
.                   Added different Name sequence for JH at label GETPNAM
.         V9.08.07  11/12/2006  J.Tan          CAR 127646
.                   Added Reference column to JH Details invoice
.         V9.08.06  05/12/2006  J.Tan         CAR 127092
.                   Mods SELF PRFA for JH Invoice
.         V9.08.05  01/12/2006  J.Tan         CAR 126728
.                   Added Quantity column for JH Invoice
.         V9.08.04  22/11/2006  Mike Laming   CAR 103368
.                   Add PTCNUCCP and routine CCPS0000 for CCPS EDI
.         V9.08.03  14/11/2006  Peter Vela    CAR 124547
.                   Added functionality for PRFA of Deceased Patients
.         V9.08.02  16/10/2006  J.Tan         CAR 121629
.                   Fixed discount from receipting
.         V9.08.01  05/10/2006  J.Tan          CAR 120654
.                   Fixed No of invoices printed for split invoices
.         V9.07.03  19/09/2006  J.Tan          CAR 119336  
.                   Added claim code to suggested classification
.         V9.07.02  31/07/2006 J.Tan      CAR 103365
.                   Mods for Johns hopkins invoice
.         V9.07.01  18/08/2006 J.Tan  CAR 115853
.                   Fixed printing claim details for Pendlebury
.         V9.05.B01 09/12/2005 J.Tan  CAR 59381
.                   Mods for printing split invoice
.         V9.04.02  25/10/2005 Sandra Barcham  81362
.                   Fixed incorrect packing of fincode for deposits
.         V9.04.01  03/08/05 J.Tan    CAR 62750             
.                   Removed redefined amount variables      
.                   19/08/05 J.Tan    CAR 64493
.                   Added Bed type to dtr file
.         V9.03.24  16/06/2005  Mike Laming   CAR 61746
.                   Added clears for PTDTTIME & PTDTCRST at SETUPTRN
.         V9.04.05  30/11/2004  J.Tan      CAR 55293
.                   Added uniq id to patdtraf and pmsmtiaf
.         V9.04.04  15/11/2004 J.Tan  CAR 54768 
.                   Mods for Pendlebury invoice layout (PTCNINVL=16)
.         V9.04.03  21/09/2004 Lina Vo CAR 53660
.                   Changed for Multi Hospital to use PTHSTFEE instead 
.                   of PTCNTFEE
.                   06/10/2004  J.Tan   CAR 53798
.                   Removed PATMSCDS
.         V9.04.02  06/09/2004 J.Tan  CAR 53073
.                   Removed closing invoice pending file
.         V9.04.01  20/08/2004 J.Tan  CAR 52835
.                   Setup hospital ID for patfinsf
.         V9.03.21  12/08/2004 J.Tan  CAR 52504
.                   Fixed pending cash
.         V9.03.20  24/06/2004 J.Tan CAR 51112
.                   Mods to check for non-banked cash when printing invoice
.                   Mods to check for refund deposit (CAR 51266)
.         V9.03.19  09/06/2004 Sylvek Litewka  CAR 50371
.                   Removed (commented out) the call to procedure PATFUTRE.PBL.
.                   08/06/2004 J.Tan  CAR 50035
.                   Write Mechanical Ventilation Billing Days to invoice file
.         V9.03.18  01/06/2004 J.Tan  CAR 50273
.                   Exclude cancelled non-bank deposit
.         V9.03.17  13/05/2004 J.Tan  CAR 49827
.                   Mods to get non-banked deposit
.         V9.03.16  06/05/2004  J.Tan  CAR 49531
.                   Fixed cash audit file
.         V9.03.15  04/05/2004  J.Tan  CAR 49519
.                   Fixed to check for refund deposit
.         V9.03.14  29/04/2004  J.Tan   
.                   Mods error message on the invoice range
.         V9.03.13  23/04/2004  J.Tan   CAR 45547
.                   Mods for backdate invoice
.         V9.03.12  22/04/2004  CAR 46224   Mike Laming
.                   Change input of TSESSION so that it is right-aligned
.                   (to match IBAOPR43) at label KSESS
.         V9.03.11  20/04/2004  J.Tan 
.                   Fixed backdated invoice date (Zeroinv routine)
.         V9.03.10  16/03/2004  Steve Armstrong  HosClaims Phase 1 (48280)
.                   Mods to create a patedsaf record when writing a new invoice
.         V9.03.09  12/02/2004  Henry Son        CAR 47385
.                   Added PTCNMBSF for checking if we allow more than
.                   Six MBS Items.
.         V9.03.08  09/01/2004 J.Tan 
.                   Fixed setting invoice number for rcpdteaf file 
.         V9.03.07  11/12/2003  J.Tan 
.                   Replace patcash with comdepaf file
.         V9.03.06  18/11/2003  Henry Son   CAR 39291
.                   Allow more than 7 Theatre stepdown fees.
.         V9.03.05  31/10/2003  S.Litewka  CAR 44328
.                   Added clear of PTMMDESC field when writing a new record
.                   to patmmbsf.
.         V9.03.04  20/10/2003  J.Tan    CAR 44172
.                   Mods to read misc.theatre item file (pmsmitaf)
.         V9.03.03  15/09/2003 Lina Vo    CAR 40497
.                   Changed index and read of patmchgf to include effective 
.                   date.
.         V9.03.02  01/08/2003 J.Tan
.                   Changed the colour of the invoice heading
.         V9.03.01  27/06/2003 J.Tan
.                   Mods for WEB Billing printing invoice
.         V9.02.16  14/04/2003  Davin  CAR/Quote 23297 (misc. charges keyin)
.                   When keyin rebate flag="Y", display default patient/rebate
.                   amounts
.         V9.02.15  08/04/2003 J.Tan
.                   Mods for WEB Billing
.         V9.02.14  15/07/2002 Tonii SRF 15099
.                   Mods to move ATIME to EFTTIME if ADATE = EFTDATE
.         V9.02.13  24/06/2002 J.Tan
.                   Mods to check if dtr record already exist before writes I*D
.         V9.02.12  12/06/2002 J.Tan
.                   Added print casepayment option
.         V9.02.11  05/06/2002 J.Tan
.                   Added future action option for public hosp
.         V9.02.10  29/05/2002 J.Tan
.                   Fixed displaying amount on item selection
.         V9.02.09  28/05/2002 J.Tan
.                   Fixed not to print estimate rebate
.         V9.02.08  27/05/2002 J.Tan
.                   Removed opening patcuraf
.         V9.02.07  23/04/2002 J.Tan
.                   Ported from r6.09
.         V6.09.10  18/03/2002 J.Tan  SRF 16281
.                   Mods undischarge casepayment to charge perdiem theatre fee
.         V6.09.09  20/02/2002  Tonii  Casemix Defects
.                   Fixed display in items entry option prior to printing 
.                   invoice
.         V6.09.08  05/02/2002  Glenn Saunders - Mayne AR Data Extraction proj 
.                   Allow for patcash file amount being +ve.  Multiply by -1   
.         V6.09.07  30/01/2002  Greg Horvat
.                   Changed to write the DRG used for billing to the casemix
.                   code variable on the invoice file
.         V6.09.06  14.01.2002  Glenn Saunders - Mayne AR Data Extraction proj 
.                   Set new 'dtr' field SAP indicator to 'N' for new records   
.         V6.09.05  02.01.2002  Glenn Saunders - Mayne AR Data Extraction proj 
.                   Add new routine to write to new SAP Cash file.              
.         V6.09.04  30/11/2001  Greg Horvat
.                   Fixed I*L on the invoice tempfile
.         V6.09.03  28/11/2001  Tonii SRF 13489
.                   Mods to display for new variable lengths in PATDTRFD
.         V6.09.02  27/10/2001  J.Tan
.                   Removed Episode flag for casemix funding
.         V6.09.01  26/07/2001  Greg Horvat & Glenn Saunders  SRF 11332
.                   Fixed the screen display when no of invoices to display is 0
.         V6.09.00  14/12/2000  Jill Habasque
.                   Casemix mods - revenue breakdown,invoice breakdown,
.                   patient moiety etc..
.         V6.07.05  13/10/2000  Steve Downing
.                   Fixed printing of admission/discharge diagnosis 1 & 2
.         V6.07.04  02/10/2000  Steve Downing
.                   Added initialsation of GST variables
.         V6.07.03  10/09/2000  Steve Downing
.                   Fixed display position of item after ? search         
.         V6.07.02  01/06/2000  Davin
.                   Set GST flag for invoice header
.         V6.07.01  01/05/2000  J.Tan
.                   Mods for backdated invoice 
.         V6.07.B03 13/04/2000  Greg Horvat
.                   Removed the ROLLF routine
.         V6.07.B02 10/04/2000  Davin
.                   Mods for templating - use PATINVHL for headers
.                                       - use PATINVTL for tails
.         V6.07.B01 29/03/2000  Jill Habasque
.                   GST Mods
.         V6.06.03  14/09/1999  Davin
.                   Changes for 4 character DRGs
.         V6.06.02  15/07/1999  Greg Horvat      99/00 PRS2 Mods
.                   Recompiled for PATASFKY - Changed to keyin contract vars
.         V6.06.01  01/06/1999  Andrew Peel
.                   EDI sent flag added and set to zero
.        V6.06.B01  03/02/1999  Davin  SRF 629833
.                   Allow keyin of pat/reb portion for theatre item if
.                   casemix flag set to zero
.         V6.05.07  08/12/1998  Nicole Harrington                             
.                   Removed CCENTRY                                           
.         V6.05.06  18/11/1998  Nicole Harrington
.                   Changed to include century in JULCON call
.         V6.05.05  13/10/1998  J.Tan
.                   Mods not to keyin rebate/patient for zero amount of theatre
.         V6.05.04  05/10/1998  J.Tan
.                   Fixed set up ACDATE and setup cshcent
.         V6.05.03  03/09/1998  J.Tan   SRF 627589
.                   Recompiled for PATINVS - initialise dtr record
.         V6.05.02  18/08/1998  Davin  605
.                   Mods to include century (displays)
.         V6.05.01  27/07/1998  Katie Nelson
.                   Ignore display statements if being used by web server
.        V6.05.B04  26/06/1998  Davin
.                   Fixed fees invoiced date (FINDATE) & Adm. no. display
.        V6.05.B03  24/06/1998  Steve Armstrong
.                   Fixed display of ADmission & Discharge Dates
.        V6.05.B02  17/06/1998  J.Tan
.                   I*C on bokrecaf
.        V6.05.B01  09/06/1998  Davin  605
.                   Fixed U/R display in screen header routine
.         V6.04.17  08/03/98 J.Tan  SRF 619732
.                   Mods to write to a text file
.         V6.04.16  17/02/98 J.Tan     SRF 623552
.                   Mods merchant card payment not to write to patfins & fixed
.                   I*C when posting (I)tems
.         V6.04.15  13/10/97 J.Tan     SRF 622241
.                   Mods to allow operator to keyin amount for zero misc.items
.         V6.04.14  25/09/97 J.Tan     SRF 621855
.                   Mods automatic adm.type to use current time if effective
.                   date is equal to current date
.         V6.04.13  08/08/1997  Davin
.                   Added new invoice layout for Peninsula - layout 22
.         V6.04.12  29/07/1997  Greg Horvat      SRF 621050
.                   Changed to make the auto admin type effective date default
.                   to the MBS date
.                   29/07/1997  Greg Horvat      SRF 620935
.                   Changed to do the auto admin type update when an exploding
.                   charge for that admin type
.         V6.04.11  08/07/1997  ROD
.                   Mods to write a DTR record with zero amount if casemix
.                   theate not setup
.         V6.04.12  23/12/1996  Greg Horvat
.                   Added new invoice layout for Greenslopes - layout 21
.         V6.04.11  08/05/1997  Greg Horvat
.                   Changed to use the patients health fund when charging
.                   exploding miscellaneous charges
.         V6.04.10  02/05/97 J.Tan     SRF 619883
.                   Mods to update deposit merchant card to patfinsf
.                   Mods to write episode type for exploding charges
.         V6.04.09  29/04/97 J.Tan     SRF 619328
.                   Mods cash audit cheque number to dim10
.         V6.04.08  18/04/1997  Greg Horvat
.                   Changed to check for exploding charges only when posting
.                   items
.         V6.04.07  09/04/97 J.Tan
.                   Mods to use discharge DRG code for casemix funding only
.                   if a DRG code is entered as the provisional DRG
.                   Fixed to reposition dtr record
.         V6.04.06  03/04/1997  Greg Horvat      SRF 619559
.                   Chanegd to reposition back on the correct record when
.                   finished updating the accounts payable flag
.         V6.04.05  14/03/1997  Greg Horvat
.                   Recompiled for CHNADMTP - Changed to use PTCNHDPS to work
.                   out which state for HDP extracts
.         V6.04.04  12/02/1997  Greg Horvat
.                   Added code to do exploding MBS charges
.         V6.04.03  20/01/1997  Greg Horvat      Holy Spirit Mods
.                   - Changed to not perform auto admin type update if any MBS
.                     items are exploding charges
.                   - Changed to update the accounts payable field on the DTR
.                     file when the invoice is balanced
.         V6.04.02  11/12/1996  Greg Horvat      Holy Spirit Mods
.                   Changed to keyin the effective date when changing
.                   subsequent admission types
.         V6.04.01  03/12/1996  Greg Horvat      Holy Spirit Mods
.                   Added new invoice layout for Holy Spirit - layout 20
.
.         V6.03.05  02/12/96 J.Tan     SRF 614739
.                   Mods item description with a multiple amount
.         V6.03.04  27/05/1996  Greg Horvat      Quote 8623
.                   Added new invoice layout for Adelaide Clinic - layout 19
.         V6.03.03  15/03/1996  Greg Horvat      SRF 614248
.                   Recompiled for PATREBAT - Problem with a branch statement
.         V6.03.02  12/02/1996  Greg Horvat      SRF 613893
.                   Recompiled for PATREBAT - Changes to printing leave records
.         V6.03.01  02/11/1995  Greg Horvat      SRF 611839
.                   Added new invoice layout for Mater - layout 18
.                   changed to write to this new variable
.         V6.03.B04 10/03/1995  Greg Horvat      SRF 134573  Quote 8300
.                   Recompiled for PATBNKFD & PATIOBNK - Added PTBFRDTE &
.                   changed to write to this variable
.         V6.03.B03 24/02/1995  Greg Horvat      New Release 6.03
.                   Changed to use PATCKEXC - Check if keyed in item is an
.                   exclusion list item
.         V6.03.B02 17/02/1995  Greg Horvat      SRF 134879
.                   Recompiled for PATAUMFD & PATIOAUM - added new variable &
.         V6.03.B01 31/01/1995  whirlpool        New Release 6.03
.                   cleared new filed ptdtbtype if not an accom record      
.         V6.03.B00 30/12/1994  Greg Horvat      New Release 6.03
.                   Changed to ask whether to continue when keying in the MBS
.                   item (Exclusion List Item)
.         V6.03.B00 11/01/1995  Greg Horvat      New Release 6.03
.                   Recompiled for PATMASFD - Changed PBDATE to be CCYYMMDD
.
.******************************************************************************
.         This is the start of the code that controls the printing of the
.         invoice. KEY6 is assumed to contain the admission number.
.******************************************************************************
INVPRT    CALL       RDMISS1
          BRANCH     OVRCD OF INVPRT9
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF INVPRT9
.
          MOVE      SP70,PMVXMHOS
          OPEN      PMSVX1A1,"pmsvx1af"
          PACK      KEY8,AADMNO
          CALL      RDPMVX11
.
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          IF        IBCNMHOS=1
            OPEN      PATHSPA1,"pathspaf"
            MOVE      SP70,PTHSTFEE
            PACK      KEY3,PMVXMHOS,SP10
            CALL      RDPTHSP1
          ENDIF
.
.         Check for a valid Admission Status (ie. Not pre-admitted or Cancelled)
.
INVPRT2   BRANCH    ASTAT OF INVPRT9,INVPRT3,INVPRT3,INVPRT3,INVPRT9
.
.         Set up a few miscellaneous variables
.
INVPRT3   MOVE      SP8,DDATE
          MOVE      AADMNO,KEY8
          CALL      RDDSCH1
          MOVE      OVRCD,DISFLG
.
          LOAD      DIM20 FROM ASTAT OF ASTAT1,ASTAT2,ASTAT3,ASTAT4,ASTAT1
.
          UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY
          PACK      B10DATE,XDAY,SLASH,XMON,SLASH,XCENT,XYEAR
.
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,A10DATE
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,D10DATE
.
          CALL      DISPINV
INVPRT9   RETURN
.
+
.******************************************************************************
.         This subroutine displays the invoice, and then asks if the invoice
.         is to be printed, have a cash receipt done, have miscellaneous items
.         added, comments added, or change the bed fee.
.******************************************************************************
DISPINV   MOVE      TWO,PROGFLAG
          MOVE      AADMNO,KEY8
          MOVE      ZERO,GROSSTOT
          MOVE      ZERO,TOTGST
          MOVE      ZERO,NETTOT
          MOVE      ZERO,GOVTOTAL
          MOVE      ZERO,TOTDAYS
          MOVE      ZERO,NODAYS
          MOVE      ZERO,WNLFLAG
          MOVE      ZERO,PREBFLAG
          LOAD      IPASS USING NUMINVS OF ZERO,ONE      * Start with pat. inv
.
          MOVE      SP70,PMVXMHOS
          OPEN      PMSVX1A1,"pmsvx1af"
          PACK      KEY8,AADMNO
          CALL      RDPMVX11
.
          MOVE      ZERO,WEBFLAG
          MATCH     "IBARSH",PGM
          IF        @EQUAL
            MOVE      ONE,WEBFLAG
          ENDIF
.
          MATCH     "PATWEB71",PRGID
          IF        @EQUAL
            MOVE      ONE,WEBFLAG
          ENDIF
.
.         Calculate the date range to be invoiced
.
          CALL      CALCTBI
.
          IF        CFEETYP=5
            CALL      NZSC0000          * Display heading for NZ Priv.invoice
          ELSE
            CALL      SCRD000           * Display heading for invoice
          ENDIF
          CALL      GETCASH           * Get any cash figures from discharge prog.
          CALL      INITIVAR          * Initialize any special variables
.
          MOVE      ZERO,IPASS
          MOVE      TWO,PROGFLAG
.
          MATCH     "1",PTCNPPIN      * New Patient Invoice functionality 
          IF        @EQUAL
            CALL      CPIN0000        * Check if Patient invoice is raised
            IF        PREBFLAG=1 & DBSFLAG=0 & ISBLFLAG=0
              CALL      RBINV000      * Display Rebate Invoice
              GOTO      DISP999
            ENDIF
          ENDIF
          PACK      PINVDTE WITH CCC,CYY,CMM,CDD
          REP       " 0",PINVDTE
.
DISP800 IF        CFEETYP=5
            CALL      NZINV000        * NZ Private Invoice (NZPCATBI)
          ELSE
            CALL      DLINE1          * Display invoice
          ENDIF
.
DISP999   RETURN
.
.         ************************************
.         Subroutine to display screen heading
.         ************************************
SCRD000   CALL       GETPNAM
          MOVE       "6",VERT
          LOAD       VERT,NUMINVS,SIX,SEVEN
.
          MATCH      "IBARSH",PGM
          GOTO       SCRD999 IF EQUAL
.
          COMPARE    ONE,DBSFLAG
          GOTO       SCRD080 IF NOT EQUAL
.
.         Display heading for Discharge Billing Statement
.
           WRITE      HTMLFILE;"<tr bgcolor=#99FFFF><b>":
                               "<td width=25% align=center><b> Date </b></td>":
                               "<td width=35%><b> Description </b></td>":
                               "<td width=10%><b> Item </b></td>":
                               "<td width=10% align=right><b> Rate </b></td>":
                               "<td width=10% align=right><b> Up To</b></td>":
                               "<td width=10% align=right><b>":
                               "Total(Inc.GST) </b></td>":
                               "</b></tr>";
.
          GOTO       SCRD999
.
SCRD080   COMPARE    TWO,INVTYPE
          GOTO       SCRD100 IF EQUAL      * Detailed invoice
.
          CALL       ABHD0000              * ABF Heading ?
.
.         Summary or Standard invoice
.
           WRITE      HTMLFILE;"<tr bgcolor=#99FFFF><b>":
                               "<td align=center><b> Date </b></td>":
                               "<td><b> Description </b></td>";
.
           IF         INVTYPE=0
             WRITE      HTMLFILE;"<td><b> Contract ID </b></td>":
                                 "<td><b> Item </b></td>":
                                 "<td><b> Qty </b></td>";
           ENDIF
.
           IF         INVTYPE=1
             WRITE      HTMLFILE;"<td><b> Item </b></td>";
           ENDIF
.
           MATCH     "1",PTCNPPIN
           IF        @EQUAL
             IF        INVTYPE=0 & IPATFLAG=0 & ISBLFLAG=0
              WRITE      HTMLFILE;"<td align=right><b> Rebate Portion</b></td>":
                                "<td align=right><b> Patient Portion</b></td>";
               GOTO      SCRD092
             ENDIF
           ENDIF
.
           WRITE      HTMLFILE;"<td align=right><b> Rate </b></td>";
.
SCRD092    WRITE      HTMLFILE;"<td align=right><b> GST Excluded </b></td>":
                               "<td align=right><b> GST Amount </b></td>":
                               "<td align=right><b> Total </b></td>":
                               "</tr>";
          GOTO       SCRD999
.
.         Detailed Invoice
.
SCRD100   WRITE      HTMLFILE;"<tr bgcolor=#99FFFF><b>":
                               "<td align=center><b> Date </b></td>":
                               "<td><b> Description </b></td>":
                               "<td><b> Reference</b></td>":
                               "<td><b> Item </b></td>":
                               "<td><b> Qty</b></td>":
                               "<td align=right><b> Unit $</b></td>":
                               "<td align=right><b> GST Excluded </b></td>":
                               "<td align=right><b> GST Amount </b></td>":
                               "<td align=right><b> Unit Total </b></td>":
                               "<td align=right><b> Amount</b></td>":
                               "</tr>";
.
.
SCRD999   RETURN
.
.         ***********************************************
.         Check for ABF invoice screen heading
.         ***********************************************
.
ABHD0000  READ      CONTROLF,HUND28;*105,PTCNUABF
          MATCH     "1",PTCNUABF
          GOTO      ABHD9999 IF NOT EQUAL   * Not using ABF
.
          PACK      KEY5,ANSC,ANSL,ACLAIM
          CALL      RDCODE1
          BRANCH    OVRCD,ABHD9999
          MATCH     "A",TCINDC2
          GOTO      ABHD9999 IF NOT EQUAL    * Not an ABF Funding
.
          UNPACK    SP70,DDRGCODE,PDRGCODE
          MOVE      AFUNDH,PENDFUND           * Save patient health fund
          MOVE      SP70,AFUNDH               * Blank HF, use Claim Grouper Ver.
          CALL      GETDRG
          MOVE      PENDFUND,AFUNDH           * restore HF
          IF        OVRCD=0
            MOVE      DDRGCODE,PDRGCODE
          ENDIF
.
          MOVE      ZERO,ECTRFLAG
          MATCH     SP70,PDRGCODE
          IF        @EQUAL
            MOVE      PTMIPDRG,PDRGCODE         * Default to Admission DRG
            IF        ASTAT=3
              MOVE      DDATE,TODATE
              MATCH     SP70,PTDSDDRG
              IF        !@EQUAL
                MOVE      PTDSDDRG,PDRGCODE     * Use Discharged DRG
              ELSE
                CALL      CODD0000              * check if patient is coded
                BRANCH    EXIT,ABHD9999         * not coded
                MOVE      ONE,ECTRFLAG          * Flag for Disc. with blank DRG
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     SP70,PDRGCODE
          GOTO      ABHD7000 IF EQUAL
.
.         Get DRG Description
.
          OPEN      PATDGWA1,"patdgwaf"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND24;*121,PTCNDGED
.
          MATCH     "1",PTCNDGED
          GOTO      ABHD3000 IF EQUAL
.
          MATCH     SP6,AFUNDH
          IF        !@EQUAL
            OPEN      PATFN1A1,"patfn1af"
            PACK      KEY14,AFUNDH,ZERO,ZERO,ZERO,ZERO,SP20
            CALL      RDFUND1                 * Read a Health Fund file record
          ENDIF
.
.         Get the grouper version from health fund
.
          PACK      KEY5,ANSC,ANSL,ACLAIM
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      PTCDGRPV,PTHFGRPV      * Use claim code grouper version
          ENDIF
          GOTO      ABHD4000
.
.        Get DRG Version using 'Effective DRG' table (NEW)
.
ABHD3000  MOVE      AFUNDH,D6                 * Save patient health fund
          MOVE      SP70,AFUNDH               * Blank HF, use Claim Grouper Ver.
          CALL      GTEDRG00
          MOVE      D6,AFUNDH                 * restore HF
          IF        EXIT=0
            MOVE      KEY3,PTHFGRPV
          ENDIF
.
ABHD4000  MATCH     SP70,PTHFGRPV
          IF        @EQUAL
            READ      CONTROLF,HUND05;*158,PTCNDGPV
            MOVE      PTCNDGPV,PTHFGRPV         * Use the Default Grouper vers.
          ENDIF
.
.
          MOVE      SP70,CONTRCID
          CALL      CDRG0000                  * look for a Contract ID
          MATCH     SP70,CONTRCID
          GOTO      ABHD7000 IF EQUAL
.
          BRANCH    ECTRFLAG,ABHD7200
.
          PACK      KEY7,PDRGCODE,PTHFGRPV,SP70
          CALL      RDPTDGW1
          IF        OVRCD=1
            MOVE      SP70,PTDWDESC
            PACK      KEY7,PDRGCODE,SP70
            CALL      RSPTDGW1
            CALL      RKPTDGW1
            IF        OVRCD=0
              MATCH     PDRGCODE,PTDWDRGC
              IF        !@EQUAL
                MOVE      SP70,PTDWDESC
              ENDIF
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td colspan=9><table border=0 width=100%>":
                             "<tr><td><big><b>DRG Code:",PDRGCODE,", ",PTDWDESC:
                             ", Contract ID:",CONTRCID,"</b></big></td>";
          GOTO      ABHD9000
.
ABHD7000  WRITE     HTMLFILE;"<tr><td colspan=9><table border=0 width=100%>":
                             "<tr><td><big><b>ABF Price Weight Not Setup.":
                             "</b></big></td>";
          GOTO      ABHD9000
.
ABHD7200  WRITE     HTMLFILE;"<tr><td colspan=9><table border=0 width=100%>":
                             "<tr><td><big><b>Blank Disc.DRG. Charges ":
                             "showing based on Prov.DRG.":
                             "</b></big></td>";
          GOTO      ABHD9000
.
ABHD9000  
    WRITE     HTMLFILE;"<td align=right><b>Patient Only Miscellaneous Charges ":
                             "and Other Items will not be included in ":
                             "ABF Invoice":
                             "</b></td></tr></table></td></tr>";

ABHD9999  RETURN
.
.*******************************************************************************
.         Check if patient is coded
.*******************************************************************************
CODD0000  MOVE      ZERO,EXIT
          PACK      KEY13,AADMNO,SP70
          CALL      RSPTECD1
          CALL      RKPTECD1
          BRANCH    OVRCD,CODD9000          * not Coded
.
          MATCH     DAADMNO,DPTEDADM
          GOTO      CODD9999 IF EQUAL       * Coded
.
CODD9000  MOVE      ONE,EXIT
CODD9999  RETURN
+
.*******************************************************************************
.         Look for a Contract ID for the DRG Code
.*******************************************************************************
CDRG0000  PACK      KEY9,ACLAIM,AFUNDH,SP70
          CALL      GETCNEFF               * Get Contract Effective From
          BRANCH    EXIT,CDRG9999
.
          MOVE      ADATE,CEFFDATE         * default to admission date
          IF        ASTAT=3
            MOVE      DDATE,KEY8           * Discharged date
          ELSE
            PACK      KEY8,CCC,CYY,CMM,CDD * Use Current date
            REP       " 0",KEY8
          ENDIF
          LOAD      CEFFDATE,CNTRCEFR,ADATE,KEY8
.
          OPEN      PMSPWAA2,"pmspwaaf"
          PACK      KEY10,PDRGCODE,SP70
          CALL      RSPMPWA2
CDRG1000  CALL      RKPMPWA2
          BRANCH    OVRCD,CDRG9999
.
          MATCH     PMPWDRGC,PDRGCODE
          GOTO      CDRG9999 IF NOT EQUAL     * Different DRG Code
.
          MOVE      PMPWCNTR,KEY6
          CALL      VALCONTR                * Validate Contract ID
          BRANCH    EXIT,CDRG1000
.
          MOVE      PMPWCNTR,CONTRCID
CDRG9999  RETURN
+
.         ***********************************************
.         Subroutine to display NZ Private screen heading
.         ***********************************************
NZSC0000  CALL       GETPNAM
          MATCH      "IBARSH",PGM
          GOTO       NZSC9999 IF EQUAL
.
          COMPARE    TWO,INVTYPE
          GOTO       NZSC1000 IF EQUAL      * Detailed invoice
.
.         Summary or Standard invoice
.
           WRITE      HTMLFILE;"<tr bgcolor=#99FFFF><b>":
                               "<td align=center><b> Date </b></td>":
                               "<td><b> Description </b></td>":
                               "<td><b> Procedure</b></td>":
                               "<td><b> Dur</b></td>":
                               "<td><b> Incl/Excl</b></td>":
                               "<td><b> Contract</b></td>":
                               "<td><b> Item </b></td>":
                               "<td align=center><b>Qty</b></td>":
                               "<td align=right><b> Unit $</b></td>":
                               "<td align=right><b> GST Exc.</b></td>":
                               "<td align=right><b> GST </b></td>":
                               "<td align=right><b> Total </b></td>":
                               "</tr>";
          GOTO       NZSC9999
.
.         Detailed Invoice
.
NZSC1000  WRITE      HTMLFILE;"<tr bgcolor=#99FFFF><b>":
                               "<td align=center><b> Date </b></td>":
                               "<td><b> Description </b></td>":
                               "<td><b> Procedure</b></td>":
                               "<td><b> Dur</b></td>":
                               "<td><b> Incl/Excl</b></td>":
                               "<td><b> Contract</b></td>":
                               "<td><b> Item </b></td>":
                               "<td align=center><b>Qty</b></td>":
                               "<td align=right><b> Unit $</b></td>":
                               "<td align=right><b> GST Exc.</b></td>":
                               "<td align=right><b> GST </b></td>":
                               "<td align=right><b> Total</b></td>":
                               "</tr>";
NZSC9999  RETURN
+
.         *******************************************************
.         Get the name of the patient (Title Given-names Surname)
.         *******************************************************
GETPNAM   CLEAR     PNAME
          REP       ", ",PTITL
          REP       ", ",PGNAME
          REP       ", ",PSNAME
          REP       ", ",PTMASNAM
          RESET     PGNAME
.
          PACK      DIM6 WITH PTITL,SP2
.
.         Get the first non-blank character of the title
.
FRSTCH    CMATCH    SP1,DIM6
          GOTO      APGNAM IF NOT EQUAL
          BUMP      DIM6
          GOTO      FRSTCH IF NOT EOS
.
.         No title, name will be Given-names Surname
.                            (or "Surname Given-names" for Singapore)
.
          IF        CFEETYP <> 9
            APPEND    PGNAME,PNAME     * original
          ELSE
            APPEND    PTMASNAM,PNAME     * Singapore
          ENDIF
          GOTO      APS1
.
.         Check for last non-blank in title, and append Given names there
.
APGNAM    APPEND    DIM6,PNAME
APG1      CMATCH    SP1,PNAME
          GOTO      APG2 IF NOT EQUAL
          BUMP      PNAME,-1
          GOTO      APG1 IF NOT EOS
.
.         Append given name at this point
.
APG2      APPEND    SP1,PNAME
          IF        CFEETYP <> 9
            APPEND    PGNAME,PNAME     * original
          ELSE
            APPEND    PTMASNAM,PNAME     * Singapore
          ENDIF
.
.         Search for last non-blank in given names to append surname there
.
APS1      CMATCH    SP1,PNAME
          GOTO      APS2 IF NOT EQUAL
          BUMP      PNAME,-1
          GOTO      APS1 IF NOT EOS
.
.         No given name or title. Name will be just Surname
.
          CLEAR     PNAME
          IF        CFEETYP <> 9
            APPEND    PTMASNAM,PNAME     * original
          ELSE
            APPEND    PGNAME,PNAME     * Singapore
          ENDIF
          GOTO      APSPS
.
.         Append surname at this point
.
APS2      APPEND    SP1,PNAME
          IF        CFEETYP <> 9
            APPEND    PTMASNAM,PNAME     * original
          ELSE
            APPEND    PGNAME,PNAME     * Singapore
          ENDIF
.
.         Append spaces to make sure there are no characters leftover from
.         previous names.
.
APSPS     APPEND    SP30,PNAME
          APPEND    SP30,PNAME
          RESET     PNAME
GETP000   RETURN
+
.**********************************************************************
.        This subroutine does the actual printing of an invoice.
.        We are only interested in invoices that have either a non-zero
.        gross total, or a non-zero number of bed days.
.**********************************************************************
PRNTINV   MOVE      SP70,PMVXMHOS
          OPEN      PMSVX1A1,"pmsvx1af"
          PACK      KEY8,AADMNO
          CALL      RDPMVX11
.
          CALL      SINVH000                 * check for template header
          BRANCH    EXIT,ENDDISP
          CALL      SINVT000                 * check for template tail
          BRANCH    EXIT,ENDDISP
.
          COMPARE   ZERO,TOTDAYS
          GOTO      PRNTINV0 IF NOT EQUAL
.
          COMPARE   ZERO,GROSSTOT
          GOTO      DISPINVE IF EQUAL
.
.         Print the invoice. This is the entry point that is used for the
.         batches of invoices from IBABIL20.
.
PRNTINV0  COMPARE   TOTDAYS,CMAXINV
          GOTO      TOOBIG IF LESS      * Invoice for too many days
          MOVE      THREE,PROGFLAG
.
PRNTINV1  MOVE      ZERO,INVPRTD
          MOVE      ZERO,GROSSTOT
          MOVE      ZERO,GSTFLAG
          IF        TOTGST <> 0
            MOVE      ONE,GSTFLAG       * set GST flag for invoice header
          ENDIF
          MOVE      ZERO,TOTGST
          MOVE      ZERO,NETTOT
          MOVE      ZERO,GOVTOTAL
          MOVE      ZERO,NODAYS
          MOVE      ZERO,ABFFLAG
.
          LOAD      IPASS USING NUMINVS OF ZERO,ONE
          LOAD      PINVTYP USING NUMINVS OF ZERO,TWO
.
          MOVE      SP10,PTDSDDRG
          MOVE      AADMNO,KEY8
          MOVE      SP8,DDATE
          CALL      RDDSCH1
          MOVE      OVRCD,DISFLG
.
          OPEN      PATDO1A1,"patdo1af"
          MOVE      ADOCTA,KEY6
          CALL      RDDOCT1
          IF        OVRCD=1
            UNPACK    SP70,DSNAME,DGNAME,DTITL
          ENDIF
.
.         Check for a "A" record in the transfer file - No "A" rec., No invoice
.         Unless it is an outstanding debt
.
          MATCH     "00000000",ADATE
          GOTO      PRNTOSD IF EQUAL
.
          PACK      KEY30 WITH AADMNO,SP20
          CALL      RDSTRAN2
          CALL      RDKTRAN2
          BRANCH    OVRCD OF MISSNGA
.
          MATCH     TADMN,AADMNO
          GOTO      MISSNGA IF NOT EQUAL
.
          CMATCH    "A",TMOVE
          GOTO      MISSNGA IF NOT EQUAL
.
          MOVE      ZERO,FORM1
          MOVE      ZERO,FORM2
.
.         PACK      KEY5,CODEA,TATYPE
.         CALL      RDCODE1
.         MOVE      TCINDC6,FORM1
.
.         Check if casepayment
.
          MATCH     ANSY,PTMICMXP
          IF        @EQUAL
.
            UNPACK   SP10,KEY4,KEY5
            UNPACK   PTMIPCMX,KEY4,KEY5     * check if it was a MBS Code
.
            MATCH    SP5,KEY5
            GOTO     PRNTOSD IF NOT EQUAL   * MBS code was entered
.
.           Check if discharged DRG Code has been allocated ?
.
            MATCH     SP4,PTDSDDRG
            GOTO      PRNTOSD IF NOT EQUAL
.
            LOAD      FORM2,FORM1,ZERO,ZERO,PTCNDSDY,ZERO,PTCNDMSO
            COMPARE   ONE,WEBFLAG
            GOTO      PRNTOSD IF NOT EQUAL
.
            IF        FORM2 = 1
              CLEAR     WEBTITLE
              APPEND    "No DRG Code Allocated -",WEBTITLE
              APPEND    " Can't print Invoice.",WEBTITLE
              APPEND    SP70,WEBTITLE
              RESET     WEBTITLE
              CALL      WEBERR00
              MOVE      ONE,EXIT
              RETURN
            ELSE
              IF        FORM2 = 2
                CLEAR     WEBTITLE
                APPEND    "No DRG Code Allocated -",WEBTITLE
                APPEND    " Can't print Invoice.",WEBTITLE
                APPEND    SP70,WEBTITLE
                RESET     WEBTITLE
                CALL      WEBERR00
                MOVE      ONE,EXIT
                RETURN
              ENDIF
            ENDIF
          ENDIF
.
.         Get the patient's name
.
PRNTOSD   CALL      GETPNAM
.
.         Get any pending cash receipts
.
          CALL      GETCASH
.
          COMPARE   SIX,PROGFLAG
          GOTO      PRONLY IF EQUAL         * Print Disc.Billing Statement
.
          IF        CFEETYP=9
            COMPARE   FIVE,PROGFLAG         * Johns Hopkins
            GOTO      PRONLY IF EQUAL       * Print invoice only, no update
          ENDIF
.
          IF        IBCNUGST=2
            CALL      PMBS0000           * Check with primary MBS
            BRANCH    EXIT,ENDDISP       * yes, can not print invoice
          ENDIF
.
.
.         Get the next invoice number and write a zero invoice record
.
          OPEN      PATINVR1,"patinvrf"
          OPEN      PATIRNG1,"patirnge"
.
NXTINVP   MOVE      CINVRIP,KEY3
          CALL      TRIRNG1
          BRANCH    OVRCD,NOIRNGE,RNGEFULL
.
          MOVE      IRNEXT,CNEXTINV
          MOVE      CNEXTINV,KEY8
          CALL      RDINV1
          COMPARE   ZERO,OVRCD
          GOTO      NXTINVP IF EQUAL
.
.         Generating Invoice
.
          CALL      ZEROINV
          CALL      FRSDAY00               * Check first day of period
.
.         Open spool file for invoice
.
PRONLY    IF        WEBFLAG=0
            CALL      OPNPRINT
          ENDIF
.
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,A10DATE
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,D10DATE
.
.         Use the discharge diagnosis if available
.
          BRANCH    DISFLG OF CNTDDAT
          MATCH     SP70,DDIAG
          GOTO      CNTDDAT IF EQUAL
          MOVE      DDIAG,ADIAG1
          MOVE      DDIAG2,ADIAG2
.
          MATCH     SP9,DFMBS
          GOTO      CNTDDAT IF EQUAL
          MOVE      DFMBS,AMBS
.
CNTDDAT   CALL      SETUPIV
          MOVE      ZERO,TOTDAYS
.
.         Set the claim type indicator if this is a claim
.
          MOVE      ZERO,CLAIM
          PACK      KEY5 WITH CODECL,ACLAIM
          CALL      RDCODE1
          BRANCH    OVRCD OF ENDSIV
.
.         Check the indicators for a W, M, or V (these are the claims)
.
          MOVE      ZERO,FORM2
SETIVLP   ADD       ONE,FORM2
          COMPARE   SIX,FORM2
          GOTO      ENDSIV IF NOT LESS
.
          LOAD      ANS USING FORM2 OF TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
.
          MATCH     "W",ANS
          GOTO      SVISCLM IF EQUAL
          MATCH     "M",ANS
          GOTO      SVISCLM IF EQUAL
          MATCH     "V",ANS
          GOTO      SVISCLM IF EQUAL
          GOTO      SETIVLP
.
.         We have a claim
.
SVISCLM   REP       "W1M2V3",ANS
          MOVE      ANS,CLAIM
.
.         Set up the person responsible for the account
.
ENDSIV    CALL      GPRESP00  
.
.         Print the invoice heading section
.
          MOVE      AADMNO,PINVADM
          MOVE      AURNO,PINVUR
          MOVE      ACLAIM,PINVCLM
          MOVE      THREE,PINVSYS
          MOVE      SP6,PINVSITE
          MOVE      ZERO,ERRFLAG
          CALL      HEAD
.
          MOVE      SP70,PMVXMHOS
          OPEN      PMSVX1A1,"pmsvx1af"
          PACK      KEY8,AADMNO
          CALL      RDPMVX11
.
.         Generate the body and tail of the invoice
.
          CALL      CALCTBI
          CALL      SETUPTRN
.
          MATCH     "1",PTCNPPIN      * New Patient Invoice functionality
          IF        @EQUAL
            CALL      CPIN0000        * Check if Patient invoice is raised
            IF        PREBFLAG=1 & DBSFLAG=0 & ISBLFLAG=0
              CALL      RBINV000      * Print Rebate Invoice
              GOTO      ENDSIV2
             ENDIF
          ENDIF
.
          CALL      DLINE1
          BRANCH    EXIT,ENDOPTN
.
.
          COMPARE   SIX,PROGFLAG
          GOTO      ENDOPTN IF EQUAL         * Print Disc.Billing Statement
.
          IF        CFEETYP=9
            COMPARE   FIVE,PROGFLAG
            GOTO      ENDOPTN IF EQUAL       * print only - no update
          ENDIF
.
ENDSIV2  CALL      UPIVR00                  * Update invoice record
.
          MATCH     "1",RACLINDC
          IF        @EQUAL
.           MATCH     SP70,PTCNECLO                * eclipse outbound path set?
.           IF        !@EQUAL & !@EOS
.             CALL      SECLX000
.           ENDIF
.
            MATCH   "1",PTCNWSIN
            IF      @EQUAL 
              MATCH   "1",PTCNIHCW
              IF      @EQUAL
                CALL    SWEBX000
              ENDIF
            ELSE
              MATCH     SP70,PTCNECLO              * eclipse outbound path set?
              IF        !@EQUAL & !@EOS
                CALL      SECLX000
              ENDIF
            ENDIF
.
          ENDIF
.
          MATCH     "1",PTCNPPIN
          GOTO      ENDPIVS IF EQUAL   * print New Patient Invoice functionality
.
          IF        IPASS=1 & IPATFLAG=1
            MATCH     "1",PTCNADEB       * Automatic reassign debt for Pat.Inv.
            IF        @EQUAL
              PROC      RDEI0000         * Update Reassign debt
            ENDIF
          ENDIF
.
.         If we are printing two invoices, loop back and print the next invoice
.
          BRANCH    IPASS OF NXTINVA,ENDPIVS
          GOTO      ENDPIVS
.
NXTINVA   MOVE      TWO,IPASS
          MOVE      ZERO,GROSSTOT
          MOVE      ZERO,NETTOT
          MOVE      ZERO,GOVTOTAL
          MOVE      ZERO,NODAYS
          MOVE      ONE,PINVTYP
.
          IF        WEBFLAG=1
            CALL      CLSPRINT
            CALL      OPNPRINT
          ENDIF
.
          GOTO      PRNTOSD
.
.         Update the admission record with the changes
.
ENDPIVS   PACK      KEY8,AADMNO
          CALL      RDMISS1  
          IF        IPASS=2
            ADD       TWO,AINVPRT                       * No of invoices
          ELSE
            ADD       ONE,AINVPRT                       * No of invoices
          ENDIF
          ADD       ONE,ATRANNO                       * Last transaction number
.
. If we are backdating invoices then we bill then the last invoice date 
. (Alacdte) is set to one day after the invoice date. This is because we always
. include the night of the "to" date when billing and we don't want to bill
. them twice for the same night !!! 
.
          IF        BDTEFLAG = 1
.
.           Backdate invoice will have invoice date on the current date
.           the ALACDTE will not be equal to the invoice date
.
.           UNPACK    PINVDTE,CCENT,CYEAR,CMON,CDAY
.           UNPACK    BACKDATE,CCENT,CYEAR,CMON,CDAY
.           MOVE      CCENT,CC
.           MOVE      CYEAR,YY
.           MOVE      CMON,MM
.           MOVE      CDAY,DD
.
.           MOVE      CC,TCENT
.           MOVE      YY,TYEAR
.           MOVE      MM,TMON
.           MOVE      DD,TDAY
.
            IF        (IPATFLAG=1 | IPATFLAG=2) & NCHACCOM=1
              GOTO      WHIRLP
            ENDIF
            MOVE      SP8,DDATE
            MOVE      AADMNO,KEY8
            CALL      RDDSCH1
            IF        OVRCD = 0
              MATCH     PINVDTE,DDATE
              IF        @EQUAL
                MOVE        ONE,ADSCHI
                GOTO        WHIRLP
              ENDIF
            ENDIF
          ELSE
            IF       ASTAT = 3
              IF        (IPATFLAG=1 | IPATFLAG=2) & NCHACCOM=1
                GOTO      WHIRLP
              ENDIF
.
.        This indicator says that a discharge invoice has been printed.
              MOVE     ONE,ADSCHI
            ENDIF
          ENDIF
.
.         If we are printing Patient Only invoice and Accommodation wasn't
.         printed, do not change ALACDTE
.
WHIRLP    IF        (IPATFLAG=1 | IPATFLAG=2) & NCHACCOM=1
            MOVE      ZERO,ADSCHI
            GOTO      UPDATEA
          ENDIF
.
          PACK      ALACDTE WITH TCENT,TYEAR,TMON,TDAY * Last Accommodation date
          REP       " 0",ALACDTE
.
          IF        BDTEFLAG = 1 & ASTAT = 3
            MATCH     ALACDTE,DDATE 
            IF        @EQUAL
             MOVE      ONE,ADSCHI
            ENDIF
          ENDIF
.
.         Update the admission record
.
UPDATEA   CALL      UPLMISS1
ENDOPTN   RETURN
.
.**********************************************************************
.         Update the invoice record with the appropriate details
.**********************************************************************
UPIVR00   MOVE      CNEXTINV,KEY8
          CALL      RDINV1
.
          MOVE      AADMNO,KEY8                     * Make sure we have got
          CALL      RDMISS1                         * the correct U/R number
.
          MOVE      AADMNO,PINVADM
          MOVE      AURNO,PINVUR
          MOVE      PTMASNAM,PINALPHA
          MOVE      GROSSTOT,PINVAMT
          MOVE      REBTOT,PINVREB
          CALL      PMOI0000
          SUB       PMOIETY,REBTOT
          MOVE      ZERO,PTINGSTA
          MOVE      TOTDISC,PTINDISC
          MOVE      ROUNDAMT,PTINROUN
.
          MATCH     "1",PTCNPPIN            * New Patient Invoice functionality
          GOTO      UPIVR10 IF NOT EQUAL
.
          MOVE      ZERO,PINVTYP            * Normal Invoice
          IF        IPATFLAG=2
            MOVE      TWO,PINVTYP           * Patient Invoice
          ELSE
            IF        PREBFLAG=1
              MOVE      ONE,PINVTYP         * Insurance Invoice
              MOVE      GROSSTOT,PINVREB
            ENDIF
          ENDIF
          GOTO      UPIVR20
.
UPIVR10   IF        CFEETYP=5
            MOVE      ONE,PINVTYP           * Insurance Invoice
            IF        PRPATINV=1
              MOVE      TWO,PINVTYP         * Patient Invoice
            ENDIF
          ENDIF
.
.         Check if we are using Aust.GST or NZ Priv GST
.
UPIVR20
          IF        IBCNUGST=2 | IBCNUGST=3
            ADD       TOTGST,PINVAMT
            MOVE      TOTGST,PTINGSTA      * GST amount
          ENDIF
.
          MOVE      THREE,PINVSYS
          MOVE      SP6,PINVSITE
          MOVE      ACLAIM,PINVCLM
.
          IF        CMXFLAG=1
            MOVE      ONE,PTINCMIX                  * Casemix invoice
            MOVE      CMCODE,PTINCMCD       * DRG code used for billing
            IF        PTCNMVCP=1
              MOVE      MVDAYS,PTINMVDY     * Mech. Vent. Billing Days
            ENDIF
          ELSE
            IF        ABFFLAG=1
              MOVE      THREE,PTINCMIX      * ABF invoice
            ENDIF
          ENDIF
.
          PACK      PTINCNID,CONTRCID,SP70    * Contract ID
          PACK      PTINUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTINUDAT             * date updated
          CLOCK     TIME,PTINUTIM             * time updated
          MOVE      USERID,PTINUUID           * web user id
.
          CALL      UPINV1
.
          IF        ABFFLAG=1
            CALL      UPAB0000                * Update Inv. no to ABF file
          ENDIF
          PROC      FLAGDEBT                * set outstanding debt indicator
.
          IF        CFEETYP=9
            CALL      UIPR0000              * Update Insurance Provider for JH
          ENDIF
.
.         CCPS EDI Extract                                            *I-103368
          CALL      CCPS0000                * extract Invoice data for CCPS
.
.        Do Cash updates, and then, if this is a discharged patient,
.        remove the patient from the list of patient who will still have
.        invoices pending. Then print invoice.
.
          CALL      CASHUPD
.
.         COMPARE   TWO,IPATFLAG
.         GOTO      UPIVR99 IF EQUAL        * Printing New Patient Portion Inv.
.
          IF        IPATFLAG=1 & NCHACCOM=1
            GOTO      UPIVR99
          ENDIF
.
          IF        BDTEFLAG = 1
            MOVE      AADMNO,KEY8
            CALL      RDDSCH1
            BRANCH    OVRCD,UPIVR99
.
.           MATCH     DDATE,PINVDTE
            MATCH     DDATE,BACKDATE
            GOTO      UPIVR99 IF LESS 
            CALL      DELIPEND
          ELSE
            COMPARE   THREE,ASTAT
            CALL      DELIPEND IF EQUAL
          ENDIF
UPIVR99   RETURN
+
.**********************************************************************
.         Update Invoice number to ABF file
.**********************************************************************
UPAB0000  READ      CONTROLF,HUND28;*105,PTCNUABF
          MATCH     "1",PTCNUABF
          GOTO      UPAB9999 IF NOT EQUAL   * Not using ABF
.
          OPEN      PMSABFA1,"pmsabfaf"
.
UPAB1000  PACK      KEY19,AADMNO,SP70
          CALL      RSPMABF1
          CALL      RKPMABF1
          BRANCH    OVRCD,UPAB9999
.
          MATCH     DAADMNO,PMABADMN       * Same admission no ?
          GOTO      UPAB9999 IF NOT EQUAL
.
          MATCH     SP70,PMABINVN
          GOTO      UPAB9999 IF NOT EQUAL  * Invoice must be blank
.
          MOVE      CNEXTINV,PMABINVN
          IF        ASTAT=3
            MOVE      PTDSDDRG,PMABADRG    * Use Discharged DRG
          ENDIF
          CALL      UPPMABF1
          GOTO      UPAB1000
.
UPAB9999  RETURN
+
.**********************************************************************
.         Set up the person responsible for the account
.**********************************************************************
GPRESP00  RESET     PGNAME
          MOVE      PGNAME,ANS
          PACK      DIM8 WITH SP1,PTITL,SP1,ANS,DOT
.
GPRESP20  CMATCH    SP1,DIM8
          GOTO      GPRESP30 IF NOT EQUAL
          BUMP      DIM8
          GOTO      GPRESP20 IF NOT EOS
.
GPRESP30  PACK      PFNAME WITH DIM8,PTMASNAM
          REP       ", ",PFNAME
          MOVE      PFNAME,PKNAME
          MOVE      PSUBRB,PKSUBR
          MOVE      PADD1,PKADD1
          MOVE      PADD2,PKADD2
          MOVE      PPOST,PKPOST
          MOVE      SP10,PKRELP
.
          COMPARE   "22",PTCNINVL
          GOTO      GPRESP40 IF NOT EQUAL       * Not Johns Hopkins invoice
.
          CALL      JHPRA000                    * Set default PRFA name
          MOVE      PFNAME,PKNAME
.
          MATCH     "PRFA",EXPPAYER
          GOTO      GPRESP42 IF EQUAL
          MATCH     SP70,EXPPAYER
          GOTO      GPRESP42 IF EQUAL
.
          PACK      KEY14,EXPPAYER,ZERO8,SP70
          CALL      RDFUND1
          IF        OVRCD=0
            PACK      PKNAME,HFNAME,SP70
            PACK      PKADD1,HFADD1,SP70
            PACK      PKADD2,HFADD2,SP70
            PACK      PKSUBR,HFADD3,SP70
            PACK      PKADD4,HFADD4,SP70
            PACK      PKPOST,HFPCODE,SP70
            GOTO      GPRESP90
          ENDIF
.
GPRESP40  PACK      PFNAME WITH PKNAME,SP30
GPRESP42  OPEN      PATRE1A1,"patre1af"
          MOVE      AADMNO,KEY8
          CALL      RDRESP1
          BRANCH    OVRCD OF GPRESP99
.
          COMPARE   "22",PTCNINVL
          GOTO      GPRESP50 IF NOT EQUAL
          MATCH     "SELF",PKRELP
          GOTO      GPRESP99 IF EQUAL
.
GPRESP50  MATCH     "1",PTCNDEES             * using deceased PRFA indicator
          GOTO      GPRESP85 IF NOT EQUAL    * no
.         
          CMATCH    "$",PKNAME
          IF        @EQUAL
            STRIP     PTCNDDES
            MOVE      PTCNDDES,PFNAME
            UNPACK    PKNAME,DIM2,KEY78      * ignore "$ "
            PACK      PFNAME,PFNAME,SP1,KEY78,SP30
            GOTO      GPRESP99
          ENDIF
.
GPRESP85  COMPARE   ONE,PTCNPAOF             * using 'parent of'?
          GOTO      GPRESP90 IF NOT EQUAL    * no
.
.---- If first character of PKNAME is "@" then replace with "Parent of"
.
          CMATCH    "@",PKNAME
          IF        @EQUAL 
            MOVE      "Parent of ",PFNAME
            UNPACK    PKNAME,KEY2,KEY78      * ignore "@ "
            PACK      PFNAME,PFNAME,KEY78,SP30
            GOTO      GPRESP99
          ENDIF
.
GPRESP90  PACK      PFNAME WITH PKNAME,SP30
.
GPRESP99  RETURN
.
.**********************************************************************
.         Invoice is for zero amount, and for zero bed days
.**********************************************************************
.
DISPINVE  MATCH     "IBARSH",PGM
          GOTO      ENDDISP IF EQUAL
.
          WRITE      HTMLFILE;"<tr><center><blink><h2><b>No Invoice details":
                                " to be printed</b></h2></blink></center><tr>";
          GOTO       ENDDISP
.
.         Missing "A" record from transfer file
.
MISSNGA   MOVE      THREE,ERRFLAG
          BRANCH    WEBFLAG,ENDDISP
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Error : Missing Transfer File record - No invoice ":
                    "produced. Contact IBA **";
          GOTO      ENDDISP
.
.         Invoice for too many bed days
.
TOOBIG    MOVE      ONE,ERRFLAG
          BRANCH    WEBFLAG,ENDDISP
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Error : Invoice for more that ",CMAXINV," days":
                    " - No invoice produced **";
          GOTO      ENDDISP
.
.         Invalid invoice range in the control file
.
NOIRNGE   MOVE      TWO,ERRFLAG
          BRANCH    WEBFLAG,ENDDISP
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Error : Invalid Invoice Range in Control file **";
          GOTO      ENDDISP
.
.         No more invoices available in invoice range
.
RNGEFULL  MOVE      TWO,ERRFLAG
          BRANCH    WEBFLAG,ENDDISP
.
          DISPLAY   *P1:24,*EL,*B,*V2LON:
                   "** Error : Next invoice number outside of valid range **";
ENDDISP   RETURN
+
.----------------------------------------------------------------------
.         WRITE A TRANSACTION,DBTRA
.----------------------------------------------------------------------
WRITETRN  COMPARE   SIX,PROGFLAG
          RETURN    IF EQUAL                * Print Discharge Billing Statement
          IF        CFEETYP=9
            COMPARE   FIVE,PROGFLAG
            RETURN    IF EQUAL              * Print invoice only (no update)
          ENDIF
.
WRITE100  CALL      NXTTRAN1
          MOVE      CNEXTINV,TREF
          PACK      KEY22 WITH TADMNO,TREF,TTRANSN1
          CALL      RDADTRN1
          COMPARE   ZERO,OVRCD
          GOTO      WRITE100 IF EQUAL     * Try the next transaction number
.
          MOVE      CNEXTINV,TREF
          MOVE      FDAY1,TFDAY
          MOVE      FMON1,TFMONTH
          MOVE      FYEAR1,TFYEAR
          MOVE      FCENT1,TFCENT
.
          MOVE      TDAY1,TTDAY
          MOVE      TMON1,TTMONTH
          MOVE      TYEAR1,TTYEAR
          MOVE      TCENT1,TTCENT
          MOVE      SP20,TRECEIPT
          MOVE      SP9,TITEMNO
.
          MOVE      ONE,TINVPRT
          MOVE      CDD,XDAY
          MOVE      CMM,XMON
          MOVE      CCC,XCENT
          MOVE      CYY,XYEAR
.         MOVE      ZERO,TPAYTYPE
.         MOVE      ZERO,PTDTEPSD           * Episode type
          MOVE      LOWFLAG,PTDTDTYP        * Day type
.         MOVE      SP10,PTDTDEPS
          PACK      PTDTEFFD,PINVDTE
.         MOVE      ZERO,PTDTCCU
.         MOVE      SP6,PTDTSURG
.         MOVE      ANSN,PTDTSSAP           * SAP Flag           v6.09.06
          MOVE      SP70,PTDTUNIQ
          MOVE      SP70,PTDTTMNO
          MOVE      SP70,PTDTPMBS
          MOVE      SP70,PTDTPCOD
.
          IF        TRECTYPE <> 1
            MOVE      SP3,PTDTBTYP          * Clear if not an accom record
          ENDIF
.
          PACK      KEY22 WITH TADMNO,TREF,TTRANSN1
          IF        CFEETYP=5 & WRDTFLAG=1
            PACK      PTDTCPRC,PRIMMBS,SP70
            CALL      WRNZRDT1
          ELSE
            CALL      WRDTRN1
          ENDIF
.
.             SET UP A TRANSACTION
.
SETUPTRN  PACK      TDCODE,AURNO
          MOVE      ZERO,TPATAMTG
          MOVE      ZERO,TPATAMTT
          MOVE      ZERO,TPATAMTP
          MOVE      ZERO,TPATAMTI
          MOVE      ZERO,TPATAMTH
          MOVE      ZERO,PTDTLSPT
          MOVE      ZERO,PTDTLSRB
          MOVE      ZERO,PTDTGSTA
          MOVE      ZERO,PTDTGSTM
          MOVE      ZERO,PTDTGSTL
          MOVE      SP10,PTDTGSTC
.
          MOVE      "DB",TTYPE
          MOVE      ZERO,TCLAIM
          MOVE      ZERO,TREBATE
          MOVE      ZERO,PTTRAEND
.         MOVE      SP6,PTDTSURG
.         MOVE      ZERO,PTDTAPAY
          MOVE      SP70,PTDTUNIQ
          MOVE      SP8,PTDTTIME                                       *I-61746
          MOVE      SP1,PTDTCRST                                       *I-61746
          PACK      TSPARE,SP30,SP30
.
.         Check with the program flag (Print invoices or Discharge program)
.
          MOVE      "INVOICES",TBATCHN
          COMPARE   ZERO,DISPROG
          RETURN    IF EQUAL
.
          MOVE      "DISCHRGE",TBATCHN
          RETURN
+
.         Subroutine to get the next transaction number
.
NXTTRAN1  MOVE      AADMNO,TADMNO
          MOVE      TADMNO,PVIBILL
          MOVE      PVIBILL,KEY8
          CALL      TRVISA1
          MOVE      PVITRAN,TTRANSN1
          MOVE      PVITRAN,PMMITRAN
          MOVE      PMMITRAN,ATRANNO
          RETURN
.
DATECHK   MOVE      ZERO,OVRCD
.
          COMPARE   ONE,FDAY
          GOTO      DATEINV IF LESS
.
          COMPARE   "32",FDAY
          GOTO      DATEINV IF NOT LESS
.
          COMPARE   ONE,FMON
          GOTO      DATEINV IF LESS
.
          COMPARE   "13",FMON
          GOTO      DATEINV IF NOT LESS
.
          COMPARE   ZERO,FYEAR
          RETURN    IF NOT LESS
.
DATEINV   CALL      OVERCOND
          RETURN
.
.*************************************************************************** 
.         Subroutine to get the total cash amount currently entered
.*************************************************************************** 
GETCASH   COMPARE   ZERO,ABFFLAG
          GOTO      CASHLP9 IF NOT EQUAL
.
          OPEN      COMDEPA2,"comdepaf"
          OPEN      RCPDTEA1,"rcpdteaf"
          OPEN      RCPDTEA6,"rcpdteaf"
          OPEN      RCPBNKA1,"rcpbnkaf"
.
          MOVE      ZERO,CSAMT
          MOVE      ZERO,CASHTOT
          MOVE      ZERO,CASHFLAG
          MOVE      ZERO,DSCNFLAG
          IF        IPATFLAG=1 | IPATFLAG=2
            MATCH     "1",PTCNADEP
            GOTO      CASHLP9 IF NOT EQUAL    * No deposits can be applied
          ENDIF
.
          MOVE      AADMNO,DAADMNO
          MOVE      "0",CMDESTAT
          PACK      KEY26,AADMNO,CMDESTAT,SP70
          CALL      RSCMDEP2
CASHLP1   CALL      RKCMDEP2
          BRANCH    OVRCD,CASHLP3
.
          MATCH     DAADMNO,CMDEADMN
          GOTO      CASHLP3 IF NOT EQUAL
          MATCH     "0",CMDESTAT
          GOTO      CASHLP3 IF NOT EQUAL
.
          PACK      KEY17,CMDERECN,CMDETCNT
          CALL      RDRCDTE1
          BRANCH    OVRCD,CASHLP1
.
          IF        RCDTDISC<>0
            MOVE      ONE,DSCNFLAG
          ENDIF
          ADD       RCDTAMNT,CSAMT
          UNPACK    CMDETDAT,XCENT,XYEAR,XMON,XDAY
          MOVE      XDAY,CSDAY
          MOVE      XMON,CSMON
          MOVE      XYEAR,CSYEAR
          MOVE      XCENT,CSCENT
          GOTO      CASHLP1
.
CASHLP3   COMPARE   ZERO,CSAMT
          GOTO      CASHLP5 IF EQUAL
          MOVE      ONE,CASHFLAG
.
.         Check for any non-banked deposits
.
CASHLP5   MOVE      ZERO,NBNKAMNT
          PACK      KEY25,AADMNO,SP70
          CALL      RSRCDTE6
CASHLP7   CALL      RKRCDTE6
          BRANCH    OVRCD,CASHLP9
.
          MATCH     DAADMNO,RCDTVIST
          GOTO      CASHLP9 IF NOT EQUAL
.
          MATCH     SP70,RCDTINVN          * Invoice number must be blank
          GOTO      CASHLP7 IF NOT EQUAL
          MATCH     SP70,RCDTRELD
          GOTO      CASHLP7 IF NOT EQUAL   * has been released
.
          PACK      KEY12,RCDTRECN
          CALL      RDRCBNK1
          BRANCH    OVRCD,CASHLP7
          MATCH     "1",RCBNSTAT            * check if cancelled
          GOTO      CASHLP7 IF EQUAL
.
          IF        RCDTDISC<>0
            MOVE      ONE,DSCNFLAG
          ENDIF
          ADD       RCDTAMNT,NBNKAMNT
          GOTO      CASHLP7
.
CASHLP9   RETURN
.
+
.      Invoice was printed, so post any cash receipts to DTRAN and INVR files
.
CASHUPD   COMPARE   ZERO,ABFFLAG
          GOTO      CASHRET IF NOT EQUAL
.
          OPEN      COMDEPA2,"comdepaf"
          OPEN      RCPDTEA1,"rcpdteaf"
          OPEN      RCPBDTA1,"rcpbdtaf"
          READ      CONTROLF,HUND16;*209,PTCNADEP
          MOVE      AADMNO,DAADMNO
.
CASHULP   MOVE      "0",CMDESTAT
          PACK      KEY26,AADMNO,CMDESTAT,SP70
          CALL      RSCMDEP2
CASHUL10  CALL      RKCMDEP2
          BRANCH    OVRCD OF POST3000
.
          MATCH     DAADMNO,CMDEADMN
          GOTO      POST3000 IF NOT EQUAL
.
          MATCH     "0",CMDESTAT
          GOTO      POST3000 IF NOT EQUAL
.
          MOVE      ZERO,RCDTAMNT
          PACK      KEY17,CMDERECN,CMDETCNT
          CALL      RDRCDTE1
          BRANCH    OVRCD,CASHUL40
.
.        If we are printing Patient Only invoice, check if payment is included
.
          COMPARE   TWO,IPATFLAG
          GOTO      CASHUL18 IF EQUAL        * Printing New Patient Portion Inv.
.
          COMPARE   ONE,IPATFLAG
          GOTO      CASHUL20 IF NOT EQUAL
.
.         Check if deposits tobe displayed on Patient Invoice
.
CASHUL18  MATCH     "1",PTCNADEP
          GOTO      CASHUL10 IF NOT EQUAL     * no cash applied
.
          MATCH     "9",RCDTSFLG
          GOTO      CASHUL20 IF EQUAL         * include on invoice
.
          MOVE      SP70,RCDTSFLG
          PACK      RCDTUDAT,CCC,CYY,CMM,CDD
          REP       " 0",RCDTUDAT             * date updated
          CLOCK     TIME,RCDTUTIM             * time updated
          MOVE      USERID,RCDTUUID           * web user id
          CALL      UPRCDTE1
          GOTO      CASHUL10
.
CASHUL20  PACK      RCDTINVN,SP4,PINVNO
          CALL      UPRCDTE1
.
CASHUL40  UNPACK    CMDETDAT,XCENT,XYEAR,XMON,XDAY
          MOVE      XDAY TO IDAY
          MOVE      XMON TO IMON
          MOVE      XCENT TO ICENT
          MOVE      XYEAR TO IYEAR
          MOVE      RCDTAMNT TO TPATAMTT
          MOVE      CMDERECN TO TRECEIPT
.
          MOVE      PINVADM,TADMNO
          MOVE      PINVADM,AADMNO
          MOVE      AADMNO,KEY8
.
          CALL      RDMISS1
.
          COMPARE   ZERO,OVRCD
          GOTO      CASHUPD1 IF EQUAL
.
          IF        WEBFLAG=0
            DISPLAY   *P1:24,*EL,*B,*V2LON:
                      "** NO ADMISSION MASTER FILE RECORD ":
                      "FOR THIS RECEIPT.  PLEASE CALL I.B.A. **",*W6;
          ENDIF
          STOP
.
CASHUPD1  MOVE      "P",TTYPEDEB
          CALL      NXTTRAN1      * Get the next transaction number
          MOVE      PINVNO,TREF
          PACK      KEY22 WITH TADMNO,TREF,TTRANSN1
          CALL      RDADTRN1
          COMPARE   ZERO,OVRCD
          GOTO      CASHUPD1 IF EQUAL     * Try the next transaction number
.
          MOVE      CMDETDAT,XDATE
          REP       " 0",XDATE
.
          IF        BDTEFLAG = 1
            MATCH     PINVDTE,XDATE
            IF        @LESS
              UNPACK    XDATE,XCENT,XYEAR,XMON,XDAY
            ELSE
              UNPACK    PINVDTE,XCENT,XYEAR,XMON,XDAY
            ENDIF
            PACK      CMDETDAT,XCENT,XYEAR,XMON,XDAY
            REP       " 0",CMDETDAT
            GOTO      CASHUPD2
          ENDIF
.
.         Are invoice dates are always todays date ?
.
          COMPARE   ZERO,CINV1ST
          GOTO      CASHUPD2 IF EQUAL
.
          MOVE      CMDETDAT,XDATE
          PACK      DIM8,CCC,CYY,CMM,CDD
          REP       " 0",DIM8
.
.         Check if the cash entered today (the same time invoice printed)
.
          MATCH     XDATE,DIM8
          GOTO      CASHUPD2 IF NOT EQUAL
.
.         Cash date must be equal to invoiced date
.
          MOVE      PINVDTE,CMDETDAT
.
CASHUPD2  UNPACK    CMDETDAT,XCENT,XYEAR,XMON,XDAY
          MOVE      XDAY,TFDAY
          MOVE      XMON,TFMONTH
          MOVE      XYEAR,TFYEAR
          MOVE      XCENT,TFCENT
.
          MOVE      ZERO,TREBATE
          MOVE      FIVE,TRECTYPE        *** RECORD TYPE CASH
.
.         Read the first record of Bank details file to get the payment type
.         ie. Cash, Cheque
.
          MOVE      SP70,PTDTPCOD
          OPEN      RCPBDTA1,"rcpbdtaf"
          PACK      KEY15,RCDTRECN,SP70
          CALL      RSRCBDT1
          CALL      RKRCBDT1
          IF        OVRCD=0
            MATCH     RCDTRECN,RCBDRECN      * Same receipt number?
            IF        @EQUAL
              MOVE      RCBDPCOD,PTDTPCOD
            ENDIF
          ENDIF
.
          MOVE      ONE,TINVPRT         *** SET AS ALREADY PRINTED
          MOVE      ZERO,TTDAY
          MOVE      ZERO,TTMONTH
          MOVE      ZERO,TTCENT
          MOVE      ZERO,TTYEAR
          MOVE      "PY",TTYPE
          MOVE      SP30,TITEMNO
          MOVE      SP10,TSESSION
          MOVE      SP10,TMISGRP
          MOVE      PINVNO,TREF
          MOVE      "1",TPAYTYPE     ***** set up Payment TYPE cash
          MOVE      ZERO,TCLAIM
          MOVE      ZERO,PTDTDTYP
.
          MOVE      SP70,PTDTBTYP
          MOVE      "PATIENT",TDDESC
          PACK      TDCODE,PURNO
          MOVE      ZERO,TPATAMTG
          MOVE      ZERO,TPATAMTH
          MOVE      ZERO,TPATAMTI
          MULT      SEQ,TPATAMTT            **** MAKE IT NEGATIVE
          MOVE      TPATAMTT,TPATAMTP
          MOVE      ZERO,PTDTLSPT
          MOVE      ZERO,PTDTLSRB
.         MOVE      ZERO,PTDTEPSD        * Episode type
          PACK      PTDTEFFD,PINVDTE
          MOVE      ZERO,PTDTCCU
.         MOVE      SP6,PTDTSURG
.         MOVE      ZERO,PTDTAPAY
          MOVE      ZERO,PTDTGSTA
          MOVE      ZERO,PTDTGSTM
          MOVE      ZERO,PTDTGSTL
          MOVE      ZERO,PTDTADPT
          MOVE      ZERO,PTDTADRB
          MOVE      SP10,PTDTGSTC
          MOVE      CMDEDTYP,TDEPTYP
          MOVE      SP70,PTDTUNIQ
          MOVE      SP70,PTDTBTYP
          MOVE      SP70,PTDTTMNO
          MOVE      SP70,PTDTPMBS
.
          PACK      KEY22 WITH TADMNO,TREF,TTRANSN1
          CALL      WRDTRN1
.
          IF        RCDTDISC<>0
            CALL      PTDIS000                   * Update Discount Receipt
          ENDIF
.
          ADD       ONE,ATRANNO
          CALL      UPLMISS1
.
          MOVE      "1",CMDESTAT            * set Deposit status to 'Applied'
          PACK      CMDEAINV,SP4,TREF,SP70
.         PACK      CMDEREFD,PINVDTE
          PACK      CMDEREFD,CCC,CYY,CMM,CDD,SP70     * Set to current date
          REP       " 0",CMDEREFD           * Date deposit applied
          CALL      IBACLOCK
          MOVE      CTIMEIS,CMDEREFT        * Time invoice raised
.
          PACK      CMDEUDAT,CCC,CYY,CMM,CDD
          REP       " 0",CMDEUDAT           * date updated
          CLOCK     TIME,CMDEUTIM           * time updated
          MOVE      USERID,CMDEUUID         * web user id
          CALL      UPCMDEP2
.
.         UPDATE LAST INVOICE
.
          OPEN      PATINVR1,"patinvrf"
          MOVE      CNEXTINV,KEY8
          CALL      RDINV1
.
          MOVE      RCDTAMNT,TPATAMTT
          MULT      SEQ,TPATAMTT            **** MAKE IT NEGATIVE
          ADD       TPATAMTT,PINVPATA
.
          ADD       RCDTDISC,PINVJOUR
          MULT      SEQ,PINVJOUR            **** MAKE IT NEGATIVE
.
.         Check if the invoice now balances
.
          MOVE      PINVAMT,TOTAMT       * Invoice amount +
          ADD       PINVPATA,TOTAMT      * Patient payments +
          ADD       PINVHFDA,TOTAMT      * H.F payments +
          ADD       PINVOTHA,TOTAMT      * Other payments +
          ADD       PINVJOUR,TOTAMT      * Journals = amount outstanding
.
          MOVE      "99999999",PINVBLCD  * Assume it doesn't balance
.
          COMPARE   ZERO,TOTAMT          * Does it balance ?
          GOTO      POST2000 IF NOT EQUAL
.
.         Invoice is now balanced. Record the date that this occured
.
          PACK      PINVBLCD,CCC,CYY,CMM,CDD
          REP       " 0",PINVBLCD
.
POST2000  PACK      PTINUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTINUDAT             * date updated
          CLOCK     TIME,PTINUTIM             * time updated
          MOVE      USERID,PTINUUID           * web user id
.
          CALL      UPINV1
          PROC      FLAGDEBT                * set outstanding debt indicator
.
          MOVE      PINVNO,FININVN
          MOVE      PINVADM,FINADMN
          MOVE      PINVUR,FINURNO
.
          MOVE      "B",FINTYPE
          PACK      FINCODE,ANSA,SP70
.
          MOVE      RCDTAMNT,FINAMT
.         PACK      FINDATE,CCC,CYY,CMM,CDD
          PACK      FINDATE,PINVDTE
          REP       " 0",FINDATE
          MOVE      THREE,FINSYS
          MOVE      SP6,FINSITE
          MOVE      ZERO,FGSTFLAG
          MOVE      PMVXMHOS,FINHOSP
          CALL      PATCALF
.
          BRANCH    IPATFLAG,CASHUL10,CASHUL10     * next receipt
          GOTO      CASHULP
.
.         Check for any non banked deposits (unreleased cash)
.
POST3000  MOVE      ZERO,NBNKAMNT
          PACK      KEY25,AADMNO,SP70
          CALL      RSRCDTE6
POST3200  CALL      RKRCDTE6
          BRANCH    OVRCD,POST4000
.
          MATCH     DAADMNO,RCDTVIST
          GOTO      POST4000 IF NOT EQUAL
.
          MATCH     SP70,RCDTINVN          * Invoice number must be blank
          GOTO      POST3200 IF NOT EQUAL
          MATCH     SP70,RCDTRELD
          GOTO      POST3200 IF NOT EQUAL   * has been released
.
.        If we are printing Patient Only invoice, check if payment is included
.
          COMPARE   TWO,IPATFLAG
          GOTO      POST3700 IF EQUAL       * Printing New Patient Portion Inv.
.
          COMPARE   ONE,IPATFLAG
          GOTO      POST3800 IF NOT EQUAL
.
.         Check if deposits tobe displayed on Patient Invoice
.
POST3700  MATCH     "1",PTCNADEP
          GOTO      POST3200 IF NOT EQUAL     * no cash applied
.
          MATCH     "9",RCDTSFLG
          GOTO      POST3800 IF EQUAL         * include on invoice
.
          MOVE      SP70,RCDTSFLG
          CALL      UPRCDTE6
          GOTO      POST3200
.
POST3800  PACK      KEY12,RCDTRECN
          CALL      RDRCBNK1
          BRANCH    OVRCD,POST3200
          MATCH     "1",RCBNSTAT            * check if cancelled
          GOTO      POST3200 IF EQUAL
.
          ADD       RCDTAMNT,NBNKAMNT
          PACK      RCDTINVN,SP4,CNEXTINV   * store the invoice number
          CALL      UPRCDTE6
          GOTO      POST3200
.
POST4000  OPEN      PATINVR1,"patinvrf"
          MOVE      CNEXTINV,KEY8
          CALL      RDINV1
          BRANCH    OVRCD,CASHRET
          MOVE      NBNKAMNT,PINVPCSH       * pending cash
          MULT      "-1",PINVPCSH
.
          PACK      PTINUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTINUDAT             * date updated
          CLOCK     TIME,PTINUTIM             * time updated
          MOVE      USERID,PTINUUID           * web user id
.
          CALL      UPINV1
CASHRET   RETURN
+
********************************************************************************
.         Post Receipt discount
********************************************************************************
PTDIS000  READ      CONTROLF,THIRTY6;*113,HJNL 
          OPEN      PATJRNL1,"patjrnlf"
          MOVE      RCDTDISC TO TPATAMTT
          MULT      "-1" TO TPATAMTT
          MOVE      TPATAMTT,TPATAMTP
          MOVE      ZERO,TREBATE
          MOVE      HJNL,TTYPE
          MOVE      "1",TPAYTYPE     ***** set up Payment TYPE cash
          MOVE      SIX,TRECTYPE        *** RECORD TYPE JOURNAL
.
PTDIS100  CALL      NXTTRAN1      * Get the next transaction number
          MOVE      PINVNO,TREF
          PACK      KEY22 WITH TADMNO,TREF,TTRANSN1
          CALL      RDADTRN1
          COMPARE   ZERO,OVRCD
          GOTO      PTDIS100 IF EQUAL     * Try the next transaction number
.
          CALL      WRDTRN1
.
. ---- write discount journal info to journal file ----
.
          MOVE      HJNL,JCODE
          MOVE      CMDETDAT,JDATE
          REP       " 0",JDATE
.
          MOVE      TADMNO,JADMN
          MOVE      PVITRAN,JTRNS
          UNPACK    RCDTINVN,DIM4,JINVN
          MOVE      SP70,PTJRINDC
.
          MOVE      USERID,PTJRCUID              * Created by
          PACK      PTJRCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTJRCDAT                * Date created
          CLOCK     TIME,PTJRCTIM                * Time created
          MOVE      SP70,JSPARE
.
          PACK      KEY24,JCODE,JDATE,JADMN,JTRNS
          CALL      WRJRNL1
.
. ---- set and write discount to PATFINS file ----
.
          MOVE      PINVNO,FININVN
          MOVE      PINVADM,FINADMN
          MOVE      PINVUR,FINURNO
.
          MOVE      PINVDTE,FINDATE
          REP       " 0",FINDATE
          MOVE      THREE,FINSYS
          MOVE      SP70,PINVSITE
          MOVE      RCDTDISC,FINAMT
          PACK      FINTYPE,ANSC,SP70
          IF        PTCNJFEE=1
            PACK      FINCODE,ACLAIM,HJNL,SP20
.                               Claim Code, Journal Code
          ELSE
            PACK      FINCODE,HJNL,SP20
.                               Journal Code
          ENDIF
          MOVE      ZERO,FGSTFLAG
          MOVE      PMVXMHOS,FINHOSP
          CALL      PATCALF
PTDIS999  RETURN
+
.         Subroutine to remove an admisssion number from the file of
.         patients who still have invoices pending.
.
DELIPEND  OPEN      PATIPEN1,"patipenf"
.
          COMPARE   TWO,IPATFLAG
          GOTO      DELIP020 IF EQUAL       * Printing New Patient Portion Inv.
.
.         if we are raising ABF invoice, check for any Patient only items 
.         to be invoiced 
.
          COMPARE   ONE,ABFFLAG
          GOTO      DELIP200 IF NOT EQUAL  * Not ABF invoice
.
DELIP020  PACK      KEY14,AADMNO,SP20
          CALL      RSPMMTI1
DELIP100  CALL      RKPMMTI1
          BRANCH    OVRCD,DELIP200
.
          MATCH     DAADMNO,PMMIVISN
          GOTO      DELIP200 IF NOT EQUAL
.
          COMPARE   ZERO,PMMIAMTT
          GOTO      DELIP100 IF EQUAL      * zero amount of item
.
          GOTO      ENDIPEND
.
DELIP200  COMPARE   TWO,IPATFLAG
          GOTO      DELIP400 IF NOT EQUAL
.
.         Check if there is HF invoice still to be raised
.
          OPEN      PATHTRA1,"pathtraf"
          PACK      KEY22 WITH AADMNO,SP70
          CALL      RSPTHTR1
DELIP220  CALL      RKPTHTR1
          BRANCH    OVRCD,DELIP400
.
          MATCH     DAADMNO,PTHTADMN
          GOTO      DELIP400 IF NOT EQUAL
.
          MATCH     SP70,PTHTTREF              * Blank Rebate Invoice no?
          GOTO      DELIP400 IF NOT EQUAL
.
          COMPARE   ZERO,PTHTRBAT
          GOTO      DELIP220 IF EQUAL          * zero amount
.
          GOTO      ENDIPEND
.
DELIP400  MOVE      AADMNO,KEY8
          MOVE      THREE,IPSYSTM
          CALL      RDIPEN1
          BRANCH    OVRCD OF ENDIPEND
          CALL      DEIPEN1
.
ENDIPEND  RETURN
+
.         Subroutine to write a zero record to the invoice file
.
ZEROINV   MOVE      PINVTYP,SPINVTYP        * Save Invoice type
          CALL      CLPATINV                * clear Invoice variables
          MOVE      SPINVTYP,PINVTYP        * restore Invoice type
.
          MOVE      CNEXTINV,KEY8
          MOVE      CNEXTINV,PINVNO
.
          PACK      PINVDTE WITH CCC,CYY,CMM,CDD
          IF        CFEETYP=9 & BDTEFLAG=1
            MOVE      BACKDATE,PINVDTE    (JH uses backdated invoice)
          ENDIF
          REP       " 0",PINVDTE
          MOVE      ZERO,PINVUR
          MOVE      THREE,PINVSYS
.
...       MOVE      "99999999",PINVBLCD
...       MOVE      "0",PTINCRST
.
          CALL      RDINV1
          COMPARE   ZERO,OVRCD
          GOTO      ZERORET IF EQUAL
.
          PACK      PTINCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTINCDAT             * date created
          CLOCK     TIME,PTINCTIM             * time created
          MOVE      USERID,PTINCUID           * web user id
          CALL      WRINV1
.
.         Check if we need to generate an electronic claim (EDI or Eclipse)
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND10;*115,PTCNUEDI
          READ      CONTROLF,HUND29;*88,PTCNIHCW
.
          COMPARE   ONE,PTCNUEDI                 * using electronic claiming ?
          GOTO      ZERORET IF NOT EQUAL         * no - don't claim
.
          OPEN      PATFX1A1,"patfx1af"
.
          PACK      KEY8,AADMNO
          CALL      RDMISS1                      * admission record found ?
          BRANCH    OVRCD,ZERORET                * no - don't claim
.
.         Set defaults for a Health Fund claim in case this is an Eclipse claim
.
          MOVE      AFUNDH,PMECHFND              * load health fund
          MOVE      ZERO,PMECEETP                * set HF claim flag
.
          MOVE      AFUNDH,WBICHFND
          MOVE      ZERO,WBICEETP
.
.         We need to check if this is a DVA claim in case this is an 
.         Eclipse claim
.
          PACK      KEY5,ANSC,ANSL,ACLAIM        * check for DVA indicator
          CALL      RDCODE1
          IF        OVRCD = 0
            MATCH     ANSX,PTCDASC2              * Associated Field 2
            GOTO      ZERORET IF EQUAL           * X - Exclude from Eclipse
.
            MOVE      ZERO,FORM1
            WHILE     FORM1 < 5
              ADD       ONE,FORM1
              LOAD      ANS,FORM1,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
              MATCH     ANSV,ANS
              IF        @EQUAL
                PACK      PMECHFND,ACLAIM,SP6
                MOVE      ONE,PMECEETP
.
                PACK      WBICHFND,ACLAIM,SP6
                MOVE      ONE,WBICEETP
.
                GOTO      ZEROINV1
              ENDIF
            DO
          ENDIF
.
.         Not a DVA claim, so check if there is a health fund
.
          MATCH     SP6,AFUNDH                   * blank health fund ?
          GOTO      ZERORET IF EQUAL             * yes - finished
.
.         We have a valid health fund claim, so get the HF extension record
.         and check to see if the health fund is participating in electronic
.         claiming
.
          PACK      KEY6,AFUNDH
          CALL      RDPTFX11                     * HF extension record found ?
          BRANCH    OVRCD,ZERORET                * no - don't claim
.
          IF        PTFXEXTR <> 1 & PTFXEXTR <> 3
            GOTO      ZERORET                    * not using electronic claiming
          ENDIF
.
          COMPARE   THREE,PTFXEXTR               * using Eclipse ?
          GOTO      ZEROINV2 IF EQUAL            * yes
.
.         Process an EDI (HosClaims) claim
.
          OPEN      PATEDSA1,"patedsaf"
          MOVE      ZERO,PTESFLAG
          MOVE      SP20,PTESBATN                            
          MOVE      AFUNDH,PTESHFND
          MOVE      AADMNO,PTESADMN
          MOVE      AURNO,PTESURNO
          MOVE      PINVNO,PTESINVN
          MOVE      SP8,PTESPBAT
          MOVE      SP8,PTESNBAT
          MOVE      SP5,PTESPCSN
          MOVE      SP5,PTESCSNO
          MOVE      SP1,PTESCCFL
          MOVE      SP20,PTESSPAR
          PACK      KEY32,PTESFLAG,PTESHFND,PTEDADMN,PTESINVN
          CALL      RAPTEDS1
          IF        OVRCD = 1
            CALL      WRPTEDS1
          ENDIF
          GOTO      ZERORET
.
.         This is a DVA claim, so check if Cat CL is being used for DVA
.         Eclipse claiming.  Check the hospital based field for multi-hospital
.         to see if DVA Eclipse claiming is turned on.
.         
ZEROINV1  IF        IBCNMHOS = 1
            MATCH     "1",PTHSUDVA               * DVA Eclipse claiming on ?
            GOTO      ZEROINV2 IF EQUAL          * yes - claim
.
            MATCH     "0",PTHSUDVA               * DVA Eclipse claiming off ?
            GOTO      ZERORET IF EQUAL           * yes - don't claim
          ENDIF
.
.         Either this is not a multi-hospital environment, or it is a
.         multi-hospital environment and the hospital has not set the
.         DVA Eclipse claiming parameter, so check the parameter to see
.         if DVA Eclipse claiming is turned on.
.
          READ      CONTROLF,HUND14;*213,PTCNUEDC
          COMPARE   ONE,PTCNUEDC                 * using DVA Eclipse claiming ?
          GOTO      ZERORET IF NOT EQUAL         * no - don't claim
.
.         Process an Eclipse claim - do not write to pmsectaf for Pat.Only Inv.
.
ZEROINV2  BRANCH    IPATFLAG,ZERORET,ZERORET     * Patient Only Invoice
.
.         READ      CONTROLF,HUND29;*88,PTCNIHCW
.         MATCH     "1",PTCNIHCW
.         GOTO      ZEROINV3 IF EQUAL
.
.         MOVE      ZERO,OVRCD
.         TRAP      OVERCOND IF IO
.         OPEN      PMSECTA1,"pmsectaf"
.         TRAPCLR   IO
.         BRANCH    OVRCD,ZERORET
.
.         MOVE      ZERO,PMECFLAG
.         MOVE      SP20,PMECBATN
.         MOVE      AADMNO,PMECADMN
.         MOVE      AURNO,PMECURNO
.         MOVE      PINVNO,PMECINVN
.         MOVE      SP8,PMECPBAT
.         MOVE      SP8,PMECNBAT
.         MOVE      SP1,PMECCCFL
.         MOVE      SP30,PMECTRID
.         MOVE      ZERO,PMECAMTC
.         MOVE      ZERO,PMECAMTP
.         MOVE      SP8,PMECPDAT
.         MOVE      SP2,PMECSTAT
.         MOVE      SP30,PMECFTID
.         IF        IBCNMHOS = 1
.           MOVE      PMVXMHOS,PMECHOSP
.         ELSE
.           MOVE      SP3,PMECHOSP
.         ENDIF
.         MOVE      PREVTRID,PMECPCTI
.         MOVE      SP70,PMECSPAR
.         PACK      KEY35,PMECHOSP,PMECFLAG,PMECHFND,PMECADMN,PMECINVN,PMECBATN
.         CALL      RAPMECT1
.         IF        OVRCD = 1
.           CALL      WRPMECT1
.         ENDIF
.
.         MATCH     "1",PTCNIHCW
.         GOTO      ZEROINV3 IF EQUAL
.
.         GOTO      ZERORET
.
ZEROINV3  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      WEBIHCA1,"webihcaf"
          TRAPCLR   IO
          BRANCH    OVRCD,ZERORET
.
          MOVE      ZERO,WBICFLAG
          MOVE      SP20,WBICBATN
          MOVE      AADMNO,WBICADMN
          MOVE      AURNO,WBICURNO
          MOVE      PINVNO,WBICINVN
          MOVE      SP8,WBICPBAT
          MOVE      SP8,WBICNBAT
          MOVE      SP1,WBICCCFL
          MOVE      SP30,WBICTRID
          MOVE      ZERO,WBICAMTC
          MOVE      ZERO,WBICAMTP
          MOVE      SP8,WBICPDAT
          MOVE      SP2,WBICSTAT
          MOVE      SP30,WBICFTID
          IF        IBCNMHOS = 1
            MOVE      PMVXMHOS,WBICHOSP
          ELSE
            MOVE      SP3,WBICHOSP
          ENDIF
          MOVE      PREVTRID,WBICPCTI
          MOVE      SP70,WBICCTYP
          MOVE      SP70,WBICMSID
          MOVE      SP70,WBICSPAR
          PACK      KEY35,WBICHOSP,WBICFLAG,WBICHFND,WBICADMN,WBICINVN,WBICBATN
          CALL      RAWBIHC1
          IF        OVRCD = 1
            CALL      IBACLOCK
            PACK      WBICCDAT,CCC,CYY,CMM,CDD
            REP       " 0",WBICCDAT
            MOVE      CTIMEIS,WBICCTIM
            MOVE      USERID,WBICCUID
            CALL      WRWBIHC1
          ENDIF
.
.          MATCH     "1",RACLINDC
.          GOTO      ZERORET IF NOT EQUAL
..
.          MATCH     SP70,PTCNECLO                * eclipse outbound path set?
.          GOTO      ZERORET IF EQUAL
..
.          CALL      SECLX000 
.
ZERORET   RETURN
+
.==============================================================================
.         GMIX0000 - Set casemix funding flag
.==============================================================================
GMIX0000  MOVE      ZERO,CMIXFLAG
          MOVE      ZERO,PTCTDCAS
          MATCH     ANSY,PTMICMXP
          GOTO      GMIX9999 IF NOT EQUAL     * not casemix
.
.         If we are using matrix and the patient is not discharge, then
.         charge theatre using patcat theatre fees
.
          IF        PTCNNWCM=1
            COMPARE   THREE,ASTAT
            GOTO      GCMX9999 IF NOT EQUAL
          ENDIF
.
          MOVE      ONE,CMIXFLAG
.
          IF        ASTAT=3
            MATCH     DDATE,ADATE
            IF        @EQUAL
              MOVE      ONE,PTCTDCAS          * Daycase
            ENDIF
          ENDIF
GMIX9999  RETURN
+
.******************************************************************************
.*                                  WREMC000              Called by:          *
.*                         Write Exploding MBS Charges                        *
.******************************************************************************
WREMC000  COMPARE   THREE,PVITYPE
          GOTO      WREMC900 IF NOT EQUAL   * Not an inpatient, exit
.
. ----- Write exploding miscellaneous items for MBS charge -----
.
          ADD       ONE,EXPCHCNT            * Increment the exploding charge cnt
.
.....     OPEN      PATMCHG1,"patmchgf"
          OPEN      PATECHA1,"patechaf"
          MOVE      TITEMNO,SAVKEY9
.
          PACK      KEY30,ACLAIM,ATYPE,AFUNDH,SP6,SAVKEY9,SP70
          CALL      PATECHRD
          BRANCH    EXIT,WREMC900
          UNPACK    KEY30,WKECCTYP,WKECATYP,WKECHFND,WKECHGRP,WKECMBSC
.
          CALL      RSPTECH1                * Position on & read the exploding
WREMC200  CALL      RKPTECH1                  charges file
          BRANCH    OVRCD,WREMC900
.
          MATCH     WKECCTYP,PTECCTYP
          GOTO      WREMC900 IF NOT EQUAL   * Different claim type
.
          MATCH     WKECATYP,PTECATYP
          GOTO      WREMC900 IF NOT EQUAL   * Different admission type
.
          MATCH     WKECHFND,PTECHFND
          GOTO      WREMC900 IF NOT EQUAL   * Different health fund
          MATCH     WKECHGRP,PTECHGRP
          GOTO      WREMC900 IF NOT EQUAL   * Different health fund group
.
          MATCH     WKECMBSC,PTECMBSC
          GOTO      WREMC900 IF NOT EQUAL   * Different MBS code
.
                                       * convert "H/F Table" to "Table Type"
          UNPACK    SP20,PTHFBCAT
          PACK      KEY14,AFUNDH,AFNDTB,SP20
          CALL      RDFUND1
.                                           * read thru Misc.Changes table
          PACK      KEY29,ACLAIM,AFUNDH,PTHFBCAT,PTECEMCH,SP20
          CALL      PATMCHRD                * Read misc. charge file
          IF        EXIT = 1
            PACK      KEY29,ACLAIM,SP6,SP3,PTECEMCH,WKMCDATE,SP10
            CALL      PATMCHRD              * Read misc. charge file
            IF        EXIT = 1
              CALL      DCLM0000
              PACK      KEY29,PTCNDCLM,SP6,SP3,PTECEMCH,WKMCDATE,SP10
              CALL      PATMCHRD            * Read misc. charge file
              BRANCH    OVRCD,WREMC900
            ENDIF
          ENDIF
.
.         Write record to item pending file
.
          MOVE      AADMNO,PMMIVISN         * Admission no
          MOVE      AADMNO,PVIBILL
          PACK      KEY8,PVIBILL
          CALL      TRVISA1                 * Get next Transaction no
          MOVE      PVITRAN,PMMITRAN        * DTR no
.
          MOVE      AURNO,PMMIURNO          * U/R no
          MOVE      " 3",PMMISYST
.
          PACK      PMMIITEM,MCHARGE,SP10   * Miscellaneous item
          MOVE      MDESC,PMMIDESC          * Miscellaneous item description
          PACK      PMMIDES2,SP30,SP10      * 2nd item description
          MOVE      SDATE,PMMITDAT
          REP       " 0",PMMITDAT
          MOVE      MITMTYP,PMMIRTYP        * Record type
.
          ASSIGN    MPATPOR+MHFPOR,PMMIAMTT
          MOVE      PTEC3MCF,FORM42
          LOAD      FORM42,EXPCHCNT,PTEC1MCF,PTEC2MCF
          ASSIGN    ((FORM42/100)*PMMIAMTT),PMMIAMTT * Total amount
.
          MOVE      ZERO,PMMIAMTG           * Gross amount - zero
          MOVE      ZERO,PMMIAMTH
.
          MOVE      MPATPOR,PMMIAMTP
          MOVE      PTEC3MCF,FORM42
          LOAD      FORM42,EXPCHCNT,PTEC1MCF,PTEC2MCF
          ASSIGN    ((FORM42/100)*PMMIAMTP),PMMIAMTP * Patient amount
.
          MOVE      MHFPOR,PMMIRBAT
          MOVE      PTEC3MCF,FORM42
          LOAD      FORM42,EXPCHCNT,PTEC1MCF,PTEC2MCF
          ASSIGN    ((FORM42/100)*PMMIRBAT),PMMIRBAT * Rebate amount
.
          MOVE      SP6,PMMITDOC            * Treating doctor - none
          MOVE      ZERO,PMMISERV           * no of services
          MOVE      SP6,PMMIODOC            * Operating doctor - none
          MOVE      SP10,PMMISESS           * Session
          MOVE      SDATE,PMMITDAT
          MOVE      MMSCGRP,PMMIMGRP        * Miscellaneous charge group
          MOVE      MNINV,PMMIINVN          * No of invoices
          MOVE      ZERO,PMMIGSTA           * GST Applicable
          MOVE      SP6,PMMIGSTC            * GST Code
          MOVE      ZERO,PMMIGSTM           * GST Amount
          MOVE      TRECEIPT,PMMIREFN       * Reference no
          MATCH     "PATWEB71",PRGID
          GOTO      WREMC202 IF EQUAL
          MATCH     "IBARSH",PRGID
          GOTO      WREMC202 IF EQUAL
          MOVE      PASSCODE,PMMIWUSR
          GOTO      WREMC204
.
WREMC202  MOVE      WBSEUID,PMMIWUSR        * WEB user id
WREMC204  MOVE      SP70,PMMIDKSM           * Distance in kms
          PACK      PMMISPAR,SP70
          PACK      PMMIUNIQ,SP70
.
          IF        DISPROG=1
            MOVE      "DISCHRGE",PMMIBTCH   * Batch no - discharge
          ELSE
            MOVE      "INVOICES",PMMIBTCH   * Batch no - invoices
          ENDIF
.
WREMC210  CALL      NXTTRAN1
          PACK      KEY14,PMMIVISN,PMMITRAN
          CALL      RAPMMTI1
          COMPARE   ZERO,OVRCD
          GOTO      WREMC210 IF EQUAL
          MOVE      SP70,PMMICNTR
          MOVE      SP70,PMMISUNQ
          MOVE      SP70,PMMIINCT
          MOVE      SP70,PMMISUBN
          MOVE      SP70,PMMIEDAD
          MOVE      SP70,PMMISVTM
          MOVE      SP70,PMMIDUPD
          MOVE      SP70,PMMITUPD
          MOVE      SP70,PMMIWUSR
          PACK      PMMIDTCR,CCC,CYY,CMM,CDD
          REP       " 0",PMMIDTCR
          CALL      IBACLOCK
          MOVE      CTIMEIS,PMMITMCR
          MOVE      PASSCODE,PMMIIDCR
          MOVE      SP70,PMMITMNO
          MOVE      SP70,PMMIPMBS
          MOVE      SP70,PMMIRBRS
          MOVE      SP70,PMMICTYP
          PACK      PMMIACOI,SP70       * After Care Override Indicator
          PACK      PMMIDSOV,SP70       * Duplicate Service Override
          PACK      PMMISTXT,SP70       * Service Text
          PACK      PMMILSPN,SP70       * Location Specific Practice No(LSPN)
          PACK      PMMIMPOV,SP70       * Multi Procedure Override
          PACK      PMMINMPT,SP70       * Number of Patients Seen
          PACK      PMMISDCD,SP70       * Self Deem Code (Cat Sd)
          PACK      PMMITDUR,SP70       * Time Duration (Mins)
          PACK      PMMIROVR,SP70       * Restricted Override Code (Cat Ro)
          PACK      PMMIHOSI,SP70       * Hospital Indicator         *T0859743
          MOVE      SP70,PMMIDKSM       * Distance in kms
          PACK      PMMISPAR,SP70,SP70
          CALL      WRPMMTI1                * Write to the item pending file
.
          GOTO      WREMC200
.
WREMC900  MOVE      ZERO,EXIT
WREMC999  RETURN
+
.******************************************************************************
.         Check for primary MBS
.         If the admission GST is unknown, check with the primary MBS:
.         -  if the primary MBS is payable then accommodation will be payable
.         -  if the MBS is not payable then accommodation will not be payable
.         -  if the MBS has unknown GST, then leave the record in patgtuaf file
.******************************************************************************
PMBS0000  MOVE      ZERO,EXIT
          MOVE      AADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,PMBS9999
          COMPARE   ZERO,AINVPRT            * Invoice printed yet ?
          GOTO      PMBS9999 IF NOT EQUAL   * yes.
.
          MOVE      NINE,FORM1              * Default
          MATCH     ANSU,PTMIGSTA           * Admission GST unknown ?
          GOTO      PMBS2000 IF NOT EQUAL   * No, check misc.charges
.
          CALL      XMBS0000                * Get Primary MBS (from PATCATBI)
          MATCH     SP9,PRIMMBS
          GOTO      PMBS1000 IF EQUAL       * no Primary MBS
.
          COMPARE   TWO,FORM1               * Unknown GST from Primary MBS ?
          GOTO      PMBS2000 IF NOT EQUAL   * no.
.
PMBS1000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,NINTY8;*129,PTCNDGST
          MOVE      TWO,FORM1               * default to unknown
          MATCH     ANSN,PTCNDGST           * check with the default
          IF        @EQUAL
            MOVE      ZERO,FORM1            * no GST
            GOTO      PMBS2000
          ELSE
            MATCH     ANSY,PTCNDGST
            IF        @EQUAL
              MOVE      ONE,FORM1           * yes GST
              GOTO      PMBS2000
            ENDIF
          ENDIF
.
          IF        WEBFLAG=1
            CLEAR     WEBTITLE
            APPEND    "Can Not print invoice, ",WEBTITLE
            APPEND    "Unknown Acccommodation GST.",WEBTITLE
            APPEND    SP70,WEBTITLE
            RESET     WEBTITLE
            CALL      WEBERR00
          ELSE
            DISPLAY   *P1:24,*EL,*B,*V2LON:
                      "Can't print invoice,Unknown Accommodation GST.";
          ENDIF
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      PMBS9999
.
.         Primary MBS has either Yes or No GST, therefore record should not
.         exist in unknown file
.
PMBS2000  PACK      KEY8,AADMNO
          OPEN      PATGTUA1,"patgtuaf"
          CALL      RDPTGTU1                * Check if a record exists
          BRANCH    OVRCD,PMBS8000
.
.         If MBS Misc. is unknown, can not print invoice
.
          IF        PTGTGSTM=1
            IF        WEBFLAG=1
              CLEAR     WEBTITLE
              APPEND    "Can Not print invoice, ",WEBTITLE
              APPEND    "Unknown MBS/Item GST.",WEBTITLE
              APPEND    SP70,WEBTITLE
              RESET     WEBTITLE
              CALL      WEBERR00
            ELSE
              DISPLAY    *P1:24,*EL,*B,*V2LON:
                         "Can't print invoice,Unknown MBS/Item GST.";
              CALL      HITENTER
            ENDIF
            MOVE      ONE,EXIT
            GOTO      PMBS9999
          ELSE
            CALL      DEPTGTU1              * delete record
          ENDIF
.
PMBS8000  IF        FORM1=1
            MOVE      ANSY,PTMIGSTA         * Yes GST
            CALL      UPMISS1
          ELSE
            IF        FORM1=0
              MOVE      ANSN,PTMIGSTA         * No GST
              CALL      UPMISS1
            ENDIF
          ENDIF
PMBS9999  RETURN
+
.******************************************************************************
.*                                  CHKM0000                                  *
.*Check casemix funding chargeable on misc items for casemix patients.        *
.******************************************************************************
CHKM0000  MOVE      ONE,COUNT
          MOVE      ONE,COUNTA
          MATCH     ANSN,PTMICMXP
          GOTO      CHKM9999 IF EQUAL
.
          COMPARE   ZERO,PTMCCFCY                * No charge on all items
          GOTO      CHKM5000 IF EQUAL
.
          COMPARE   ONE,PTMCCFCY                 * Always chargeable 
          GOTO      CHKM9999 IF EQUAL
.
CHKM1000  IF        COUNT<=UNIQUE
            MATCH     MCHARGE,MCHGARR[COUNT]
            IF        @EQUAL
              ADD       ONE,COUNTA
            ENDIF
            ADD       ONE,COUNT
            GOTO      CHKM1000
          ENDIF
          
.
          COMPARE   PTMCCFCY,COUNTA              * Charge from item number ?
          GOTO      CHKM5000 IF LESS
          GOTO      CHKM9999
.
CHKM5000  MOVE      ZERO,MPATPOR
          MOVE      ZERO,MHFPOR
.
.
CHKM9999  RETURN
+
.**************************************************************************
.*                                FDOCT000                                *
.*              See if these are doctor deposits, and if so, set up       *
.*              the transaction description                               *
.**************************************************************************
.
          DEFPROC   FDOCT000
.
          INC       RCPCONFD/INC
.
.
DIM15     DIM       14
.
ANAEST    INIT      "ANAESTHETIST  "
DEPOSIT   INIT      "DEPOSIT FOR "
MDSAVE    INIT      "MEDISAVE      "
MDSHLD    INIT      "MEDISHIELD    "
OTHER1    INIT      "OTHER DOCT. 1 "
OTHER2    INIT      "OTHER DOCT. 2 "
PAEDTN    INIT      "PAEDIATRICIAN "
PRINCP    INIT      "PRINCIPLE DOC."
.
.
.
FDOCT000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,THIRTY6;*152,RCCNPDOC,RCCNODO1,RCCNODO2,RCCNPAED:
                                          RCCNANTH,RCCNMERG,*177,RCCNMSHD
.
          MOVE      ZERO,FORM1
          WHILE     FORM1 < 7
            ADD       ONE,FORM1
            LOAD      KEY3,FORM1,RCCNPDOC,RCCNODO1,RCCNODO2,RCCNPAED,RCCNANTH:
                                 RCCNMERG,RCCNMSHD
            MATCH     CMDEDTYP,KEY3
            IF        @EQUAL
              LOAD      DIM15,FORM1,PRINCP,OTHER1,OTHER2,PAEDTN,ANAEST,MDSAVE:
                                    MDSHLD
              PACK      TDDESC,DEPOSIT,DIM15
              GOTO      FDOCT900
            ENDIF
          DO
.
FDOCT900
          GOTO      ENDFDOCT
.
ENDFDOCT  ENDPROC
+
.------------------------------------------------------------
.                            CCPS0000
.                     write Invoice data for CCPS Extract
.------------------------------------------------------------
CCPS0000  MOVE      ZERO,EXIT
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND03;*218,PTCNUCCP
.
          MATCH     "1",PTCNUCCP            * "1" is "Using CCPS EDI extract"
          GOTO      CCPS9999 IF NOT EQUAL
.
          OPEN      PATEDTA1,"patedtaf"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATFX1A1,"patfx1af"
          OPEN      VISPAYA1,"vispayaf"
          PACK      KEY8,AADMNO,SP8
.
.                                           * initialise "patedtaf"
CCPS1000  MOVE      ZERO,PTETFLAG           * 0 = EDI not sent
          MOVE      PTETFLAG,DPTETFLG
          MOVE      "CCPS  ",PTETPAYC
          MOVE      AADMNO,PTETADMN
          MOVE      PINVNO,PTETINVN
          UNPACK    SP30,PTETBATN,PTETPBAT,PTETNBAT
          MOVE      PINVUR,PTETURNO
          MOVE      PINVDTE,PTETINDT
          MOVE      "0",PTETINTY            * 0 = Medical only, 1 = Claim
          MOVE      "0",PTETEXCL            * 0 = not excluded
          MOVE      " 0",PTETCCST           * 0 = not sent   
          MOVE      PINVAMT,PTETINAM
          MOVE      PINVSYS,PTETISYS
          UNPACK    SP70,PTETRCAL,PTETSPAR
.
          PACK      KEY14,AADMNO,SP10
          CALL      RSVSPAY1
CCPS2000  CALL      RKVSPAY1
          BRANCH    OVRCD,CCPS5000
.
          MATCH     VSPAVISN,KEY8
          GOTO      CCPS5000 IF NOT EQUAL
.
          PACK      KEY6,VSPAPAYC
          CALL      RDPTFX11
          BRANCH    OVRCD,CCPS5000
.
          COMPARE   ZERO,PTFXEXTR           * is Expected Payor HF for EDI ?
          GOTO      CCPS2000 IF EQUAL       * no, get next one
.
          MOVE      "1",PTETINTY            * set Invoice Type to "Claim"
          GOTO      CCPS5000
.
.         Write CCPS EDI Summary record to "patedtaf"
.
CCPS5000  PACK      KEY26,DPTETFLG,PTETADMN,PTETINVN,SP30
          CALL      RAPTEDT1
          IF        OVRCD = 1
            CALL      WRPTEDT1
          ENDIF
.
CCPS9999  RETURN
.
.------------------------------------------------------------
.         Set up JH PRFA name
.------------------------------------------------------------
JHPRA000  STRIP     PTITL
          STRIP     PGNAME
.
          MOVE      SP70,PFNAME
          CLEAR     PFNAME
          APPEND    PTITL,PFNAME
          APPEND    SP1,PFNAME
          APPEND    PGNAME,PFNAME
          APPEND    SP1,PFNAME
          APPEND    PTMASNAM,PFNAME
          APPEND    SP70,PFNAME
          RESET     PFNAME
JHPRA999  RETURN
.
.------------------------------------------------------------
.         Update Insurance Provider for JH
.------------------------------------------------------------
UIPR0000  OPEN      PATIPRA1,"patipraf"
          MOVE      PINVADM,KEY8
          PACK      KEY19,KEY8,SP70
          CALL      RSPTIPA1
UIPR1000  CALL      RKPTIPA1
          BRANCH    OVRCD,UIPR9999
.
          MATCH     KEY8,PTIRVISN
          GOTO      UIPR9999 IF NOT EQUAL
.
          MATCH     SP70,PTIRINVN
          GOTO      UIPR9999 IF NOT EQUAL
.
.         set invoice number to insurance providers
.
          MOVE      PINVNO,PTIRINVN
          PACK      KEY19,PTIRVISN,PTIRINVN,PTIRCOUN,SP70
          CALL      RAPTIPA1
          COMPARE   ZERO,OVRCD
          GOTO      UIPR1000 IF EQUAL
.
          PACK      KEY19,PTIRVISN,PTIRINVN,PTIRCOUN,SP70
          CALL      WRPTIPA1
.
          MOVE      SP70,PTIRINVN
          PACK      KEY19,PTIRVISN,PTIRINVN,PTIRCOUN,SP70
          CALL      RDPTIPA1                        * reposition
.
          GOTO      UIPR1000
.
UIPR9999  RETURN
+
.------------------------------------------------------------
. Set Default Printer
.------------------------------------------------------------
SETDEF00  MOVE      PRGID,SAVPRGID
          MOVE      REPORTNO,SAVREPTN
.
          MOVE      SCHDPRNT,SELPRINT
          MOVE      SETDFPRG,PRGID
          MOVE      SETDFRPT,REPORTNO
          CALL      UPDFPT00
.
          MOVE      SAVPRGID,PRGID
          MOVE      SAVREPTN,REPORTNO
          RETURN
+
.--------------------------------------------------------------------------
.*                             SECLX000                                   *
.*               Schedule Eclipse Extract (IBAPMS04)                      *
.--------------------------------------------------------------------------
SECLX000  MOVE      "IBAPMS04",SCHDPGID
          MOVE      "Eclipse Claims Extract",SCHDDESC
          PACK      SCHDDATE,CCC,CYY,CMM,CDD
          REP       " 0",SCHDDATE
          CLOCK     TIME,SCHDTIME
          MOVE      "S  ",SCHDPRNT           * initialise to spool
          MATCH     SP70,SELPRINT
          IF        !@EQUAL
            MOVE      SELPRINT,SCHDPRNT      * printer code
          ENDIF
          MOVE      "IBAPMS04",SETDFPRG
          MOVE      "8",SETDFRPT
          CALL      SETDEF00                 * save default printer
.
.         Write Keyin parameters
.
          MOVE      ONE,COUNT
.         MOVE      HOSPCODE,KEYSTARR[COUNT]    * hospital id
          IF        IBCNMHOS = 1
            MOVE      PMVXMHOS,KEYSTARR[COUNT]
          ELSE
            MOVE      SP3,KEYSTARR[COUNT]
          ENDIF
.
          ADD       ONE,COUNT
          MOVE      ONE,OPTNFLAG
          IF        PMECEETP=ONE
            MOVE       TWO,OPTNFLAG
          ENDIF
          MOVE      OPTNFLAG,KEYSTARR[COUNT]    * extract by fund(1)/claim(2)

          ADD       ONE,COUNT
          MOVE      PMECHFND,KEYSTARR[COUNT]    * health fund / claim code
.
          MOVE      THREE,F3                    * first 3 keyins
.
          ADD       ONE,COUNT
          CLEAR     ECCLMARR
.
          IF        IBCNMHOS=ONE
            APPEND    PMVXMHOS,ECCLMARR
          ELSE
            APPEND    SP3,ECCLMARR
          ENDIF  
          APPEND    PMECADMN,ECCLMARR
          APPEND    PMECINVN,ECCLMARR
          APPEND    PMECURNO,ECCLMARR
          APPEND    PINVDTE,ECCLMARR
.
          ASSIGN    (PINVAMT+PINVPATA+PINVHFDA+PINVOTHA+PINVJOUR+PTINCRTT),PMECAMTC
          IF        IBCNUGST=2
            ADD       PTINGSTJ,PMECAMTC
          ENDIF
.
          APPEND    PMECAMTC,ECCLMARR          
          RESET     ECCLMARR
          MOVE      ECCLMARR,KEYSTARR[COUNT]    * to be extracted
.
          ADD       ONE,F3                  * eclipse claims keyins
.
          ADD       ONE,COUNT
          PACK      KEYSTARR[COUNT],SP70         * blank record to signal exit
          ADD       ONE,F3      
.
          ADD       ONE,COUNT
          MOVE      ANSP,KEYSTARR[COUNT]         * (P)ost
.>>>>
.>>>>     ADD       ONE,COUNT
.>>>>     MOVE      ANSY,KEYSTARR[COUNT]         * (Y) to extract (if ok)
.>>>>
          ADD       ONE,COUNT
          MOVE      SP70,KEYSTARR[COUNT]         * (Enter) to exit
          ADD       ONE,COUNT
          MOVE      "\",KEYSTARR[COUNT]          * (\) to exit
.
          ADD       THREE,F3                     * add last 3 keyins (if error)
.>>>>     ADD       FOUR,F3                      * add last 4 keyins (if ok)
          MOVE      F3,KEYSTCNT                  * Number of Keyins
.
          CALL      SCHPRO00   * Schedule Extract
.
SECLX999  RETURN
+
.--------------------------------------------------------------------------
.*                             SWEBX000                                   *
.*               Schedule WebServices IHCW Extract (IBAWEB04)             *
.--------------------------------------------------------------------------
SWEBX000  MOVE      "IBAWEB04",SCHDPGID
          MOVE      "WebServices IHCW Raise and Claim Extract",SCHDDESC
          PACK      SCHDDATE,CCC,CYY,CMM,CDD
          REP       " 0",SCHDDATE
          CLOCK     TIME,SCHDTIME
          MOVE      "S  ",SCHDPRNT           * initialise to spool
          MATCH     SP70,SELPRINT
          IF        !@EQUAL
            MOVE      SELPRINT,SCHDPRNT      * printer code
          ENDIF
          MOVE      "IBAWEB04",SETDFPRG
          MOVE      "8",SETDFRPT
          CALL      SETDEF00                 * save default printer
.
.         Write Keyin parameters
.
          MOVE      ONE,COUNT
.         MOVE      HOSPCODE,KEYSTARR[COUNT]    * hospital id
          IF        IBCNMHOS = 1
            MOVE      PMVXMHOS,KEYSTARR[COUNT]
          ELSE
            MOVE      SP3,KEYSTARR[COUNT]
          ENDIF
.
          ADD       ONE,COUNT
          MOVE      ONE,OPTNFLAG
          IF        WBICEETP=ONE
            MOVE       TWO,OPTNFLAG
          ENDIF
          MOVE      OPTNFLAG,KEYSTARR[COUNT]    * extract by fund(1)/claim(2)

          ADD       ONE,COUNT
          MOVE      WBICHFND,KEYSTARR[COUNT]    * health fund / claim code
.
          MOVE      THREE,F3                    * first 3 keyins
.
          ADD       ONE,COUNT
          CLEAR     ECCLMARR
.
          IF        IBCNMHOS=ONE
            APPEND    PMVXMHOS,ECCLMARR
          ELSE
            APPEND    SP3,ECCLMARR
          ENDIF
          APPEND    WBICADMN,ECCLMARR
          APPEND    WBICINVN,ECCLMARR
          APPEND    WBICURNO,ECCLMARR
          APPEND    PINVDTE,ECCLMARR
.
          ASSIGN    (PINVAMT+PINVPATA+PINVHFDA+PINVOTHA+PINVJOUR+PTINCRTT),WBICAMTC
          IF        IBCNUGST=2
            ADD       PTINGSTJ,WBICAMTC
          ENDIF
.
          APPEND    WBICAMTC,ECCLMARR
          RESET     ECCLMARR
          MOVE      ECCLMARR,KEYSTARR[COUNT]    * to be extracted
.
          ADD       ONE,F3                  * eclipse claims keyins
.
          ADD       ONE,COUNT
          PACK      KEYSTARR[COUNT],SP70         * blank record to signal exit
          ADD       ONE,F3
.
          ADD       ONE,COUNT
          MOVE      ANSP,KEYSTARR[COUNT]         * (P)ost
.>>>>
.>>>>     ADD       ONE,COUNT
.>>>>     MOVE      ANSY,KEYSTARR[COUNT]         * (Y) to extract (if ok)
.>>>>
          ADD       ONE,COUNT
          MOVE      SP70,KEYSTARR[COUNT]         * (Enter) to exit
          ADD       ONE,COUNT
          MOVE      "\",KEYSTARR[COUNT]          * (\) to exit
.
          ADD       THREE,F3                     * add last 3 keyins (if error)
.>>>>     ADD       FOUR,F3                      * add last 4 keyins (if ok)
.
          ADD       ONE,COUNT
          PACK      KEYSTARR[COUNT],SP70         * blank record to signal exit
          ADD       ONE,F3
.
          MOVE      F3,KEYSTCNT                  * Number of Keyins
.
          CALL      SCHPRO00   * Schedule Extract
.
SWEBX999  RETURN
+
.-------------------------------------------------------------------------------
. Check if Patient invoice has been raised (ie record exist in HF Debtors)
.-------------------------------------------------------------------------------
CPIN0000  MOVE      ZERO,PREBFLAG              * default to print patient inv.
          COMPARE   TWO,IPATFLAG
          GOTO      CPIN9999 IF EQUAL          * printing Patient invoice
.
.         Check if there is a rebate invoice to be printed
.
          OPEN      PATHTRA1,"pathtraf"
          PACK      KEY22 WITH AADMNO,SP70
          CALL      RSPTHTR1
CPIN1000  CALL      RKPTHTR1
          BRANCH    OVRCD,CPIN9000
.
          MATCH     DAADMNO,PTHTADMN
          GOTO      CPIN9000 IF NOT EQUAL
.
          MATCH     SP70,PTHTTREF              * Blank Rebate Invoice no?
          GOTO      CPIN9000 IF NOT EQUAL
.
.         Rebate invoice has not been raised, check if the patient invoice
.         has been credited
.
          MOVE      PTHTPINV,KEY8
          CALL      RDINV1
          BRANCH    OVRCD,CPIN1000
.
          MATCH     "1",PTINCRST
          GOTO      CPIN1000 IF EQUAL     * fully credited
.
.         found a patient invoice with Rebate invoice NOT raised yet
.
          MOVE      ONE,PREBFLAG          * flag for printing Rebate patient
          MATCH     "1",PTHTRECT
          IF        @EQUAL
            MOVE      PTHTFCEN,FCENT
            MOVE      PTHTFYER,FYEAR
            MOVE      PTHTFMON,FMON
            MOVE      PTHTFDAY,FDAY
          ENDIF
.
CPIN9000  PACK     PINVDTE WITH CCC,CYY,CMM,CDD
          REP      " 0",PINVDTE
CPIN9999  RETURN
+
.-------------------------------------------------------------------------
.         Write a record to the Reassign Debt file
.         after raising Patient Only invoice (IPATFLAG=1)
.-------------------------------------------------------------------------
          DEFPROC   RDEI0000
.
          INC       PATRASFD/INC
          INC       PATUREFD/INC
.
          OPEN      PATRASA1,"patrasaf"
          OPEN      PATUREA1,"patureaf"
.
          MOVE      "dm",KEY2
.
.         Get the first code for category dm with ind1=P
.
          PACK      KEY5,KEY2,SP70
          CALL      RDSCODE1
RDEI1000  CALL      RDKCODE1
          BRANCH    OVRCD,RDEI9999
.
          MATCH     KEY2,TCODE
          GOTO      RDEI9999 IF NOT EQUAL
.
          MATCH     "P",TCINDC1
          GOTO      RDEI1000 IF NOT EQUAL
.
          CALL      RECI0000         * Update Unreconciled invoice file
.
          MOVE      ONE,FORM2
.
.         Get the last counter
.
          PACK      KEY10,PINVNO,Z70
          CALL      RSPTRAS1
          CALL      RPPTRAS1
          IF        OVRCD=0
            MATCH     PINVNO,PTRAINVN      * Same invoice ?
            IF        @EQUAL
              MOVE      PTRACNTN,FORM2
            ENDIF
          ENDIF
.
RDEI2000  MOVE      FORM2,PTRACNTN
          PACK      KEY10,PINVNO,PTRACNTN,SP70
          CALL      RDPTRAS1
          IF        OVRCD=0
            ADD       ONE,FORM2
            GOTO      RDEI2000
          ENDIF
          PACK      CURRDATE,CCC,CYY,CMM,CDD  * write a new "patrasaf" record
          REP       " 0",CURRDATE
          MOVE      FORM2,PTRACNTN
          MOVE      PINVNO,PTRAINVN
          MOVE      "3",PTRASYST            * Inpatient
          MOVE      PTUROSTD,PTRAOUST       * Outstanding amount
          MOVE      ACODE,PTRARMOV          * Reason of Movement
          MOVE      SP70,PTRADBAG           * Debt Collection Agency (code)
          MOVE      PTUROSTD,PTRAPPOR       * Patient portion=Outstanding amt
          MOVE      PINVBLCD,PTRAIBAL       * Date invoice balanced
          MOVE      "0",PTRADELE            * set Deleted flag off
.
          CALL      IBACLOCK
          MOVE      CURRDATE,PTRACDAT
          MOVE      CTIMEIS,PTRACTIM
          MOVE      WBSEUID,PTRACUID        * WEB user id
.
          UNPACK    SP70,PTRAUDAT,PTRAUTIM,PTRAUUID
          CALL      WRPTRAS1
          GOTO      RDEI9999
.
.------------------------------------------------------------
.         Write a record to the Unreconciled invoice file
.         after raising Patient Only invoice (IPATFLAG=1)
.------------------------------------------------------------
RECI0000  MOVE      CNEXTINV,KEY8
          CALL      RDPTURE1             * shouldn't have record in unreconciled
          COMPARE   ZERO,OVRCD           * file as the invoice had just been
          GOTO      RECI9999 IF EQUAL    * raised.
.
          CALL      RDINV1
          BRANCH    OVRCD,RECI9999
.
          MOVE      PINVAMT,PTUROSTD     * Invoice amount +
          ADD       PINVPATA,PTUROSTD    * Patient payments +
          ADD       PINVHFDA,PTUROSTD    * H.F payments +
          ADD       PINVOTHA,PTUROSTD    * Other payments +
          ADD       PTINCRTT,PTUROSTD    * Credit Notes +
          ADD       PINVJOUR,PTUROSTD    * Journals = amount outstanding
          ADD       PTINGSTJ,PTUROSTD    * Journal GST = amount outstanding
.
          MOVE      PINVNO,PTURINVN
          MOVE      PINVADM,PTURVISN
          MOVE      "0",PTURSTAT                * New status
          MOVE      PINVSYS,PTURSYST
          MOVE      ACLAIM,PTURCLMN
          MOVE      AFUNDH,PTURHFND
          MOVE      AFNDTB,PTURHTAB
.
          MOVE      PINVAMT,PTURINVT     * Invoice total
          MOVE      PINVPATA,PTURPAYM    * Patient payments +
          ADD       PINVHFDA,PTURPAYM    * H.F payments +
          ADD       PINVOTHA,PTURPAYM    * Other payments +
.
          MOVE      PTINCRTT,PTURJRNL    * Credit Notes +
          ADD       PINVJOUR,PTURJRNL    * Journals = amount outstanding
          ADD       PTINGSTJ,PTURJRNL    * Journal GST = amount outstanding
.
          CALL      IBACLOCK
          PACK      PTURCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTURCDAT
          MOVE      CTIMEIS,PTURCTIM
          MOVE      WBSEUID,PTURCUID        * WEB user id
.
          UNPACK    SP70,PTURUDAT,PTURUTIM,PTURUUID
          CALL      WRPTURE1
.
RECI9999  RETURN
+
OVERCOND  MOVE      ONE,OVRCD
          RETURN
.
          INC       PATRASIO/INC
          INC       PATUREIO/INC
.
RDEI9999  ENDPROC
+
          INC       ISBINVS               * Independence Services billing
          INC       NZPINVS               * Southern Cross printing invoice
.
