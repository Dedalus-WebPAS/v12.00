.------------------------------------------------------------
. Result Details
.------------------------------------------------------------
RESP0000  BRANCH    FLDITMNO,RESP0100,RESP0200,RESP0300,RESP0400,RESP0500:
                             RESP0600,RESP0700,RESP0800,RESP0900,RESP1000:
                             RESP1100,RESP1200,RESP1300,RESP1400,RESP1500:
                             RESP1600,RESP1700,RESP1800,RESP1900,RESP2000:
                             RESP2100,RESP2200,RESP2300
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      RESP9999
.
RESP0100  REP       " +",RESULTKY
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
          WRITE     HTMLFILE;LINKPRG,".pbl?":
                             "&reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&resultky=",RESULTKY;
          REP       "+ ",RESULTKY
          GOTO      RESP9999
.
RESP0200  MATCH     SP70,REPHRDT
          GOTO      RESP9999 IF EQUAL
          UNPACK    REPHRDT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      RESP9999
.
RESP0300  WRITE     HTMLFILE;REPHRTM;
          GOTO      RESP9999
.
RESP0400  WRITE     HTMLFILE;REPHPID;
          GOTO      RESP9999
.
RESP0500  WRITE     HTMLFILE;REPHVIS;
          GOTO      RESP9999
.
RESP0600  MATCH     "0",REPHFUR
          IF        @EQUAL
            WRITE     HTMLFILE;"No";
          ELSE
            WRITE     HTMLFILE;"Yes";
          ENDIF
          GOTO      RESP9999
.
RESP0700  WRITE     HTMLFILE;REPHNAM;
          GOTO      RESP9999
.
RESP0800  WRITE     HTMLFILE;REPHLAB;
          GOTO      RESP9999
.
RESP0900  WRITE     HTMLFILE;REPHRRN;
          GOTO      RESP9999
.
RESP1000  MATCH     SP70,REPHCDT
          GOTO      RESP9999 IF EQUAL
          UNPACK    REPHCDT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      RESP9999
.
RESP1100  WRITE     HTMLFILE;REPHCTM;
          GOTO      RESP9999
.
RESP1200  MATCH     "0",REPHNOR
          IF        @EQUAL
            WRITE     HTMLFILE;"No";
          ENDIF
          MATCH     "1",REPHNOR
          IF        @EQUAL
            WRITE     HTMLFILE;"Yes";
          ENDIF
          GOTO      RESP9999
.
RESP1300  MATCH     "0",REPHCON
          IF        @EQUAL
            WRITE     HTMLFILE;"No";
          ENDIF
          MATCH     "1",REPHCON
          IF        @EQUAL
            WRITE     HTMLFILE;"Yes";
          ENDIF
          GOTO      RESP9999
.
RESP1400  MATCH     "R",REPHTCT
          IF        @EQUAL
            WRITE     HTMLFILE;"Routine";
          ENDIF
          MATCH     "U",REPHTCT
          IF        @EQUAL
            WRITE     HTMLFILE;"Urgent";
          ENDIF
          GOTO      RESP9999
.
RESP1500  MATCH     "0",REPHCOM
          IF        @EQUAL
            WRITE     HTMLFILE;"No";
          ENDIF
          MATCH     "1",REPHCOM
          IF        @EQUAL
            WRITE     HTMLFILE;"Yes";
          ENDIF
          GOTO      RESP9999
.
RESP1600  CALL      TXTDEA00    * Details
          GOTO      RESP9999
.
RESP1700  CALL      AUDTAB00    * Audit Table
          GOTO      RESP9999
.
RESP1800  WRITE     HTMLFILE;RESULTKY;
          GOTO      RESP9999
.
RESP1900  WRITE     HTMLFILE;AUDITKEY;
          GOTO      RESP9999
.
RESP2000  UNPACK    DIM127,D1,GRPHWID,DIM127
          UNPACK    DIM127,D1,GRPHHGH,DIM127
          IF        CUMLGPH=1
            CALL      CMRGPH00
          ENDIF
          GOTO      RESP9999
.
RESP2100  CALL      TXTALL00    * Output all Text Details
          GOTO      RESP9999
.
RESP2200  CALL      LSTRES00    * Link to Last Result for Patient
          GOTO      RESP9999
.
RESP2300  CALL      NXTRES00    * Link to Next Result for Patient
          GOTO      RESP9999
.
RESP9999  RETURN
.------------------------------------------------------------
. Determine Link to Next Result for the Patient
.------------------------------------------------------------
NXTRES00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          PACK      RESLNKFL,PRELEN,RESULTKY          * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA1,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      NXTRES90 IF EQUAL                 * end I CAR 38290
.
          MOVE      "01",LINKTYPE
          MOVE      PURNO,URNUMBER
          PACK      LINKKEYF,URNUMBER,SP70
          PACK      KEY29,LINKTYPE,LINKKEYF,RESULTKY
          CALL      RDRELN1
          BRANCH    OVRCD,NXTRES90
          CALL      RKRELN1
          BRANCH    OVRCD,NXTRES90
          MATCH     "01",RELNLTY
          GOTO      NXTRES90 IF NOT EQUAL
          MATCH     URNUMBER,RELNLKY
          GOTO      NXTRES90 IF NOT EQUAL 
.
          PACK      KEY17,RELNRDT,RELNRTM,RELNRUN
          REP       " +",KEY17
          WRITE     HTMLFILE;"location.href=#"",LINKPRG,".pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&resultky=",KEY17,"#"";
          GOTO      NXTRES99
.
NXTRES90  WRITE     HTMLFILE;"alert(#"No More Patient Results#")";
.
NXTRES99  RETURN
.------------------------------------------------------------
. Determine Link to Last Result for the Patient
.------------------------------------------------------------
LSTRES00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          PACK      RESLNKFL,PRELEN,RESULTKY          * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA1,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      LSTRES90 IF EQUAL                 * end I CAR 38290
.
          MOVE      "01",LINKTYPE
          MOVE      PURNO,URNUMBER
          PACK      LINKKEYF,URNUMBER,SP70
          PACK      KEY29,LINKTYPE,LINKKEYF,RESULTKY
          CALL      RDRELN1
          BRANCH    OVRCD,LSTRES90
          CALL      RPRELN1
          BRANCH    OVRCD,LSTRES90
          MATCH     "01",RELNLTY
          GOTO      LSTRES90 IF NOT EQUAL
          MATCH     URNUMBER,RELNLKY
          GOTO      LSTRES90 IF NOT EQUAL 
.
          PACK      KEY17,RELNRDT,RELNRTM,RELNRUN
          REP       " +",KEY17
          WRITE     HTMLFILE;"location.href=#"",LINKPRG,".pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&resultky=",KEY17,"#"";
          GOTO      LSTRES99
.
LSTRES90  WRITE     HTMLFILE;"alert(#"No More Patient Results#")";
.
LSTRES99  RETURN
.------------------------------------------------------------
. Result Patient Details (PID)
.------------------------------------------------------------
PIDF0000  BRANCH    FLDITMNO,PIDF0100,PIDF0200,PIDF0300,PIDF0400,PIDF0500:
                             PIDF0600,PIDF0700,PIDF0800,PIDF0900,PIDF1000:
                             PIDF1100,PIDF1200,PIDF1300
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      PIDF9999
.
PIDF0100  WRITE     HTMLFILE;REPIPID;
          GOTO      PIDF9999
.
PIDF0200  WRITE     HTMLFILE;REPISNM;
          GOTO      PIDF9999
.
PIDF0300  WRITE     HTMLFILE;REPIGNM;
          GOTO      PIDF9999
.
PIDF0400  WRITE     HTMLFILE;REPIINT;
          GOTO      PIDF9999
.
PIDF0500  UNPACK    REPIDOB,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR; 
          GOTO      PIDF9999
.
PIDF0600  WRITE     HTMLFILE;REPISEX;
          GOTO      PIDF9999
.
PIDF0700  WRITE     HTMLFILE;REPIAD1;
          GOTO      PIDF9999
.
PIDF0800  WRITE     HTMLFILE;REPIAD2;
          GOTO      PIDF9999
.
PIDF0900  WRITE     HTMLFILE;REPIAD3;
          GOTO      PIDF9999
.
PIDF1000  WRITE     HTMLFILE;REPIAD4;
          GOTO      PIDF9999
.
PIDF1100  WRITE     HTMLFILE;REPIPC;
          GOTO      PIDF9999
.
PIDF1200  WRITE     HTMLFILE;REPIPHH;
          GOTO      PIDF9999
.
PIDF1300  WRITE     HTMLFILE;REPIPHB;
          GOTO      PIDF9999
.
PIDF9999  RETURN
.
.------------------------------------------------------------
. Output Text details
.------------------------------------------------------------
TXTDEA00  MOVE      ZERO,CUMLGPH
          MOVE      ZERO,PITF200
          MOVE      ZERO,PITF300
          WRITE     HTMLFILE;"<tr>":
                             "<td align=center bgcolor=##CCCCCC width=100%>":
                             "<b>Patient Header</b></td>":
                             "<td align=right bgcolor=##CCCCCC>":
                             "<input type=button ":
                             "class=OpenMoreButton id=pit1 ":
                             "onclick='ShowPatientHeader()' >":
                             "</td></tr>"
          WRITE     HTMLFILE;"<tr><td colspan=2>":
                             "<div id=pit100 class=pit100>":
                             "<pre>"
          PACK      KEY20,RESULTKY,SP70
          CALL      RSREPD1
TXTDEA10  CALL      RKREPD1
          BRANCH    OVRCD,TXTDEA99
          PACK      KEY17,REPDRDT,REPDRTM,REPDRUN,SP70
          MATCH     KEY17,RESULTKY
          GOTO      TXTDEA99 IF NOT EQUAL
.
          CALL      CHKLEV00      * Check for Level Change
.
          MATCH     "299",REPDPTL
          GOTO      TXTDEA10 IF EQUAL
          MATCH     "390",REPDPTL
          GOTO      TXTDEA10 IF EQUAL
          MATCH     "399",REPDPTL
          GOTO      TXTDEA10 IF EQUAL
.
          MATCH     "311",REPDPTL
          IF        @EQUAL
            CALL      CMRRES00    * Process Cumulative Result
            GOTO      TXTDEA10
          ENDIF
.
          CALL      PROLIN00
.
          GOTO      TXTDEA10
.
TXTDEA99  WRITE     HTMLFILE;"</pre></div></td></tr>"
          RETURN
.
CHKLEV00  MATCH     "2",REPDPTL
          GOTO      CHKLEV10 IF EQUAL
          MATCH     "3",REPDPTL
          GOTO      CHKLEV20 IF EQUAL
          GOTO      CHKLEV99
.
CHKLEV10  BRANCH    PITF200,CHKLEV99
          MOVE      ONE,PITF200
          WRITE     HTMLFILE;"</pre></div></td></tr>"
          WRITE     HTMLFILE;"<tr><td align=center bgcolor=##CCCCCC>":
                             "<b>Result Header</b></td>":
                             "<td align=right bgcolor=##CCCCCC>":
                             "<input type=button ":
                             "class=OpenMoreButton id=pit2 ":
                             "onclick='ShowResultHeader()' >":
                             "</td></tr>"
          WRITE     HTMLFILE;"<tr><td colspan=2>":
                             "<div id=pit200 class=pit200>":
                             "<pre>"
          GOTO      CHKLEV99
.
CHKLEV20  BRANCH    PITF300,CHKLEV99
          MOVE      ONE,PITF300
          WRITE     HTMLFILE;"</pre></div></td></tr>"
          WRITE     HTMLFILE;"<tr><td align=center bgcolor=##CCCCCC>":
                             "<b>Result Details</b></td>":
                             "<td align=right bgcolor=##CCCCCC>":
                             "<input type=button ":
                             "class=OpenMoreButton id=pit3 ":
                             "onclick='ShowResultDetail()' >":
                             "</td></tr>"
          WRITE     HTMLFILE;"<tr><td colspan=2>":
                             "<div id=pit300 class=pit300>":
                             "<pre>"
          GOTO      CHKLEV99
.
CHKLEV99  RETURN
.------------------------------------------------------------
. Output Result Audit Table
.------------------------------------------------------------
AUDTAB00  WRITE     HTMLFILE;"<tr>":
                             "<td class=HeadingCell>Web User</td>":
                             "<td class=HeadingCell>Date & Time</td>":
                             "<td class=HeadingCell>Marked as Read</td></tr>"
.
          PACK      RESLNKFL,PREAEA,RESULTKY          * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESAUDA1,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      AUDTAB99 IF EQUAL                 * end I CAR 38290
.
          MOVE      WBSEUID,SAVEWUID
          PACK      KEY40,RESULTKY,SP70
          CALL      RSREAU1
AUDTAB10  CALL      RKREAU1
          BRANCH    OVRCD,AUDTAB99
          PACK      KEY17,REAURDT,REAURTM,REAURUN,SP70
          MATCH     RESULTKY,KEY17
          GOTO      AUDTAB99 IF NOT EQUAL
.
          UNPACK    REAUSDT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          MOVE      REAUUID,KEY10
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      REAUUID,WBSENAM
          ENDIF
          MOVE      "No ",YESNO
          MATCH     "1",REAUMRK
          IF        @EQUAL
            MOVE      "Yes",YESNO
          ENDIF
          WRITE     HTMLFILE;"<tr><td>",WBSENAM,"</td>":
                                 "<td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR:
                                 " at ",REAUSTM,"</td>":
                                 "<td>",YESNO,"</td></tr>"
          GOTO      AUDTAB10
.
AUDTAB99  MOVE      SAVEWUID,KEY10
          CALL      RDWBSE1
          RETURN
.--------------------------------------------------------------------
. Decode PIT Highlight Codes
.--------------------------------------------------------------------
DECODE00  ENDSET    REPDTXT
          RESET     REPDTXT
          PACK      TXT127,REPDTXT,SP70
.
DECODE10  SCAN      "~",TXT127
          GOTO      DECODE90 IF NOT EQUAL
          MOVEFPTR  TXT127,TXTLEN
          SUB       ONE,TXTLEN
          IF        TXTLEN>0
            RESET     TXT127
            SFORMAT   VAR,TXTLEN
            MOVE      TXT127,VAR
            WRITE     HTMLFILE;VAR;
            SFORMAT   VAR,127
            ADD       ONE,TXTLEN
            RESET     TXT127,TXTLEN
            SUB       ONE,TXTLEN
          ENDIF

          UNPACK    TXT127,KEY1,HIGHCODE,TXT127 * Get First Code
.
          CALL      PROHIG00                    * Process Highlight
  
DECODE20  MATCH     "~",TXT127                  * End ~  ?
          GOTO      DECODE30 IF EQUAL
.
          UNPACK    TXT127,HIGHCODE,TXT127      * Get Next Code
          CALL      PROHIG00                    * Process Highlight
          GOTO      DECODE20
.
DECODE30  UNPACK    TXT127,KEY1,TXT127 * Get First Code
          GOTO      DECODE10
.
DECODE90  STRIP     TXT127       * Output Remaining String
          MOVELPTR  TXT127,TXTLEN
          IF        TXTLEN>0
            SFORMAT   VAR,TXTLEN
            MOVE      TXT127,VAR
            WRITE     HTMLFILE;VAR
            SFORMAT   VAR,127
          ELSE
            WRITE     HTMLFILE;SP1
          ENDIF
.
DECODE99  RETURN
.
PROHIG00  MATCH     "SBLD",HIGHCODE
          IF        @EQUAL
            WRITE     HTMLFILE;"<b>";
            GOTO      PROHIG99
          ENDIF
.         
          MATCH     "EBLD",HIGHCODE
          IF        @EQUAL
            WRITE     HTMLFILE;"</b>";
            GOTO      PROHIG99
          ENDIF
.         
          MATCH     "SUND",HIGHCODE
          IF        @EQUAL
            WRITE     HTMLFILE;"<u>";
            GOTO      PROHIG99
          ENDIF
.         
          MATCH     "EUND",HIGHCODE
          IF        @EQUAL
            WRITE     HTMLFILE;"</u>";
            GOTO      PROHIG99
          ENDIF
.         
          MATCH     "SBLK",HIGHCODE
          IF        @EQUAL
            WRITE     HTMLFILE;"<blink>";
            GOTO      PROHIG99
          ENDIF
.         
          MATCH     "EBLK",HIGHCODE
          IF        @EQUAL
            WRITE     HTMLFILE;"</blink>";
            GOTO      PROHIG99
          ENDIF
.         
          MATCH     "FG",HIGHCODE
          IF        @EQUAL
            CALL      SETCOL00
            WRITE     HTMLFILE;"<font color=",COLORSTR,">";
            GOTO      PROHIG99
          ENDIF
.         
PROHIG99  RETURN
.         
SETCOL00  UNPACK    HIGHCODE,KEY2,KEY2
          MOVE      KEY2,F2
          BRANCH    F2,SETCOL01,SETCOL02,SETCOL03,SETCOL04,SETCOL05:
                       SETCOL06,SETCOL07,SETCOL08,SETCOL09,SETCOL10:
                       SETCOL11,SETCOL12,SETCOL13,SETCOL14,SETCOL15
          MOVE      "Black",COLORSTR
          GOTO      SETCOL99
SETCOL01  MOVE      "Blue        ",COLORSTR
          GOTO      SETCOL99
SETCOL02  MOVE      "Green       ",COLORSTR
          GOTO      SETCOL99
SETCOL03  MOVE      "Cyan        ",COLORSTR
          GOTO      SETCOL99
SETCOL04  MOVE      "Red         ",COLORSTR
          GOTO      SETCOL99
SETCOL05  MOVE      "Magenta     ",COLORSTR
          GOTO      SETCOL99
SETCOL06  MOVE      "Brown       ",COLORSTR
          GOTO      SETCOL99
SETCOL07  MOVE      "LightGrey   ",COLORSTR
          GOTO      SETCOL99
SETCOL08  MOVE      "DarkGrey    ",COLORSTR
          GOTO      SETCOL99
SETCOL09  MOVE      "LightBlue   ",COLORSTR
          GOTO      SETCOL99
SETCOL10  MOVE      "LightGreen  ",COLORSTR
          GOTO      SETCOL99
SETCOL11  MOVE      "LightCyan   ",COLORSTR
          GOTO      SETCOL99
SETCOL12  MOVE      "LightRed    ",COLORSTR
          GOTO      SETCOL99
SETCOL13  MOVE      "LightMagenta",COLORSTR
          GOTO      SETCOL99
SETCOL14  MOVE      "Yellow      ",COLORSTR
          GOTO      SETCOL99
SETCOL15  MOVE      "White       ",COLORSTR
          GOTO      SETCOL99
SETCOL99  RETURN
.------------------------------------------------------------
. Extract Results for Cumulative Display
.------------------------------------------------------------
CMRRES00  MATCH     "DHM",REPHLAB
          GOTO      CMRRES10 IF EQUAL
.
          CALL      CSNRES00    * S & N Cumulative
          GOTO      CMRRES99
.
CMRRES10  CALL      CDHRES00    * DHM Cumulative
.
CMRRES99  RETURN
.----------------------------------------------------------------------
. DHM Cumulative Format
.----------------------------------------------------------------------
CDHRES00  CALL      PROLIN00
          UNPACK    REPDTXT,KEY48,KEY8
          PACK      KEY8,KEY8,SP70
          MATCH     SP70,KEY8
          GOTO      CDHRES99 IF EQUAL
.
          MOVE      ZERO,HEADCOL
          MOVE      ZERO,CMLROWNO
          UNPACK    REPHRDT,CCENT,CYEAR,CMON,CDAY
          CALL      CHDDAT00   * Add Current Result Date
.
          MATCH     "FBC",REPHNAM
          IF        @EQUAL
            CALL      FBCDHM00   * FBC for Douglas Hanly Moir
          ELSE
            PACK      REPDTXT,REPDTXT,SP70,SP70
            UNPACK    REPDTXT,KEY48,CDAY,KEY1,CMON,KEY1,CYEAR
            CALL      CHDDAT00   * Add Second Result Set
          ENDIF
.       
CDHRES05  CALL      RKREPD1
          BRANCH    OVRCD,CDHRES99
          PACK      KEY17,REPDRDT,REPDRTM,REPDRUN,SP70
          MATCH     KEY17,RESULTKY
          GOTO      CDHRES99 IF NOT EQUAL
          MATCH     "390",REPDPTL
          GOTO      CDHRES05 IF EQUAL
          MATCH     "399",REPDPTL
          GOTO      CDHRES05 IF EQUAL
.
          MATCH     "Z-VA",REPDTXT
          GOTO      CDHRES05 IF EQUAL
          MATCH     "X-VA",REPDTXT
          GOTO      CDHRES05 IF EQUAL
          MATCH     "------",REPDTXT
          GOTO      CDHRES05 IF EQUAL
.
          CALL      PROLIN00
.
          MATCH     SP20,REPDTXT
          IF        @EQUAL
            CALL      CDHHED00
            GOTO      CDHRES05
          ENDIF
.
          MATCH     "319",REPDPTL          * Blank Line
          GOTO      CDHRES05 IF EQUAL
          MATCH     "311",REPDPTL          * Cumulative Line Finished
          GOTO      CDHRES99 IF NOT EQUAL
.
          PACK      TXT127,SP70,SP70
          PACK      REPDTXT,REPDTXT,SP70,SP70
          MATCH     REPDTXT,TXT127
          GOTO      CDHRES05 IF EQUAL
.
          MATCH     "FBC",REPHNAM
          IF        @EQUAL
            UNPACK    REPDTXT,KEY4,DHMNAM,KEY9,KEY2,DHMLOW,KEY1,DHMHIG
          ELSE
            UNPACK    REPDTXT,DHMNAM,KEY2,DHMLOW,KEY1,DHMHIG
          ENDIF
          MATCH     " (",KEY2
          GOTO      CDHRES05 IF NOT EQUAL
          MATCH     "-",KEY1
          GOTO      CDHRES05 IF NOT EQUAL
          SQUEEZE   DHMLOW
          SQUEEZE   DHMHIG
          MOVE      DHMLOW,REFRLOW
          MOVE      DHMHIG,REFRHIG
.
          MOVE      ONE,CUMLGPH    * Yes we have something
.    
          MATCH     "FBC",REPHNAM
          IF        @EQUAL
            UNPACK    REPDTXT,KEY41,DHMVAL01,DHMVAL02,DHMVAL03,DHMVAL04:
                                    DHMVAL05,DHMVAL06,DHMVAL07
          ELSE
            UNPACK    REPDTXT,KEY38,DHMVAL01,DHMVAL02,DHMVAL03,DHMVAL04:
                                    DHMVAL05,DHMVAL06,DHMVAL07
          ENDIF
          ADD       ONE,CMLROWNO
          PACK      CMLROWHD[CMLROWNO],DHMNAM,SP70
          CLEAR     DSET0VAL[CMLROWNO]
          CLEAR     DSET1VAL[CMLROWNO]
          CLEAR     DSET2VAL[CMLROWNO]
          CLEAR     DSETDATE[CMLROWNO]
          MOVE      ONE,STRFLG
          MOVE      ZERO,F2
CDHRES10  ADD       ONE,F2
          IF        F2>HEADCOL
            RESET     DSET0VAL[CMLROWNO]
            RESET     DSET1VAL[CMLROWNO]
            RESET     DSET2VAL[CMLROWNO]
            RESET     DSETDATE[CMLROWNO]
            GOTO      CDHRES05 
          ENDIF
.
          LOAD      KEY10,F2,DHMVAL01,DHMVAL02,DHMVAL03,DHMVAL04,DHMVAL05:
                             DHMVAL06,DHMVAL07
          PACK      KEY10,KEY10,SP70
          REP       "* ",KEY10
          MATCH     SP70,KEY10
          GOTO      CDHRES10 IF EQUAL
          SQUEEZE   KEY10
          MOVE      KEY10,F6P4
          IF        STRFLG=1
            MOVE      ZERO,STRFLG
          ELSE
            APPEND    COMMA,DSET1VAL[CMLROWNO]
            APPEND    COMMA,DSETDATE[CMLROWNO]
            APPEND    COMMA,DSET0VAL[CMLROWNO]
            APPEND    COMMA,DSET2VAL[CMLROWNO]
          ENDIF
          APPEND    REFRLOW,DSET0VAL[CMLROWNO]
          APPEND    F6P4,DSET1VAL[CMLROWNO]
          APPEND    REFRHIG,DSET2VAL[CMLROWNO]
          APPEND    HEADDAT[F2],DSETDATE[CMLROWNO]
          GOTO      CDHRES10
.         
CDHRES99  RETURN
.
FBCDHM00  PACK      REPDTXT,REPDTXT,SP70,SP70
          UNPACK    REPDTXT,KEY51,CDAY,KEY1,CMON,KEY1,CYEAR
          MATCH     SP2,CDAY
          GOTO      FBCDHM99 IF EQUAL
          CALL      CHDDAT00   * Add Second Result Set
.
          PACK      REPDTXT,REPDTXT,SP70,SP70
          UNPACK    REPDTXT,KEY61,CDAY,KEY1,CMON,KEY1,CYEAR
          MATCH     SP2,CDAY
          GOTO      FBCDHM99 IF EQUAL
          CALL      CHDDAT00   * Add Third Result Set
.
          PACK      REPDTXT,REPDTXT,SP70,SP70
          UNPACK    REPDTXT,KEY71,CDAY,KEY1,CMON,KEY1,CYEAR
          MATCH     SP2,CDAY
          GOTO      FBCDHM99 IF EQUAL
          CALL      CHDDAT00   * Add fourth Result Set
.
          PACK      REPDTXT,REPDTXT,SP70,SP70
          UNPACK    REPDTXT,KEY80,KEY1,CDAY,KEY1,CMON,KEY1,CYEAR
          MATCH     SP2,CDAY
          GOTO      FBCDHM99 IF EQUAL
          CALL      CHDDAT00   * Add fifth Result Set
.
          PACK      REPDTXT,REPDTXT,SP70,SP70
          UNPACK    REPDTXT,KEY80,KEY11,CDAY,KEY1,CMON,KEY1,CYEAR
          MATCH     SP2,CDAY
          GOTO      FBCDHM99 IF EQUAL
          CALL      CHDDAT00   * Add sixth Result Set
.
          PACK      REPDTXT,REPDTXT,SP70,SP70
          UNPACK    REPDTXT,KEY80,KEY21,CDAY,KEY1,CMON,KEY1,CYEAR
          MATCH     SP2,CDAY
          GOTO      FBCDHM99 IF EQUAL
          CALL      CHDDAT00   * Add seventh Result Set
.
          PACK      REPDTXT,REPDTXT,SP70,SP70
          UNPACK    REPDTXT,KEY80,KEY31,CDAY,KEY1,CMON,KEY1,CYEAR
          MATCH     SP2,CDAY
          GOTO      FBCDHM99 IF EQUAL
          CALL      CHDDAT00   * Add eight Result Set
.
FBCDHM99  RETURN
.
CDHHED00  BRANCH    HEADCOL,CDHHED10,CDHHED20,CDHHED30,CDHHED40,CDHHED50:
                            CDHHED60,CDHHED70
          GOTO      CDHHED99
.
CDHHED10  PACK      REPDTXT,REPDTXT,SP70,SP70
          UNPACK    REPDTXT,KEY48,CDAY,KEY1,CMON,KEY1,CYEAR
          CALL      CHDDAT00   * Add Second Result Set
          GOTO      CDHHED99
.
CDHHED20  PACK      REPDTXT,REPDTXT,SP70,SP70
          UNPACK    REPDTXT,KEY58,CDAY,KEY1,CMON,KEY1,CYEAR
          CALL      CHDDAT00   * Add Second Result Set
          GOTO      CDHHED99
.
CDHHED30  PACK      REPDTXT,REPDTXT,SP70,SP70
          UNPACK    REPDTXT,KEY68,CDAY,KEY1,CMON,KEY1,CYEAR
          CALL      CHDDAT00   * Add Second Result Set
          GOTO      CDHHED99
.
CDHHED40  PACK      REPDTXT,REPDTXT,SP70,SP70
          UNPACK    REPDTXT,KEY78,CDAY,KEY1,CMON,KEY1,CYEAR
          CALL      CHDDAT00   * Add Second Result Set
          GOTO      CDHHED99
.
CDHHED50  PACK      REPDTXT,REPDTXT,SP70,SP70
          UNPACK    REPDTXT,KEY80,KEY8,CDAY,KEY1,CMON,KEY1,CYEAR
          CALL      CHDDAT00   * Add Second Result Set
          GOTO      CDHHED99
.
CDHHED60  PACK      REPDTXT,REPDTXT,SP70,SP70
          UNPACK    REPDTXT,KEY80,KEY18,CDAY,KEY1,CMON,KEY1,CYEAR
          CALL      CHDDAT00   * Add Second Result Set
          GOTO      CDHHED99
.
CDHHED70  PACK      REPDTXT,REPDTXT,SP70,SP70
          UNPACK    REPDTXT,KEY80,KEY28,CDAY,KEY1,CMON,KEY1,CYEAR
          CALL      CHDDAT00   * Add Second Result Set
          GOTO      CDHHED99
.
CDHHED99  RETURN
.------------------------------------------------------------
. Add DHM Date Heading
.------------------------------------------------------------
CHDDAT00  ADD       ONE,HEADCOL
          MOVE      CYEAR,F2
          IF        F2>80
            MOVE      "19",CCENT
          ELSE
            MOVE      "20",CCENT
          ENDIF
          MOVE      "/",KEY1
          PACK      HEADDAT[HEADCOL],CMON,KEY1,CDAY,KEY1,CCENT,CYEAR,SP70
          RETURN
.----------------------------------------------------------------------
. S & N Cumulative Format
.----------------------------------------------------------------------
CSNRES00  CALL      PROLIN00
.       
CSNRES05  CALL      RKREPD1
          BRANCH    OVRCD,CSNRES80
          PACK      KEY17,REPDRDT,REPDRTM,REPDRUN,SP70
          MATCH     KEY17,RESULTKY
          GOTO      CSNRES80 IF NOT EQUAL
          MATCH     "390",REPDPTL          * Blank Line
          GOTO      CSNRES05 IF EQUAL
          MATCH     "399",REPDPTL          * Blank Line
          GOTO      CSNRES05 IF EQUAL
.
          CALL      PROLIN00
          MATCH     "319",REPDPTL          * Blank Line
          GOTO      CSNRES05 IF EQUAL
          PACK      TXT127,SP70,SP70
          PACK      REPDTXT,REPDTXT,SP70,SP70
          MATCH     REPDTXT,TXT127
          GOTO      CSNRES05 IF EQUAL
.
          CALL      CMRTOP00               * Process Top Extract Date Columns
          BRANCH    EXIT,CSNRES90
.        
          MOVE      ZERO,CMLROWNO
.
CSNRES10  CALL      RKREPD1
          BRANCH    OVRCD,CSNRES80
          PACK      KEY17,REPDRDT,REPDRTM,REPDRUN,SP70
          MATCH     KEY17,RESULTKY
          GOTO      CSNRES80 IF NOT EQUAL
          MATCH     "390",REPDPTL          * End of Report
          GOTO      CSNRES10 IF EQUAL
          MATCH     "399",REPDPTL          * End of Report
          GOTO      CSNRES10 IF EQUAL
.
          CALL      PROLIN00
          MATCH     "319",REPDPTL          * Blank Line
          GOTO      CSNRES10 IF EQUAL
          MATCH     "311",REPDPTL
          GOTO      CSNRES80 IF NOT EQUAL
          PACK      TXT127,SP70,SP70
          PACK      REPDTXT,REPDTXT,SP70,SP70
          MATCH     REPDTXT,TXT127
          GOTO      CSNRES10 IF EQUAL
.
          CALL      CMRLIN00
.
          GOTO      CSNRES10
.
CSNRES80  MOVE      ZERO,EXIT
          MOVE      ONE,CUMLGPH
          GOTO      CSNRES99
.
CSNRES90  MOVE      ONE,EXIT
.
CSNRES99  RETURN
.------------------------------------------------------------
. Check for top Line and work out dates for results
.------------------------------------------------------------
CMRTOP00  PACK      TXT127,REPDTXT,SP70,SP70
.
CMRTOP10  MOVE      ZERO,HEADCOL
          MOVE      ZERO,POSCNT
.
CMRTOP20  ADD       ONE,HEADCOL
          MOVE      ZERO,STRFLG
          CLEAR     HEADDAT[HEADCOL]
          MOVE      ZERO,HEADSTR[HEADCOL]
          MOVE      ZERO,HEADEND[HEADCOL]

CMRTOP30  ADD       ONE,POSCNT
          UNPACK    TXT127,ANS,TXT127
          CMATCH    SP1,ANS                 * Ignore Blank Space
          GOTO      CMRTOP30 IF EQUAL
          APPEND    ANS,HEADDAT[HEADCOL]
          IF        STRFLG=0
            MOVE      ONE,STRFLG
            MOVE      POSCNT,HEADSTR[HEADCOL]
          ENDIF
          MOVE      POSCNT,HEADEND[HEADCOL]
          CMATCH    SP1,TXT127
          GOTO      CMRTOP30 IF NOT EQUAL
          RESET     HEADDAT[HEADCOL]
          MATCH     "R.RANGE",HEADDAT[HEADCOL]
          IF        !@EQUAL
            UNPACK    HEADDAT[HEADCOL],CDAY,KEY1,CMON,KEY1,KEY4
            PACK      HEADDAT[HEADCOL],CMON,KEY1,CDAY,KEY1,KEY4,SP70
            GOTO      CMRTOP20
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      CMRTOP99
.
CMRTOP90  MOVE      ONE,EXIT
.
CMRTOP99  RETURN
.----------------------------------------------------------------------
. Process Cumulative Result Line Details
.----------------------------------------------------------------------
CMRLIN00  PACK      TXT127,REPDTXT,SP70,SP70
          MOVE      HEADSTR[1],F3                * Determine Row Heading
          SUB       ONE,F3                       * Determine Row Heading
          SFORMAT   VAR,F3                       * Determine Row Heading
          MOVE      TXT127,VAR
          MATCH     SP70,VAR
          GOTO      CMRLIN99 IF EQUAL
          ADD       ONE,CMLROWNO
          PACK      CMLROWHD[CMLROWNO],VAR,SP70
          SFORMAT   VAR,127
          CLEAR     DSET0VAL[CMLROWNO]
          CLEAR     DSET1VAL[CMLROWNO]
          CLEAR     DSET2VAL[CMLROWNO]
          CLEAR     DSETDATE[CMLROWNO]
          MOVE      ONE,STRFLG
          MOVE      ZERO,CNTVAL
.
          MOVE      ZERO,F2
CMRLIN10  ADD       ONE,F2
          COMPARE   F2,HEADCOL
          GOTO      CMRLIN20 IF EQUAL
.
          RESET     TXT127,HEADSTR[F2]                * Determine Row Heading
          SETLPTR   TXT127,HEADEND[F2]
          PACK      KEY20,TXT127,SP70
          REP       "* ",KEY20
          MATCH     SP70,KEY20
          GOTO      CMRLIN10 IF EQUAL
.
          ADD       ONE,CNTVAL
          SQUEEZE   KEY20
          MOVE      KEY20,F6P4
          IF        STRFLG=1
            MOVE      ZERO,STRFLG
          ELSE
            APPEND    COMMA,DSET1VAL[CMLROWNO]
            APPEND    COMMA,DSETDATE[CMLROWNO]
          ENDIF
          APPEND    F6P4,DSET1VAL[CMLROWNO]
          APPEND    HEADDAT[F2],DSETDATE[CMLROWNO]
          GOTO      CMRLIN10
.         
. Reference Range
.
CMRLIN20  RESET     DSET0VAL[CMLROWNO]
          RESET     DSET1VAL[CMLROWNO]
          RESET     DSET2VAL[CMLROWNO]
          RESET     DSETDATE[CMLROWNO]
          SUB       ONE,F2
          RESET     TXT127,HEADEND[F2]                * Determine Row Heading
          ADD       ONE,F2
          SETLPTR   TXT127,HEADEND[F2]
          PACK      TXT127,TXT127,SP70,SP70
          REP       "* ",TXT127
          SQUEEZE   TXT127
          PACK      TXT127,TXT127,SP70,SP70
          SCAN      "-",TXT127
          MOVEFPTR  TXT127,TXTLEN
          RESET     TXT127
          SUB       ONE,TXTLEN
          SFORMAT   VAR,TXTLEN
          MOVE      TXT127,VAR
          SQUEEZE   VAR
          MOVE      ZERO,REFRLOW
          MOVE      VAR,REFRLOW
          SFORMAT   VAR,127
.
          SETLPTR   TXT127,127             * reset logical length
          ADD       TWO,TXTLEN            * position after comma
          RESET     TXT127,TXTLEN          * reset to this point
          PACK      VAR,TXT127,SP70     * reset form pointer
          SQUEEZE   VAR
          MOVE      ZERO,REFRHIG
          MOVE      VAR,REFRHIG
.
          MOVE      ZERO,F2
          MOVE      ONE,STRFLG
CMRLIN30  ADD       ONE,F2
          IF        STRFLG=1
            MOVE      ZERO,STRFLG
          ELSE
            APPEND    COMMA,DSET0VAL[CMLROWNO]
            APPEND    COMMA,DSET2VAL[CMLROWNO]
          ENDIF
          APPEND    REFRLOW,DSET0VAL[CMLROWNO]
          APPEND    REFRHIG,DSET2VAL[CMLROWNO]
          COMPARE   F2,CNTVAL
          GOTO      CMRLIN30 IF NOT EQUAL
.
          RESET     DSET0VAL[CMLROWNO]
          RESET     DSET2VAL[CMLROWNO]
.
CMRLIN99  RETURN
.----------------------------------------------------------------------
. Process Line
.----------------------------------------------------------------------
PROLIN00  PACK      REPDTXT,REPDTXT,SP70
          SCAN      "~",REPDTXT
          IF        @EQUAL
            CALL      DECODE00
            GOTO      PROLIN99
          ENDIF
.
          STRIP     REPDTXT
          MOVELPTR  REPDTXT,TXTLEN
          IF        TXTLEN>0
            SFORMAT   VAR,TXTLEN
          ELSE
            SFORMAT   VAR,ONE
          ENDIF
          MOVE      REPDTXT,VAR
          WRITE     HTMLFILE;VAR
          SFORMAT   VAR,127
.
PROLIN99  RETURN
.----------------------------------------------------------------------
. Output Graph
.----------------------------------------------------------------------
CMRGPH00  MOVE      CMLROWNO,ENDROWNO
          MOVE      ZERO,CMLROWNO
.
CMRGPH10  ADD       ONE,CMLROWNO
          SCAN      ",",DSETDATE[CMLROWNO]   
          GOTO      CMRGPH90 IF NOT EQUAL   * 1 or Zero Results to Graph
          WRITE     HTMLFILE;"<applet code=javachart.applet.dateLineApp.class ":
                             "codebase=/ width=",GRPHWID," height=",GRPHHGH,">"
          WRITE     HTMLFILE;"<param name=CopyrightNotification value=":
                             "#"JavaChart is a copyrighted work, ": 
                             "and subject to full legal protection#">"
          WRITE     HTMLFILE;"<param name=titleFont value=#"Arial,12,1#">":
                             "<param name=titleString value=#"":
                             CMLROWHD[CMLROWNO],"#">"
          WRITE     HTMLFILE;"<param name=dataset0dateValues value=#"":
                             DSETDATE[CMLROWNO],"#" >"
          WRITE     HTMLFILE;"<param name=dataset1dateValues value=#"":
                             DSETDATE[CMLROWNO],"#" >"
          WRITE     HTMLFILE;"<param name=dataset2dateValues value=#"":
                             DSETDATE[CMLROWNO],"#" >"
          WRITE     HTMLFILE;"<param name=dataset0yValues value=#"":
                             DSET0VAL[CMLROWNO],"#" >"
          WRITE     HTMLFILE;"<param name=dataset1yValues value=#"":
                             DSET1VAL[CMLROWNO],"#" >"
          WRITE     HTMLFILE;"<param name=dataset2yValues value=#"":
                             DSET2VAL[CMLROWNO],"#" >":
                             "</applet>"
CMRGPH90  COMPARE   ENDROWNO,CMLROWNO
          GOTO      CMRGPH10 IF NOT EQUAL
          RETURN
.------------------------------------------------------------
. Output Raw Text details with line numbers
.------------------------------------------------------------
TXTALL00  WRITE     HTMLFILE;"<pre>"
          PACK      KEY20,RESULTKY,SP70
          CALL      RSREPD1
TXTALL10  CALL      RKREPD1
          BRANCH    OVRCD,TXTALL99
          PACK      KEY17,REPDRDT,REPDRTM,REPDRUN,SP70
          MATCH     KEY17,RESULTKY
          GOTO      TXTALL99 IF NOT EQUAL
.
          WRITE     HTMLFILE;REPDPTL,SP1;
          CALL      PROLIN00     
.
          GOTO      TXTALL10
.
TXTALL99  WRITE     HTMLFILE;"</pre>"
          RETURN
.
