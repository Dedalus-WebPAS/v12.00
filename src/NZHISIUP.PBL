.*****************************************************************************
.*  Include  :  NZHISIUP.PBL                                                 *
.*                                                                           *
.*  Function :  This include contains the routines required                  *
.*              update the NZ National Database.                             *
.*                                                                           *
.*  Includes :  NZHISIFD.INC - data variables used by this include           *
.*                                                                           *
.*  Routines :                                                               *
.*              NEWHCU   -  Get New NHI No. for patient                      *
.*              UPDHCU   -  Update Nat. database with details                *
.*              MRGHCU   -  Inform NHI of Merge of 2 Nat. No.'s              *
.*              UPDDNR   -  Update Donor and Contact details                 *
.*              UPDMWS   -  Update Medical Warning details                   *
.*                                                                           *
.*        V9.03.01  24/10/2003  Jill Habasque SRF 43783                      *
.*                  Changed reads into two, errors then fields               *
.*****************************************************************************
.************************************************************************
.*  NEWHCU  :  Get a new NHI Number for a patient                       *
.*             Parameters : PMI details in NZIB.... form                *
.************************************************************************
NEWHCU    MOVE      "2 ",NZIBACTN                 * action identifier
          CALL      FIFHD000                      * set up header details
.
          MOVE      "655",WRLNGTH                 * FIFO record length (write)
          WRITE     NZWFIFA1;NZIBSTX,NZIBACTN,NZIBUSER,NZIBVDU,NZIBRFIF:
                    NZIBSURN,NZIBGIV1,NZIBGIV2,NZIBGIV3,NZIBPRNM,NZIBADD1:
                    NZIBADD2,NZIBSUBR,NZIBCITY,NZIBCTRY,NZIBPANO,NZIBDOB:
                    NZIBDDTH,NZIBDOMC,NZIBRESI,NZIBETHN,NZIBETH2,NZIBETH3:
                    NZIBSEX,NZIBACNT:
                    NZIBALSR[1],NZIBALG1[1],NZIBALG2[1],NZIBALG3[1],NZIBALPN[1]:
                    NZIBALSR[2],NZIBALG1[2],NZIBALG2[2],NZIBALG3[2],NZIBALPN[2]:
                    NZIBALSR[3],NZIBALG1[3],NZIBALG2[3],NZIBALG3[3],NZIBALPN[3]:
                    NZIBALSR[4],NZIBALG1[4],NZIBALG2[4],NZIBALG3[4],NZIBALPN[4]:
                    NZIBSCRB,NZIBDUPL,NZIBUPDT;
.
. now get the reply (firstly checking for an error)
.
          MOVE      "86",RDLNGTH                 * FIFO record length (reply)
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBAPPC,NZIBERRN,NZIBERRD;
.
          MATCH     "AA",NZIBAPPC                 * valid read?
          GOTO      NWHCU900 IF NOT EQUAL         * error
.
          MOVE      "276",RDLNGTH                 * FIFO record length (reply)
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBAPPC,NZIBERRN,NZIBERRD:
                    NZIBNMPI,NZIBSURN,NZIBGIV1,NZIBGIV2,NZIBGIV3,NZIBPRNM:
                    NZIBADD1,NZIBADD2,NZIBSUBR,NZIBCITY,NZIBCTRY,NZIBDOB:
                    NZIBDDTH,NZIBDOMC,NZIBRESI,NZIBETHN,NZIBETH2,NZIBETH3:
                    NZIBSEX;
.
          MOVE      ZERO,OVRCD                    * valid read
          GOTO      NWHCU999
.
NWHCU900  MOVE      ONE,OVRCD                     * an error was encountered
.
NWHCU999  RETURN
.************************************************************************
.*  UPDHCU  :  Update National database with details from local system  *
.************************************************************************
UPDHCU    MATCH     NZTMPURP,NMPNUMB
          GOTO      UPHCU800 IF EQUAL       * have a Temp NHI Number 
.
          MOVE      "4 ",NZIBACTN                 * action identifier
          CALL      FIFHD000                      * set up header details
.
          MOVE      "311",WRLNGTH                 * FIFO record length (write)
          WRITE    NZWFIFA1;NZIBSTX,NZIBACTN,NZIBUSER,NZIBVDU,NZIBRFIF,NZIBNMPI:
                          NZIBNMCF,NZIBSURN,NZIBGIV1,NZIBGIV2,NZIBGIV3,NZIBPRNM:
                          NZIBADD1,NZIBADD2,NZIBSUBR,NZIBCITY,NZIBCTRY,NZIBDOB:
                          NZIBDDTH,NZIBDOMC,NZIBRESI,NZIBETHN,NZIBETH2,NZIBETH3:
                          NZIBSEX,NZIBSCRB,NZIBDUPL,NZIBUPDT;
.
. now get the reply (firstly checking for an error)
.
          MOVE      "86",RDLNGTH                 * FIFO record length (reply)
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBAPPC,NZIBERRN,NZIBERRD;
.
          MOVE      "276",RDLNGTH                 * FIFO record length (reply)
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBNMPI:
                          NZIBSURN,NZIBGIV1,NZIBGIV2,NZIBGIV3,NZIBPRNM:
                          NZIBADD1,NZIBADD2,NZIBSUBR,NZIBCITY,NZIBCTRY,NZIBDOB:
                          NZIBDDTH,NZIBDOMC,NZIBRESI,NZIBETHN,NZIBETH2,NZIBETH3:
                          NZIBSEX;
.
          MATCH     "AA",NZIBAPPC                 * valid read?
          GOTO      UPHCU900 IF NOT EQUAL         * error
.
UPHCU800  MOVE      ZERO,OVRCD                    * valid read
          GOTO      UPHCU999
.
UPHCU900  MOVE      ONE,OVRCD                     * an error was encountered
.
UPHCU999  RETURN
.************************************************************************
.*  MRGHCU  :  Inform the NHI of the merger of two national numbers     *
.************************************************************************
MRGHCU    MOVE      "7 ",NZIBACTN                 * action identifier
          CALL      FIFHD000                      * set up header details
.
          MOVE      "45",WRLNGTH
          WRITE     NZWFIFA1;NZIBSTX,NZIBACTN,NZIBUSER,NZIBVDU,NZIBRFIF:
                             NZIBNMPI,NZIBALNO;
.
. now get the reply (firstly checking for an error)
.
          MOVE      "86",RDLNGTH
          READ      NZRFIFA1;NZIBAPPC,NZIBERRN,NZIBERRD;
.
          MATCH     "AA",NZIBAPPC               * valid read?
          GOTO      MRHCU900 IF NOT EQUAL         * error
.
          MOVE      ZERO,OVRCD                    * valid read
          GOTO      MRHCU999
.
MRHCU890  MOVE      TWO,OVRCD                     * Application rejected
          GOTO      MRHCU999
.
MRHCU900  MOVE      ONE,OVRCD                     * an error was encountered
.
MRHCU999  RETURN
+
.******************************************************************************
.*  UPDDNR  :  Update the Donor and Contact details for a patient on Nat. Sys *
.******************************************************************************
UPDDNR    MATCH     NZTMPURP,NMPNUMB
          GOTO      UPDNR800 IF EQUAL       * have a Temp U/R Number 
.
          MOVE      "14",NZIBACTN                 * action identifier
          CALL      FIFHD000                      * set up header details
.
          MOVE      "342",WRLNGTH                 * FIFO record length (write)
          WRITE     NZWFIFA1;NZIBSTX,NZIBACTN,NZIBUSER,NZIBVDU,NZIBRFIF:
                             NZIBNMPI,NZIBUACT,NZIBSTSU:
                             NZIBPCSN,NZIBPCG1,NZIBPCG2,NZIBPCG3,NZIBPRNM:
                             NZIBPCA1,NZIBPCA2,NZIBPCSB,NZIBPCTW,NZIBPCCT:
                             NZIBRELC,NZIBWKPH,NZIBAHPH;
.
. now get the reply (firstly checking for an error)
.
          MOVE      "86",RDLNGTH                  * FIFO record length (reply)
          READ      NZRFIFA1;NZIBAPPC,NZIBERRN,NZIBERRD;
.
          MATCH     "AA",NZIBAPPC                 * valid read?
          GOTO      UPDNR900 IF NOT EQUAL         * error
.
UPDNR800  MOVE      ZERO,OVRCD                    * valid read
          GOTO      UPDNR999
.
UPDNR890  MOVE      TWO,OVRCD                     * Application rejected
          GOTO      UPDNR999
.
UPDNR900  MOVE      ONE,OVRCD                     * an error was encountered
.
UPDNR999  RETURN
+
.****************************************************************************
.*  UPDWMS  :  Update the Medical Warning details for a patient on Nat. Sys *
.****************************************************************************
UPDMWS    
          MATCH     NZTMPURP,NMPNUMB
          GOTO      UPMWS800 IF EQUAL       * have a Temp NHI Number 
.
          MOVE      "13",NZIBACTN                 * action identifier
          CALL      FIFHD000                      * set up header details
.
          MOVE      "157",WRLNGTH                 * FIFO record length (write)
          WRITE     NZWFIFA1;NZIBSTX,NZIBACTN,NZIBUSER,NZIBVDU,NZIBRFIF:
                             NZIBNMPI,NZIBUACT,NZIBSEVR:
                             NZIBDTON,NZIBWTXT,NZIBMARC,NZIBFACC:
                             NZIBDOCC,NZIBSYSI,NZIBMWCD,NZIBENTM;
.
. now get the reply (firstly checking for an error)
.
          MOVE      "86",RDLNGTH                  * FIFO record length (reply)
          READ      NZRFIFA1;NZIBAPPC,NZIBERRN,NZIBERRD;
.
          MATCH     "AA",NZIBAPPC               * valid read?
          GOTO      UPMWS900 IF NOT EQUAL         * error
.
UPMWS800  MOVE      ZERO,OVRCD                    * valid read
          GOTO      UPMWS999
.
UPMWS890  MOVE      TWO,OVRCD                     * Application rejected
          GOTO      UPMWS999
.
UPMWS900  MOVE      ONE,OVRCD                     * an error was encountered
.
UPMWS999  RETURN
.----------------------------------------------------------------------
. Procedure to Load Alias Details into the a file for modifications.
.----------------------------------------------------------------------
          DEFPROC   SETALI00
.
NZARSURN  DIM       25[15]   * Surname/Family name
NZARGIV1  DIM       20[15]   * Given Name 1
NZARGIV2  DIM       20[15]   * Given Name 2
NZARGIV3  DIM       20[15]   * Given Name 3
NZARPRNM  DIM       1[15]    * preferred name indicator
.
.------------------------------------------------------------
. Setup File for Alias
.------------------------------------------------------------
SETALI00  DISPLAY  *P1:24,*EL,*BLINKON,*HON,"  Accessing NZHIS NHI/MWS   "
          OPEN     TMPALIA1,"tmpaliaf"
          MOVE     NHMANMPI,KEY7
          CALL     CHKHCU00
          BRANCH   EXIT,SETALI95,SETALI99
.
SETALI10  CALL     DELALI00
.
          MATCH    ANSY,NZIBALNM
          GOTO     SETALI90 IF NOT EQUAL
.
          MOVE     ZERO,TOTALALI
.
          MOVE     ZERO,F3
          MOVE     F3,TMALLIN
.
SETALI20  CALL     GETAKA
          BRANCH   OVRCD,SETALI90
.
          SQUEEZE  NZIBNARP
          MOVE     NZIBNARP,FORM3
          COMPARE  ZERO,FORM3
          GOTO     SETALI90 IF EQUAL
.
          CALL     ADDALI00
.
          SQUEEZE  NZIBNMRE
          MOVE     NZIBNMRE,FORM3
          COMPARE  ZERO,FORM3
          GOTO     SETALI20 IF NOT EQUAL
.
SETALI90  MOVE     ZERO,EXIT
          GOTO     SETALI99
.
SETALI95  MOVE     ONE,EXIT
          GOTO     SETALI99
.
SETALI99  GOTO     ENDSETAL
.------------------------------------
. Add Alias
.------------------------------------
ADDALI00  MOVE      ZERO,COUNT
          SQUEEZE   NZIBNARP
          MOVE      NZIBNARP,FORM3
          MOVE      TMALLIN,F3
.
ADDALI10  ADD       ONE,COUNT
          IF        COUNT>FORM3
            GOTO      ADDALI99
          ENDIF
          MOVE      NHMAURNO,TMALURNO           * U/R Number
.
ADDALI20  ADD       ONE,F3
          MOVE      F3,TMALLIN
          PACK      KEY11,TMALURNO,TMALLIN
          CALL      RDTMAL1
          COMPARE   ZERO,OVRCD
          GOTO      ADDALI20 IF EQUAL
          MOVE      NZARSURN[COUNT],TMALSUR
          MOVE      NZARGIV1[COUNT],TMALGV1
          MOVE      NZARGIV2[COUNT],TMALGV2
          MOVE      NZARGIV3[COUNT],TMALGV3
          MOVE      NZARPRNM[COUNT],TMALPRE
          CALL      WRTMAL1
          GOTO      ADDALI10
.
ADDALI99  RETURN
.------------------------------------------------------------
. Delete Alias From System
.------------------------------------------------------------
DELALI00  PACK      KEY11,NHMAURNO,SP20
DELALI10  CALL      RSTMAL1
          CALL      RKTMAL1
          BRANCH    OVRCD,DELALI99
          MATCH     NHMAURNO,TMALURNO
          GOTO      DELALI99 IF NOT EQUAL
          PACK      KEY11,TMALURNO,TMALLIN
          CALL      DETMAL1
          GOTO      DELALI10
.
DELALI99  RETURN
.------------------------------------------------------------------------
. GETAKA  :  Get a list of aliases for a NHI Number                
.------------------------------------------------------------------------
GETAKA    MOVE      "9 ",NZIBACTN                 * action identifier
          CALL      FIFHD000                      * set up header details
.
          MOVE      "62",WRLNGTH
          WRITE     NZWFIFA1;NZIBSTX,NZIBACTN,NZIBUSER,NZIBVDU,NZIBRFIF:
                             NZIBNMPI,NZIBCPTR,NZIBMXRP;
.
. now get the reply (firstly checking for an error)
.
          MOVE      "86",RDLNGTH
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBAPPC,NZIBERRN,NZIBERRD;
.
          MATCH     "AA",NZIBAPPC               * valid read?
          GOTO      AKHCU900 IF NOT EQUAL         * error
.
          MOVE      "30",RDLNGTH
          READ      NZRFIFA1,RDLNGTH,PTCNNHTM;NZIBNMFN,NZIBNMRE:
                             NZIBNARP,NZIBCPTR;
          MOVE      "86",RDLNGTH
          MOVE      ONE,NZIBCNT
          SQUEEZE   NZIBNARP
          MOVE      NZIBNARP,FORM3
          WHILE     NZIBCNT <= FORM3
            READ      NZRFIFA1;NZARSURN[NZIBCNT],NZARGIV1[NZIBCNT]:
                               NZARGIV2[NZIBCNT],NZARGIV3[NZIBCNT]:
                               NZARPRNM[NZIBCNT];
            ADD       ONE,NZIBCNT
          DO
.
          MOVE      ZERO,OVRCD                    * valid read
          GOTO      AKHCU999
.
AKHCU900  MOVE      ONE,OVRCD                     * an error was encountered
.
AKHCU999  RETURN
.
ENDSETAL  ENDPROC
.
.
