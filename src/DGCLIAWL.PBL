.************************************************************************
.*  Procedure: DGCLIAWL  -  HL7 Add Waiting List Entry                  *
.*                                                                      *
.*  Author   : Davin Sloan     (17/08/2017)                             *
.*                                                                      *
.*  Requires : PATMA1FD (Patient Master & Extension)                    *
.*             PMSPX2FD (Patient Master Extn.2)                         *
.*             WATTR1FD (Waiting List Patient Procedures)               *
.*             WATTX1FD (Waiting List file extension 1)                 *
.*                                                                      *
.*             WATCONFD (Waiting List Control File)                     *
.*                                                                      *
.*  Mods     :                                                          *
.*                                                                      *
.************************************************************************
.*  V11.04.01 09/01/2024 Thanh T           TSK 0936263                  *
.*            Added OPEN/CLOSE PMSQPXA1 index                           *
.************************************************************************
.*  V10.11.00 17/08/2017  Davin            TSK 0837617                  *
.*            Created include                                           *
.************************************************************************
          DEFPROC   DGCLIAWL
.
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
          INC       PMSQPTFD/INC
          INC       WATQUEFD/INC
.
CISMFLAG  FORM      1
IBAMFLAG  FORM      1
MESSAGID  DIM       20
.
DGCLI000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,HUND11;*32,WTCNI12M
.
          MATCH     "1",IBCNUCIS                 * using HL7 messaging ?
          GOTO      DGCLI999 IF LESS             * no - finished
.
.         Check if we are using this feature
.
          MATCH     "1",WTCNI12M                 * sending I12 messages ?
          GOTO      DGCLI999 IF NOT EQUAL        * no - finished
.
.         Open the relevant broadcast message holding tables
.
          OPEN      HL7CISA1,"hl7cisaf"
          OPEN      PMSQPTA1,"pmsqptaf"
          OPEN      PMSQPXA1,"pmsqpxaf"
          OPEN      WATQUEA1,"watqueaf"
.
          MOVE      ONE,CISMFLAG                 * yes- set flag for CIS message
.
          CALL      DGMESSID                     * get the unique message id
.
.         Write a record to pmsqptaf (holding patient details)
.
          MOVE      MESSAGID,PMQPMESI
          MOVE      SP8,PMQPOURN
          PACK      PMQPSPAR,SP70,SP70
          CALL      WRPMQPT1
.
.         Write a record to watqueaf (holding W/L details)
.
          MOVE      MESSAGID,WTQUMESI
          PACK      WTQUSPAR,SP70
          CALL      WRWTQUE1
.
.         Write header record (hl7cisaf)
.
          MOVE      "I12",H7CIMETP
          MOVE      HL7TRGID,H7CITRID
          MOVE      HL7INCLD,H7CIINCL
          CALL      WRCIS000
.
          CLOSE     HL7CISA1
          CLOSE     PMSQPTA1
          CLOSE     PMSQPXA1
          CLOSE     WATQUEA1
.
DGCLI999  GOTO      DGCLIEND                     * end procedure
.
.         IO's go in here
.
          INC       BRHLCOMN
          INC       CLPMSQPX
.
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       IBAOVRIO/INC
          INC       PMSQPTIO/INC
          INC       WATQUEIO/INC
.
DGCLIEND  ENDPROC
+
