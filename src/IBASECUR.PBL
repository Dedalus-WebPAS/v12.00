.******************************************************************************
.* Include Name   : IBASECUR.PBL                                              *
.*                                                                            *
.* Description    : Keyin The System Parameter Security Code                  *
.*                                                                            *
.* Author         : Junior  29/12/1995                                        *
.*                                                                            *
.* Files Req      : IBACONFD/INC            * System control file             *
.*                                                                            *
.* Variables Req  : FORM8     FORM      8                                     *
.*                  PASS      DIM       8   * Keyed in password               *
.*                  REPFLAG   FORM      1   * Valid security entry flag (set  *
.*                                            to 0 at program selection)      *
.*                                                                            *
.* Returns        : EXIT      1             * No/invalid password             *
.*                            0             * Valid password                  *
.*                  REPFLAG   1             * Valid security entry exists     *
.*                            0             * No security entry               *
.*                                                                            *
.******************************************************************************
.* Mods     : V5.07     06/10/98  Jim Stathopoulos                            *
.*                      507 Changes                                           *
.******************************************************************************
IBASECUR  BRANCH    REPFLAG,IBASEC90      * Already have a valid security entry
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWO;*233,CSPASSWD
.
. ----- Keyin The Password -----
.
          KEYIN     *P1:24,*EL,*P20:24,"Enter Password : ",*EOFF,PASS;
.
          RESET     PASS
          GOTO      IBASEC95 IF EOS         * No password entered
.
          ENDSET    PASS
          APPEND    SP8,PASS
          RESET     PASS
.
          MATCH     PASS,CSPASSWD
          GOTO      IBASEC95 IF NOT EQUAL   * Not the correct password
.
. ----- Keyin The Date -----
.
          MOVE      ZERO,FORM8
          KEYIN     *EOFF,PASS;
          MOVE      PASS,FORM8
          SUB       CDD,FORM8
          SUB       CMM,FORM8
          SUB       CYY,FORM8
          COMPARE   ZERO,FORM8
          GOTO      IBASEC95 IF NOT EQUAL   * Not the correct date
.
IBASEC90  MOVE      ONE,REPFLAG             * Set as valid security entry
          MOVE      ZERO,EXIT               * Valid password
          GOTO      IBASEC99
.
IBASEC95  MOVE      ZERO,REPFLAG            * Set as invalid security entry
          MOVE      ONE,EXIT                * Invalid password
IBASEC99  RETURN
