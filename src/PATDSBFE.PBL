.PATDSBFE.PBL
.
.   This is an include to display the bed fee for a given claim code,
.      Health fund and health fund table.
.
.     COMFLAG = Computer Generate Rate flag
.     CBFLAG  = Flag for the current bed fee
.     NOFEE   = Flag for no bed fee available
.     DEFLAG   =  1    - using default
.
.Modifications :
.        V11.04.01  28/11/2023  J.Tan          TSK 0939602
.                   Added default Hospital id from websecaf for pre admission
.        V10.05.02  02/03/2015  J.Tan          CAR 311352
.                   Mods for Public Hosp. Bed fees with only Overnight fee setup
.        V10.05.01  19/02/2015  J.Tan          CAR 311352
.                   Mods for Public Hosp. Bed fees with only Daycase fee setup
.        V10.04.01  25/06/2014  J.Tan          CAR 302233
.                   Mods to get daycase rate for NHOSP=D
.        V10.03.01  05/09/2012  J.Tan          CAR 267784
.                   Mods Checking for TCINDC19 Claim Code
.        V10.01.02  09/03/2011  J.Tan         CAR 233267
.                   Removed CFINAN
.        V10.01.01  09/12/2010 J.Tan   CAR 233034
.                   Mods bed fees file to use health fund table type
.         V9.08.02  20/03/06 J.Tan   CAR 120014
.                   Fixed checking for eff.date of NZ Bed fees
.         V9.08.01  24/11/06 J.Tan   CAR 120004
.                   Mods for NZ Private billing
.         V9.04.01  05/10/04 J.Tan   CAR 53798
.                   Mods for default claim code for multi hospital  
.         V9.02.03  06/05/03 J.Tan
.                   Fixed no bedfee setup when it's the last record 
.                   in bedfee file
.         V9.02.02  09/08/02 J.Tan
.                   Mods for specialty beds
.         V9.02.01  27/05/2001  J.Tan                                        
.                   Removed displays     
.         V5.08.01  06/06/2000  Jill Habasque                                
.                   Changed from key10 to key14 for the health fund file      
.         V5.07.01  13/07/1999  Greg Horvat
.                   Replaced certain variables & with KEY variables
.                V5.01.01  DIG  15/01/91
.                          Added handling of PTCNADBF and PTCNPRSB parameters
.                          for the use of the PATBFEE file in HOS31
.
PATDSBFE  
PRDS0000
          COMPARE       FIVE,CFEETYP
          GOTO          NZPDSBFE IF EQUAL         * NZ Private bed fees
.
          MOVE          ZERO,NOFEE
          MOVE          ZERO,CBFLAG
          MOVE          ZERO,DEFLAG
          MOVE          ZERO,BFCOL
          MOVE          ZERO,SBFENDDY
          MOVE          ZERO,SAVCOL
          MOVE          ZERO,DEFCOL         * initialise current bed fee column
.
          MOVE          SP70,PTHFBCAT
          PACK          KEY14 WITH AFUNDH,AFNDTB,SP10
          MATCH         SP70,KEY14
          IF            !@EQUAL
            CALL          RDFUND1
            BRANCH        OVRCD,PRDS9000
          ENDIF
.
          IF            CFEETYP<>9
            PACK          KEY9,ACLAIM,AFUNDH,SP70
            CALL          GETCNEFF               * Get Contract Effective From
            BRANCH        EXIT,PRDS9000
          ENDIF
.
          PACK          KEY18 WITH ACLAIM,AFUNDH,PTHFBCAT,SAVATYP,SAVBTYP
          PACK          KEY23,ACLAIM,AFUNDH,AFNDTB,SAVATYP,SAVBTYP
          MOVE          ZERO,FORM2
.
.               Display the Bed Fee for the above codes
.
PRDS1000  MOVE          ZERO,NUMBER
          MOVE          SP3,SBFBTYPE
          MOVE          ZERO,BFCOL
          MOVE          ZERO,SBFENDDY
.
.         Display  all  bed fee for the above parameters
.
          COMPARE       NINE,CFEETYP
          GOTO          PRDS2000 IF EQUAL         * Johns Hopkins bed fees
.
          PACK          KEY27 WITH KEY18,SP70
          CALL          RDSBFEE1
PRDS1200  CALL          RDKBFEE1
          BRANCH        OVRCD OF PRDS3000
.
          PACK          DIM18A WITH BFCLM,BFHFUND,PTBFHTYP,BFATYPE,BFBTYPE,SP70
.
          MATCH         KEY18,DIM18A
          GOTO          PRDS3000 IF NOT EQUAL
.
          MOVE          ADATE,CEFFDATE
          LOAD          CEFFDATE,CNTRCEFR,ADATE,DDATE
.
.         If Contract Effective From is 'For Discharges From', Discharged date
.         is blank and TCINDC19=D, default Effective date to Current date
.
          IF        CNTRCEFR=2
            MATCH     "PATWEB89",PRGID
            IF        @EQUAL
              MOVE      ADATE,CEFFDATE    * Use admission date for Adm.prog.
            ELSE
              PACK      DDATE,DDATE,SP70
              MATCH     SP70,DDATE
              IF        @EQUAL
                PACK      KEY5,CODECL,ACLAIM
                CALL      RDCODE1
                IF        OVRCD=0
                  MATCH     "D",TCINDC19
                  IF        @EQUAL
                    PACK      CEFFDATE,CCC,CYY,CMM,CDD,SP70
                    REP       " 0",CEFFDATE
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          MOVE          PTBFCNID,KEY6         * Contract ID
          CALL          VALCONTR              * Validate the Contract ID
          BRANCH        EXIT,PRDS1200
          MOVE          PTBFCNID,CONTRCID     * Contract ID
          GOTO          PRDS4000
.
.         Johns Hopkins bed fees
.
PRDS2000  OPEN          JHSBFEA1,"jhsbfeaf"
          PACK          KEY26 WITH KEY23,SP70
          CALL          RSJHBFE1
PRDS2200  CALL          RKJHBFE1
          BRANCH        OVRCD OF PRDS3000
.
          PACK          DIM23A WITH BFCLM,BFHFUND,BFHFTAB,BFATYPE,BFBTYPE,SP70
.
          MATCH         KEY23,DIM23A
          GOTO          PRDS4000 IF EQUAL
.
.         Check to see if we found anything
.
PRDS3000  MOVE          ZERO,OVRCD
.
.         Public Hospital, if the previous bed fee column was for Daycase only
.         check for next default setup
.
          IF            CHOSTYP=1
            MATCH         "PATWEB89",PRGID
            GOTO          PRDS3100 IF NOT EQUAL
.
            IF            BFCOL<>0
              COMPARE       ZERO,SBFENDDY
              GOTO          PRDS3150 IF EQUAL
            ENDIF
          ENDIF
.
PRDS3100  COMPARE       ZERO,BFCOL
          GOTO          PRDS9999 IF NOT EQUAL
.
PRDS3150  MOVE          ONE,OVRCD
          ADD           ONE,FORM2
          BRANCH        FORM2 OF PRDS3200,PRDS3400
          GOTO          PRDS9000
.
PRDS3200  PACK          KEY18 WITH ACLAIM,SP6,SP3,SAVATYP,SAVBTYP
          PACK          KEY23 WITH ACLAIM,SP6,SP8,SAVATYP,SAVBTYP
          GOTO          PRDS1000
.
PRDS3400  MOVE          ONE,DEFLAG
          IF            IBCNMHOS=1
            OPEN          PMSVX1A1,"pmsvx1af"
            OPEN          PATHSPA1,"pathspaf"
.
            MOVE          SP10,PMVXMHOS
            MOVE          AADMNO,KEY8
            CALL          RDPMVX11
            IF            OVRCD=1
              PACK          KEY3,WBSEHOSP,SP70    * use websec hospital
            ELSE
              MOVE          PMVXMHOS,KEY3
            ENDIF
            CALL          RDPTHSP1
            IF            OVRCD=0
              MOVE          PTHSDCLM,PTCNDCLM     * default claim code charging
            ENDIF
          ENDIF
.
          PACK          KEY18 WITH PTCNDCLM,SP6,SP3,SAVATYP,SAVBTYP
          PACK          KEY23 WITH PTCNDCLM,SP6,SP8,SAVATYP,SAVBTYP
          MOVE          SP1,ANS
.
          BRANCH        PTCNADBF,PRDS1000   * skip if automatically using
.                                             defaults for PRS2
          GOTO          PRDS9000
.
.         Display the bed fee
.
PRDS4000  MATCH         BFBTYPE,SBFBTYPE
          GOTO          PRDS4200 IF EQUAL
.
          MOVE          BFBTYPE,SBFBTYPE
          MOVE          ZERO,BFCOL
          MOVE          ZERO,SBFENDDY
.
PRDS4200  MOVE          ZERO,TOTAL
          MOVE          BFPAT,TOTAL
          ADD           BFHF,TOTAL
          ADD           ONE,BFCOL
          MOVE          BFENDDY,SBFENDDY
.
.         Check if the patient is a day case
.
          COMPARE       THREE,ASTAT
          GOTO          PRDS4400 IF NOT EQUAL
.
          PACK          NHOSP,NHOSP,SP70
          CMATCH        "D",NHOSP
          GOTO          PRDS4400 IF NOT EQUAL
.
.     For DAYCASE Fee the BFENDDY is equal to zero
.
          COMPARE       ZERO,BFENDDY
          GOTO          PRDS4800 IF EQUAL
.
          IF            CHOSTYP=1
            MATCH         "PATWEB96",PRGID
            IF            @EQUAL
              BRANCH        BFCOL,PRDS4800     * Take the first column, if no DC
            ENDIF
          ENDIF
          GOTO          PRDS5000
.
.         If the NOHOSP is zero then the default is the first column of 
.                    the bed fee but not the day case
.
PRDS4400  COMPARE       ZERO,NOHOSP
          GOTO          PRDS4600 IF NOT EQUAL
.
          COMPARE       ZERO,BFENDDY
          GOTO          PRDS5000 IF EQUAL
          GOTO          PRDS4800
.
PRDS4600  COMPARE       NOHOSP,BFENDDY
          GOTO          PRDS5000 IF LESS
.
PRDS4800  BRANCH        CBFLAG OF PRDS5000
          MOVE          ONE,CBFLAG              * Current bed fee
          MOVE          BFPAT,SAVPAT
          MOVE          BFHF,SAVHF
          MOVE          BFENDDY,SAVEND
          MOVE          BFCOL,SAVCOL
          MOVE          BFCOL,DEFCOL            * save the current bed column
.
          BRANCH        COMFLAG OF PRDS5000
.
.         Only Generate once
.
          MOVE          BFPAT,RCGPAT
          MOVE          BFHF,RCGHF
          MOVE          BFENDDY,REDAYS
          MOVE          ONE,COMFLAG
.
PRDS5000  MOVE          ZERO,NUMBER
          MOVE          BFENDDY,NUMBER
          ADD           ONE,NUMBER
          STORE         BFENDDY WITH BFCOL OF BFEND1,BFEND2,BFEND3,BFEND4:
                                              BFEND5,BFEND6
.
          MOVE          ZERO,BFENDDY
          MOVE          BFCOL,MAXNUM
          COMPARE       NINE,CFEETYP
          GOTO          PRDS2200 IF EQUAL       * Johns Hopkins bed fees
          GOTO          PRDS1200
.
PRDS9000  MOVE          ONE,NOFEE
PRDS9999  RETURN
+
.******************************************************************************
.        NZ Private bed fees
.******************************************************************************
NZPDSBFE
NZPDS000  OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      NZPBFEA1,"nzpbfeaf"
          MOVE      PTWDHOSN,PMVXMHOS
.
          IF        IBCNMHOS=1
            OPEN      PATHSPA1,"pathspaf"
            MOVE      PMVXMHOS,KEY3
            CALL      RDPTHSP1
            IF        OVRCD=0
              MOVE      PTHSDCLM,PTCNDCLM     * default claim code charging
            ENDIF
          ENDIF
.
          MOVE      ZERO,NOFEE
          MOVE      ZERO,CBFLAG
          MOVE      ZERO,DEFLAG
          MOVE      ZERO,BFCOL
          MOVE      ZERO,SAVCOL
          MOVE      ZERO,DEFCOL         * initialise current bed fee column
.                          
          PACK      KEY5,ANSC,ANSL,ACLAIM,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            MATCH     ANSF,TCINDC7
            IF        @EQUAL
              GOTO      NZPDS999     * no bed fees for contract patients
            ENDIF
          ENDIF
.
          PACK      KEY26 WITH PMVXMHOS,ACLAIM,AFUNDH,SP8:
                               SAVATYP,SAVBTYP,SP70
          MOVE      ZERO,FORM2
.
NZPDS100  CALL      CEDT0000                  * Get last effective date
          MATCH     SP70,DIM8
          GOTO      NZPDS140 IF EQUAL         * No record found
.
          MOVE      ZERO,NUMBER
          MOVE      SP3,SBFBTYPE
          MOVE      ZERO,BFCOL
.
.         Display  all  bed fee for the above parameters
.
          PACK      KEY34,KEY26,DIM8,SP70
          PACK      KEY37,KEY26,DIM8,SP70
          CALL      RSNZBFE1
NZPSD120  CALL      RKNZBFE1
          BRANCH    OVRCD OF NZPDS140
.
          PACK      DIM34A WITH NZBFHOSP,NZBFCLMC,NZBFCNTR,NZBFFTAB,NZBFATYP:
                                NZBFBTYP,NZBFEFDT,SP70
          MATCH     KEY34,DIM34A
          GOTO      NZPDS200 IF EQUAL
.
.         Check to see if we found anything
.
NZPDS140  MOVE      ZERO,OVRCD
          COMPARE   ZERO,BFCOL
          GOTO      NZPDS300 IF NOT EQUAL
.
          MOVE      ONE,OVRCD
          ADD       ONE,FORM2
          BRANCH    FORM2 OF NZPDS180,NZPDS190
.
          MOVE      ONE,NOFEE
          GOTO      NZPDS999
.
NZPDS180  PACK      KEY26 WITH PMVXMHOS,ACLAIM,SP6,SP8,SAVATYP,SAVBTYP
          GOTO      NZPDS100
.
NZPDS190  MOVE      ONE,DEFLAG
          PACK      KEY26 WITH PMVXMHOS,PTCNDCLM,SP6,SP8,SAVATYP,SAVBTYP
          GOTO      NZPDS100
.
.             Display the bed fee
.
NZPDS200  MOVE      ZERO,TOTAL
          MOVE      NZBFRATE,TOTAL
          ADD       NZBFREBA,TOTAL
          ADD       ONE,BFCOL
.
.         Check if the patient is a day case
.
          COMPARE   THREE,ASTAT
          GOTO      NZPDS230 IF NOT EQUAL
.
          PACK      NHOSP,NHOSP,SP70
          CMATCH    "D",NHOSP
          GOTO      NZPDS230 IF NOT EQUAL
.
.         For DAYCASE Fee the NZBFEDAY is equal to zero
.
          MATCH     "  0",NZBFEDAY
          GOTO      NZPDS260 IF EQUAL
          GOTO      NZPDS280
.
.         If the NOHOSP is zero then the default is the first column of
.                    the bed fee but not the day case
.
NZPDS230  MOVE      ZERO,FORM3
          MOVE      NZBFEDAY,FORM3
          COMPARE   ZERO,NOHOSP
          GOTO      NZPDS240 IF NOT EQUAL
.
          COMPARE   ZERO,FORM3
          GOTO      NZPDS280 IF EQUAL
          GOTO      NZPDS260
.
NZPDS240  COMPARE   NOHOSP,FORM3
          GOTO      NZPDS280 IF LESS
.
NZPDS260  BRANCH    CBFLAG OF NZPDS280
          MOVE      ONE,CBFLAG              * Current bed fee
          MOVE      NZBFRATE,SAVPAT
          MOVE      NZBFREBA,SAVHF
          MOVE      NZBFEDAY,SAVEND
          MOVE      BFCOL,SAVCOL
          MOVE      BFCOL,DEFCOL            * save the current bed column
.
          BRANCH    COMFLAG OF NZPDS280
.
.         Only Generate once
.
          MOVE      NZBFRATE,RCGPAT
          MOVE      NZBFREBA,RCGHF
          MOVE      NZBFEDAY,REDAYS
          MOVE      ONE,COMFLAG
.
NZPDS280  MOVE      ZERO,NUMBER
          MOVE      NZBFEDAY,NUMBER
          ADD       ONE,NUMBER
.
          MOVE      NZBFEDAY,FORM3
          STORE     FORM3 WITH BFCOL OF BFEND1,BFEND2,BFEND3,BFEND4:
                                        BFEND5,BFEND6
          MOVE      SP70,BFENDDY
          MOVE      BFCOL,MAXNUM
          GOTO      NZPSD120
.
NZPDS300  MOVE      ZERO,SAVTOT
          ADD       SAVPAT,SAVTOT
          ADD       SAVHF,SAVTOT
.
          COMPARE   ZERO,SAVCOL
          GOTO      NZPDS999 IF EQUAL
.
          LOAD      SAVEND WITH SAVCOL OF BFEND1,BFEND2,BFEND3,BFEND4:
                                          BFEND5,BFEND6
.
          PACK      KEY37 WITH KEY26,DIM8,SAVEND,SP70
          CALL      RDNZBFE1
          BRANCH    OVRCD OF NZPDS999
.
          MOVE      NZBFREBA,SAVHF
          MOVE      NZBFRATE,SAVPAT
          MOVE      NZBFEDAY,SAVEND
.
          MOVE      ZERO,SAVTOT
          ADD       SAVHF,SAVTOT
          ADD       SAVPAT,SAVTOT
.
NZPDS999  RETURN
+
.------------------------------------------------------------
.         Get the last effective date
.------------------------------------------------------------
CEDT0000  MOVE      SP70,DIM8
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY34,KEY26,KEY8,SP70
          PACK      KEY37 WITH KEY34,SP70
          CALL      RSNZBFE1
CEDT1000  CALL      RKNZBFE1
          BRANCH    OVRCD,CEDT2000
          PACK      DIM34A WITH NZBFHOSP,NZBFCLMC,NZBFCNTR,NZBFFTAB,NZBFATYP:
                                NZBFBTYP,NZBFEFDT,SP70
          MATCH     DIM34A,KEY34
          GOTO      CEDT4000 IF EQUAL
.
CEDT2000  CALL      RPNZBFE1                  * check for previous record
          BRANCH    OVRCD,CEDT9999
.
          PACK      DIM26A WITH NZBFHOSP,NZBFCLMC,NZBFCNTR,NZBFFTAB,NZBFATYP:
                                NZBFBTYP,SP70
          MATCH     DIM26A,KEY26
          GOTO      CEDT9999 IF NOT EQUAL
.
CEDT4000  MOVE      NZBFEFDT,DIM8         * Save the effective date
CEDT9999  RETURN
+
