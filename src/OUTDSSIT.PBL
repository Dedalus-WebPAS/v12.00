+
.******************************************************************************
.
.         OUTDSSIT.PBL
.         ------------
.         This include displays entries in Site Master file.
.
.     Modifications: 28/11/89 Graeme Williams
.                    Added the parameter CNEWDS to determine if the include
.                    is to use the new standards for "?" routines
.
.------------------------------------------------------------------------------
.         Display Category Heading, and then loop over entries
.------------------------------------------------------------------------------
.
OUTDSSIT  DISPLAY   *P1:3,*EF,*P30:4,*V2LON,*ULON,"SITE MASTER"
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
.
          PACK      KEY6,SP6
          CALL      RDSSITA1
ODSS1000  CALL      RDKSITA1
          BRANCH    OVRCD,ODSS2000
.
          DISPLAY   *PCCOL:CVERT,*V2LON,OSTSITE,SP1,*HOFF,OSTDESC
.
          ADD       ONE,CVERT
          COMPARE   TWENTY4,CVERT
          GOTO      ODSS1000 IF LESS
.
          MOVE      FIVE,CVERT
          ADD       "40",CCOL
          COMPARE   "70",CCOL
          GOTO      ODSS1000 IF LESS
.
.         Check for new standards question, or old question
.
          BRANCH    CNEWDS,ODSS1200
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSE,ANS
          GOTO      ODSS3000 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      OUTDSSIT IF EQUAL
.
          GOTO      ODSS1400
.
.         Use the new standards question
.
ODSS1200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSC,ANS
          GOTO      ODSS3000 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      OUTDSSIT IF EQUAL
.
ODSS1400  MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
          GOTO      ODSS1000
.
.      End of codes
.
ODSS2000  BRANCH    CNEWDS,ODSS3000
.
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
.
ODSS3000  MOVE      ZERO,OVRCD
          MOVE      SP30,OSTDESC
          RETURN
.
