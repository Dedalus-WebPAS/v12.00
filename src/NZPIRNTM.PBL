. *----------------------------------------------------------------------
. * Include Name  :   NZPIRNTM.PBL
. *
. * Function      :   Common CGI functions for NZ Private Invoice Range File
. *
. * Modifications :
. *
. * V10.01.01 11/11/2010 Ebon Clements 233071
. *           Added hospital to next and prev
. * V10.00.01 29/07/2010 J.Tan     CAR 227062
. *           Added Next and Previous screen
. * V9.08.00  14/11/2006 J.Tan     CAR 124752
. *           Created Include
. *----------------------------------------------------------------------
.------------------------------------------------------------------------
.   Clear CGI Parameters for NZPIRNFD
.------------------------------------------------------------------------
CPNZIR00  MOVE      SP70,NZIRN001
          MOVE      SP70,NZIRN002
          MOVE      SP70,NZIRN003
          MOVE      ZERO,NZIRN004
          MOVE      ZERO,NZIRN005
CPNZIR99  RETURN
.
.------------------------------------------------------------
.   Set Parameters for NZPIRNFD
.------------------------------------------------------------
SPNZIR00  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPNZIR01,SPNZIR02,SPNZIR03,SPNZIR04,SPNZIR05
.
          MOVE      ONE,EXIT
          GOTO      SPNZIR99
.
SPNZIR01  PACK      NZIRN001,QRYDATA,SP70
          GOTO      SPNZIR99
.
SPNZIR02  PACK      NZIRN002,QRYDATA,SP70
          GOTO      SPNZIR99
.
SPNZIR03  PACK      NZIRN003,QRYDATA,SP70
          GOTO      SPNZIR99
.
SPNZIR04  MOVE      QRYDATA,NZIRN004
          GOTO      SPNZIR99
.
SPNZIR05  MOVE      QRYDATA,NZIRN005
          GOTO      SPNZIR99
.
SPNZIR99  RETURN
.
.--------------------------------------------------------------------------
. NZ Private Invoice Range Maintenance Output
.--------------------------------------------------------------------------
NZIR0000  BRANCH    FLDITMNO,NZIR0100,NZIR0200,NZIR0300,NZIR0400,NZIR0500:
                             NZIR0600,NZIR0700
          GOTO      NZIR9999
.
NZIR0100  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          COMPARE   ONE,IBCNMHOS
          GOTO      NZIR9999 IF NOT EQUAL
.
          BRANCH    F1,NZIR0110,NZIR0120
.
          PACK      KEY3,NZIRHOSP,SP70
          CALL      RDPTHSP1
          BRANCH    OVRCD,NZIR9999
.
          WRITE     HTMLFILE;PTHSNAME;        * hospital name
          GOTO      NZIR9999
.
NZIR0110  WRITE     HTMLFILE;NZIRHOSP;        * hospital code
          GOTO      NZIR9999
.
NZIR0120  MOVE      NZIRHOSP,DIM3
          CALL      NZIH0000                  * hospital selectin list
          GOTO      NZIR9999
.
NZIR0200  WRITE     HTMLFILE;NZIRIRNG;        * Invoice range code
          GOTO      NZIR9999
.
NZIR0300  WRITE     HTMLFILE;NZIRDESC;        * Invoice range description
          GOTO      NZIR9999
.
NZIR0400  WRITE     HTMLFILE;NZIRNEXT;        * Next invoice number
          GOTO      NZIR9999
.
NZIR0500  WRITE     HTMLFILE;NZIRMAXI;        * Maximum invoice range number
          GOTO      NZIR9999
.
NZIR0600  CALL      NEXTIR00                  * Next screen
          GOTO      NZIR9999
.
NZIR0700  CALL      PREVIR00                  * Previous screen
          GOTO      NZIR9999
.
NZIR9999  RETURN
+
.------------------------------------------------------------
. Link to Next set of Hospital based Invoice Range
.------------------------------------------------------------
.
NEXTIR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REP       " +",STARTKEY
          REP       " +",NZIRN001
          REP       " +",NZIRN002
          REP       " +",FILTHOSP
.
          WRITE     HTMLFILE;"patweb61.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&startkey=",STARTKEY:
                                 "&nzirn001=",NZIRN001:
                                 "&nzirn002=",NZIRN002:
                                 "&filthosp=",FILTHOSP;
.
          REP       "+ ",STARTKEY
          REP       "+ ",NZIRN001
          REP       "+ ",NZIRN002
          REP       "+ ",FILTHOSP
.
NEXTIR99  RETURN
+
.------------------------------------------------------------------------------
. Link to previous set of Hospital based Invoice Range
.------------------------------------------------------------------------------
.
PREVIR00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE    ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F1
.
          REP       " +",STARTKEY
          REP       " +",NZIRN001
          REP       " +",NZIRN002
          REP       " +",FILTHOSP
.
          WRITE     HTMLFILE;"patweb61.pbl":
                                 "?reportno=",F1:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&startkey=",STARTKEY:
                                 "&nzirn001=",NZIRN001:
                                 "&nzirn002=",NZIRN002:
                                 "&filthosp=",FILTHOSP;
.
          REP       "+ ",STARTKEY
          REP       "+ ",NZIRN001
          REP       "+ ",NZIRN002
          REP       "+ ",FILTHOSP
.
PREVIR99  RETURN
+
.------------------------------------------------------------
.  Update NZPIRNFD file
.------------------------------------------------------------
MVNZIR00  MOVE      NZIRN001,NZIRHOSP
          MOVE      NZIRN002,NZIRIRNG
          MOVE      NZIRN003,NZIRDESC
          MOVE      NZIRN004,NZIRNEXT
          MOVE      NZIRN005,NZIRMAXI
MVNZIR99  RETURN
.
.------------------------------------------------------------
. Hospital Selection
.------------------------------------------------------------
NZIH0000  PACK      KEY3,SP70
          CALL      RSPTHSP1
NZIH0100  CALL      RKPTHSP1
          BRANCH    OVRCD,NZIH9999
.
          MOVE      PTHSNAME,KEY50
          PACK      KEY5,QUOTE,PTHSHOSP,QUOTE
          MATCH     DIM3,PTHSHOSP
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY5," selected>":
                               KEY50,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY5,">",KEY50,"</option>"
          ENDIF
          GOTO      NZIH0100
.
NZIH9999  RETURN
.
