+
.******************************************************************************
.
.         RCPCIDDS.PBL
.         ------------
.         This include displays entries in Cashier ID Master file.
.
. MODS    08/03/94    Ian Rutt
.                     Renamed from RCPDSCID
.
.------------------------------------------------------------------------------
.         Display Category Heading, and then loop over entries
.------------------------------------------------------------------------------
.
RCPCIDDS  DISPLAY   *P1:4,*EF,*P30:4,*V2LON,*ULON," CASHIER ID MASTER "
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
.
          PACK      KEY6,SP6
          CALL      RDSCIDA1
RDSC1000  CALL      RDKCIDA1
          BRANCH    OVRCD,RDSC2000
.
          DISPLAY   *PCCOL:CVERT,*V2LON,RCPCASH,SP1,*HOFF,RCPSUR
.
          ADD       ONE,CVERT
          COMPARE   TWENTY4,CVERT
          GOTO      RDSC1000 IF LESS
.
          MOVE      FIVE,CVERT
          ADD       "40",CCOL
          COMPARE   "70",CCOL
          GOTO      RDSC1000 IF LESS
.
          BRANCH    CNEWDS,RDSC1500
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen : ":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSE,ANS
          GOTO      RDSC3000 IF EQUAL
.
          CMATCH    ANSF,ANS
          GOTO      RCPCIDDS IF EQUAL
.
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
          GOTO      RDSC1000
.
RDSC1500  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen : ":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSE,ANS
          GOTO      RDSC3000 IF EQUAL
.
          CMATCH    ANSF,ANS
          GOTO      RCPCIDDS IF EQUAL
.
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
          GOTO      RDSC1000
.
.         End of codes
.
RDSC2000  BRANCH    CNEWDS,RDSC3000
          KEYIN     *P1:24,*EL,"Tap ",*V2LON,"<ENTER>",*HOFF:
                    " to continue ... ",*EOFF,ANS;
.
RDSC3000  MOVE      ZERO,OVRCD
          RETURN
.
