.
.     PATDSCAT : Display Codes File Entries
.
.     This include displays entries in the codes file for a given
.      category.
.                   Input variable : CODE  contains category
.
.     Modifications: 14/10/89 Graeme Williams
.                    Added the parameter CNEWDS to determine if the include
.                    is to use the new standards for "?" routines
.                    02/12/89 Graeme Williams
.                    Changed the include to use the second index of the codes
.                    file to get the screen in description order.
.
PATCATDS  OPEN      PATCODE2,"patcodes"
          PACK      KEY5,SP10
          MOVE      SP10,ACODE
          CALL      RDCODE1
.          BRANCH    OVRCD OF EDLOP1
.
.         Display Category Heading, and then loop over entries
.
          DISPLAY   *P1:3,*EF,*P20:4,*V2LON,*ULON,"CATEGORY CODES"
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
.
.
DISPCAT0  CALL      RDSCODE1
DISPCAT1  CALL      RDKCODE2
          BRANCH    OVRCD OF EDLOP1
.
          MATCH     SP3,ACODE
          GOTO      DISPCAT2 IF EQUAL
.
          PACK      KEY5,ACODE,ANSZ,ANSZ,ANSZ,ANSZ,ANSZ 
          GOTO      DISPCAT0
.
DISPCAT2  DISPLAY   *PCCOL:CVERT,*V2LON,TCODE,*HOFF,SP2,TDESC;
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      DISPCAT1 IF LESS
.
          MOVE      FIVE,CVERT
          ADD       TWENTY6,CCOL
          COMPARE   "70",CCOL
          GOTO      DISPCAT3 IF NOT LESS
.
          PACK      KEY5,ACODE,ANSZ,ANSZ,ANSZ,ANSZ,ANSZ 
          GOTO      DISPCAT0
.
DISPCAT3  
.>>>>>>   TRAPCLR   F1
.
.         Check for new standards question, or old question
.
          BRANCH    CNEWDS,DSCAT200
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSE,ANS
          GOTO      EDLOP2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATCATDS IF EQUAL
.
          GOTO      DSCAT400
.
.         Use the new standards question
.
DSCAT200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSC,ANS
          GOTO      EDLOP2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATCATDS IF EQUAL
.
.         Clear the screen
.
DSCAT400  MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
.
.>>>>>>   TRAP      EDLOP0 IF F1
.
          PACK      KEY5,ACODE,ANSZ,ANSZ,ANSZ,ANSZ,ANSZ 
          GOTO      DISPCAT0
.
.       IF MISSING CATEGORY SHOW  CODE MISSING
.
.
EDLOP0    NORETURN
.>>>>>>   TRAPCLR   F1
.
.      End of codes
.
EDLOP1    BRANCH    CNEWDS,EDLOP2
.
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
.
EDLOP2    MOVE      ZERO,OVRCD
          CLOSE     PATCODE2
          RETURN
