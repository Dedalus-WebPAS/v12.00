.CALCLEAV.PBL
.
.******************************************************************************
.         CALCLEAV : Calculate minutes duration for leave
.         Requires : AADMNO
.         Return   : FORM6A   
.
.         Modifications :
.
.         V12.00.01 14/05/2025  Ebon Clements   TSK 0955096
.                   Alphanueric visit number changes
.         V11.01.01 01/06/2021 Jill Parkinson Task 0900650
.         Created include
.
.******************************************************************************
          DEFPROC   CALCLEAV
.
TOTLEVHR  FORM      8       * Total leave hours
TOTLEVMN  FORM      8       * Total leave minutes
SAVDATE   DIM       8
SAVTIME   DIM       8
TTRNDATE  DIM       8
TTRNTIME  DIM       8
.
          INC       PATTRNFD/INC
.
.---------------------------------------------------------------------
. CCODE00 - Recalculate hrs of ICU and CCU from admission date
.---------------------------------------------------------------------

CALCLEAV  MOVE      ZERO,FORM6A
          OPEN      PATTRAN2,"pattranf"
.
          UNPACK    SP70,SAVDATE,SAVTIME,TTRNDATE,TTRNTIME
          MOVE      ZERO,FORM6A
          PACK      KEY30,AADMNO,SP70
          CALL      RDSTRAN2
CALC1200  CALL      RDKTRAN2
          BRANCH    OVRCD,CALC4000
.
          MATCH     TADMN,AADMNO           * Admission No?
          GOTO      CALC4000 IF NOT EQUAL
.
          MATCH     ANSA,TMOVE              * Admission record ?
          GOTO      CALC1200 IF EQUAL
.
          CMATCH    ANSC,TMOVE              * Change record ?
          GOTO      CALC1200 IF EQUAL
.
          MATCH     ANSL,TMOVE              * Leave record ?
          GOTO      CALC2000 IF EQUAL
.
          MATCH     ANSR,TMOVE              * Return record ?
          GOTO      CALC3000 IF EQUAL
.
          MATCH     ANSD,TMOVE              * Discharge record ?
          GOTO      CALC3000 IF EQUAL
.
          GOTO      CALC3000
.
.         Leave record
.
CALC2000  MOVE      TDATE,SAVDATE        * Save the transfer date
          MOVE      TTIME,SAVTIME        * Save Transfer Time
          GOTO      CALC1200
.
.         Return/Discharge
.
CALC3000  MATCH     SP70,SAVDATE
          GOTO      CALC3200 IF EQUAL
.
          MOVE      TDATE,TTRNDATE
          MOVE      TTIME,TTRNTIME
          CALL      CDUR0000
.
CALC3200  MATCH     "R",TMOVE
          IF        @EQUAL
            MOVE      SP70,SAVDATE
            MOVE      SP70,SAVTIME
            GOTO      CALC1200
          ENDIF
.
          CMATCH    "D",TMOVE
          GOTO      CALC9000 IF EQUAL
.
.         Check the last record
.
CALC4000  MATCH     SP70,SAVDATE
          GOTO      CALC9000 IF EQUAL
.
          CALL      IBACLOCK
          PACK      TTRNDATE,CCC,CYY,CMM,CDD
          REP       " 0",TTRNDATE
          MOVE      CTIMEIS,TTRNTIME
          CALL      CDUR0000
.
CALC9000  MOVE      ZERO,FORM6
          MOVE      ZERO,F6
          MOVE      ZERO,F2
.
          CLOSE     PATTRAN2
.
CALC9999  GOTO      CALCL999
+
.******************************************************************************
.         Calculate the Dates and Times difference
.******************************************************************************
CDUR0000  MOVE      SAVDATE,FIRSTDAT            * Previous transfer date
          MOVE      TTRNDATE,LASTDATE           * New Transfer date
.
          UNPACK    SAVTIME,CHOUR,KEY1,CMIN
          PACK      FIRSTIME,CHOUR,CMIN         * Previous Transfer Time
.
          UNPACK    TTRNTIME,CHOUR,KEY1,CMIN
          PACK      LASTTIME,CHOUR,CMIN         * New Transfer Time
          CALL      TIMEDIFF                    * Calculate Time Diff in Mins
          ADD       CALCTIME,FORM6A             * Save the total time
.
          UNPACK    SP70,SAVDATE,SAVTIME,TTRNDATE,TTRNTIME
CDUR9999  RETURN
+
          INC       IBAOVRIO/INC
          INC       PATTRNIO/INC
.
CALCL999  ENDPROC

