.*****************************************************************************
.* Include Name  : PATDPATH.PBL                                              *
.*                                                                           *
.* Function      : To loop over the hospital file and get the DPATHS for     *
.*                 each hospital in the file.  It does this by executing     *
.*                 a TBL program (which extracts the DPATH to a temp file)   *
.*                 for the third RUN macro in the pathospf file (PTHORUNB).  *
.*                                                                           *
.* Also contains : OMDFN000 -  Open Multi-database file                      *
.*                                                                           *
.* Author        : Justin Coates      18/01/1994                             *
.*                                                                           *
.* Includes Reqd : PATDPTHV       variables for this include                 *
.*                                                                           *
.*                                                                           *
.* Returns       : HDPATH1 - 6         each hospitals DPATHS      (DIM120)   *
.*                 HDNAME1 - 6         each hospitals CODE        (DIM4)     *
.*                 HDDESC1 - 6         each hospitals DESCRIPTION (DIM30)    *
.*                                                                           *
.* Modifications :                                                           *
.*                 23/11/2017 Ania P         TSK 0261630                     *
.*                 Removed use of PORT                                       *
.*                 11/03/2016  Peter McMullen CAR 0815423                    *
.*                 Use mandatory env var QISNAME to detect Oracle            *
.*                 12/05/2005  Jill Habasque  CAR 52841                      *
.*                 Mods for oracle databases                                 *
.*                 11/05/1998  Steve Armstrong      STH                      *
.*                 Mods for 6 databases instead of 5                         *
.*                 18/05/1994     LOFTY & POOLSTER with help from PB         *
.*                 change the execute so it re-directs to temp file in here  *
.*****************************************************************************
PATDPATH  PACK      HDPATH1,SP30,SP30,SP30,SP30
          PACK      HDPATH2,SP30,SP30,SP30,SP30
          PACK      HDPATH3,SP30,SP30,SP30,SP30
          PACK      HDPATH4,SP30,SP30,SP30,SP30
          PACK      HDPATH5,SP30,SP30,SP30,SP30
          PACK      HDPATH6,SP30,SP30,SP30,SP30
          MOVE      SP30,HDDESC1
          MOVE      SP30,HDDESC2
          MOVE      SP30,HDDESC3
          MOVE      SP30,HDDESC4
          MOVE      SP30,HDDESC5
          MOVE      SP30,HDDESC6
          UNPACK    SP30,HDNAME1,HDNAME2,HDNAME3,HDNAME4,HDNAME5,HDNAME6
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,SIXTY;*129,MRCNNOHS
          COMPARE   ZERO,MRCNNOHS
          GOTO      PTDPTH99 IF EQUAL       * no hospitals set up

          OPEN      PATHOSP2,"pathospf"
          MOVE      SP2,KEY2
          CALL      RDSHOSP2
.
PTDPTH10  CALL      RDKHOSP2
          BRANCH    OVRCD,PTDPTH99
.
          PACK      PTHORUNB,PTHORUNB,SP30,SP30
          MATCH     SP30,PTHORUNB
          GOTO      PTDPTH10 IF EQUAL       * no macro
          MATCH     ANSI,PTHOACTV
          GOTO      PTDPTH10 IF EQUAL       * inactive hospital
.
. ------- run the program which returns the DPATH to a temp file -------
.
          STRIP     PTHORUNB
          CLEAR     CMDLINE
          APPEND    PTHORUNB,CMDLINE
          APPEND    " > ",CMDLINE
          PACK      KEY17,DPTMP,CFNAMEDP,DPTXT,SP30
          APPEND    KEY17,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          PACK      DIM120,SP30,SP30,SP30,SP30
          PACK      KEY17,DPTMP,CFNAMEDP,DPTXT,SP30
          OPEN      TEMPDP,KEY17
          READ      TEMPDP,SEQ;DIM120
          GOTO      PTDPTH10 IF OVER
          CLOSE     TEMPDP,DELETE
.
          PACK      DIM120,DIM120,SP30,SP30,SP30,SP30
          STORE     DIM120,HOSINDX,HDPATH1,HDPATH2,HDPATH3,HDPATH4,HDPATH5,HDPATH6
          STORE     HOSCODE,HOSINDX,HDNAME1,HDNAME2,HDNAME3,HDNAME4,HDNAME5,HDNAME6
          STORE     HOSDESC,HOSINDX,HDDESC1,HDDESC2,HDDESC3,HDDESC4,HDDESC5,HDDESC6
          GOTO      PTDPTH10
.
PTDPTH99  RETURN
+
.*********************************************************************
.         open the multi-database file 
.         Para's  : XXDPATH       full list of directories
.                   OPFILE        the file name to open
.                   FILENUM       the ifile identifier to open
.         Returns : EXIT = ONE    file not found
.*********************************************************************
OMDFN000  PACK      PATH,SP30,SP20          * clear open variable
          CLEAR     PATH
          PACK      XXDPATH,XXDPATH,SP30,SP30,SP30,SP30
          MOVE      XXDPATH,DIM120          * work variable
          MOVE      ZERO,FORM2              * clear FP position
.
          MATCH     SP30,DIM120
          GOTO      OMDFN500 IF EQUAL       * no directory
.
.         search for a colon
.
OMDFN100  MATCH     SP1,DIM120
          GOTO      OMDFN910 IF EQUAL       * no more items to check
          COMPARE   "50",FORM2
          GOTO      OMDFN910 IF NOT LESS    * finished the string
.
          SCAN      COLON,DIM120
          IF        @EQUAL
            MOVEFPTR  DIM120,FORM2
          ELSE 
            MOVE      "51",FORM2
          ENDIF
.
.         move appropriate length of string
.
          PACK      PATH,SP30,SP20          * clear open variable
          CLEAR     PATH
          RESET     DIM120
          MOVE      SP70,DIM19
          GETENV    "QISNAME",DIM19
          MATCH     SP70,DIM19
          IF        !@EQUAL
            SUB       ONE,FORM2               * oracle
            SETLPTR   DIM120,FORM2
            PACK      PATH,SLASH,DIM120
          ELSE
            SUB       ONE,FORM2               * get before the colon
            SETLPTR   DIM120,FORM2
            MOVE      DIM120,PATH
          ENDIF
          STRIP     PATH
          ENDSET    PATH
          APPEND    "/",PATH
.
.         now reset DIM120 to check for further details
.
          RESET     DIM120
          SETLPTR   DIM120,120
          ADD       TWO,FORM2
          RESET     DIM120,FORM2
          PACK      DIM120,DIM120,SP30,SP30,SP30,Sp30
.
OMDFN500  APPEND    OPFILE,PATH
          RESET     PATH
          STRIP     PATH
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      OTRF0000                * open the required file
          TRAPCLR   IO
          BRANCH    OVRCD,OMDFN100          * no file
.
          MOVE      ZERO,EXIT
          GOTO      OMDFN999
.
OMDFN910  MOVE      ONE,EXIT
.
OMDFN999  RETURN
+
