.------------------------------------------------------------
.  File          :  PATIK1PR.PBL
.
.  Function      :  Update Keyword Index File for MBS Item Code in newitemn 
.
.  Parameters    :  ITEMNO - Key to MBS Item Table
.
.  Modifications :
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Procedure to Update
.  a Key Word Index Table that can be used for searching 
.  Masterfiles. The procedure should be called from the 
.  Add, Update and Delete functions in the Masterfile 
.  Maintenance Function.
.  
.  A Key Word Index Table is Defined in the following Manner
.                              
.  ssffxxxx    DIM       xx    * Code Field Segment 1
.  ssffyyyy    DIM       xx    * Code Field Segment n
.  ssffKWD     DIM       15    * Key Word
.  ssffSPA     DIM       10    * Spare
.
.  Index 1   ssffxxxx, ssffyyyy, ssffKWD
.  Index 2   ssffKWD, ssffxxxx, ssffyyyy
.
.  To convert this Procedure the following should be performed
.    Global Change PAT to the System ID eg :%s/PAT/PAT/g
.    Global Change fff to the File   ID eg :%s/fff/DKI/g
.    Global Change ssff to the IO Call ID eg :%s/ssff/PTDK/g
.    Modify the KEYINDEX defintion to combined Code Field Lengths eg 6
.    Global Change KEYxx to the Key for the Key Word Table eg KEY21
.    Change xxxxxxx to the Code Fields eg DCODEhh
.    Change the GENR0000 routine to call the BWORD000 for each
.    string of key words to be indexed for the Coded Field
.    eg MOVE    DSNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    DGNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    TDESC,KEYWORDS
.       CALL    BWORD000
.------------------------------------------------------------
          DEFPROC   PATIK1UP
.         
          INC       PATIK1FD/INC
.
CODEA     INIT      "A "
CODEFI    INIT      "FI"
KEYINDEX  DIM       9         * Code Index in Table
KEYWORDS  DIM       60        * String Containing the Key Words to be Indexed
SIXTY     FORM      "60"
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
.------------------------------------------------------------
. Generate Word Index
.------------------------------------------------------------
GENR0000  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     PATIK1A1,"patik1af"
          TRAPCLR  IO
          BRANCH   OVRCD,GENR9999
.
          PACK     ITEMNO,ITEMNO,SP70
          MOVE     ITEMNO,KEYINDEX
          CALL     CLEAR000           * Remove Current Key Word Index
          BRANCH   EXIT,GENR9999
.
          MOVE     ITEMNO,KEY9      * Validate Record on File
          CALL     RDIT1M1 
          BRANCH   OVRCD,GENR1900     * If Not then it Must be a Delete/Clear
.
          MOVE     ITEMNO,KEYINDEX
          MOVE     ITEMNO,KEYWORDS
          CALL     BWORD000
.
          MOVE     IDESC,KEYWORDS
          CALL     BWORD000
.
          MATCH    SP70,ICLSS  
          GOTO     GENR1900 IF EQUAL
          PACK     KEY5,CODEA,ICLSS 
          CALL     RDCODE1
          IF       OVRCD=0
            MOVE     TDESC,KEYWORDS
            CALL     BWORD000
          ENDIF
.
          MATCH    SP70,IMISGRP
          GOTO     GENR1900 IF EQUAL
          PACK     KEY5,CODEFI,IMISGRP
          CALL     RDCODE1
          IF       OVRCD=0
            MOVE     TDESC,KEYWORDS
            CALL     BWORD000
          ENDIF
.
GENR1900  CLOSE    PATIK1A1
.
GENR9999  GOTO     PATIK1EP
.------------------------------------------------------------
. Clear Current Key Words
.------------------------------------------------------------
CLEAR000  PACK     KEY24,KEYINDEX,SP70
          CALL     RSPTIK11
          CALL     RKPTIK11
          BRANCH   OVRCD,CLEAR999
          MATCH    KEYINDEX,PTIKITM
          GOTO     CLEAR999 IF NOT EQUAL
.
          PACK     KEY24,PTIKITM,PTIKKWD,SP70
          CALL     DEPTIK11
          GOTO     CLEAR000
.
CLEAR999  RETURN
.-------------------------------------------------
. Determine Words to Index
.-------------------------------------------------
BWORD000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      BWORD000 IF NOT EOS
            GOTO      BWORD999               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWORDS,SP30      * reset form pointer
          MOVE      KEY60,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      BWORD100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Save this Word
.
BWORD100  MOVE      KEYWORDS,KEY15
          PACK      KEY15,KEY15,SP70
          REP       UPPLOW,KEY15             * Always Upper Case
          PACK      KEY24,KEYINDEX,KEY15
          UNPACK    KEY24,PTIKITM,PTIKKWD
          CALL      RDPTIK11
          IF        OVRCD=1
            CALL      WRPTIK11
          ENDIF
          GOTO      BWORD190
.
.         Check for any more words
.
BWORD190  SETLPTR   KEYWORDS,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY60,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY60,KEYWORDS
          GOTO      BWORD000
.
BWORD999  RETURN
.
          INC       PATIK1IO/INC
          INC       IBAOVRIO/INC
.
PATIK1EP  ENDPROC                          * End of Procedure
.
