KAPMAA00  MOVE      "Creditor",APMAKMOD     * set for creditor keyin
          CALL      KAPMAX00
          RETURN
.
KAPMAB00  MOVE      "Supplier",APMAKMOD     * set for supplier keyin
          CALL      KAPMAX00
          RETURN
.
.---------------------------------------------------------------------------
.   System    :   Accounts Payable System            
.   Include   :   APSMASKY
.   Routine   :   KAPMAX00 Keyin Creditor/Supplier Code       
.   Parameter :   CKEYTYP 0 = Keyin , 1 = Accept, 2 = Either
.   Return    :   EXIT = 1 null entry, 2 EXITCHAR entered
.-------------------------------------------------------------------------------
KAPMAX00  TOPIC     "APMACRD "
          MOVE      UNDLN70,APMACRD 
          KEYIN     *PCCOL:CVERT,*DV,APMACRD:                                   
                    *PCCOL:CVERT,*V2LON,*JR,*ZF,APMACRD:                        
                    *PCCOL:CVERT,*DV,APMACRD 
.
          ENDSET    APMACRD 
          APPEND    SP70,APMACRD 
          RESET     APMACRD 
.
          MATCH     SP70,APMACRD 
          GOTO      KAPMAX90 IF EQUAL
.
          ENDSET    APMACRD 
          CMATCH    EXITCHAR,APMACRD 
          GOTO      KAPMAX95 IF EQUAL
.
          CMATCH    QUEST,APMACRD 
          GOTO      KAPMAX10 IF NOT EQUAL
.
          MOVE      CCITEM,SCCITEM
          CALL      HAPMAX00
          MOVE      SCCITEM,CCITEM
          BRANCH    OVRCD,KAPMAX00
.
          ENDSET    APMACRD 
          APPEND    SP70,APMACRD 
          RESET     APMACRD 
.
          MATCH     SP70,APMACRD 
          GOTO      KAPMAX00 IF EQUAL
.
          DISPLAY   *PCCOL:CVERT,*V2LON,APMACRD                                 
KAPMAX10
          RESET     APMACRD 
          PACK      KEY5,APMACRD                                                
          CALL      RAAPMA1 
          BRANCH    OVRCD,KAPMAX80
          BRANCH    CKEYTYP,KAPMAX75
          CALL      RDAPMA1 
KAPMAX70
          MOVE      ZERO,EXIT
          GOTO      KAPMAX99
KAPMAX75
          CLEAR     DISPMSG
          APPEND    APMAKMOD,DISPMSG
          APPEND    " Code Already Exists - ",DISPMSG
          CALL      MESSAGE1
          GOTO      KAPMAX00
KAPMAX80
          MOVE      ZERO,EXIT
          BRANCH    CKEYTYP,KAPMAX99,KAPMAX99
.
          CLEAR     DISPMSG
          APPEND    APMAKMOD,DISPMSG
          APPEND    " Code Does Not Exist - ",DISPMSG
          CALL      MESSAGE1
          GOTO      KAPMAX00
KAPMAX90
          MOVE      ONE,EXIT
          GOTO      KAPMAX99
KAPMAX95
          MOVE      TWO,EXIT
KAPMAX99 TOPIC    PRGID
          RETURN
.
.---------------------------------------------------------------------------
.   System    :   Accounts Payable System            
.   Include   :   APSMXRKY
.   Routine   :   HAPMAX00 Creditor/Supplier Help       
.   Return    :           
.-------------------------------------------------------------------------------
HAPMAX00 TOPIC     "APMACRD "
.
         PROC      HAPCKI00
.
HAPMAX99  TOPIC    PRGID
          RETURN
.
