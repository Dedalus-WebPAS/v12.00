.------------------------------------------------------------
.  File       :  SINKCTPR
.
.  Function   :  Update Keyword Index File from Alias Details
.
.  Parameters : SIIACAT  -  Catalog to Update
.
.------------------------------------------------------------
          DEFPROC   WRKCT000
.
          INC       SINKCTFD/INC
          INC       SINKEXFD/INC
.
CATDES60  DIM       60
SAVKEY22  DIM       22
SIXTY     FORM      "60"
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
.
.------------------------------------------------------------
. Generate Catalog Word Index
.------------------------------------------------------------
GENR0000  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     SINKCTA2,"sinkctaf"
          TRAPCLR  IO
          BRANCH   OVRCD,GENR9999
.
          CALL     CLEAR000
          BRANCH   EXIT,GENR9999
.
          OPEN     SINKCTA1,"sinkctaf"
          OPEN     SINKEXA1,"sinkexaf"
.
          PACK     KEY67,SIIACAT,SP70
          CALL     RSSIIF2
GENR1000  CALL     RKSIIF2
          BRANCH   OVRCD,GENR1900
          MATCH    SIIFCAT,SIIACAT
          GOTO     GENR1900 IF NOT EQUAL
.
          MOVE     SIIFALI,CATDES60
          CALL     BWORD000
.
          GOTO     GENR1000
GENR1900
          CLOSE    SINKCTA2
          CLOSE    SINKCTA1
          CLOSE    SINKEXA1
.
GENR9999  GOTO     WRKCT999
.------------------------------------------------------------
. Recreate File
.------------------------------------------------------------
CLEAR000  PACK     KEY22,SIIACAT,SP70
          CALL     RSSIKC2
          CALL     RKSIKC2
          BRANCH   OVRCD,CLEAR999
          MATCH    SIIACAT,SIKCCAT
          GOTO     CLEAR999 IF NOT EQUAL
.
          PACK     KEY22,SIKCCAT,SIKCKWD,SP70
          CALL     DESIKC2
          GOTO     CLEAR000
.
CLEAR999  RETURN
.-------------------------------------------------
. Format Given Names
.-------------------------------------------------
BWORD000  CMATCH    SP1,CATDES60             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      CATDES60
            GOTO      BWORD000 IF NOT EOS
            GOTO      BWORD999               * entire name if blank
          ENDIF
          PACK      KEY60,CATDES60,SP30        * reset form pointer
          MOVE      KEY60,CATDES60
.
          SCAN      SP1,CATDES60               * Locate the first blank
          GOTO      BWORD100 IF NOT EQUAL
          BUMP      CATDES60,-1              * go back 1 to end of name
          MOVEFPTR  CATDES60,CCITEM          * another handly form field
          RESET     CATDES60                 * reset the form pointer
          SETLPTR   CATDES60,CCITEM          * set logical length to end of name
.
.         Save this Word
.
BWORD100  MOVE      CATDES60,KEY15
          PACK      KEY15,KEY15,SP70
          REP       UPPLOW,KEY15
          CALL      RDSIKE1                  * Check Exclusion
          COMPARE   ZERO,OVRCD
          GOTO      BWORD190 IF EQUAL
.
          PACK      KEY22,KEY15,SIIFCAT
          UNPACK    KEY22,SIKCKWD,SIKCCAT
          CALL      RDSIKC1
          IF        OVRCD=1
            CALL      WRSIKC1
          ENDIF
          GOTO      BWORD190
.
.         Check for any more words
.
BWORD190  SETLPTR   CATDES60,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     CATDES60,CCITEM        * reset to this point
          PACK      KEY60,CATDES60,SP30    * reset form pointer
          MOVE      KEY60,CATDES60
.
          GOTO      BWORD000
.
BWORD999  RETURN
.
          INC       SINKCTIO/INC
          INC       SINKEXIO/INC
          INC       IBAOVRIO/INC
.
WRKCT999  ENDPROC
.
