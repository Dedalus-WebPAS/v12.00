.
.       PATDSTBL : Display Health Fund File Entries
.
.       This include is used to display various options available, using
.       whatever file it needs, along with any special parameter supplied
.       by the calling program.
.
.     Modifications: 
.*        V5.08.01  06/06/2000  Jill Habasque                                 
.*                  Changed from key10 to key14 for the health fund file      
.                    28/11/89 Graeme Williams
.                    Added the parameter CNEWDS to determine if the include
.                    is to use the new standards for "?" routines
.                    17/03/99 J.Tan
.                    Check for active/inactive
.
.         Subroutine to display health fund tables. The health fund to
.         be used is in MHFUND. OVRCD is set if not a valid health fund
.
PATTBLDS  MOVE       FIVE,CVERT
          MOVE       ONE,CCOL
          DISPLAY    *P1:3,*EF,*P20:4,*V2LON,*ULON,HFNAME
          PACK       KEY14 WITH MHFUND,SP4,SP10
          CALL       RDSFUND1
          CALL       RDKFUND1
          BRANCH     OVRCD OF DSTBL999
          MATCH      MHFUND,HCODE
          GOTO       OVERCOND IF NOT EQUAL
.
.
....      TRAP       ENDFUND0 IF F1
.
.         Loop over file
.
HFTBLP1   CALL       RDKFUND1
          BRANCH     OVRCD OF ENDFUND1
.
          MATCH      MHFUND,HCODE
          GOTO       ENDFUND1 IF NOT EQUAL
.
          BRANCH     PHFTACT,HFTBLP1          * Inactive
          DISPLAY    *PCCOL:CVERT,*V2LON,HFTABL,SP4,*HOFF,HFNAME
          ADD        ONE,CVERT
          COMPARE    TWENTY4,CVERT
          GOTO       HFTBLP1 IF LESS
.
          MOVE       FIVE,CVERT
          ADD        "40",CCOL
          COMPARE    "70",CCOL
          GOTO       HFTBLP1 IF LESS
.
....      TRAPCLR    F1
.
.         Check for new standards question, or old question
.
          BRANCH    CNEWDS,DSTBL600
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, ":
                    "(",*V2LON,"F",*HOFF,")irst Screen, ":
                    "(",*V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSE,ANS
          GOTO      DSTBL999 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATTBLDS IF EQUAL
.
          GOTO      DSTBL800
.
.         Use the new standards question
.
DSTBL600  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, ":
                    "(",*V2LON,"F",*HOFF,")irst Screen, ":
                    "(",*V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSC,ANS
          GOTO      DSTBL999 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATTBLDS IF EQUAL
.
DSTBL800  MOVE       FIVE,CVERT
          MOVE       ONE,CCOL
          DISPLAY    *P1:5,*EF
.
....      TRAP       ENDFUND0 IF F1
          GOTO       HFTBLP1
.
DSTBL999  RETURN
