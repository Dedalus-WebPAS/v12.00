.******************************************************************************
.*  Include Name  : PATHGPKY.PBL                                              *
.*  Description   : Keyin of a Health Fund Group                              *
.*  Author        : Junior                                                    *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF6 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                                                                            *
.*  Returns       : PTHGCODE - Keyed in code                                  *
.*                  PTHGDESC - Description                                    *
.*                  EXIT = 0   Everything Ok                                  *
.*                       = 1   Spaces Input                                   *
.*                       = 2   Nothing Input                                  *
.*                       = 3   Exitchar input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Modifications :                                                           *
.*        V5.03.01  28/11/1995  Greg Horvat      SRF 612910                   *
.*                  Changed to allow spaces as a valid keyin                  *
.*                                                                            *
.******************************************************************************
PATHGPKY
KHFGP000  MATCH     SP6,CKYIDEF6            * Using a default ?
          GOTO      KHFGP050 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN6:     * No default used
                    *PECOL:EVERT,*V2LON,PTHGCODE:
                    *PECOL:EVERT,*DV,PTHGCODE
          GOTO      KHFGP100
.
KHFGP050  MOVE      CKYIDEF6,PTHGCODE
          KEYIN     *PECOL:EVERT,*DV,PTHGCODE:   * Default used
                    *PECOL:EVERT,*V2LON,*RV,PTHGCODE:
                    *PECOL:EVERT,*DV,PTHGCODE
.
. ----- Check What Was Entered -----
.
KHFGP100  CALL      HGPCK000
          BRANCH    EXIT,KHFGP450,KHFGP150,KHFGP550,KHFGP650,KHFGP750
.                        Valid    ?        Spaces   Nothing  Exitchar
          GOTO      KHFGP000
.
. ----- A "?" Was Input -----
.
KHFGP150  MOVE      "0",HLEF                * Save all of the screen
          CALL      GETSCR00                * Save screen
KHFGP200  MOVE      ONE,CNEWDS
          CALL      PATDSHGP                * Display all HFG
.
. ----- Now Ask For Code While Leaving Displayed Codes On Screen -----
.
KHFGP250  MATCH     SP6,CKYIDEF6            * Using a default
          GOTO      KHFGP300 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Code    : ":
                    *P11:24,*DV,UNDLN6:     * No default used
                    *P11:24,*V2LON,PTHGCODE:
                    *P11:24,*DV,PTHGCODE
          GOTO      KHFGP350
.
KHFGP300  MOVE      CKYIDEF6,PTHGCODE
          KEYIN     *P1:24,*EL,*DV,"Code    : ":
                    *P11:24,*DV,CKYIDEF6:   * Default used
                    *P11:24,*V2LON,*RV,PTHGCODE:
                    *P11:24,*DV,PTHGCODE
.
. ----- Repeat The Validation Of The Code Check What Was Entered -----
.
KHFGP350  CALL      HGPCK000
          BRANCH    EXIT,KHFGP400,KHFGP200,KHFGP500,KHFGP600,KHFGP700
.                        Valid    ?        Spaces   Nothing  Exitchar
          GOTO      KHFGP250
.
. ----- A Valid Code Was Entered -----
. 
KHFGP400  CALL      PUTSCR00                * Restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,PTHGCODE
KHFGP450  MOVE      ZERO,EXIT               * Valid input
          GOTO      KHFGP999 
.
. ----- Spaces Was Input -----
.
KHFGP500  CALL      PUTSCR00                * Restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,PTHGCODE
KHFGP550  PACK      PTHGDESC,SP30,SP20      * Blank out description
          MOVE      ONE,EXIT                * Nothing entered
          GOTO      KHFGP999 
.
. ----- Nothing Was Input -----
.
KHFGP600  CALL      PUTSCR00                * Restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,PTHGCODE
KHFGP650  PACK      PTHGDESC,SP30,SP20      * Blank out description
          MOVE      TWO,EXIT                * Nothing entered
          GOTO      KHFGP999 
.
. ----- Exitchar Input -----
.
KHFGP700  CALL      FRESCR00                * Free the screen saved
KHFGP750  MOVE      THREE,EXIT              * Exitchar
.
KHFGP999  RETURN
+
.***************************************************************************
.*                              HGPCK000             Called by: PATHGPKY   *
.*                        check what was entered                           *
.*          Returns:  EXIT  = 0   invalid                                  *
.*                          = 1   valid                                    *
.*                          = 2   "?"                                      *
.*                          = 3   spaces                                   *
.*                          = 4   nothing                                  *
.*                          = 5   exitchar                                 *
.***************************************************************************
HGPCK000  RESET     PTHGCODE
          GOTO      HGPCK400 IF EOS         * Return entered

          ENDSET    PTHGCODE
          APPEND    SP6,PTHGCODE
          RESET     PTHGCODE
.
          MATCH     EXITCHAR,PTHGCODE
          GOTO      HGPCK500 IF EQUAL       * Exitchar entered
.
          MATCH     QUEST,PTHGCODE
          GOTO      HGPCK200 IF EQUAL       * Question mark entered
.
          MATCH     SP6,PTHGCODE
          GOTO      HGPCK300 IF EQUAL       * Spaces entered
.
. ----- A Code Was Entered So Validate It -----
. 
          PACK      KEY6,PTHGCODE,SP6
          CALL      RDPAHGP1
          BRANCH    OVRCD,HGPCK800
.
. ----- Validate Code -----
.
HGPCK100  MOVE      ONE,EXIT
          GOTO      HGPCK999
.
. ----- "?" Entered -----
.
HGPCK200  MOVE      TWO,EXIT
          GOTO      HGPCK999
.
. ----- Spaces Entered -----
.
HGPCK300  MOVE      THREE,EXIT
          PACK      PTHGCODE,SP6            * Blank out code
          PACK      PTHGDESC,SP30,SP20      * Blank out description
          GOTO      HGPCK999
.
. ----- Nothing Entered -----
.
HGPCK400  BRANCH    CKYIMAND,HGPCK900       * Mandatory
.
          MOVE      FOUR,EXIT
          PACK      PTHGDESC,SP30,SP20      * Blank out description
          GOTO      HGPCK999
.
. ----- Exitchar Entered -----
.
HGPCK500  MOVE      FIVE,EXIT
          GOTO      HGPCK999
.
. ----- Invalid Code Entered -----
.
HGPCK800  DISPLAY   *P1:24,*EL,*B,"Invalid code.  ";
          CALL      HITENTER
          MOVE      ZERO,EXIT
          GOTO      HGPCK999
.
HGPCK900  BEEP
          MOVE      ZERO,EXIT
HGPCK999  RETURN
.
          INC       PATHGPDS
