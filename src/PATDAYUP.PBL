.*********************************************************************
.*                                                                   *
.*                              PATDAYUP                             *
.*                              --------                             *
.*                                                                   *
.*    Include to update the patdayaf file for a given date range     *
.*                                                                   *
.*    AUTHOR           : DIG                                         *
.*                                                                   *
.*    DATE             : 19/04/90                                    *
.*                                                                   *
.*    VARIABLES NEEDED : ADMISNO     FORM     8                      *
.*                       ENDDATE     DIM      8 (CCYYMMDD)           *
.*                       FORM4       FORM     4                      *
.*                       SMONDAY     DIM      4 (MMDD)               *
.*                       STRTDATE    DIM      8 (CCYYMMDD)           *
.*                       SYEAR       DIM      4 (CCYY)               *
.*                       TASKID      DIM      8                      *
.*                       THISYEAR    DIM      4 (CCYY)               *
.*                       TYEARR      DIM      4 (CCYY)               *
.*                       WORKDATE    DIM      8 (CCYYMMDD)           *
.*                                                                   *
.*    PARAMETERS       : First day for update in STRTDATE            *
.*                       Last  day for update in ENDDATE             *
.*                       Admission number for update in ADMISNO      *
.*                                                                   *
.*    FILES NEEDED     : PATDAYFD    PATDAYIO                        *
.*                                                                   *
.*    RETURNS          :                                             *
.*                                                                   *
.*    MODIFICATIONS    :                                             *
.*        V5.07.01  13/07/1999  Greg Horvat                          *
.*                  Replaced certain variables & with KEY variables  *
.*                       V5.01.01  19/12/90  DIG                     *
.*                                 Made sure the day file is opened  *
.*                                 if a new one has had to be        *
.*                                 created.                          *
.*                       V5.01.02  06/02/91  DIG                     *
.*                                 Fixed problem with infinite loop  *
.*                                 when the end month and day we are *
.*                                 updating the day file for is      *
.*                                 exactly equal to 31/12            *
.*                                                                   *
.*********************************************************************
.
.------ Add records to the patdayaf file ------
.
PATDAYAD  UNPACK    STRTDATE,SYEAR,SMONDAY
          MOVE      STRTDATE,WORKDATE
.
.------ close previous day file and set up the name of the next one ------
.
PATAD100  CLOSE     PATDAYA1
          PACK      CFNAME,FILDAYA1,SYEAR
          MOVE      SYEAR,THISYEAR
.
          TRAP      PATCR000 IF IO        * trap and create new file 
          OPEN      PATDAYA1,CFNAME         if it does not exist
          TRAPCLR   IO
.
.------ position on daily list file ------
.
PATAD200  PACK      KEY12,SMONDAY,ADMISNO
          CALL      RDPTDAY1
          COMPARE   ZERO,OVRCD
.
          GOTO      PATAD250 IF EQUAL
.
          MOVE      SMONDAY,PTDYDATE
          MOVE      ADMISNO,PTDYADMN
.
          CALL      WRPTDAY1               * write to the patdayaf file
.
.------ see if we have reached the end date to update ------
.
PATAD250  MATCH     ENDDATE,WORKDATE
          GOTO      PATAD260 IF LESS
.
          GOTO      PATAD999
.
PATAD260  CALL      PATIN000               * increment working date
.
          MATCH     THISYEAR,SYEAR         * if the year has changed then
          GOTO      PATAD100 IF NOT EQUAL    open new file
.
          GOTO      PATAD200
.
PATAD999  RETURN
+
.**********************************************************************
.*                         PATCR000                                   *
.*      create the new patdayaf file that is needed to write to       *
.**********************************************************************
PATCR000  CLEAR     KEY97
          APPEND    "isbuild ",KEY97
          APPEND    CFNAME,KEY97
          APPEND    " 13 U1-4,5-12 ",KEY97
          RESET     KEY97
          EXECUTE   KEY97,TASKID
          OPEN      PATDAYA1,CFNAME
.
PATCR999  RETURN
+
.**********************************************************************
.*                         PATIN000                                   *
.*              Increment the working date                            *
.**********************************************************************
PATIN000  UNPACK    SYEAR,CCENT,CYEAR
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          UNPACK    SMONDAY,CMON,CDAY
          MOVE      CMON,MM
          MOVE      CDAY,DD
.
          CALL      DMYCON                 * convert to julian date
.
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
.
          CALL      JULCON  
.
          PACK      SYEAR,CC,YY
          PACK      SMONDAY,MM,DD
          REP       " 0",SYEAR
          REP       " 0",SMONDAY
          PACK      WORKDATE,SYEAR,SMONDAY
.
PATIN999  RETURN
+
.**********************************************************************
.*                         PATDAYDL                                   *
.*              Delete records from the patdayaf file                 *
.**********************************************************************
PATDAYDL  UNPACK    STRTDATE,SYEAR,SMONDAY
          MOVE      STRTDATE,WORKDATE
          UNPACK    ENDDATE,CCENT,CYEAR,CMON,CDAY
          PACK      TYEARR,CCENT,CYEAR
.
.------ close previous day file and set up the name of the next one ------
.
PATDL100  CLOSE     PATDAYA1
          PACK      CFNAME,FILDAYA1,SYEAR
          MOVE      SYEAR,THISYEAR
.
          TRAP      PATDL490 IF IO         * trap if file does not exist
          OPEN      PATDAYA1,CFNAME
          TRAPCLR   IO
.
PATDL200  PACK      KEY12,SMONDAY,ADMISNO  * position on daily patient list file
          CALL      RDPTDAY1
          BRANCH    OVRCD,PATDL300
.
          CALL      DEPTDAY1               * delete from the patdayaf file
.
.------ see if we have reached the end date to update ------
.
PATDL300  MATCH     ENDDATE,WORKDATE
          GOTO      PATDL350 IF LESS
.
          GOTO      PATDL999
.
PATDL350  CALL      PATIN000               * increment working date
.
          MATCH     THISYEAR,SYEAR         * if the year has changed then
          GOTO      PATDL100 IF NOT EQUAL    open new file
.
          GOTO      PATDL200
.
.------ need to pop the return address off the stack if I/O error trapped -----
.
PATDL490  NORETURN
.
.------ either file does not exist or we have reached the end of the ------
.------ current file ------
.
PATDL500  MATCH     TYEARR,SYEAR            * test if we have reached the end
          GOTO      PATDL999 IF NOT LESS      year in the date range
.
          MOVE      SYEAR,FORM4             * if not, increment the start year
          ADD       ONE,FORM4                 to the next year and process
          MOVE      FORM4,SYEAR               the file for this year
          REP       " 0",SYEAR
          MOVE      "0101",SMONDAY
.
          GOTO      PATDL100
.
PATDL999  RETURN
+
.
