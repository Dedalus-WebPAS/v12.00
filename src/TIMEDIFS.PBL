.----------------------------------------------------------------------
. Modifications  : 
.----------------------------------------------------------------------
. V11.00.00  26/05/2020  Thanh T       TSK 0857573
.            Initial Version. Copied from V10.09
.----------------------------------------------------------------------
. System    :   All                                                       
. Include   :   TIMEDIFS.PBL                                              
. Desc      :   Time Differences Calculation                              
.---------------------------------------------------------------------
. Date      :   26/08/2019                                                 
. Author    :   Thanh T                                                   
. Function  :   This routine will calculate the differences in days,       
.               hours, minutes and seconds between two dates/times        
. Requires  :   DTFRSDTE   DIM 8  (first date CCYYMMDD format)          
.               DTFRSTIM   DIM 8  (first time HH:MM:SS format)          
.               DTLSTDTE   DIM 8  (last date CCYYMMDD format)           
.               DTLSTTIM   DIM 8  (last time HH:MM:SS format)           
.                                                                         
. Returns   :   DTOSIGN    FORM 1     * 1 - if last date/time <
.                                     *        first date/time
.                                     * 0 - otherwise
.               DTODAYS    FORM 6     * no. of days differences
.               DTOHOUR    FORM 2     * no. of hours differences
.               DTOMIN     FORM 2     * no. of minutes differences
.               DTOSEC     FORM 2     * no. of seconds differences
.----------------------------------------------------------------------
.
TIMEDIFS  MOVE      ZERO,DTOSIGN
          MOVE      ZERO,DTODAYS
          MOVE      ZERO,DTOHOUR
          MOVE      ZERO,DTOMIN
          MOVE      ZERO,DTOSEC
.
          UNPACK    DTFRSTIM,DTHH1,DIM1,DTMM1,DIM1,DTSS1
          PACK      DTSTAMP1,DTFRSDTE,DTHH1,DTMM1,DTSS1
          UNPACK    DTLSTTIM,DTHH2,DIM1,DTMM2,DIM1,DTSS2
          PACK      DTSTAMP2,DTLSTDTE,DTHH2,DTMM2,DTSS2
.
          MATCH     DTSTAMP1,DTSTAMP2
          GOTO      TMDIFS99 IF EQUAL
.
          IF        @LESS
            MOVE      DTSTAMP1,DTTMP14        * Last date/time  < first date/time
            MOVE      DTSTAMP2,DTSTAMP1       * swap them around
            MOVE      DTTMP14,DTSTAMP2
            MOVE      ONE,DTOSIGN             * set sign to 1 (negative)
          ENDIF
.                                
          UNPACK    DTSTAMP1,DTDATE1,DTHH1,DTMM1,DTSS1
          MOVE      DTHH1,DTHOUR1
          MOVE      DTMM1,DTMINUT1
          MOVE      DTSS1,DTSECON1   
          UNPACK    DTSTAMP2,DTDATE2,DTHH2,DTMM2,DTSS2 
          MOVE      DTHH2,DTHOUR2
          MOVE      DTMM2,DTMINUT2
          MOVE      DTSS2,DTSECON2            
.
. Calc no. of days differences 
.
          MOVE      DTDATE1,CDYSFDTE
          MOVE      DTDATE2,CDYSTDTE
          CALL      CALCDAYS                  * calculate days inclusive
          SUB       ONE,CDYSDAYS              * sub 1 day for difference
          MOVE      CDYSDAYS,DTODAYS 
.
. Calc seconds differences
.
          COMPARE   DTSECON1,DTSECON2         * last sec < first sec ?
          IF        @LESS
            ADD       SIXTY,DTSECON2          * yes - add 60 sec to last sec
            CALL      SUBMIN00                * sub 1 min from last min
          ENDIF
          ASSIGN    (DTSECON2 - DTSECON1),DTOSEC          
.
. Calc minutes differences
.
          COMPARE   DTMINUT1,DTMINUT2         * last min < first min ?
          IF        @LESS
            ADD       SIXTY,DTMINUT2          * yes - add 60 min to last min
            CALL      SUBHOR00                * sub 1 hour from last hour
          ENDIF
          ASSIGN    (DTMINUT2 - DTMINUT1),DTOMIN
.
.
. Calc hours differences
.
          COMPARE   DTHOUR1,DTHOUR2           * last hour < first hour ?
          IF        @LESS
            ADD       TWENTY4,DTHOUR2         * yes - add 24 min to last hour
            SUB       ONE,DTODAYS             * sub 1 day from calc days
          ENDIF
          ASSIGN    (DTHOUR2 - DTHOUR1),DTOHOUR          
.
TMDIFS99  RETURN
.
.****************************************************************************
.* Subtract 1 from last minute                                              *
.****************************************************************************
.
SUBMIN00  IF        DTMINUT2 = 0
            CALL      SUBHOR00 
            MOVE      FIFTY9,DTMINUT2   
          ELSE
            SUB       ONE,DTMINUT2
          ENDIF
.
SUBMIN99  RETURN
.
.****************************************************************************
.* Subtract 1 from last hour                                                *
.****************************************************************************
.
SUBHOR00  IF        DTHOUR2 = 0
            SUB       ONE,DTODAYS             * sub 1 day from calc days
            MOVE      TWENTY3,DTHOUR2   
          ELSE
            SUB       ONE,DTHOUR2
          ENDIF
.
SUBHOR99  RETURN
.
