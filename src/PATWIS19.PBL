.******************************************************************************
.* System         : Patient Management                                        *
.* Include        : PATWIS19.PBL                                              *
.* Description    : Calculate WIES19 value for an individual admission        *
.******************************************************************************
.* Date           : 07/08/2012                                                *
.* Author         : Davin Sloan                                               *
.*                                                                            *
.* Vars Required  : VARWIS19 - Variables required for WIES19                  *
.*                                                                            *
.* Files Required Open :                                                      *
.*        PATASFFD  PATASFA1 - Admission special funding arrangement file     *
.*        PATCODFD  PATCODE1 - Codes file                                     *
.*        PATECDFD  PATECDA2 - Episode disease coding file                    *
.*        PATECOFD  PATECOA2 - Episode operation coding file                  *
.*        PAT10OFD  PATI10O1 - ICD10 operations file                          *
.*        PATICUFD  PATICUA1 - ICU file                                       *
.*        PATTRNFD  PATTRAN2 - Transfer file                                  *
.*        PATW19FD  PATW19A1 - Victorian cost weights (for WIES19) file       *
.*                                                                            *
.* Parameters Req : Successful read on admission file                         *
.*                  Successful read on discharge file                         *
.*                  HIHLOS   - for Hopsipta in Home LOS (from RHIHDAYS)       *
.*                  NBRDAYS  - for LOS calculation (from NHOSPDAY)            *
.*                  PNSWRACE - User defined field for race                    *
.*                  WIS19DRG - DRG to calculate WIES19 for                    *
.*                                                                            *
.* Returns        : TOTWIS19 - Total WIES19 value                             *
.*                  WIS19DRG - DRG used to calculate WIES19                   *
.*                  WIS19LOS - LOS used to calculate WIES19                   *
.*                  WIS19WGT - Weight used to calculate WIES19                *
.*                  WIS19DSC - Description of DRG weight used                 *
.*                                                                            *
.* Modifications  :                                                           *
.*       V12.00.01 19/05/2025  J.Tan          TSK 0955096                     *
.*                  Added alpanumeric visit number generation                 *
.*       V10.03.03  04/10/2013  Peter Vela       CAR 284261                   *
.*                   Fixed positional read of patasfaf @ C19TR000             *
.*       V10.03.02  30/08/2013  Davin            CAR 290919                   *
.*                  Changed key14 to key16 for reads on patecdaf & patecoaf   *
.*       V10.03.01  20/09/2012  Davin            CAR 273279                   *
.*                  Care types U and K not eligible for WIES 19 funding       *
.*       V10.03.00  07/08/2012  Davin            CAR 270114                   *
.*                  Created for 2012/2013 WIES19 changes:                     *
.******************************************************************************
+
PATWIS19  CALL      IN19W000                * Initialise variables
          BRANCH    EXIT,WIS19950
.
          CALL      C19TY000                * Determine and Check Care Type
          BRANCH    EXIT,WIS19950
.
          CALL      C19TR000                * Check for Contracted Patient
          BRANCH    EXIT,WIS19950
.
          CALL      M19RG000                * Modify DRG if necessary
.
          CALL      C19RG000                * Check for problem DRG
          BRANCH    EXIT,WIS19950
.
          CALL      M19CH000                * Check for Mechanical Ventilation
.
          CALL      T19AL000                * Check for Thalessaemia
.
          CALL      A19AA000                * Check for AAA
.
          CALL      A19SD000                * Check for ASD
.
          CALL      B19SW000                * Calculate Base WIES19
.
          CALL      C19AB000                * Check for aboriginality
.
. ---- Calculate WIES19 value ----
.
          MOVE      TOTBWIES,TOTWIS19       * base WIES
          ADD       MVCOPAY,TOTWIS19        * mechv copayment
          ADD       THCOPAY,TOTWIS19        * thal  copayment
          ADD       AAACOPAY,TOTWIS19       * AAA   copayment
          ADD       ASDCOPAY,TOTWIS19       * ASD   copayment
          ADD       ATSIWIES,TOTWIS19       * ATSI  copayment
.
          MOVE      ZERO,EXIT
          GOTO      WIS19999
.
WIS19950  MOVE      ONE,EXIT
WIS19999  RETURN
+
.******************************************************************************
.*                                  IN19W000              Called by: PATWIS19 *
.*                         Initialise WIES19 variables                        *
.******************************************************************************
IN19W000  MOVE      ZERO,TOTWIS19
          MOVE      ZERO,WIS19LOS
          MOVE      ZERO,WIS19WGT
          MOVE      SP20,WIS19DSC
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATW19A1,"patw19af"
          TRAPCLR   IO
          BRANCH    OVRCD,IN19W950
.
IN19W900  MOVE      ZERO,EXIT
          GOTO      IN19W999
.
IN19W950  MOVE      ONE,EXIT
IN19W999  RETURN
+
.******************************************************************************
.*                                  C19TY000              Called by: PATWIS19 *
.*                 Check for valid Care Type & Incomplete Coding              *
.******************************************************************************
C19TY000  PACK     KEY5,ANSC,ANSC,ACARE     * use admission care type
          CALL     RDCODE1
          BRANCH   OVRCD,C19TY900
.
          MATCH    "6",TCINDC4              * Check Program Funding Source
          GOTO     C19TY900 IF NOT EQUAL
.
          MATCH    "0   ",THCSCOD           * check care types
          GOTO     C19TY900 IF EQUAL
          MATCH    "1   ",THCSCOD
          GOTO     C19TY900 IF EQUAL
          MATCH    "2   ",THCSCOD
          GOTO     C19TY900 IF EQUAL
          MATCH    "6   ",THCSCOD
          GOTO     C19TY900 IF EQUAL
          MATCH    "7   ",THCSCOD
          GOTO     C19TY900 IF EQUAL
          MATCH    "8   ",THCSCOD
          GOTO     C19TY900 IF EQUAL
          MATCH    "9   ",THCSCOD
          GOTO     C19TY900 IF EQUAL
          MATCH    "5   ",THCSCOD
          GOTO     C19TY900 IF EQUAL
          MATCH    "3   ",THCSCOD
          GOTO     C19TY900 IF EQUAL
          MATCH    "E   ",THCSCOD
          GOTO     C19TY900 IF EQUAL
          MATCH    "F   ",THCSCOD
          GOTO     C19TY900 IF EQUAL
          MATCH    "J   ",THCSCOD
          GOTO     C19TY900 IF EQUAL
          MATCH    "K   ",THCSCOD
          GOTO     C19TY900 IF EQUAL
          MATCH    "U   ",THCSCOD
          GOTO     C19TY900 IF EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     C19TY999
.
C19TY900  MOVE     ONE,EXIT                 * Not valid
C19TY999  RETURN
+
.******************************************************************************
.*                                  C19TR000              Called by: PATWIS19 *
.*                        Check for Contracted Patient                        *
.******************************************************************************
C19TR000  PACK     KEY25,AADMNO,SP70
          CALL     RSPTASF1
C19TR100  CALL     RKPTASF1                 * Read adm. special funding file
          BRANCH   OVRCD,C19TR800           * still process
.
          MATCH    AADMNO,PTAFADMN
          GOTO     C19TR800 IF NOT EQUAL    * still process
.
          MATCH    ANSA,PTAFTYPE            * special fund. arrang. type = adm
          GOTO     C19TR100 IF NOT EQUAL
.
          MATCH    ANSB,PTAFCROL            * Ignore if contract role = 'B'
          GOTO     C19TR900 IF EQUAL
.
C19TR800  MOVE     ZERO,EXIT                * valid
          GOTO     C19TR999
.
C19TR900  MOVE     ONE,EXIT                 * invalid
C19TR999  RETURN
+
.******************************************************************************
.*                                  M19RG000              Called by: PATWIS19 *
.*                      Modify DRG if necessary for WIES19                    *
.******************************************************************************
M19RG000  CALL      R19IO000           * Radiotherapy
M19RG999  RETURN
+
.******************************************************************************
.*                                  R19IO000              Called by: M19RG000 *
.*                         Modify DRG if Radiotherapy                         *
.******************************************************************************
.>>>>     Don't modify these DRGs:
.
R19IO000  MATCH     "B61A",WIS19DRG
          GOTO      R19IO999 IF EQUAL
.
          MATCH     "B61B",WIS19DRG
          GOTO      R19IO999 IF EQUAL
.
          MATCH     "A40Z",WIS19DRG
          GOTO      R19IO999 IF EQUAL
.
          MATCH     "W60Z",WIS19DRG
          GOTO      R19IO999 IF EQUAL
.
          MATCH     "W61A",WIS19DRG
          GOTO      R19IO999 IF EQUAL
.
          MATCH     "W61B",WIS19DRG
          GOTO      R19IO999 IF EQUAL
.
          MATCH     "S65A",WIS19DRG
          GOTO      R19IO999 IF EQUAL
.
          MATCH     "S65B",WIS19DRG
          GOTO      R19IO999 IF EQUAL
.
          MATCH     "S65C",WIS19DRG
          GOTO      R19IO999 IF EQUAL
.
          MATCH     "B60A",WIS19DRG
          GOTO      R19IO999 IF EQUAL
.
          MATCH     "B60B",WIS19DRG
          GOTO      R19IO999 IF EQUAL
.
          MATCH     "B82A",WIS19DRG
          GOTO      R19IO999 IF EQUAL
.
          MATCH     "B82B",WIS19DRG
          GOTO      R19IO999 IF EQUAL
.
          MATCH     "B82C",WIS19DRG
          GOTO      R19IO999 IF EQUAL
.
.>>>>     Check if operation code exists:
.
          PACK      ICDRDDTE,DDATE,SP10
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
R19IO050  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,R19IO999
.
          MATCH     AADMNO,PTEOADMN         * same admission?
          GOTO      R19IO999 IF NOT EQUAL
.
.>>>>     Check if block number is in range 1786-1792, 1794 or 1795:
.
          PACK      KEY9,PTEOCODE,SP9
          CALL      RDPTICO1
          BRANCH    OVRCD,R19IO050
.
          MATCH     "1786",PT0OBLKN         * block code >= 1786?
          GOTO      R19IO050 IF LESS
.
          MATCH     "1796",PT0OBLKN         * block code <=1795?
          GOTO      R19IO050 IF NOT LESS
.
          MATCH     "1793",PT0OBLKN         * block code = 1793?
          GOTO      R19IO050 IF EQUAL
.
.>>>>     Check if disease code exists in range C00-D48:
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
R19IO100  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,R19IO999
.
          MATCH     AADMNO,PTEDADMN         * same admission?
          GOTO      R19IO999 IF NOT EQUAL
.
          MATCH     "C00",PTEDCODE          * neoplasm code >= C00?
          GOTO      R19IO100 IF LESS
.
          MATCH     "D49",PTEDCODE          * neoplasm code <= D48.9?
          GOTO      R19IO100 IF NOT LESS
.
          MOVE      "R64Z",WIS19DRG         * modified for WIES19
R19IO999  RETURN
+
.******************************************************************************
.*                                  C19RG000              Called by: PATWIS19 *
.*                            Check for Problem DRG                           *
.******************************************************************************
C19RG000  PACK     KEY4,WIS19DRG
          CALL     RDPT191                  * check WIES19 weights file
          BRANCH   OVRCD,C19RG900
.
          MATCH    "960Z",PT19DRGC
          GOTO     C19RG900 IF EQUAL
.
          MATCH    "961Z",PT19DRGC
          GOTO     C19RG900 IF EQUAL
.
          MATCH    "962Z",PT19DRGC
          GOTO     C19RG900 IF EQUAL
.
          MATCH    "963Z",PT19DRGC
          GOTO     C19RG900 IF EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     C19RG999
.
C19RG900  MOVE     ONE,EXIT
C19RG999  RETURN
+
.******************************************************************************
.*                                  M19CH000              Called by: PATWIS19 *
.*                Calculate Mechanical Ventialtion Co-payment                 *
.******************************************************************************
M19CH000  MOVE      ZERO,ADJMVDAY
          MOVE      ZERO,MVCOPAY
.
          PACK      KEY8,AADMNO
          CALL      RDPTICU1                * get mech. vent. hours
          BRANCH    OVRCD,M19CH999
.
          MATCH     "D",PT19MVEL            * check mech. vent. eligibility
          IF        @EQUAL
            IF        PTICUMEC > 6
              ASSIGN    ((PTICUMEC+12)/24),FORM12P4    * days receiving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7659)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
            GOTO      M19CH999
          ENDIF
.
          MATCH     "4",PT19MVEL
          IF        @EQUAL
            IF        PTICUMEC > 96
              ASSIGN    (((PTICUMEC+12)/24)-4),FORM12P4   * days recving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7659)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
          ENDIF
.
M19CH999  RETURN
+
.******************************************************************************
.*                                  T19AL000              Called by: PATWIS19 *
.*                      Calculate Thalessaemia Co-payment                     *
.******************************************************************************
T19AL000  MOVE      ZERO,THCOPAY
          MATCH     "T",PT19CPAY            * check WIES19 weights file
          IF        @EQUAL
            PACK      KEY16,AADMNO,SP20
            CALL      RSPTECD2              * Position on & read a patient
T19AL100    CALL      RKPTECD2                episode disease file record
            BRANCH    OVRCD,T19AL999
.
            MATCH     AADMNO,PTEDADMN
            GOTO      T19AL999 IF NOT EQUAL
.
            MATCH     "D56",PTEDCODE        * check ICD-10 code
            IF        !@EQUAL
              MATCH     "D57.2",PTEDCODE    * check ICD-10 code
              GOTO      T19AL100 IF NOT EQUAL
            ENDIF
            MOVE      "0.2648",THCOPAY      * eligible for thal. copayment
          ENDIF
T19AL999  RETURN
+
.******************************************************************************
.*                                  A19AA000              Called by: PATWIS19 *
.*                          Calculate AAA Co-payment                          *
.******************************************************************************
A19AA000  MOVE      ZERO,AAACOPAY
          MATCH     "AAA",PT19CPAY          * check WIES19 weights file
          GOTO      A19AA999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
A19AA100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,A19AA999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      A19AA999 IF NOT EQUAL
.
          MATCH     "33116-00",PTEOCODE     * check ICD-10 code
          GOTO      A19AA100 IF NOT EQUAL
.
          MOVE      "3.1421",AAACOPAY       * eligible for AAA copayment
A19AA999  RETURN
+
.******************************************************************************
.*                                  A19SD000              Called by: PATWIS19 *
.*                          Calculate ASD Co-payment                          *
.******************************************************************************
A19SD000  MOVE      ZERO,ASDCOPAY
          MATCH     "ASD",PT19CPAY          * check WIES19 weights file
          GOTO      A19SD999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
A19SD100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,A19SD999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      A19SD999 IF NOT EQUAL
.
          MATCH     "38742-00",PTEOCODE     * check ICD-10 code
          GOTO      A19SD100 IF NOT EQUAL
.
          MOVE      "2.4713",ASDCOPAY       * eligible for ASD copayment
A19SD999  RETURN
+
.******************************************************************************
.*                                  B19SW000              Called by: PATWIS19 *
.*                            Calculate Base WIES19                           *
.******************************************************************************
B19SW000  CALL      D19SC000                * length of stay category
          CALL      C19ST000                * calculate inlier status
.
          MATCH     "L",W19NLIER            * low outlier
          CALL      BW19L000 IF EQUAL       * calculate base WIES19
.
          MATCH     "I",W19NLIER            * inlier
          CALL      BW19N000 IF EQUAL       * calculate base WIES19
.
          MATCH     "H",W19NLIER            * high outlier
          CALL      BW19H000 IF EQUAL       * calculate base WIES19
.
B19SW999  RETURN
+
.******************************************************************************
.*                                  D19SC000              Called by: B19SW000 *
.*                      Determine Length Of Stay Category                     *
.******************************************************************************
D19SC000  MOVE      NBRDAYS,WIS19LOS        * length of stay from NHOSPDAY
          MOVE      "M",LOSCAT              * set LOS category to multiday
          MATCH     ADATE,DDATE             * check for sameday
          IF        @EQUAL
            MOVE      ONE,SAMEDAY           * sameday
            MOVE      ONE,WIS19LOS          * LOS = 1
            MOVE      "S",LOSCAT            * set LOS category to sameday
            GOTO      D19SC999
          ELSE
            MOVE      ZERO,SAMEDAY          * not sameday
          ENDIF
.
          IF        SAMEDAY = 0 & WIS19LOS = 1
            MOVE      "O",LOSCAT            * set LOS category to oneday
            GOTO      D19SC999
          ENDIF
.
          IF        WIS19LOS > 1825
            MOVE      "1825",WIS19LOS       * maximum LOS is 5 years
          ENDIF
.
D19SC999  RETURN
+
.******************************************************************************
.*                                  C19ST000              Called by: B19SW000 *
.*                           Calculate Inlier Status                          *
.******************************************************************************
C19ST000  MOVE      "I",W19NLIER            * set status to inlier
          IF        WIS19LOS < PT19LIBP
            MOVE      "L",W19NLIER          * set status to low outlier
            GOTO      C19ST999
          ENDIF
.
          ASSIGN    (PT19HIBP+ADJMVDAY),FORM12P4   * high bound. + mech.v days
          IF        WIS19LOS > FORM12P4
            MOVE      "H",W19NLIER          * set status to high outlier
          ENDIF
C19ST999  RETURN
+
.******************************************************************************
.*                                  BW19L000              Called by: B19SW000 *
.*                     Calculate Base WIES19 for Low Outlier                  *
.******************************************************************************
BW19L000  MATCH     "S",LOSCAT            * sameday
          IF        @EQUAL
            MOVE      PT19TSDW,TOTBWIES             * base WIES19
            ASSIGN    (TOTBWIES/PT19TIMW),TOTIES19  * IES19
.
            MOVE      PT19TSDW,WIS19WGT             * for display
            MOVE      "Same Day Wgt ",WIS19DSC
            GOTO      BW19L999
          ENDIF
.
          MATCH     "O",LOSCAT            * oneday
          IF        @EQUAL
            MOVE      PT19TODW,TOTBWIES             * base WIES19
            ASSIGN    (TOTBWIES/PT19TIMW),TOTIES19  * IES19
.
            MOVE      PT19TODW,WIS19WGT             * for display
            MOVE      "One Day Wgt  ",WIS19DSC
            GOTO      BW19L999
          ENDIF
.
          MATCH     "M",LOSCAT            * multiday
          IF        @EQUAL
            ASSIGN    (PT19TODW+((WIS19LOS-1)*PT19TLOM)),TOTBWIES
.                                                    * base WIES19
            ASSIGN    (TOTBWIES/PT19TIMW),TOTIES19   * IES19
.
            MOVE      PT19TLOM,WIS19WGT             * for display
            MOVE      "Low Outlr Wgt",WIS19DSC
            GOTO      BW19L999
          ENDIF
BW19L999  RETURN
+
.******************************************************************************
.*                                  BW19N000              Called by: B19SW000 *
.*                      Calculate Base WIES19 for Inlier                      *
.******************************************************************************
BW19N000  MOVE      ONE,TOTIES19          * IES19 = 1
          MATCH     "S",LOSCAT            * sameday
          IF        @EQUAL
            MOVE      PT19TSDW,TOTBWIES   * base WIES19
.
            MOVE      PT19TSDW,WIS19WGT             * for display
            MOVE      "Same Day Wgt ",WIS19DSC
            GOTO      BW19N999
          ENDIF
.
          MATCH     "O",LOSCAT            * oneday
          IF        @EQUAL
            MOVE      PT19TODW,TOTBWIES   * base WIES19
.
            MOVE      PT19TODW,WIS19WGT             * for display
            MOVE      "One Day Wgt  ",WIS19DSC
            GOTO      BW19N999
          ENDIF
.
          MATCH     "M",LOSCAT            * multiday
          IF        @EQUAL
            MOVE      PT19TIMW,TOTBWIES   * base WIES19
.
            MOVE      PT19TIMW,WIS19WGT             * for display
            MOVE      "Inlier Wgt   ",WIS19DSC
            GOTO      BW19N999
          ENDIF
BW19N999  RETURN
+
.******************************************************************************
.*                                  BW19H000              Called by: B19SW000 *
.*                    Calculate Base WIES19 for High Outlier                  *
.******************************************************************************
BW19H000  ASSIGN    ((WIS19LOS-PT19HIBP)-ADJMVDAY),HIGHDAYS  * high outlr days
          COMPARE   ZERO,HIGHDAYS
          IF        @LESS
            MOVE      ZERO,HIGHDAYS         * highdays must be >= zero
          ENDIF
.
          COMPARE   HIGHDAYS,HIHLOS
          IF        @LESS
            MOVE      HIHLOS,HITHDAYS       * get lesser of highdays and
          ELSE                              *  HIHLOS
            MOVE      HIGHDAYS,HITHDAYS
          ENDIF
.
          ASSIGN    (HIGHDAYS-HITHDAYS)*PT19THOM,TOTBWIES    *high WIES
          ASSIGN    (HITHDAYS*PT19THIH)+TOTBWIES,TOTBWIES    *HITH WIES
          ASSIGN    (PT19TIMW+TOTBWIES),TOTBWIES             *base WIES19
          ASSIGN    (TOTBWIES/PT19TIMW),TOTIES19             * IES19
.
          MOVE      PT19THOM,WIS19WGT                        * for display
          MOVE      "High Outl Wgt",WIS19DSC
BW19H999  RETURN
+
.******************************************************************************
.*                                  C19AB000              Called by: PATWIS19 *
.*                          Check for Aboriginality                           *
.******************************************************************************
C19AB000  MOVE      ZERO,ATSIWIES                * set ATSI WIES to zero
          IF        PNSWRACE<1 | PNSWRACE>14
            GOTO      C19AB999              * Invalid parameter
          ENDIF
.
          LOAD      TCODE,PNSWRACE,CATP1,CATP2,CATP3,CATP4,CATP5:
                                   CATP6,CATA1,CATA2,CATA3,CATA4:
                                   CATA5,CATA6,CATA7,CATVA
          LOAD      ACODE,PNSWRACE,PUSR1,PUSR2,PUSR3,PUSR4,PUSR5:
                                   PUSR6,AUSR1,AUSR2,AUSR3,AUSR4:
                                   AUSR5,AUSR6,AUSR7,PMPXABRG
.
          MATCH     ACODE,SP3
          GOTO      C19AB999 IF EQUAL
.
          PACK      KEY5,TCODE,ACODE
          CALL      RDCODE1                 * Read a code file record
          BRANCH    OVRCD,C19AB999
.
          UNPACK    THCSCOD,DIM1,KEY1       * use 2nd char of HDP Equiv
.>>>>     MOVE      THCSCOD,KEY1
          TYPE      KEY1
          GOTO      C19AB999 IF NOT EQUAL   * Non numeric, error
.
          MOVE      KEY1,FORM1
.
. ---- Calculate ATSI Loading if aboriginality = 1, 2, or 3 ----
.
          IF        FORM1=1 | FORM1=2 | FORM1=3
            MOVE      TOTBWIES,FORM12P4       * base WIES
            ADD       MVCOPAY,FORM12P4        * mechv copayment
            ADD       THCOPAY,FORM12P4        * thal  copayment
            ADD       AAACOPAY,FORM12P4       * AAA   copayment
            ADD       ASDCOPAY,FORM12P4       * ASD   copayment
            ASSIGN    (0.3*FORM12P4),ATSIWIES
          ENDIF
.
C19AB999  RETURN
+
