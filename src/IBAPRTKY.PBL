.******************************************************************************
.*  Include Name  : IBAPRTKY.PBL                                              *
.*  Description   : Keyin of Printer Codes                                    *
.*  Author        : Sandra Barcham                                            *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF3 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMAND - keyin insert mode  (0=No  1=Yes)               *
.*                                                                            *
.*  Returns       : KEY3       - Keyed in code                                *
.*                  PRTDESC    - Description                                  *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Mods          :                                                           *
.*                  25/10/2006  Steve Armstrong  CAR 107236                   *
.*                              Added validation for numeric only printer     *
.*                              code.                                         *
.******************************************************************************
IBAPRTKY
IBPRKY00  MATCH     SP3,CKYIDEF3                  * using a default ?
          GOTO      IBPRKY10 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN3:      * no default used
                    *PECOL:EVERT,*V2LON,*JR,KEY3:
                    *PECOL:EVERT,*DV,KEY3
          GOTO      IBPRKY20
.
IBPRKY10  MOVE      CKYIDEF3,KEY3                 * default
          KEYIN     *PECOL:EVERT,*DV,KEY3:        * default used
                    *PECOL:EVERT,*V2LON,*RV,*JR,KEY3:
                    *PECOL:EVERT,*DV,KEY3
.
.         check what was entered
.
IBPRKY20  CALL      PRTCK000
          BRANCH    EXIT,IBPRKY25,IBPRKY31,IBPRKY41,IBPRKY95
.                        "?"      valid    nothing  exitchar
          GOTO      IBPRKY00
.
.         a "?" was input
.
IBPRKY25  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
IBPRKY26  CALL      IBAPRTDS
.
.         now ask for code while leaving displayed codes on screen
.
IBPRKY27  MATCH     SP3,CKYIDEF3                  * using a default
          GOTO      IBPRKY28 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Printer Code : ":
                    *P16:24,*DV,UNDLN3:      * no default used
                    *P16:24,*V2LON,*JR,KEY3:
                    *P16:24,*DV,KEY3
          GOTO      IBPRKY29
.
IBPRKY28  MOVE      CKYIDEF3,KEY3
          KEYIN     *P1:24,*EL,*DV,"Printer Code : ":
                    *P16:24,*DV,KEY3:     * default used
                    *P16:24,*V2LON,*RV,*JR,KEY3:
                    *P16:24,*DV,KEY3
.
.         repeat the validation of the code
.         check what was entered
.
IBPRKY29  CALL      PRTCK000
          BRANCH    EXIT,IBPRKY26,IBPRKY30,IBPRKY40,IBPRKY90
.                        "?"      valid    nothing  exitchar
.
          GOTO      IBPRKY27
.
.         a valid code was entered 
. 
IBPRKY30  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,KEY3
.
IBPRKY31  MOVE      FALSE,EXIT                    * valid input
          GOTO      IBPRKY99 
.
.         nothing was input
.
IBPRKY40  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,KEY3
.
IBPRKY41  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      IBPRKY99 
.
.         EXITCHAR input
.
IBPRKY90  CALL      FRESCR00                * free the screen saved
.
IBPRKY95  MOVE      TWO,EXIT                * EXITCHAR
.
IBPRKY99  RETURN
+
.*********************************************************************
.*                  PRTCK000                    Called by : IBAPRTKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
PRTCK000  PACK      KEY3,KEY3,SP3
.
          PACK      DIM3,SP2,EXITCHAR
          MATCH     DIM3,KEY3
          GOTO      PRTCK600 IF EQUAL       * exit char
.
          MATCH     SP3,KEY3
          GOTO      PRTCK700 IF EQUAL       * nothing
.
          MATCH     "  ?",KEY3
          GOTO      PRTCK800 IF EQUAL       * ? mark
.
          TYPE      KEY3
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,"Non-numeric Printer Code. ";
            GOTO      PRTCK990
          ENDIF
.
.         a code was entered so validate
. 
PRTCK500  PACK      KEY3,KEY3,SP20
          CALL      RDPRTA1
          IF        CKYIMODE=ZERO
            BRANCH    OVRCD,PRTCK900          * invalid code
          ELSE
            COMPARE   ZERO,OVRCD
            GOTO      PRTCK900 IF EQUAL       * code exists
          ENDIF 
.
          MOVE      TWO,EXIT                * valid code
          GOTO      PRTCK999
.
.         exitchar entered
.
PRTCK600  MOVE      FOUR,EXIT
          MOVE      ZERO,PRTCODE
          MOVE      SP20,PRTDESC              * clear desc field
          GOTO      PRTCK999
.
.         nothing entered
.
PRTCK700  BRANCH    CKYIMAND,PRTCK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      ZERO,PRTCODE
          MOVE      SP20,PRTDESC              * clear desc field
          GOTO      PRTCK999
.
.         "?" entered
.
PRTCK800  MOVE      ONE,EXIT
          GOTO      PRTCK999
.
.         invalid code entered
.
PRTCK900  IF        CKYIMODE=ZERO
            DISPLAY   *P1:24,*EL,*B,"Invalid Printer Code. ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF 
          MOVE      ZERO,EXIT
          GOTO      PRTCK999
.
.         field is mandatory
.
PRTCK950  DISPLAY   *P1:24,*B,*EL,"Field is mandatory. ";
PRTCK990  CALL      HITENTER 
          MOVE      ZERO,EXIT
.
PRTCK999  RETURN
+
          INC       IBAPRTDS
.
