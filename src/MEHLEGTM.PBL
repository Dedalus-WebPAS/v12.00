.----------------------------------------------------------------------
. Program       : MEHLEGTM
.
. Function      : Output Mental Health Legal Status History Variables           
.
. Modifications :
.                 V9.02.00  J.TAN   Created
.----------------------------------------------------------------------
.
.------------------------------------------------------------
. Visit Details 
.------------------------------------------------------------
MHLG0000  BRANCH    FLDITMNO,MHLG0100,MHLG0200,MHLG0300,MHLG0400,MHLG0500:
                             MHLG0600,MHLG0700,MHLG0800
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      MHLG9999
.
MHLG0100  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
          WRITE     HTMLFILE;LINKPRG,".pbl?":
                             "&reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&urnumber=",URNUMBER:
                             "&admissno=",ADMISSNO;
          GOTO      MHLG9999
.
MHLG0200  WRITE     HTMLFILE;MHLEADMN;
          GOTO      MHLG9999
.
MHLG0300  MATCH     MHLEDATE,SP70
          GOTO      MHLG9999 IF EQUAL
          UNPACK    MHLEDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      MHLG9999
.
MHLG0400  WRITE     HTMLFILE;MHLETIME;
          GOTO      MHLG9999
.
MHLG0500  MOVE      "LS",CATEGORY
          PACK      CODE,MHLESTAT,SP70
          CALL      CODITM00
          GOTO      MHLG9999
.
MHLG0600  MOVE      "LR",CATEGORY
          PACK      CODE,MHLEREVW,SP70
          CALL      CODITM00
          GOTO      MHLG9999
.
.         Employer  organisation code
.
MHLG0700  UNPACK    DIM127,DIM1,KEY1,DIM127
          TYPE      KEY1
          GOTO      MHLG0720 IF NOT EQUAL
.
          MOVE      KEY1,F1
          BRANCH    F1,MHLG0710
          GOTO      MHLG9999
.
MHLG0710  WRITE     HTMLFILE;MHLEEMPC;
          GOTO      MHLG9999
.
MHLG0720  PACK      KEY7,KEY1,SP70
          CALL      RSPTEOR1
MHLG0730  CALL      RKPTEOR1
          BRANCH    OVRCD,MHLG9999
          MATCH     KEY1,PTERTYPE
          GOTO      MHLG9999 IF NOT EQUAL
          MATCH     "1",PTERSTAT
          GOTO      MHLG0730 IF EQUAL      * not active
.
          MOVE      PTERNAME,KEY20
          PACK      KEY8,QUOTE,PTERCODE,QUOTE
          MATCH     PTERCODE,MHLEEMPC
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY8," selected>",KEY20:
                               "</option>";
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY8,">",KEY20:
                               "</option>";
          ENDIF
          GOTO      MHLG0730
.
.         Employer Organisation type
.
MHLG0800  UNPACK    DIM127,DIM1,KEY1,DIM127
          MOVE      KEY1,F1
          BRANCH    F1,MHLG0810,MHLG0820
          GOTO      MHLG9999
.
MHLG0810  WRITE     HTMLFILE;MHLEEMPT;
          GOTO      MHLG9999
.
MHLG0820  MOVE      ZERO,F1
MHLG0830  ADD       ONE,F1
          COMPARE   NINE,F1
          GOTO      MHLG9999 IF NOT LESS
          MOVE      SP70,KEY1
          LOAD      KEY1,F1,ANSA,ANSL,ANSC,ANSP,ANSI,ANSB,ANSD,ANSJ
          BRANCH    F1,MHLG0831,MHLG0832,MHLG0833,MHLG0834,MHLG0835:
                       MHLG0836,MHLG0837,MHLG0838
.
MHLG0831  MOVE      "External Agencies        ",KEY25
          GOTO      MHLG0839
MHLG0832  MOVE      "Lawyers                  ",KEY25
          GOTO      MHLG0839
MHLG0833  MOVE      "Schools                  ",KEY25
          GOTO      MHLG0839
MHLG0834  MOVE      "Private Hospitals        ",KEY25
          GOTO      MHLG0839
MHLG0835  MOVE      "Indigenous Organisation  ",KEY25
          GOTO      MHLG0839
MHLG0836  MOVE      "Public Hospitals         ",KEY25
          GOTO      MHLG0839
MHLG0837  MOVE      "Residental               ",KEY25
          GOTO      MHLG0839
MHLG0838  MOVE      "Justice Department       ",KEY25
.
MHLG0839  PACK      KEY3,QUOTE,KEY1,QUOTE
          MATCH     KEY1,MHLEEMPT
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY3," selected>",KEY25:
                               "</option>";
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY3,">",KEY25:
                               "</option>";
          ENDIF
          GOTO      MHLG0830
.
MHLG9999  RETURN
+
.------------------------------------------------------------
. Clear cgi variables
.------------------------------------------------------------
CLRLEG00  MOVE      Z70,MEHLG001
          MOVE      Z70,MEHLG002
          MOVE      Z70,MEHLG003
          MOVE      Z70,MEHLG004
          MOVE      Z70,MEHLG005
          MOVE      Z70,MEHLG006
          MOVE      Z70,MEHLG007
CLRLEG99  RETURN
+
.------------------------------------------------------------
.   Set Parameters for mental health  Legal status table
.------------------------------------------------------------
SETMHL00  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
.
          BRANCH    F3,SETMHL01,SETMHL02,SETMHL03,SETMHL04,SETMHL05:
                       SETMHL06,SETMHL07
.
          MOVE      ONE,EXIT
          GOTO      SETMHL99
.
SETMHL01  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      MEHLG001,CCENT,CYEAR,CMON,CDAY
          GOTO      SETMHL99
.
SETMHL02  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      MEHLG002,CCENT,CYEAR,CMON,CDAY
          GOTO      SETMHF99
.
SETMHL03  MOVE      QRYDATA,MEHLG003
          GOTO      SETMHF99
.
SETMHL04  MOVE      QRYDATA,MEHLG004
          GOTO      SETMHF99
.
SETMHL05  MOVE      QRYDATA,MEHLG005
          GOTO      SETMHF99
.
SETMHL06  MOVE      QRYDATA,MEHLG006
          GOTO      SETMHF99
.
SETMHL07  MOVE      QRYDATA,MEHLG007
          GOTO      SETMHF99
.
SETMHL99  RETURN
+
.------------------------------------------------------------
.   Move cgi's into file variables               
.------------------------------------------------------------
.
MVMHL000  MATCH     Z70,MEHLG001
          IF        !@EQUAL
            MOVE      MEHLG001,MHLEADMN
          ENDIF
.
          MATCH     Z70,MEHLG002
          IF        !@EQUAL
            MOVE      MEHLG002,MHLEDATE
          ENDIF
.
          MATCH     Z70,MEHLG003
          IF        !@EQUAL
            MOVE      MEHLG003,MHLETIME
          ENDIF
.
          MATCH     Z70,MEHLG004
          IF        !@EQUAL
            MOVE      MEHLG004,MHLESTAT
            MATCH     MEHLG004,PTMXLS         * legal status still the same ?
            IF        !@EQUAL
              MOVE      ONE,CHNGLEGL          * Change legal status
            ENDIF
          ENDIF
.
          MATCH     Z70,MEHLG005
          IF        !@EQUAL
            MOVE      MEHLG005,MHLEREVW
            MATCH     MEHLG005,SAVEREVW       * review status still the same ?
            IF        !@EQUAL
              MOVE      ONE,CHNGREVW          * Change review status
            ENDIF
          ENDIF
.
          MATCH     Z70,MEHLG006
          IF        !@EQUAL
            MOVE      MEHLG006,MHLEEMPC       * Employer organisation code
          ENDIF
.
          MATCH     Z70,MEHLG007
          IF        !@EQUAL
            MOVE      MEHLG007,MHLEEMPT       * Employer organisation type
          ENDIF
.
          RETURN
.
