.-----------------------------------------------------------------------------
. Get concession card details
. Input:
. 1. PURNO = U/R Number 
. 2. DIM1  = 1 - Concession Card
.            2 - Safety Net Card
.            3 - DVA Card
.            4 - Pension Card
.-----------------------------------------------------------------------------
. Modifications  :
.-----------------------------------------------------------------------------
. V10.10.01 20/03/2017  Thanh T.          TSK 0832066
.           Changed DVA PREPAT/PMPXDVAC fields to use the new
.           concession card PMCDCNUM/PMCDDVAC fields
.-----------------------------------------------------------------------------
GCCARD00  MOVE      "ct",DIM2
          PACK      KEY19,PURNO,SP70
          CALL      RSPMCCD1
GCCARD20  CALL      RKPMCCD1
          BRANCH    OVRCD,GCCARD80
.
          MATCH     PURNO,PMCDURNO                 * same U/R still ?
          GOTO      GCCARD80 IF NOT EQUAL          * no - nothing found
.
.         Check if the card type is DVA or Pension (Cat ct, Indicator 1)
.
          PACK      KEY5,DIM2,PMCDCTYP            
          CALL      RDCODE1                        * code on file ?
          BRANCH    OVRCD,GCCARD20                 * no - ignore record
.
          MATCH     DIM1,TCINDC1                   * same concession card type?
          GOTO      GCCARD20 IF NOT EQUAL          * no - ignore record
.
.         A concession card of the same type has been found, so check
.         if there is an expiry date
.
          MATCH     SP8,PMCDEXDT                 * expiry date blank ?
          GOTO      GCCARD99 IF EQUAL            * yes - assume valid
.
          CALL      IBACLOCK
          PACK      DIM8,CCC,CYY,CMM,CDD
          REP       " 0",DIM8
.
.         There is an expiry date for the card, so check that the
.         card is current
.
          MATCH     PMCDEXDT,DIM8                * card valid for date ?
          GOTO      GCCARD99 IF LESS             * no - nothing found
.
          GOTO      GCCARD20
.
GCCARD80  MOVE      SP70,PMCDCNUM
          MOVE      SP70,PMCDCTYP
          MOVE      SP70,PMCDEXDT
          MOVE      SP70,PMCDDVAC
          GOTO      GCCARD99
.
GCCARD99  RETURN
