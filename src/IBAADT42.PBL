******************************************************************************
.* System         :  Patient Billing                                          *
.* Program        :  IBAADT42                                                 *
.* Description    :  Upload IFC Other/Consumables/Prosthetic file             *
.******************************************************************************
.* Date           :  12/08/2014                                               *
.* Author         :  J.Tan                                                    *
.* Function       :  Upload IFC Other/Consumables/Prosthetic from ASCII file  *
.* Modifications  :                                                           *
.*       V11.00.01   04/03/2020  J.Tan          TSK 0838262                   *
.*                   Added Effective from and to date to MBS Item file        *
.*       V10.04.00   19/05/2014 J.Tan   CAR 268740                            *
.*                   Created                                                  *
.******************************************************************************
.
          INC       STD001FD
.
          INC       IBACONFD/INC
          INC       PATCONFD/INC
          INC       PATCODFD/INC            * Codes file
          INC       PATFN1FD/INC            * Health fund file
          INC       PATFX1FD/INC            * Health fund file
          INC       PATHGPFD/INC            * Health fund group
          INC       PATIFCFD/INC            * IFC charge file
          INC       PATITMFD/INC            * MBS Item file
          INC       PATCMCFD/INC            * Casemix code file
.
.         Temporary file for IFC Charge records which
.         are loaded from the upload file provided by the customer
.
.         IFC Charge Upload file
.
UMPROTXT  FILE       HL7, FIXED=1000        * Upload file for IFC charge file
.
.Name     Type     Size     Start
.----     ----     ----     -----
XCLMCD    DIM         3     Claim Code
XHFCOD    DIM         6     Health Fund code
XHFTYP    DIM         3     Health Fund table type
XCCMIX    DIM         9     Casemix code
XCPMBS    DIM         9     Provisional MBS Code
XCATYP    DIM         3     Admission Type
XCCTYP    DIM         3     Charge Type
XCRAMT    FORM        12.2  Rebate amount
XCPAMT    FORM        12.2  Patient amount
.
.End of Record
.
. ----- Program Variables -----
.
ADDCOUNT  FORM      8
CMDLINE   DIM       127
DIM3      DIM       3
DIM9      DIM       9
DIM15     DIM       15
ERRCOUNT  FORM      8
ERRORMSG  DIM       30
FNAMEI    DIM       150
HEADFLAG  FORM      1
KYCLAIM   DIM       3
KYHFGRP   DIM       6
KYHFUND   DIM       6
KYHFTAB   FORM      1
LINECNTR  FORM      8
PATHNAME  DIM       150
SAVKEY20  DIM       20
USERID    DIM       10
UPDCOUNT  FORM      8
ZERO8     INIT      "0000    "  
VALDPASS  FORM      1
.
XFLD0001  DIM       30
XFLD0002  DIM       30
XFLD0003  DIM       30
XFLD0004  DIM       30
XFLD0005  DIM       30
XFLD0006  DIM       30
XFLD0007  DIM       30
XFLD0008  DIM       30
XFLD0009  DIM       30
.
PRGID     INIT      "IBAADT42"
PRGNAM    INIT      "Upload & Update IFC Charge file"
VERSION   INIT      "V12.00.00"
+
.******************************************************************************
.*                                  ML0000                                    *
.*                                Main Module                                 *
.******************************************************************************
ML0000    CALL      INIT0000                * Initailize variables & open files
          CALL      KOPT0000                * Keyin Upload or Update option
          BRANCH    EXIT,ML9999
.
          BRANCH    COPTION,ML1000,ML2000
          GOTO      ML9999
.
ML1000    CALL      KASC0000                * Keyin ascii file
          BRANCH    EXIT,ML9900
.
          CALL      OPTN0000                * Choose options
          BRANCH    EXIT,ML9999
.
ML1100    CALL      SCRD0000                * Display screen
.
          CALL      KFLD0000                * Keyin fields
          BRANCH    EXIT,ML0000
.
          CALL      KUSR0000                * Keyin user id
          BRANCH    EXIT,ML0000
.
          CALL      KPTH0000                * Keyin pathname
          BRANCH    EXIT,ML0000
.
          CALL      SFLD0000
          BRANCH    EXIT,ML1100
.
          CALL      PRHD0000                * Print header
.
          CALL      CTOT0000                * Clear total vars
.
          MOVE      ONE,VALDPASS            * Validation Pass Set to Yes
          CALL      POST0000         
.
          IF        ERRCOUNT=0
            MOVE      ZERO,VALDPASS         * If no errors proceed         
            CALL      POST0000              * with Updating file
          ENDIF
.
          CALL      PTOT0000                * Print report totals
.
          GOTO      ML9999
.
.         Remove ICF Charge 
.
ML2000    CALL      OPTN0000                * Choose options
          BRANCH    EXIT,ML9999
.
          CALL      SCRR0000                * Display screen
.
          CALL      KCLM0000                * Keyin claim code
          BRANCH    EXIT,ML9999
.
          IF        OPTION<>3
            CALL      KFND0000              * Keyin HF code/ HF Group
            BRANCH    EXIT,ML9999
          ENDIF
.
ML2200    CALL      KUSR0000                * Keyin user id
          BRANCH    EXIT,ML0000
.
          CALL      PRHD0000                * Print header
          CALL      RMMP0000                * Remove ICF Charge
          GOTO      ML9999
.
ML9900    CALL      HEAD132
          PRINT     *N,"Failed to open upload file. "
.
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                 INIT0000                                   *
.*                     Initialize Variables & Open Files                      *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          CALL      IBACLOCK
.
          DISPLAY   *P56:24,"Opening patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patfn1af"
          OPEN      PATFN1A1,"patfn1af"
          OPEN      PATFN1A3,"patfn1af"
.
          DISPLAY   *P64:24,"pathgrpf"
          OPEN      PATHGRP1,"pathgrpf"
.
          DISPLAY   *P64:24,"patifcaf";
          OPEN      PATIFCA1,"patifcaf"
.
          DISPLAY   *P64:24,"patitemn";
          OPEN      PATITEM1,"patitemn"
.
          DISPLAY   *P64:24,"patcmcaf";
          OPEN      PATCMCA1,"patcmcaf"
.
          OPEN      CONTROLF,"controlf"
          OPEN      CONTROLF,"controlf"
.
INIT9999  RETURN
+
.******************************************************************************
.*                                 KOPT0000                                   *
.*                            Choose Upload or Update files                   *
.******************************************************************************
KOPT0000  MOVE      ZERO,EXIT
          MOVE      ZERO,COPTION
          DISPLAY   *P1:6,*EF:
                    *P1:6,*V2LON,"0",*HOFF," = Exit":
                    *P1:7,*V2LON,"1",*HOFF," = Upload IFC Charge":
                    *P1:8,*V2LON,"2",*HOFF," = Remove IFC Charge";
          DISPLAY   *P1:9,"Select Option : ";
.
KOPT1000  KEYIN     *P17:9,*V2LON,COPTION;
          BRANCH    COPTION,KOPT9999,KOPT9999
.
          COMPARE   ZERO,COPTION
          GOTO      KOPT9000 IF EQUAL
          BEEP
          GOTO      KOPT1000
.
KOPT9000  MOVE      ONE,EXIT
KOPT9999  RETURN
+
.******************************************************************************
.*                                 OPTN0000                                   *
.*                            Choose HF group or HF code                      *
.******************************************************************************
OPTN0000  MOVE      ZERO,EXIT
          MOVE      ZERO,OPTION
          DISPLAY   *P1:16,*EF:
                    *P1:16,*V2LON,"0",*HOFF," = Exit":
                    *P1:17,*V2LON,"1",*HOFF," = By Health fund Group":
                    *P1:18,*V2LON,"2",*HOFF," = By Health fund":
                    *P1:19,*V2LON,"3",*HOFF," = Default Health fund/table"
          DISPLAY   *P1:22,"Select Option : ";
.
OPTN1000  KEYIN     *P17:22,*V2LON,OPTION;
          BRANCH    OPTION,OPTN9999,OPTN9999,OPTN9999,OPTN9999
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                 KASC0000                                   *
.*                            Keyin ASCI file                                 *
.******************************************************************************
KASC0000  MOVE      ZERO,EXIT
          PACK      FNAMEI WITH SP70,SP70,SP70
.
          DISPLAY   *P19:13,*EF,"ICF Charge Load File";
.
          KEYIN     *P1:14,*EL,"Keyin Path & File Name : ",*V2LON,FNAMEI
.
          PACK      FNAMEI,FNAMEI,SP70,SP70,SP70
.
          MATCH     SP70,FNAMEI
          GOTO      KASC9000 IF EQUAL
.
          SCAN      ".txt",FNAMEI
          IF        !@EQUAL
            SQUEEZE   FNAMEI
            ENDSET    FNAMEI
            APPEND    ".txt",FNAMEI      * add ".txt" if there is a path
            APPEND    SP70,FNAMEI
            RESET     FNAMEI
          ENDIF
          RESET     FNAMEI
.
          DISPLAY   *P26:14,*V2LON,*EL,FNAMEI;
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      UMPROTXT,FNAMEI
          TRAPCLR   IO
          BRANCH    OVRCD,KASC8000
.
          CALL      EXCUS000
          MOVE      ZERO,EXIT
          GOTO      KASC9999
.
KASC8000  DISPLAY   *P1:24,*EL,*B,"File not found.  ";
          CALL      HITENTER
          GOTO      KASC0000
.
KASC9000  MOVE      ONE,EXIT
.
KASC9999  RETURN
+
.******************************************************************************
.*                                 KFLD0000                                   *
.*                            Keyin Fields                                    *
.******************************************************************************
KFLD0000  CALL      IVAR0000                * Initialise variables
.
          CALL      KCLM0000                * Keyin claim code
          BRANCH    EXIT,KFLD9999
.
          COMPARE   THREE,OPTION
          GOTO      KFLD2000 IF EQUAL       * default hf/table
.
          CALL      KFND0000                * Keyin HF code/ HF Group
          BRANCH    EXIT,KFLD9999
.
          CALL      KTAB0000                * Keyin health fund table 
          BRANCH    EXIT,KFLD9999           * exit
.
KFLD2000  MOVE      ZERO,EXIT
.
KFLD9999  RETURN
+
.******************************************************************************
.*                                 IVAR0000                                   *
.*                            Initialise variable                             *
.******************************************************************************
IVAR0000  MOVE      SP70,KYCLAIM
          MOVE      SP70,KYHFGRP
          MOVE      SP70,KYHFUND
          MOVE      ZERO,KYHFTAB
IVAR9999  RETURN
+
.******************************************************************************
.*                                 SCRD0000                                   *
.*                            Screen display                                  *
.******************************************************************************
SCRD0000  DISPLAY   *P1:10,*EF;
.
          DISPLAY   *P1:11," 1. Claim Code         :";
.
          COMPARE   THREE,OPTION
          GOTO      SCRD3000 IF NOT EQUAL
.
          DISPLAY   *P1:14," 2. Keyin User id      :":
                    *P1:15," 3. Keyin Path name    :";
          GOTO      SCRD9999
.
SCRD3000  IF        OPTION=1
            DISPLAY   *P1:12," 2. Health Fund Group  :";
          ELSE
            DISPLAY   *P1:12," 2. Health Fund Code   :";
          ENDIF
.
          DISPLAY   *P1:13," 3. Health Fund Table  :":
                    *P1:14," 4. Keyin User id      :":
                    *P1:15," 5. Keyin Path name    :";
SCRD9999  RETURN
+
.******************************************************************************
.*                                 SCRR0000                                   *
.*                            Screen display                                  *
.******************************************************************************
SCRR0000  DISPLAY   *P1:10,*EF;
.
          DISPLAY   *P1:11," 1. Claim Code         :";
.
          COMPARE   THREE,OPTION
          GOTO      SCRR3000 IF NOT EQUAL
.
          DISPLAY   *P1:14," 2. Keyin User id      :"
          GOTO      SCRR9999
.
SCRR3000  IF        OPTION=1
            DISPLAY   *P1:12," 2. Health Fund Group  :";
          ELSE
            DISPLAY   *P1:12," 2. Health Fund Code   :";
          ENDIF
.
          DISPLAY   *P1:14," 3. Keyin User id      :"
SCRR9999  RETURN
+
.******************************************************************************
.*                                 KCLM0000                                   *
.*                            Keyin Claim code                                *
.******************************************************************************
KCLM0000  MOVE      SP10,KYCLAIM
          MOVE      ZERO,EXIT
          KEYIN     *P26:11,*EL,*V2LON,KYCLAIM;
          ENDSET    KYCLAIM
          APPEND    SP3,KYCLAIM
          RESET     KYCLAIM
.
          MATCH     SP3,KYCLAIM
          GOTO      KCLM0800 IF EQUAL    * blank claim code
.
          MOVE      "CL",TCODE
          PACK      KEY5,TCODE,KYCLAIM,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,KCLM2000
.
          DISPLAY   *P26:11,*V2LON,KYCLAIM,*P30:11,TDESC;
          GOTO      KCLM1000
.
KCLM0800  DISPLAY   *P26:11,SP3,*P30:11,"Blank claim code";
.
KCLM1000  MOVE      ZERO,EXIT
          GOTO      KCLM9999
.
KCLM2000  DISPLAY   *P1:24,*EL,*B,*V2LON,"Invalid Claim code.  ";
          CALL      HITENTER
          GOTO      KCLM0000
.
KCLM9000  MOVE      ONE,EXIT
KCLM9999  RETURN
+
.******************************************************************************
.*                                 KFGR0000                                   *
.*                            Keyin Health fund group                         *
.******************************************************************************
KFGR0000  MOVE      SP10,KYHFGRP
          MOVE      ZERO,EXIT
          KEYIN     *P26:12,*EL,*V2LON,KYHFGRP;
          ENDSET    KYHFGRP
          APPEND    SP3,KYHFGRP
          RESET     KYHFGRP
.
          MATCH     SP3,KYHFGRP
          GOTO      KFGR9000 IF EQUAL
.
          PACK      KEY6,KYHFGRP
          CALL      RDPAHGP1
          BRANCH    OVRCD,KFGR2000
.
          DISPLAY   *P26:12,*V2LON,KYHFGRP,*P30:12,PTHGDESC;
          GOTO      KFGR8000
.
KFGR2000  DISPLAY   *P1:24,*EL,*B,*V2LON,"Invalid Health fund Group code.  ";
          CALL      HITENTER
          GOTO      KFGR0000
.
KFGR8000  MOVE      ZERO,EXIT
          GOTO      KFGR9999
.
KFGR9000  MOVE      ONE,EXIT
KFGR9999  RETURN
+
.******************************************************************************
.*                                 KHFN0000                                   *
.*                            Keyin Health fund code                          *
.******************************************************************************
KHFN0000  MOVE      SP10,KYHFUND
          MOVE      ZERO,EXIT
.
          KEYIN     *P26:12,*EL,*EL,*V2LON,KYHFUND;
          RESET     KYHFUND
          GOTO      KHFN9999 IF EOS
.
          ENDSET    KYHFUND
          APPEND    SP6,KYHFUND
          RESET     KYHFUND
.
          CMATCH    EXITCHAR,KYHFUND
          GOTO      KHFN9000 IF EQUAL
.
          MATCH     SP6,KYHFUND
          GOTO      KHFN9200 IF EQUAL
.
          PACK      KEY14,KYHFUND,ZERO8
          CALL      RDFUND1
          BRANCH    OVRCD,KHFN2000
.
          DISPLAY   *P26:12,*V2LON,KYHFUND,*P30:12,HFNAME;
          GOTO      KHFN9999
.
KHFN2000  DISPLAY   *P1:24,*EL,*B,*V2LON,"Invalid Health Fund code.  "
          CALL      HITENTER
          GOTO      KHFN0000        
.
KHFN9000  MOVE      ONE,EXIT        * exit char. entered
          GOTO      KHFN9999
.
KHFN9200  MOVE      TWO,EXIT        * spaces entered
          GOTO      KHFN9999
.
KHFN9999  RETURN
+
.******************************************************************************
.*                                 KTAB0000                                   *
.*                            Keyin HF Table                                  *
.******************************************************************************
KTAB0000  MOVE      ZERO,KYHFTAB
          MOVE      ZERO,EXIT
          DISPLAY   *P1:24,*EL,"1 = All, 2 = As per Upload";
          KEYIN     *P26:13,*EL,*V2LON,KYHFTAB;
          BRANCH    KYHFTAB,KTAB1000,KTAB1000
          DISPLAY   *P1:24,*EL,*B,*V2LON,"Must be one or two.  ";
          CALL      HITENTER
          GOTO      KTAB0000
.
KTAB1000  IF        KYHFTAB=1
            MOVE      "All                ",TDESC
          ELSE
            MOVE      "As Per Upload      ",TDESC
          ENDIF
          DISPLAY   *P26:13,*V2LON,KYHFTAB,*P36:13,TDESC;
          DISPLAY   *P1:24,*EL;
          MOVE      ZERO,EXIT
          GOTO      KTAB9999
.
KTAB9000  MOVE      ONE,EXIT
KTAB9999  RETURN
+
.******************************************************************************
.*                                  KUSR0000                                  *
.*                     Keyin user id                                          *
.******************************************************************************
KUSR0000  KEYIN     *P26:14,*EL,*V2LON,USERID;
          PACK      USERID,USERID,SP70
          MATCH     SP70,USERID
          GOTO      KUSR9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KUSR9999
.
KUSR9500  MOVE      ONE,EXIT
KUSR9999  RETURN
+
.******************************************************************************
.*                                  KPTH0000                                  *
.*                     Keyin path name                                        *
.******************************************************************************
KPTH0000  PACK      PATHNAME,SP70,SP70,SP70
          KEYIN     *P26:15,*EL,*V2LON,PATHNAME;
          ENDSET    PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          APPEND    SP70,PATHNAME
          RESET     PATHNAME
.
.         PACK      PATHNAME,PATHNAME,SP70,SP70,SP70
          MATCH     SP70,PATHNAME
          GOTO      KPTH9500 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      KPTH9999
.
KPTH9500  MOVE      ONE,EXIT
KPTH9999  RETURN
+
.******************************************************************************
.*                                  SFLD0000                                  *
.*                        Select Field                                        *
.******************************************************************************
SFLD0000  CALL      MAINQST                 * Select Item, (P)ost, (C)ancel ?
.
          COMPARE   ZERO,CCITEM
          GOTO      SFLD9999 IF EQUAL
          GOTO      SFLD1000 IF LESS
.
          MOVE      ZERO,EXIT
          IF        OPTION=3
            PERFORM   CCITEM,KCLM0000:             * Keyin claim code
                             KUSR0000:             * Keyin user id
                             KPTH0000              * Keyin pathname
          ELSE
            PERFORM   CCITEM,KCLM0000:             * Keyin claim code
                             KFND0000:             * Keyin Health fund
                             KTAB0000:             * Keyin health fund table 
                             KUSR0000:             * Keyin user id
                             KPTH0000              * Keyin pathname
          ENDIF
          BRANCH    EXIT,SFLD9999
          GOTO      SFLD0000
.
SFLD1000  MOVE      ONE,EXIT
SFLD9999  RETURN
+
.******************************************************************************
.         Keyin health fund group or health fund code
.******************************************************************************
KFND0000  IF        OPTION=1
            CALL      KFGR0000                * Keyin health fund group code
          ELSE
            CALL      KHFN0000                * Keyin health fund code
          ENDIF
KFND9999  RETURN
+
.******************************************************************************
.*                                  POST0000                                  *
.*                        Post record                                         *
.******************************************************************************
POST0000  BRANCH    OPTION,POST2000     * by health fund group
.
          IF        OPTION=3
            MOVE      SP70,KYHFUND      * blank health fund
          ENDIF
          CALL      GFND0000            * Generate fees for the health fund
          GOTO      POST9999
.
.         Get health funds for the HF group
.
POST2000  MOVE      SP70,SAVKEY20
          PACK      KEY20,KYHFGRP,SP70
          CALL      RDSFUND3
POST4000  CALL      RDKFUND3
          BRANCH    OVRCD,POST9999
          MATCH     KYHFGRP,PTHFHGRP    * Same group?
          GOTO      POST9999 IF NOT EQUAL
          MATCH     "0000    ",HFTABL
          GOTO      POST4000 IF NOT EQUAL
.
          BRANCH    PHFTACT,POST4000    * Inactive
.
          MOVE      HCODE,KYHFUND
          PACK      SAVKEY20,PTHFHGRP,HCODE,HFTABL,SP70
.
          CALL      GFND0000            * Generate fees for the health fund
.
          MOVE      SAVKEY20,KEY20
          CALL      RDFUND3             * Reposition
.
          GOTO      POST4000
.
POST9999  RETURN
+
.******************************************************************************
.*                                  PRHD0000                                  *
.*                        Print the selection details                         *
.******************************************************************************
PRHD0000  CALL      HEAD132
.
          IF        COPTION=1
            PRINT     *N,"Upload ICF Charge"
          ELSE
            PRINT     *N,"Remove ICF Charge"
          ENDIF
.
          PRINT     *N,"Date     : ",CDATE:
                    *N,"Time     : ",CTIMEIS:
                    *N,"User ID  : ",USERID
.
          BRANCH    COPTION,PRHD1000,PRHD2000
          GOTO      PRHD9999
.
PRHD1000  PRINT     *N,"Claim Code      : ";
.
          MATCH     SP70,KYCLAIM
          IF        @EQUAL
            PRINT     "Default Blank Claim code";
          ELSE
            PRINT     KYCLAIM;
          ENDIF
.
          IF        OPTION=1
            PRINT     *N,"By Health Fund Group : ",KYHFGRP
          ELSE
            IF        OPTION=2
              PRINT     *N,"By Health Fund  : ",KYHFUND
            ELSE
              PRINT     *N,"Health Fund     : Default Health Fund/table"
              GOTO      PRHD1100
            ENDIF
          ENDIF
.
          IF        KYHFTAB=1
            MOVE      "All          ",KEY15
          ELSE
            MOVE      "As Per Upload",KEY15
          ENDIF
          PRINT     *N,"Health Fund Table            : ",KEY15;
.
PRHD1100  PRINT     *N,"ICF Charge Load file: ":
                    *N,"     ",PATHNAME
.
PRHD1200  CALL      UND132
.
          PRINT     *1,"| Line",*12,"| Error ",*44,"| Record Detail":
                    *132,"|"
          CALL      UND132
          GOTO      PRHD9999
.
PRHD2000  PRINT     *N,"Claim Code : ";
.
          MATCH     SP70,KYCLAIM
          IF        @EQUAL
            PRINT     "Default Blank Claim code";
          ELSE
            PRINT     KYCLAIM;
          ENDIF
.
          IF        OPTION=1
            PRINT     *N,"By Health Fund Group : ",KYHFGRP
          ELSE
            IF        OPTION=2
              PRINT     *N,"By Health Fund  : ",KYHFUND
            ELSE
              PRINT     *N,"Default Health Fund/table"
            ENDIF
          ENDIF
.
PRHD9999  RETURN
+
.******************************************************************************
.*                                  GFND0000                                  *
.*                        Generate record of a health fund                    *
.******************************************************************************
GFND0000  MOVE      ZERO,LINECNTR
          MOVE      ONE,HEADFLAG
.
          OPEN      UMPROTXT,FNAMEI
.
GFND1000  READ      UMPROTXT,SEQ;XFLD0001,XFLD0002,XFLD0003,XFLD0004,XFLD0005:
                                 XFLD0006,XFLD0007,XFLD0008,XFLD0009
          GOTO      GFND9000 IF OVER
.
          MATCH     SP70,XFLD0007
          GOTO      GFND1000 IF EQUAL       * Skip blank charge type
.
          ADD       ONE,LINECNTR            * Text file line counter for error
.
          PACK      XFLD0001,XFLD0001,SP70
          PACK      XFLD0002,XFLD0002,SP70
          PACK      XFLD0003,XFLD0003,SP70
          PACK      XFLD0004,XFLD0004,SP70
          PACK      XFLD0005,XFLD0005,SP70
          PACK      XFLD0006,XFLD0006,SP70
          PACK      XFLD0007,XFLD0007,SP70
          PACK      XFLD0008,XFLD0008,SP70
          PACK      XFLD0009,XFLD0009,SP70
.
          IF        VALDPASS=1
            CALL      VFLD0000                * Validation pass so
            BRANCH    EXIT,GFND1000           * validate fields
          ENDIF
.
          PACK      XCLMCD,XFLD0001,SP70
          MATCH     XCLMCD,KYCLAIM
          IF        !@EQUAL
            MOVE      "Claim Code Mismatch",ERRORMSG
            CALL      PERR0000
            GOTO      GFND1000
          ENDIF
.
          PACK      XHFCOD,XFLD0002,SP70
.
          BRANCH    OPTION,GFND2100,GFND2200,GFND2300
          GOTO      GFND1000
.
GFND2100  PACK      XHFCOD,KYHFUND,SP70
          PACK      XHFTYP,XFLD0003,SP70
          GOTO      GFND3000
.
GFND2200  MATCH     XHFCOD,KYHFUND
          IF        !@EQUAL
            MOVE      "Health Fund Mismatch",ERRORMSG
            CALL      PERR0000
            GOTO      GFND1000
          ENDIF
.
          PACK      XHFTYP,XFLD0003,SP70
          GOTO      GFND3000
.
GFND2300  MOVE      SP70,XHFCOD
          MOVE      SP70,XHFTYP
          GOTO      GFND3000
.
GFND3000  PACK      XCCMIX,XFLD0004,SP70
          PACK      XCPMBS,XFLD0005,SP70
          PACK      XCATYP,XFLD0006,SP70
          PACK      XCCTYP,XFLD0007,SP70
.
          MOVE      ZERO,XCRAMT
          SQUEEZE   XFLD0008
          MOVE      XFLD0008,XCRAMT
.
          MOVE      ZERO,XCPAMT
          SQUEEZE   XFLD0009
          MOVE      XFLD0009,XCPAMT
.
          COMPARE   THREE,OPTION
          GOTO      GFND3800 IF EQUAL       * Default blank HF/table
.
          BRANCH    KYHFTAB,GFND4000        * All Health fund table
.
.         as per Upload file
.
GFND3800  CALL      LIFC0000
          GOTO      GFND1000
.
.         All health fund table type of the health fund
.
GFND4000  PACK      KEY14,KYHFUND,SP70
          CALL      RDSFUND1
GFND5000  CALL      RDKFUND1
          BRANCH    OVRCD,GFND1000
          MATCH     KYHFUND,HCODE
          GOTO      GFND1000 IF NOT EQUAL
.
          MATCH     "0000    ",HFTABL
          GOTO      GFND5000 IF EQUAL  
.
          BRANCH    PHFTACT,GFND5000        * Inactive
.
          MOVE      PTHFBCAT,XHFTYP         * Health fund Table type
          CALL      LIFC0000
          GOTO      GFND5000                * get next record
.
GFND9000  CLOSE     UMPROTXT
.
GFND9999  RETURN
+
.******************************************************************************
.*                                  LIFC0000                                  *
.*                             Write ICF Charge record                        *
.******************************************************************************
LIFC0000  UNPACK    SP70,PTFCCLTY,PTFCFUND,PTFCHTAB,PTFCRTYP,PTFCCMAT,PTFCCTYP
.
          MATCH     SP70,XCCMIX
          IF        !@EQUAL
            MOVE      ONE,PTFCRTYP                 * Casemix code
            PACK      PTFCCMAT,XCCMIX,SP70
          ELSE
            MATCH     SP70,XCPMBS
            IF        !@EQUAL
              MOVE      TWO,PTFCRTYP               * MBS Code
              PACK      PTFCCMAT,XCPMBS,SP70
            ELSE
              MATCH     SP70,XCATYP
              IF        !@EQUAL
                MOVE      THREE,PTFCRTYP           * Adm.type
                PACK      PTFCCMAT,XCATYP,SP70
              ENDIF
            ENDIF
          ENDIF
          PACK      KEY25,XCLMCD,XHFCOD,XHFTYP,PTFCRTYP,PTFCCMAT,XCCTYP,SP70
          CALL      RDPTIFC1
          IF        OVRCD=1
            UNPACK   KEY25,PTFCCLTY,PTFCFUND,PTFCHTAB,PTFCRTYP,PTFCCMAT,PTFCCTYP
            MOVE      XCRAMT,PTFCRAMT
            MOVE      XCPAMT,PTFCPAMT
            UNPACK    SP70,PTFCSPRE
            IF        VALDPASS=0
              ADD       ONE,ADDCOUNT        * Total records added
              CALL      WRPTIFC1            * Write if not validation pass
            ENDIF
          ELSE
            MOVE      XCRAMT,PTFCRAMT
            MOVE      XCPAMT,PTFCPAMT
            IF        VALDPASS=0
              ADD       ONE,UPDCOUNT        * Total records updated
              CALL      UPPTIFC1            * Update if not validation pass
            ENDIF                         
          ENDIF
LIFC9999  RETURN
+
.******************************************************************************
.         Remove ICF Charge record
.******************************************************************************
RMMP0000  BRANCH    OPTION,RMMP2000         * by health fund group
          IF        OPTION=3
            MOVE      SP70,KYHFUND          * blank health fund
          ENDIF
          CALL      DREC0000                * Deleting existing records
          GOTO      RMMP9999
.
.         Get health funds for the HF group
.
RMMP2000  MOVE      SP70,SAVKEY20
          PACK      KEY20,KYHFGRP,SP70
          CALL      RDSFUND3
RMMP4000  CALL      RDKFUND3
          BRANCH    OVRCD,RMMP9999
          MATCH     KYHFGRP,PTHFHGRP    * Same group?
          GOTO      RMMP9999 IF NOT EQUAL
          MATCH     "0000    ",HFTABL
          GOTO      RMMP4000 IF NOT EQUAL
.
          MOVE      HCODE,KYHFUND
          PACK      SAVKEY20,PTHFHGRP,HCODE,HFTABL,SP70
.
          CALL      DREC0000                * Deleting existing records
.
          MOVE      SAVKEY20,KEY20
          CALL      RDFUND3                 * Reposition
          GOTO      RMMP4000
.
RMMP9999  RETURN
+
.******************************************************************************
.         Deleting existing records
.******************************************************************************
DREC0000  PACK      KEY25,KYCLAIM,KYHFUND,SP70
          CALL      RSPTIFC1
          CALL      RKPTIFC1
          BRANCH    OVRCD,DREC9999
.
          MATCH     KYCLAIM,PTFCCLTY
          GOTO      DREC9999 IF NOT EQUAL
.
          MATCH     KYHFUND,PTFCFUND
          GOTO      DREC9999 IF NOT EQUAL
.
	  PACK      KEY25,PTFCCLTY,PTFCFUND,PTFCHTAB,PTFCRTYP,PTFCCMAT:
                          PTFCCTYP,SP70
          MATCH     SP70,KEY25
          GOTO      DREC9999 IF EQUAL
          CALL      DEPTIFC1
          GOTO      DREC0000
.
DREC9999  RETURN
+
.******************************************************************************

.         Execute us1 command before processing
.******************************************************************************
EXCUS000  CLEAR     CMDLINE
          APPEND    "ibaadt42.us1 ",CMDLINE
          APPEND    FNAMEI,CMDLINE
          APPEND    SP70,CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
EXCUS999  RETURN
+
.******************************************************************************
. Validate bed fee input text file fields
.******************************************************************************
VFLD0000  MOVE      ZERO,EXIT
.
          MATCH     SP70,XFLD0001
          IF        !@EQUAL
            PACK      KEY5,ANSC,ANSL,XFLD0001,SP70
            CALL      RDCODE1      
            IF        OVRCD=1
              MOVE      "Invalid Claim Code",ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF
          ENDIF
.
          MATCH     SP70,XFLD0002
          IF        !@EQUAL
            PACK      KEY6,XFLD0002,SP70
            PACK      KEY14,KEY6,ZERO8,SP70
            CALL      RDFUND1
            IF        OVRCD=1
              MOVE      "Invalid Health Fund",ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF
          ENDIF
.
          MATCH     SP70,XFLD0003
          IF        !@EQUAL
            PACK      KEY5,ANSH,ANST,XFLD0003,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Health Fund Table Type",ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF
          ENDIF
.
          MOVE      ZERO,F1
          MATCH     SP70,XFLD0004
          IF        !@EQUAL
            MOVE      ONE,F1
            PACK      KEY9,XFLD0004,SP70
            CALL      RDPTCMC1
            IF        OVRCD=1
              MOVE      "Invalid Casemix code",ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF
          ENDIF
.
          MATCH     SP70,XFLD0005
          IF        !@EQUAL
            MOVE      ONE,F1
            PACK      KEY17,XFLD0005,SP70
            CALL      PATITMRD
            IF        OVRCD=1
              MOVE      "Invalid MBS Item code",ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF
          ENDIF
.
          MATCH     SP70,XFLD0006
          IF        !@EQUAL
            MOVE      ONE,F1
            MOVE      "A ",KEY2
            PACK      KEY5,KEY2,XFLD0006,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      "Invalid Admission Type",ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF
          ENDIF
          IF        F1=0
            CLEAR     ERRORMSG
            APPEND    "Blank Adm.Typ/MBS/Casemix code",ERRORMSG
            APPEND    SP70,ERRORMSG
            RESET     ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
          ENDIF
.
          MATCH     SP70,XFLD0007
          IF        @EQUAL
            MOVE      "Blank Charge Type",ERRORMSG
            CALL      PERR0000
            MOVE      ONE,EXIT
          ENDIF
.
          PACK      XFLD0008,XFLD0008,SP70
          MATCH     SP70,XFLD0008
          IF        !@EQUAL
            SQUEEZE   XFLD0008
            TYPE      XFLD0008
            IF        !@EQUAL | @EOS
              MOVE      "Invalid Rebate Amount",ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF
          ENDIF
.      
          PACK      XFLD0009,XFLD0009,SP70
          MATCH     SP70,XFLD0009
          IF        !@EQUAL
            SQUEEZE   XFLD0009
            TYPE      XFLD0009
            IF        !@EQUAL | @EOS
              MOVE      "Invalid Patient Amount",ERRORMSG
              CALL      PERR0000
              MOVE      ONE,EXIT
            ENDIF
          ENDIF
.      
VFLD9999  RETURN
+
.******************************************************************************
. Print error line
.******************************************************************************
PERR0000  ADD       ONE,ERRCOUNT            * Total error count
.
          UNPACK    XFLD0001,KEY3
          UNPACK    XFLD0002,KEY6
          UNPACK    XFLD0003,DIM3
.
          PRINT     *1,"|",LINECNTR,*12,"|",ERRORMSG,*44,"|":
                    KEY3,SP2,KEY6,SP2,DIM3,SP2;
.
          UNPACK    XFLD0004,DIM9
          UNPACK    XFLD0005,KEY9
          UNPACK    XFLD0006,KEY3
.
          PRINT     DIM9,SP2,KEY9,SP2,KEY3,SP2;
.
          UNPACK    XFLD0007,KEY3
          UNPACK    XFLD0008,DIM15
          UNPACK    XFLD0009,KEY15
.
          PRINT     KEY3,SP2,DIM15,SP2,KEY15:
                    *132,"|"
.
PERR9999  RETURN
+
.******************************************************************************
. Clear total vars
.******************************************************************************
CTOT0000  MOVE      ZERO,ERRCOUNT
          MOVE      ZERO,ADDCOUNT
          MOVE      ZERO,UPDCOUNT
.
CTOT999   RETURN
+
.******************************************************************************
. Print total line
.******************************************************************************
PTOT0000  CALL      UND132
.
          PRINT     *1,"| Total Errors  : ",ERRCOUNT,*132,"|"
          PRINT     *1,"| Total Added   : ",ADDCOUNT,*132,"|"
          PRINT     *1,"| Total Updated : ",UPDCOUNT,*132,"|"
.
          CALL      UND132
.
PTOT9999  RETURN
+
.******************************************************************************
.
          INC       STD001IO
          INC       PATITMRD
.
          INC       PATCODIO/INC
          INC       PATIFCIO/INC            * ICF Charge file
          INC       PATFN1IO/INC            * Health fund file
          INC       PATFX1IO/INC            * Health fund file
          INC       PATHGPIO/INC            * Health fund group
          INC       PATITMIO/INC            * MBS Item file
          INC       PATCMCIO/INC            * Casemix code file
.
