.------------------------------------------------------------
. Include  : WEBRSHCD.PBL
.
. Function : Standard Routines for Scheduling Reports from
.            Web Servers
.           
.------------------------------------------------------------
. Schedule a Report for Execution
.------------------------------------------------------------
. Inputs Required
.-----------------
.  SCHDDATE     Schedule Date (Blank for Immediate)
.  SCHDTIME     Schedule Time (Blank for Immediate)
.  SCHDPROG     Program ID
.  SCHDNAME     Description 
.  SCHDUDR1     User Ref 1
.  SCHDUDR2     User Ref 2
.  SCHDUDD1     User Date 1
.  SCHDUDD2     User Date 2
.  SCHDUDY1     User Y/N 1
.  SCHDUDY2     User Y/N 2
.  PASSCODE     User Passcode
.
. Returns
.--------
.  Locked Schedule Record with all Details
.  New Parameter File
.------------------------------------------------------------
NEWRSH00  CALL      IBACLOCK
          MOVE      "   91",AUDTSECT
          READ      CONTROLF,AUDTSECT;*99,CWEBRDIR,CWEBRCFG,CWEBROOT
.
          CALL      NXTRSH00                   * Get Next Schedule Number
          MOVE      "1",WBRSSTA                * Status 1 = Waiting
          MATCH     SP70,SCHDDATE
          IF        @EQUAL
            PACK      WBRSSDT,CCC,CYY,CMM,CDD  * Request Date  
            REP       " 0",WBRSSDT
            MOVE      CTIMEIS,WBRSSTM          * Schedule Time
          ELSE
            MOVE      SCHDDATE,WBRSSDT         * Schedule Date
            MOVE      SCHDTIME,WBRSSTM         * Schedule Time
            MATCH     SP70,SCHDTIME
            IF        @EQUAL
              MOVE      CTIMEIS,WBRSSTM        * Schedule Time
            ENDIF
          ENDIF
          MOVE      SCHDPROG,WBRSPRG           * Program ID
          MOVE      SCHDNAME,WBRSDES           * Description 
          MOVE      WBSEUID,WBRSUSR            * User
          PACK      WBRSRDT,CCC,CYY,CMM,CDD    * Request Date  
          REP       " 0",WBRSRDT
          MOVE      CTIMEIS,WBRSRTM            * Request Time 
          MOVE      SP70,WBRSBDT               * Start Date 
          MOVE      SP70,WBRSBTM               * Start Time
          MOVE      ZERO,WBRSBFL               * Start Flag
          MOVE      SCHDTYPE,WBRSTYP           * Type 0=txt 1=html
          MOVE      ZERO,WBRSERC               * Expd No of Records
          MOVE      ZERO,WBRSPRC               * Proc No of Records
          MOVE      SP70,WBRSUTM               * Last Update Time 
          MOVE      SCHDUDR1,WBRSUR1           * User Ref 1
          MOVE      SCHDUDR2,WBRSUR2           * User Ref 2
          MOVE      SCHDUDD1,WBRSUD1           * User Date 1
          MOVE      SCHDUDD2,WBRSUD2           * User Date 2
          MOVE      SCHDUDY1,WBRSUY1           * User Y/N 1
          MOVE      SCHDUDY2,WBRSUY2           * User Y/N 2
          MOVE      SP70,WBRSSPA               * Last Update Time 
          MOVE      WBRSFNO,KEY8
          CALL      RDWBRS1
          COMPARE   ZERO,OVRCD
          GOTO      NEWRSH00 IF EQUAL
          CALL      WRWBRS1
          CALL      RLWBRS1
.
          CLEAR     PARANAME
          STRIP     CWEBRCFG
          APPEND    CWEBRCFG,PARANAME
          APPEND    WBRSFNO,PARANAME
          RESET     PARANAME
          PREP      PARAFILE,PARANAME
.
NEWRSH99  RETURN
.------------------------------------------------------------
. Get Next Report Schedule Number
.------------------------------------------------------------
NXTRSH00  PACK      KEY8,Z70
          CALL      RSWBRS1
NXTRSH10  CALL      RPWBRS1
          BRANCH    OVRCD,NXTRSH90
          MOVE      WBRSFNO,SCHDFLNO
          ADD       ONE,SCHDFLNO
          GOTO      NXTRSH95
.          
NXTRSH90  MOVE      ONE,SCHDFLNO
.
NXTRSH95  MOVE      SCHDFLNO,WBRSFNO
          REP       " 0",WBRSFNO
.         
NXTRSH99  RETURN
.--------------------------------------------------
. Clear Scheduling Parameters
.--------------------------------------------------
CLRRSH00  MOVE     SP70,SCHDFILE     * Schedule File
          MOVE     SP70,SCHDDATE     * Schedule Date (Blank for Immediate)
          MOVE     SP70,SCHDTIME     * Schedule Time (Blank for Immediate)
          MOVE     SP70,SCHDPROG     * Program ID
          MOVE     ZERO,SCHDTYPE     * Program ID
          MOVE     SP70,SCHDNAME     * Description 
          MOVE     SP70,SCHDUDR1     * User Ref 1
          MOVE     SP70,SCHDUDR2     * User Ref 2
          MOVE     SP70,SCHDUDD1     * User Date 1
          MOVE     SP70,SCHDUDD2     * User Date 2
          MOVE     SP70,SCHDUDY1     * User Y/N 1
          MOVE     SP70,SCHDUDY2     * User Y/N 2
          RETURN
.--------------------------------------------------
. Set Scheduling Parameters
.--------------------------------------------------
SETRSH00  MATCH     "schdfile=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SCHDFILE     * Schedule File Number
            GOTO      SETSCH99
          ENDIF
.
          MATCH     "schddate=",QRYNAME
          IF        @EQUAL
            PACK      QRYDATA,QRYDATA,SP70
            MATCH     SP70,QRYDATA
            IF        !@EQUAL
              UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
              CALL      SETMTH00        * Set CMON from MTHNAM3
              PACK      SCHDDATE,CCENT,CYEAR,CMON,CDAY
              REP       " 0",SCHDDATE
            ENDIF
            GOTO      SETSCH99
          ENDIF
.
          MATCH     "schdtime=",QRYNAME
          IF        @EQUAL
            PACK      QRYDATA,QRYDATA,SP70
            MATCH     SP70,QRYDATA
            IF        !@EQUAL
              MOVE      QRYDATA,SCHDTIME   * Schedule Time (Blank for Immediate)
            ENDIF
            GOTO      SETSCH99
          ENDIF
.
          MATCH     "schdprog=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SCHDPROG     * Program ID
            GOTO      SETSCH99
          ENDIF
.
          MATCH     "schdtype=",QRYNAME
          IF        @EQUAL
            SQUEEZE   QRYDATA
            MOVE      QRYDATA,SCHDTYPE     * Program ID
            GOTO      SETSCH99
          ENDIF
.
          MATCH     "schdname=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SCHDNAME     * Description
            GOTO      SETSCH99
          ENDIF
.
          MATCH     "schdudr1=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SCHDUDR1     * User Ref 1
            GOTO      SETSCH99
          ENDIF
.
          MATCH     "schdudr2=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SCHDUDR2     * User Ref 2
            GOTO      SETSCH99
          ENDIF
.
          MATCH     "schdudd1=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SCHDUDD1     * User Date 1
            GOTO      SETSCH99
          ENDIF
.
          MATCH     "schdudd2=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SCHDUDD2     * User Date 2
            GOTO      SETSCH99
          ENDIF
.
          MATCH     "schdudy1=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SCHDUDY1     * User Y/N 1
            GOTO      SETSCH99
          ENDIF
.
          MATCH     "schdudy2=",QRYNAME
          IF        @EQUAL
            MOVE      QRYDATA,SCHDUDY2     * User Y/N 2
            GOTO      SETSCH99
          ENDIF
.
SETSCH99  RETURN
.
          INC       WEBRSHIO/INC
.
