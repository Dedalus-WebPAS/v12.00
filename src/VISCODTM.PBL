. *----------------------------------------------------------------------
. * Include Name  :   VISCODTM.PBL
. *
. * Function      :   CGI functions for VISCODFD - viscodaf.dat
. * 
. * Usage         :   Requires VISCODFD - Open on index 1
. *                            VISCODIO
. *                            VISCODTD
. *                            VISCODTM
. * 
. *                   CLRPAR00 CALL CPVSCO00
. *
. *                   SETPAR00 
. *
. *                   MATCH     "vscdcatg",QRYNAME
. *                   IF        @EQUAL
. *                     CALL      SVSCAT00
. *                     GOTO      SETPAR99
. *                   ENDIF
. *
. *                   MATCH     "vscdcode",QRYNAME
. *                   IF        @EQUAL
. *                     CALL      SVSCOD00
. *                     GOTO      SETPAR99
. *                   ENDIF
. *
. *                   Add, Update or delete CALL MVVSCO00
. *
. *                   PROFLD tag CALL VISITM00
. *----------------------------------------------------------------------
.  Modifications Done :
.
.  V10.14.01  04/02/2019  Ebon Clements     TSK 0862664
.             Added .9.1 Selection list with indicators
.  V10.10.01  17/03/2017  Ebon Clements     TSK 0814747
.             Added .8 display code description option - VISLDS00
.  V10.09.01  17/01/2017  Davin             TSK 0814742
.             Don't display selection list if category does not exist (visslc00)
.  V10.08.02  24/03/2016  Peter Vela        TSK 0321157
.             Added Link Type 03 Pre-Operative Check List
.  V10.08.01  29/03/2016  Steve Armstrong   TSK 0309924
.             Added VSCODELT,VSCODDAT,VSCODTIM & VSCODUID to CLVISCOD
.  V10.03.03  08/11/2013  Davin             CAR 292582
.             Added tag parameters 5 & 6 for default codes (visitm00/visbod00)
.  V10.03.03  02/10/2013  Peter Vela        CAR 291724
.             Added clear to VISCODFD variables after failed direct read
.             in VISBOX00
.  V10.03.02  31/05/2013  Davin             CAR 283509
.             Increased VISCODKY to 30 chars and changed to KEY37
.             Added type 2 (cancer reg) to lvsk0000
.  V10.03.01  04/10/2012  Ebon Clements     CAR 273462
.             Fixed selection list blank display and write
.  V10.03.00  17/08/2012  Ebon Clements     CAR 270232
.             Created include
. *----------------------------------------------------------------------
.------------------------------------------------------------------------
. Clear cgi parameter array for visit coding fields
.------------------------------------------------------------------------
CPVSCO00  MOVE      ONE,VSCATCNT
          WHILE     VSCATCNT <= 20
            PACK      VSCATARR[VSCATCNT],SP70
            ADD       ONE,VSCATCNT
          DO
          MOVE      ZERO,VSCATCNT
.
          MOVE      ONE,VSCODCNT
          WHILE     VSCODCNT <= 200
            PACK      VSCODARR[VSCODCNT],SP70,SP70,SP70
            ADD       ONE,VSCODCNT
          DO
          MOVE      ZERO,VSCODCNT
.
CPVSCO99  RETURN
.
.------------------------------------------------------------
. Set cgi parameters array for visit coding category fields
.------------------------------------------------------------
SVSCAT00  ADD       ONE,VSCATCNT
          IF        VSCATCNT>20
            GOTO      SVSCAT99
          ENDIF
.
          PACK      VSCATARR[VSCATCNT],QRYDATA,SP70
.
SVSCAT99  RETURN
.
.------------------------------------------------------------
. Set cgi parameters array for visit coding category and code fields
.------------------------------------------------------------
SVSCOD00  ADD       ONE,VSCODCNT
          IF        VSCODCNT>200
            GOTO      SVSCOD99
          ENDIF
.
          PACK      VSCODARR[VSCODCNT],QRYDATA,SP70,SP70,SP70
.
SVSCOD99  RETURN
.
.------------------------------------------------------------
. Update visit coding fields
.------------------------------------------------------------
MVVSCO00  COMPARE   ZERO,VSCATCNT                * No records in category
          GOTO      MVVSCO99 IF EQUAL            * array
.
          MOVE      ZERO,F3
.
MVVSCO10  ADD       ONE,F3
          IF        F3>VSCATCNT 
            GOTO      MVVSCO50                   * Finished category array
          ENDIF
.
          UNPACK    VSCATARR[F3],VISCODTY,VISCODCT
          CALL      LVSK0000                         * Load coding key
.
          PACK      KEY37,VISCODTY,VISCODKY,VISCODCT,SP70
MVVSCO15  CALL      RSVSCOD1
          CALL      RKVSCOD1                     * Delete records before
          BRANCH    OVRCD,MVVSCO10               * update
.
          MATCH     VISCODTY,VSCOTYPE            * Correct link type
          GOTO      MVVSCO10 IF NOT EQUAL
.
          MATCH     VISCODKY,VSCOTKEY            * Correct link key
          GOTO      MVVSCO10 IF NOT EQUAL
.
          MATCH     VISCODCT,VSCOCATG            * Correct link category
          GOTO      MVVSCO10 IF NOT EQUAL
.
          PACK      KEY37,VSCOTYPE,VSCOTKEY,VSCOCATG,VSCOCODE,SP70
          CALL      DEVSCOD1
          GOTO      MVVSCO15
.
MVVSCO50  COMPARE   ZERO,VSCODCNT                * No records in category
          GOTO      MVVSCO99 IF EQUAL            * code array
.
          MOVE      ZERO,F3
.
MVVSCO60  ADD       ONE,F3
          IF        F3>VSCODCNT
            GOTO      MVVSCO99
          ENDIF
.
          CALL      CLVISCOD
.
          UNPACK    VSCODARR[F3],VSCOTYPE,VSCOCATG,VSCOCODE,VSCOTEXT
          CALL      LVSK0000                         * Load coding key
          MOVE      VISCODKY,VSCOTKEY
.
          MATCH     SP70,VSCOCODE                    * Skip blank codes
          GOTO      MVVSCO60 IF EQUAL
.
          MOVE      USERID,VSCOCUID
          PACK      VSCOCDAT,CCC,CYY,CMM,CDD
          REP       " 0",VSCOCDAT
          CLOCK     TIME,VSCOCTIM
.
          PACK      KEY37,VSCOTYPE,VSCOTKEY,VSCOCATG,VSCOCODE,SP70
          CALL      RAVSCOD1
          IF        OVRCD=1
            CALL      WRVSCOD1
          ENDIF       
.
          GOTO      MVVSCO60
.
MVVSCO99  RETURN
.
.------------------------------------------------------------
. Clear visit coding fields
.------------------------------------------------------------
CLVISCOD  MOVE      SP70,VSCOTYPE
          MOVE      SP70,VSCOTKEY
          MOVE      SP70,VSCOCATG
          MOVE      SP70,VSCOCODE
          MOVE      SP70,VSCOTEXT
          MOVE      SP70,VSCOCDAT
          MOVE      SP70,VSCOCTIM
          MOVE      SP70,VSCOCUID
          MOVE      SP70,VSCODELT
          MOVE      SP70,VSCODDAT
          MOVE      SP70,VSCODTIM
          MOVE      SP70,VSCODUID
          PACK      VSCOSPAR,SP70,SP70
.
          RETURN
.
.------------------------------------------------------------
. Tags to output visit based coding details
. Usage - XXXWEB01.XX.XXX.AA.BB.C
.
. Tag Parameter 
.          AA = Link Type
.          BB = Link Category
.          C  = 1 - Check box option list - Short Description
.               2 - Check box option list - Long Description
.               3 - Selection  list       - Short Description
.               4 - Selection  list       - Long Description
.               5 - Check box option list - Short Description with default code
.               6 - Check box option list - Long Description with default code
.               7 - Check box option list - Long/Short Description , default
.                                           and comments
.               8 - Long description of category code
.               9 - Next subset of tags .9.X
.                   .1 - Selection list Short Description with indicators
.                   .2 - Selection list long Description with indicators
.------------------------------------------------------------
VISITM00  UNPACK    DIM127,D1,VISCODTY,DIM127        * Link Type
          UNPACK    DIM127,D1,VISCODCT,DIM127        * Link Category
.
          UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
.
          CALL      LVSK0000                         * Load coding key
.
          BRANCH    F1,VISITM10,VISITM20,VISITM30,VISITM40,VISITM50:
                       VISITM60,VISITM70,VISITM80,VISITM90
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      VISITM99
.
VISITM10  CALL      VISBOX00              
          GOTO      VISITM99
.
VISITM20  CALL      VISBOX00
          GOTO      VISITM99
.
VISITM30  CALL      VISSLC00
          GOTO      VISITM99
.
VISITM40  CALL      VISSLC00
          GOTO      VISITM99
.
VISITM50  CALL      VISBOD00
          GOTO      VISITM99
.
VISITM60  CALL      VISBOD00
          GOTO      VISITM99
.
VISITM70  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,VSCODLDS
          MOVE      KEY1,VSCODLDS
          UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,VSCODDEF
          MOVE      KEY1,VSCODDEF
          UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,VSCODCOM
          MOVE      KEY1,VSCODCOM
          CALL      VISCOM00
          GOTO      VISITM99
.
VISITM80  CALL      VISLDS00
          GOTO      VISITM99
.
VISITM90  UNPACK    DIM127,D1,KEY1,DIM127 * Second subset of tags from .9
          MOVE      ZERO,F1
          MOVE      KEY1,F1
.
          BRANCH    F1,VISITM91,VISITM92
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      VISITM99
.
VISITM91  CALL      VISSLI00      
          GOTO      VISITM99
.
VISITM92  CALL      VISSLI00      
          GOTO      VISITM99
.
VISITM99  RETURN
.
.------------------------------------------------------------
. Add check box options
.------------------------------------------------------------
VISBOX00  WRITE     HTMLFILE;"<input type=#"hidden#" name=#"vscdcatg#"":
                             " value=#"",VISCODTY,VISCODCT,"#">"
.
          PACK      PTCDKEY2,VISCODCT,SP70
          CALL      RDSCODE2
VISBOX10  CALL      RDKCODE2
          BRANCH    OVRCD,VISBOX99
.
          MATCH     VISCODCT,TCODE
          GOTO      VISBOX99 IF NOT EQUAL
.
          MATCH     SP70,ACODE
          GOTO      VISBOX10 IF EQUAL
.
          PACK      KEY7,VISCODTY,TCODE,ACODE,SP70
.
          CALL      LVSK0000                         * Load coding key
.
          PACK      KEY37,VISCODTY,VISCODKY,TCODE,ACODE,SP70
          CALL      RDVSCOD1
          IF        OVRCD<>1
            MATCH     ANSO,TCINDC12
            IF        @EQUAL
              WRITE     HTMLFILE;"<input type=#"checkbox#" name=#"vscdcode#" ":
                                 "id=#"",ANSC,KEY7,"#" ":
                                 "onclick=#"VisCodToggle('",KEY7,"');#" ":
                                 "value=#"",VISCODTY,TCODE,ACODE,VSCOTEXT,"#" ":
                                 "checked>";
.
              IF        F1=1
                WRITE     HTMLFILE;TDESC;
              ENDIF
              IF        F1=2
                WRITE     HTMLFILE;PTCDLDES;
              ENDIF
              WRITE     HTMLFILE;"<br>"
.
              WRITE     HTMLFILE;"<input type=#"text#" title=#"",TDESC,"#" ":
                                 "name=#"d_vscdcode#" class=#"Mandatory#" ":
                                 "id=#"",ANST,KEY7,"#" ":
                                 "onblur=#"VisCodField('",KEY7,"');#" ":
                                 "value=#"",VSCOTEXT,"#" ":
                                 "size=80 maxlength=200><br> "
            ELSE
              WRITE     HTMLFILE;"<input type=#"checkbox#" name=#"vscdcode#" ":
                                 "value=#"",VISCODTY,TCODE,ACODE,"#" checked>";
              IF        F1=1
                WRITE     HTMLFILE;TDESC;
              ENDIF
              IF        F1=2
                WRITE     HTMLFILE;PTCDLDES;
              ENDIF
              WRITE     HTMLFILE;"<br>"
            ENDIF
          ELSE
.
            CALL      CLVISCOD
.
            MATCH     "I",PTCOACTV
            GOTO      VISBOX10 IF EQUAL
.
            IF        IBCNMHOS=1
              MATCH    "1",PTCDHOSS
              IF       @EQUAL
                PACK     KEY8,WBSEHOSP,TCODE,ACODE,SP70
                CALL     RDMLCOD1
                BRANCH   OVRCD,VISBOX10
              ENDIF
            ENDIF
.
            MATCH     ANSO,TCINDC12
            IF        @EQUAL
              WRITE     HTMLFILE;"<input type=#"checkbox#" name=#"vscdcode#" ":
                                 "id=#"",ANSC,KEY7,"#" ":
                                 "onclick=#"VisCodToggle('",KEY7,"');#" ":
                                 "value=#"",VISCODTY,TCODE,ACODE,VSCOTEXT,"#">";
              IF        F1=1
                WRITE     HTMLFILE;TDESC;
              ENDIF
              IF        F1=2
                WRITE     HTMLFILE;PTCDLDES;
              ENDIF
              WRITE     HTMLFILE;"<br>"
.
              WRITE     HTMLFILE;"<input type=#"text#" title=#"",TDESC,"#" ":
                                 "name=#"d_vscdcode#" disabled ":
                                 "style=#"display:none#" ":
                                 "id=#"",ANST,KEY7,"#" ":
                                 "onblur=#"VisCodField('",KEY7,"');#" ":
                                 "value=#"",VSCOTEXT,"#" ":
                                 "size=80 maxlength=200><br> "
            ELSE
              WRITE     HTMLFILE;"<input type=#"checkbox#" name=#"vscdcode#"":
                                 " value=#"",VISCODTY,TCODE,ACODE,"#">";
              IF        F1=1
                WRITE     HTMLFILE;TDESC;
              ENDIF
              IF        F1=2
                WRITE     HTMLFILE;PTCDLDES;
              ENDIF
              WRITE     HTMLFILE;"<br>"
            ENDIF
          ENDIF
          GOTO      VISBOX10
.
VISBOX99  RETURN
.
.------------------------------------------------------------
. Add check box options (with default code - for add screens)
.------------------------------------------------------------
VISBOD00  WRITE     HTMLFILE;"<input type=#"hidden#" name=#"vscdcatg#"":
                             " value=#"",VISCODTY,VISCODCT,"#">"
.
          PACK      PTCDKEY2,VISCODCT,SP70
          CALL      RDSCODE2
VISBOD10  CALL      RDKCODE2
          BRANCH    OVRCD,VISBOD99
.
          MATCH     VISCODCT,TCODE
          GOTO      VISBOD99 IF NOT EQUAL
.
          MATCH     SP70,ACODE
          GOTO      VISBOD10 IF EQUAL
.
          PACK      KEY7,VISCODTY,TCODE,ACODE,SP70
.
          CALL      LVSK0000                         * Load coding key
.
          PACK      KEY37,VISCODTY,VISCODKY,TCODE,ACODE,SP70
          CALL      RDVSCOD1
          IF        OVRCD<>1
            MATCH     ANSO,TCINDC12
            IF        @EQUAL
              WRITE     HTMLFILE;"<input type=#"checkbox#" name=#"vscdcode#" ":
                                 "id=#"",ANSC,KEY7,"#" ":
                                 "onclick=#"VisCodToggle('",KEY7,"');#" ":
                                 "value=#"",VISCODTY,TCODE,ACODE,VSCOTEXT,"#" ":
                                 "checked>";
.
              IF        F1=5
                WRITE     HTMLFILE;TDESC;
              ENDIF
              IF        F1=6
                WRITE     HTMLFILE;PTCDLDES;
              ENDIF
              WRITE     HTMLFILE;"<br>"
.
              WRITE     HTMLFILE;"<input type=#"text#" title=#"",TDESC,"#" ":
                                 "name=#"d_vscdcode#" class=#"Mandatory#" ":
                                 "id=#"",ANST,KEY7,"#" ":
                                 "onblur=#"VisCodField('",KEY7,"');#" ":
                                 "value=#"",VSCOTEXT,"#" ":
                                 "size=80 maxlength=200><br> "
            ELSE
              WRITE     HTMLFILE;"<input type=#"checkbox#" name=#"vscdcode#" ":
                                 "value=#"",VISCODTY,TCODE,ACODE,"#" checked>";
              IF        F1=5
                WRITE     HTMLFILE;TDESC;
              ENDIF
              IF        F1=6
                WRITE     HTMLFILE;PTCDLDES;
              ENDIF
              WRITE     HTMLFILE;"<br>"
            ENDIF
          ELSE
.
            CALL      CLVISCOD
.
            MATCH     "I",PTCOACTV
            GOTO      VISBOD10 IF EQUAL
.
            IF        IBCNMHOS=1
              MATCH    "1",PTCDHOSS
              IF       @EQUAL
                PACK     KEY8,WBSEHOSP,TCODE,ACODE,SP70
                CALL     RDMLCOD1
                BRANCH   OVRCD,VISBOD10
              ENDIF
            ENDIF
.
            MATCH     ANSO,TCINDC12
            IF        @EQUAL
              MATCH     "1",PTCDDEFT
              IF        @EQUAL
              WRITE     HTMLFILE;"<input type=#"checkbox#" name=#"vscdcode#" ":
                                 "id=#"",ANSC,KEY7,"#" ":
                                 "onclick=#"VisCodToggle('",KEY7,"');#" ":
                                 "value=#"",VISCODTY,TCODE,ACODE,VSCOTEXT,"#" ":
                                 "checked>";
              ELSE
              WRITE     HTMLFILE;"<input type=#"checkbox#" name=#"vscdcode#" ":
                                 "id=#"",ANSC,KEY7,"#" ":
                                 "onclick=#"VisCodToggle('",KEY7,"');#" ":
                                 "value=#"",VISCODTY,TCODE,ACODE,VSCOTEXT,"#">";
              ENDIF
              IF        F1=5
                WRITE     HTMLFILE;TDESC;
              ENDIF
              IF        F1=6
                WRITE     HTMLFILE;PTCDLDES;
              ENDIF
              WRITE     HTMLFILE;"<br>"
.
              MATCH     "1",PTCDDEFT
              IF        @EQUAL
              WRITE     HTMLFILE;"<input type=#"text#" title=#"",TDESC,"#" ":
                                 "name=#"d_vscdcode#" class=#"Mandatory#" ":
                                 "id=#"",ANST,KEY7,"#" ":
                                 "onblur=#"VisCodField('",KEY7,"');#" ":
                                 "value=#"",VSCOTEXT,"#" ":
                                 "size=80 maxlength=200><br> "
              ELSE
              WRITE     HTMLFILE;"<input type=#"text#" title=#"",TDESC,"#" ":
                                 "name=#"d_vscdcode#" disabled ":
                                 "style=#"display:none#" ":
                                 "id=#"",ANST,KEY7,"#" ":
                                 "onblur=#"VisCodField('",KEY7,"');#" ":
                                 "value=#"",VSCOTEXT,"#" ":
                                 "size=80 maxlength=200><br> "
              ENDIF
            ELSE
              MATCH     "1",PTCDDEFT
              IF        @EQUAL
              WRITE     HTMLFILE;"<input type=#"checkbox#" name=#"vscdcode#"":
                                 " value=#"",VISCODTY,TCODE,ACODE,"#" checked>";
              ELSE
              WRITE     HTMLFILE;"<input type=#"checkbox#" name=#"vscdcode#"":
                                 " value=#"",VISCODTY,TCODE,ACODE,"#">";
              ENDIF
              IF        F1=5
                WRITE     HTMLFILE;TDESC;
              ENDIF
              IF        F1=6
                WRITE     HTMLFILE;PTCDLDES;
              ENDIF
              WRITE     HTMLFILE;"<br>"
            ENDIF
          ENDIF
          GOTO      VISBOD10
.
VISBOD99  RETURN
+
.------------------------------------------------------------
. Add Selection options to 
.------------------------------------------------------------
VISSLC00  WRITE     HTMLFILE;"<input type=#"hidden#" name=#"vscdcatg#"":
                             " value=#"",VISCODTY,VISCODCT,"#">"
.
          PACK      KEY5,VISCODCT,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,VISSLC99
.
          PACK      KEY4,VISCODTY,TCODE,SP70
          WRITE     HTMLFILE;"<select name=#"vscdcode#" ":
                             "id=#"",ANSS,KEY4,"#">"
.
          WRITE     HTMLFILE;"<option ":
                             "value=#"",VISCODTY,VISCODCT,SP3,"#">":
                             "</option>";
.
          PACK      PTCDKEY2,VISCODCT,SP70
          CALL      RDSCODE2
VISSLC10  CALL      RDKCODE2
          BRANCH    OVRCD,VISSLC90
.
          MATCH     VISCODCT,TCODE
          GOTO      VISSLC90 IF NOT EQUAL
.
          MATCH     SP70,ACODE
          GOTO      VISSLC10 IF EQUAL
.
          PACK      KEY37,VISCODTY,VISCODKY,TCODE,ACODE,SP70
          CALL      RDVSCOD1
          IF        OVRCD<>1
            WRITE     HTMLFILE;"<option ":
                               "value=#"",VISCODTY,TCODE,ACODE,"#" selected>";
            IF        F1=3
              WRITE     HTMLFILE;TDESC;
            ENDIF
            IF        F1=4
              WRITE     HTMLFILE;PTCDLDES;
            ENDIF
            WRITE     HTMLFILE;"</option>"
          ELSE
            MATCH     "I",PTCOACTV
            GOTO      VISSLC10 IF EQUAL
.
            IF        IBCNMHOS=1
              MATCH    "1",PTCDHOSS
              IF       @EQUAL
                PACK     KEY8,WBSEHOSP,TCODE,ACODE,SP70
                CALL     RDMLCOD1
                BRANCH   OVRCD,VISSLC10
              ENDIF
            ENDIF
.
            WRITE     HTMLFILE;"<option ":
                               "value=#"",VISCODTY,TCODE,ACODE,"#">";
.
            IF        F1=3
              WRITE     HTMLFILE;TDESC;
            ENDIF
            IF        F1=4
              WRITE     HTMLFILE;PTCDLDES;
            ENDIF
            WRITE     HTMLFILE;"</option>"
          ENDIF
          GOTO      VISSLC10
.
VISSLC90  WRITE     HTMLFILE;"</select>"
.
VISSLC99  RETURN
.
.------------------------------------------------------------
. Load link type key according to link type  
. 01 ED visit   = EMVIADMN
. 02 Cancer reg = Key from patcrdaf (set in calling program)
. 03 Pre-Operative Check List = Key from oprpocaf (set in calling program)
.------------------------------------------------------------
LVSK0000  MOVE      ZERO,F2
          MOVE      VISCODTY,F2
.
          LOAD      VISCODKY,F2,EMVIADMN,VISCODKY,VISCODKY,EMVIADMN:
                                EMVIADMN,EMVIADMN
.
          PACK      VISCODKY,VISCODKY,SP70
.
LVSK9999  RETURN
.-------------------------------------------------------------------
. Add check box options with default code and comment input on all
.-------------------------------------------------------------------
.VSCODLDS  FORM      1        Output Long Description         0=No 1=Yes
.VSCODDEF  FORM      1        Output Check Default            0=No 1=Yes
.VSCODCOM  FORM      1        Output All with Comments        0=No 1=Yes
VISCOM00  WRITE     HTMLFILE;"<input type=#"hidden#" name=#"vscdcatg#"":
                             " value=#"",VISCODTY,VISCODCT,"#">"
          PACK      PTCDKEY2,VISCODCT,SP70
          CALL      RDSCODE2
VISCOM10  CALL      RDKCODE2
          BRANCH    OVRCD,VISCOM99
          MATCH     VISCODCT,TCODE
          GOTO      VISCOM99 IF NOT EQUAL
.
          MATCH     SP70,ACODE
          GOTO      VISCOM10 IF EQUAL
.
          PACK      KEY7,VISCODTY,TCODE,ACODE,SP70
          CALL      LVSK0000                         * Load coding key
.
          MOVE      SP70,KEY8                       * Checked - No
          MOVE      "fldHidden",KEY9
          PACK      KEY37,VISCODTY,VISCODKY,TCODE,ACODE,SP70
          CALL      RDVSCOD1 
          IF        OVRCD=0
            MOVE      " checked",KEY8
            MOVE      "Mandatory",KEY9
          ELSE
            CALL      CLVISCOD
            MATCH     "I",PTCOACTV
            GOTO      VISCOM10 IF EQUAL
            IF        IBCNMHOS=1
              MATCH    "1",PTCDHOSS
              IF       @EQUAL
                PACK     KEY8,WBSEHOSP,TCODE,ACODE,SP70
                CALL     RDMLCOD1
                BRANCH   OVRCD,VISCOM10
              ENDIF
            ENDIF
            MATCH     ANSO,TCINDC12
            IF        VSCODDEF=1
              MATCH     "1",PTCDDEFT
              IF        @EQUAL
                MOVE      " checked",KEY8
                MOVE      "Mandatory",KEY9
              ENDIF
            ENDIF
          ENDIF
          WRITE     HTMLFILE;"<input type=#"checkbox#" name=#"vscdcode#" ":
                             "id=#"",ANSC,KEY7,"#" ":
                             "onclick=#"VisCodToggle('",KEY7,"');#" ":
                             "value=#"",VISCODTY,TCODE,ACODE,VSCOTEXT,"#"":
                             KEY8,"><span id=#"cboLabel",KEY7,"#" class=#"cboLabel#">";
          IF        VSCODLDS=0
            WRITE     HTMLFILE;TDESC;
          ELSE 
            WRITE     HTMLFILE;PTCDLDES;
          ENDIF
          WRITE     HTMLFILE;"</span>";
.
          BRANCH    VSCODCOM,VISCOM20
          MATCH     ANSO,TCINDC12
          GOTO      VISCOM20 IF EQUAL
          WRITE     HTMLFILE;"</br>"
          GOTO      VISCOM10
.         
VISCOM20  WRITE     HTMLFILE;"<input type=#"text#" title=#"",TDESC,"#" ":
                             "name=#"d_vscdcode#" class=#"cboTextInput ",KEY9,"#" ":
                             "id=#"",ANST,KEY7,"#" ":
                             "onblur=#"VisCodField('",KEY7,"');#" ":
                             "value=#"",VSCOTEXT,"#" ":
                             "size=30 maxlength=200><br> "
.
          GOTO      VISCOM10
.
VISCOM99  RETURN
+
.------------------------------------------------------------
. Output first category code long description
.------------------------------------------------------------
VISLDS00  PACK      KEY37,VISCODTY,VISCODKY,SP70
          CALL      RSVSCOD1
VISLDS10  CALL      RKVSCOD1
          BRANCH    OVRCD,VISLDS99
.
          MATCH     VISCODTY,VSCOTYPE
          GOTO      VISLDS99 IF NOT EQUAL
.
          MATCH     VISCODKY,VSCOTKEY
          GOTO      VISLDS99 IF NOT EQUAL
. 
          MATCH     SP70,VSCOCATG
          GOTO      VISLDS10 IF EQUAL
.
          MATCH     SP70,VSCOCODE
          GOTO      VISLDS10 IF EQUAL
.
          PACK      KEY5,VSCOCATG,VSCOCODE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,VISLDS10
.
          WRITE     HTMLFILE;PTCDLDES;
.
VISLDS99  RETURN
+
.------------------------------------------------------------
. Add Selection short description with indicators
.------------------------------------------------------------
VISSLI00  WRITE     HTMLFILE;"<input type=#"hidden#" name=#"vscdcatg#"":
                             " value=#"",VISCODTY,VISCODCT,"#">"
.
          PACK      KEY5,VISCODCT,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,VISSLI99
.
          PACK      KEY4,VISCODTY,TCODE,SP70
          WRITE     HTMLFILE;"<select name=#"vscdcode#" ":
                             "id=#"",ANSS,KEY4,"#">"
.
          WRITE     HTMLFILE;"<option ":
                             "value=#"",VISCODTY,VISCODCT,SP3,"#">":
                             "</option>";
.
          PACK      PTCDKEY2,VISCODCT,SP70
          CALL      RDSCODE2
VISSLI10  CALL      RDKCODE2
          BRANCH    OVRCD,VISSLI90
.
          MATCH     VISCODCT,TCODE
          GOTO      VISSLI90 IF NOT EQUAL
.
          MATCH     SP70,ACODE
          GOTO      VISSLI10 IF EQUAL
.
          PACK      KEY37,VISCODTY,VISCODKY,TCODE,ACODE,SP70
          CALL      RDVSCOD1
          IF        OVRCD<>1
            WRITE     HTMLFILE;"<option ":
                               "value=#"",VISCODTY,TCODE,ACODE:
                               TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5:
                               TCINDC6,TCINDC7,TCINDC8,TCINDC9,TCINDC10:
                               TCINDC11,THCSCOD,PTCDNHCP,TCFORM6:
                               TCINDC12,TCINDC13,TCINDC14,TCINDC15,TCINDC16:
                               TCINDC17,TCINDC18,TCINDC19,TCINDC20,TCINDC21:
                               PTCOACTV,PTCDASC1,PTCDASC2,PTCDASC3,PTCDASC4:
                               PTCDASC5,TCINDC22,TCINDC23,TCINDC24,TCINDC25:
                               TCINDC26,TCINDC27,TCINDC28,TCINDC29,TCINDC30:
                               TCINDC31,TCINDC32,TCINDC33,TCINDC34,TCINDC35:
                               TCINDC36,TCINDC37,TCINDC38,TCINDC39,TCINDC40:
                               TCINDC41,PTCDASC6,PTCDASC7,PTCDASC8,PTCDASC9:
                               PTCDAS10,PTCDAS11,PTCDAS12,PTCDAS13,PTCDAS14:
                               PTCDAS15:
                               "#" selected>";
            IF        F1=1
              WRITE     HTMLFILE;TDESC;
            ENDIF
            IF        F1=2
              WRITE     HTMLFILE;PTCDLDES;
            ENDIF
            WRITE     HTMLFILE;"</option>"
          ELSE
            MATCH     "I",PTCOACTV
            GOTO      VISSLI10 IF EQUAL
.
            IF        IBCNMHOS=1
              MATCH    "1",PTCDHOSS
              IF       @EQUAL
                PACK     KEY8,WBSEHOSP,TCODE,ACODE,SP70
                CALL     RDMLCOD1
                BRANCH   OVRCD,VISSLI10
              ENDIF
            ENDIF
.
            WRITE     HTMLFILE;"<option ":
                               "value=#"",VISCODTY,TCODE,ACODE:
                               TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5:
                               TCINDC6,TCINDC7,TCINDC8,TCINDC9,TCINDC10:
                               TCINDC11,THCSCOD,PTCDNHCP,TCFORM6:
                               TCINDC12,TCINDC13,TCINDC14,TCINDC15,TCINDC16:
                               TCINDC17,TCINDC18,TCINDC19,TCINDC20,TCINDC21:
                               PTCOACTV,PTCDASC1,PTCDASC2,PTCDASC3,PTCDASC4:
                               PTCDASC5,TCINDC22,TCINDC23,TCINDC24,TCINDC25:
                               TCINDC26,TCINDC27,TCINDC28,TCINDC29,TCINDC30:
                               TCINDC31,TCINDC32,TCINDC33,TCINDC34,TCINDC35:
                               TCINDC36,TCINDC37,TCINDC38,TCINDC39,TCINDC40:
                               TCINDC41,PTCDASC6,PTCDASC7,PTCDASC8,PTCDASC9:
                               PTCDAS10,PTCDAS11,PTCDAS12,PTCDAS13,PTCDAS14:
                               PTCDAS15:
                               "#">";
.
            IF        F1=1
              WRITE     HTMLFILE;TDESC;
            ENDIF
            IF        F1=2
              WRITE     HTMLFILE;PTCDLDES;
            ENDIF
            WRITE     HTMLFILE;"</option>"
          ENDIF
          GOTO      VISSLI10
.
VISSLI90  WRITE     HTMLFILE;"</select>"
.
VISSLI99  RETURN
