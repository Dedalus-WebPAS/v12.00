.------------------------------------------------------------
. File       : MRTVISCD.PBL
.
. Functions  : This file contains standard routines for
.              adding new records to MRTVISFD        
.
. Routines   : ADDVIS00  - Adds new record to MRTVISFD        
.
. Variables  : PVIBILL - Visit Number
.              PVIURNO - U/R number   
.              PMVXMHOS - Hospital ID
.              IBCNMHOS - Multi hopsital 
.
. Includes   : MRTVISFD
.              MRTVISIO
.------------------------------------------------------------
.         V10.07.01 08/03/2016  Nicole Torrisi TSK 0809293
.                   Added calling CLMRTMAS to ADDVIS20
.         V10.05.01 28/11/2014  Ebon Clements  CAR 306866
.                   Added clear of MRMACOMM before write of new MR
.         V10.03.01 19/06/2013  Ebon Clements  CAR 286907
.                   Check WBSEHHOS and WHSEHLOC are both populated before using
.         V10.02.01 26/05/2011  Mike Laming    CAR 240724
.                   Mods for MRT Hospital/Location - add Hospitals, change Keys
.         V10.00.01 17/09/2010  Ebon Clements  CAR 230239
.                   Check home location and document type when
.                   selecting a record when not multi hospital  - ADDVIS00
.         V9.08.01  25/01/2007  Peter Vela     CAR 127930
.                   Added MRMADTCR MRMATMCR before WRTMAS1  
.         V9.04.02  22/12/2004  Jill Habasque  CAR 54412
.                   Fixed HOSVIS00 - incorrect GOTO 
.         V9.04.01  24/09/2004  Ebon Clements  CAR 53266
.                   Added HOSVIS00 multi hospital processing
.         V9.03.00  23/01/2004  Ebon Clements  CAR 38490 and 46979
.                   Added MRCNACRV auto create medical record for visits
.         V9.02.05  05/07/2002  Pat Dirito              SRF 17952
.                   Now setting Medical Record Status to Parameter.
.         V9.02.04  08/02/2002  Pat Dirito 
.                   Cleaned up code!!!  Lack of RA's causing 
.                   I * D's on Site
.         V9.02.03  14/11/2001  Bronko Sosic 
.                   Set the current location of record to home location
.         V9.02.02  26/10/2001  Jill Habasque
.                   Fixed default medical record volume number
.         V9.02.01  19/10/2001  Jill Habasque
.                   Fixed move of PVIURNO to MRMVURNO
.         V5.10.01  19/04/2001  Bronko Sosic
.                   Removed overcondition on read
.         V5.09.00  03/01/2001  Bronko Sosic
.                   Created Include
.------------------------------------------------------------

ADDVIS00  IF        IBCNMHOS=1
            CALL      HOSVIS00          * Process Multi Hospital            
            GOTO      ADDVIS99
          ENDIF
.
          PACK      KEY8,PVIBILL,SP70
          CALL      RDMRVS1
          IF        OVRCD=0
            GOTO      ADDVIS99
          ENDIF
.
          PACK      KEY20,PVIURNO,Z70
          CALL      RSMRMAS1
ADDVIS10  CALL      RPMRMAS1             
          BRANCH    OVRCD,ADDVIS20
.
          MATCH     PVIURNO,MRMAURNO
          GOTO      ADDVIS20 IF NOT EQUAL
.
          MATCH     MRCNHLOC,MRMAHLOC
          GOTO      ADDVIS10 IF NOT EQUAL
.
          MATCH     MRCNRCTY,MRMADOTY
          GOTO      ADDVIS10 IF NOT EQUAL
.
          MOVE      PVIBILL,MRVSVSNO
          MOVE      MRMAKEY,MRVSMKEY
          MOVE      SP70,MRVSSPAR
.
          PACK      KEY8,PVIBILL,SP70
          CALL      RAMRVS1
          IF        OVRCD=1
            CALL      WRMRVS1
          ENDIF
.
          GOTO      ADDVIS99
.
ADDVIS20  READ      CONTROLF,NINTY7;*153,MRCNACRV
.
          COMPARE   ONE,MRCNACRV                 * Auto create MR for all visits
          GOTO      ADDVIS99 IF NOT EQUAL
.
          CALL      CLMRTMAS
          MOVE      PVIURNO,MRMAURNO
          READ      CONTROLF,SIXTY;*84,MRCNHLOC
          READ      CONTROLF,SIXTY;*88,MRCNRCTY
          READ      CONTROLF,SIXTY;*91,MRCNVOLM
          READ      CONTROLF,SIXTY;*108,MRCNSTAT
          READ      CONTROLF,NINTY7;*154,MRCNUSRD
.
          UNPACK    SP70,MRMAHHOS,MRMACHOS,MRMAOHOS
          MOVE      MRCNHLOC,MRMAHLOC            * Home Location
          MOVE      MRCNHLOC,MRMACLOC            * Current Location
          MOVE      MRCNRCTY,MRMADOTY            * Document Type
          MOVE      MRCNSTAT,MRMASTAT            * Status
          MOVE      SP70,MRMACOMM                * Comments
.
          COMPARE   ONE,MRCNUSRD                 * Using websecaf auto create
          GOTO      ADDVIS25 IF NOT EQUAL        * MR defaults
.
          MATCH     SP70,WBSEDTYP
          IF        !@EQUAL
            MOVE      WBSEDTYP,MRMADOTY
          ENDIF
.
          MATCH     SP70,WBSEHHOS
          IF        !@EQUAL
            MATCH     SP70,WBSEHLOC
            IF        !@EQUAL
              MOVE      WBSEHLOC,MRMAHLOC
              MOVE      WBSEHLOC,MRMACLOC
            ENDIF
          ENDIF
.
ADDVIS25  SQUEEZE   MRCNVOLM
          MOVE      MRCNVOLM,MRMAVOLN
.
          MATCH     SP70,MRMAHLOC
          GOTO      ADDVIS99 IF EQUAL
.
          MATCH     SP70,MRMADOTY
          GOTO      ADDVIS99 IF EQUAL
.
ADDVIS30  READ      CONTROLF,SIXTY;*95,MRCNUKEY
          ADD       ONE,MRCNUKEY
          WRITAB    CONTROLF,SIXTY;*95,MRCNUKEY
          SUB       ONE,MRCNUKEY
          MOVE      MRCNUKEY,MRMAKEY
.
          PACK      KEY10,MRMAKEY,SP70
          CALL      RAMRMAS2                * Read a master file record
          COMPARE   ONE,OVRCD
          GOTO      ADDVIS30 IF NOT EQUAL   * Record exists, get next unq key
.
          PACK      KEY20,MRMAURNO,MRMAHHOS,MRMAHLOC,MRMADOTY,MRMAVOLN,SP70
          CALL      RAMRMAS1                * Read a master file record
          IF        OVRCD=1 
            CALL      IBACLOCK
            PACK      MRMADTCR,CCC,CYY,CMM,CDD
            REP       " 0",MRMADTCR
            MOVE      CTIMEIS,MRMATMCR
            MOVE      WBSEUID,MRMACUID
            MOVE      SP70,MRMADTUP
            MOVE      SP70,MRMATMUP
            MOVE      SP70,MRMAUUID
            CALL      WRMRMAS1
          ENDIF
.           
          MOVE      PVIBILL,MRVSVSNO
          MOVE      MRMAKEY,MRVSMKEY
          MOVE      SP70,MRVSSPAR
.
          PACK      KEY8,PVIBILL,SP70
          CALL      RAMRVS1
          IF        OVRCD=1
            CALL      WRMRVS1
          ENDIF
.
ADDVIS99  RETURN          
.
.------------------------------------------------------------
. Process multi hospital link medical record to visit
.------------------------------------------------------------
HOSVIS00  MATCH     SP70,PMVXMHOS           * Exit if no visit hospital
          GOTO      HOSVIS99 IF EQUAL
.
          PACK      KEY8,PVIBILL,SP70
          CALL      RDMRVS1
          IF        OVRCD=0
            GOTO      HOSVIS99
          ENDIF     
.           
          PACK      KEY3,PMVXMHOS,SP70
          CALL      RDPTHSP1
          BRANCH    OVRCD,HOSVIS99
.
          MATCH     SP70,PTHSHLOC
          GOTO      HOSVIS99 IF EQUAL
.
          MATCH     SP70,PTHSRCTY
          GOTO      HOSVIS99 IF EQUAL
.
          PACK      KEY20,PVIURNO,Z70
          CALL      RSMRMAS1
HOSVIS10  CALL      RPMRMAS1         
          BRANCH    OVRCD,HOSVIS20
.
          MATCH     PVIURNO,MRMAURNO
          GOTO      HOSVIS20 IF NOT EQUAL
.
          MATCH     PTHSHHOS,MRMAHHOS
          GOTO      HOSVIS10 IF NOT EQUAL
.
          MATCH     PTHSHLOC,MRMAHLOC
          GOTO      HOSVIS10 IF NOT EQUAL
.
          MATCH     PTHSRCTY,MRMADOTY
          GOTO      HOSVIS10 IF NOT EQUAL
.
          MOVE      PVIBILL,MRVSVSNO
          MOVE      MRMAKEY,MRVSMKEY
          MOVE      SP70,MRVSSPAR
.
          PACK      KEY8,PVIBILL,SP70
          CALL      RAMRVS1
          IF        OVRCD=1
            CALL      WRMRVS1
          ENDIF
.
          GOTO      HOSVIS99
.
HOSVIS20  READ      CONTROLF,NINTY7;*153,MRCNACRV
.
          COMPARE   ONE,MRCNACRV                 * Auto create MR for all visits
          GOTO      HOSVIS99 IF NOT EQUAL
.
          CALL      CLMRTMAS
          MOVE      PVIURNO,MRMAURNO
          MOVE      PTHSHHOS,MRMAHHOS            * Home Hospital
          MOVE      PTHSHLOC,MRMAHLOC            * Home Location
          MOVE      PTHSHHOS,MRMACHOS            * Current Hospital
          MOVE      PTHSHLOC,MRMACLOC            * Current Location
          MOVE      PTHSHHOS,MRMAOHOS            * Originating Hospital
          MOVE      PTHSRCTY,MRMADOTY            * Document Type
          MOVE      PTHSSTAT,MRMASTAT            * Status
          MOVE      SP70,MRMACOMM                * Comments
.
          READ      CONTROLF,NINTY7;*154,MRCNUSRD
.
          COMPARE   ONE,MRCNUSRD                 * Using websecaf auto create
          GOTO      HOSVIS25 IF NOT EQUAL        * MR defaults
.
          MATCH     SP70,WBSEDTYP
          IF        !@EQUAL
            MOVE      WBSEDTYP,MRMADOTY
          ENDIF
.
          MATCH     SP70,WBSEHHOS
          IF        !@EQUAL
            MATCH     SP70,WBSEHLOC
            IF        !@EQUAL
              MOVE      WBSEHHOS,MRMAHHOS          * Home 
              MOVE      WBSEHLOC,MRMAHLOC
              MOVE      WBSEHHOS,MRMAHHOS          * Current
              MOVE      WBSEHLOC,MRMACLOC
              MOVE      WBSEHHOS,MRMAOHOS          * Originating
            ENDIF
          ENDIF
.
HOSVIS25  SQUEEZE   PTHSVOLM
          MOVE      PTHSVOLM,MRMAVOLN
.
          MATCH     SP70,MRMAHLOC
          GOTO      HOSVIS99 IF EQUAL
.
          MATCH     SP70,MRMADOTY
          GOTO      HOSVIS99 IF EQUAL
.
HOSVIS30  READ      CONTROLF,SIXTY;*95,MRCNUKEY
          ADD       ONE,MRCNUKEY
          WRITAB    CONTROLF,SIXTY;*95,MRCNUKEY
          SUB       ONE,MRCNUKEY
          MOVE      MRCNUKEY,MRMAKEY
.
          PACK      KEY10,MRMAKEY,SP70
          CALL      RAMRMAS2                * Read a master file record
          COMPARE   ONE,OVRCD
          GOTO      HOSVIS30 IF NOT EQUAL   * Record exists, get next unq key
.
          PACK      KEY20,MRMAURNO,MRMAHHOS,MRMAHLOC,MRMADOTY,MRMAVOLN,SP70
          CALL      RAMRMAS1                * Read a master file record
          IF        OVRCD=1
            CALL      IBACLOCK
            PACK      MRMADTCR,CCC,CYY,CMM,CDD
            REP       " 0",MRMADTCR
            MOVE      CTIMEIS,MRMATMCR
            MOVE      WBSEUID,MRMACUID
            CALL      WRMRMAS1
          ENDIF
.
          MOVE      PVIBILL,MRVSVSNO
          MOVE      MRMAKEY,MRVSMKEY
          MOVE      SP70,MRVSSPAR
.
          PACK      KEY8,PVIBILL,SP70
          CALL      RAMRVS1
          IF        OVRCD=1
            CALL      WRMRVS1
          ENDIF
.
HOSVIS99  RETURN
