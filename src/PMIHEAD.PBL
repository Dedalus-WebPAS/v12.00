.*************************************************************************
.* PMIHEAD.PBL   : Include to display the standard 2 line header for a   *
.*                 given PMI record                                      *
.*                 This include is called by the PATHEAD, OUTHEAD and    *
.*                 AAEHEAD routines                                      *
.*                                                                       *
.* PARAMETERS    : A PATMA1FD record                                     *
.*                 ALRTMASK    Alert Mask Details DIM 8; 0=Off  1=On     *
.*                             eg : ALRTMASK = '11111111'  all alerts on *
.*                                                                       *
.*        V11.00.01 17/03/2020  Jill Parkinson Task 0879163              *
.*                  Added SEXDSCIO                                       *
.*        V10.02.02 13/07/2011  Steve Armstrong   CAR 240722             *
.*                  Further mods to cater for second given name as       *
.*                  part of PATGSRFD keys                                *
.*        V10.02.01 22/06/2011  Steve Armstrong   CAR 240722             *
.*                  Mods to cater for changes to PATGSRFD:               *
.*                       - GSRSNAM & GSRGNAM extended to 30 chars.       *
.*                       - PTGSGNM2 added.                               *
.*                       - all indexes changed in length.                *
.*************************************************************************
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2 *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" *
.*                  - "Intersex" added                                   *
.*************************************************************************
.*        V9.01.02  20/11/2001  J.Tan                                    *
.*                  Fixed checking Sex for patgsrnf                      *
.*        V9.01.01  08/09/2001  Sandra Barcham                           *
.*                  Added DOB & SEX TO patgsrnf                        . *
.*************************************************************************
.*        V5.10.B01 28/03/2001  Glenn Saunders                           *
.*                  Remove access to PTCNSNDX and associated processing. *
.*                  Remove all references to PATSUR file (old surname).  *
.*************************************************************************
.*        V5.08.01  21/08/2000  Tonii                                    *
.*                  - Mods to accept "U" & "I" for PSEX, "Unknown" and   *
.*                    "Indeter" respectively                             *
.*************************************************************************
.*                  08/10/1998  Glenn Berry      5.07 changes            *
.*************************************************************************
.*        V5.06.01  19/07/1998    Steve Armstrong   SRF 626595           *
.*                  Mods to ignore alias check if u/r is zero            *
.*************************************************************************
.*        V5.03.02  15/02/1996    Justin Coates  (SUHT mods)             *
.*                  added update of last U/R file                        *
.*        V5.03.01  20/11/1995    Justin Coates  (SUHT mods)             *
.*                  included CONPURAL for the SYSHEAD routines           *
.*        V5.03.B0  26/05/1995  Paul Howells      SRF 134643             *
.*                  If patient deceased set AGEDATE to date of death.    *
.*************************************************************************
.*        V5.02.01  09/01/1995  Graeme Williams                          *
.*                  Fixed getting of national number for 0 U/R numbers   *
.*                  It was reading the NID file, not the PNI file        *
.*************************************************************************
.
. ----- format patient name -----
.
PMIHEAD   
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TEN9;*75,CCNOTES
          READ      CONTROLF,SEVENTY7;*201,PTCNIDHD
          READ      CONTROLF,TWENTY;*225,PTCNNMDX
          READ      CONTROLF,EIGHTY;*229,PTCNDATE
.
.
          PACK      PATHDNAM WITH SP30,SP30
          MOVE      SP10,PATHDDOB
          MOVE      SP10,PATHDSEX
.
PMIHD100  MOVE      PSNAME,PATHDNAM
          ENDSET    PATHDNAM
          CALL      PMIHD150                        * get rid of spaces
          APPEND    ", ",PATHDNAM
          APPEND    PTITL,PATHDNAM
          CALL      PMIHD150                        * get rid of spaces
          APPEND    SP1,PATHDNAM
          APPEND    PGNAME,PATHDNAM
.
          PROC      CHKALIAS                        * check for aliases
.
          MATCH     SP4,KEY4
          IF        !@EQUAL
            CALL      PMIHD150                      * get rid of spaces
            LENSET    PATHDNAM
            RESET     PATHDNAM
            MOVE      PATHDNAM,KEY42
            PACK      PATHDNAM,KEY42,KEY4,SP20,SP20,SP20
          ENDIF
          RESET     PATHDNAM
          GOTO      PMIHD200
.
PMIHD150  CMATCH    SP1,PATHDNAM                      * get rid of spaces
          RETURN    IF NOT EQUAL                    * in patient name
          BUMP      PATHDNAM BY -1
          RETURN    IF EOS
          GOTO      PMIHD150
.
. ----- format dob and sex description -----
.
PMIHD200  UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,PATHDDOB
.
.  ----- If PTCNDATE = 1 we need to convert patient age to one of
.  ----- day, week, month, or year.  Calcage will calcuate the age
.  ----- in the variable (PSAGECON) and what the age is recorded as in 
.  ----- (PSAGETYP)
.
            CALL      IBACLOCK
.
. --------- check if patient is dead ----------------------------
.
            IF        PCEASE=1
              MOVE      PDECDTE,AGEDATE            * Date of Death
            ELSE
              PACK      AGEDATE,CCC,CYY,CMM,CDD    * today's date
            ENDIF
            REP       " 0",AGEDATE
.
            CALL      CALCAGE
            MOVE      PSAGEYRS,PSAGE
.
          IF        PTCNDATE=1
....        CALL      IBACLOCK
....        PACK      AGEDATE,CCC,CYY,CMM,CDD  
....        CALL      CALCAGE
            CALL      CONVRT00   
          ENDIF
.
PMIHD280  MOVE      SP8,PATHDSEX
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,PATHDSEX
.* ******************************************   End of Changes         *C-49016
.
.         Set up case notes indicator
.
PMIHD700  MOVE      SP1,ANS
.
          IF        PTCNIDHD=1
            PROC      NATIDN00
.
PMIHD750    MATCH     SP1,KEY20
            GOTO      PMIHD850 IF NOT EQUAL            
PMIHD800    BUMP      KEY20
            GOTO      PMIHD850 IF EOS
            MATCH     SP1,KEY20
            GOTO      PMIHD800 IF EQUAL            
.          
PMIHD850    MOVE      KEY20,KEY21
            MOVE      KEY21,KEY20
            RESET     KEY20
            STRIP     KEY20
            DISPLAY   *P1:3,*EF:
                      *P1:4,"U/R Number   : ",*V2LON,PURNO,ANS,*HOFF:
                      *P26:4,"Name ",*V2LON,PATHDNAM,*HOFF:
                      *P68:4," Sex : ",*V2LON,PATHDSEX,*HOFF:
                      *P26:5,PTCNNMDX," : ",*V2LON,KEY20,*HOFF
            IF        PTCNDATE=1
              DISPLAY   *P59:5,"DOB:",*V2LON,PATHDDOB,*HOFF:
                        *P74:5,*V2LON,PSAGECON,*HOFF," ",PSAGETYP
            ELSE
              DISPLAY   *P59:5,"DOB : ",*V2LON,PATHDDOB,*HOFF:
                        *P76:5,*V2LON,"(",PSAGE,")"
            ENDIF

          ELSE
            DISPLAY   *P1:3,*EF:
                      *P1:4,"U/R Number   : ",*V2LON,PURNO,ANS,*HOFF:
                      *P26:4,"Name : ",*V2LON,PATHDNAM,*HOFF:
                      *P26:5,"Sex  : ",*V2LON,PATHDSEX,*HOFF:
                      *P43:5,"Born : ",*V2LON,PATHDDOB,*HOFF
            IF        PTCNDATE=1
              DISPLAY   *P61:5,"Age : ",*V2LON,PSAGECON," ",*HOFF,PSAGETYP
            ELSE 
              DISPLAY   *P61:5,"Age : ",*V2LON,PSAGE
            ENDIF
          ENDIF
.
          PROC      PHWLUE00                * update last U/R file
          CALL      PATALERT                * display alerts
.
PMIHD999  RETURN
.
.******************************************************************************
. CONVRT00 : Moves the appropriate age description to age PSAGETYP.  When 
.            using the National number in the header there is only room for   
.            a three character description, otherwise a full description is used
.*******************************************************************************
CONVRT00  MATCH     "d",PSAGETYP 
          GOTO      CONVRT20 IF EQUAL
.
          MATCH     "w",PSAGETYP
          GOTO      CONVRT30 IF EQUAL
.
          MATCH     "m",PSAGETYP
          GOTO      CONVRT40 IF EQUAL
.
          MATCH     "y",PSAGETYP
          GOTO      CONVRT50 IF EQUAL
.
CONVRT20  IF        PTCNIDHD=1
            MOVE      "Dys",PSAGETYP
          ELSE
            MOVE      "Days",PSAGETYP
          ENDIF
          GOTO      CONVRT99
.
CONVRT30  IF        PTCNIDHD=1
            MOVE      "Wks",PSAGETYP
          ELSE
            MOVE      "Weeks",PSAGETYP
          ENDIF
          GOTO      CONVRT99
.
CONVRT40  IF        PTCNIDHD=1
            MOVE      "Mth",PSAGETYP
          ELSE
            MOVE      "Months",PSAGETYP
          ENDIF
          GOTO      CONVRT99
.
CONVRT50  IF        PTCNIDHD=1
            MOVE      "Yrs",PSAGETYP
          ELSE
            MOVE      "Years",PSAGETYP
          ENDIF
.
CONVRT99  RETURN
.****************************************************************************

          DEFPROC   NATIDN00
.
          INC       PATNIDFD/INC
          INC       PATPNIFD/INC
.
          OPEN      PATNIDA1,"patnidaf"
          OPEN      PATPNIA1,"patpniaf"
          MOVE      SP20,KEY20                   * clear field
          PACK      KEY10,CHOSINDX,PURNO,SP10    * get PTNINMPI
          CALL      RDPTNID1
          CLOSE     PATNIDA1
          BRANCH    OVRCD,NATIDN50
.
          MOVE      PTNINMPI,KEY20
          GOTO      NATIDN99
.
NATIDN50  PACK      KEY10,CHOSINDX,AADMNO,SP10    * get PTNINMPI
          CALL      RDPTPNI1
          CLOSE     PATPNIA1
          BRANCH    OVRCD,NATIDN99 
.
          MOVE      PTNINMPI,KEY20
          GOTO      NATIDN99
.
          INC       IBAOVRIO/INC
          INC       PATNIDIO/INC
          INC       PATPNIIO/INC
.
NATIDN99  ENDPROC
.
.*********************************************************************
.         write the last U/R entered for this operator
.*********************************************************************
          DEFPROC   PHWLUE00
.
          INC       PATLUEFD/INC
.
PTCNULUE  FORM      1
.
WLUE0000  READ      CONTROLF,SEVENTY7;*10,PTCNULUE
          COMPARE   ONE,PTCNULUE
          GOTO      WLUE9999 IF NOT EQUAL   * not using this feature
.
          OPEN      PATLUEA1,"patlueaf"
          MOVE      SP8,PTLUURNO  
          MOVE      PASSCODE,KEY4
          CALL      RDPTLUE1
.
          MATCH     PURNO,PTLUURNO
          GOTO      WLUE9999 IF EQUAL       * UR number hasnt changed
.
          MOVE      PASSCODE,PTLUOPER
          MOVE      PURNO,PTLUURNO
.
          IF        OVRCD = ZERO
            CALL      UPPTLUE1
          ELSE
            CALL      WRPTLUE1
          ENDIF
          MOVE      ZERO,OVRCD
          GOTO      WLUE9999
.
          INC       IBAOVRIO/INC
          INC       PATLUEIO/INC
.
WLUE9999  ENDPROC
+
.******************************************************************************
.                               SEXDSC00
.                         get sex description
.         Requires DSEXCHAR as input
.         Returns  DSEXDESC    8 character description
.                  DSEXNO      Form 1 field for 1=M 2=F 3=I 4=R 9=U
.******************************************************************************
.
SEXDSC00  MOVE      NINE,DSEXNO             * default 9
.
          PACK      KEY5,ANSG,SP1,DSEXCHAR,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,SEXDSC90
.
          MOVE      ZERO,F1
          MOVE      TCINDC1,F1
          MOVE      F1,DSEXNO
.
          PACK      DSEXDESC,TDESC,SP70
          GOTO      SEXDSC99
.
SEXDSC90  MOVE      "Invalid Sex",DSEXDESC
.
SEXDSC99  RETURN
+
.*********************************************************************
.         Check for Aliases for the patient              
.*********************************************************************
          DEFPROC   CHKALIAS
.
          INC       PATGSRFD/INC
.
LEFTBRK   INIT      "("
RIGHTBRK  INIT      ")"
.
CHKALI00  MATCH     "       0",PURNO             * if zero ur - dont check alias
          GOTO      CHKALI90 IF EQUAL
.
. ------- Check for aliases on new surname file -------------------------
.
CHKALI50  OPEN      PATGSRN1,"patgsrnf"
          PACK      KEY115,PURNO,SP70,SP70
          CALL      RSPTGSR1
CHKALI60  CALL      RKPTGSR1
          BRANCH    OVRCD,CHKALI90
.
          MATCH     GSRURNO,PURNO
          GOTO      CHKALI90 IF NOT EQUAL
.
          MATCH     GSRSNAM,PTMASNAM
          GOTO      CHKALI80 IF NOT EQUAL
.
          MATCH     GSRGNAM,PMPXFNAM
          GOTO      CHKALI80 IF NOT EQUAL
.
          MATCH     PTGSGNM2,PMPXSNAM
          GOTO      CHKALI80 IF NOT EQUAL
.
          MATCH     GSRDOB,PBDATE
          GOTO      CHKALI80 IF NOT EQUAL
.
          MATCH     GSRSEX,PSEX
          GOTO      CHKALI60 IF EQUAL
.
CHKALI80  PACK      KEY4,SP1,LEFTBRK,ANSA,RIGHTBRK
          GOTO      CHKALI99
.
CHKALI90  MOVE      SP4,KEY4
.
CHKALI99  GOTO      ENDALIAS
.
          INC       IBAOVRIO/INC
          INC       PATGSRIO/INC
.
ENDALIAS  ENDPROC
