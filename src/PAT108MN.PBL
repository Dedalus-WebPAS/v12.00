.******************************************************************************
.*  File          :  PAT108MN.PBL                                             *
.*                                                                            *
.*  Function      :  ICD10 Ed.8 Coding Maintenance Routines for PATWEB88      *
.*                                                                            *
.*  Parameters    :                                                           *
.*                                                                            *
.*  Modifications :                                                           *
.*                                                                            *
.        V11.02.01  19/07/2022  Bella Turco    TSK 0921957                    *
.                   Added NOMORE00 - Display "No more records" at end of list *
.                   Added PTCNNRTD - Displays number of rows set by parameter *
.                   value                                                     *
.        V10.04.01  13/02/2014  Patrick Adair   CAR 273419                    *
.                   Amended to use PT0ODSC2 and PT0DDSC2                      *
.        V10.03.00  23/04/2013  Davin           CAR 284420   HDP 2013/2014    *
.                   Created - cloned from PAT107MN.PBL                        *
.******************************************************************************
.
.------------------------------------------------------------------------------
. ICD10 Edition 8 Diagnosis Coding Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------------------------
ICD8DI00  RESET     PTICD001
          MOVE      "8",ICD10EDN
.                                           * make sure files are open
          OPEN      PATI18D1,"pat10d8f"     * Open ICD10 Ed.8 Diseases
          OPEN      PATK8DA1,"patkcd8f"     * ICD10 Ed.8 Key Word Diag.
          OPEN      PATK8DA2,"patkcd8f"     * ICD10 Ed.8 Key Word Diag.
.
          RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      ICD8DI40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      ICD8DI10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      ICD8DI20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      ICD8DI30 IF EQUAL
.
          MATCH     "V",UPDTTYPE    * Validate Formaction
          IF        @EQUAL
            CALL      VAL8CD00
            GOTO      ICD8DI99
          ENDIF
.
          GOTO      ICD8DI93
.
ICD8DI10  CALL      CHKDIS00  * Checks mandatory fields & checks for blank
          BRANCH    EXIT,ICD8DI99
          PACK      KEY9,PTICD001,SP70  * ADD FORMACTION
          CALL      RDPT18D1
          COMPARE   ZERO,OVRCD
          GOTO      ICD8DI92 IF EQUAL
          CALL      DISSAV00      * Moves input variables to file variables
          CALL      WRPT18D1      * Writes away the data
          PROC      PATK8DUP      * Update Keyword table
.
          CALL      ADDCAN00      * Add Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICD8DI99
.
ICD8DI20  PACK      KEY9,PTICD001,SP70  * UPDATE FORMACTION
          CALL      RDPT18D1
          BRANCH    OVRCD,ICD8DI91
          CALL      CHKDIS00      * Checks mandatory fields
          BRANCH    EXIT,ICD8DI99
          CALL      DISSAV00      * Moves input variables to file variables
          CALL      UPPT18D1      * Writes away the data
          PROC      PATK8DUP      * Update Key word Table
.
          CALL      UPDCAN00      * Update Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICD8DI99
.
ICD8DI30  PACK      KEY9,PTICD001,SP70   * DELETE FORMACTION
          CALL      RDPT18D1
          BRANCH    OVRCD,ICD8DI91
          CALL      DEPT18D1      * Deletes the data
          PROC      PATK8DUP      * Update Key word Table
.
          CALL      DELCAN00      * Delete Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICD8DI99
.
ICD8DI40  PACK      KEY9,PTICD001,SP70
          CALL      RDPT18D1
          IF        OVRCD=1
            CALL      CLRDIS00    * Clear all the file variables
          ENDIF
.
          MOVE      ONE,CANCERCD
          PACK      KEY9,PTICD001,SP70
          CALL      RDPTCAN1
          IF        OVRCD=1
            MOVE     ZERO,CANCERCD
            MOVE     ZERO,PTCCLATR
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "ICD10 Edition 8 Diagnosis Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ICD8DI99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      ICD8DI99
.
ICD8DI91  MOVE      "ICD10 Edition 8 Diagnosis Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD8DI99
.
ICD8DI92  MOVE      "ICD10 Edition 8 Diagnosis Code exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD8DI99
.
ICD8DI93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD8DI99
.
ICD8DI99  RETURN
+
.------------------------------------------------------------------------------
. ICD10 Edition 8 Operation Coding Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------------------------
ICD8OP00  RESET     PTICO001
          MOVE      "8",ICD10EDN
.
          OPEN      PATI18O1,"pat10o8f"     * Open ICD10 Ed.8 Operations
          OPEN      PATK8OA1,"patkco8f"     * ICD10 Ed.8 Key Word Proc.
          OPEN      PATK8OA2,"patkco8f"     * ICD10 Ed.8 Key Word Proc.
.
          RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      ICD8OP40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      ICD8OP10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      ICD8OP20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      ICD8OP30 IF EQUAL
.
          MATCH     "V",UPDTTYPE    * Validate formaction
          IF        @EQUAL
            CALL      VAL8CD00
            GOTO      ICD8OP99
          ENDIF
.
          GOTO      ICD8OP93
.
ICD8OP10  CALL      CHKICD00  * Checks mandatory fields & checks for blank
          BRANCH    EXIT,ICD8OP99
          PACK      KEY9,PTICO001,SP70  * ADD FORMACTION
          CALL      RDPT18O1
          COMPARE   ZERO,OVRCD
          GOTO      ICD8OP92 IF EQUAL
          CALL      ICDSAV00      * Moves input variables to file variables
          CALL      WRPT18O1      * Writes away the data
          PROC      PATK8OUP      * Update Keyword table
          MOVE      ZERO,EXIT
          GOTO      ICD8OP99
.
ICD8OP20  PACK      KEY9,PTICO001,SP70  * UPDATE FORMACTION
          CALL      RDPT18O1
          BRANCH    OVRCD,ICD8OP91
          CALL      CHKICD00      * Checks mandatory fields
          BRANCH    EXIT,ICD8OP99
          CALL      ICDSAV00      * Moves input variables to file variables
          CALL      UPPT18O1      * Writes away the data
          PROC      PATK8OUP      * Update Key word Table
          MOVE      ZERO,EXIT
          GOTO      ICD8OP99
.
ICD8OP30  PACK      KEY9,PTICO001,SP70   * DELETE FORMACTION
          CALL      RDPT18O1
          BRANCH    OVRCD,ICD8OP91
          CALL      DEPT18O1      * Deletes the data
          PROC      PATK8OUP      * Update Key word Table
          MOVE      ZERO,EXIT
          GOTO      ICD8OP99
.
ICD8OP40  PACK      KEY9,PTICO001,SP70
          CALL      RDPT18O1
          IF        OVRCD=1
            CALL      CLRICD00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "ICD10 Edition 8 Procedure Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ICD8OP99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      ICD8OP99
.
ICD8OP91  MOVE      "ICD10 Edition 8 Procedure Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD8OP99
.
ICD8OP92  MOVE      "ICD10 Edition 8 Procedure Code exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD8OP99
.
ICD8OP93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD8OP99
.
ICD8OP99  RETURN
+
.------------------------------------------------------------------------------
. Online ICD10 Edition 8 Code Validation
.------------------------------------------------------------------------------
VAL8CD00  MOVE      SP70,VALDNAME
          MATCH     SP70,VALDCODE
          GOTO      VAL8CD90 IF EQUAL
.
          MOVE      "Invalid Code Entered",VALDNAME
          BRANCH    VALDTYPE,VAL8CD10,VAL8CD20
          MOVE      "Invalid Validation Type",VALDNAME
          GOTO      VAL8CD90
.
VAL8CD10  MOVE      VALDCODE,KEY9
          CALL      RDPT18O1
          IF        OVRCD=0
            MOVE      PT0ODSC2,VALDNAME
          ENDIF
          GOTO      VAL8CD90
.
VAL8CD20  MOVE      VALDCODE,KEY9
          CALL      RDPT18D1
          IF        OVRCD=0
            MOVE      PT0DDSC2,VALDNAME
          ENDIF
          GOTO      VAL8CD90
.
VAL8CD90  CALL      OPENHTML
          BRANCH    EXIT,VAL8CD99
          WRITE     HTMLFILE;"<h3 align=center>Checking Database</h3>":
                             "<script type=#"text/javascript#">":
                             "{ opener.ValidateName.value=#"",VALDNAME,"#";":
                             "  opener.ValidateCode.value=#"",VALDCODE,"#";"
.
          MATCH     "Invalid",VALDNAME
          IF        @EQUAL
            WRITE     HTMLFILE;"  alert(#"Invalid Code Entered#");";
          ENDIF
.
          WRITE     HTMLFILE;"  close(); } </script>"
          CALL      CLOSHTML
.
VAL8CD99  RETURN
+
.-----------------------------------------------------------
. ICD10 Edition 8 Procedure Code: Search Table
.-----------------------------------------------------------
SPT8OA00  WRITE     HTMLFILE;"<tr align=center>":
                             "<td class=HeadingCell colspan=2>":
                             "<b>You Searched for ":
                               KEYWORDS,"</td>":
                             "</tr>":
                             "<tr>":
                             "<td class=HeadingCell width=15%><b>Code</b></td>":
                             "<td class=HeadingCell><b>Description</b></td>":
                             "</tr>"
.
          CALL      GETWRD00
.
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
....      UNPACK    PTCNGDV8,KEY4,KEY5
....      MATCH     "9999",KEY4          * is ICD10 Ed.8 in use yet?
....      GOTO      SPT8OA90 IF EQUAL
.
          STRIP     KEYWRD01
          PACK      KEY24,KEYWRD01,SP70
          CALL      RSPT8O2
SPT8OA10  CALL      RKPT8O2
          BRANCH    OVRCD,SPT8OA90
.
          CALL      SPT8OB00
          BRANCH    OVRCD,SPT8OA10,SPT8OA90
.
          MOVE      PTCOITM,KEY9
          CALL      RDPT18O1
          BRANCH    OVRCD,SPT8OA10
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      SPT8OA10
          ENDIF
.
          BRANCH    LNK2PRT,SPT8OA20,SPT8OA30
          GOTO      SPT8OA99
.
SPT8OA20  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:showICD02":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&ptico001=",OPCODE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             OPCODE,"</td>":
                             "<td>",PT0ODSC2,"</td>":
                             "</tr>"
.
          GOTO      SPT8OA80
.
SPT8OA30  WRITE     HTMLFILE;"<tr valign=top>":
                              "<td><a href=#"#" ":
                              "OnClick=#"updateParent('",PT0ODSC2:
                              "','",OPCODE,"')#">":
                              "<img src=../images/MaintenanceIcon.gif hspace=4":
                              " border=0 align=absmiddle></a>":
                              OPCODE,"</td>":
                              "<td>",PT0ODSC2,"</td>":
                             "</tr>"
.
SPT8OA80  ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      SPT8OA10 IF NOT EQUAL
          GOTO      SPT8OA99
.
SPT8OA90  CALL      NOMORE00        * Display No More Records Message
.
SPT8OA99  RETURN
.------------------------------------------------------------
. Check ICD10 Edition 8 Procedure Record
.------------------------------------------------------------
SPT8OB00  MATCH     KEYWRD01,PTCOKWD
          GOTO      SPT8OB92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY24,PTCOKWD,PTCOITM
            CALL      SPT8OC00
            BRANCH    EXIT,SPT8OB91
            MOVE      SAVKEY24,KEY24
            CALL      RDPT8O2
          ENDIF
.
. Insert Other Check for Valid Record Here i.e. Suspended or In Active
.   eg    COMPARE    ONE,INACTIVE
.         GOTO       SPT8OB91 IF EQUAL
.
          GOTO      SPT8OB90
.
SPT8OB90  MOVE      ZERO,OVRCD
          GOTO      SPT8OB99
SPT8OB91  MOVE      ONE,OVRCD
          GOTO      SPT8OB99
SPT8OB92  MOVE      TWO,OVRCD
SPT8OB99  RETURN
.------------------------------------------------------------
. Check for Matching ICD10 Edition 8 Procedure Words
.------------------------------------------------------------
SPT8OC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY9,PTCOITM,SP70
.
SPT8OC10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      SPT8OC99 IF EQUAL     * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY24,PTCOITM,KEYWRDXX
          CALL      RDPT8O1               * Check for Exact Match
          COMPARE   ZERO,OVRCD            * Ok so Continue Loop
          GOTO      SPT8OC10 IF EQUAL
.
          CALL      RKPT8O1               * Not Exact so Read Next Record
          BRANCH    OVRCD,SPT8OC90        * No Match So Exit No Match
          PACK      KEY9,PTCOITM,SP70
          MATCH     SAVKEY9,KEY9        * Match Key Fields
          GOTO      SPT8OC90 IF NOT EQUAL * No Match So Exit
          STRIP     KEYWRDXX
          MATCH     PTCOKWD,KEYWRDXX         * Check Key Word
          GOTO      SPT8OC90 IF NOT EQUAL * No Match So Exit
          GOTO      SPT8OC10              * Check Next Search Word
.
SPT8OC90  MOVE      ONE,EXIT
SPT8OC99  RETURN
+
.-----------------------------------------------------------
. ICD10 Edition 8 Diagnosis Code: Search Table
.-----------------------------------------------------------
SPT8DA00  WRITE     HTMLFILE;"<tr align=center>":
                             "<td class=HeadingCell colspan=2>":
                             "<b>You Searched for ":
                               KEYWORDS,"</td></tr><tr>":
                               "<td class=HeadingCell width=15%><b>Code</b>":
                               "</td>":
                               "<td class=HeadingCell><b>Description</b></td>":
                             "</tr>"
.
          CALL      GETWRD00
.
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
....      UNPACK    PTCNGDV8,KEY4,KEY5
....      MATCH     "9999",KEY4          * is ICD10 Ed.8 in use yet?
....      GOTO      SPT8DA90 IF EQUAL
.
          STRIP     KEYWRD01
          PACK      KEY24,KEYWRD01,SP70
          CALL      RSPT8D2
SPT8DA10  CALL      RKPT8D2
          BRANCH    OVRCD,SPT8DA90
.
          CALL      SPT8DB00
          BRANCH    OVRCD,SPT8DA10,SPT8DA90
.
          MOVE      PTCDITM,KEY9
          CALL      RDPT18D1
          BRANCH    OVRCD,SPT8DA10
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      SPT8DA10
          ENDIF
.
          BRANCH    LNK2PRT,SPT8DA20,SPT8DA30
          GOTO      SPT8DA99
.
SPT8DA20  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:showICD03":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&pticd001=",DPCODE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             DPCODE,"</td>":
                             "<td>",PT0DDSC2,"</td>":
                             "</tr>"
.
          GOTO      SPT8DA80
.
SPT8DA30  WRITE     HTMLFILE;"<tr valign=top>":
                              "<td><a href=#"#" ":
                              "OnClick=#"updateParent('",PT0DDSC2:
                              "','",DPCODE,"')#">":
                              "<img src=../images/MaintenanceIcon.gif hspace=4":
                              " border=0 align=absmiddle></a>":
                              DPCODE,"</td>":
                              "<td>",PT0DDSC2,"</td>":
                             "</tr>"
.
SPT8DA80  ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      SPT8DA10 IF NOT EQUAL
          GOTO      SPT8DA99
.
SPT8DA90  CALL      NOMORE00           * Display No More Records Message
.
SPT8DA99  RETURN
.------------------------------------------------------------
. Check ICD10 Edition 8 Diagnosis Record
.------------------------------------------------------------
SPT8DB00  MATCH     KEYWRD01,PTCDKWD
          GOTO      SPT8DB92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY24,PTCDKWD,PTCDITM
            CALL      SPT8DC00
            BRANCH    EXIT,SPT8DB91
            MOVE      SAVKEY24,KEY24
            CALL      RDPT8D2
          ENDIF
.
. Insert Other Check for Valid Record Here i.e. Suspended or In Active
.   eg    COMPARE    ONE,INACTIVE
.         GOTO       SPT8DB91 IF EQUAL
.
          GOTO      SPT8DB90
.
SPT8DB90  MOVE      ZERO,OVRCD
          GOTO      SPT8DB99
SPT8DB91  MOVE      ONE,OVRCD
          GOTO      SPT8DB99
SPT8DB92  MOVE      TWO,OVRCD
SPT8DB99  RETURN
.------------------------------------------------------------
. Check for Matching ICD10 Edition 8 Diagnosis Words
.------------------------------------------------------------
SPT8DC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY9,PTCDITM,SP70
.
SPT8DC10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      SPT8DC99 IF EQUAL     * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY24,PTCDITM,KEYWRDXX
          CALL      RDPT8D1               * Check for Exact Match
          COMPARE   ZERO,OVRCD            * Ok so Continue Loop
          GOTO      SPT8DC10 IF EQUAL
.
          CALL      RKPT8D1               * Not Exact so Read Next Record
          BRANCH    OVRCD,SPT8DC90        * No Match So Exit No Match
          PACK      KEY9,PTCDITM,SP70
          MATCH     SAVKEY9,KEY9        * Match Key Fields
          GOTO      SPT8DC90 IF NOT EQUAL * No Match So Exit
          STRIP     KEYWRDXX
          MATCH     PTCDKWD,KEYWRDXX         * Check Key Word
          GOTO      SPT8DC90 IF NOT EQUAL * No Match So Exit
          GOTO      SPT8DC10              * Check Next Search Word
.
SPT8DC90  MOVE      ONE,EXIT
SPT8DC99  RETURN
