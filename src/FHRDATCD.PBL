.------------------------------------------------------------
. Output Fire Resources Patient and Encounter
.------------------------------------------------------------
. Modifications :
. V11.04.01 11/07/2024   Peter Vela     TSK WEBPAS / ORBIS UI R&D
.           Added Patient Resource properties:
.           communication property
.           countryofbirth extension property
.           indigenousstatus extension property
.           resident extension property
. V11.03.06 01/08/2023   Peter Vela     TSK 0927262
.           Fixed location display heirarchy
. V11.03.05 21.07.2023   Jacob Jackson  TSK 0927731
.           Remove Unknown / N/A location tag
. V11.03.04 11.07.2023   DA Sarkies     TSK 0927398
.           Changed Array '[]' to object '{}' for meta tag
. V11.03.03 22/06/2023 Jacob Jackson    TSK 0927731
.           Add changes so that FHIR Response contains a location object
.           for Ward and an object for Bed
.           14/06/2023 Sunny            TSK 0927398
.           Removed lastUpdated (PTMXCTIM)
. V11.03.02 29.05.2023   DA Sarkies     TSK 0927398
.           Added JSON meta tag for FHIR and also tag for celebrity status
. V11.03.01 23.05.2023   Peter Vela     TSK 0927399
.           Changed gender validation to Indicator 7
. V10.13.02 25.01.2019   B.G.Ackland    TSK 0835482
.           Supress output of reason if blank
. V10.13.01 12.11.2018   B.G.Ackland    TSK 0835482
.           Fix Cardinality of reason in Encounter Resource
.------------------------------------------------------------
. V10.11.01 28.08.2017  B.G.Ackland    TSK 0835482 
.           FHIR Output Change to Patient Reference ID to strip spaces
.           FHIR Change Extension Naming
.------------------------------------------------------------
. V10.10.02 11.07.2017   B.G.Ackland    TSK 0835482
.           Various Updates for Testing with ClinicalAide
. V10.10.01 08/05/2017   B.G.Ackland    TSK 0835482
.           Various Updates
. V10.10.00 05/05/2017   B.G.Ackland    TSK 0835482
.           Created include
.------------------------------------------------------------
. FHIR Encounter
.------------------------------------------------------------
RESENC00  REP      " -",FHRENCID *  Encounter ID
          SQUEEZE  FHRPATID      *  Patient Code
          PACK     FHRREAS,FHRREAS,SP70
          MATCH    SP70,FHRREAS
          IF       @EQUAL
            MOVE     SP70,FHRREAS
          ENDIF
          STRIP    FHRSTAT  *  Encounter Status
          STRIP    FHRCLAS  *  Encounter Class
          STRIP    FHRPATN  *  Patient Display Name
          STRIP    FHRSTRDT *  Period Start Date
          STRIP    FHRREAS  *  Reason
          STRIP    WBTPFIL  *  Template
          UNPACK   FHRENCID,DIM8,KEY8
          REP      "- ",KEY8
          SQUEEZE  KEY8
.
          WRITE    HTMLFILE;*+,"{ #"resourceType#": #"Encounter#",":
                    " #"id#" : #"",FHRENCID,"#",":
                    " #"identifier#": [ { #"value#": #"",KEY8,"#" } ],":
                    " #"extension#": [":
                    " { #"url#":#"%BASEURL/extension/DebugVisitType#"":
                        " ,#"valueString#":#"",PVITYPE,"#"},":
                    " { #"url#":#"%BASEURL/extension/webPASService#"":
                        " ,#"valueString#":#"",PRGID,SP1,PRGNAM,SP1,VERSION,"#"},":
                    " { #"url#":#"%BASEURL/extension/webPASTemplate#"":
                        " ,#"valueString#":#"%TEMPLATEHOME/",WBTPFIL," %TEMPLATEVERSION#"}";
          MATCH     SP70,FHRVISA
          IF        !@EQUAL
            WRITE     HTMLFILE;*+,",{#"url#":#"%BASEURL/extension/dxc-hc-visitorsallowed#"":
                                  " ,#"valueBoolean#": ",FHRVISA," }";
          ENDIF
          MATCH     SP70,FHRPHNA
          IF        !@EQUAL
            WRITE     HTMLFILE;*+,",{#"url#":#"%BASEURL/extension/dxc-hc-callsallowed#"":
                                  " ,#"valueBoolean#": ",FHRPHNA," }";
          ENDIF
          WRITE    HTMLFILE;*+," ],":
                    " #"status#" : #"",FHRSTAT,"#",":
                    " #"class#"  : { #"code#" : #"",FHRCLAS,"#" },":
                    " #"period#" : { #"start#": #"",FHRSTRDT,"#"";
          MATCH     SP70,FHRENDDT
          IF        !@EQUAL 
            STRIP     FHRENDDT *  Period End Date
            WRITE     HTMLFILE;*+,", #"end#": #"",FHRENDDT,"#"";
          ENDIF
          WRITE    HTMLFILE;*+,"},":
                    "  #"subject#": { #"reference#" : #"Patient/",FHRPATID,"#", ":
                    "                 #"display#" : #"",FHRPATN,"#" }";
          MATCH    SP70,FHRREAS
          IF       !@EQUAL
            WRITE    HTMLFILE;*+,",  #"reason#" : [ { #"text#" : #"",FHRREAS,"#" } ] ";
          ENDIF
          PACK     FHRUNITD,FHRUNITD,SP70
          MATCH    SP70,FHRUNITD
          IF       !@EQUAL
            STRIP    FHRUNITD
            WRITE    HTMLFILE;*+,", #"type#" : [ {":
                      " #"coding#" : [ {":
                      "  #"code#" : #"",FHRUNITC,"#",":
                      "  #"display#" : #"",FHRUNITD,"#"":
                      " } ] ":
                      "} ] ";
          ENDIF
.
.         MATCH    SP70,FHRLOCN 
.         IF       !@EQUAL  
.           STRIP    FHRWLOCC  *  Location Code
.           STRIP    FHRLOCN  *  Location Name
.           WRITE    HTMLFILE;*+,",#"location#" : [":
.                    "{ #"location#" : ":
.                    " { #"reference#" : #"Location/",FHRWLOCC,"#",":
.                    "   #"display#"   : #"",FHRLOCN,"#" } ":
.                    " }";
.         ENDIF
.
.         MATCH     SP70,FHRBLOCN
.         IF        !@EQUAL
.           STRIP     FHRBLOCC
.           STRIP     FHRBLOCN
.           WRITE     HTMLFILE;*+,",":
.                     "{ #"location#" : ":
.                     " { #"reference#" : #"Location/",FHRBLOCC,"#",":
.                     "   #"display#"   : #"",FHRBLOCN,"#" } ":
.                     " } ]";
.         ELSE
.           WRITE     HTMLFILE;*+," ]"  
.         ENDIF
.
          MATCH    SP70,FHRLOCN
          IF       !@EQUAL
            STRIP    FHRWLOCC  *  Location Code
            STRIP    FHRLOCN  *  Location Name
            WRITE    HTMLFILE;*+,",#"location#" : [":
                     "{ #"location#" : ":
                     " { #"reference#" : #"Location/",FHRWLOCC,"#",":
                     "   #"display#"   : #"",FHRLOCN,"#" } ":
                     " }";
.
            MATCH     SP70,FHRBLOCN
            IF        !@EQUAL
              STRIP     FHRBLOCC
              STRIP     FHRBLOCN
              WRITE     HTMLFILE;*+,",":
                        "{ #"location#" : ":
                        " { #"reference#" : #"Location/",FHRBLOCC,"#",":
                        "   #"display#"   : #"",FHRBLOCN,"#" } ":
                        " } ";
            ENDIF
.
            WRITE     HTMLFILE;*+," ]"
.
          ELSE
.
            MATCH     SP70,FHRBLOCN
            IF        !@EQUAL
              STRIP     FHRBLOCC
              STRIP     FHRBLOCN
              WRITE    HTMLFILE;*+,",#"location#" : [":
                        "{ #"location#" : ":
                        " { #"reference#" : #"Location/",FHRBLOCC,"#",":
                        "   #"display#"   : #"",FHRBLOCN,"#" } ":
                        " } ]";
            ENDIF
.
          ENDIF
.
          MATCH    SP70,FHRDOCC1
          IF       !@EQUAL 
            STRIP    FHRDOCC1 *  Doctor Code
            STRIP    FHRDOCN1 *  Doctor Name
            WRITE    HTMLFILE;*+,",#"participant#" : [ { ";
            PACK     FHRDOCT1,FHRDOCT1,SP70
            MATCH    SP70,FHRDOCT1
            IF       !@EQUAL 
              STRIP    FHRDOCT1 *  Doctor Type
              WRITE    HTMLFILE;*+,"#"type#": [ { #"coding#": [ {":
                        " #"system#": #"http://hl7.org/fhir/participant-type#",":
                        " #"code#": #"",FHRDOCT1,"#" } ] } ],";
            ENDIF
            WRITE    HTMLFILE;*+," #"individual#" : ":
                                 " { #"reference#" : #"Practitioner/",FHRDOCC1,"#",":
                                 "   #"display#" : #"",FHRDOCN1,"#" } } ";
            MATCH    SP70,FHRDOCC2
            IF       !@EQUAL 
              STRIP    FHRDOCC2 *  Doctor Code
              STRIP    FHRDOCN2 *  Doctor Name
              WRITE    HTMLFILE;*+,", { ";
              PACK     FHRDOCT2,FHRDOCT2,SP70
              MATCH    SP70,FHRDOCT2
              IF       !@EQUAL 
                STRIP    FHRDOCT2 *  Doctor Type
                WRITE    HTMLFILE;*+,"#"type#": [ { #"coding#": [ {":
                            "  #"system#": #"http://hl7.org/fhir/participant-type#",":
                            "  #"code#": #"",FHRDOCT2,"#" } ] } ],";
              ENDIF
              WRITE    HTMLFILE;*+," #"individual#" : ":
                                   "  { #"reference#" : #"Practitioner/",FHRDOCC2,"#",":
                                   "    #"display#" : #"",FHRDOCN2,"#" } }  ";
            ENDIF
            WRITE    HTMLFILE;*+,"]"
          ENDIF
.
          CALL     RESDIA00       * Resource Diagnosis Details not in List Outputs
          CALL     RESEXT00       * Resource Extra Data
.
          WRITE    HTMLFILE;*+,"}"
          RETURN
.
+
.------------------------------------------------------------
. FHIR Patient Resource
.------------------------------------------------------------
RESPAT00  REP      " -",FHRENCID *  Encounter ID
          MOVE     PMPXURNO,FHRPATID
          SQUEEZE  FHRPATID      *  Patient Code
          REP      "#" ",PTMASNAM
          REP      "#" ",PGNAME
          REP      "#" ",PTITL
          STRIP    PTMASNAM
          STRIP    PGNAME
          STRIP    PTITL
          STRIP    WBTPFIL  *  Template
          UNPACK   PBDATE,CCENT,CYEAR,CMON,CDAY
          MOVE     PSEX,DSEXCHAR
          CALL     SEXDSC00                * get sex description (in IBACOMN)
.
.          MOVE     DSEXDE13,DISPSEX                                 *C-51164
.
          MOVE     SP70,DISPSEX
.
          MATCH    "M",TCINDC7
          IF       @EQUAL
            MOVE     "male",DISPSEX          
          ENDIF
.
          MATCH    "F",TCINDC7
          IF       @EQUAL
            MOVE     "female",DISPSEX
          ENDIF
.
          MATCH    "O",TCINDC7
          IF       @EQUAL
            MOVE     "other",DISPSEX
          ENDIF
.
          MATCH    "U",TCINDC7
          IF       @EQUAL
            MOVE     "unknown",DISPSEX
          ENDIF
.
          REP      LOWUPP,DISPSEX                                   *C-51164
          WRITE    HTMLFILE;*+,"{ #"resourceType#": #"Patient#",":
                    " #"id#": #"",FHRPATID,"#",":
                    " #"extension#": [":
                    " { #"url#":#"%BASEURL/extension/webPASService#"":
                        " ,#"valueString#":#"",PRGID,SP1,PRGNAM,SP1,VERSION,"#"},":
                    " { #"url#":#"%BASEURL/extension/webPASTemplate#"":
                        " ,#"valueString#":#"%TEMPLATEHOME/",WBTPFIL," %TEMPLATEVERSION#"}";
.
          MATCH    SP70,PCONT
          IF       !@EQUAL & !@EOS
.
            WRITE     HTMLFILE;"       ,{  #"url#" : #"%BASEURL/extension/dxc-hc-countryofbirth#",";
            WRITE     HTMLFILE;"           #"valueCodeableConcept#" : {";
            WRITE     HTMLFILE;"                #"coding#": [ {";
            WRITE     HTMLFILE;"                  #"system#": #"%BASEURL/ValueSet/Category-",ANSC,"-#", ";
            WRITE     HTMLFILE;"                  #"code#": #"",PCONT,"#" ";
.
            PACK      KEY5,ANSC,SP1,PCONT,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              WRITE     HTMLFILE;"                  ,#"display#": #"",TDESC,"#"";
            ENDIF
.
            WRITE     HTMLFILE;"                 } ]";
            WRITE     HTMLFILE;"            }";
            WRITE     HTMLFILE;"        }";
.
          ENDIF
.
          MATCH    SP70,PMPXABRG
          IF       !@EQUAL & !@EOS
.
            WRITE     HTMLFILE;"       ,{  #"url#" : #"%BASEURL/extension/dxc-hc-indigenousstatus#",";
            WRITE     HTMLFILE;"           #"valueCodeableConcept#" : {";
            WRITE     HTMLFILE;"                #"coding#": [ {";
            WRITE     HTMLFILE;"                  #"system#": #"%BASEURL/ValueSet/Category-",ANSV,ANSA,"#", ";
            WRITE     HTMLFILE;"                  #"code#": #"",PMPXABRG,"#" ";
.
            PACK      KEY5,ANSV,ANSA,PMPXABRG,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              WRITE     HTMLFILE;"                  ,#"display#": #"",TDESC,"#"";
            ENDIF
.
            WRITE     HTMLFILE;"                 } ]";
            WRITE     HTMLFILE;"            }";
            WRITE     HTMLFILE;"        }";
.
          ENDIF
.
          MATCH    SP70,PTYPE
          IF       !@EQUAL & !@EOS
.
            WRITE     HTMLFILE;"       ,{  #"url#" : #"%BASEURL/extension/dxc-hc-resident#",";
            WRITE     HTMLFILE;"           #"valueCodeableConcept#" : {";
            WRITE     HTMLFILE;"                #"coding#": [ {";
            WRITE     HTMLFILE;"                  #"system#": #"%BASEURL/ValueSet/Category-",ANST,"-#", ";
            WRITE     HTMLFILE;"                  #"code#": #"",PTYPE,"#" ";
.
            PACK      KEY5,ANST,SP1,PTYPE,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              WRITE     HTMLFILE;"                  ,#"display#": #"",TDESC,"#"";
            ENDIF
.
            WRITE     HTMLFILE;"                 } ]";
            WRITE     HTMLFILE;"            }";
            WRITE     HTMLFILE;"        }";
.
          ENDIF
.
.UPTOHERE
.
          WRITE    HTMLFILE;" ],":
                    " #"identifier#": [":
                    "  {":
                    "    #"type#":  { ":
                    "      #"coding#":  [ {":
                    "        #"code#": #"MRN#",":
                    "        #"display#": #"Medical Record Number#" } ] },":
                    "    #"value#": #"",PMPXURNO,"#"":
                    "  } ],":
                    " #"meta#":":
                    "  {":
                    "    #"profile#":[":
                    "            #"http://azlor005.dxchealthlad.io/LMHIA":
                    "Service/api/v50/schema/Patient#"":
                    "    ]";

          MATCH     SP3,PMPXPRVI
          GOTO      RESPAT05 IF EQUAL
          MOVE      "PV",CATEGORY
          MOVE      PMPXPRVI,CODE
          PACK      KEY5,CATEGORY,CODE,SP70
          CALL      RDCODE1 
          BRANCH    OVRCD,RESPAT05
          MATCH     "P",TCINDC1
          GOTO      RESPAT05 IF NOT EQUAL
          WRITE     HTMLFILE;",   #"security#":[":
                    "           {":
                    "               #"system#":#"https://www.hl7.org/fhir":
                    "/ValueSet/v3-InformationSensitivityPolicy#",":
                    "               #"code#":#"VIP#",":          
                    "               #"display#":#"celebrity information":
                    " sensitivity#"": 
                    "           }":
                    "    ]";
RESPAT05  WRITE     HTMLFILE;"  },":
                    " #"name#": [":
                    "  {":
                    "    #"use#": #"usual#",":
                    "    #"family#": #"",PTMASNAM,"#",":
                    "    #"given#" : [#"",PMPXFNAM,"#"";
          MATCH     SP70,PMPXSNAM
          IF        !@EQUAL
            STRIP     PMPXSNAM
            WRITE     HTMLFILE;*+,",#"",PMPXSNAM,"#"";
          ENDIF
          WRITE     HTMLFILE;*+,"],":
                      "      #"prefix#" : [#"",PTITL,"#"]":
                      "     }";
          IF        FHRALIAF=1
            STRIP     FHRASNM  *  Alias Surname Search
            STRIP     FHRAGNM1 *  Alias First Name Search
            WRITE     HTMLFILE;*+," ,{":
                        "      #"use#": #"anonymous#",":
                        "      #"family#": #"",FHRASNM,"#",":
                        "      #"given#" : [#"",FHRAGNM1,"#"";
            MATCH     SP70,FHRAGNM2
            IF        !@EQUAL
              STRIP     FHRAGNM2
              WRITE     HTMLFILE;*+,",#"",FHRAGNM2,"#"";
            ENDIF
            WRITE     HTMLFILE;*+,"]":
                        "     }";
          ENDIF
          WRITE     HTMLFILE;*+,"],":
                      "   #"gender#": #"",DISPSEX,"#",":
                      "   #"birthDate#": #"",CCENT,CYEAR,"-",CMON,"-",CDAY,"#",";
          IF        PCEASE=1
            WRITE     HTMLFILE;*+,"  #"deceasedBoolean#": true,";
            MATCH     SP70,PDECDTE
            IF        !@EQUAL
              UNPACK    PDECDTE,CCENT,CYEAR,CMON,CDAY
              WRITE     HTMLFILE;*+,"  #"deceasedDateTime#": #"",CCENT,CYEAR,"-",CMON,"-",CDAY,"#", ";
            ENDIF
          ELSE
            WRITE     HTMLFILE;*+,"  #"deceasedBoolean#": false, ";
          ENDIF
.
          MATCH    SP70,PMSTAT
          IF       !@EQUAL & !@EOS
.
            WRITE     HTMLFILE;"           #"maritalStatus#" : {";
            WRITE     HTMLFILE;"                #"coding#": [ {";
            WRITE     HTMLFILE;"                  #"system#": #"%BASEURL/ValueSet/Category-",ANSM,"-#", ";
            WRITE     HTMLFILE;"                  #"code#": #"",PMSTAT,"#" ";
.
            PACK      KEY5,ANSM,SP1,PMSTAT,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              WRITE     HTMLFILE;"                  ,#"display#": #"",TDESC,"#"";
            ENDIF
.
            WRITE     HTMLFILE;"                 } ]";
            WRITE     HTMLFILE;"            },";
.
          ENDIF
.UPTOHERE
          WRITE     HTMLFILE;"   #"address#": [":
                      "      {#"use#": #"home#",":
                      "       #"line#": [#"",PADD1,"#"";
          MATCH     SP70,PADD2
          IF        !@EQUAL
            STRIP     PADD2
            WRITE     HTMLFILE;*+,",#"",PADD2,"#"";
          ENDIF
          WRITE     HTMLFILE;*+,"],":
                      "       #"city#": #"",PSUBRB,"#",":
                      "       #"state#": #"",PADD4,"#",":
                      "       #"postalCode#": #"",PPOST,"#"":
                      "      }":
                      "    ]";
.
.
          MATCH     SP70,PMPXLNG1
          IF        !@EQUAL & !@EOS
            WRITE     HTMLFILE;"  ,#"communication#": [";
            WRITE     HTMLFILE;"        {";
            WRITE     HTMLFILE;"           #"language#" : {";
            WRITE     HTMLFILE;"                #"coding#": [ {";
            WRITE     HTMLFILE;"                  #"system#": #"%BASEURL/ValueSet/Category-",ANSL,ANSA,"#", ";
            WRITE     HTMLFILE;"                  #"code#": #"",PMPXLNG1,"#" ";
.
            PACK      KEY5,ANSL,ANSA,PMPXLNG1,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              WRITE     HTMLFILE;"                  ,#"display#": #"",TDESC,"#"";
            ENDIF
.
            WRITE     HTMLFILE;"                 } ]";
            WRITE     HTMLFILE;"            },";
            WRITE     HTMLFILE;"           #"preferred#" : true";
            WRITE     HTMLFILE;"        }";
            WRITE     HTMLFILE;"    ]";
          ENDIF
.
.
          CALL      FHRCON00                * Add Contacts (Only in PATWEB98 not in search or list operations)
.
          WRITE     HTMLFILE;*+,"  }";
          RETURN
+
.--------------------------------------------
. Clear FHIR Variables
.--------------------------------------------
FHRCLR00  MOVE     ZERO,FHRALIAF  *  Alias Flag for Search
          MOVE     SP70,FHRASNM   *  Alias Surname Search
          MOVE     SP70,FHRAGNM1  *  Alias First Name Search
          MOVE     SP70,FHRAGNM2  *  Alias Second Name Search
          MOVE     SP70,FHRSTAT   *  Encounter Status
          MOVE     SP70,FHRCLAS   *  Encounter Class
          MOVE     SP70,FHRPATN   *  Patient Name
          MOVE     SP70,FHRSTRDT  *  Period Start Date
          MOVE     SP70,FHRENDDT  *  Period End Date
          MOVE     SP70,FHRLOCC   *  Location Code
          MOVE     SP70,FHRWLOCC  *  Location Code (Ward)
          MOVE     SP70,FHRBLOCC  *  Location Code (Bed)
          MOVE     SP70,FHRLOCN   *  Location Name
          MOVE     SP70,FHRBLOCN  *  Location Name (Bed)
          MOVE     SP70,FHRDOCT1  *  Doctor  Type
          MOVE     SP70,FHRDOCC1  *  Doctor  Code
          MOVE     SP70,FHRDOCN1  *  Doctor  Name
          MOVE     SP70,FHRDOCT2  *  Doctor  Type
          MOVE     SP70,FHRDOCC2  *  Doctor  Code
          MOVE     SP70,FHRDOCN2  *  Doctor  Name
          MOVE     SP70,FHRREAS   *  Reason for Visit
          MOVE     SP70,FHRFAMN   *  Family Name
          MOVE     SP70,FHRGIVN   *  Given Name
          MOVE     SP70,FHRTITL   *  Title
          MOVE     SP70,FHRVISA   *  Vistors Allowed
          MOVE     SP70,FHRPHNA   *  Phone Calls Allowed
          RETURN
