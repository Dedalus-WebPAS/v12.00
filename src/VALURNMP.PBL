.*********************************************************************
.*                  VALURNMP                    From PATCOMN1        *
.*        Check if entered UR number is on National Master PMI       *
.*        when it isnt on normal master file                         *
.*        Para's  : CMPURNO       keyed in UR number (20 chars)      *
.*                  CPPURNO       keyed in UR number (8 chars)       *
.*        Returns : OVRCD = 0     have a NMPMI UR number sets PURNO  *
.*                  OVRCD = 1     invalid UR number                  *
.*                  OVRCD = 2     Abort the UR number                *
.*********************************************************************
          DEFPROC   VALURNMP
.
          INC       PATNIDFD/INC            * national master patient index file
.
SP12      INIT      "            "
PTCNNMDX  DIM       12
PTCNNULU  FORM      1
PTCNNMPI  FORM      1
 IFGE     $$VER,7
CONTROLF  SFILE    ASCII, FIXED=256
 XIF
 IFEQ     $$VER,6
CONTROLF  FILE     ASCII, FIXED=256
 XIF
.
VLNMP000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY;*225,PTCNNMDX,PTCNNULU
          READ      CONTROLF,FIFTY9;*234,PTCNNMPI
          COMPARE   ZERO,PTCNNMPI
          GOTO      VLNMP910 IF EQUAL       * not using NMPI so invalid UR
          COMPARE   ONE,PTCNNHII            * New Zealand Not Using This
          GOTO      VLNMP910 IF EQUAL       * not using NMPI so invalid UR
.
.         Hong Kong/England National ID file
.
          OPEN      PATNIDA2,"patnidaf"
          PACK      KEY30,CMPURNO,CHOSINDX,SP20
          CALL      RSPTNID2
          CALL      RKPTNID2
          CLOSE     PATNIDA2
          BRANCH    OVRCD,VLNMP910          * no record on file
          MATCH     CMPURNO,PTNINMPI
          GOTO      VLNMP910 IF NOT EQUAL   * different National UR
          COMPARE   CHOSINDX,PTNIHNUM
          GOTO      VLNMP910 IF NOT EQUAL   * different Hospital Number
.
.         the entered UR number is a National one, so return the Local number
.
          COMPARE   ONE,PTCNNULU
          GOTO      VLNMP800 IF NOT EQUAL   * dont tell user about link
.
VLNMP700  CLEAR     WEBTITLE
          APPEND    PTCNNMDX,WEBTITLE
          APPEND    " has a Local U/R No. of ",WEBTITLE
          APPEND    PTNILNUM,WEBTITLE
          RESET     WEBTITLE
          CALL      DISALT00                * Display Alert
.
.         obtain the new PMI details
.
VLNMP800  MOVE      PTNILNUM,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,VLNMP910          * new UR number is invalid
          MOVE      PURNO,CPPURNO           * set UR number
...          DISPLAY   *PCCOL:CVERT,*V2LON,CPPURNO,*EL;
          MOVE      ZERO,OVRCD              * set OVRCD to say valid UR number
          GOTO      VLNMP990
.
.         the entered UR number is invalid
.
VLNMP910  MOVE      ONE,OVRCD               * set OVRCD to say invalid UR number
          GOTO      VLNMP990
.
.         abort the entered UR number
.
VLNMP920  MOVE      TWO,OVRCD
.
VLNMP990  GOTO      VLNMP999
.
.         I/O's for procedure
.
          INC       PATNIDIO/INC            * national master patient index
          INC       IBAOVRIO/INC            * IO labels
.
VLNMP999  ENDPROC
