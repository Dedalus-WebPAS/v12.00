.******************************************************************************
.*  Include Name  : PATICDKY.PBL                                              *
.*  Description   : Keyin of Disease codes                                    *
.*  Author        : Ian Rutt                                                  *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF9 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMODE - is keyin mode                                  *
.*                  DSICDDTE - Date this ICD code is valid for compared to the*
.*                             ICD10 version 2 go live date (PTCNGDV2)        *
.*                                                                            *
.*  Returns       : DPCODE  - Keyed in code                                   *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or spaces input                        *
.*                       = 2   Exitchar input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Modifications :                                                           *
.*       V11.05.01  17/03/2025  Davin           TSK 0956210                   *
.*                  Added ICD10 Ed.13 Go-live Date PTCNGDX3 (Sect.130 Pos.86) *
.*       V11.02.01  28/03/2022  Davin           TSK 0917793                   *
.*                  Added ICD10 Ed.12 Go-live Date PTCNGDX2 (Sect.128 Pos.231)*
.*       V10.14.01  14/03/2019  Don Nguyen      TSK 0869412                   *
.*                  Added ICD10 Ed.11 Go-live Date PTCNGDX1 (Sect.117 Pos.220)*
.*       V10.10.01  08/03/2017  Davin           TSK 0833093   HDP 2017/2018   *
.*                  Add ICD10 Ed.10 Go-live Date PTCNGDVX (Sect.110 Pos.85)   *
.*       V10.06.01  17/04/2015  Davin           CAR 311669    HDP 2015/2016   *
.*                  Add ICD10 Ed.9 Go-live Date PTCNGDV9 (Sect.110 Pos.77 )   *
.*       V10.03.01  23/04/2013  Davin           CAR 284420    HDP 2013/2014   *
.*                  Add ICD10 Ed.8 Go-live Date PTCNGDV8 (Sect.110 Pos.69 )   *
.*       V10.00.01  01/04/2010  Mike Laming     CAR 219246    HDP 2010        *
.*                  Add ICD10 Ed.7 Go-live Date PTCNGDV7 (Sect.110 Pos.61 )   *
.*        V9.09.01  19/03/2008  Mike Laming     CAR 163918    HDP 2008        *
.*                  Add ICD10 Ed.6 Go-live Date PTCNGDV6 (Sect.112 Pos.239) - *
.*                  also add bits missing since 2000 incl. Ed.4 & Ed.5        *
.*        V5.08.02  14/08/2000  Greg Horvat                                   *
.*                  Changed the ICD10 V2 validation - if before the ICD10 V2  *
.*                  go live date only ICD10 V1 codes are valid, if on or after*
.*                  only ICD10 V2 codes are valid                             *
.*        V5.08.01  27/06/2000  Greg Horvat                                   *
.*                  Added code to validate ICD10 version 2 codes              *
.*        V5.07.01  03/11/1999  Greg Horvat                                   *
.*                  Changed to not automatically reformat the else code       *
.*                  (xxxxxxxxx) cause its required for complex mappings       *
.*        V5.06.00  25/09/1998    Katie Nelson                                *
.*                  Added procedure HPTDIA00 to key word search               *
.*        V6.05.01  03/06/1998  Greg Horvat                                   *
.*                  Changed to auto allocate the . & / for ICD10 codes        *
.*        V6.03.00  26/09/1996  Greg Horvat      Holy Spirit Mods             *
.*                  Ported from release 5.04                                  *
.*                                                                            *
.******************************************************************************
PATICDKY
PTICKY00  MATCH     SP9,CKYIDEF9                  * using a default ?
          GOTO      PTICKY10 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN9:      * no default used
                    *PECOL:EVERT,*V2LON,DPCODE:
                    *PECOL:EVERT,*DV,DPCODE
          GOTO      PTICKY20
.
PTICKY10  MOVE      CKYIDEF9,DPCODE                 * default
          KEYIN     *PECOL:EVERT,*DV,DPCODE:     * default used
                    *PECOL:EVERT,*V2LON,*RV,DPCODE:
                    *PECOL:EVERT,*DV,DPCODE
.
.         check what was entered
.
PTICKY20  CALL      ICDCK000
          BRANCH    EXIT,PTICKY25,PTICKY31,PTICKY41,PTICKY95
.                        "?"      valid    nothing  exitchar
          GOTO      PTICKY00
.
.         a "?" was input
.
PTICKY25  CALL      HPTDIA00
          BRANCH    OVRCD,PTICKY00
          IF        CKYIMODE = 1
            CALL      REXISTS
          ENDIF
PTICKY31  MOVE      FALSE,EXIT                    * valid input
          GOTO      PTICKY99 
.
.         nothing was input
.
PTICKY41  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      PTICKY99 
.
.         EXITCHAR input
.
PTICKY95  MOVE      TWO,EXIT                * EXITCHAR
.
PTICKY99  RETURN
+
.*********************************************************************
.*                  ICDCK000                    Called by : PATICDKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
ICDCK000  ENDSET    DPCODE
          APPEND    SP9,DPCODE
          RESET     DPCODE
.
          MATCH     EXITCHAR,DPCODE
          GOTO      ICDCK600 IF EQUAL       * exit char
.
          MATCH     SP9,DPCODE
          GOTO      ICDCK700 IF EQUAL       * nothing
.
          MATCH     QUEST,DPCODE
          GOTO      ICDCK800 IF EQUAL       * ? mark
.
.         a code was entered so validate
.
ICDCK500  PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MATCH     PTCNI10D,KEY8
          CALL      FTICD000 IF NOT LESS
. 
          PACK      KEY9,DPCODE,SP9
          CALL      RDPTICD1                  * was RCICD1            *C-163918
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,ICDCK900          * invalid code
.
            CALL      VDV2C000              * Validate dis ICD10 version 2 code
            BRANCH    EXIT,ICDCK905
          ELSE
            COMPARE   ZERO,OVRCD   
            GOTO      ICDCK900 IF EQUAL
          ENDIF  
.
          MOVE      TWO,EXIT                * valid code
          GOTO      ICDCK999
.
.         exitchar entered
.
ICDCK600  MOVE      FOUR,EXIT
          MOVE      SP9,DPCODE
          GOTO      ICDCK999
.
.         nothing entered
.
ICDCK700  BRANCH    CKYIMAND,ICDCK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP9,DPCODE
          GOTO      ICDCK999
.
.         "?" entered
.
ICDCK800  MOVE      ONE,EXIT
          GOTO      ICDCK999
.
.         invalid code entered
.
ICDCK900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
.
          IF        CKYIMODE = 0
            DISPLAY   *P1:24,*EL,*B,"Invalid Disease Code. ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          CALL      PUTSCR00
.
ICDCK905  MOVE      ZERO,EXIT
          GOTO      ICDCK999
.
.         field is mandatory
.
ICDCK950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is mandatory. ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
ICDCK999  RETURN
+
.******************************************************************************
.*                                  FTICD000              Called by: ICDCK000 *
.*                              Format ICD10 Code                             *
.******************************************************************************
FTICD000  MATCH     "xxxxxxxxx",DPCODE
          GOTO      FTICD100 IF EQUAL       * Dont format else code for complex
.                                             mapping
          REP       UPPLOW,DPCODE
          SCAN      DOT,DPCODE
          GOTO      FTICD100 IF EQUAL       * Code already formatted
.
          SCAN      "/",DPCODE
          GOTO      FTICD100 IF EQUAL       * Code already formatted
.
          UNPACK    DPCODE,KEY3,KEY6
          MATCH     SP6,KEY6
          GOTO      FTICD100 IF EQUAL       * Header code, dont format
.
          MOVE      DPCODE,KEY9
          UNPACK    DPCODE,KEY1,KEY8
          TYPE      KEY1
          IF        @EQUAL
            STRIP     KEY9
            MOVELPTR  KEY9,FORM2
            IF        FORM2>=5
              MOVE      "/",KEY1
              UNPACK    DPCODE,KEY4,KEY5
              PACK      DPCODE,ANSM,KEY4,KEY1,KEY5
            ENDIF
          ELSE
            STRIP     KEY9
            MOVELPTR  KEY9,FORM2
            IF        FORM2>=6
              MOVE      "/",KEY1
              UNPACK    DPCODE,KEY5,KEY4
              PACK      DPCODE,KEY5,KEY1,KEY4
            ELSE
              UNPACK    DPCODE,KEY3,KEY6
              PACK      DPCODE,KEY3,DOT,KEY6
            ENDIF
          ENDIF
.
FTICD100  RESET     DPCODE
FTICD900  MOVE      ZERO,EXIT
FTICD999  RETURN
+
.******************************************************************************
.*                                  VDV2C000              Called by: ICDCK000 *
.*                    Validate Disease ICD10 Version 2 Code                   *
.******************************************************************************
VDV2C000  MATCH     SP8,DSICDDTE
          GOTO      VDV2C900 IF EQUAL     * Blank ICD date
          GOTO      VDV2C900 IF EOS       * Blank ICD date
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND05;*217,PTCNGDV4,*227,PTCNGDV5
          READ      CONTROLF,HUND12;*239,PTCNGDV6                     *I-163918
          READ      CONTROLF,HUND10;*61,PTCNGDV7:                     *I-219246
                                    *69,PTCNGDV8:                     *I-284420
                                    *77,PTCNGDV9:                     *I-311669
                                    *85,PTCNGDVX                      *I-0833093
          READ      CONTROLF,HUND17;*220,PTCNGDX1
          READ      CONTROLF,HUND28;*231,PTCNGDX2                     *I-0917793
          READ      CONTROLF,HUND30;*86,PTCNGDX3                      *I-0956210
.
.                                           ***** ICD10 Ed.13         *I-0956210
          MATCH     PTCNGDX3,DSICDDTE
          IF        !@LESS
            IF        PT0DVX3C<>1
              DISPLAY   *P1:24,*EL,*B,"This disease code is not a valid ":
                        "ICD10 Ed.13 code.  ";
              CALL      HITENTER
              GOTO      VDV2C950            * not valid ICD10 Ed.13 code
            ENDIF
            GOTO      VDV2C900              * valid ICD10 Ed.13 code
          ENDIF
.
.                                           ***** ICD10 Ed.12         *I-0917793
          MATCH     PTCNGDX2,DSICDDTE
          IF        !@LESS
            IF        PT0DVX2C<>1
              DISPLAY   *P1:24,*EL,*B,"This disease code is not a valid ":
                        "ICD10 Ed.12 code.  ";
              CALL      HITENTER
              GOTO      VDV2C950            * not valid ICD10 Ed.12 code
            ENDIF
            GOTO      VDV2C900              * valid ICD10 Ed.12 code
          ENDIF
.
.                                           ***** ICD10 Ed.11
          MATCH     PTCNGDX1,DSICDDTE
          IF        !@LESS
            IF        PT0DVX1C<>1
              DISPLAY   *P1:24,*EL,*B,"This disease code is not a valid ":
                        "ICD10 Ed.11 code.  ";
              CALL      HITENTER
              GOTO      VDV2C950            * not valid ICD10 Ed.11 code
            ENDIF
            GOTO      VDV2C900              * valid ICD10 Ed.11 code
          ENDIF
.
.                                           ***** ICD10 Ed.10         *I-0833093
          MATCH     PTCNGDVX,DSICDDTE
          IF        !@LESS
            IF        PT0DVXCD<>1
              DISPLAY   *P1:24,*EL,*B,"This disease code is not a valid ":
                        "ICD10 Ed.10 code.  ";
              CALL      HITENTER
              GOTO      VDV2C950            * not valid ICD10 Ed.10 code
            ENDIF
            GOTO      VDV2C900              * valid ICD10 Ed.10 code
          ENDIF
.
.                                           ***** ICD10 Ed.9          *I-311669
          MATCH     PTCNGDV9,DSICDDTE
          IF        !@LESS
            IF        PT0DV9CD<>1
              DISPLAY   *P1:24,*EL,*B,"This disease code is not a valid ":
                        "ICD10 Ed.9 code.  ";
              CALL      HITENTER
              GOTO      VDV2C950            * not valid ICD10 Ed.9 code
            ENDIF
            GOTO      VDV2C900              * valid ICD10 Ed.9 code
          ENDIF
.
.                                           ***** ICD10 Ed.8          *I-284420
          MATCH     PTCNGDV8,DSICDDTE
          IF        !@LESS
            IF        PT0DV8CD<>1
              DISPLAY   *P1:24,*EL,*B,"This disease code is not a valid ":
                        "ICD10 Ed.8 code.  ";
              CALL      HITENTER
              GOTO      VDV2C950            * not valid ICD10 Ed.8 code 
            ENDIF
            GOTO      VDV2C900              * valid ICD10 Ed.8 code
          ENDIF
.
.                                           ***** ICD10 Ed.7          *I-219246
          MATCH     PTCNGDV7,DSICDDTE
          IF        !@LESS
            IF        PT0DV7CD<>1
              DISPLAY   *P1:24,*EL,*B,"This disease code is not a valid ":
                        "ICD10 Ed.7 code.  ";
              CALL      HITENTER
              GOTO      VDV2C950            * not valid ICD10 Ed.7 code 
            ENDIF
            GOTO      VDV2C900              * valid ICD10 Ed.7 code
          ENDIF
.
.                                           ***** ICD10 Ed.6          *I-163918
          MATCH     PTCNGDV6,DSICDDTE
          IF        !@LESS
            IF        PT0DV6CD<>1
              DISPLAY   *P1:24,*EL,*B,"This disease code is not a valid ":
                        "ICD10 Ed.6 code.  ";
              CALL      HITENTER
              GOTO      VDV2C950            * not valid ICD10 Ed.6 code
            ENDIF
            GOTO      VDV2C900              * valid ICD10 Ed.6 code
          ENDIF
.
.                                           ***** ICD10 Ed.5
          MATCH     PTCNGDV5,DSICDDTE
          IF        !@LESS
            IF        PT0DV5CD<>1
              DISPLAY   *P1:24,*EL,*B,"This disease code is not a valid ":
                        "ICD10 Ed.5 code.  ";
              CALL      HITENTER
              GOTO      VDV2C950            * not valid ICD10 Ed.5 code
            ENDIF   
            GOTO      VDV2C900              * valid ICD10 Ed.5 code
          ENDIF
.
          MATCH     PTCNGDV4,DSICDDTE
          IF        !@LESS
            IF        PT0DV4CD<>1
              DISPLAY   *P1:24,*EL,*B,"This disease code is not a valid ":
                        "ICD10 Ed. 4 code.  ";
              CALL      HITENTER
              GOTO      VDV2C950            * not valid ICD10 e4 code
            ENDIF
            GOTO      VDV2C900              * valid ICD10 e4 code
          ENDIF
.
          READ      CONTROLF,NINTY8;*149,PTCNGDV2
          MATCH     PTCNGDV2,DSICDDTE
          IF        @LESS
            IF        PT0DV1CD<>1
              DISPLAY   *P1:24,*EL,*B,"This disease code is not a valid ":
                        "ICD10 version 1 code.  ";
              CALL      HITENTER
              GOTO      VDV2C950
            ENDIF
          ELSE
            IF        PT0DV2CD<>1
              DISPLAY   *P1:24,*EL,*B,"This disease code is not a valid ":
                        "ICD10 version 2 code.  ";
              CALL      HITENTER
              GOTO      VDV2C950
            ENDIF
          ENDIF
.
VDV2C900  MOVE      ZERO,EXIT
          GOTO      VDV2C999
.
VDV2C950  MOVE      ONE,EXIT
VDV2C999  RETURN
.
.         INCLUDE   PATICDDS
          INC       ICDKYWSR
+
