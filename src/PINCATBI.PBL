.        PINCATBI : Display and Print New Patient Invoice functionality
.                   PTCNPPIN=1 and IPATFLAG=2
.
.        VERSION  :
.
.        V12.00.01  18/07/2025  J.Tan          TSK 0963587
.                   Mod checking if patient is discharged & invoiced
.        V11.03.02  12/09/2023  J.Tan          TSK 0935891
.                   Added ACCMDESC for displaying longer bed fees desc.
.        V11.03.01  10/03/2023  J.Tan          TSK 0924587
.                   Mod NOT to print zero line of Accommodation and Items
.        V11.01.00  16/03/2021  J.Tan    TSK 0863287
.                   Created new routine for Patient Portion invoice
.
.        IF PROGFLAG = 1  THEN WORK OUT THE AMOUNT TO BE INVOICED
.
.        IF PROGFLAG = 2  THEN DISPLAY NEXT INVOICE
.
.        IF PROGFLAG = 3  THEN PRINT NEXT INVOICE
.
.        IF PROGFLAG = 4  THEN UPDATE PATFINS WITH AMOUNTS TO BE INVOICED
.
.--------------------------------------------------------------------------
.
.
.*******************************************************************************
.          Display/write accommodation record for Patient Portion
.*******************************************************************************
XPRG0000  MATCH    ANSD,TMOVE
          IF       @EQUAL
            IF        CMXFLAG=1 & MVBFLAG=1
              UNPACK    DDATE,TCENT1,TYEAR1,TMON1,TDAY1  * use ddate for invoice
            ENDIF
          ENDIF
.
          ADD       PATLNGST,PATLNTOT
          ADD       PATLNTOT,PPAYABLE
.
          ADD       PATLNGST,TOTPGST
.
          MOVE      ONE,FRSTACCM
.
XPRG1000  BRANCH    PROGFLAG OF XPRG8000,XPRG2000,XPRG5000,XPRG6000,XPRG9999
.
XPRG2000  CALL      PACM0000       * Display accommodation record
.
          CALL      SCPAY0000      * Set co-payment for private/shared
          GOTO      XPRG9999
.
.         Print the accommodation line
.
XPRG5000  MOVE      STATYPE,TADMTYP
          PACK      DFDATE,FCENT1,FYEAR1,FMON1,FDAY1,SP70
          PACK      DTDATE,TCENT1,TYEAR1,TMON1,TDAY1,SP70
.
          COMPARE   ZERO,LSTRATE
          GOTO      XPRG5400 IF EQUAL       * Accommodation  with zero amount
.
          IF        CMXFLAG=1
            MATCH     ADATE,DDATE
            IF        @EQUAL
              IF        LSTRATE = 0
                GOTO      XPRG5400          * daycase without daily charge
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      ONE,RECFLAG     * flag for printing accommodation
          CALL      PRLN0000        * Print line of invoice
.
XPRG5400  IF        PATLRATE =0
            BRANCH     ADSCHI,XPRG9000        * Discharged and Invoiced
          ENDIF
.
.         Set up DTRAN record ready for writing
.
          CALL      PADT0000                  * Write DTR for accommodation
.
XPRG6000  CALL      WAFN0000                  * Set up key for a call to PATCALF
.
XPRG8000  CALL      MTDNFD00                  * Move to date to next from date
.
XPRG9000  MOVE      ZERO,EXIT
XPRG9999  RETURN
+
.*******************************************************************************
.          Display/write accommodation line
.*******************************************************************************
PACM0000  COMPARE   ZERO,PATLNTOT
          GOTO      PACM9999 IF EQUAL 
.
          PACK      ACCMDESC,ACCMDESC,SP70
          MATCH     SP70,ACCMDESC
          IF        @EQUAL
            PACK      ACCMDESC,TDDESC,SP70    * longer var for Accom.description
          ENDIF
.
          COMPARE   ONE,DCFLAG
          GOTO      PACM2500 IF NOT EQUAL
.
          COMPARE   ZERO,NODAYS
          GOTO      PACM9999 IF EQUAL          * no additional charge
.
          MOVE      FMON1,FORM2
          LOAD      MTHNAM3,FORM2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG:
                                  NSEP,NOCT,NNOV,NDEC
          MOVE      TMON1,FORM2
          LOAD      MTHNAM3A,FORM2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL:
                              NAUG,NSEP,NOCT,NNOV,NDEC
.
          MOVE      SP70,ANS
          PACK      KEY5,ANSA,SP1,STATYPE
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      TCINDC12,ANS
          ENDIF
.
          PACK      KEY4,FCENT1,FYEAR1
          REP       " 0",KEY4
          MOVE      LINETOT,FORM12P2
          ADD       LINEGST,FORM12P2
          WRITE     HTMLFILE;"<tr><td>",FDAY1,SP1,MTHNAM3,SP1,KEY4:
                               " to ";
.
          PACK      KEY4,TCENT1,TYEAR1
          REP       " 0",KEY4
          WRITE     HTMLFILE;TDAY1,SP1,MTHNAM3A,SP1,KEY4;
.
          WRITE     HTMLFILE;"</td><td><table width=100%><tr><td>",ACCMDESC;
          MATCH     SP70,BEDDESC
          IF        !@EQUAL
             MOVE    ZERO,EXIT
             PROC    CETT0000         *certificat type
             IF        EXIT=1
               WRITE    HTMLFILE;"<font color=red>",BEDDESC,"</font>";
             ELSE
               WRITE    HTMLFILE;"<font color=green>",BEDDESC,"</font>";
             ENDIF
          ENDIF
.
          MATCH     SP70,PTDTDES2
          IF        !@EQUAL
            WRITE     HTMLFILE;"<font color=red>/",PTDTDES2,"</font>";
          ENDIF
.
          PACK      KEY8,FCENT1,FYEAR1,FMON1,FDAY1,SP70
          REP       " 0",KEY8
          WRITE     HTMLFILE;"</td><td align=right>":
                             "<img src=../images/CommentIcon.gif ":
                             " class=ListIcon onclick='ShowStepdown(#"":
                             AURNO,"#",#"":
                             AADMNO,"#",#"":
                             KEY8,"#",#"":
                             ANS,"#",#"",SAVCONTR,"#")'></td></tr></table>";
.
          WRITE     HTMLFILE;"</td><td>&nbsp;",SAVCONTR:  * contract id
                             "</td><td>&nbsp;":
                             "</td><td>&nbsp;";
.
          WRITE     HTMLFILE;"</td><td align=right>",PATLRATE;
          MOVE      PATLRATE,FORM12P2
          MULT      NODAYS,FORM12P2
          WRITE     HTMLFILE;"</td><td align=right>",FORM12P2:
                             "</td><td align=right>",PATLNGST:
                             "</td><td align=right>",PATLNTOT:
                             "</td></tr>"
          GOTO      PACM9999
.
.         Overnight
.
PACM2500  COMPARE   ONE,CMXFLAG
          GOTO      PACM2800 IF NOT EQUAL
.
          MATCH     ADATE,DDATE
          IF        @EQUAL
            IF        LSTRATE = 0
              GOTO      PACM9999          * daycase without daily charge
            ENDIF
          ENDIF
.
PACM2800  MOVE      SP70,KEY70
          CLEAR     KEY70
.         APPEND    TDDESC,KEY70
          APPEND    ACCMDESC,KEY70
          MATCH     SP70,PTDTDES2
          IF        !@EQUAL
            RESET     KEY70
            STRIP     KEY70
            ENDSET    KEY70
            APPEND    "/",KEY70
            APPEND    PTDTDES2,KEY70
          ENDIF
          APPEND    SP70,KEY70
          RESET     KEY70
.
          MOVE      SP70,ANS
          PACK      KEY5,ANSA,SP1,STATYPE
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      TCINDC12,ANS
          ENDIF
.
          MOVE      FMON1,FORM2
          LOAD      MTHNAM3,FORM2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG:
                                  NSEP,NOCT,NNOV,NDEC
          MOVE      TMON1,FORM2
          LOAD      MTHNAM3A,FORM2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG:
                                     NSEP,NOCT,NNOV,NDEC
.
          IF        NODAYS=1
            MOVE      "DAY",DAYFORMT
          ELSE
            MOVE      "DAYS",DAYFORMT
          ENDIF
.
          MOVE      LINETOT,FORM12P2
          ADD       LINEGST,FORM12P2
          PACK      KEY4,FCENT1,FYEAR1
          REP       " 0",KEY4
          WRITE     HTMLFILE;"<tr><td>",FDAY1,SP1,MTHNAM3,SP1,KEY4;
.
          PACK      KEY4,TCENT1,TYEAR1
          REP       " 0",KEY4
          WRITE     HTMLFILE;" to ":
                                 TDAY1,SP1,MTHNAM3A,SP1,KEY4;
.
          COMPARE   ONE,PARTFLAG
          GOTO      PACM2824 IF NOT EQUAL      * Not partial charges
.
          MOVE      NODAYS,FORM41
          MOVE      TDAY1,CDAY
          MOVE      TMON1,CMON
          MOVE      TYEAR1,CYEAR
          MOVE      TCENT1,CCENT
          PACK      KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KEY8
          MATCH     DDATE,KEY8
          IF        @EQUAL
            ADD       "0.5",FORM41
            MOVE      "DAYS",DAYFORMT
          ENDIF
.
          WRITE     HTMLFILE;"</td><td>":
                              FORM41,SP1,DAYFORMT,SP1,ACCMDESC;
          GOTO      PACM2828
.
PACM2824  WRITE     HTMLFILE;"</td><td><table width=100%><tr><td>":
                             NODAYS,SP1,DAYFORMT,SP1,KEY70;
          MATCH     SP70,BEDDESC
          IF        !@EQUAL
             MOVE    ZERO,EXIT
             PROC    CETT0000         *certificat type
             IF        EXIT=1
               WRITE    HTMLFILE;"<font color=red>",BEDDESC,"</font>";
             ELSE
               WRITE    HTMLFILE;"<font color=green>",BEDDESC,"</font>";
             ENDIF
          ENDIF
.
PACM2828  PACK      KEY8,FCENT1,FYEAR1,FMON1,FDAY1,SP70
          REP       " 0",KEY8
          WRITE     HTMLFILE;"</td>":
                             "<td align=right>";
.
          MATCH     "Mechanical Ventilation",TDDESC
          GOTO      PACM3000 IF NOT EQUAL
          COMPARE   ZERO,LSTRATE
          GOTO      PACM3000 IF NOT EQUAL
.
          WRITE     HTMLFILE;"<img src=../images/DietIconTrans.gif ";
          GOTO      PACM4000
.
PACM3000  IF        CMXFLAG=0 & ECTRFLAG=2
            WRITE     HTMLFILE;"</td></tr></table>";
            GOTO      PACM5000             * No pay day
          ENDIF
.
          WRITE     HTMLFILE;"<img src=../images/CommentIcon.gif ";
.
PACM4000  WRITE     HTMLFILE;" class=ListIcon onclick='ShowStepdown(#"":
                             AURNO,"#",#"":
                             AADMNO,"#",#"":
                             KEY8,"#",#"":
                             ANS,"#",#"",SAVCONTR,"#")'></td></tr></table>";
.
PACM5000  WRITE     HTMLFILE;"</td><td>&nbsp;",SAVCONTR:  * contract id
                               "</td><td>&nbsp;":
                               "</td><td>&nbsp;";
.
          WRITE     HTMLFILE;"</td><td align=right>",PATLRATE;
          MOVE      PATLRATE,FORM12P2
          MULT      NODAYS,FORM12P2
          WRITE     HTMLFILE;"</td><td align=right>",FORM12P2:
                             "</td><td align=right>",PATLNGST:
                             "</td><td align=right>",PATLNTOT:
                             "</td></tr>"
          GOTO      PACM9999

PACM9999  RETURN
+
.*******************************************************************************
.         Write accommodation to DTR
.*******************************************************************************
PADT0000  MOVE      ZERO,TPAYTYPE
          MOVE      "P" TO TTYPEDEB
          MOVE      "DB",TTYPE
          MOVE      ONE,TRECTYPE      (1 = ACCOMMODATION)
          MOVE      ZERO,TCLAIM      *** set up as not yet claimed 3/9/86
.
          MOVE      PATLRATE,TPATAMTT
          MULT      NODAYS,TPATAMTT
          MOVE      PATLRATE,TPATAMTI
          MOVE      SP70,TMISGRP
.
          MOVE      PATLRATE,TPATAMTP        * patient daily rate
          MOVE      PATARATE,PTDTADPT        * patient additional rate
.
          MOVE      ZERO,TREBATE
          MOVE      ZERO,PTDTADRB
.
          MOVE      STRATEF,TPATAMTH         *** save original rate
          IF        CMXFLAG=1
            ADD       SPTTRADP,TPATAMTH
            IF        PTDTADPT<>0 | PTDTADRB<>0
              MOVE      SAVMSGRP,TMISGRP     * Category FI for additional rate
            ENDIF
          ENDIF
          MOVE      SPTTRLSP,PTDTLSPT
          MOVE      ZERO,PTDTLSRB
.
          MOVE      LSDESC1,PTDTLSD1
          MOVE      LSDESC2,PTDTLSD2
.
          MOVE      ZERO,PTDTGSTM
          MOVE      ZERO,PTDTGSTA
          MOVE      ZERO,PTDTGSTL
          MOVE      SP10,PTDTGSTC
.
          IF        IBCNUGST=2
            MATCH     SP70,PTCNAGST
            IF        !@EQUAL
              MOVE      ONE,PTDTGSTA       * GST Applicable
            ENDIF
            MOVE      TPATAMTT,PTDTGSTM
            MULT      GSTPCEN,PTDTGSTM     * GST amount
            DIV       "100.00",PTDTGSTM    * Divide by 100 to get wanted fig.
            MOVE      PTCNAGST,PTDTGSTC    * Accommodation GST code
.
            MOVE      PTDTLSPT,PTDTGSTL    * lumpsum
            MULT      GSTPCEN,PTDTGSTL     * GST amount
            DIV       "100.00",PTDTGSTL    * Divide by 100 to get wanted fig.
          ENDIF
          MOVE      SAMTG,TPATAMTG
          MOVE      NODAYS,TNODAYS
.
          MOVE      STWARD,TDWARD          * set up new DTRAN variables
          MOVE      STATYPE,TADMTYP        * when printing invoice
          MOVE      ACLAIM,TCLMTYP
          MOVE      STRBTYP,PTDTBTYP
.
          MOVE      ZERO,FORM62
          IF        CMXFLAG=1
            MOVE      SPTTRADP,FORM62
            ADD       SPTTRADR,FORM62
          ENDIF
          IF        FORM62 = 0
            MOVE      ZERO,PTDTCCU         * no additional charges
          ELSE
            MOVE      ONE,PTDTCCU          * flag for CCU
          ENDIF
.
          MOVE      ACLAIM,PTTRCLTY
          MOVE      CONTRCID,PTDTCNID      * Contract ID
          CALL      WRITETRN
.
          CALL      WRHR0000               * Write Rebate portion to HF Debtor
.
.         If we have extra's, then write a DTRA record for them too
.
          COMPARE   ZERO,STEXTRA
          GOTO      WADT9999 IF EQUAL
.
          MOVE      TOTXTRA,TPATAMTT
          MOVE      STEXTRA,TPATAMTI
          MOVE      ZERO,TPATAMTG
          MOVE      STEXTRA,TPATAMTH         *** save original rate
          MOVE      ZERO,TREBATE
          MOVE      TEDESC,TDDESC
          MOVE      ZERO,PTDTGSTM
          MOVE      ZERO,PTDTGSTA
          MOVE      ZERO,PTDTGSTL
          MOVE      SP10,PTDTGSTC
          MOVE      ZERO,TPAYTYPE
          MOVE      ZERO,PTDTADPT
          MOVE      ZERO,PTDTADRB
.
          IF        IBCNUGST=2
            MATCH     SP70,PTCNAGST
            IF        !@EQUAL
              MOVE      ONE,PTDTGSTA       * GST Applicable
            ENDIF
            MOVE      TPATAMTT,PTDTGSTM
            MULT      GSTPCEN,PTDTGSTM     * GST amount
            DIV       "100.00",PTDTGSTM    * Divide by 100 to get wanted fig.
            MOVE      PTCNAGST,PTDTGSTC    * Accommodation GST code
          ENDIF
.
          CALL      WRITETRN
+
PADT9999  RETURN
+
.*******************************************************************************
.         Write the Health fund portion amount to HF Debtor transaction
.*******************************************************************************
WRHR0000  CALL      CLPATHTR
          CALL      MDHT0000               * move patdtr fields to pathtraf
.
          MOVE      CNEXTINV,PTHTPINV      * Patient invoice number
          MOVE      TTRANSN1,PTHTPTRN      * Transaction number
.
          MOVE      SP70,PTHTTREF          * blank invoice number
          MOVE      TTRANSN1,PTHTTRN1
.
          MOVE      FNDLRATE,PTHTAMTT      * Health Fund portion
          MULT      NODAYS,PTHTAMTT
          MOVE      FNDLRATE,PTHTAMTH
          MOVE      PTHTAMTH,PTHTAMTI
.
          MOVE      ZERO,PTHTAMTP          * zero patient portion
          MOVE      ZERO,PTHTADPT          * zero additinal patient portion
.
          MOVE      FNDLRATE,PTHTRBAT      * rebate portion
          IF        CMXFLAG=1
            MOVE      PATHRATE,PTHTADRB      * HF Additional rate
            IF        PTHTADPT<>0 | PTHTADRB<>0
              MOVE      SAVMSGRP,PTHTMISG    * Category FI for additional rate
            ENDIF
          ENDIF
.
          MOVE      ZERO,PTHTLSPT
          MOVE      SPTTRLSR,PTHTLSRB
          MOVE      LSDESC1,PTHTLSD1
          MOVE      LSDESC2,PTHTLSD2
.
          MOVE      ZERO,PTHTGSTM
          MOVE      ZERO,PTHTGSTL
.
          IF        IBCNUGST=2
            MATCH     SP70,PTCNAGST
            IF        !@EQUAL
              MOVE      ONE,PTHTGSTA       * GST Applicable
            ENDIF
            MOVE      PTHTAMTT,PTHTGSTM
            MULT      GSTPCEN,PTHTGSTM     * GST amount
            DIV       "100.00",PTHTGSTM    * Divide by 100 to get wanted fig.
            MOVE      PTCNAGST,PTHTGSTC    * Accommodation GST code
.
            MOVE      PTHTLSRB,PTHTGSTL    * lumpsum
            MULT      GSTPCEN,PTHTGSTL     * GST amount
            DIV       "100.00",PTHTGSTL    * Divide by 100 to get wanted fig.
          ENDIF
          GOTO      WRHR2200
.
WRHR2000  CALL      NXTTRAN1
          MOVE      TTRANSN1,PTHTTRN1
.
WRHR2200  PACK      KEY22,PTHTADMN,PTHTTREF,PTHTTRN1
          CALL      RAPTHTR1
          COMPARE   ZERO,OVRCD
          GOTO      WRHR2000 IF EQUAL
.
          CALL      WRPTHTR1
WRHR9999  RETURN
.
.*******************************************************************************
.        Generate items
.*******************************************************************************
XENI0000 CALL      RSIT0000                 * Read item file
         BRANCH    EXIT,XENI2320
.
XENI2300 CALL      RKIT0000
         BRANCH    EXIT,XENI9000
.
XENI2320 CALL      SITM0000                 * Setup items
         BRANCH    NEXTFLAG,XENI2300
.
         MOVE      LINETOT,XLINETOT
         MOVE      LINEGST,XLINEGST
         PACK      DIM25,TDDESC,SP20
         PACK      DIM30,TDDESC,SP70
.
         ADD       PATLNGST,PATLNTOT
         ADD       PATLNTOT,PPAYABLE        * Patient balance payable
.
         ADD       PATLNGST,TOTPGST
.
         BRANCH    PROGFLAG,XENI2300,XENI2600,XENI2800,XENI8800
.
XENI2600 CALL      XSIM0000        * Display miscellaneous item
         GOTO      XENI2300        * read next DTR
.
.        Print miscellaneous item
.
.        Do not print item if patient amount is zero, only update pathtraf file
.        for item to be raised on HF invoice
.
XENI2800  IF        LINETOT=0
            CALL      UITM0000      * Update the debtor & Financial record
            GOTO      XENI2300
          ENDIF
.
          CALL      PITM0000      * Print Item line
.
          CALL      UITM0000      * Update the debtor & Financial record
.
.        Update the Fees Invoiced Summary file
.
XENI8800  CALL      UFIN0000        * Update Financial summary file
          GOTO      XENI2300        * read next DTR
.
XENI9000  COMPARE   THREE,PROGFLAG
          GOTO      XENI9999 IF NOT EQUAL   * not printing invoice
.
          CALL      RITM0000      * Remove all un-used items if any
.
          IF        PTCNINVL=24
            MATCH    SP70,SAVDATE1
            IF       !@EQUAL
              CALL     GJGA0000
            ENDIF
          ENDIF
          GOTO      XENI9999
.
XENI9999  RETURN
+
.*******************************************************************************
.        Display items
.*******************************************************************************
XSIM0000 COMPARE   ZERO,PATLNTOT
         GOTO      XSIM9999 IF EQUAL         * item with zero amount
.
         MOVE      SP70,DIM20
         MATCH     SP70,PTITTCER
         IF        !@EQUAL & !@EOS
           PACK      KEY5,CATCR,PTITTCER,SP70
           CALL      RDCODE1
           IF        OVRCD=0
.            IF        TCFORM6=1 | TCFORM6=2
             MATCH     "D",TCINDC1
             IF        @EQUAL
               MOVE      TDESC,DIM20
               MOVE      ONE,CERTINDC
             ENDIF
           ENDIF
         ENDIF
.
         MOVE      SP70,DIM21
         MATCH     "1",PTITIWRN
         IF        @EQUAL
           MOVE      "Prosthetic Required  ",DIM21
         ENDIF
.
         MOVE      TFMONTH,FORM2
         LOAD      MTHNAM3,FORM2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG:
                                      NSEP,NOCT,NNOV,NDEC
         MOVE      LINETOT,FORM12P2
         ADD       LINEGST,FORM12P2
         MOVE      FORM12P2,FORM72
         PACK      KEY4,TFCENT,TFYEAR
         REP       " 0",KEY4
.
         IF        TRECTYPE=2 & PMMISERV=0
           MOVE      ONE,PMMISERV
         ENDIF
.
         MATCH     "1",PMMIMVBR
         IF        @EQUAL
           WRITE     HTMLFILE;"<tr><td>",TFDAY,SP1,MTHNAM3,SP1,KEY4,"</td>":
                              "<td>",MVDAYS,"x ",DIM30,"</td>";
.
           IF        INVTYPE=0
             WRITE     HTMLFILE;"<td>&nbsp;</td>";
           ENDIF
.
           WRITE     HTMLFILE;"<td>",TITEMNO,"</td>";
.
.          MV will have no quantity if zero MV days
.
           IF        INVTYPE=0
             IF        MVDAYS<>0
               WRITE     HTMLFILE;"<td>",MVDAYS,"</td>";
             ELSE
               WRITE     HTMLFILE;"<td>&nbsp;</td>";
             ENDIF
           ENDIF
.
           MOVE      PMMIAMTP,FORM12D2
           ADD       PMMIRBAT,FORM12D2
           WRITE     HTMLFILE;"<td align=right>",FORM12D2,"</td>";
           GOTO      XSIM8000
         ELSE
           WRITE     HTMLFILE;"<tr><td>",TFDAY,SP1,MTHNAM3,SP1,KEY4;
.
.          If this item is from Community billing, check if this item is one
.          of the Regime
.
           COMPARE   ONE,ISBLFLAG
           GOTO      XSIM2000 IF NOT EQUAL
.
           MATCH     "1",PMMIINCT
           GOTO      XSIM2000 IF NOT EQUAL
.
.          Display the To date for item from the Regime
.
           PACK      KEY8,TFCENT,TFYEAR,TFMONTH,TFDAY
           REP       " 0",KEY8
           IF        PMMISERV<>0
             MOVE      KEY8,ISBLDATE
             ADD       PMMISERV,ISBLDATE
             MOVE      ISBLDATE,KEY8
           ENDIF
           UNPACK    KEY8,XCENT,XYEAR,XMON,XDAY
           MOVE      XCENT,TFCENT
           MOVE      XYEAR,TFYEAR
           MOVE      XMON,TFMONTH
           MOVE      XDAY,TFDAY
           PACK      KEY4,TFCENT,TFYEAR
           REP       " 0",KEY4
           MOVE      TFMONTH,FORM2
           LOAD      MTHNAM3,FORM2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG:
                                        NSEP,NOCT,NNOV,NDEC
           WRITE     HTMLFILE;" to ",TFDAY,SP1,MTHNAM3,SP1,KEY4:
                              "</td>";
           GOTO      XSIM4000
.
XSIM2000   WRITE     HTMLFILE;"</td>";
.
XSIM4000   MATCH     SP70,DIM20
           IF        @EQUAL | @EOS
             WRITE     HTMLFILE;"<td>",DIM30;
           ELSE
             STRIP     DIM20
             MOVE      ZERO,EXIT         * check certificate return parameter
             PROC      CCERT000          * Check Certificate record
             IF        EXIT=0
               WRITE     HTMLFILE;"<td>",DIM30:
                                  " (<font color=green>",DIM20,"</font>)";
             ELSE
               WRITE     HTMLFILE;"<td>",DIM30:
                                  " (<font color=red>",DIM20,"</font>)";
             ENDIF
           ENDIF
           MATCH     SP70,DIM21
           IF        !@EQUAL
             WRITE     HTMLFILE;" (<font color=red>",DIM21,"</font>)</td>";
           ELSE
             WRITE     HTMLFILE;"</td>";
           ENDIF
.
           IF        INVTYPE=0
             WRITE     HTMLFILE;"<td>&nbsp;",MBSCONTR,"</td>";
           ENDIF
.
           MOVE      "0",KEY1              * Set 'No' to Change item desc.
           MOVE      "0",DIM1              * Set 'No' to Change item amount
           IF        TRECTYPE=3
             MATCH     "2",PMMIITYP        * Exploding item?
             IF        !@EQUAL
               CALL      GMIS0000                * recalculate misc.
.
               IF        TRECTYPE=3
                 COMPARE   ONE,MINDIC
                 IF        @EQUAL
                   MOVE      "1",KEY1          * Set 'Yes' to Change item desc.
                 ENDIF
               ENDIF
               IF        TRECTYPE=3
                 MATCH     "Y",PTMCKREB
                 IF        @EQUAL
                   MOVE      "1",DIM1          * Set 'Yes' to Change item amount
                 ENDIF
               ENDIF
             ENDIF
           ENDIF
.
           WRITE     HTMLFILE;"<td><img src=../images/EditIcon.gif ":
                              " class=ListIcon onclick='DeleteItem(#"":
                              AURNO,"#",#"":
                              AADMNO,"#",#"":
                              PMMITRAN,"#",#"":
                              KEY1,"#",#"":
                              DIM1,"#",#"":
                              PMMIAMTP,"#",#"",PMMIRBAT,"#")'>":
                              TITEMNO,"</td>";
         ENDIF
.
         MOVE      PMMIAMTP,FORM12P2
         IF        PMMISERV<>0
           MULT      PMMISERV,FORM12P2
           WRITE     HTMLFILE;"<td>",PMMISERV,"</td>";
         ELSE
           WRITE     HTMLFILE;"<td>&nbsp;</td>";
         ENDIF
.
         WRITE     HTMLFILE;"<td align=right>",PMMIAMTP,"</td>":
                            "<td align=right>",FORM12P2,"</td>":
                            "<td align=right>",PATLNGST,"</td>":
                            "<td align=right>",PATLNTOT,"</td>":
                            "</tr>"
         GOTO      XSIM9999
.
XSIM8000 MOVE      PMMIAMTP,FORM12P2
         IF        PMMISERV<>0
           MULT      PMMISERV,FORM12P2
         ENDIF
.
         WRITE     HTMLFILE;"<td align=right>",FORM12P2,"</td>":
                            "<td align=right>",PATLNGST,"</td>":
                            "<td align=right>",PATLNTOT,"</td>":
                            "</tr>"
XSIM9999  RETURN
+
.*******************************************************************************
.        Move DTR fields to HF debtor transaction (pathtraf)
.*******************************************************************************
MDHT0000  MOVE      TADMNO,PTHTADMN       * Admission Number
          MOVE      TTRANSN1,PTHTTRN1     * Transaction Number
          MOVE      TTYPEDEB,PTHTTDEB     * Type of Debt
          MOVE      TDCODE,PTHTDCOD       * Code for HF,Insurance
          MOVE      TPATAMTT,PTHTAMTT     * Gross Total Amount
          MOVE      TPATAMTG,PTHTAMTG     * Government Portion
          MOVE      TPATAMTH,PTHTAMTH     * Original Rate before any Disc.Allw
          MOVE      TPATAMTI,PTHTAMTI     * Daily Rate amount
          MOVE      TPATAMTP,PTHTAMTP     * Patient Portion
          MOVE      TFCENT,PTHTFCEN       * Century On/From
          MOVE      TFYEAR,PTHTFYER       * Year    On/From
          MOVE      TFMONTH,PTHTFMON      * Month   On/From
          MOVE      TFDAY,PTHTFDAY        * Day     On/From
          MOVE      TTCENT,PTHTTCEN       * Centrury, To date(Accom.rec.only)
          MOVE      TTYEAR,PTHTTYER       * Year, To date(Accom.record only)
          MOVE      TTMONTH,PTHTTMON      * Month,To date(Accom.record only)
          MOVE      TTDAY,PTHTTDAY        * To date(Accom.record only)
          MOVE      TTYPE,PTHTTTYP        * Transaction type
          MOVE      TITEMNO,PTHTITEM      * Item Number
          MOVE      TREF,PTHTTREF         * Reference (Invoice Number)
          MOVE      TPAYTYPE,PTHTPAYT     * Payment Method
          MOVE      TRECEIPT,PTHTRCPT     * Receipt Number
          MOVE      TDDESC,PTHTDESC       * Description
          MOVE      TINVPRT,PTHTINVP      * Patient Invoice printed(0=No,1=Yes)
          MOVE      TRECTYPE,PTHTRECT     * Record Type
          MOVE      TNODAYS,PTHTNDAY      * No of days (Accom.only)
          MOVE      TCLAIM,PTHTCLMD       * Claim(0=No,1=Yes)
          MOVE      TREBATE,PTHTRBAT      * Estimated Fund Rebate
          MOVE      TDOCTA,PTHTDOCT       * Treating Doctor
          MOVE      TSERVS,PTHTSERV       * Number of Services
          MOVE      TDOCTO,PTHTDOCO       * Operating Doctor
          MOVE      TSESSION,PTHTSESS     * Session (Theatre Only)
          MOVE      TMISGRP,PTHTMISG      * Misc.Group (Cat FI)
          MOVE      TDEPTYP,PTHTDEPT      * Deposit Type
          MOVE      TDWARD,PTHTWARD       * Ward of Accommodation record
          MOVE      TCLMTYP,PTHTCLMT      * Claim type when printing Invoice
          MOVE      TADMTYP,PTHTADMT      * Adm.Type when printing Invoice
          MOVE      TBATCHN,PTHTBTCH      * Batch Number for Misc/Cash/Journal
          MOVE      TNINVS,PTHTNINV       * Number of Invoices to be printed
          MOVE      PTDTLSPT,PTHTLSPT     * Lumpsum amount Patient Portion
          MOVE      PTDTLSRB,PTHTLSRB     * Lumpsum amount Rebate Portion
          MOVE      PTDTDTYP,PTHTDTYP     * Day type
          MOVE      PTDTDES2,PTHTDES2     * Description 2
          MOVE      PTDTEPSD,PTHTEPSD     * Mechanical Ventilation Billing Rec?
          MOVE      PTDTCCU,PTHTTCCU      * Flag for Additional Charge(Accom.)
          MOVE      PTDTGSTA,PTHTGSTA     * Is GST applicable to this time ?
          MOVE      PTDTGSTC,PTHTGSTC     * GST Payable code
          MOVE      PTDTGSTM,PTHTGSTM     * GST Amount
          MOVE      PTDTEFFD,PTHTEFFD     * Date Item assigned to Fees Inv.
          MOVE      PTDTGSTL,PTHTGSTL     * GST Lumpsum Amount
          MOVE      PTDTTIME,PTHTTTIM     * Time of ORD message (hh:mm:ss)
          MOVE      PTDTCRST,PTHTCRST     * Credit Note Status
          MOVE      PTDTUNIQ,PTHTUNIQ     * Unique Id for Theatre booking
          MOVE      PTDTBTYP,PTHTBTYP     * Bed Type (Cat BT) Accom.only
          MOVE      PTDTCNTR,PTHTCNTR     * Contract/Health Fund
          MOVE      PTDTSUNQ,PTHTSUNQ     * Subsequence Proc.Uniq.No
          MOVE      PTDTBTCH,PTHTBATC     * Batch Details
          MOVE      PTDTADPT,PTHTADPT     * Additional rate Pat Portion C/P
          MOVE      PTDTADRB,PTHTADRB     * Additional rate Reb Porton C/P
          MOVE      PTDTCNID,PTHTCNID     * Contract Id
          MOVE      PTDTLSD1,PTHTLSD1     * Lumpsum Descripttion 1
          MOVE      PTDTLSD2,PTHTLSD2     * Lumpsum Description 2
          MOVE      PTDTTMNO,PTHTTMNO     * Patient MBS Team (oprpmbaf)
          MOVE      PTDTPCOD,PTHTPMBS     * Patient MBS Counter (oprpmbaf)
.
MDHT9999  RETURN
+
