.************************************************************************
.*  Procedure: DGCLICUA  -  Send New Alias details after Update         *
.*                                                                      *
.*  Author   : Rod  (08/03/96)                                          *
.*                                                                      *
.*  Requires : PURNO    -  U/R Number                                   *
.*             KEY115   -  U/R, Billing No., Surname, Given Name, DOB,  *
.*                         Sex.                                         *
.*             FORM1    -  (1=Add 2=Delete)                             *
.*                                                                      *
.*  Mods     :                                                          *
.*                                                                      *
.************************************************************************
.*  V11.04.01 09/01/2024 Thanh T           TSK 0936263                  *
.*            Added OPEN/CLOSE PMSQPXA1 index                           *
.************************************************************************
.* V10.06.01   12/03/2015  Steve Armstrong  CAR 314108                  *
.*             Removed definition of PTCGDATE as PATCGPFD is no longer  *
.*             in use.                                                  *
.************************************************************************
.* V10.03.01   26/07/2013  Steve Armstrong  CAR 287787                  *
.*             Mods to save and reload PTMASNAM                         *
.************************************************************************
.* V10.02.01   12/07/2011  Steve Armstrong  CAR 240722                  *
.*             Mods for changes to PATGSRFD variables                   *
.************************************************************************
.* V9.08.01    05/01/2007  Steve Armstrong  CAR 130015                  *
.*             Mods to ignore CIS interface.                            *
.************************************************************************
.* V9.04.01    07/07/2005  Davin            CAR 64087                   *
.*             Mods to handle CIS interface.                            *
.* V9.02.03    13/01/2003  Davin  SRF 23333                             *
.*             Changed to write directly to patient holding file        *
.* V9.02.02    02/10/2001  Davin & MikeL                                *
.*             Mods to use message holding file                         *
.*             Rename DGCLIUAL to DGCLICUA                              *
.* V9.02.01    26/09/2001 (25/06/2001)  Mike Laming                     *
.*             Add PSEX & PBDATE to Alias Details for WEB Based system  *
.* V5.         08/07/1999  Nicole Harrington                            *
.*             Added IF statements around display for web programs      *
.*             17/04/1997  Steve Armstrong                              *
.*             Mods to use system parameter for broadcast file name     *
.*                                                                      *
.************************************************************************
          DEFPROC   DGCLICUA
.
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
          INC       PMSQPTFD/INC
          INC       PMSPX2FD/INC
.
CISMFLAG  FORM      1             * CIS message flag
.                                    0 = don't send HL7 CIS message
.                                    1 = send HL7 CIS message
IBAMFLAG  FORM      1             * IBA proprietary message flag
.                                    0 = don't send IBA proprietary message
.                                    1 = send IBA proprietary message
.
MESSAGID  DIM       20                          * message id
SAVPSNAM  DIM       20                          * save vars
SAVPGNAM  DIM       25
SAVLSNAM  DIM       40
SAVPSEX   DIM       1
SAVPBDAT  DIM       8
.
ZED30     INIT      "~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~"
.
DGCLI000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,SEVENTY9;*116,PTCNDGUA
.
          MOVE      ZERO,CISMFLAG                * set default interface flag
.         MATCH     "1",IBCNUCIS                 * using CIS interface ?
.         IF        @EQUAL
.             MOVE      ONE,CISMFLAG             * yes- set flag for CIS message
.         ENDIF
.
.         Check if we are sending any messages.
.
          IF        PTCNDGUA <> 1 & CISMFLAG <> 1
            GOTO      DGCLI999
          ENDIF
.
.         We are sending IBA Proprietary and/or CIS HL7 messages, so
.         first open the relevant holding tables
.
          MOVE      ZERO,IBAMFLAG
          IF        PTCNDGUA = 1
            OPEN      HL7INB01,"hl7inbaf"
            MOVE      ONE,IBAMFLAG
          ENDIF
          IF        CISMFLAG = 1
            OPEN      HL7CISA1,"hl7cisaf"
          ENDIF
          OPEN      PMSQPTA1,"pmsqptaf"
          OPEN      PMSQPXA1,"pmsqpxaf"
.
          CALL      DGMESSID                    * Get Message ID
.
.         Write message
.
CUAM0000  MOVE      PSNAME,SAVPSNAM             * save master file vars
          MOVE      PGNAME,SAVPGNAM
          MOVE      PTMASNAM,SAVLSNAM
          MOVE      PSEX,SAVPSEX
          MOVE      PBDATE,SAVPBDAT
.
.         Write a record to pmsqptaf (holding patient details)
.
          MOVE      GSRSNAM,PSNAME              * send patgsrnf vars
          MOVE      GSRSNAM,PTMASNAM
          MOVE      GSRGNAM,PGNAME
          STRIP     PGNAME
          ENDSET    PGNAME
          APPEND    SP1,PGNAME
          APPEND    PTGSGNM2,PGNAME
          RESET     PGNAME
          MOVE      GSRGNAM,PMPXFNAM
          MOVE      PTGSGNM2,PMPXSNAM
          MOVE      GSRSEX,PSEX
          MOVE      GSRDOB,PBDATE
.
          MOVE      MESSAGID,PMQPMESI
          MOVE      SP8,PMQPOURN
          MOVE      FORM1,CSALIAS
          PACK      PMQPSPAR,SP70,SP70
          CALL      WRPMQPT1
.
          MOVE      SAVPSNAM,PSNAME             * restore master file vars
          MOVE      SAVLSNAM,PTMASNAM
          MOVE      SAVPGNAM,PGNAME
          MOVE      SAVPSEX,PSEX
          MOVE      SAVPBDAT,PBDATE
.
          COMPARE   ONE,PTCNDGUA                 * sending proprietary message?
          IF        @EQUAL
            MOVE      "CUA",H7INMETP
            CALL      WRINB000                   * yes - write hl7inbaf record
          ENDIF
.
          CLOSE     HL7INB01
          CLOSE     HL7CISA1
          CLOSE     PMSQPTA1
          CLOSE     PMSQPXA1
.
DGCLI999  GOTO      DGCLIEND                    * end procedure
.
.         I/O Routines
.
          INC       BRHLCOMN
          INC       CLPMSQPX
.
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       PMSQPTIO/INC
          INC       IBAOVRIO/INC
.
DGCLIEND  ENDPROC
+
