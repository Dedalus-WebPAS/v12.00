. *----------------------------------------------------------------------
. * Include Name  :   PMSRDITM.PBL
. *
. * Function      :   CGI variables for PMSRDIFD - pmsrdiaf.dat
. *
. * Includes Needed : PMSRDIFD - ABF Radiotherapy/Dialysis codes
. *                   PMSRDIIO - IO for ABF Radiotherapy/Dialysis codes
. * Modifications :
. *
. *----------------------------------------------------------------------
.
.------------------------------------------------------------------------
.   Clear Parameters for ABF Radiotherapy/Dialysis codes
.------------------------------------------------------------------------
.
CLRRDI00  MOVE      SP70,PMRDI001
          MOVE      SP70,PMRDI002
          MOVE      SP70,PMRDI003
          MOVE      SP70,PMRDI004
          RETURN
.
.------------------------------------------------------------------------
. ABF Radiotherapy/Dialysis codes fields
.------------------------------------------------------------------------
PMRD0000  BRANCH    FLDITMNO,PMRD0100,PMRD0200,PMRD0300,PMRD0400
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      PMRD9999
.
PMRD0100  UNPACK    DIM127,D1,KEY1,DIM127        * Contract ID
          MOVE      KEY1,F1
          BRANCH    F1,PMRD0110
.
          WRITE     HTMLFILE;PMRICNTR;
          GOTO      PMRD9999
.
PMRD0110  PACK      KEY6,PMRICNTR,SP70
          CALL      RDPMCID1
          BRANCH    OVRCD,PMRD9999
.
          WRITE     HTMLFILE;PMCIDESC;           * Contract ID Description
          GOTO      PMRD9999
.
PMRD0200  UNPACK    DIM127,D1,KEY1,DIM127
          MATCH     SP70,PMRICTYP
          GOTO      PMRD9999 IF EQUAL
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,PMRD0210
.
          WRITE     HTMLFILE;PMRICTYP;           * Type of code
          GOTO      PMRD9999
.
PMRD0210  MATCH     " 1",PMRICTYP
          IF        @EQUAL
            WRITE     HTMLFILE;"Radiotherapy";
          ELSE
            MATCH     " 2",PMRICTYP
            IF        @EQUAL
              WRITE     HTMLFILE;"Dialysis";
            ENDIF
          ENDIF
          GOTO      PMRD9999
.
PMRD0300  UNPACK    DIM127,D1,KEY1,DIM127
          MATCH     SP70,PMRISTYP
          GOTO      PMRD9999 IF EQUAL
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,PMRD0310
.
          WRITE     HTMLFILE;PMRISTYP;           * Coding System
          GOTO      PMRD9999
.
PMRD0310  MATCH     "1",PMRISTYP
          IF        @EQUAL
            WRITE     HTMLFILE;"ICD10";
          ENDIF
          GOTO      PMRD9999
.
PMRD0400  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,PMRD0410
.
          WRITE     HTMLFILE;PMRIICDC;           * ICD10 Code
          GOTO      PMRD9999
.
PMRD0410  PACK      ICD10DES,SP70,SP70,SP70
          PACK      KEY9,PMRIICDC,SP30
          CALL      RDPTICO1                     * Read ICD10 (current date)
          IF        OVRCD=1
            CALL      RD10T9O1                   * Read ICD9
            BRANCH    OVRCD,PMRD9999
          ENDIF
          IF        !@EOS & !@EQUAL
            PACK      ICD10DES,PT0ODSC2
          ELSE
            PACK      ICD10DES,ODESC
          ENDIF
.
          WRITE     HTMLFILE;ICD10DES;
          GOTO      PMRD9999
.
PMRD9999  RETURN
.
.------------------------------------------------------------
. Set Parameters for ABF Radiotherapy/Dialysis codes
.------------------------------------------------------------
SPPRD000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPPRD001,SPPRD002,SPPRD003,SPPRD004
.
          MOVE      ONE,EXIT
          GOTO      SPPRD999
.
SPPRD001  MOVE      QRYDATA,PMRDI001
          PACK      PMRDI001,PMRDI001,SP70
          GOTO      SPPRD999
.
SPPRD002  MOVE      QRYDATA,PMRDI002
          PACK      PMRDI002,PMRDI002,SP70
          RJUSTIFY  PMRDI002
          GOTO      SPPRD999
.
SPPRD003  MOVE      QRYDATA,PMRDI003
          PACK      PMRDI003,PMRDI003,SP70
          GOTO      SPPRD999
.
SPPRD004  MOVE      QRYDATA,PMRDI004
          PACK      PMRDI004,PMRDI004,SP70
          GOTO      SPPRD999
.
SPPRD999  RETURN
.
.------------------------------------------------------------------------
.         Move work variables to ABF Radiotherapy/Dialysis codes file variables
.------------------------------------------------------------------------
MVPMRD00  MATCH     PMRDI001,SP70
          IF        !@EQUAL
            MOVE      PMRDI001,PMRICNTR
          ENDIF
.
          MATCH     PMRDI002,SP70
          IF        !@EQUAL
            MOVE      PMRDI002,PMRICTYP
          ENDIF
.
          MATCH     PMRDI003,SP70
          IF        !@EQUAL
            MOVE      PMRDI003,PMRISTYP
          ENDIF
.
          MATCH     PMRDI004,SP70
          IF        !@EQUAL
            MOVE      PMRDI004,PMRIICDC
          ENDIF
.
MVPMRD99  RETURN
