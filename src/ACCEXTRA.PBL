.******************************************************************************
.*   Module     :  ACCEXTRA                                                   *
.*                 Create NZ ACC XML Extract file ( Accident Insurance )      *
.*                 Used in PATWEB89                                           *
.*                                                                            *
.*   Parameters :  PTCLM011  ACC Number  (=> ACCCLMNO)                        *
.*                 URNUMBER  UR Number                                        *
.*                 ADDMISNO  Visit Number                                     *
.*                                                                            *
.* Modifications:                                                             *
.*----------------------------------------------------------------------------*
.*        V11.01.07 20/12/2021  Don Nguyen     TSK 0875549                    *
.*                  Modified to use PTCDUDF8 (User Defined Field 8) for HPI   *
.*                  Organisation Number.                                      *
.*        V11.01.06 01/12/2021  Don Nguyen     TSK 0875549                    *
.*                  Write Claim Lodgement record (WRTLODGE) before API POST   *
.*        V11.01.05 12/11/2021  Don Nguyen     TSK 0875549                    *
.*                  Modified validation for "workCapacity" and adjusted       *
.*                  "incapacityDateRange" values accordingly.                 *
.*                  Corrected "returnToNormalWorkDate" value.                 *
.*                  Added both "Fullyunfitforwork" and "Fitforselectedwork"   *
.*                  sections when both Full Incapacity and Restricted Duties  *
.*                  (Alternative Work) exists.                                *
.*        V11.01.04 22/10/2021  Don Nguyen     TSK 0875549                    *
.*                  Modified JSNCLM00 to omit "workCapacity" if patient is    *
.*                  currently not in paid employment.                         *
.*        V11.01.03 13/09/2021  Don Nguyen     TSK 0875549                    *
.*                  Modified code (APIPOS00) to write a new 'acclodaf' record *
.*                  for API (JSON) claim error.                               *
.*        V11.01.02 17/08/2021  Don Nguyen     TSK 0875549                    *
.*                  Added new routines to create the JSON claim data and      *
.*                  submit it to the API via 'patweb60.us1'.                  *
.*        V11.01.01 02/07/2021  Thanh T        TSK 0901286                    *
.*                  Changed XMLCLM22 to use address and postcode from         *
.*                  pmscexaf instead of patmx1af when the new contacts        *
.*                  parameter is turned on                                    *
.*----------------------------------------------------------------------------*
.*        V11.00.01 27/03/2020  Jill Parkinson Task 0879163                   *
.*                  Added read of category G to use indicator 2 for sex       *
.*                  27/03/2020  Jill Parkinson Task 0889904                   *
.*                  Added in strip of NHMAETH                                 *
.*----------------------------------------------------------------------------*
.*        V10.14.01 13/09/2019  Richa Phogat   TSK 0870756                    *
.*                  Added condition for accdiaaf.ACDICSYC=3(READ) under       *
.*                  when DIGHDPCD=0                                           *
.*        V10.13.01 13/11/2018  Jill Parkinson Task 0866132                   *
.*                  Mods to write INJ2006 seperately to value to allow 255    *
.*        V10.11.14 19/12/2017  Richa Phogat   TSK 0850144                    *
.*                  Updated Claim_Patient_Address to display only 30 char for *
.*                  Postal Address Lines 2,3 and 4                            *
.*        V10.11.13 07/12/2017  Richa Phogat   TSK 0849400                    *
.*                  Fixed XMLCLM31 to display all the fields with blank value *
.*        V10.11.12 05/12/2017  Richa Phogat     TSK 0849400                  *
.*                  Remove line feed from Alt_Work_Restrictions field         *
.*        V10.11.11 05/12/2017  Richa Phogat   TSK 0849400                    *
.*                  Populate Paid_Employment_Ind and Occupation_Code fields   *
.*                  correctly                                                 *
.*                  - Remove line feed for Cause_of_Injury, Injury_Comments   *
.*                    and Suggested Treatment                                 *
.*        V10.11.10 14/11/2017  Don Nguyen     TSK 0816814                    *
.*                  Modified XMLCLM65 to use new SNOMED mapping table values  *
.*                  where applicable.                                         *
.*                  09/11/2017  Peter Vela       TSK 0847214                  *
.*                  Fixed defaults for Continue Normal Work Ind and           *
.*                  Alternative Work Ind                                      *
.*        V10.11.09 25/10/2017  Peter Vela     TSK 0843396                    *
.*                  Added new functionality for PTCNACTP                      *
.*        V10.11.08 09/10/2017  Richa Phogat   TSK 0845211                    *
.*                  Reverted changes done for task 0842718 by deleting display*
.*                  'Not Known' when Ethnicity code is 61                     *
.*        V10.11.07 15/07/2017  Richa Phogat   TSK 0841373                    *
.*                  Added condition to write Mobile Phone in ACC XML file if  *
.*                  available otherwise Home Phone                            *
.*        V10.11.06 14/09/2017  Richa Phogat   TSK 0839897                    *
.*                  Display Diagnosis code value depending on the value of new*
.*                  parameter added 'PTCNDIGN'                                *
.*        V10.11.05 12/09/2017  Richa Phogat   TSK 0842717                    *
.*                  Added Patient Postal Address under <Claim_Patient_Address>*
.*        V10.11.04 06/09/2017  Richa Phogat   TSK 0844400                    *
.*                  Added condition to not display any other fields value in  *
.*                  <Work_Capacity> section when <Continue_Normal_Work_Ind>   *
.*                  has value 'Y'                                             *
.*        V10.11.03 17/08/2017  Richa Phogat   TSK 0842712                    *
.*                  Removed PMHCTITL to populate value for <Provide_Title_Code*
.*        V10.11.02 07/08/2017  Richa Phogat   TSK 0842718                    *
.*                  Added code to display 'Not Know' value for Ethnicity Code *
.*                  #61                                                       *
.*        V10.11.01 03/08/2017  Richa Phogat   TSK 0832291                    *
.*                  Change agency description from PTCNHADF parameter to      *
.*                  description of code from CAT, HA pathspaf.pthsagn for     *
.*                  Multihospital                                             *
.*        V10.10.01 22/05/2017  Richa Phogat   TSK 0838700                    *
.*                  Append Hopital ID to the end of directory in ACPREP00     *
.*        V10.09.01 06/12/2016  Jill Parkinson TSK 0822329                    *
.*                  Added gradual process injury and admitted to hospital     *
.*        V10.08.01 22/11/2016  Nicole Torrisi TSK 0814468                    *
.*                  Changed to no longer send post code field                 *
.*        V10.00.02 17/08/2010  Mike Laming   CAR 229732                      *
.*                  Add Employment status checks - if "Not in Paid Employment"*
.*                  do not write "Occupation" or any "Employment" data        *
.*        V10.00.01 31/08/2010  Mike Laming   CAR 228831                      *
.*                  Change "Occupation" to use Cat."Pn" PTCDNHCP (not THCSCOD)*
.*        V9.10.07  19/09/2008  Mike Laming   CAR 178850                      *
.*                  Mod to WRK2001 & WRK2002 to change blank to null          *
.*        V9.10.06  18/09/2008  Mike Laming   CAR 178850                      *
.*                  Mod to "Alterative_Work_Ind" WRK2002 (set to " " not "N") *
.*        V9.10.05  30/08/2008  Mike Laming   CAR 153670                      *
.*                  Mods to Ethnicity & Injury Tag as per e-mail (01/08/2008) *
.*        V9.10.04  30/08/2008  Mike Laming   CAR 153670                      *
.*                  Mods as per Jon's e-mail (29/07/2008)                     *
.*        V9.10.03  02/07/2008  Mike Laming   CAR 153670                      *
.*                  Mods as per Jon's e-mail                                  *
.*        V9.10.02  01/07/2008  Mike Laming   CAR 153670                      *
.*                  Mods as per Jon's e-mail                                  *
.*        V9.10.01  02/04/2008  Mike Laming   CAR 153670                      *
.*                  Added fields ACLOURNO, ACOLUDAT, ACOLUTIM & ACOLWUID      *
.*        V9.09.00  06/03/2008  Mike Laming   CAR 153670                      *
.*                  New routine                                               *
.*                                                                            *
.******************************************************************************
.
ACCEXTRA
ACCXML00  MOVE      ZERO,EXIT
          MOVE      ZERO,CLMDONE
.
          OPEN      ACCLODA1,"acclodaf"          * to go into PATWEB89 sometime?
....      OPEN      PMSTLEA1,"pmstleaf"          * open Patient Titles table
.
          READ      CONTROLF,TEN3;*1,CHADD1
          READ      CONTROLF,HUND12;*179,PTCNACPT
          READ      CONTROLF,HUND14;*1,PTCNACSN,*61,PTCNACVR
          READ      CONTROLF,HUND17;*215,PTCNDIGN,*219,PTCNACTP
          READ      CONTROLF,EIGHTY8;*56,PTCNHADF
          READ      CONTROLF,TWENTY1;*224,PTCNEIOC
          READ      CONTROLF,TEN;*54,CFILENO     * Hospital File Number
.
.         Get the ACC Transmission Method (XML/API), API Endpoint & API Key
          READ      CONTROLF,HUND29;*92,PTCNACTM,*93,PTCNACAE,*194,PTCNACAK
.
.                                           * Read the ACC Claim record
          PACK      ACCCLMNO,PTCLM011,SP20
          PACK      KEY44,ACCCLMNO,URNUMBER,ADMISSNO,SP70
          CALL      RDSWCOM3
          CALL      RDKWCOM3
          BRANCH    OVRCD,ACCX8000          * ACC Claim record not found
.
          PACK      DIM36A,WCCLMNO,PTWCURNO,DWCADMNO
          MATCH     DIM36A,KEY44
          GOTO      ACCX8000 IF NOT EQUAL
.
          MATCH     "1",PTCNACTM            * ACC Transmission Method = API
          IF        @EQUAL
            CALL      JSPREP00              * set up and prep JSON file
            CALL      JSBEGN00              * write the JSON headers
          ELSE
            CALL      ACPREP00              * set up and prep the extract file
            CALL      XMLBGN00              * write the xml header record
          ENDIF
.
.                                           * read all master data for ACC Numb.
          CALL      CLNHIMAS
          CALL      CLPATMAS
...       CALL      CLPATVIS
          CALL      CLPMSVX1
.
          PACK      KEY8,URNUMBER,SP8
          CALL      RDNHMAS2                * Read NHI Master
          BRANCH    OVRCD,ACCX8100
.
          PACK      KEY8,URNUMBER,SP8
          CALL      RDMAST1                 * Read PMI Master
.
          PACK      KEY8,ADMISSNO,SP8
...       CALL      RDVISA1                 * Read Visit record
          CALL      RDPMVX11                * Read Visit extension
.
          CALL      RDPMWX11                * Read ACC Claim extension
.
          MATCH     "1",PTCNACTM            * ACC Transmission Method = API
          IF        @EQUAL
            CALL      JSNCLM00              * generate Claim JSON object
            GOTO      ACCX9200
          ELSE
            CALL      XMLCLM00              * generate Claim XML
            GOTO      ACCX9000
          ENDIF
.
          GOTO      ACCX9999
.
.
ACCX8000  MOVE      "ACC Information does not Exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ACCX9999
.
ACCX8100  MOVE      "NHI Master not Found",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ACCX9000
.
ACCX9000  MOVE      ZERO,F1 
.                                           * end the EClaim Message
          WRITE     EXTFILE1,SEQ;"</EClaim_Message>"
          CLOSE     EXTFILE1
          GOTO      ACCX9900
.
ACCX9200  MOVE      ZERO,F1
          WRITE     JSONFIL1,SEQ;JSBRKEND   * Close off JSON obj with "}"
          CLOSE     JSONFIL1
          GOTO      ACCX9900
.
ACCX9900  IF        CLMDONE = 1
            CALL      WRTLODGE

            MATCH     "1",PTCNACTM          * ACC Transmission Method = API
            IF        @EQUAL
              CALL      APIPOS00            * POST the claim (JSON) via the API
            ENDIF
          ENDIF
ACCX9999  RETURN
+
.******************************************************************************
.*                                  ACCXMT00                                  *
.*                     Update Transmission Status flag                        *
.******************************************************************************
ACCXMT00  MOVE      ZERO,EXIT
          MATCH     ACCLO002,Z70
          GOTO      ACCXMT99 IF EQUAL
.
          OPEN      ACCLODA1,"acclodaf"
.
ACCXMT10  PACK      KEY20,ACCNUMBR,SP20
          CALL      RDACLOD1
          BRANCH    OVRCD,ACCXMT99
.
          CALL      IBACLOCK
.
          MATCH     "1",ACCLO002            * new status = 'Sent' ?
          IF        @EQUAL
            MATCH     "1",ACLOSTAT
            IF        !@EQUAL
              PACK      ACLOSDAT,CCC,CYY,CMM,CDD
              REP       " 0",ACLOSDAT
            ENDIF      
          ENDIF
.
          MOVE      ACCLO002,ACLOSTAT
          PACK      ACLOUDAT,CCC,CYY,CMM,CDD
          REP       " 0",ACLOUDAT 
          UNPACK    CTIMEIS,ACLOUTIM,DIM4A
          REP       " 0",ACLOUTIM
          MOVE      WBSEUID,ACLOWUID
.
          CALL      UPACLOD1

ACCXMT99  RETURN
+
.******************************************************************************
.*                                  ACPREP00                                  *
.*                     Prep extract files and reset variables                 *
.******************************************************************************
ACPREP00  UNPACK    ACCCLMNO,DIM8
          SQUEEZE   DIM8
          PACK      EXTFNAME,DIM8,DOT,XML
          STRIP     PTCNACPT
          PACK      PATHNAME,SP30,SP30,SP30,SP10
          CLEAR     PATHNAME
.
          COMPARE   "1",IBCNMHOS
          GOTO      ACPREP10 IF NOT EQUAL
.
          PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,ACPREP10
.
          STRIP     WBSEHOSP
          PACK      PATHNAME,PTCNACPT,WBSEHOSP,SLASH,EXTFNAME
          GOTO      ACPREP20
.
ACPREP10  PACK      PATHNAME,PTCNACPT,EXTFNAME
.
ACPREP20  PREP      EXTFILE1,PATHNAME
          MOVE      PATHNAME,KEY44
.
ACPREP99  RETURN
.
.******************************************************************************
.*                             XMLBGN00                                       *
.*                  Create and write xml header & Message Info data           *
.******************************************************************************
XMLBGN00  MOVE      ZERO,EXIT
.                                           * write xml version data
          WRITE     EXTFILE1,SEQ;HED0001
          WRITE     EXTFILE1,SEQ;HED0002,HED0003
          WRITE     EXTFILE1,SEQ;HED0004
.
          WRITE     EXTFILE1,SEQ;ST,DOC0000,EN             * <Message_Info>
.
          CALL      IBACLOCK
          MOVE      CCC,CCENT
          MOVE      CYY,CYEAR
          MOVE      CMM,CMON
          MOVE      CDD,CDAY
          CALL      PACDATE
          STRIP     EXTFNAME
          STRIP     PTCNACVR
.
          STRIP     PTCNACSN
          WRITE     EXTFILE1,SEQ;ST,DOC0001,EN,CPCDATE,STS,DOC0001,EN
          PACK      TXT250B,ST,DOC0002,EN,PTCNACSN,STS,DOC0002,EN  * strip blank
          WRITE     EXTFILE1,SEQ;*+,TXT250B
.
          WRITE     EXTFILE1,SEQ;ST,DOC0003,EN,"IBA_PATWEB89",STS,DOC0003,EN
          WRITE     EXTFILE1,SEQ;ST,DOC0004,EN,VERSION,STS,DOC0004,EN
          WRITE     EXTFILE1,SEQ;ST,DOC0005,EN,"ACC",STS,DOC0005,EN
.....     PACK      TXT250B,ST,DOC0006,EN,EXTFNAME,STS,DOC0006,EN  * strip blank
.....     WRITE     EXTFILE1,SEQ;*+,TXT250B
.....     PACK      TXT250B,ST,DOC0007,EN,PTCNACVR,STS,DOC0007,EN  * strip blank
.....     WRITE     EXTFILE1,SEQ;*+,TXT250B
          WRITE     EXTFILE1,SEQ;STS,DOC0000,EN            * </Message_Info>
.
XMLHED99  RETURN
.
.******************************************************************************
.*                             XMLCLM00                                       *
.*                  Generate Claim XML                                        *
.******************************************************************************
XMLCLM00  MOVE      ZERO,EXIT
.                                           * write Claim xml         
          STRIP     WCCLMNO
          WRITE     EXTFILE1,SEQ;ST,CLM0000,EN             * <Claim>
          PACK      TXT250B,ST,CLM1001,EN,WCCLMNO,STS,CLM1001,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
.
          PACK      TXT250B,ST,CLM1002,EN,WCCLMNO,STS,CLM1002,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,CLM1003,EN,ACC45,STS,CLM1003,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
.
.                                           * setup & write Provider xml  *****
.                                           * clear Provider fields
XMLCLM10  CALL      CLPMSHCP
          UNPACK    SP70,TDESC
.
. ** Task 0822329 - use new approving HCP if completed, else Treatment Provider
.         MATCH     SP70,PMWXAPPR
.         IF        !@EQUAL
.           PACK      KEY10,PMWXAPPR,SP10
.         ELSE
.           PACK      KEY10,PMWXTPRO,SP10
.         ENDIF
.
          PACK      KEY10,PMWXTPRO,SP10
.
          MATCH     "0",PTCNACTP
          IF        @EQUAL
            IF        IBCNMHOS = 1
              PACK      KEY3,WBSEHOSP,SP70
              CALL      RDPTHSP1
              IF        OVRCD=0
                MATCH     SP70,PTHSACTR
                IF        !@EQUAL
                  PACK      KEY10,PTHSACTR,SP10
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          MATCH     "2",PTCNACTP
          IF        @EQUAL
            MATCH     SP70,PMWXAPPR
            IF        !@EQUAL
              PACK      KEY10,PMWXAPPR,SP10
            ENDIF
          ENDIF
.
          CALL      RDPMHCP1                * Read HCP/Provider file
          MOVE      PMHCMRBN,DIM10A
          MATCH     SP10,PMHCPRV1
          IF        !@EQUAL
            MOVE      PMHCPRV1,DIM10A       * get Provider No.
          ENDIF
.
          UNPACK    SP70,DIM20A
          MATCH     SP3,PMHCSPC1            * get Speciality
          IF        !@EQUAL
            PACK      KEY5,CATDT,PMHCSPC1,SP5
            CALL      RDCODE1               * Cat."DT"
            MOVE      TDESC,DIM20A
          ENDIF
.
          IF        IBCNMHOS = 1
            UNPACK    SP70,PTHSNAME
            PACK      KEY3,WBSEHOSP,SP70
            CALL      RDPTHSP1
            BRANCH    OVRCD,CRMED999
            MOVE      PTHSNAME,DIM35A       * multi-hosp - description
            MOVE      PTHSAGNC,PTCNHADF
          ELSE
            MOVE      CHADD1,DIM35A         * single Hosp - address line 1
          ENDIF
.
          UNPACK    SP70,TDESC
          PACK      KEY5,CATHA,PTCNHADF
          CALL      RDCODE1                 * Cat."HA" - Default Health Agency
          MOVE      TDESC,DIM20B
.
          UNPACK    SP70,TDESC
          PACK      KEY5,CATDT,PMHCSPC1
          CALL      RDCODE1                 * Cat."DT" - Default Health Agency
          MOVE      PTCDNHCP,DIM20C
.
          UNPACK    PMWXPDDT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          STRIP     CPCDATE
.
          STRIP     PMHCTITL
          STRIP     PMHCSNAM
          STRIP     PMHCGNAM
          STRIP     DIM10A
          STRIP     DIM20A
          STRIP     DIM20B
          STRIP     DIM35A
          STRIP     PMHCADR1
          STRIP     PMHCADR2
          STRIP     PMHCADR3
          STRIP     PMHCADR4
          STRIP     PMHCPOST
          STRIP     DIM20C  
.                                           * write Provider xml
          WRITE     EXTFILE1,SEQ;ST,PRV1000,EN            * <Claim_.._Provider>
          PACK      TXT250B,ST,PRV2001,EN,DIM10A,STS,PRV2001,EN  
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PRV2002,EN,DIM20A,STS,PRV2002,EN  
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PRV2003,EN,DIM35A,STS,PRV2003,EN  
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PRV2004,EN,DIM20B,STS,PRV2004,EN  
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PRV2005,EN,STS,PRV2005,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PRV2006,EN,PMHCSNAM,STS,PRV2006,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PRV2007,EN,PMHCGNAM,STS,PRV2007,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PRV2008,SLSH,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          WRITE     EXTFILE1,SEQ;ST,PRV2009,EN,"Business",STS,PRV2009,EN
          PACK      TXT250B,ST,PRV2010,EN,PMHCADR1,STS,PRV2010,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PRV2011,EN,PMHCADR2,STS,PRV2011,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PRV2012,EN,PMHCADR3,STS,PRV2012,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PRV2013,EN,PMHCADR4,STS,PRV2013,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PRV2014,EN,PMHCPOST,STS,PRV2014,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PRV2015,EN,STS,PRV2015,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PRV2016,EN,DIM20C,STS,PRV2016,EN  
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PRV2017,EN,CPCDATE,STS,PRV2017,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          WRITE     EXTFILE1,SEQ;STS,PRV1000,EN
.
.                                           * setup & write Patient xml ******
XMLCLM20  UNPACK    NHMADOB,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DIM10A          * Patient DOB
. 
.                                           * get EDI Occupation code
          UNPACK    SP70,PTCDNHCP,THCSCOD,TDESC                       *C-228831
          LOAD      DIM1,PTCNEIOC,ONE,TWO,THREE,FOUR,FIVE,SIX
          LOAD      DIM3,PTCNEIOC,PUSR1,PUSR2,PUSR3,PUSR4,PUSR5,PUSR6
          PACK      KEY5,CATP,DIM1,DIM3,SP10
          CALL      RDCODE1                 * Cat."Pn"
....      MOVE      THCSCOD,DIM4A                                     *D-228831
          MOVE      PTCDNHCP,DIM4A                                    *I-228831
.
XMLCLM22  UNPACK    SP70,PMRLDESC           * get Relationship Description
          PACK      KEY10,PMWXARRP,SP10
          CALL      RDPMREL1
.
          UNPACK    PMWXPDDT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DIM10B          * Patient Declaration Date
.
          STRIP     NHMANMPI
.
....      STRIP     PTITL
          PACK      PMTLDESC,PTITL,SP30
          PACK      KEY4,PTITL,SP5
          CALL      RDPMTLE1
          PACK      DIM30A,PMTLDESC
. 
          STRIP     NHMASURN
          STRIP     NHMAGIV1
          STRIP     NHMAGIV2
          STRIP     NHMASEX
          PACK      KEY5,ANSG,SP1,NHMASEX,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            PACK      NHMASEX,TCINDC2
          ENDIF
          STRIP     PTELEB
          MATCH     SP70,PTMXCELL    * Use Mobile Phone if available
          IF        !@EQUAL
            MOVE      PTMXCELL,PTELEP
          ENDIF
          STRIP     PTELEP
.
          STRIP     NHMAETH
          UNPACK    SP30,DIM10C       * clear "Ethnicity"
          MATCH     "54",NHMAETH
          IF        @EQUAL
            MOVE      "Not Known",DIM10C
          ENDIF
.
....      STRIP     PMWXARTL
          PACK      PMTLDESC,PMWXARTL,SP30
          PACK      KEY4,PMWXARTL,SP5
          CALL      RDPMTLE1
          PACK      DIM30B,PMTLDESC

          STRIP     PMWXARSN
          STRIP     PMWXARG1
          STRIP     PMWXARG2
          STRIP     PMRLDESC
          STRIP     DIM2A 
          STRIP     DIM4A 
          STRIP     DIM10B
          STRIP     DIM10C
          STRIP     DIM30A
          STRIP     DIM30B
.
          WRITE     EXTFILE1,SEQ;ST,PAT1000,EN             * <Claim_Patient>
          WRITE     EXTFILE1,SEQ;ST,PAT2000,SLSH,EN
          PACK      TXT250B,ST,PAT2001,EN,NHMANMPI,STS,PAT2001,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PAT2002,EN,DIM30A,STS,PAT2002,EN        * PTITL
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PAT2003,EN,NHMASURN,STS,PAT2003,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PAT2004,EN,NHMAGIV1,STS,PAT2004,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PAT2005,EN,NHMAGIV2,STS,PAT2005,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          WRITE     EXTFILE1,SEQ;ST,PAT2006,EN,DIM10A,STS,PAT2006,EN
          PACK      TXT250B,ST,PAT2007,EN,NHMASEX,STS,PAT2007,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PAT2008,EN,DIM4A,STS,PAT2008,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PAT2009,EN,PTELEB,STS,PAT2009,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PAT2010,EN,PTELEP,STS,PAT2010,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
.
          PACK      TXT250B,ST,PAT2011,EN,NHMAETH,STS,PAT2011,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PAT2012,EN,DIM10C,STS,PAT2012,EN      * NHMAETH
          WRITE     EXTFILE1,SEQ;*+,TXT250B
.
          PACK      TXT250B,ST,PAT2013,EN,DIM30B,STS,PAT2013,EN      * PMWXARTL
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PAT2014,EN,PMWXARSN,STS,PAT2014,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PAT2015,EN,PMWXARG1,STS,PAT2015,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PAT2016,EN,PMWXARG2,STS,PAT2016,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PAT2017,EN,PMRLDESC,STS,PAT2017,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,PAT2018,EN,DIM10B,STS,PAT2018,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
.
          STRIP     NHMAADD1
          STRIP     NHMAADD2
          STRIP     NHMAADD3
          STRIP     NHMAADD4
          STRIP     NHMAADD5
.          STRIP     NHMADOMC                        * 0814468 - Removed 
.
          WRITE     EXTFILE1,SEQ;ST,ADD2000,EN        * <Claim_Patient_Address>
          WRITE     EXTFILE1,SEQ;ST,ADD3001,EN,"Home",STS,ADD3001,EN
          PACK      TXT250B,ST,ADD3002,EN,NHMAADD1,STS,ADD3002,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,ADD3003,EN,NHMAADD2,STS,ADD3003,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,ADD3004,EN,NHMAADD3,STS,ADD3004,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,ADD3005,EN,NHMAADD4,STS,ADD3005,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,ADD3006,EN,STS,ADD3006,EN            * 0814468 - Changed to empty tag
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,ADD3007,EN,NHMAADD5,STS,ADD3007,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          WRITE     EXTFILE1,SEQ;STS,ADD2000,EN
.                                                          * <Postal_Address>
          MOVE      PTMXADD1,POSTADD1
          MOVE      PTMXADD2,POSTADD2
          MOVE      PTMXADD3,POSTADD3
          MOVE      PTMXADD4,POSTADD4
          MOVE      PTMXPOST,POSTCODE
.
XMLCLM24  MATCH     "1",PTCNNEWC
          IF        @EQUAL
            MOVE      "2",CONTTYPE                  * Postal Address
            CALL      XCON0000
            MOVE      PMCEADD1,POSTADD1
            MOVE      PMCEADD2,POSTADD2
            MOVE      PMCEADD3,POSTADD3
            MOVE      PMCEADD4,POSTADD4
            MOVE      PMCEPOST,POSTCODE
          ENDIF
.
          STRIP     POSTADD1
          STRIP     POSTADD2
          STRIP     POSTADD3
          STRIP     POSTADD4
          STRIP     POSTCODE
.
          WRITE     EXTFILE1,SEQ;ST,ADD2000,EN        * <Claim_Patient_Address>
          WRITE     EXTFILE1,SEQ;ST,ADD3001,EN,"Postal",STS,ADD3001,EN
          PACK      TXT250B,ST,ADD3002,EN,POSTADD1,STS,ADD3002,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,ADD3003,EN,POSTADD2,STS,ADD3003,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,ADD3004,EN,POSTADD3,STS,ADD3004,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,ADD3005,EN,POSTADD4,STS,ADD3005,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,ADD3006,EN,POSTCODE,STS,ADD3006,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,ADD3007,EN,STS,ADD3007,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          WRITE     EXTFILE1,SEQ;STS,ADD2000,EN            
.                                                          * </Postal_Address>
          WRITE     EXTFILE1,SEQ;STS,PAT1000,EN            * </Claim_Patient>
.
.                                           * setup & write Employment xml ****
XMLCLM30  UNPACK    SP70,TDESC,TCINDC1,THCSCOD
          PACK      KEY5,CATIF,PMWXESTA
          CALL      RDCODE1                 * Cat."IF" 
          MATCH     "N",TCINDC1             * check Employment Status *I-229732 
          GOTO      XMLCLM31 IF EQUAL       * if Status "Not in Paid Employment"
.
.                                           * setup Employment stuff
          MOVE      TCINDC1,DIM1A
          MOVE      THCSCOD,DIM4A
.
          UNPACK    SP70,TDESC,THCSCOD
          PACK      KEY5,CATIX,PMWXTWRK
          CALL      RDCODE1                 * Cat."IX" 
          MOVE      THCSCOD,DIM4B
.
          STRIP     DIM4A
          STRIP     DIM4B
          STRIP     PMWXOEST
          STRIP     WCENAME
          STRIP     WCEADD1
          STRIP     WCEADD2
          STRIP     WCEADD3
          STRIP     WCEADD4
          STRIP     WCEPOST
.                                           * write Employment xml
          WRITE     EXTFILE1,SEQ;ST,EMP1000,EN
          WRITE     EXTFILE1,SEQ;ST,EMP2001,EN,DIM1A,STS,EMP2001,EN
          PACK      TXT250B,ST,EMP2002,EN,DIM4A,STS,EMP2002,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,EMP2003,EN,PMWXOEST,STS,EMP2003,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,EMP2004,EN,WCENAME,STS,EMP2004,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          WRITE     EXTFILE1,SEQ;ST,EMP2005,EN,"Business",STS,EMP2005,EN
          PACK      TXT250B,ST,EMP2006,EN,WCEADD1,STS,EMP2006,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,EMP2007,EN,WCEADD2,STS,EMP2007,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,EMP2008,EN,WCEADD3,STS,EMP2008,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,EMP2009,EN,WCEADD4,STS,EMP2009,EN
          STRIP     NHMAADD4
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,EMP2010,EN,WCEPOST,STS,EMP2010,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          WRITE     EXTFILE1,SEQ;ST,EMP2011,EN,STS,EMP2011,EN         * no data
          PACK      TXT250B,ST,EMP2012,EN,DIM4B,STS,EMP2012,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          WRITE     EXTFILE1,SEQ;STS,EMP1000,EN
          GOTO      XMLCLM40
.
XMLCLM31  MOVE      TCINDC1,DIM1A
          WRITE     EXTFILE1,SEQ;ST,EMP1000,EN
          WRITE     EXTFILE1,SEQ;ST,EMP2001,EN,DIM1A,STS,EMP2001,EN
          WRITE     EXTFILE1,SEQ;ST,EMP2002,EN,STS,EMP2002,EN
          WRITE     EXTFILE1,SEQ;ST,EMP2003,EN,STS,EMP2003,EN
          WRITE     EXTFILE1,SEQ;ST,EMP2004,EN,STS,EMP2004,EN
          WRITE     EXTFILE1,SEQ;ST,EMP2005,EN,STS,EMP2005,EN
          WRITE     EXTFILE1,SEQ;ST,EMP2006,EN,STS,EMP2006,EN
          WRITE     EXTFILE1,SEQ;ST,EMP2007,EN,STS,EMP2007,EN
          WRITE     EXTFILE1,SEQ;ST,EMP2008,EN,STS,EMP2008,EN
          WRITE     EXTFILE1,SEQ;ST,EMP2009,EN,STS,EMP2009,EN
          WRITE     EXTFILE1,SEQ;ST,EMP2010,EN,STS,EMP2010,EN
          WRITE     EXTFILE1,SEQ;ST,EMP2011,EN,STS,EMP2011,EN         * no data
          WRITE     EXTFILE1,SEQ;ST,EMP2012,EN,STS,EMP2012,EN
          WRITE     EXTFILE1,SEQ;STS,EMP1000,EN
.                                           * set up & write Injury xml *******
XMLCLM40  UNPACK    WCACDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DIM10A          * Injury date
          UNPACK    PMWXATME,DIM5A          * Injury time
.
          UNPACK    SP70,TDESC,THCSCOD
          PACK      KEY5,CATIP,PMWXPLIN
          CALL      RDCODE1                 * Cat."IP"
          MOVE      THCSCOD,DIM4A
.
          UNPACK    SP70,TDESC,THCSCOD
          PACK      KEY5,CATIM,PMWXACLC
          CALL      RDCODE1                 * Cat."IM"
          MOVE      THCSCOD,DIM4B
.
          MOVE      "N",DIM1A
          MOVE      ZERO,F1
          MOVE      PMWXAINZ,F1
          LOAD      DIM1A,F1,ANSY
.
          UNPACK    SP70,ACCMACCN,ACCMTYPE,DACCMLIN
          CLEAR     TXT250A
          MOVE      "001",KEY3                   * Cause of Injury
          PACK      KEY26,ACCCLMNO,KEY3,SP10     * Accident No.
          CALL      RSACCMT1
XMLCLM45  CALL      RKACCMT1                     * read Cause of Injury Comments
          BRANCH    OVRCD,XMLCLM46
          MATCH     ACCCLMNO,ACCMACCN            * same Accident No ?
          GOTO      XMLCLM46 IF NOT EQUAL
          MATCH     KEY3,ACCMTYPE
          GOTO      XMLCLM46 IF NOT EQUAL
          STRIP     ACCMDATA
          APPEND    ACCMDATA,TXT250A
          APPEND    SP1,TXT250A
          GOTO      XMLCLM45
.
XMLCLM46  RESET     TXT250A
          STRIP     TXT250A
.
.
          MOVE      "N",DIM1B
          MOVE      ZERO,F1
          MOVE      PMWXMOVV,F1
          LOAD      DIM1B,F1,ANSY
.
          UNPACK    SP70,TDESC,TCINDC1,THCSCOD
          PACK      KEY5,CATIQ,PMWXACCF
          CALL      RDCODE1                 * Cat."IQ" 
          MOVE      TCINDC1,DIM1C
          STRIP     DIM1C
.
          MOVE      "N",DIM1D
          MOVE      ZERO,F1
          MOVE      PMWXSPTI,F1
          LOAD      DIM1D,F1,ANSY
.
          UNPACK    SP70,TDESC,THCSCOD
          PACK      KEY5,CATIB,PMWXSPRT
          CALL      RDCODE1                 * Cat."IB"
          MOVE      THCSCOD,DIM4C
          STRIP     DIM4C
.
          STRIP     DIM4A
          STRIP     DIM4B
          STRIP     DIM5A
          STRIP     DIM10A
.
          WRITE     EXTFILE1,SEQ;ST,INJ1000,EN             * <Injury>
          PACK      TXT250B,ST,INJ2001,EN,DIM10A,STS,INJ2001,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,INJ2002,EN,DIM5A,STS,INJ2002,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,INJ2003,EN,DIM4A,STS,INJ2003,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,INJ2004,EN,DIM4B,STS,INJ2004,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          WRITE     EXTFILE1,SEQ;ST,INJ2005,EN,DIM1A,STS,INJ2005,EN
.
          WRITE     EXTFILE1,SEQ;*+,ST,INJ2006,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250A,STS,INJ2006,EN
.
          WRITE     EXTFILE1,SEQ;ST,INJ2007,EN,DIM1B,STS,INJ2007,EN
          WRITE     EXTFILE1,SEQ;ST,INJ2008,EN,DIM1C,STS,INJ2008,EN
          WRITE     EXTFILE1,SEQ;ST,INJ2009,EN,STS,INJ2009,EN         * no data
          WRITE     EXTFILE1,SEQ;ST,INJ2010,EN,DIM1D,STS,INJ2010,EN
          PACK      TXT250B,ST,INJ2011,EN,DIM4C,STS,INJ2011,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          WRITE     EXTFILE1,SEQ;ST,INJ2012,EN,STS,INJ2012,EN         * no data
          WRITE     EXTFILE1,SEQ;STS,INJ1000,EN
.
.                                           * set up & write Injury Diagnosis
XMLCLM50  UNPACK    SP70,ACCMACCN,ACCMTYPE,DACCMLIN
          UNPACK    SP70,ACCMDATA
          UNPACK    SP70,TXT250A 
          CLEAR     TXT250A
          MOVE      "002",KEY3                   * Injury Comments
          PACK      KEY26,ACCCLMNO,KEY3,SP10
          CALL      RSACCMT1
XMLCLM55  CALL      RKACCMT1                     * read Cause of Injury Comments
          BRANCH    OVRCD,XMLCLM56
          MATCH     ACCCLMNO,ACCMACCN            * same Accident No ?
          GOTO      XMLCLM56 IF NOT EQUAL
          MATCH     KEY3,ACCMTYPE
          GOTO      XMLCLM56 IF NOT EQUAL
          STRIP     ACCMDATA
          APPEND    ACCMDATA,TXT250A
          APPEND    SP1,TXT250A
          GOTO      XMLCLM55
.
XMLCLM56  RESET     TXT250A
          STRIP     TXT250A
.
          MOVE      "1",DIM1A               * default (ACC 1=No)
          MOVE      ZERO,F1
          CMATCH    "1",PMWXNEED            * ACC Rehab.Code (IBA 0=No, 1=Yes)
          IF        @EQUAL
            MOVE      "4",DIM1A             * (ACC 4=Yes)
          ENDIF
.
          MOVE      "N",DIM1B
          MOVE      ZERO,F1
          MOVE      PMWXASST,F1
          LOAD      DIM1B,F1,ANSY
.
          STRIP     PMWXTRIC
.
          WRITE     EXTFILE1,SEQ;ST,IDI1000,EN             * <Injury>
          PACK      TXT250B,ST,IDI2001,EN,TXT250A      * long text field
          WRITE     EXTFILE1,SEQ;*+,TXT250B,STS,IDI2001,EN
          MOVE      "N",DIM1C
          MATCH     "1",PMWXTRIC
          GOTO      XMLCLM58 IF NOT EQUAL
          MOVE      "Y",DIM1C
.
XMLCLM58  PACK      TXT250B,ST,IDI2002,EN,DIM1C,STS,IDI2002,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          WRITE     EXTFILE1,SEQ;ST,IDI2003,EN,STS,IDI2003,EN         * no data
          WRITE     EXTFILE1,SEQ;ST,IDI2004,EN,DIM1A,STS,IDI2004,EN
          WRITE     EXTFILE1,SEQ;ST,IDI2005,EN,STS,IDI2005,EN         * no data
          WRITE     EXTFILE1,SEQ;ST,IDI2006,EN,DIM1B,STS,IDI2006,EN
.
. ** Gradual Process Injury 1=Yes ***
          MOVE      "N",DIM1A
          MOVE      ZERO,F1
          MOVE      PMWXGRAD,F1
          LOAD      DIM1A,F1,ANSY
.
          WRITE     EXTFILE1,SEQ;ST,IDI2007,EN,DIM1A,STS,IDI2007,EN
.
. ** Admitted to Hospital 1=Yes ***
          MOVE      "N",DIM1A
          MOVE      ZERO,F1
          MOVE      PMWXAHOS,F1
          LOAD      DIM1A,F1,ANSY
.
          WRITE     EXTFILE1,SEQ;ST,IDI2008,EN,DIM1A,STS,IDI2008,EN
.
.                                           * set up & write Diagnosis Codes
XMLCLM60  UNPACK    SP70,EMIC9COD,EMICDESC
          PACK      KEY23,ACCCLMNO,SP20
          CALL      RSACDIA1
XMLCLM65  CALL      RKACDIA1
          BRANCH    OVRCD,XMLCLM69
          MATCH     ACCCLMNO,ACDIACCN       * same Accident Claim no ?
          GOTO      XMLCLM69 IF NOT EQUAL
.
          UNPACK    SP70,EMIC9COD,EMICDESC
          PACK      KEY9,ACDIDIAG,SP10
          CALL      RDEMICD1
.
...       STRIP     ACDICSYC
          MOVE      PTCNDIGN,DIGHDPCD   * Use EMR Diag HDP Code & Coding System?
          UNPACK    SP70,DIM30,DIM30A
          PACK      DIM256,SP100,SP100,SP70
.
          PACK      DIM30A,EMICVEMD,SP70         * use EMR Diag Code
          PACK      DIM256,EMICDESC,SP100,SP100  * use EMR Diag Code Description
.
          IF        DIGHDPCD=0
            MOVE      ACDICSYC,DIM3
            SQUEEZE   DIM3
            MOVE      DIM3,F3
            IF        F3=0 | F3=1 | F3=2 | F3=3
              MOVE      ACDICSYC,DIM30      * ACDICSYC 1=ICD9 2=ICD10 3=READ
              REP       "1A2B3C",DIM30
              REP       "A2B3C1",DIM30      * ACC NZ   1=Read 2=ICD9 3=ICD10
              MOVE      ACDIDIAG,DIM30A
            ELSE
              UNPACK    SP70,DIM30,DIM30A
              PACK      DIM256,SP100,SP100,SP70
.
              MOVE      "emricdaf",PMVMTNAM
              PACK      PMVMTIDX,ACDIDIAG,SP70
              PACK      PMVMMVSI,ACDICSYC,SP10
.
              PACK      KEY41,PMVMTNAM,PMVMTIDX,PMVMMVSI,SP70
              CALL      RDPMVMT1
              IF        OVRCD=0
                PACK      KEY3,PMVMMVSI,SP10
                CALL      RDPMVST1               * get Value Set
                IF        OVRCD=0
                  MOVE      PMVTHDPE,DIM30       * use HDP Equivalent of Set ID 
                ENDIF
.
                MOVE      PMVMMCOD,DIM30A        * use Value Set Mapping Code
.
                PACK      KEY33,PMVMMVSI,PMVMMCOD,SP70
                CALL      RDPMVSC1               * get Value Set Code (mapped)
                IF        OVRCD=0
                  MOVE      PMVCPTRM,DIM256      * use Preferred Term
                ENDIF
              ENDIF
            ENDIF
          ELSE
            IF         DIGHDPCD=1
              MOVE       "1",DIM30               * SNOMED Code mapped to READ
            ELSE
              MOVE       "3",DIM30               * SNOMED Code mapped to ICD10
            ENDIF
          ENDIF
.
          STRIP     DIM30    (ACDICSYC / SNOMED Value Set HDP Equivalent)
          STRIP     DIM30A   (ACDIDIAG / SNOMED Mapped Code)
          STRIP     ACDISIDE
          STRIP     DIM256   (EMICDESC / SNOMED Value Set Code Preferred Term)
.
          WRITE     EXTFILE1,SEQ;ST,DIA2000,EN             * <Diagnosis_Info>
          PACK      TXT250B,ST,DIA3001,EN,DIM30,STS,DIA3001,EN       * ACDICSYC
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,DIA3002,EN,DIM30A,STS,DIA3002,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          PACK      TXT250B,ST,DIA3003,EN,ACDISIDE,STS,DIA3003,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          WRITE     EXTFILE1,SEQ;ST,DIA3004,EN,STS,DIA3004,EN         * no data
          PACK      TXT250B,ST,DIA3005,EN,DIM256,STS,DIA3005,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          WRITE     EXTFILE1,SEQ;STS,DIA2000,EN            * </Diagnosis_Info>
          GOTO      XMLCLM65
.
XMLCLM69  WRITE     EXTFILE1,SEQ;STS,IDI1000,EN            * </Injury>
.
.                                           * set up & write Work Capacity   
XMLCLM70  MOVE      "Y",DIM1A
          MATCH     SP70,PMWXCONT
          IF        !@EQUAL & !@EOS
            MOVE      "N",DIM1A
            MOVE      ZERO,F1
            MOVE      PMWXCONT,F1
            LOAD      DIM1A,F1,ANSY
          ENDIF
.
....      MOVE      " ",DIM1B               * set to blank (was "N")  *C-178850
          MOVE      SP70,DIM1B
          MATCH     SP70,PMWXALTW
          IF        !@EQUAL & !@EOS
            MOVE      "N",DIM1B             * changed back to "N"     *C-229732
            MOVE      ZERO,F1
            MOVE      PMWXALTW,F1
            LOAD      DIM1B,F1,ANSY
          ENDIF
.
          UNPACK    SP70,TDESC,THCSCOD
          PACK      KEY5,CATIH,PMWXTYPA
          CALL      RDCODE1                 * Cat."IH"
          MOVE      THCSCOD,DIM4A
.
          UNPACK    PMWXSALT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DIM10A          * Injury date
.
          UNPACK    SP70,ACCMACCN,ACCMTYPE,DACCMLIN
          CLEAR     TXT250A
          MOVE      "003",KEY3                   * Alt Work Restrictions
          PACK      KEY26,ACCCLMNO,KEY3,SP10
          CALL      RSACCMT1
XMLCLM75  CALL      RKACCMT1                     * read Alt Work Restrictions
          BRANCH    OVRCD,XMLCLM76
          MATCH     ACCCLMNO,ACCMACCN            * same Accident No ?
          GOTO      XMLCLM76 IF NOT EQUAL
          MATCH     KEY3,ACCMTYPE
          GOTO      XMLCLM76 IF NOT EQUAL
          STRIP     ACCMDATA
          APPEND    ACCMDATA,TXT250A
          APPEND    SP1,TXT250A
          GOTO      XMLCLM75
.
XMLCLM76  RESET     TXT250A
          STRIP     TXT250A
.
          UNPACK    SP70,TDESC,THCSCOD
          PACK      KEY5,CATUI,PMWXUNFT
          CALL      RDCODE1                 * Cat."UI"
          MOVE      THCSCOD,DIM4B
.
          UNPACK    PMWXFISD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DIM10B          * Injury date
.
          UNPACK    PMWXRNWD,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DIM10C          * Injury date
.
          STRIP     DIM1A
          STRIP     DIM1B
          STRIP     DIM4A
          STRIP     DIM4B
          STRIP     DIM10A
          STRIP     DIM10B
          STRIP     DIM10C
          STRIP     PMWXHPDA
          STRIP     PMWXUNFD
.
.  When <Continue_Normal_Work_Ind> field contains a 'Y' no other fields in the
.  <Work_Capacity> section should be populated.
.
          MATCH     ANSY,DIM1A
          IF        @EQUAL
            WRITE     EXTFILE1,SEQ;ST,WRK1000,EN             * <Work_Capacity>
            PACK      TXT250B,ST,WRK2001,EN,DIM1A,STS,WRK2001,EN
            WRITE     EXTFILE1,SEQ;*+,TXT250B
            PACK      TXT250B,ST,WRK2002,EN,STS,WRK2002,EN
            WRITE     EXTFILE1,SEQ;*+,TXT250B
            PACK      TXT250B,ST,WRK2003,EN,STS,WRK2003,EN
            WRITE     EXTFILE1,SEQ;*+,TXT250B
            PACK      TXT250B,ST,WRK2004,EN,STS,WRK2004,EN
            WRITE     EXTFILE1,SEQ;*+,TXT250B
            PACK      TXT250B,ST,WRK2005,EN,STS,WRK2005,EN
            WRITE     EXTFILE1,SEQ;*+,TXT250B
            PACK      TXT250B,ST,WRK2006,EN,STS,WRK2006,EN
            WRITE     EXTFILE1,SEQ;*+,TXT250B
            PACK      TXT250B,ST,WRK2007,EN,STS,WRK2007,EN
            WRITE     EXTFILE1,SEQ;*+,TXT250B
            PACK      TXT250B,ST,WRK2008,EN,STS,WRK2008,EN
            WRITE     EXTFILE1,SEQ;*+,TXT250B
            PACK      TXT250B,ST,WRK2009,EN,STS,WRK2009,EN
            WRITE     EXTFILE1,SEQ;*+,TXT250B
            PACK      TXT250B,ST,WRK2010,EN,STS,WRK2010,EN
            WRITE     EXTFILE1,SEQ;*+,TXT250B
            WRITE     EXTFILE1,SEQ;STS,WRK1000,EN            * </Work_Capacity>
          ELSE
            WRITE     EXTFILE1,SEQ;ST,WRK1000,EN             * <Work_Capacity>
....        WRITE     EXTFILE1,SEQ;ST,WRK2001,EN,DIM1A,STS,WRK2001,EN
            PACK      TXT250B,ST,WRK2001,EN,DIM1A,STS,WRK2001,EN
            WRITE     EXTFILE1,SEQ;*+,TXT250B
....        WRITE     EXTFILE1,SEQ;ST,WRK2002,EN,DIM1B,STS,WRK2002,EN
            PACK      TXT250B,ST,WRK2002,EN,DIM1B,STS,WRK2002,EN
            WRITE     EXTFILE1,SEQ;*+,TXT250B
            PACK      TXT250B,ST,WRK2003,EN,DIM4A,STS,WRK2003,EN
            WRITE     EXTFILE1,SEQ;*+,TXT250B
            PACK      TXT250B,ST,WRK2004,EN,PMWXHPDA,STS,WRK2004,EN
            WRITE     EXTFILE1,SEQ;*+,TXT250B
            PACK      TXT250B,ST,WRK2005,EN,DIM10A,STS,WRK2005,EN
            WRITE     EXTFILE1,SEQ;*+,TXT250B
            PACK      TXT250B,ST,WRK2006,EN,TXT250A          * long text field
            WRITE     EXTFILE1,SEQ;*+,TXT250B,STS,WRK2006,EN
            PACK      TXT250B,ST,WRK2007,EN,PMWXUNFD,STS,WRK2007,EN
            WRITE     EXTFILE1,SEQ;*+,TXT250B
            PACK      TXT250B,ST,WRK2008,EN,DIM4B,STS,WRK2008,EN
            WRITE     EXTFILE1,SEQ;*+,TXT250B
            PACK      TXT250B,ST,WRK2009,EN,DIM10B,STS,WRK2009,EN
            WRITE     EXTFILE1,SEQ;*+,TXT250B
            PACK      TXT250B,ST,WRK2010,EN,DIM10C,STS,WRK2010,EN
            WRITE     EXTFILE1,SEQ;*+,TXT250B
            WRITE     EXTFILE1,SEQ;STS,WRK1000,EN            * </Work_Capacity>
          ENDIF
.
. 
.                                           * set up & write Referral xml
XMLCLM80  UNPACK    SP70,ACCMACCN,ACCMTYPE,DACCMLIN
          PACK      KEY23,ACCCLMNO,SP20
          CALL      RSACRFP1                
XMLCLM85  CALL      RKACRFP1
          BRANCH    OVRCD,XMLCLM89
          MATCH     ACCCLMNO,ACRFACCN       * same Accident Claim no ?
          GOTO      XMLCLM89 IF NOT EQUAL
.
          UNPACK    SP70,TDESC,THCSCOD
          PACK      KEY5,CATIW,ACRFRPRT
          CALL      RDCODE1                 * Cat."IW"
          MOVE      THCSCOD,DIM4A
          STRIP     ACRFSTC1
          STRIP     ACRFSTC2
          STRIP     ACRFSTC3
.
          CLEAR     TXT250A
          APPEND    ACRFSTC1,TXT250A
          APPEND    SP1,TXT250A
          APPEND    ACRFSTC2,TXT250A
          APPEND    SP1,TXT250A
          APPEND    ACRFSTC3,TXT250A
          RESET     TXT250A
          STRIP     TXT250A
          STRIP     DIM4A
.
          WRITE     EXTFILE1,SEQ;ST,REF1000,EN             * <Referral>
          PACK      TXT250B,ST,REF2001,EN,DIM4A,STS,REF2001,EN
          WRITE     EXTFILE1,SEQ;*+,TXT250B
          WRITE     EXTFILE1,SEQ;ST,REF2002,EN,STS,REF2002,EN         * no data
          WRITE     EXTFILE1,SEQ;ST,REF2003,EN,STS,REF2003,EN         * no data
          WRITE     EXTFILE1,SEQ;ST,REF2004,EN,STS,REF2004,EN         * no data
          PACK      TXT250B,ST,REF2005,EN,TXT250A          * long text field
          WRITE     EXTFILE1,SEQ;*+,TXT250B,STS,REF2005,EN
          WRITE     EXTFILE1,SEQ;STS,REF1000,EN            * </Referral>
.
          GOTO      XMLCLM85
.
XMLCLM89  MOVE      ZERO,F1
.                                                * end the Claim XML
          WRITE     EXTFILE1,SEQ;STS,CLM0000,EN            * </Claim>
          MOVE      ONE,CLMDONE
.
XMLCLM99  RETURN
+
.******************************************************************************
.*                             WRTLODGE                                       *
.*                  Write/update the ACC Claim Lodgement record               *
.******************************************************************************
WRTLODGE  MOVE      ZERO,EXIT
          CALL      IBACLOCK
          PACK      DIM8A,CCC,CYY,CMM,CDD
          REP       " 0",DIM8A 
          UNPACK    CTIMEIS,DIM8B,DIM4A
          REP       " 0",DIM8B
.
          PACK      KEY20,ACCCLMNO,SP20
          CALL      RDACLOD1
          IF        OVRCD = 1
            PACK      ACLOACCN,ACCCLMNO,SP20
            PACK      ACLOURNO,URNUMBER,SP10
            MOVE      "0",ACLOSTAT
            MOVE      SP8,ACLOSDAT
            MOVE      DIM8A,ACLOUDAT
            MOVE      DIM8B,ACLOUTIM  
            MOVE      WBSEUID,ACLOWUID
            PACK      ACLOSPAR,SP70
            CALL      WRACLOD1
          ELSE
            MOVE      "0",ACLOSTAT
            MOVE      SP8,ACLOSDAT
            MOVE      DIM8A,ACLOUDAT
            MOVE      DIM8B,ACLOUTIM  
            MOVE      WBSEUID,ACLOWUID
            CALL      UPACLOD1
          ENDIF
.
WRTLOD99  RETURN
+
.
.------------------------------------------------------------------------------
. Routines to prepare the data file (JSON) for submission via the ACC API ...
.------------------------------------------------------------------------------
.
.******************************************************************************
.*                                  JSPREP00                                  *
.*                  Prep the JSON file for submission to the ACC Claim API    *
.******************************************************************************
JSPREP00  UNPACK    ACCCLMNO,DIM8
          SQUEEZE   DIM8
          PACK      EXTFNAME,DIM8,DOT,JSON
          STRIP     PTCNACPT
          PACK      PATHNAME,SP30,SP30,SP30,SP10
          CLEAR     PATHNAME
.
          COMPARE   "1",IBCNMHOS
          GOTO      JSPREP10 IF NOT EQUAL
.
          PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,JSPREP10
.
          STRIP     WBSEHOSP
          PACK      PATHNAME,PTCNACPT,WBSEHOSP,SLASH,EXTFNAME
          GOTO      JSPREP20
.
JSPREP10  PACK      PATHNAME,PTCNACPT,EXTFNAME
.
JSPREP20  PREP      JSONFIL1,PATHNAME
.
JSPREP99  RETURN
+
.******************************************************************************
.*                             JSBEGN00                                       *
.*        Write JSON header data ("pmsSoftwareName" & "pmsSoftwareVersion")   *
.******************************************************************************
JSBEGN00  MOVE      ZERO,EXIT
.
.         {
.           "pmsSoftwareName": "IBA_PATWEB89",
.           "pmsSoftwareVersion": "",
.
          WRITE     JSONFIL1,SEQ;JSBRKSTA
          WRITE     JSONFIL1,SEQ;SP2,JSOFTNAM,COLON,SP1,QUOTE,"IBA_PATWEB89",QUOTE,COMMA
          WRITE     JSONFIL1,SEQ;SP2,JSOFTVER,COLON,SP1,QUOTE,VERSION,QUOTE,COMMA
.
JSBEGN99  RETURN
+
.******************************************************************************
.*                             JSNCLM00                                       *
.*        Write JSON Claim Details                                            *
.******************************************************************************
JSNCLM00  MOVE      ZERO,EXIT
.
.         Add Claim Number
.           "claimNumber": "",
.
          STRIP     WCCLMNO
          WRITE     JSONFIL1,SEQ;SP2,JSCLAIMN,COLON,SP1,QUOTE,*+,WCCLMNO,*-,QUOTE,COMMA
.
.
.         ----------------------------------------
.         Vendor Details
.         ----------------------------------------
.
.         Get Practice Name (Facility)
          IF        IBCNMHOS = 1
            UNPACK    SP70,PTHSNAME
            PACK      KEY3,WBSEHOSP,SP70
            CALL      RDPTHSP1
            BRANCH    OVRCD,CRMED999
            MOVE      PTHSNAME,DIM35A       * multi-hosp - description
            MOVE      PTHSAGNC,PTCNHADF
          ELSE
            MOVE      CHADD1,DIM35A         * single Hosp - address line 1
          ENDIF
.
.         Get HPI Organisation Number
          UNPACK    SP70,DIM10A
          PACK      KEY5,CATdh,PTHSHDHB
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      PTCDUDF8,DIM10A       * User Defined Field 8
          ENDIF
.
.         Get HPI Facility Number
          UNPACK    SP10,DIM8A
          IF        IBCNMHOS = 1
            MOVE      PTHSHDPC,DIM8A        * Health Department Code
          ELSE
            MOVE      CFILENO,DIM8A         * Hospital File Number
          ENDIF
.
.
.         ----------------------------------------
.         Provider Details
.         ----------------------------------------
.
          CALL      CLPMSHCP
          UNPACK    SP10,DIM10B
.
.         Get Provider Number
          MATCH     "0",PTCNACTP
          IF        @EQUAL
            IF        IBCNMHOS = 1
              PACK      KEY3,WBSEHOSP,SP70
              CALL      RDPTHSP1
              IF        OVRCD=0
                MATCH     SP70,PTHSACTR     * Default ACC Treatment Provider
                IF        !@EQUAL
                  PACK      KEY10,PTHSACTR,SP10
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          PACK      KEY10,PMWXTPRO,SP10     * HCP Treatment Provider
.
          MATCH     "2",PTCNACTP
          IF        @EQUAL
            MATCH     SP70,PMWXAPPR         * Approving HCP
            IF        !@EQUAL
              PACK      KEY10,PMWXAPPR,SP10
            ENDIF
          ENDIF
.
.         Read HCP file
          CALL      RDPMHCP1                * Read HCP/Provider file
          IF        OVRCD=0
            MOVE      PMHCMRBN,DIM10B       * Medical Registration Board Number
.
            MATCH     SP10,PMHCPRV1
            IF        !@EQUAL
              MOVE      PMHCPRV1,DIM10B     * Provider Number 1
            ENDIF
          ENDIF
.
.         Get Provider Type Code
          UNPACK    SP70,DIM4A
          MATCH     SP3,PMHCSPC1            * get Speciality
          IF        !@EQUAL
            PACK      KEY5,CATDT,PMHCSPC1,SP5
            CALL      RDCODE1               * Cat."DT"
            IF        OVRCD=0
              MOVE      PTCDNHCP,DIM4A      * use HCP Equivalent
            ENDIF
          ENDIF
.
          STRIP     PMHCTITL
          STRIP     PMHCSNAM
          STRIP     PMHCGNAM
          STRIP     DIM4A
          STRIP     DIM8A
          STRIP     DIM10A
          STRIP     DIM10B
          STRIP     DIM20A
          STRIP     DIM35A
          STRIP     PMHCADR1
          STRIP     PMHCADR2
          STRIP     PMHCADR3
          STRIP     PMHCADR4
          STRIP     PMHCADR5
          STRIP     PMHCPOST
.
.
.         ----------------------------------------
.         Vendor (Facility) Details
.         ----------------------------------------
.
.           "vendor": {
.             "practiceName": "",
.             "hpiOrganisationNumber": "",
.             "hpiFacilityNumber": ""
.           },
.
          WRITE     JSONFIL1,SEQ;SP2,JSVENDOR,COLON,SP1,JSBRKSTA      * open "{"
          WRITE     JSONFIL1,SEQ;SP2,SP2,JSPRACNM,COLON,SP1,QUOTE,*+,DIM35A,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,JSHPIORG,COLON,SP1,QUOTE,*+,DIM10A,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,JSHPIFAC,COLON,SP1,QUOTE,*+,DIM8A,QUOTE,*-
          WRITE     JSONFIL1,SEQ;SP2,JSBRKEND,COMMA  * Close vendor block "}"
.
.
.         Add Provider details ..
.
.           "provider": {
          WRITE     JSONFIL1,SEQ;SP2,JSPROVDR,COLON,SP1,JSBRKSTA      * open "{"
.
.             "details": {
.               "providerId": "",
.               "providerTypeCode": "",
.               "firstName": "",
.               "surname": ""
.             },
          WRITE     JSONFIL1,SEQ;SP2,SP2,JDETAILS,COLON,SP1,JSBRKSTA  * open "{"
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JSPROVID,COLON,SP1,QUOTE,*+,DIM10B,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JSPROVTC,COLON,SP1,QUOTE,*+,DIM4A,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JFRSTNAM,COLON,SP1,QUOTE,*+,PMHCGNAM,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JSURNAME,COLON,SP1,QUOTE,*+,PMHCSNAM,QUOTE,*-
          WRITE     JSONFIL1,SEQ;SP2,SP2,JSBRKEND,COMMA    * close details "}"
.
.             "address": {
.               "line1": "",
.               "line2": "",
.               "suburb": "",
.               "city": "",
.               "postCode": "",
.               "country": "",
.               "type": "Home"
.             }
.           },
          WRITE     JSONFIL1,SEQ;SP2,SP2,JADDRESS,COLON,SP1,JSBRKSTA  * open "{"
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JADDLNE1,COLON,SP1,QUOTE,*+,PMHCADR1,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JADDLNE2,COLON,SP1,QUOTE,*+,PMHCADR2,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JADDSUBB,COLON,SP1,QUOTE,*+,PMHCADR3,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JADDCITY,COLON,SP1,QUOTE,*+,PMHCADR4,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JADDPCOD,COLON,SP1,QUOTE,*+,PMHCPOST,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JADDCTRY,COLON,SP1,QUOTE,*+,PMHCADR5,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JSTYPE,COLON,SP1,QUOTE,"Home",QUOTE
          WRITE     JSONFIL1,SEQ;SP2,SP2,JSBRKEND          * close address "}"
          WRITE     JSONFIL1,SEQ;SP2,JSBRKEND,COMMA        * close provider "}"
.
.
.         ----------------------------------------
.         Patient Details
.         ----------------------------------------
.
          STRIP     NHMANMPI
          STRIP     NHMAGIV1
          STRIP     NHMAGIV2
          STRIP     NHMASURN
.
          UNPACK    SP10,DIM10A
          UNPACK    NHMADOB,CCENT,CYEAR,CMON,CDAY
          PACK      DIM10A,CCENT,CYEAR,DASH,CMON,DASH,CDAY  * "YYYY-MM-DD"
          STRIP     DIM10A
.
.         We will use Indicator 2 of Cat."G" for gender
          UNPACK    SP70,TDESC,TCINDC2,THCSCOD
          PACK      KEY5,CATG,PSEX
          CALL      RDCODE1                 * Cat."G"
          MOVE      TCINDC2,DIM1A
          STRIP     DIM1A
.
.           "patient": {
          WRITE     JSONFIL1,SEQ;SP2,JPATIENT,COLON,SP1,JSBRKSTA      * open "{"
.
.             "details": {
.               "nhi": "",
.               "firstName": "",
.               "middleName": "",
.               "surname": "",
.               "dateOfBirth": "",
.               "gender": "",
.               "otherEthnicity": "",
.               "ethnicityCode": ""
.             },
          WRITE     JSONFIL1,SEQ;SP2,SP2,JDETAILS,COLON,SP1,JSBRKSTA  * open "{"
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JSNHI,COLON,SP1,QUOTE,*+,NHMANMPI,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JFRSTNAM,COLON,SP1,QUOTE,*+,NHMAGIV1,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JMIDLNAM,COLON,SP1,QUOTE,*+,NHMAGIV2,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JSURNAME,COLON,SP1,QUOTE,*+,NHMASURN,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JSDOB,COLON,SP1,QUOTE,*+,DIM10A,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JSGENDER,COLON,SP1,QUOTE,*+,DIM1A,QUOTE,*-,COMMA

          STRIP     NHMAETH
          MOVE      NHMAETH,F2
          IF        F2=54 | F2=61
            UNPACK    SP10,DIM10B       * clear "Other Ethnicity"
            IF        F2=54
              MOVE      "Not Known",DIM10B
            ENDIF
            IF        F2=61
              MOVE      SP10,DIM10B
            ENDIF
            WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JETHNIOT,COLON,SP1,QUOTE,*+,DIM10B,QUOTE,*-,COMMA
          ENDIF
.
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JETHNICD,COLON,SP1,QUOTE,*+,NHMAETH,QUOTE,*-
          WRITE     JSONFIL1,SEQ;SP2,SP2,JSBRKEND,COMMA    * close details "}"
.
.         Address Fields
          STRIP     NHMAADD1
          STRIP     NHMAADD2
          STRIP     NHMAADD3
          STRIP     NHMAADD4
          STRIP     NHMAADD5
.
.         Get last 4 digits of Address Line 4 for the Postcode
          UNPACK    SP10,DIM4A
          MOVELPTR  NHMAADD4,FORM2
          SUB       FOUR,FORM2
          BUMP      NHMAADD4,FORM2
          MOVE      NHMAADD4,DIM4A
          RESET     NHMAADD4
.
          TYPE      DIM4A         * Test if value is numberic
          IF        !@EQUAL
            MOVE      NHMADOMC,DIM4A     * Use Domicile Code instead
            STRIP     DIM4A
            GOTO      JSNCLM10
          ENDIF
.
.         Truncate Address Line 4 to exclude the last 4 characters (Postcode)
          MOVELPTR  NHMAADD4,FORM2
          SUB       FOUR,FORM2
          SETLPTR   NHMAADD4,FORM2
          STRIP     NHMAADD4
.
.             "address": {
.               "line1": "",
.               "line2": "",
.               "suburb": "",
.               "city": "",
.               "postCode": "",
.               "country": "",
.               "type": "Home"
.             },
.
JSNCLM10  WRITE     JSONFIL1,SEQ;SP2,SP2,JADDRESS,COLON,SP1,JSBRKSTA  * open "{"
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JADDLNE1,COLON,SP1,QUOTE,*+,NHMAADD1,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JADDLNE2,COLON,SP1,QUOTE,*+,NHMAADD2,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JADDSUBB,COLON,SP1,QUOTE,*+,NHMAADD3,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JADDCITY,COLON,SP1,QUOTE,*+,NHMAADD4,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JADDPCOD,COLON,SP1,QUOTE,*+,DIM4A,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JADDCTRY,COLON,SP1,QUOTE,*+,NHMAADD5,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JSTYPE,COLON,SP1,QUOTE,"Home",QUOTE
          WRITE     JSONFIL1,SEQ;SP2,SP2,JSBRKEND,COMMA    * close address "}"
.
.             "contact": {
.               "emailAddress": "",
.               "mobilePhone": "",
.               "homePhone": "",
.               "workPhone": ""
.             },
          STRIP     PMPXPEML
.
          SQUEEZE   PTMXCELL
          STRIP     PTMXCELL
.
          UNPACK    SP20,DIM20A
          MATCH     SP70,PTMXCELL
          IF        @EQUAL | @EOS
            MOVE      PTELEP,DIM20A
          ELSE
            MOVE      PTMXCELL,DIM20A
          ENDIF
          REP       "( ) ",DIM20A      * Remove parentheses
          SQUEEZE   DIM20A
          STRIP     DIM20A
.
          REP       "( ) ",PTELEB      * Remove parentheses
          SQUEEZE   PTELEB
          STRIP     PTELEB
.
          WRITE     JSONFIL1,SEQ;SP2,SP2,JCONTACT,COLON,SP1,JSBRKSTA  * open "{"
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JEMAILAD,COLON,SP1,QUOTE,*+,PMPXPEML,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JMOBPHON,COLON,SP1,QUOTE,*+,PTMXCELL,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JHOMPHON,COLON,SP1,QUOTE,*+,DIM20A,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JWRKPHON,COLON,SP1,QUOTE,*+,PTELEB,QUOTE,*-
          WRITE     JSONFIL1,SEQ;SP2,SP2,JSBRKEND,COMMA    * close contact "}"
.
.             "occupationCode": ""
          UNPACK    SP10,DIM4A
          PACK      KEY5,CATP6,PUSR6,SP5
          CALL      RDCODE1               * Cat."P6"
          IF        OVRCD=0
            MOVE      PTCDNHCP,DIM4A      * use HCP Equivalent
          ENDIF
          STRIP     DIM4A
          WRITE     JSONFIL1,SEQ;SP2,SP2,JOCCPCOD,COLON,SP1,QUOTE,*+,DIM4A,QUOTE,*-
.
.           },
          WRITE     JSONFIL1,SEQ;SP2,JSBRKEND,COMMA        * close patient "}"
.
.
.         ----------------------------------------
.         Employment Details
.         ----------------------------------------
.
.           "employment": {
          WRITE     JSONFIL1,SEQ;SP2,JEMPLMNT,COLON,SP1,JSBRKSTA      * open "{"
.
.         Get Employment Status
          UNPACK    SP70,DIM4A,DIM5A,TCINDC1,THCSCOD
          PACK      KEY5,CATIF,PMWXESTA
          CALL      RDCODE1                 * Cat."IF"
          MATCH     "Y",TCINDC1             * In paid employment
          GOTO      JSNCLM11 IF EQUAL
.
.         ----------------------
.         Not in paid employment
.         ----------------------
.             "inPaidEmployment": "False"
          MOVE      ZERO,INPAIDEM
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JEMPPAID,COLON,SP1,JSFALSE
          GOTO      JSNCLM12
.
.         ------------------
.         In paid employment
.         ------------------
JSNCLM11  MOVE      ONE,INPAIDEM
          MOVE      THCSCOD,DIM4A      * Employment Status Code (HDP Default)
          STRIP     DIM4A
.
.             "inPaidEmployment": "True",
.             "employmentStatusCode": "",
.             "otherEmployment": "",
.             "workTypeCode": "",
.           },
          WRITE     JSONFIL1,SEQ;SP2,SP2,JEMPPAID,COLON,SP1,JSTRUE,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,JEMPSTCD,COLON,SP1,QUOTE,*+,DIM4A,QUOTE,*-,COMMA
.
.         Other Employment
          MATCH     "O",TCINDC2        * 'Other' Employment Status
          IF        @EQUAL
            STRIP     PMWXOEST
            WRITE     JSONFIL1,SEQ;SP2,SP2,JEMPOTHR,COLON,SP1,QUOTE,*+,PMWXOEST,QUOTE,*-,COMMA
          ENDIF
.
.         Work Type Code
          UNPACK    SP70,TDESC,THCSCOD
          PACK      KEY5,CATIX,PMWXTWRK
          CALL      RDCODE1                 * Cat."IX" 
          MOVE      THCSCOD,DIM4B           * Work Type Code (HDP Default)
          STRIP     DIM4B
          WRITE     JSONFIL1,SEQ;SP2,SP2,JWRKTYPC,COLON,SP1,QUOTE,*+,DIM4B,QUOTE,*-,COMMA
.
.         ----------------
.         Employer Details
.         ----------------
.             "employer": {
.               "employerName": "",
.               "address": {
.                 "line1": "",
.                 "line2": "",
.                 "suburb": "",
.                 "city": "",
.                 "postCode": "",
.                 "country": "",
.                 "type": "Home"
.               }
.             }
          STRIP     WCENAME
          STRIP     WCEADD1
          STRIP     WCEADD2
          STRIP     WCEADD3
          STRIP     WCEADD4
          STRIP     WCEPOST
.
          WRITE     JSONFIL1,SEQ;SP2,SP2,JEMPLOYR,COLON,SP1,JSBRKSTA  * open "{"
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JEMPLNAM,COLON,SP1,QUOTE,*+,WCENAME,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JADDRESS,COLON,SP1,JSBRKSTA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,JADDLNE1,COLON,SP1,QUOTE,*+,WCEADD1,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,JADDLNE2,COLON,SP1,QUOTE,*+,WCEADD2,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,JADDSUBB,COLON,SP1,QUOTE,*+,WCEADD3,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,JADDCITY,COLON,SP1,QUOTE,*+,WCEADD4,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,JADDPCOD,COLON,SP1,QUOTE,*+,WCEPOST,QUOTE,*-,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,JSTYPE,COLON,SP1,QUOTE,"Home",QUOTE
.
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JSBRKEND     * close address "}"
          WRITE     JSONFIL1,SEQ;SP2,SP2,JSBRKEND         * close employer "}"
.
JSNCLM12  WRITE     JSONFIL1,SEQ;SP2,JSBRKEND,COMMA       * close employment "}"
.
.
.         ----------------------------------------
.         Injury Details
.         ----------------------------------------
.
.         Accident Date & Time
          UNPACK    WCACDAT,CCENT,CYEAR,CMON,CDAY
          PACK      DIM10A,CCENT,CYEAR,DASH,CMON,DASH,CDAY  * "YYYY-MM-DD"
          STRIP     DIM10A                  * Injury Date
          UNPACK    PMWXATME,DIM5A          * Injury time
.
.         Accident Scene Code
          UNPACK    SP70,TDESC,THCSCOD
          PACK      KEY5,CATIP,PMWXPLIN
          CALL      RDCODE1                 * Cat."IP"
          MOVE      THCSCOD,DIM2A           * Use HDP Default
          STRIP     DIM2A
.
.         Accident Location Code
          UNPACK    SP70,TDESC,THCSCOD
          PACK      KEY5,CATIM,PMWXACLC
          CALL      RDCODE1                 * Cat."IM"
          MOVE      THCSCOD,DIM3A           * Use HDP Default
          STRIP     DIM3A
.
.         Cause of Injury
          UNPACK    SP70,ACCMACCN,ACCMTYPE,DACCMLIN
          CLEAR     TXT250A
          MOVE      "001",KEY3                   * Cause of Injury
          PACK      KEY26,ACCCLMNO,KEY3,SP10     * Accident No.
          CALL      RSACCMT1
JSNCLM20  CALL      RKACCMT1                     * read Cause of Injury Comments
          BRANCH    OVRCD,JSNCLM21
          MATCH     ACCCLMNO,ACCMACCN            * same Accident No ?
          GOTO      JSNCLM21 IF NOT EQUAL
          MATCH     KEY3,ACCMTYPE
          GOTO      JSNCLM21 IF NOT EQUAL
          STRIP     ACCMDATA
          APPEND    ACCMDATA,TXT250A
          APPEND    SP1,TXT250A
          GOTO      JSNCLM20
.
JSNCLM21  RESET     TXT250A
          STRIP     TXT250A
.
.           "injury": {
.             "accidentDate": "",
.             "injuryComments": "",
.             "accidentLocationCode": "",
.             "accidentSceneCode": "",
.             "causeOfAccident": "",
.             "involvesVehicle": "",
.             "medicalTreatmentInjury": "",
.             "workInjury": "",
.             "sportingInjury": "",
.             "sportNameCode": "",
.             "accContactProvider": "",
.             "gradualProcessInjury": "",
.             "admittedToHospital": ""
.           },
          WRITE     JSONFIL1,SEQ;SP2,JSINJURY,COLON,SP1,JSBRKSTA  * open "{"
          WRITE     JSONFIL1,SEQ;SP2,SP2,JACCDATE,COLON,SP1,QUOTE,DIM10A,QUOTE,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,JACCCAUS,COLON,SP1,QUOTE,*+,TXT250A,*-,QUOTE,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,JACCSCCD,COLON,SP1,QUOTE,*+,DIM2A,*-,QUOTE,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,JACCLOCD,COLON,SP1,QUOTE,*+,DIM3A,*-,QUOTE,COMMA
.
.         Injury Comments
          UNPACK    SP70,ACCMACCN,ACCMTYPE,DACCMLIN
          UNPACK    SP70,ACCMDATA
          UNPACK    SP70,TXT250A 
          CLEAR     TXT250A
          MOVE      "002",KEY3                   * Injury Comments
          PACK      KEY26,ACCCLMNO,KEY3,SP10
          CALL      RSACCMT1
JSNCLM22  CALL      RKACCMT1                     * read Cause of Injury Comments
          BRANCH    OVRCD,JSNCLM23
          MATCH     ACCCLMNO,ACCMACCN            * same Accident No ?
          GOTO      JSNCLM23 IF NOT EQUAL
          MATCH     KEY3,ACCMTYPE
          GOTO      JSNCLM23 IF NOT EQUAL
          STRIP     ACCMDATA
          APPEND    ACCMDATA,TXT250A
          APPEND    SP1,TXT250A
          GOTO      JSNCLM22
.
JSNCLM23  RESET     TXT250A
          STRIP     TXT250A
.
          WRITE     JSONFIL1,SEQ;SP2,SP2,JINJCMNT,COLON,SP1,QUOTE,*+,TXT250A,*-,QUOTE,COMMA
.
.         Involves Vehicle
          MATCH     "Y",PMWXMOVV       * Involves vehicle
          IF        @EQUAL
            WRITE     JSONFIL1,SEQ;SP2,SP2,JINVLVEH,COLON,SP1,JSTRUE,COMMA
          ELSE
            WRITE     JSONFIL1,SEQ;SP2,SP2,JINVLVEH,COLON,SP1,JSFALSE,COMMA
          ENDIF
.
.         Medical Treatment Injury
          MATCH     "1",PMWXTRIC
          IF        @EQUAL
            WRITE     JSONFIL1,SEQ;SP2,SP2,JMEDTRMT,COLON,SP1,JSTRUE,COMMA
          ELSE
            WRITE     JSONFIL1,SEQ;SP2,SP2,JMEDTRMT,COLON,SP1,JSFALSE,COMMA
          ENDIF
.
.         Work Injury
          UNPACK    SP70,TDESC,TCINDC1,THCSCOD
          PACK      KEY5,CATIQ,PMWXACCF
          CALL      RDCODE1                 * Cat."IQ" 
          MOVE      TCINDC1,DIM1C
          MATCH     "Y",TCINDC1
          IF        @EQUAL
            WRITE     JSONFIL1,SEQ;SP2,SP2,JWRKINJR,COLON,SP1,JSTRUE,COMMA
          ELSE
            WRITE     JSONFIL1,SEQ;SP2,SP2,JWRKINJR,COLON,SP1,JSFALSE,COMMA
          ENDIF
.
.         Sporting Injury
          MATCH     "1",PMWXSPTI
          IF        @EQUAL
            WRITE     JSONFIL1,SEQ;SP2,SP2,JSPRTINJ,COLON,SP1,JSTRUE,COMMA
.
            UNPACK    SP70,TDESC,THCSCOD
            PACK      KEY5,CATIB,PMWXSPRT
            CALL      RDCODE1                 * Cat."IB"
            MOVE      THCSCOD,DIM4C           * Sporting Name Code
            STRIP     DIM4C
.
            WRITE     JSONFIL1,SEQ;SP2,SP2,JSPRTNAM,COLON,SP1,QUOTE,*+,DIM4C,*-,QUOTE,COMMA
          ELSE
            WRITE     JSONFIL1,SEQ;SP2,SP2,JSPRTINJ,COLON,SP1,JSFALSE,COMMA
          ENDIF
.
.         Gradual Process Injury
          MATCH     "1",PMWXGRAD
          IF        @EQUAL
            WRITE     JSONFIL1,SEQ;SP2,SP2,JGRADINJ,COLON,SP1,JSTRUE,COMMA
          ELSE
            WRITE     JSONFIL1,SEQ;SP2,SP2,JGRADINJ,COLON,SP1,JSFALSE,COMMA
          ENDIF
.
.         Admitted to Hospital
          MATCH     "1",PMWXAHOS
          IF        @EQUAL
            WRITE     JSONFIL1,SEQ;SP2,SP2,JADMTHOS,COLON,SP1,JSTRUE,COMMA
          ELSE
            WRITE     JSONFIL1,SEQ;SP2,SP2,JADMTHOS,COLON,SP1,JSFALSE,COMMA
          ENDIF
.
.         Assistance is Required
          MATCH     "1",PMWXASST
          IF        @EQUAL
            WRITE     JSONFIL1,SEQ;SP2,SP2,JASSTREQ,COLON,SP1,JSTRUE,COMMA
          ELSE
            WRITE     JSONFIL1,SEQ;SP2,SP2,JASSTREQ,COLON,SP1,JSFALSE,COMMA
          ENDIF
.
.         ACC should contact the Provider
          MATCH     "1",PMWXNEED       * Yes
          IF        @EQUAL
            WRITE     JSONFIL1,SEQ;SP2,SP2,JCONPROV,COLON,SP1,QUOTE,"4",QUOTE
          ELSE
            WRITE     JSONFIL1,SEQ;SP2,SP2,JCONPROV,COLON,SP1,QUOTE,"1",QUOTE
          ENDIF
.
          WRITE     JSONFIL1,SEQ;SP2,JSBRKEND,COMMA        * close injury "}"
.
.
.         ----------------------------------------
.         Diagnosis Details
.         ----------------------------------------
.
          WRITE     JSONFIL1,SEQ;SP2,JDIAGNOS,COLON,SP1,JSARRSTA  * open "["
          MOVE      ZERO,ISFIRST  * is first diagnosis block
.
          UNPACK    SP70,EMIC9COD,EMICDESC
          PACK      KEY23,ACCCLMNO,SP20
          CALL      RSACDIA1
JSNCLM31  CALL      RKACDIA1
          BRANCH    OVRCD,JSNCLM39
.
          MATCH     ACCCLMNO,ACDIACCN       * same Accident Claim no ?
          GOTO      JSNCLM39 IF NOT EQUAL
.
          UNPACK    SP70,EMIC9COD,EMICDESC
          PACK      KEY9,ACDIDIAG,SP10
          CALL      RDEMICD1
.
          MOVE      PTCNDIGN,DIGHDPCD   * Use EMR Diag HDP Code & Coding System?
          UNPACK    SP70,DIM30,DIM30A
          PACK      DIM256,SP100,SP100,SP70
.
          PACK      DIM30A,EMICVEMD,SP70         * use EMR Diag Code
          PACK      DIM256,EMICDESC,SP100,SP100  * use EMR Diag Code Description
.
          IF        DIGHDPCD=0
            MOVE      ACDICSYC,DIM3
            SQUEEZE   DIM3
            MOVE      DIM3,F3
            IF        F3=0 | F3=1 | F3=2 | F3=3
              MOVE      ACDICSYC,DIM30      * ACDICSYC 1=ICD9 2=ICD10 3=READ
              REP       "1A2B3C",DIM30
              REP       "A2B3C1",DIM30      * ACC NZ   1=Read 2=ICD9 3=ICD10
              MOVE      ACDIDIAG,DIM30A
            ELSE
              UNPACK    SP70,DIM30,DIM30A
              PACK      DIM256,SP100,SP100,SP70
.
              MOVE      "emricdaf",PMVMTNAM
              PACK      PMVMTIDX,ACDIDIAG,SP70
              PACK      PMVMMVSI,ACDICSYC,SP10
.
              PACK      KEY41,PMVMTNAM,PMVMTIDX,PMVMMVSI,SP70
              CALL      RDPMVMT1
              IF        OVRCD=0
                PACK      KEY3,PMVMMVSI,SP10
                CALL      RDPMVST1               * get Value Set
                IF        OVRCD=0
                  MOVE      PMVTHDPE,DIM30       * use HDP Equivalent of Set ID 
                ENDIF
.
                MOVE      PMVMMCOD,DIM30A        * use Value Set Mapping Code
.
                PACK      KEY33,PMVMMVSI,PMVMMCOD,SP70
                CALL      RDPMVSC1               * get Value Set Code (mapped)
                IF        OVRCD=0
                  MOVE      PMVCPTRM,DIM256      * use Preferred Term
                ENDIF
              ENDIF
            ENDIF
          ELSE
            IF         DIGHDPCD=1
              MOVE       "1",DIM30               * SNOMED Code mapped to READ
            ELSE
              MOVE       "3",DIM30               * SNOMED Code mapped to ICD10
            ENDIF
          ENDIF
.
          STRIP     DIM30    (ACDICSYC / SNOMED Value Set HDP Equivalent)
          STRIP     DIM30A   (ACDIDIAG / SNOMED Mapped Code)
          STRIP     ACDISIDE
          STRIP     DIM256   (EMICDESC / SNOMED Value Set Code Preferred Term)
.
.         Diagnosis Side
          UNPACK    SP20,DIM20A
          MOVE      ZERO,F1
          MOVE      ACDISIDE,F1
          LOAD      DIM20A,F1,JSIDELEF,JSIDERIG,JNOTAPPL
          STRIP     DIM20A
.
          IF        ISFIRST=0
            WRITE     JSONFIL1,SEQ;SP2,SP2,JSBRKSTA        * open "{"
          ELSE
            WRITE     JSONFIL1,SEQ;SP2,SP2,COMMA,JSBRKSTA  * open ",{"
          ENDIF
.
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JDIAGCOD,COLON,SP1,QUOTE,*+,DIM30A,*-,QUOTE,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JDIAGTYP,COLON,SP1,QUOTE,*+,DIM30,*-,QUOTE,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JDIAGDES,COLON,SP1,QUOTE,*+,DIM256,*-,QUOTE,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JDIAGSID,COLON,SP1,*+,DIM20A,*-
.
          WRITE     JSONFIL1,SEQ;SP2,SP2,JSBRKEND     * close "}"
.
          IF        ISFIRST=0
            MOVE      ONE,ISFIRST      * Not the first diagnosis block
          ENDIF
.
          GOTO      JSNCLM31
.
JSNCLM39  WRITE     JSONFIL1,SEQ;SP2,JSARREND,COMMA   * close diagnosis "],"
.         
.
.         ----------------------------------------
.         Work Capacity Details
.         ----------------------------------------
.
          COMPARE   ONE,INPAIDEM       * In paid employment ?
          GOTO      JSNCLM50 IF NOT EQUAL
.
          MOVE      ZERO,F1
          MATCH     SP70,PMWXCONT      * Continue Normal Hours of Work Y/N
          IF        !@EQUAL & !@EOS
            MOVE      PMWXCONT,F1
          ELSE
            MOVE      ONE,F1           * Can resume normal work
          ENDIF
.
.           "workCapacity": {
          WRITE     JSONFIL1,SEQ;SP2,JWRKCAPC,COLON,SP1,JSBRKSTA  * open "{"
.
          IF        F1=1
.           ----------------------
.           Can resume normal work
.           ----------------------
.             "canResumeNormalWork": "True"
            WRITE     JSONFIL1,SEQ;SP2,SP2,JCANRESU,COLON,SP1,JSTRUE
.
          ELSE
.           ----------------------------
.           Unable to resume normal work
.           ----------------------------
.             "canResumeNormalWork": "False",
.
            WRITE     JSONFIL1,SEQ;SP2,SP2,JCANRESU,COLON,SP1,JSFALSE,COMMA
.
.           ------------------
.           Incapacity Details
.           ------------------
.               "incapacity": [
            WRITE     JSONFIL1,SEQ;SP2,SP2,JINCAPCI,COLON,SP1,JSARRSTA
.
            MOVE      ZERO,F1
            MOVE      PMWXALTW,F1      * Alternative Work Indicator; 0=No 1=Yes
            IF        F1=0
.
.                 {
.                   "incapacityType": "Fullyunfitforwork",
.                   "incapacityDateRange": {
.                     "fromDate": "2017-12-11",
.                     "toDate": "2017-12-14"
.                   }
.                 }
.
              UNPACK    SP70,DIM10A,DIM10B
.
.             Full Incapacity Start Date
              UNPACK    PMWXFISD,CCENT,CYEAR,CMON,CDAY
              PACK      DIM10A,CCENT,CYEAR,DASH,CMON,DASH,CDAY  * "YYYY-MM-DD"
              STRIP     DIM10A
.
.             Return to Normal Work Date
              MOVE      PMWXRNWD,RNGTODTE   * Return to Normal Work Date
              SUB       ONE,RNGTODTE
              UNPACK    RNGTODTE,CCENT,CYEAR,CMON,CDAY
              PACK      DIM10B,CCENT,CYEAR,DASH,CMON,DASH,CDAY  * "YYYY-MM-DD"
              STRIP     DIM10B
.
              WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JSBRKSTA       * open "{"
              WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,JINCAPTY,COLON,SP1,JUNFITFW,COMMA
              WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,JINCAPDR,COLON,SP1,JSBRKSTA
              WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,SP2,JSFRDATE,COLON,SP1,QUOTE,*+,DIM10A,*-,QUOTE,COMMA
              WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,SP2,JSTODATE,COLON,SP1,QUOTE,*+,DIM10B,*-,QUOTE
              WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,JSBRKEND   * close
              WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JSBRKEND  * close "}"
.
            ELSE
              MATCH     SP70,PMWXFISD       * Full Incapacity Start Date exists
              IF        !@EQUAL
.
.                 {
.                   "incapacityType": "Fullyunfitforwork",
.                   "incapacityDateRange": {
.                     "fromDate": "2017-12-09",
.                     "toDate": "2017-12-10"
.                   },
.                   "selectedAlternativeWork": ""
.                 },
.
                UNPACK    SP70,DIM10A,DIM10B
.
.               Full Incapacity Start Date
                UNPACK    PMWXFISD,CCENT,CYEAR,CMON,CDAY
                PACK      DIM10A,CCENT,CYEAR,DASH,CMON,DASH,CDAY  * "YYYY-MM-DD"
                STRIP     DIM10A
.
.               Full Incapacity End Date
                MOVE      PMWXFISD,RNGTODTE      * Full Incapacity Start Date
                RJUSTIFY  PMWXUNFD
                MOVE      ZERO,F2
                MOVE      PMWXUNFD,F2            * Unfit for Work For Duration
                ADD       F2,RNGTODTE
                SUB       ONE,RNGTODTE
                UNPACK    RNGTODTE,CCENT,CYEAR,CMON,CDAY
                PACK      DIM10B,CCENT,CYEAR,DASH,CMON,DASH,CDAY  * "YYYY-MM-DD"
                STRIP     DIM10B
.
                WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JSBRKSTA          * open "{"
                WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,JINCAPTY,COLON,SP1,JUNFITFW,COMMA
                WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,JINCAPDR,COLON,SP1,JSBRKSTA
                WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,SP2,JSFRDATE,COLON,SP1,QUOTE,*+,DIM10A,*-,QUOTE,COMMA
                WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,SP2,JSTODATE,COLON,SP1,QUOTE,*+,DIM10B,*-,QUOTE
                WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,JSBRKEND,COMMA
                WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,JSELALTW,COLON,SP1,QUOTE,QUOTE
                WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JSBRKEND,COMMA    * close "}"
.
              ENDIF
.
.                 {
.                   "incapacityType": "Fitforselectedwork",
.                   "incapacityDateRange": {
.                     "fromDate": "2017-12-11",
.                     "toDate": "2017-12-14"
.                   },
.
              UNPACK    SP70,DIM10A,DIM10B
.
              UNPACK    PMWXSALT,CCENT,CYEAR,CMON,CDAY
              PACK      DIM10A,CCENT,CYEAR,DASH,CMON,DASH,CDAY  * "YYYY-MM-DD"
              STRIP     DIM10A
.
              MATCH     SP70,PMWXRNWD
              IF        !@EQUAL
                MOVE      PMWXRNWD,RNGTODTE   * Return to Normal Work Date
                SUB       ONE,RNGTODTE
                UNPACK    RNGTODTE,CCENT,CYEAR,CMON,CDAY
                PACK      DIM10B,CCENT,CYEAR,DASH,CMON,DASH,CDAY  * "YYYY-MM-DD"
                STRIP     DIM10B
              ENDIF
.
              WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JSBRKSTA       * open "{"
              WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,JINCAPTY,COLON,SP1,JFITSELW,COMMA
              WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,JINCAPDR,COLON,SP1,JSBRKSTA
              WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,SP2,JSFRDATE,COLON,SP1,QUOTE,*+,DIM10A,*-,QUOTE,COMMA
              WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,SP2,JSTODATE,COLON,SP1,QUOTE,*+,DIM10B,*-,QUOTE
              WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,JSBRKEND,COMMA
.
.             -------------------------
.             Selected Alternative Work
.             -------------------------
              UNPACK    SP70,TDESC,THCSCOD
              PACK      KEY5,CATIH,PMWXTYPA
              CALL      RDCODE1                 * Cat."IH"
              MOVE      THCSCOD,DIM4A
              STRIP     DIM4A
.
              STRIP     PMWXHPDA
.
              UNPACK    SP70,ACCMACCN,ACCMTYPE,DACCMLIN
              UNPACK    SP70,ACCMDATA
              UNPACK    SP70,TXT250A 
              CLEAR     TXT250A
              MOVE      "003",KEY3               * Injury Comments
              PACK      KEY26,ACCCLMNO,KEY3,SP10
              CALL      RSACCMT1
JSNCLM41      CALL      RKACCMT1                 * read Cause of Injury Comments
              BRANCH    OVRCD,JSNCLM42
              MATCH     ACCCLMNO,ACCMACCN        * same Accident No ?
              GOTO      JSNCLM42 IF NOT EQUAL
              MATCH     KEY3,ACCMTYPE
              GOTO      JSNCLM42 IF NOT EQUAL
              STRIP     ACCMDATA
              APPEND    ACCMDATA,TXT250A
              APPEND    SP1,TXT250A
              GOTO      JSNCLM41
.
JSNCLM42      RESET     TXT250A
              STRIP     TXT250A
.
              MOVE      ZERO,FORM2
              MOVE      PMWXHPDA,FORM2      * Hours per day of Alternative Work
.
.                   "selectedAlternativeWork": {
.                     "alternativeWorkTypeCode": "1",
.                     "physicalRestrictions": [
.                       {
.                         "restriction": ""
.                       }
.                     ],
.                     "restrictedHoursPerDay": 3,
.                     "restrictionComment": ""
.                   }
.                 }
              WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,JSELALTW,COLON,SP1,JSBRKSTA
              WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,SP2,JALTWKTC,COLON,SP1,QUOTE,*+,DIM4A,*-,QUOTE,COMMA
.
.               "physicalRestrictions": [
.             WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,SP2,JPHYSRES,COLON,SP1,JSARRSTA
.             WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,SP2,SP2,JSBRKSTA
.             WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,SP2,SP2,SP2,JRESTRTN,COLON,SP1,QUOTE,*+,TXT250A,*-,QUOTE
.             WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,SP2,SP2,JSBRKEND
.             WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,SP2,JSARREND,COMMA
.
              WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,SP2,JREHOURS,COLON,SP1,FORM2,COMMA
              WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,SP2,JRECOMNT,COLON,SP1,QUOTE,*+,TXT250A,*-,QUOTE
              WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,SP2,JSBRKEND   * close "}"
              WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JSBRKEND       * close "}"
            ENDIF
.
.           Close incapacity section (array)
            WRITE     JSONFIL1,SEQ;SP2,SP2,JSARREND,COMMA       * close "],"
.
.
.           --------------------------
.           Return to Normal Work Date
.           --------------------------
            UNPACK    SP10,DIM10A
            MOVE      PMWXRNWD,RTWDATE
            MOVE      RTWDATE,DIM8A
            UNPACK    DIM8A,CCENT,CYEAR,CMON,CDAY
            PACK      DIM10A,CCENT,CYEAR,DASH,CMON,DASH,CDAY    * "YYYY-MM-DD"
            STRIP     DIM10A
.
.             "returnToNormalWorkDate": ""
            WRITE     JSONFIL1,SEQ;SP2,SP2,JRTWDATE,COLON,SP1,QUOTE,*+,DIM10A,QUOTE,*-
          ENDIF
.
          WRITE     JSONFIL1,SEQ;SP2,JSBRKEND,COMMA     * close workCapacity "}"
.
.
.         ----------------------------------------
.         Referral Details
.         ----------------------------------------
.
.           "referrals": [
JSNCLM50  WRITE     JSONFIL1,SEQ;SP2,JREFERAL,COLON,SP1,JSARRSTA  * open "["
          MOVE      ZERO,ISFIRST  * is first referral block
          MOVE      ZERO,F1       * reset counter
.
          UNPACK    SP70,ACCMACCN,ACCMTYPE,DACCMLIN
          PACK      KEY23,ACCCLMNO,SP20
          CALL      RSACRFP1
JSNCLM51  CALL      RKACRFP1
          BRANCH    OVRCD,JSNCLM59
          MATCH     ACCCLMNO,ACRFACCN       * same Accident Claim no ?
          GOTO      JSNCLM59 IF NOT EQUAL
.
          ADD       ONE,F1
.
          UNPACK    SP70,TDESC,THCSCOD
          PACK      KEY5,CATIW,ACRFRPRT
          CALL      RDCODE1                 * Cat."IW"
          MOVE      THCSCOD,DIM4A
.
          STRIP     ACRFSTC1
          STRIP     ACRFSTC2
          STRIP     ACRFSTC3
.
          CLEAR     TXT250A
          APPEND    ACRFSTC1,TXT250A
          APPEND    SP1,TXT250A
          APPEND    ACRFSTC2,TXT250A
          APPEND    SP1,TXT250A
          APPEND    ACRFSTC3,TXT250A
          RESET     TXT250A
.
          STRIP     TXT250A
          STRIP     DIM4A
.
.             {
.               "providerTypeCode": "81",
.               "referralReason": ""
.             }
          IF        ISFIRST=0
            WRITE     JSONFIL1,SEQ;SP2,SP2,JSBRKSTA        * open "{"
          ELSE
            WRITE     JSONFIL1,SEQ;SP2,SP2,COMMA,JSBRKSTA  * open ",{"
          ENDIF
.
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JSPROVTC,COLON,SP1,QUOTE,*+,DIM4A,*-,QUOTE,COMMA
          WRITE     JSONFIL1,SEQ;SP2,SP2,SP2,JREFREAS,COLON,SP1,QUOTE,*+,TXT250A,*-,QUOTE
          WRITE     JSONFIL1,SEQ;SP2,SP2,JSBRKEND          * close "}"
.
          IF        ISFIRST=0
            MOVE      ONE,ISFIRST      * Not the first diagnosis block
          ENDIF
.
          COMPARE   F1,FIVE            * limit to 5 referrals
          GOTO      JSNCLM59 IF EQUAL
.
          GOTO      JSNCLM51           * get next referral
.
.           ],
JSNCLM59  WRITE     JSONFIL1,SEQ;SP2,JSARREND,COMMA   * close referrals "],"
.
.
.         ----------------------------------------
.         Declaration Details
.         ----------------------------------------
.
.         Patient Declaration Date
          UNPACK    SP10,DIM10A
          UNPACK    PMWXPDDT,CCENT,CYEAR,CMON,CDAY
          PACK      DIM10A,CCENT,CYEAR,DASH,CMON,DASH,CDAY  * "YYYY-MM-DD"
          STRIP     DIM10A
.
.           "declaration": {
.             "providerDeclaration": "YYYY-MM-DD"
.           }
          WRITE     JSONFIL1,SEQ;SP2,JDECLARN,COLON,SP1,JSBRKSTA  * open "{"
          WRITE     JSONFIL1,SEQ;SP2,SP2,JSPROVDC,COLON,SP1,QUOTE,DIM10A,QUOTE
          WRITE     JSONFIL1,SEQ;SP2,JSBRKEND         * close declaration "}"
.
          MOVE      ONE,CLMDONE        * set flag to write new acclodaf record
JSNCLM99  RETURN
+
.******************************************************************************
.*                             APIPOS00                                       *
.*        Post the JSON Claim Details (via patweb60.us1)                      *
.******************************************************************************
APIPOS00  MOVE      ZERO,EXIT
.
          UNPACK    ACCCLMNO,DIM8
          SQUEEZE   DIM8
          PACK      EXTFNAME,DIM8,DOT,JSON
.
          PACK      PATHNAME,SP30,SP30,SP30,SP10
          CLEAR     PATHNAME
.
          COMPARE   "1",IBCNMHOS
          GOTO      APIPOS10 IF NOT EQUAL
.
          PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,APIPOS10
.
          STRIP     WBSEHOSP
          PACK      PATHNAME,PTCNACPT,WBSEHOSP,SLASH,EXTFNAME
          GOTO      APIPOS20
.
APIPOS10  PACK      PATHNAME,PTCNACPT,EXTFNAME
.
APIPOS20  CLEAR     CMDLINE1
          APPEND    "patweb60.us1 ",CMDLINE1
          STRIP     PTCNACAE
          APPEND    PTCNACAE,CMDLINE1
          APPEND    SP1,CMDLINE1
          STRIP     PTCNACAK
          APPEND    PTCNACAK,CMDLINE1
          APPEND    SP1,CMDLINE1
          APPEND    PATHNAME,CMDLINE1
          RESET     CMDLINE1
.
          EXECUTE   CMDLINE1,TASKID
.
          CALL      IBACLOCK
          PACK      DIM8A,CCC,CYY,CMM,CDD
          REP       " 0",DIM8A 
          UNPACK    CTIMEIS,DIM8B,DIM4A
          REP       " 0",DIM8B
.
          PACK      KEY20,ACCCLMNO,SP20
          CALL      RDACLOD1
          BRANCH    OVRCD,APIPOS99
.
          MATCH     "1",TASKID
          IF        @EQUAL
            MOVE      THREE,ACLOSTAT   * 3 - Failed; script failure
            MOVE      ONE,EXIT
          ELSE
            MOVE      ONE,ACLOSTAT     * 1 - Sent
          ENDIF
.
          MOVE      DIM8A,ACLOSDAT
          MOVE      DIM8A,ACLOUDAT
          MOVE      DIM8B,ACLOUTIM  
          MOVE      WBSEUID,ACLOWUID
          CALL      UPACLOD1
.
APIPOS99  RETURN
+
