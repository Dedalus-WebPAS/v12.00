.************************************************************************
.*  Procedure: DGCLIWLU  -  W/L record updated for Scheduled patient    *
.*                                                                      *
.*  Author   : Steve Armstrong (03/11/2011)                             *
.*                                                                      *
.*  Requires : PATMA1FD (Patient Master & Extension)                    *
.*             PMSPX2FD (Patient Master Extn.2)                         *
.*             WATTR1FD (Waiting List Patient Procedures)               *
.*             WATTX1FD (Waiting List file extension 1)                 *
.*                                                                      *
.*             PATCONFD (Patient Control File)                          *
.*             WATCONFD (Waiting List Control File)                     *
.*                                                                      *
.*  Mods     :                                                          *
.*                                                                      *
.************************************************************************
.*  V11.04.01 09/01/2024 Thanh T           TSK 0936263                  *
.*            Added OPEN/CLOSE PMSQPXA1 index                           *
.************************************************************************
.*  V10.03.02 17/04/2013  Davin S          CAR 280425                   *
.*            Changed IBCNUCIS - value 1 or greater will write trigger  *
.*        V10.03.01 Steve Armstrong  CAR 267329                         *
.*                  Added CISMFLAG and IBAMFLAG variables for compiling *
.*                  purposes only.                                      *
.*                  Also added check on parameter for sending HL7       *
.*                  messages.                                           *
.*                                                                      *
.************************************************************************
          DEFPROC   DGCLIWLU
.
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
          INC       PMSQPTFD/INC
          INC       WATQUEFD/INC
.
CISMFLAG  FORM      1
IBAMFLAG  FORM      1
MESSAGID  DIM       20
.
DGCLI000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,HUND11;*TWENTY5,WTCNA08M
.
          MATCH     "1",IBCNUCIS                 * sending HL7 messages ?
          GOTO      DGCLI999 IF LESS             * no - finished
.
          COMPARE   ONE,WTCNA08M
          GOTO      DGCLI999 IF NOT EQUAL
.
.         Open the broadcast message holding tables
.
          OPEN      HL7CISA1,"hl7cisaf"
          OPEN      PMSQPTA1,"pmsqptaf"
          OPEN      PMSQPXA1,"pmsqpxaf"
          OPEN      WATQUEA1,"watqueaf"
.
          CALL      DGMESSID                     * get the unique message id
.
.         Write a record to pmsqptaf (holding patient details)
.
          MOVE      MESSAGID,PMQPMESI
          MOVE      SP8,PMQPOURN
          PACK      PMQPSPAR,SP30,SP30,SP20
          CALL      WRPMQPT1
.
.         Write a record to watqueaf (holding W/L details)
.
          MOVE      MESSAGID,WTQUMESI
          PACK      WTQUSPAR,SP30,SP30
          CALL      WRWTQUE1
.
          MOVE      "A08",H7CIMETP
          COMPARE   TEN2,HL7TRGID
          IF        @EQUAL
            MOVE      "A02",H7CIMETP        * transfer message
          ENDIF
          MOVE      HL7TRGID,H7CITRID
          MOVE      HL7INCLD,H7CIINCL
          MOVE      SP20,H7CIRTAG
          MOVE      SP3,H7CISTAG
          CALL      WRCIS000
.
          CLOSE     HL7CISA1
          CLOSE     PMSQPTA1
          CLOSE     PMSQPXA1
          CLOSE     WATQUEA1
.
DGCLI999  GOTO      DGCLIEND                     * end procedure
.
.         IO's go in here
.
          INC       BRHLCOMN
          INC       CLPMSQPX
.
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       IBAOVRIO/INC
          INC       PMSQPTIO/INC
          INC       WATQUEIO/INC
.
DGCLIEND  ENDPROC
