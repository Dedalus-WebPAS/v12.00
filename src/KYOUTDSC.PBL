.******************************************************************************
.* System         : Outpatients                                               *
.*                                                                            *
.* Include Name   : KYOUTDSC.PBL                                              *
.*                                                                            *
.* Function       : To keyin the Outpatient Discharge Details                 *
.*                : also contains MYOUTDSC which loads up the variable DIM80  *
.*                  for the routine 'spck0000' for mandatory fields check     *
.*                                                                            *
.* Author         : Paul Howells       22/04/1997                             *
.*                                                                            *
.* Parameters     : SCRPSTFD/SCRSLTFD records                                 *
.*                  ORIGVISI      original number of visits                   *
.*                  ACCEPT        1=insert mode, 2=change mode                *
.*                                                                            *
.* Modifications  :                                                           *
.*        V11.00.01 10/03/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*                  21/11/1997  Steve Armstrong                               *
.*                  Added keyins for UDF fields 5 - 8                         *
.******************************************************************************
KYOUTDSC  MOVE      SCPSITM,FIELDNO
          MOVE      SCPSROW,EVERT
          MOVE      SCPSCOL,ECOL
          MOVE      SCPSROW,CVERT
          MOVE      SCPSCOL,CCOL
          MOVE      SCPSMAN,CKYIMAND
          SFORMAT   KEYFIELD,127
          MOVE      ZERO,EXIT
.
          BRANCH    FIELDNO,KOTDS001,KOTDS999,KOTDS003,KOTDS999,KOTDS005:
                            KOTDS006,KOTDS999,KOTDS008,KOTDS999,KOTDS010:
                            KOTDS999,KOTDS012,KOTDS999
.
          BEEP
          GOTO      KOTDS999
.
KOTDS001  CALL      KDSTAT00                * Discharge Status
          GOTO      KOTDS999
.
KOTDS003  CALL      KDDIAG00                * Diagnosis
          GOTO      KOTDS999
.
KOTDS005  CALL      KDRANK00                * Patient Rank         
          GOTO      KOTDS999
.
KOTDS006  CALL      KD5USR00                * User Defined Field 5
          GOTO      KOTDS999
.
KOTDS008  CALL      KD6USR00                * User Defined Field 6
          GOTO      KOTDS999
.
KOTDS010  CALL      KD7USR00                * User Defined Field 7
          GOTO      KOTDS999
.
KOTDS012  CALL      KD8USR00                * User Defined Field 8
          GOTO      KOTDS999
.
KOTDS999  RETURN
.
.*********************************************************************
.         clear the outpatient discharge details
.*********************************************************************
CLOUTDSC  UNPACK    SP30,OBOUSR5,OBOUSR6,OBOUSR7,OBOUSR8
          RETURN
+
+
.*********************************************************************
.         loads up the variable for mandatory field check
.         Para's  : FORM5         the variable number to check for
.*********************************************************************
MYOUTDSC  LOAD      DIM80,FORM5,OBADISCH,ANSA,OPDICODE,OPDIDESC,OTBBRANK:
                                OBOUSR5,ANSA,OBOUSR6,ANSA,OBOUSR7,ANSA:
                                OBOUSR8,ANSA
          RETURN
+
.******************************************************************************
.         Discharge Status - OBADISCH - Cat DO
.******************************************************************************
KDSTAT00  PACK      CODE,ANSD,ANSO          * Category 
          MOVE      OBADISCH,CKYIDEF3       * Default code
.
          CALL      PATCODKY                * Keyin the code
          COMPARE   TWO,EXIT
          GOTO      KDSTAT95 IF EQUAL       *  EXITCHAR entered
          BRANCH    ABORT,KDSTAT99          * EXITCHAR input
.
          MOVE      ACODE,OBADISCH          * the code
          CALL      DISPVARS                * redisplay variable
.
          MOVE      "    2",KEY5            * set for desc
          CALL      SCRCKFLD                * see if to display description
          BRANCH    EXIT,KDSTAT95           * dont display
          PACK      VAR,TDESC,SP30,SP30,SP20
          CALL      DISPITEM                * display field
.
KDSTAT90  MOVE      ZERO,EXIT
          GOTO      KDSTAT99
KDSTAT95  MOVE      ONE,EXIT
.
KDSTAT99  RETURN
.******************************************************************************
.         Diagnosis
.******************************************************************************
KDDIAG00  MOVE      CCOL,ECOL                * set default keyin column
          MOVE      CVERT,EVERT              * set default row
.
          BRANCH    HDIAFLG,KDDIAG10,KDDIAG20    * MBS / ICD
          MOVE      SP9,OPDICODE             * No code, using free format
          GOTO      KDDIAG30
.
.         Input an MBS code
.
KDDIAG10  KEYIN     *PECOL:EVERT,*DV,UNDLN9:
                    *PECOL:EVERT,*V2LON,OPDICODE:
                    *PECOL:EVERT,*DV,OPDICODE
          MOVE      ECOL,CCOL
          ADD       TEN,CCOL                * description column
.
          ENDSET    OPDICODE
          APPEND    SP9,OPDICODE
          RESET     OPDICODE
.
          MATCH     SP9,OPDICODE
          GOTO      KDDIAG30 IF EQUAL        * No MBS code entered
.
          PACK      KEY17,OPDICODE,SP70
          OPEN      PATITEM1,"patitemn"
          CALL      PATITMRD
          CLOSE     PATITEM1
          BRANCH    OVRCD,KDDIAG18
.
          PACK      OPDIDESC,IDESC,SP30
          GOTO      KDDIAG30
.
KDDIAG18  DISPLAY   *P1:24,*EL,*B,"Invalid MBS Code.  ";
          CALL      HITENTER
          GOTO      KDDIAG10
.
.         Input an ICD9CM code
.
KDDIAG20  KEYIN     *PECOL:EVERT,*DV,UNDLN9:
                    *PECOL:EVERT,*V2LON,OPDICODE:
                    *PECOL:EVERT,*DV,OPDICODE
          MOVE      ECOL,CCOL
          ADD       TEN,CCOL                * description column
.
          ENDSET    OPDICODE
          APPEND    SP9,OPDICODE
          RESET     OPDICODE
.
          MATCH     SP9,OPDICODE
          GOTO      KDDIAG30 IF EQUAL        * No MBS code entered
.
          MOVE      OPDICODE,KEY9
          OPEN      PATICDD1,"paticddf"
          OPEN      PATI10D1,"pati10df"
          CALL      RD10T9D1
          IF        OVRCD=1
              CALL      RDPT10D1
          ENDIF
          CLOSE     PATICDD1
          CLOSE     PATI10D1
          BRANCH    OVRCD,KDDIAG28
.
          PACK      OPDIDESC,DDESC,SP30
          GOTO      KDDIAG30
.
KDDIAG28  DISPLAY   *P1:24,*EL,*B,"Invalid ICD9 Code.  ";
          CALL      HITENTER
          GOTO      KDDIAG20
.
.         Input the free format description, using a retain variable on keyin
.         If we are using code input, the keyin will be at column 30 (after the
.         code). If we are using free format input, the keyin will be at
.         column 20 (to line up with all the other keyin's).
.
KDDIAG30  MATCH     SP30,OPDIDESC
          GOTO      KDDIAG40 IF EQUAL
.
          KEYIN     *PCCOL:EVERT,*DV,OPDIDESC:
                    *PCCOL:EVERT,*RV,*V2LON,OPDIDESC:
                    *PCCOL:EVERT,*DV,OPDIDESC
          GOTO      KDDIAG60
.
KDDIAG40  KEYIN     *PCCOL:EVERT,*DV,UNDLN30,*DV,UNDLN20:
                    *PCCOL:EVERT,*V2LON,OPDIDESC:
                    *PCCOL:EVERT,*DV,OPDIDESC
.
KDDIAG60  ENDSET    OPDIDESC
          APPEND    SP30,OPDIDESC
          APPEND    SP30,OPDIDESC
          RESET     OPDIDESC
.
          MOVE      ONE,CHGDIA               * Diagnosis changed
.
KDDIAG99  RETURN
.******************************************************************************
.* KDRANK00 - Keyin Patient Rank                                    
.******************************************************************************
KDRANK00  MOVE      OTBBRANK,FORM6 
          KEYIN     *PCCOL:CVERT,*V2LON,*DV,FORM6:   
                    *PCCOL:CVERT,*RV,FORM6:
                    *PCCOL:CVERT,*DV,FORM6;
.
          COMPARE   ZERO,FORM6      * test less than zero
          IF        @LESS
            DISPLAY   *P1:24,*EL,*B,"Cannot be Less Than Zero. ";
            CALL      HITENTER
            GOTO      KDRANK00
          ENDIF
.
          MOVE      FORM6,OTBBRANK
.
KDRANK99  RETURN
+
.------------------------------------------------------------------------------
.         User defined O5 - OBOUSR5
.------------------------------------------------------------------------------
KD5USR00  PACK      CODE,ANSO,FIVE          * Category 
          MOVE      OBOUSR5,CKYIDEF3         * Default code
.
          CALL      PATCODKY                * Keyin the code
          COMPARE   TWO,EXIT
          GOTO      KD5USR95 IF EQUAL       *  EXITCHAR entered
          BRANCH    ABORT,KD5USR99          * EXITCHAR input
.
          MOVE      ACODE,OBOUSR5           * the code
          CALL      DISPVARS                * redisplay variable
.
          MOVE      "    7",KEY5            * set for desc
          CALL      SCRCKFLD                * see if to display description
          BRANCH    EXIT,KD5USR90           * dont display
.
          PACK      VAR,TDESC,SP30,SP30,SP20
          CALL      DISPITEM                * display field
.
KD5USR90  MOVE      ZERO,EXIT
          GOTO      KD5USR99
KD5USR95  MOVE      ONE,EXIT
.
KD5USR99  RETURN
.
.------------------------------------------------------------------------------
.         User defined O6 - OBOUSR6
.------------------------------------------------------------------------------
KD6USR00  PACK      CODE,ANSO,SIX           * Category 
          MOVE      OBOUSR6,CKYIDEF3         * Default code
.
          CALL      PATCODKY                * Keyin the code
          COMPARE   TWO,EXIT
          GOTO      KD6USR95 IF EQUAL       *  EXITCHAR entered
          BRANCH    ABORT,KD6USR99          * EXITCHAR input
.
          MOVE      ACODE,OBOUSR6           * the code
          CALL      DISPVARS                * redisplay variable
.
          MOVE      "    9",KEY5            * set for desc
          CALL      SCRCKFLD                * see if to display description
          BRANCH    EXIT,KD6USR90           * dont display
.
          PACK      VAR,TDESC,SP30,SP30,SP20
          CALL      DISPITEM                * display field
.
KD6USR90  MOVE      ZERO,EXIT
          GOTO      KD6USR99
KD6USR95  MOVE      ONE,EXIT
.
KD6USR99  RETURN
.
.------------------------------------------------------------------------------
.         User defined O7 - OBOUSR7
.------------------------------------------------------------------------------
KD7USR00  PACK      CODE,ANSO,SEVEN         * Category 
          MOVE      OBOUSR7,CKYIDEF3         * Default code
.
          CALL      PATCODKY                * Keyin the code
          COMPARE   TWO,EXIT
          GOTO      KD7USR95 IF EQUAL       *  EXITCHAR entered
          BRANCH    ABORT,KD7USR99          * EXITCHAR input
.
          MOVE      ACODE,OBOUSR7           * the code
          CALL      DISPVARS                * redisplay variable
.
          MOVE      "   11",KEY5            * set for desc
          CALL      SCRCKFLD                * see if to display description
          BRANCH    EXIT,KD7USR90           * dont display
.
          PACK      VAR,TDESC,SP30,SP30,SP20
          CALL      DISPITEM                * display field
.
KD7USR90  MOVE      ZERO,EXIT
          GOTO      KD7USR99
KD7USR95  MOVE      ONE,EXIT
.
KD7USR99  RETURN
.
.------------------------------------------------------------------------------
.         User defined O8 - OBOUSR8
.------------------------------------------------------------------------------
KD8USR00  PACK      CODE,ANSO,EIGHT         * Category 
          MOVE      OBOUSR8,CKYIDEF3         * Default code
.
          CALL      PATCODKY                * Keyin the code
          COMPARE   TWO,EXIT
          GOTO      KD8USR95 IF EQUAL       *  EXITCHAR entered
          BRANCH    ABORT,KD8USR99          * EXITCHAR input
.
          MOVE      ACODE,OBOUSR8           * the code
          CALL      DISPVARS                * redisplay variable
.
          MOVE      "   13",KEY5            * set for desc
          CALL      SCRCKFLD                * see if to display description
          BRANCH    EXIT,KD8USR90           * dont display
.
          PACK      VAR,TDESC,SP30,SP30,SP20
          CALL      DISPITEM                * display field
.
KD8USR90  MOVE      ZERO,EXIT
          GOTO      KD8USR99
KD8USR95  MOVE      ONE,EXIT
.
KD8USR99  RETURN
.
