. ************************************************************
. *                I N C L U D E  S U M M A R Y              *
. *                - - - - - - -  - - - - - - -              *
. *                                                          *
. *     PROCEDURE NAME:        IBATIME                       *
. *                                                          *
. *     FUNCTION:              KEYIN TIME     KEYTIME        *
. *                            DISPLAY TIME   DSPTIME        *
. *                            PRINT   TIME   PRTTIME        *
. *                            UNDLN   TIME   UDLTIME        *
. *                                                          *
. *     AUTHOR:                MALCOLM HAYSE (I.B.A)         *
. *                                                          *
. *     WRITTEN:               24/02/88                      *
. *                                                          *
. *     INPUT :                CMIN IS THE MIN               *
. *                            CHOUR IS THE HOUR             *
. *                            CCOL IS STARTING COL          *
. *                            CVERT IS STRATING VERTICAL    *
. *                                                          *
. *     MODIFICATIONS : V5.01.01 11/01/90 Graeme Williams    *
. *                              Added CCANLDTE as new param.*
. *                              SRF 103238                  *
. *                     V5.01.02 30/04/92 Graeme Williams    *
. *                              Use *NOMASK for keyins      *
. *       V5.01.03  02/04/1993 Graeme Williams               *
. *                 Copied from release 6. Uses              *
. *                 GETVAR/PUTVAR to preserve line           *
. *                 24 after error messages                  *
. *                                                          *
. *                                                          *
. *     NOTE   IF NEW TIME CMIN=SP2,CHOUR=SP2                *
. *            OTHERWISE WE ASSUME WE HAVE A PREVIOUS        *
. *            TIME ENTERED AND DISPLAYED                    *
. *            THEREFORE WHEN KEYING IN NEW TIME             *
. *            THE OLD FIGURES WILL BE RETAINED              *
. *                                                          *
. ************************************************************
.
.         Subroutine to key in a TIME
.
KEYTIME   MOVE       CHOUR,XHOUR
          MOVE       CMIN,XMIN
          MOVE       ZERO,OVRCD
.
          CALL       DSPTIME
          CALL       UDLTIME
.
          MOVE       CCOL,CCOL1
          MOVE       CCOL,CCOL2
          ADD        THREE,CCOL2
.
KEYTME1   CALL       KEYHOUR
          BRANCH     OVRCD OF STOPTIME,KEYTME9
.
KEYTME2   CALL       KEYMIN
          BRANCH     OVRCD OF KEYTIME
KEYTME9   RETURN
.
.         KEYIN THE HOUR OF THE TIME
.
KEYHOUR   MOVE       ZERO,OVRCD
          MOVE       ZERO,CQUEST
.
          MATCH      SP2,CHOUR
          GOTO       RETKHUR IF NOT EQUAL
.
.         NEW TIME
.
          DISPLAY    *PCCOL1:CVERT,UNDLN2
.
          BRANCH     CHIGHLT OF KEYHOURN
          KEYIN      *PCCOL1:CVERT,*NOMASK,*V2LON,*ZF,*JR,*+,XHOUR;
          GOTO       KEYHOURO
.
KEYHOURN  KEYIN      *PCCOL1:CVERT,*NOMASK,*ZF,*JR,*+,XHOUR;
.
KEYHOURO  RESET      XHOUR
          GOTO       OVERCOND IF EOS
.
          GOTO       CONTHOUR
.
.         IF A CHOUR HAS A VALUE RETAIN VARIABLE (OLD TIME)
.
RETKHUR   BRANCH     CHIGHLT OF RETKHURN
          KEYIN      *PCCOL1:CVERT,*NOMASK,*V2LON,*ZF,*JR,*+,XHOUR;
          GOTO       CONTHOUR
.
RETKHURN  KEYIN      *PCCOL1:CVERT,*NOMASK,*ZF,*JR,*+,XHOUR;
.
CONTHOUR  RESET      XHOUR
          GOTO       CONTHR01 IF NOT EOS
.
          BRANCH     CDEFDTE,OVERLOCK
.
          MOVE       CHOUR,XHOUR
.
CONTHR01  MATCH      "0?",XHOUR
          GOTO       CHRQST IF EQUAL
.
          MATCH      SP2,XHOUR
          GOTO       KHRCNCL IF EQUAL
.
          MATCH      "0 ",XHOUR              * Single blank entered (*ZF).
          GOTO       KHRCNCL IF EQUAL
.
CNTHR02   MOVE       XHOUR,IHOUR
.
          COMPARE    ZERO,IHOUR
          GOTO       INVHOUR IF LESS
.
          COMPARE    "24",IHOUR
          GOTO       INVHOUR IF NOT LESS
.
          MOVE       IHOUR,XHOUR
          REP        " 0",XHOUR
          BRANCH     CHIGHLT OF CONTHOUN
          DISPLAY    *PCCOL1:CVERT,*V2LON,XHOUR;
          RETURN
.
CONTHOUN  DISPLAY    *PCCOL1:CVERT,XHOUR;
          RETURN
.
KHRCNCL   COMPARE    ZERO,CCANLDTE
          GOTO       CNTHR02 IF EQUAL
.
          UNPACK     SP4,CHOUR,CMIN
          MOVE       ONE,OVRCD
          RETURN
.
INVHOUR   MOVE      "Invalid Hour",DDISPMSG
          CALL      DATEMSGC                 * found in IBADATE.PBL
          CALL       DSPTIME
          CALL       UDLTIME
          GOTO      KEYHOUR
.
.         KEYIN THE MINUTE OF THE TIME
.
KEYMIN    MOVE       ZERO,OVRCD
          MATCH      SP2,CMIN
          GOTO       RETKMIN IF NOT EQUAL
.
.         NEW TIME
.
          DISPLAY    *PCCOL2:CVERT,UNDLN2
.
          BRANCH     CHIGHLT OF KEYMINN
          KEYIN      *PCCOL2:CVERT,*NOMASK,*V2LON,*ZF,*JR,*-,XMIN;
          GOTO       KEYMINO
.
KEYMINN   KEYIN      *PCCOL2:CVERT,*NOMASK,*ZF,*JR,*-,XMIN;
KEYMINO   RESET      XMIN
          GOTO       OVERCOND IF EOS
.
          MOVE       XMIN,IMIN
          GOTO       CONTMIN
.
.         IF A CHOUR HAS A VALUE RETAIN VARIABLE  (OLD TIME)
.
RETKMIN   MOVE       CMIN,XMIN
          BRANCH     CHIGHLT OF RETKMINN
          KEYIN      *PCCOL2:CVERT,*NOMASK,*V2LON,*ZF,*JR,*RV,*-,XMIN;
          GOTO       RETKMINO
.
RETKMINN  KEYIN      *PCCOL2:CVERT,*NOMASK,*ZF,*JR,*RV,*-,XMIN;
RETKMINO  MOVE       XMIN,IMIN
.
CONTMIN   COMPARE    ZERO,IMIN
          GOTO       INVMIN IF LESS
.
          COMPARE    "60",IMIN
          GOTO       INVMIN IF NOT LESS
.
          MOVE       IMIN,XMIN
          REP        " 0",XMIN
          BRANCH     CHIGHLT OF CONTMINN
          DISPLAY    *PCCOL2:CVERT,*V2LON,XMIN;
          GOTO       ENDHHMM
.
CONTMINN  DISPLAY    *PCCOL2:CVERT,XMIN;
.
ENDHHMM   MOVE       XHOUR,CHOUR
          MOVE       XMIN,CMIN
          RETURN
.
INVMIN    MOVE      "Invalid Minute",DDISPMSG
          CALL      DATEMSGC                 * found in IBADATE.PBL
          GOTO      KEYMIN
.
CHRQST    MOVE       ONE,CQUEST           *** ? OPTION
          GOTO       OVERCOND             *** MUST BE SET
+
.
.       DISPLAY ANY TIME IN CORRECT FORMAT
.
DSPTIME   MATCH      SP2,CHOUR
          RETURN     IF EQUAL
.
          BRANCH     CHIGHLT OF DSPHIGT
          DISPLAY    *PCCOL:CVERT,*V2LON,CHOUR,COLON,CMIN;
          RETURN
.
DSPHIGT   DISPLAY    *PCCOL:CVERT,CHOUR,COLON,CMIN;
          RETURN
.
.       DISPLAY UNDERLINE FOR TIME IN CORRECT FORMAT
.
UDLTIME   MATCH      SP2,CHOUR
          RETURN     IF NOT EQUAL
          DISPLAY    *PCCOL:CVERT,UNDLN2,COLON,UNDLN2
          RETURN
.
.       REDISPLAY UDLTIME AS SPACES.. NEW TIME EOS ON KEYIN
.       OVRCD FLAG IS SET    (BY KEAYHOUR OR KEYMIN)
.
STOPTIME  DISPLAY    *PCCOL:CVERT,SP5
          RETURN
