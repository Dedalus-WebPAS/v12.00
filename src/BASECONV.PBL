.******************************************************************************
.* BASECONV       : Convert a number between base 10 and a given base         *
.* Procedures     : TOBASE    Convert from base 10 to base NEWBASE            *
.*                  FROMBASE  Convert from base NEWBASE to base 10            *
.*                                                                            *
.* Author         : Graeme Williams  23/06/1993                               *
.******************************************************************************
.******************************************************************************
.* TOBASE         : Convert from base 10 to base NEWBASE                      *
.*                                                                            *
.* Parameters     : B10NUMB   FORM      ??   Define the size as required      *
.*                  NEWBASE   FORM      2    Initialise to the new base       *
.*                  BASEDGTS  INIT      ??   Initialise to the digits for the *
.*                                           new base, starting from zero.    *
.*                                                                            *
.*                  Example: Converting to base 16                            *
.*                  NEWBASE   FORM      "16"                                  *
.*                  BASEDGTS  INIT      "0123456789ABCDEF"                    *
.*                                                                            *
.* Returns        : NEWBNUMB  DIM       ??   B10NUMB in new base              *
.******************************************************************************
          DEFPROC   TOBASE
.
OLDNUMB   FORM      21
INDEXF    FORM      3
INDEXT    FORM      3
.
TOBASE    PACK      NEWBNUMB,SP30,SP30,SP30,SP30
          MOVELPTR  NEWBNUMB,INDEXT          * position being updated
.
          MOVE      B10NUMB,OLDNUMB
          WHILE     OLDNUMB > 0 & INDEXT > 0
            ASSIGN    (OLDNUMB % NEWBASE)+1,INDEXF
            RESET     BASEDGTS,INDEXF
            RESET     NEWBNUMB,INDEXT
            CMOVE     BASEDGTS,NEWBNUMB
            ASSIGN    (OLDNUMB / NEWBASE),OLDNUMB
            SUB       ONE,INDEXT
          DO
          RESET     NEWBNUMB
          RESET     BASEDGTS
          ENDPROC
.******************************************************************************
.* FROMBASE       : Convert from base NEWBASE to base 10                      *
.*                                                                            *
.* Parameters     : NEWBNUMB  DIM       ??   Number in base NEWBASE           *
.*                  NEWBASE   FORM      2    Initialise to the new base       *
.*                  BASEDGTS  INIT      ??   Initialise to the digits for the *
.*                                           new base, starting from zero.    *
.*                                                                            *
.*                  Example: Converting from base 16                          *
.*                  NEWBASE   FORM      "16"                                  *
.*                  BASEDGTS  INIT      "0123456789ABCDEF"                    *
.*                                                                            *
.* Returns        : B10NUMB   FORM      ??   Number in base 10                *
.******************************************************************************
          DEFPROC   FROMBASE
.
INDEX     FORM      3
.
FROMBASE  MOVE      ZERO,B10NUMB
          RESET     NEWBNUMB
          WHILE     !@EOS
            MOVE      NEWBNUMB,ANS
            RESET     BASEDGTS
            SCAN      ANS,BASEDGTS
            IF        @EQUAL
              MOVEFPTR  BASEDGTS,INDEX
              ASSIGN    (B10NUMB * NEWBASE) + INDEX - 1,B10NUMB
            ENDIF
            BUMP      NEWBNUMB
          DO
          RESET     BASEDGTS
          ENDPROC
