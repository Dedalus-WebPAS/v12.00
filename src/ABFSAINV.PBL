. Program  : ABFSAINV
.
. Function : This include produces NWAU/ABF Sub-Acute calculations
.            (NOT invoicing)
.
. Parameter: PCAREFLG  - flag for Palliative Phase of Care
.            NSNAPFLG  - flag for Qucksnap Extract
.            PRDSDATE  - Period Start date for Palliative phase of care
.            PRDEDATE  - Period End date for Palliative phase of care
.
.*******************************************************************************
. Modification : 
.            V12.00.01 14/05/2025  Peter Vela     TSK 0955096
.                      Alphanumeric Visit Number changes
.            V11.04.03 02/04/2024  J.Tan          TSK 0931613
.                      Mod to default CEFFDATE to Current date for 'As at Eff.'
.            V11.04.02 27/03/2024  J.Tan          TSK 0931613
.                      Mod to setup PRDSDATE for Rehab.
.            V11.04.01 12/01/2024  J.Tan          TSK 0941779
.                      Mod percentage of  APPSAMNT & checking for Priv.Patient
.            V11.03.05 17/11/2023  J.Tan          TSK 0940089
.                      Mod checking for blank Discharged DRG
.            V11.03.04 28/09/2023  J.Tan          TSK 0931613
.                      Mod for ABF Sub-Acute Palliative phase of Care
.            V11.03.05 20/09/2023  J.Tan          TSK 0916870
.                      Mod defaulting NEP value with blank code set/indicator
.            V11.03.04 28/08/2023  J.Tan          TSK 0916870
.                      Added Claim code Cat CL Multiple NEP value
.            V11.03.03 13/07/2023  J.Tan          TSK 0923703
.                      Fixed checking for Daycase Private patient
.            V11.03.02 10/07/2023  J.Tan          TSK 0923703
.                      Fixed Hospital Remoteness for multi hospital
.            V11.03.01 23/05/2023  J.Tan          TSK 0923703
.                      Mod to 6 decimal places in NWAU calculations
.                      Mod to set APPSAMNT - Private Patient service adjustment
.            V11.03.01 24/11/2022 J.Tan    TSK 0906597
.                      Fixed checking Contract Close off date
.            V11.01.00 15/08/2022 J.Tan    TSK 0906597
.                      Created
.*******************************************************************************
.
          DEFPROC   ABFSAINV
.
          INC       IBAPOSFD/INC
          INC       BOKRX1FD/INC
          INC       PATDADFD/INC
          INC       PMSVMTFD/INC
          INC       PATECOFD/INC
          INC       PATRDEFD/INC
          INC       PATACCFD/INC
.
          INC       PMSNEPFD/INC
          INC       PMSADJFD/INC
          INC       PMSPWAFD/INC
          INC       PMSABFFD/INC
          INC       PATABFFD/INC
          INC       PMSRDIFD/INC
          INC       PMSSAWFD/INC
.
APPSFLAG  FORM      1
ATYPCODE  DIM       3[99]
ATYPBDAY  FORM      3[99]
CALCTIME  FORM      6
CALCFLAG  FORM      1
COUNTER   FORM      3
DDRGCODE   DIM       4
FIRSTDAT  DIM       8
FIRSTIME  DIM       4
LASTDATE  DIM       8
LASTTIME  DIM       4
MINUTE1   FORM      3
HOUR1     FORM      6
MINUTE2   FORM      3
HOUR2     FORM      6
LASTHOUR  DIM       2
LASTMIN   DIM       2
FIRSTHR   DIM       2
FIRSTMIN  DIM       2
HOUR      DIM       2
MIN       DIM       2
NEPVALUE  FORM      10.4
.
FORM6     FORM      6
FORM6A    FORM      6
FORM102   FORM      10.2
TTRNDATE  DIM       8
TTRNTIME  DIM       8
SAVATYPE  DIM       3
SAVDATE   DIM       8
SAVTIME   DIM       8
.
          CALL      IVAR0000                  * Initialise variables
.
          MOVE      ZERO,OVRCD               * assume file exists
          TRAP      OVERCOND IF IO           * trap for error openning file
          OPEN      PMSSAWA1,"pmssawaf"
          TRAPCLR   IO
          BRANCH    OVRCD,SAIV9000           * open failed
.
          TRAP      OVERCOND IF IO           * trap for error openning file
          OPEN      PMSSAWA2,"pmssawaf"
          TRAPCLR   IO
          BRANCH    OVRCD,SAIV9000           * open failed
.
          MOVE      ADATE,KEY8                * Use Adm.date
          IF        ASTAT=3
            MOVE      DDATE,KEY8              * Discharged date
          ENDIF
.
          MATCH     "20220701",KEY8
          GOTO      SAIV1000 IF LESS
.
          MOVE      ONE,CALCFLAG              * 2023/2024 ABF calculation
.
SAIV1000  OPEN      PATECDA1,"patecdaf"
          OPEN      PMSVMTA1,"pmsvmtaf"
          OPEN      PATDADA1,"patdadaf"
          OPEN      BOKRX1A1,"bokrx1af"
.
          OPEN      PMSADJA1,"pmsadjaf"
          OPEN      PMSRDIA4,"pmsrdiaf"
          OPEN      PATTRAN2,"pattranf"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATECOA1,"patecoaf"
          OPEN      PATRDEA1,"patrdeaf"
          OPEN      PATACCA1,"pataccaf"
.
          OPEN      PMSABFA1,"pmsabfaf"
          OPEN      PATABFA1,"patabfaf"
          OPEN      PATCHCO2,"patchcof"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND24;*121,PTCNDGED
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          CALL      DABF0000                  * Delete existing Patient ABF det.
.
          MOVE      ZERO,CHGCCLSS
.
          MOVE      AFUNDH,D6                 * Save patient health fund
          MOVE      SP70,AFUNDH               * Blank HF, use Claim Grouper Ver.
          CALL      GETDRG
          MOVE      D6,AFUNDH                 * restore HF
          IF        OVRCD=0
            MOVE      DDRGCODE,PDRGCODE
          ENDIF
.
          MATCH     SP70,PDRGCODE
          IF        @EQUAL
            MOVE      PTMIPDRG,PDRGCODE       * Default to Admission DRG
            IF        ASTAT=3
              MATCH     SP70,PTDSDDRG
              IF        !@EQUAL
                MOVE      PTDSDDRG,PDRGCODE   * Use Discharged DRG
              ENDIF
            ENDIF
          ENDIF
.
.         Check for Palliative phase of care
.
          MOVE      SP70,CONTRCID
.
          COMPARE   ONE,PCAREFLG
          GOTO      SAIV1800 IF NOT EQUAL
.
.         delete records from patabfaf file except for AN-SNAP adjustment record
.
          CALL      DPAB0000                  * Delete existing Patient ABF det.
.
.         Check if AN-SNAP code/version was from QuickSnap Extract
.
          COMPARE   ZERO,NSNAPFLG
          GOTO      SAIV1800 IF EQUAL      * No
.
.         Get the AN-SNAP code and version from patabfaf file
.
          PACK      KEY27,AADMNO,PRDSDATE,SP70
          CALL      RSPTABF1
          CALL      RKPTABF1
          BRANCH    OVRCD,SAIV2000
.
          MATCH     DAADMNO,PTABADMN
          GOTO      SAIV2000 IF NOT EQUAL
          MATCH     PTABENCT,PRDSDATE
          GOTO      SAIV2000 IF NOT EQUAL
.
          MATCH     SP70,PTABINVN
          GOTO      SAIV2000 IF NOT EQUAL
.
          MOVE      "070",PTABADJT          * AN-SNAP Adjustment
          GOTO      SAIV2000 IF NOT EQUAL
. 
          UNPACK    PTABCDES,PTRDSNAP,KEY1,PTRDVERS,KEY1,DPTRDEPN
          MOVE      PTABADMN,DPTRDADM
          GOTO      SAIV3000
.
SAIV1800  MOVE      ADATE,PRDSDATE          * default start from adm.date

.         Process each episode from  Rehabilitation extract
.
SAIV2000  MOVE      ZERO,NSNAPFLG
.
          PACK      PRDEPSOD,PRDEPSOD,SP70
          PACK      KEY10,DADMNO,PRDEPSOD,SP70
          CALL      RDPTRDE1
          COMPARE   ZERO,OVRCD
          GOTO      SAIV3000 IF EQUAL
.
          PACK      KEY10,DADMNO,Z70
          CALL      RSPTRDE1                * Read the rehab file
          CALL      RPPTRDE1
          BRANCH    OVRCD,SAIV9000
.
          MATCH     DADMNO,PTRDADMN
          GOTO      SAIV9000 IF NOT EQUAL
.
SAIV3000  CALL      NEPS0000                * Get end of episode
.
          MATCH     SP70,PTRDSNAP
          GOTO      SAIV9000 IF EQUAL       * AN-SNAP code not assigned
.
          IF        ASTAT=3
            MATCH     ADATE,DDATE
            IF        @EQUAL
              MOVE      ONE,ACDAYCS           * flag for daycase
            ENDIF
          ENDIF
          CALL      CPRV0000                * Check for Private patient
.
          MOVE      PRDSDATE,FROMDATE
          REP       " 0",FROMDATE
          MOVE      PRDEDATE,TODATE
          REP       " 0",TODATE
.
          CALL      LCNT0000                  * look for a Contract ID
          BRANCH    EXIT,SAIV9000
.
          PACK      CONTRCID,CONTRCID,SP70
          MATCH     SP70,CONTRCID
          GOTO      SAIV9000 IF EQUAL         * Can not find Contract
          MOVE      CONTRCID,SAVCONTR
.
.         Get the length of stay
.
          CALL      NHOSPDAY
          MOVE      NBRDAYS,CALDAYS
.
          MOVE      NBRDAYS,TOTDAYS           * Actual LOS
          MATCH     PRDSDATE,PRDEDATE
          IF        @EQUAL
            MOVE      ONE,TOTDAYS             * Daycase LOS=1
          ENDIF
.
          MOVE      NBRDAYS,LOSNDAYS          * Patient LOS
          CALL      CLOS0000                  * Check for sameday LOS
.
.         UNPACK    IDATEF INTO FCENT1,FYEAR1,FMON1,FDAY1
.         MOVE      PRDSEDATE,IDATET
.         REP       " 0",IDATET
.
          MOVE      ONE,ABFFLAG
.
          CALL      SNPW0000                  * Save Price weight value
.
.         Check for default Multiple NEP value (blank code set/indicator)
.
          MOVE      SP70,KEY2
          MOVE      SP70,D3
          CALL      MNEP0000
.
          CALL      GADJ0000                  * Get Adjustment value
.
.         Check Multiple NEP value for matching indicator on Care class
.
          MOVE      "CC",KEY2                 * Care class
          MOVE      PRDCCLSS,D3
          CALL      MNEP0000                  * Check Care Class mult.NEP values
.
.         If Multiple NEP value for matching indicator on Claim code exists,
.         this will override the NEP Value from Care class and IND22 of Cat CL
.
          MOVE      "CL",KEY2                 * Claim code
          MOVE      ACLAIM,D3
          CALL      MNEP0000                  * Check Claim code mult.NEP values
.
.         Write NEP Value to ABF file
.
          MOVE      NEPVALUE,NEPAMNTS         * NEP value Adjust.
          MOVE      NEPVALUE,FORM104          * Adjustment value
          PACK      ADJMDESC,SP70,SP70
          MOVE      "061",KEY3                * NEP adjust.
          CALL      WABF0000                  * Write to ABF Patient details
.
          CALL      CPPW0000                  * Calculate Patient weight
.
          CALL      CABF0000                  * Calculate ABF amount
          MULT      NEPAMNTS,ABFCHRGE         * ABF Charge= NWAU x NEP 
          MOVE      ABFCHRGE,GROSSTOT
          MOVE      GROSSTOT,NETTOT
.
          MOVE      ZERO,EXIT
          MOVE      ZERO,NOFEE
.
          GOTO      SAIV9999
.
SAIV9000  MOVE      ONE,NOFEE                 * No ABF charge setup
          MOVE      ONE,EXIT
.
SAIV9999  GOTO      NDSA9999
+
.--------------------------------------------------------------------------
. Get Start & End of episode and Change of care class
.--------------------------------------------------------------------------
NEPS0000  COMPARE   ZERO,CHGCCLSS
          GOTO      NEPS1000 IF EQUAL
.
          IF        NSNAPFLG=1
            MATCH     PRDEDATE,CHDATE
            GOTO      NEPS9999 IF NOT LESS  * no change
          ENDIF
.
          GOTO      NEPS1200              * get next care class change
.
NEPS1000  PACK      KEY26,DPTRDADM,DPTRDEPN,SP70
          CALL      RDSCHCO2
NEPS1200  CALL      RDKCHCO2
          BRANCH    OVRCD,NEPS8000
.
          MATCH     DDADMNO,DCHADMN
          GOTO      NEPS8000 IF NOT EQUAL
.
          MATCH     DPTRDEPN,CHEPISNO     * Check episode number
          GOTO      NEPS2000 IF NOT EQUAL
.
          MATCH     "CC",CHCATG           * care class change ?
          GOTO      NEPS1200 IF NOT EQUAL
.
          PACK      KEY5,CHCATG,CHCODE
          CALL      RDCODE1
          BRANCH    OVRCD,NEPS1200
.
          MOVE      ZERO,FORM1
          SQUEEZE   PTCDUDF6
          MOVE      PTCDUDF6,FORM1
          COMPARE   THREE,FORM1
          GOTO      NEPS1200 IF NOT EQUAL     * NOT Palliative care
.
          MOVE      SP70,KEY2
          UNPACK    THCSCOD,KEY2
          MATCH     "8",KEY2
          GOTO      NEPS1200 IF NOT EQUAL    * NOT Palliative phase of Care
.
          IF        NSNAPFLG=0
            MOVE      CHDATE,PRDEDATE       * set End date of episode care
          ENDIF
          MOVE      CHCODE,PRDCCLSS
          ADD       ONE,CHGCCLSS
          GOTO      NEPS9999
.
NEPS2000  IF        NSNAPFLG=0
            MOVE      CHDATE,PRDSDATE       * start of new episode
          ENDIF
          GOTO      NEPS1200
.
NEPS8000  IF        NSNAPFLG=0
            MOVE      DDATE,PRDEDATE        * use discharged date
          ENDIF
          MOVE      ACARE,PRDCCLSS
.
NEPS9999  RETURN
+
.*******************************************************************************
.         Initialise variables
.*******************************************************************************
IVAR0000  MOVE     ZERO,ARESFLAG
          MOVE     ZERO,ATREFLAG
          MOVE     ZERO,ACCMFLAG
          MOVE     ZERO,ANEPFLAG
          MOVE     ZERO,PRIVFLAG
          MOVE     ZERO,CALCFLAG
          MOVE     ZERO,APPSFLAG
.
          MOVE     ZERO,APPSAMNT
          MOVE     ZERO,AINDAMNT
          MOVE     ZERO,AACCAMNT
          MOVE     ZERO,ARESAMNT
          MOVE     ZERO,ARTAMNTS
          MOVE     ZERO,ATAAMNTS
          MOVE     ZERO,ADIAAMNT
.
          MOVE     ZERO,ABFCHRGE
          MOVE     ZERO,ABFFLAG
          MOVE     ZERO,ACDAYCS              * Assume not a day case
.
          MOVE     ZERO,LOSNDAYS
          MOVE     ZERO,NEPAMNTS
          MOVE     ZERO,NEPVALUE
          MOVE     ZERO,PWAMOUNT
.
          MOVE     SP70,PCARETYP
          MOVE     SP70,PDRGCODE
          PACK     ADJMDESC,SP70,SP70
.
IVAR9999  RETURN
+
.*******************************************************************************
.         Look for a Contract ID for the AN-SNAP and Episode type
.*******************************************************************************
LCNT0000  PACK      KEY9,ACLAIM,AFUNDH,SP70
          CALL      GETCNEFF               * Get Contract Effective From
          BRANCH    EXIT,LCNT9000
.
          PACK      CEFFDATE,CCC,CYY,CMM,CDD   * default to Current date
          REP       " 0",CEFFDATE
          IF        ASTAT=3
            MOVE      PRDEDATE,CEFFDATE        * use period end date
          ENDIF
.
          IF        ASTAT=3
            MOVE      DDATE,KEY8               * default to Disc.date
          ELSE
            PACK      KEY8,CCC,CYY,CMM,CDD * Use Current date
            REP       " 0",KEY8
          ENDIF
          LOAD      CEFFDATE,CNTRCEFR,ADATE,KEY8
.
          MATCH     SP70,PRDCCLSS
          GOTO      LCNT9000 IF EQUAL
          PACK      KEY5,ANSC,ANSC,PRDCCLSS
          CALL      RDCODE1
          BRANCH    OVRCD,LCNT9000
.
          MATCH     SP70,PTCDUDF6
          GOTO      LCNT9000 IF EQUAL
.
.         UDF 6, 2=Rehabilitation, 3=Palliative, 4=GEM,
.                5=Psychogeriatirc, 6=Maintenance
.
          UNPACK    PTCDUDF6,KEY2
          STRIP     KEY2
          PACK      KEY2,KEY2,SP70            * left justified
.
          PACK      KEY13,PTRDSNAP,PTRDVERS,KEY2,SP70
          CALL      RSPMSAW2
LCNT2000  CALL      RKPMSAW2
          BRANCH    OVRCD,LCNT9000
.
          MATCH     PMSACLCD,PTRDSNAP
          GOTO      LCNT9000 IF NOT EQUAL     * Different AN-SNAP Class.
          MATCH     PMSAASVR,PTRDVERS
          GOTO      LCNT9000 IF NOT EQUAL     * Different AN-SNAP Version
          MATCH     PMSACCOD,KEY2
          GOTO      LCNT9000 IF NOT EQUAL     * Different Care type
.
          MOVE      KEY2,PCARETYP
          MOVE      PMSACNTR,KEY6
          CALL      VALCONTR                * Validate Contract ID
          BRANCH    EXIT,LCNT2000
.
.         Checking the Close Off date
.
          MATCH     SP70,PMCICLDT
          IF        !@EQUAL
            MATCH     PMCICLDT,CEFFDATE
            GOTO      LCNT2000 IF NOT LESS
          ENDIF
.
          MOVE      PMSACNTR,CONTRCID
          MOVE      ZERO,EXIT
          GOTO      LCNT9999
.
LCNT9000  MOVE      ONE,EXIT
LCNT9999  RETURN
+
.*******************************************************************************
.         Deleting existing ABF Patient details
.*******************************************************************************
DABF0000  PACK      KEY19,AADMNO,SP70
          CALL      RSPMABF1
          CALL      RKPMABF1
          BRANCH    OVRCD,DABF9999
.
          MATCH     DAADMNO,PMABADMN       * Same admission no ?
          GOTO      DABF9999 IF NOT EQUAL
.
          MATCH     SP70,PMABINVN
          GOTO      DABF9999 IF NOT EQUAL  * Invoice must be blank
.
          PACK      KEY19,PMABADMN,PMABINVN,PMABADJT
          CALL      DEPMABF1
          GOTO      DABF0000
.
DABF9999  RETURN
+
.*******************************************************************************
.         Deleting existing ABF Patient details (patabfaf)
.         delete ABF records from patabfaf except for Adjustment 070 AN-SNAP
.*******************************************************************************
DPAB0000  PACK      KEY27,AADMNO,SP70
          CALL      RSPTABF1
DPAB1000  CALL      RKPTABF1
          BRANCH    OVRCD,DPAB9999
.
          MATCH     DAADMNO,PTABADMN       * Same admission no ?
          GOTO      DPAB9999 IF NOT EQUAL
.
          MATCH     SP70,PTABINVN
          GOTO      DPAB9999 IF NOT EQUAL  * Invoice must be blank
.
.         Do not remove adjustment 070 AN-SNAP
.
          MATCH     "070",PTABADJT
          GOTO      DPAB1000 IF EQUAL
.
          PACK      KEY27,PTABADMN,PTABENCT,PTABINVN,PTABADJT
          CALL      DEPTABF1
.
          GOTO      DPAB1000
.
DPAB9999  RETURN
+
.*******************************************************************************
.         Save Price weight value to Patient details
.*******************************************************************************
SNPW0000  MOVE      "001",KEY3             * Same-day Payment list Adjustment
          MOVE      ZERO,FORM104           * 0 for No
          MATCH     "1",PMSASDOV           * Same day payment list ?
          IF        @EQUAL
            MOVE      ONE,FORM104          * 1 for Yes
          ENDIF
          CALL      WABF0000               * Write to ABF Patient details
.
          MOVE      "003",KEY3             * LOS Adjustment
          MOVE      PMSAALOS,FORM104
          CALL      WABF0000               * Write to ABF Patient details
.
          MOVE      "004",KEY3             * Lower bound Adjustment
          MOVE      PMSALBIN,FORM104
          CALL      WABF0000               * Write to ABF Patient details
.
          MOVE      "005",KEY3             * Upper bound Adjustment
          MOVE      PMSAUBIN,FORM104
          CALL      WABF0000               * Write to ABF Patient details
.
          MOVE      "008",KEY3             * Same day
          MOVE      PMSASDPW,FORM104
          CALL      WABF0000               * Write to ABF Patient details
.
          MOVE      "010",KEY3             * Short Stay perdiem PW Adjustment
          MOVE      PMSASSOP,FORM104
          CALL      WABF0000               * Write to ABF Patient details
.
          MOVE      "011",KEY3             * Inlier PW Adjustment
          IF        PMSAINPW<>0
            MOVE      PMSAINPW,FORM104
            CALL      WABF0000             * Write to ABF Patient details
          ENDIF
.
          MOVE      "012",KEY3             * Long stay per diem PW Adjustment
          MOVE      PMSALSOP,FORM104
          CALL      WABF0000               * Write to ABF Patient details
.
.       Check if AN-SNAP was from QuickSnap (IBAPMS63),do not update the record
.
          BRANCH    NSNAPFLG,SNPW5000      * AN-SNAP code from QuickSnap Extract
.
          MOVE      "070",KEY3             * AN-SNAP Code
          MOVE      ZERO,FORM104
          PACK      ADJMDESC,PTRDSNAP,SLASH,PTRDVERS,SP70
          CALL      WABF0000               * Write to ABF Patient details
.
SNPW5000  MOVE      "072",KEY3             * Rehab.Episode number
          PACK      ADJMDESC,DPTRDEPN,SP70
          CALL      WABF0000               * Write to ABF Patient details
.
SNPW9999  RETURN
+
.*******************************************************************************
.         Check LOS for Same day LOS
.*******************************************************************************
CLOS0000  COMPARE   ZERO,LOSNDAYS
          GOTO      CLOS9999 IF NOT EQUAL
.
          COMPARE   TWO,ASTAT
          GOTO      CLOS9999 IF NOT EQUAL    * Not currently admitted 
.
.         LOS =0, if Intended Stay is 'Same day' (Cat VI ind1=2)
.         LOS = Planned LOS (ASTAY) if 'Overnight' (Cat VI ind1=1)
.
          MOVE      PMVXIDUS,KEY3
          MATCH     SP70,PMVXIDUS
          IF        @EQUAL
            PACK      KEY8,AADMNO
            CALL      RDBKRX11
            IF        OVRCD=0
              MOVE      BKRXUDF5,KEY3
            ENDIF
          ENDIF
.
          MATCH     SP70,KEY3            * Check Intended Stay
          GOTO      CLOS9999 IF EQUAL
.
          MOVE      "VI",KEY2
          PACK      KEY5,KEY2,KEY3
          CALL      RDCODE1
          BRANCH    OVRCD,CLOS9999
.
          MATCH     "2",TCINDC1
          GOTO      CLOS9000 IF EQUAL   * Same day patient
          MATCH     "1",TCINDC1
          GOTO      CLOS9999 IF NOT EQUAL
.
          MOVE      ASTAY,LOSNDAYS    * Use Planned LOS
          GOTO      CLOS9999
.
CLOS9000  MOVE      ZERO,LOSNDAYS     * Same day
CLOS9999  RETURN
+
.*******************************************************************************
.         Calculate Patient PW
.*******************************************************************************
CPPW0000  COMPARE   ZERO,LOSNDAYS
          GOTO      CPPW1000 IF NOT EQUAL
.
.         if Payment list = No and Patient is admitted today, 
.           do not display ABF charges
.
          MATCH     "1",PMSASDOV             * Same day payment list ?
          IF        !@EQUAL
            PACK      KEY8,CCC,CYY,CMM,CDD
            REP       " 0",KEY8
            MATCH     PRDSDATE,KEY8
            GOTO      CPPW9000 IF EQUAL      * Patient admitted today
            MOVE      ONE,LOSNDAYS           * LOS =1
          ELSE
            MOVE      PMSASDPW,PWAMOUNT      * Use same day PW
            MOVE      ONE,LOSNDAYS           * LOS =1
            GOTO      CPPW9000
          ENDIF
.
CPPW1000  
.
.         Check Lower and Upper bound
.
          COMPARE   PMSALBIN,LOSNDAYS
          GOTO      CPPW4000 IF LESS       * less than lower bound
.
          COMPARE   LOSNDAYS,PMSAUBIN
          GOTO      CPPW6000 IF LESS       * > Upper bound
.
.         If LOS within Lower bound and Upper bound, use the Inlier PW
.
          MOVE      PMSAINPW,PWAMOUNT      * Inlier PW
          GOTO      CPPW9000
.
.         If LOS < Lower bound, use Short stay outlier PW + (Short Stay Perdiem
.                   rate PW x LOS)
.
CPPW4000  MOVE      PMSASSOP,PWAMOUNT      * Short Stay perdiem
          MULT      LOSNDAYS,PWAMOUNT
          GOTO      CPPW9000
.
.         If LOS > Upper bound, use the Inlier PW + (Long Stay perdiem 
.                   rate PW x Additional days)
.             Addtional days = LOS - Upper Bound
.
CPPW6000  MOVE      ZERO,FORM4
          MOVE      LOSNDAYS,FORM4
          SUB       PMSAUBIN,FORM4         * Additional days
.
          MOVE      PMSALSOP,PWAMOUNT      * Long Stay perdiem
          IF        FORM4>0
            MULT      FORM4,PWAMOUNT
          ENDIF
          ADD       PMSAINPW,PWAMOUNT      * Inlier PW
.
CPPW9000  MOVE      PWAMOUNT,FORM104
          MOVE      "062",KEY3             * Patient PW
          CALL      WABF0000               * Write to ABF Patient details
.
          MOVE      LOSNDAYS,FORM104
          MOVE      "064",KEY3             * Patient PW
          CALL      WABF0000               * Write to ABF Patient details
.
CPPW9999  RETURN
+
.*******************************************************************************
.         Get Adjustment value
.*******************************************************************************
GADJ0000  PACK      KEY9,CONTRCID,SP70
          CALL      RSPMADJ1
GADJ1000  CALL      RKPMADJ1
          BRANCH    OVRCD,GADJ9999
.
          MATCH     CONTRCID,PMAJCNTR      * Same contract id ?
          GOTO      GADJ9999 IF NOT EQUAL
.
          CALL      CADJ0000               * Check adjustment value
          GOTO      GADJ1000
.
GADJ9999  RETURN
+
.*******************************************************************************
.         Adjustment
.*******************************************************************************
CADJ0000  MOVE      ZERO,ADJTYVAL
          MOVE      PMAJADJT,ADJTYVAL         * Adjustment type
.
          BRANCH    ADJTYVAL,CADJ9999,CADJ9999,CADJ9999,CADJ2000,CADJ2000:
                             CADJ9999,CADJ2000,CADJ9999,CADJ3000,CADJ4000:
                             CADJ5000,CADJ9999,CADJ6200,CADJ6200,CADJ6000:
                             CADJ6000,CADJ6000,CADJ6000,CADJ6000,CADJ9999:
                             CADJ9999,CADJ9999,CADJ7000,CADJ7000
.
          MOVE      ADJTYVAL,F3
          SUB       "24",F3
          BRANCH    F3,CADJ9000,CADJ9000,CADJ9000   * NEP
.
          GOTO      CADJ9999
.
CADJ2000  BRANCH    ARESFLAG,CADJ9999
          CALL      ARES0000        * Residential Remoteness Area adjustment
          GOTO      CADJ9999
.
CADJ3000  CALL      AIND0000        * Indigenous Adjustment
          GOTO      CADJ9999
.
CADJ4000  MOVE      " 1",DIM2       * 1 for Radiotherapy
          CALL      ARTD0000        * Radiotherapy/Dialysis Adjustment
          GOTO      CADJ9999
.
CADJ5000  MOVE      " 2",DIM2       * 2 for Dialysis
          CALL      ARTD0000        * Radiotherapy/Dialysis Adjustment
          GOTO      CADJ9999
.
.   Private Patient Service adjustment
.   PCARETYP=2 for Rehab, 3=Palliative care,4=GEM,
.          5=Psychogeriatric care,6=Maintenance
.
CADJ6000  COMPARE   ONE,PRIVFLAG
          GOTO      CADJ9999 IF NOT EQUAL
.
          BRANCH    APPSFLAG,CADJ9999
.
          MATCH     "015",PMAJADJT
          IF        @EQUAL
            MATCH     "3",PCARETYP
            IF        @EQUAL
              MOVE      "027",KEY3       * Palliative
              GOTO      CADJ6100
            ENDIF
          ENDIF
.
          MATCH     "016",PMAJADJT
          IF        @EQUAL
            MATCH     "2",PCARETYP
            IF        @EQUAL
              MOVE      "028",KEY3       * Rehab
              GOTO      CADJ6100
            ENDIF
          ENDIF
.
          MATCH     "017",PMAJADJT
          IF        @EQUAL
            MATCH     "5",PCARETYP
            IF        @EQUAL
              MOVE      "029",KEY3       * Psychologeriatric care
              GOTO      CADJ6100
            ENDIF
          ENDIF
.
          MATCH     "018",PMAJADJT
          IF        @EQUAL
            MATCH     "4",PCARETYP
            IF        @EQUAL
              MOVE      "030",KEY3         * GEM
              GOTO      CADJ6100
            ENDIF
          ENDIF
.
          MATCH     "019",PMAJADJT
          IF        @EQUAL
            MATCH     "6",PCARETYP
            IF        @EQUAL
              MOVE      "031",KEY3       * Maintenance
              GOTO      CADJ6100
            ENDIF
          ENDIF
          GOTO      CADJ9999
.
CADJ6100  MOVE      PMAJADJV,APPSAMNT  * Private Patient Service adjust.
          IF        APPSAMNT>0
            DIV       "100",APPSAMNT
          ENDIF
          MOVE      APPSAMNT,FORM104   * Adjustment value
          CALL      WABF0000           * Write to ABF Patient details
          MOVE      ONE,APPSFLAG
          GOTO      CADJ9999
.
CADJ6200  IF        PRIVFLAG=1
            CALL      ACCM0000       * Private Patient Accommodation Adjustment
          ENDIF
          GOTO      CADJ9999
.
CADJ7000  BRANCH    ATREFLAG,CADJ9999
          CALL      ETRE0000            * Patient Treatment remoteness area adj.
          GOTO      CADJ9999
.
CADJ9000  BRANCH    ANEPFLAG,CADJ9999
          CALL      ANEP0000        * National Efficient Price (NEP)
          GOTO      CADJ9999
.
CADJ9999  RETURN
+
.*******************************************************************************
.         Check Residential remoteness area adjustment
.*******************************************************************************
ARES0000  MOVE      PMAJADJV,FORM104       * Adjustment value
.
          OPEN      IBAPOST1,"ibapostf"
          PACK      KEY45,PSUBRB,SP70
          PACK      KEY56,PMVXPOST,KEY45,PADD4,SP70
          CALL      RDIBPOS1
          COMPARE   ZERO,OVRCD
          GOTO      ARES2000 IF EQUAL
.
          PACK      KEY56,PMVXPOST,KEY45,SP70
          CALL      RSIBPOS1
          CALL      RKIBPOS1
          BRANCH    OVRCD,ARES9999
.
          MATCH     PMVXPOST,IBPOPCOD
          GOTO      ARES9999 IF NOT EQUAL     * Different postcode
.
ARES2000  MATCH     SP70,IBPORRAV
          GOTO      ARES9999 IF EQUAL
.
          MATCH     "R2   ",IBPORRAV
          IF        @EQUAL
            IF        ADJTYVAL=4
              MOVE      "016",KEY3           * Patient remoteness area adjust.
              CALL      WABF0000             * Write to ABF Patient details
              MOVE      PMAJADJV,ARESAMNT    * Residential value adjust.
              MOVE      ONE,ARESFLAG
            ENDIF
          ELSE
            MATCH     "RA3  ",IBPORRAV
            IF        @EQUAL
              IF        ADJTYVAL=5
                MOVE      "017",KEY3         * Patient remoteness area adjust.
                CALL      WABF0000           * Write to ABF Patient details
                MOVE      PMAJADJV,ARESAMNT  * Residential value adjust.
                MOVE      ONE,ARESFLAG
              ENDIF
            ELSE
              MATCH     "RA4  ",IBPORRAV
              IF        @EQUAL
                IF        ADJTYVAL=7
                  MOVE      "019",KEY3       * Patient remoteness area adjust.
                  CALL      WABF0000         * Write to ABF Patient details
                  MOVE      PMAJADJV,ARESAMNT  * Residential value adjust.
                  MOVE      ONE,ARESFLAG
                ENDIF
              ENDIF
            ENDIF
          ENDIF
ARES9999  RETURN
+
.*******************************************************************************
.         Check Indigenous Adjustment
.*******************************************************************************
AIND0000  MOVE      PMAJADJV,FORM104       * Adjustment value
          MATCH     SP70,PMVXABRG          * Aboriginality
          GOTO      AIND9999 IF EQUAL
          PACK      KEY5,ANSV,ANSA,PMVXABRG
          CALL      RDCODE1
          BRANCH    OVRCD,AIND9999
.
          MATCH     "4",TCINDC2
          GOTO      AIND9999 IF EQUAL
          MATCH     "9",TCINDC2
          GOTO      AIND9999 IF EQUAL
.
          MOVE      PMAJADJV,AINDAMNT  * Indigeneous value adjust.
          MOVE      "021",KEY3         * Indigeneous adjust.
          CALL      WABF0000           * Write to ABF Patient details
.
AIND9999  RETURN
+
.*******************************************************************************
.         Check Radiotherapy/Dialysis Adjustment
.*******************************************************************************
ARTD0000  MOVE      PMAJADJV,FORM104       * Adjustment value
          PACK      KEY13,AADMNO,SP70
          CALL      RSPTECO1
ARTD1000  CALL      RKPTECO1
          BRANCH    OVRCD,ARTD9999
          MATCH     DPTEOADM,DAADMNO
          GOTO      ARTD9999 IF NOT EQUAL
.
          MOVE      "1",KEY1           * for ICD10
          PACK      KEY29,CONTRCID,KEY1,PTEOCODE,SP70
          CALL      RSPMRDI4
ARTD2000  CALL      RKPMRDI4
          BRANCH    OVRCD,ARTD1000
.
          MATCH     CONTRCID,PMRICNTR
          GOTO      ARTD1000 IF NOT EQUAL    * same contract id ?
          MATCH     KEY1,PMRISTYP            * ICD10 ?
          GOTO      ARTD1000 IF NOT EQUAL
          MATCH     PTEOCODE,PMRIICDC        * same code ?
          GOTO      ARTD1000 IF NOT EQUAL
.
          MATCH     DIM2,PMRICTYP
          GOTO      ARTD2000 IF NOT EQUAL    * different type of code
.
          MATCH     " 1",DIM2
          IF        @EQUAL
            MOVE      PMAJADJV,ARTAMNTS  * Radiotherapy value adjust.
            MOVE      "022",KEY3         * Radiotherapy adjust.
          ELSE
            MATCH     "L61Z",PDRGCODE    * Exclude Haemodialysis
            GOTO      ARTD1000 IF EQUAL
            MATCH     "L68Z",PDRGCODE    * Exclude Peritoneal Dialysis
            GOTO      ARTD1000 IF EQUAL
.
            MOVE      PMAJADJV,ADIAAMNT  * Dialysis value adjust.
            MOVE      "023",KEY3         * Dialysis adjust.
          ENDIF
          CALL      WABF0000           * Write to ABF Patient details
          GOTO      ARTD1000
.
ARTD9999  RETURN
+
.*******************************************************************************
.         Private Patient Accommodation Adjustment
.*******************************************************************************
ACCM0000  BRANCH    ACCMFLAG,ACCM9999
.
.         ADJTYVAL = 013 for sameday, 014 for Overnight
.
          IF        ADJTYVAL=13
            COMPARE   ONE,ACDAYCS           * daycase
            GOTO      ACCM9999 IF NOT EQUAL
            MOVE      PMAJADJV,FORM104      * Adjustment value
            MOVE      "025",KEY3
            MOVE      "Private Pt Accom Sameday",ADJMDESC
          ELSE
            IF          ADJTYVAL=14
              BRANCH      ACDAYCS,ACCM9999
              MOVE        PMAJADJV,FORM104  * Adjustment value
              MOVE        "026",KEY3
              MOVE        "Private Pt Accom Overnight",ADJMDESC
            ENDIF
          ENDIF
.
          CALL      WABF0000                 * Write to ABF Patient details
          PACK      ADJMDESC,SP70,SP70
          MOVE      FORM104,AACCAMNT
          MOVE      ONE,ACCMFLAG
.
ACCM9999  RETURN
+
.*******************************************************************************
.         Patient Treatment remoteness (Hospital)
.*******************************************************************************
ETRE0000  MOVE      PMAJADJV,FORM104       * Adjustment value
          UNPACK    SP70,KEY8,KEY45,KEY3
.
          READ      CONTROLF,TEN3;CHADD1,CHADD2,CHPOST
          PACK      KEY8,CHPOST,SP70      * Postcode
          PACK      KEY45,CHADD2,SP70     * Suburb
.
          OPEN      PATHSPA1,"pathspaf"
          PACK      KEY3,SP70
          CALL      RSPTHSP1
          CALL      RKPTHSP1
          BRANCH    OVRCD,ETRE1000
.
          IF        IBCNMHOS=1
            PACK      KEY3,PMVXMHOS,SP70
            CALL      RDPTHSP1
          ENDIF
.
          PACK      KEY8,PTHSPCOD,SP7 0   * Postcode
          PACK      KEY45,PTHSADD2,SP70   * Suburb
          PACK      KEY3,PTHSADD3,SP70    * State
.
ETRE1000  OPEN      IBAPOST1,"ibapostf"
          PACK      KEY56,KEY8,KEY45,KEY3,SP70
          CALL      RDIBPOS1
          COMPARE   ZERO,OVRCD
          GOTO      ETRE2000 IF EQUAL
.
          PACK      KEY56,KEY8,KEY45,SP70
          CALL      RSIBPOS1
          CALL      RKIBPOS1
          BRANCH    OVRCD,ETRE9999
.
          MATCH     KEY8,IBPOPCOD
          GOTO      ETRE9999 IF NOT EQUAL     * Different postcode
.
ETRE2000  MATCH     SP70,IBPORRAV
          GOTO      ETRE9999 IF EQUAL
.
          MATCH     "RA3  ",IBPORRAV
          IF        @EQUAL
            IF        ADJTYVAL=23
                MOVE      "036",KEY3         * Pat.Treatment remoteness area adj
                MOVE      "Hospital Area Remote",ADJMDESC
                CALL      WABF0000           * Write to ABF Patient details
                PACK      ADJMDESC,SP70,SP70
                MOVE      PMAJADJV,ATAAMNTS  * Pat.Treatment value adjust.
                MOVE      ONE,ATREFLAG
            ENDIF
          ELSE
            MATCH     "RA4  ",IBPORRAV
            IF        @EQUAL
              IF        ADJTYVAL=24
                MOVE      "037",KEY3       * Pat.Treatment remoteness area adj.
                MOVE      "Hospital Area Very Remote",ADJMDESC
                CALL      WABF0000           * Write to ABF Patient details
                PACK      ADJMDESC,SP70,SP70
                MOVE      PMAJADJV,ATAAMNTS  * Pat.Treatment value adjust.
                MOVE      ONE,ATREFLAG
              ENDIF
            ENDIF
          ENDIF
.
ETRE9999  RETURN
+
.*******************************************************************************
.         NEP Adjustment
.*******************************************************************************
ANEP0000  MOVE      "CL",KEY2
          PACK      KEY5,KEY2,ACLAIM
          CALL      RDCODE1
          BRANCH    OVRCD,ANEP9999
.
          MATCH     "B",TCINDC22
          IF        @EQUAL
            COMPARE   "26",ADJTYVAL
            GOTO      ANEP8000 IF EQUAL
          ENDIF
.
          MATCH     "C",TCINDC22
          IF        @EQUAL
            COMPARE   "27",ADJTYVAL
            GOTO      ANEP8000 IF EQUAL
          ENDIF
.
          MATCH     SP70,TCINDC22
          GOTO      ANEP2000 IF EQUAL
          MATCH     "A",TCINDC22
          GOTO      ANEP9999 IF NOT EQUAL
.
ANEP2000  COMPARE   "25",ADJTYVAL
          GOTO      ANEP9999 IF NOT EQUAL
.
ANEP8000  MOVE      PMAJADJV,NEPVALUE  * NEP value Adjust.
          MOVE      ONE,ANEPFLAG
ANEP9999  RETURN
+
.*******************************************************************************
.         Calculate ABF amount
.*******************************************************************************
CABF0000  
.
. {PW x (1 + ARes + AInd + ART + ADia) x (1 + ATreat) - 
.  [PW x APPS + LOS x AAcc]} x NEP 
.
          MOVE      ONE,F106A
          ADD       ARESAMNT,F106A           * A_Res
          ADD       AINDAMNT,F106A           * A_Ind
          ADD       ARTAMNTS,F106A           * A_RT
          ADD       ADIAAMNT,F106A           * A_Dia
.
          MOVE      ZERO,F106B
          MOVE      ONE,F106B
          ADD       ATAAMNTS,F106B           * x A_ATA/A_Treat
.
          MOVE      LOSNDAYS,F106D           * LOS
          MULT      AACCAMNT,F106D           * x A_acc
          MOVE      PWAMOUNT,F106C           * PW
          MULT      APPSAMNT,F106C           * x APPS
          ADD       F106D,F106C              * + (LOS x A_acc)
.
          MOVE      PWAMOUNT,ABFCHRGE        * PW
          MULT      F106A,ABFCHRGE           * 1 + AInd + ARes + ART + ADia
          MULT      F106B,ABFCHRGE           * PW x ATreat
          SUB       F106C,ABFCHRGE           * PW x APPS + LOS x AAcc
.
          IF        ABFCHRGE<0
            MOVE      ZERO,ABFCHRGE         * No negative NWAU
          ENDIF
          MOVE      ABFCHRGE,NWAUAMNT       * save NWAU amount with 6 decimal
.
          MOVE      SP70,KEY19
          MOVE      NWAUAMNT,KEY19          * NWAU amount in 6 decimal places
          SQUEEZE   KEY19
          PACK      ADJMDESC,KEY19,SP70,SP20
.
          MOVE      ABFCHRGE,FORM104        * NWAU amount
          MOVE      "053",KEY3
          CALL      WABF0000                * Write to ABF Patient details
          PACK      ADJMDESC,SP70,SP70
.
CABF9999  RETURN
+
.*******************************************************************************
.         Write to ABF Patient details
.*******************************************************************************
WABF0000  BRANCH    PCAREFLG,WABF2000        * Palliative phase of care
.
          MOVE      AADMNO,PMABADMN          * Admission number
          MOVE      SP70,PMABINVN            * Blank Inv.no for tobe invoiced
          MOVE      KEY3,PMABADJT            * Type of adjustment
.
          PACK      KEY19,PMABADMN,PMABINVN,PMABADJT,SP70
          CALL      RDPMABF1
          BRANCH    OVRCD,WABF1000
.
          MOVE      FORM104,PMABADJV       * Value of adjustment
          MATCH     SP70,ADJMDESC
          IF        !@EQUAL
            PACK      PMABCDES,ADJMDESC,SP70    * Description of adjustment
          ENDIF
.
          PACK      PMABUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMABUDAT
          CLOCK     TIME,PMABUTIM
          MOVE      WBSEUID,PMABUUID
          CALL      UPPMABF1
.
          GOTO      WABF9999
.
WABF1000  CALL      CLPMSABF                 * Clear fields
          MOVE      "4",PMABABFT             * Type of ABF Details
          PACK      PMABADRG,PDRGCODE,SP70   * AR-DRG
.
          UNPACK    KEY19,PMABADMN,PMABINVN,PMABADJT
          MOVE      FORM104,PMABADJV       * Value of adjustment
          MATCH     SP70,ADJMDESC
          IF        !@EQUAL
            PACK      PMABCDES,ADJMDESC,SP70    * Description of adjustment
          ENDIF
.
          PACK      PMABCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMABCDAT
          CLOCK     TIME,PMABCTIM
          MOVE      WBSEUID,PMABCUID
.
          CALL      RAPMABF1
          IF        OVRCD=1
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            CALL      WRPMABF1
            TRAPCLR   IO
          ENDIF
          GOTO      WABF9999
.
.         write ABF details to patabfaf file for Palliative phase of care
.
WABF2000  MOVE      AADMNO,PTABADMN          * Admission number
          MOVE      PRDSDATE,PTABENCT        * Start date
          MOVE      SP70,PTABINVN            * Blank Inv.no for tobe invoiced
          MOVE      KEY3,PTABADJT            * Type of adjustment
.
          PACK      KEY27,PTABADMN,PTABENCT,PTABINVN,PTABADJT,SP70
          CALL      RDPTABF1
          BRANCH    OVRCD,WABF3000
.
          MOVE      FORM104,PTABADJV       * Value of adjustment
          MATCH     SP70,ADJMDESC
          IF        !@EQUAL
            PACK      PTABCDES,ADJMDESC,SP70    * Description of adjustment
          ENDIF
.
          PACK      PTABUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTABUDAT
          CLOCK     TIME,PTABUTIM
          MOVE      WBSEUID,PTABUUID
          CALL      UPPTABF1
.
          GOTO      WABF9999
.
WABF3000  CALL      CLPATABF                 * Clear fields
          MOVE      "7",PTABABFT             * Type of ABF Details
.
          UNPACK    KEY27,PTABADMN,PTABENCT,PTABINVN,PTABADJT
          MOVE      FORM104,PTABADJV       * Value of adjustment
          MATCH     SP70,ADJMDESC
          IF        !@EQUAL
            PACK      PTABCDES,ADJMDESC,SP70    * Description of adjustment
          ENDIF
.
          PACK      PTABCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTABCDAT
          CLOCK     TIME,PTABCTIM
          MOVE      WBSEUID,PTABUUID
.
          CALL      RAPTABF1
          IF        OVRCD=1
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            CALL      WRPTABF1
            TRAPCLR   IO
          ENDIF
.
WABF9999  RETURN
+
.******************************************************************************
.        Check for Claim Code/Care class Multiple NEP Values
.******************************************************************************
MNEP0000  MATCH     SP70,KEY2
          IF        !@EQUAL
            MATCH     SP70,D3
            GOTO      MNEP9999 IF EQUAL
            PACK      KEY5,KEY2,D3
            CALL      RDCODE1
            BRANCH    OVRCD,MNEP9999
          ENDIF
.
          MOVE      ZERO,OVRCD               * assume file exists
          TRAP      OVERCOND IF IO           * trap for error openning file
          OPEN      PMSNEPA1,"pmsnepaf"      * open NEP file
          TRAPCLR   IO
          BRANCH    OVRCD,MNEP9999           * open failed
.
          MOVE      " 3",DIM2                * Inpatients
          PACK      KEY58,CONTRCID,DIM2,KEY2,Z70
          CALL      RSPMNEP1
MNEP1000  CALL      RPPMNEP1
          BRANCH    OVRCD,MNEP9999
.
          MATCH     PMNECNTR,CONTRCID        * Same contract id ?
          GOTO      MNEP9999 IF NOT EQUAL
.
          MATCH     DIM2,PMNEVTYP            * Check type of visit
          GOTO      MNEP9999 IF NOT EQUAL
.
          MATCH     KEY2,PMNECODE            * Check Code set
          GOTO      MNEP9999 IF NOT EQUAL
.
          MATCH     "1",PMNEACTV
          GOTO      MNEP1000 IF EQUAL        * Inactive
.
          CALL      CIND0000                 * Check indicators
          BRANCH    EXIT,MNEP1000            * Check next record
.
MNEP9999  RETURN
+
.******************************************************************************
.        Check for Indicator number and value
.        Exit = 1 (Not valid)
.******************************************************************************
CIND0000  MATCH     SP70,PMNEINDC          * check for blank Indicator number
          GOTO      CIND2000 IF EQUAL
.
          STRIP     PMNEINDC
          MOVE      ZERO,F2
          MOVE      PMNEINDC,F2
.
.         Check indicator number and value of patient care class
.
          MOVE      SP70,KEY1
          LOAD      KEY1,F2,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5:
                            TCINDC6,TCINDC7,TCINDC8,TCINDC9,TCINDC10:
                            TCINDC11,TCINDC12,TCINDC13,TCINDC14,TCINDC15:
                            TCINDC16,TCINDC17,TCINDC18,TCINDC19,TCINDC20:
                            TCINDC21,TCINDC22,TCINDC23,TCINDC24,TCINDC25:
                            TCINDC26,TCINDC27,TCINDC28,TCINDC29,TCINDC30:
                            TCINDC31,TCINDC32,TCINDC33,TCINDC34,TCINDC35:
                            TCINDC36,TCINDC37,TCINDC38,TCINDC39,TCINDC40:
                            TCINDC41
.
          STRIP     PMNEINDV
          PACK      D1,PMNEINDV,SP70
          MATCH     KEY1,D1
          GOTO      CIND9000 IF NOT EQUAL
.
CIND2000  MOVE      PMNENEPP,NEPVALUE  * NEP value Adjust.
          MOVE      ZERO,EXIT
          GOTO      CIND9999
.
CIND9000  MOVE      ONE,EXIT
CIND9999  RETURN
+
.*******************************************************************************
.         Check for Private Patient
.*******************************************************************************
CPRV0000  MOVE      PRDSDATE,FROMDATE
          MOVE      SP70,SAVATYPE
          MOVE      ZERO,COUNTER
          MOVE      SP70,STMOVE
.
          PACK      KEY30,AADMNO,SP70
          CALL      RDSTRAN2
CPRV1000  CALL      RDKTRAN2
          BRANCH    OVRCD,CPRV2000
.
          MATCH     TADMN,AADMNO             * same admission ?
          GOTO      CPRV2000 IF NOT EQUAL    * No.
.
          MOVE      TMOVE,STMOVE
          MATCH     "A",TMOVE
          GOTO      CPRV1200 IF EQUAL
.
          MATCH     "R",TMOVE
          GOTO      CPRV1400 IF NOT EQUAL        * Return from leave
.
CPRV1200  MOVE      TDATE,FROMDATE
          MOVE      TATYPE,SAVATYPE
          GOTO      CPRV1000
.
CPRV1400  MATCH     "L",TMOVE
          GOTO      CPRV3000 IF EQUAL             * leave record
          MATCH     "D",TMOVE
          GOTO      CPRV3000 IF EQUAL             * discharge record
.
.         must be Change record, check if Adm.type has changed
.
          BRANCH    ACDAYCS,CPRV4000              * daycase use the last tatype
.
          MATCH     TATYPE,SAVATYPE
          GOTO      CPRV1000 IF EQUAL
          GOTO      CPRV3000
.
.         Check previous movement
.
CPRV2000  MOVE      ONE,OVRCD
          MATCH     "D",STMOVE
          GOTO      CPRV5000 IF EQUAL
.
          MATCH     "L",STMOVE
          GOTO      CPRV5000 IF EQUAL             * Currently on leave
.
          PACK      TDATE,CCC,CYY,CMM,CDD
          REP       " 0",TDATE
.
CPRV3000  CALL      ABDY0000                      * store no of days
          BRANCH    OVRCD,CPRV5000
.
          MATCH     "D",STMOVE
          GOTO      CPRV5000 IF EQUAL
.
CPRV4000  MOVE      TATYPE,SAVATYPE
          MOVE      TDATE,FROMDATE
.
          GOTO      CPRV1000
.
.         Loop through the array for No of bed days for each Adm.type
.
CPRV5000  COMPARE   ZERO,COUNTER
          GOTO      CPRV9999 IF EQUAL
.
          MOVE      ZERO,F2
CPRV6000  ADD       ONE,F2
          COMPARE   F2,COUNTER
          GOTO      CPRV9999 IF LESS
.
          MOVE      ZERO,FORM3
          MOVE      ATYPCODE[F2],KEY3   * adm.type
          MOVE      ATYPBDAY[F2],FORM3  * Bed days
.
          IF        ACDAYCS=1
            MOVE      ZERO,FORM3             * day case
          ENDIF
.
          PACK      KEY9,ACLAIM,KEY3,FORM3
          CALL      RDPTACC1                 * read the account status file
          COMPARE   ZERO,OVRCD
          GOTO      CPRV6400 IF EQUAL
.
CPRV6200  CALL      RKPTACC1                 * get the next record on the
          BRANCH    OVRCD,CPRV6000           * account status file
.
          MATCH     ACLAIM,PTASCLAM          * match claim codes
          GOTO      CPRV6000 IF NOT EQUAL
          MATCH     KEY3,PTASATYP            * match admission types
          GOTO      CPRV6000 IF NOT EQUAL
.
CPRV6400  CMATCH    "P",PTASACCS             * Check 1st char =P for Private
          GOTO      CPRV6000 IF NOT EQUAL
.
          MOVE      ONE,PRIVFLAG             * Flag for Private Patient
.
CPRV9999  RETURN
+
.******************************************************************************
.         Add bed days to the array
.******************************************************************************
ABDY0000  MOVE      ZERO,NBRDAYS
          MOVE      TDATE,TODATE
          DAYSEP    FROMDATE,TODATE,NBRDAYS
.
          MATCH     FROMDATE,TODATE
          IF        @EQUAL
            COMPARE   ONE,ACDAYCS
            GOTO      ABDY9999 IF NOT EQUAL
            MOVE      ZERO,NBRDAYS           * daycase
          ENDIF
.
          MOVE      ZERO,F2
ABDY1000  COMPARE   COUNTER,F2
          GOTO      ABDY2000 IF NOT LESS
.
          ADD       ONE,F2
.
          MATCH     SAVATYPE,ATYPCODE[F2]
          GOTO      ABDY1000 IF NOT EQUAL
.
          ADD       NBRDAYS,ATYPBDAY[F2]
          GOTO      ABDY9999
.
ABDY2000  ADD       ONE,F2
          MOVE      SAVATYPE,ATYPCODE[F2]   * adm.type
          MOVE      NBRDAYS,ATYPBDAY[F2]  * Bed days
          ADD       ONE,COUNTER
ABDY9999  RETURN
+
.******************************************************************************
          INC       CLPMSABF
          INC       CLPATABF
          INC       GETDRG
.
          INC       IBAPOSIO/INC
          INC       BOKRX1IO/INC
          INC       PATDADIO/INC
          INC       PMSVMTIO/INC
          INC       PATECOIO/INC
          INC       PATRDEIO/INC
          INC       PATACCIO/INC
.
          INC       PMSNEPIO/INC
          INC       PMSADJIO/INC
          INC       PMSPWAIO/INC
          INC       PMSABFIO/INC
          INC       PATABFIO/INC
          INC       PMSRDIIO/INC
          INC       PMSSAWIO/INC
          INC       IBAOVRIO/INC
.
NDSA9999  ENDPROC
+
