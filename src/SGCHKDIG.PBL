.*****************************************************************************
.*                           SGCHKDIG.PBL                                    *
.* Date      :   16/11/2006                                                  *
.* Author    :   Steve Armstrong                                             *
.* Function  :   Include used to calculate Singapore HRN check digit         *
.* Requires  :   PURNO  - patient U/R number                                 *
.* Returns   :   CHKDIGNO - full HRN including check digit                   *
.*                                                                           *
.* Mods      :                                                               *
.*        V9.07.01  Steve Armstrong  CAR 126039                              *
.*                  Mods to handle X numbers                                 *
.*****************************************************************************
.
SGCHKDIG  MOVE      SP9,CHKDIGNO                 * initialise full HRN field
.
.         Load each character  from the U/R (2-7) into a working variable
.
          UNPACK    PURNO,ANS,ATOGCHAR[1],ATOGCHAR[2],ATOGCHAR[3],ATOGCHAR[4]:
                              ATOGCHAR[5],ATOGCHAR[6],ATOGCHAR[7]
.
.         Check if the HRN type is "X" as no check digit applies and hence
.         there is no need for any validations
.
          MATCH     ANSX,ANS                     * "X" ?
          IF        @EQUAL
            MOVE      SP1,ANS
            GOTO      CHKDIG40                   * yes - finish
          ENDIF
.
.         Make sure that each character (2-7) is a numeric
.
          MOVE      ZERO,CHKDGCNT                * initialise counter
          WHILE     CHKDGCNT < 7
            ADD       ONE,CHKDGCNT               * increment counter
            TYPE      ATOGCHAR[CHKDGCNT]         * numeric ?
            GOTO      CHKDIG99 IF NOT EQUAL      * no - finish
            MOVE      ATOGCHAR[CHKDGCNT],ATOGFRM1[CHKDGCNT]  * load numeric var.
          DO
.
.         Multiply each numeric by the amount specified in the algorithm
.
          ASSIGN    (ATOGFRM1[1]*2),ATOGFRM2[1]
          ASSIGN    (ATOGFRM1[2]*7),ATOGFRM2[2]
          ASSIGN    (ATOGFRM1[3]*6),ATOGFRM2[3]
          ASSIGN    (ATOGFRM1[4]*5),ATOGFRM2[4]
          ASSIGN    (ATOGFRM1[5]*4),ATOGFRM2[5]
          ASSIGN    (ATOGFRM1[6]*3),ATOGFRM2[6]
          ASSIGN    (ATOGFRM1[7]*2),ATOGFRM2[7]
.
.         Add all the results to give a final total
.
          ASSIGN    (ATOGFRM2[1]+ATOGFRM2[2]+ATOGFRM2[3]+ATOGFRM2[4]+ATOGFRM2[5]+ATOGFRM2[6]+ATOGFRM2[7]),CHKDGTOT
.
.         If the first character is "T" or "G", then add 4 to the total
.
          MATCH     ANST,ANS
          GOTO      CHKDIG10 IF EQUAL
.         
          MATCH     ANSG,ANS
          GOTO      CHKDIG10 IF EQUAL
          GOTO      CHKDIG20
.
CHKDIG10  ADD       FOUR,CHKDGTOT
.
.         Divide the total by 11 and get the remainder
.
CHKDIG20  ASSIGN    (CHKDGTOT%11),CHKDGREM
.
.         Use the remainder to position on the corresponding character in the
.         check digit string ("S" and "T" numbers use the same string, as do
.         "F" and "G" numbers).  This character then represents the check digit.
.
          MATCH     ANSF,ANS
          GOTO      CHKDIG30 IF EQUAL
.
          MATCH     ANSG,ANS
          GOTO      CHKDIG30 IF EQUAL
.
          BUMP      CHKDGST1,CHKDGREM
          MOVE      CHKDGST1,ANS
          GOTO      CHKDIG40
.
CHKDIG30  BUMP      CHKDGST2,CHKDGREM
          MOVE      CHKDGST2,ANS
.
CHKDIG40  PACK      CHKDIGNO,PURNO,ANS
          RESET     CHKDGST1
          RESET     CHKDGST2
.
CHKDIG99  RETURN
+
