.
. V5.01.001 21/04/1994  bez Created
.******************************************************************************
. STDDETIO.PBL
.******************************************************************************
.
.        STDIO002.PBL
.
.        Defines standard includes used in all programs. 
.        Same as STDIO002 without IBAIOPRN
. Mods:
.         C1.00.01  05/02/1996  LOFTY                                    
.                   added conmtcmi         
.         V5.02.01  13/02/1995  Greg Horvat      SRF 134920 134922 134932
.                   Recompiled for IBAPRINT
.
.------------------------------------------------------------------------------
.
          INC      IBADATE
          INC      IBATIME
          INC      JULCON
          INC      DMYCON
          INC      IBACOMN
          INC      IBAOVRIO/INC
          INC      IBAPORIO/INC
          INC      IBACODIO/INC
          INC      IBAPRINT
          INC      CONMTCMI
.
.------------------------------------------------------------------------------
.         SETX0000 - Set up COMMON variables
.         Adapted from ANSWER.PBL & IBAMENU.PBL
.------------------------------------------------------------------------------
.
SETX0000  MOVE      SP1,SECACC1
          MOVE      SP1,SECACC2
          MOVE      SP1,SECACC3
          MOVE      SP1,SECACC4
          MOVE      SP8,PGM
          MOVE      SP5,FNM
          MOVE      SP6,IDDD
          MOVE      SP20,ERROR
          MOVE      SP3,UID
          MOVE      SP4,PASSCODE
          MOVE      ZERO,ERRA
          MOVE      ZERO,PSEC
          MOVE      ZERO,CPBS
          MOVE      ZERO,PERIOD
          MOVE      ZERO,SECLEV
          MOVE      SP8,SPLFILE
          MOVE      SP2,PASSCD
          MOVE      ZERO,CPPAPER
          MOVE      ZERO,CPRTFLG
          MOVE      ZERO,CPROGLEV
          MOVE      SP2,CPRTGRP
          MOVE      SP3,CDEPCDE
          MOVE      ZERO,CHOSINDX
          MOVE      SP3,CNOCOPY  
          MOVE      SP3,CCODDIMB
          MOVE      SP1,EXITCHAR
          MOVE      SP3,CCDEPT
          MOVE      SP8,CCMENU
.
.         Open control file
.
          TRAP      NOFILE IF IO
          OPEN      CONTROLF,"controlf"
          TRAPCLR   IO
.
.         Read relevant variables from control file
.
          READ      CONTROLF,ZERO;*8,CCC,EXITCHAR
          READ      CONTROLF,TWO;*4,CONAME,*65,CCOUNTRY:
                                 *41,PORTADD,*249,SPOOL
          READ      CONTROLF,TEN;*244,CHOSINDX
.
.         Set up NSM department and port number
.
          CLOCK     DEPT,CCDEPT
          CLOCK     PORT,PORT
          MOVE      PORT,PORTNO
          ADD       PORTADD,PORTNO
.
.         Set up miscellaneous variables
.
          REP       " \",EXITCHAR
          MOVE      CONAME,CNAME
          MOVE      CCOUNTRY,ERRA
.
.         Write to port file
.
          OPEN      IBAPORT1,"ibaportf"
          MOVE      PRGID,IBAPPROG           * Current program being accessed
          CALL      WRPORT1
.
          CALL      IBACLOCK
.
          MOVE      SP8,SPLFILE
          MOVE      SPOOL,CPRTFLG
.
          CLOSE     CONTROLF
.
SETX9999  RETURN
.
. ------- Control file disappeared -------
.
NOFILE    KEYIN     *P1:24,*B,*EL,"FATAL ERROR : System Control File Missing ":
                    " Hit ",*V2LON,"<RETURN>",*HOFF," to continue ",*EOFF,ANS;
          SHUTDOWN  ""
.
.------------------------------------------------------------------------------
.         ENDX0000 - Clean up for printer system.
.         This code is adapted from IBAXXX99 programs.
.------------------------------------------------------------------------------
.
.         OPEN       IBACODE1,"ibacodef"
.         OPEN       IBAPRNT1,"ibaprntf"
.
ENDX0000  LOAD       CPRTFLG,CPRTFLG,ONE,TWO,ZERO
          CALL       SPLEMPTY
          BRANCH     OVRCD,DELSPL,NOPRNTS
.
          CALL       SELPRINT                     * Select printer
          CALL       CLSPRINT                     * Print & delete spool file
          GOTO       NOPRNTS
.
DELSPL    CALL       SPLDELET                     * Delete spool file
.
.         If spooling inside a program was selected, reset at this point
.         (ie. If CPRTFLG is 3, change it to zero)
.
NOPRNTS   LOAD      CPRTFLG,CPRTFLG,ONE,TWO,ZERO
          SPLCLOSE
          CALL      ULPORT1
          RETURN
.------------------------------------------------------------
. Function   : System Locking Routines
.              These Routines are to be used in places where the
.              pi instruction would have been used to protect
.              updates to ASCII files etc.
.              System Lock Codes will be stored on the system
.              codes table ibacodes.
.              For Control File (controlf) updates the System Lock Code
.              will be the Sector that is being updated.
.              Other System Lock Code will arbitary Alpha values.
.              Arbitary System Lock Codes will be allocated as required
.              by Analysts or Senior Programmers.
.
. Lock Codes : Code  Description    
.              BNK - PATBNKFD
.              FIN - PATFINFD
.              PFR - PRIFINFD
.              STA - PATSTAFD
.              AST - AAESTAFD
.              AUX - AAEAUXFD
.              OUX - OUTAUXFD
.              PDS - PCPDSCFD
.              PUC - PATAUCFD
.              PUM - PATAUMFD
.              PUR - PATRCAUD
.              PUF - PATAUFFD
.              PUL - PATAULFD
.              PUJ - PATAUJFD
.              PUD - PATAUDFD
.              PUB - PATAUBFD
.              PUI - PATAUIFD
.              PUP - PATAUPFD
.              PU1 - PATAU1FD
.              PZ1 - PATAZ1FD
.              PUE - PATAUEFD
.              PUT - PATAUTFD
.              PFG - PATFIGFD
.              PRF - PATRFNFD
.              PRG - PRIFIGFD
.              PRJ - PRIAUJFD
.              PUA - PATAUAFD
.
. Routine    : GETSLK00 - Get System Lock
.            : RELSLK00 - Release System Lock
.
. Input      : PRXCODE - System Lock Code
.
.------------------------------------------------------------
. Get Record Lock
.------------------------------------------------------------
GETSLK00   OPEN      IBACODE1,"ibacodef"
           MOVE      "~~",KEY2
           MOVE      PRXCODE,KEY3
           PACK      KEY5,KEY2,KEY3
           CALL      RLIBCOD1
           BRANCH    OVRCD,GETSLK90,GETSLK91
           GOTO      GETSLK99
.
GETSLK90   CALL      WRTLCK00     * Write Lock Record to ibacodes
           GOTO      GETSLK00
.
GETSLK91   DISPLAY   *W1,"Waiting on System Lock ",KEY5
           GOTO      GETSLK00
.
GETSLK99   RETURN
.------------------------------------------------------------
. Release Record Lock
.------------------------------------------------------------
RELSLK00   CALL      UUIBCOD1
           RETURN
.------------------------------------------------------------
. Write Lock Record to patcodes
.------------------------------------------------------------
WRTLCK00   MOVE      "~~",PRXCATA
           MOVE      KEY3,PRXCODE
           CLEAR     PRXDESC
           APPEND    "System Lock ",PRXDESC
           APPEND    PRXCODE,PRXDESC
           RESET     PRXDESC
           CALL      WRIBCOD1
           RETURN
.
