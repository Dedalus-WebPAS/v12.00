. *----------------------------------------------------------------------
. * Include Name  :   ALLCTMTM.PBL
. *
. * Function      :   CGI variables for ALLCTMFD - allctmaf.dat
. *
. * Includes Needed :
. *
. *----------------------------------------------------------------------
.  Modifications Done :
.
.  V9.04.01 18/11/2004  Ebon Clements CAR 54855
.           Created include
.------------------------------------------------------------------------
.   Clear parameters for case team member details file
.------------------------------------------------------------------------
.
CPALCT00  MOVE      Z70,ALCTM001
          MOVE      Z70,ALCTM002
          MOVE      Z70,ALCTM003
          MOVE      Z70,ALCTM004
.
CPALCT99  RETURN
.------------------------------------------------------------
. Add case team member table
.------------------------------------------------------------
ALCT0000  BRANCH    FLDITMNO,ALCT0010,ALCT0020,ALCT0030,ALCT0040,ALCT0050:
                             ALCT0060,ALCT0070,ALCT0080,ALCT0090,ALCT0100
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      ALCT9999
.
ALCT0010  WRITE     HTMLFILE;ALCTTEAM;
          GOTO      ALCT9999
.
ALCT0020  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,ALCT0021
.
          PACK      KEY10,ALCTHCPC,SP70
          CALL      RDPMHCP1
          BRANCH    OVRCD,ALCT9999
.
          MOVE      PMHCTITL,FMTTITLE
          MOVE      PMHCGNAM,FMTGNAME
          MOVE      PMHCSNAM,FMTSNAME
          CALL      DOCNM000
          WRITE     HTMLFILE;DOCFNAME;
          GOTO      ALCT9999
.
ALCT0021  WRITE     HTMLFILE;ALCTHCPC;
          GOTO      ALCT9999
.
ALCT0030  MATCH     ALCTSDAT,SP70
          GOTO      ALCT9999 IF EQUAL
.
          UNPACK    ALCTSDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      ALCT9999
.
ALCT0040  MATCH     ALCTEDAT,SP70
          GOTO      ALCT9999 IF EQUAL
.
          UNPACK    ALCTEDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      ALCT9999
.
ALCT0050  MATCH     ALCTCDAT,SP70
          GOTO      ALCT9999 IF EQUAL
.
          UNPACK    ALCTCDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      ALCT9999
.
ALCT0060  WRITE     HTMLFILE;ALCTCTIM;
          GOTO      ALCT9999
.
ALCT0070  MATCH     SP70,ALCTCUID
          GOTO      ALCT9999 IF EQUAL
.
          PACK      KEY10,ALCTCUID,SP70
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      ALCTCUID,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM;
          GOTO      ALCT9999
.
ALCT0080  MATCH     ALCTUDAT,SP70
          GOTO      ALCT9999 IF EQUAL
.
          UNPACK    ALCTUDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      ALCT9999
.
ALCT0090  WRITE     HTMLFILE;ALCTUTIM;
          GOTO      ALCT9999
.
ALCT0100  MATCH     SP70,ALCTUUID
          GOTO      ALCT9999 IF EQUAL
.
          PACK      KEY10,ALCTUUID,SP70
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      ALCTUUID,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM;
          GOTO      ALCT9999
.
ALCT9999  RETURN
.
.------------------------------------------------------------
.   Set Parameters for case team member details
.------------------------------------------------------------
SPALT000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPALT001,SPALT002,SPALT003,SPALT004
.
          MOVE      ONE,EXIT
          GOTO      SPALT999
.
SPALT001  SQUEEZE   QRYDATA
          MOVE      QRYDATA,ALCTM001
          PACK      ALCTM001,ALCTM001,SP70
          GOTO      SPALT999
.
SPALT002  MOVE      QRYDATA,ALCTM002
          GOTO      SPALT999
.
SPALT003  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      ALCTM003,CCENT,CYEAR,CMON,CDAY
          GOTO      SPALT999
.
SPALT004  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      ALCTM004,CCENT,CYEAR,CMON,CDAY
          GOTO      SPALT999
.
SPALT999  RETURN
.
.------------------------------------------------------------
.  Update case team member details
.------------------------------------------------------------
MVALCT00  MATCH     ALCTM001,Z70
          IF        !@EQUAL
            MOVE      ALCTM001,ALCTTEAM
          ENDIF
.
          MATCH     ALCTM002,Z70
          IF        !@EQUAL
            MOVE      ALCTM002,ALCTHCPC
          ENDIF
.
          MATCH     ALCTM003,Z70
          IF        !@EQUAL
            MOVE      ALCTM003,ALCTSDAT
          ENDIF
.
          MATCH     ALCTM004,Z70
          IF        !@EQUAL
            MOVE      ALCTM004,ALCTEDAT
          ENDIF
.
MVALCT99  RETURN
.
.------------------------------------------------------------
.  Clear case team member details
.------------------------------------------------------------
CLALCT00  MOVE      SP70,ALCTTEAM
          MOVE      SP70,ALCTHCPC
          MOVE      SP70,ALCTSDAT
          MOVE      SP70,ALCTEDAT
          MOVE      SP70,ALCTCDAT
          MOVE      SP70,ALCTCTIM
          MOVE      SP70,ALCTCUID
          MOVE      SP70,ALCTUDAT
          MOVE      SP70,ALCTUTIM
          MOVE      SP70,ALCTUUID
          MOVE      SP70,ALCTSPAR
.
CLALCT99  RETURN 
