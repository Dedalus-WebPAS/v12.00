.******************************************************************************
.* System         : Patient Management                                        *
.* Include        : PATWIS14.PBL                                              *
.* Description    : Calculate WIES14 value for an individual admission        *
.******************************************************************************
.* Date           : 17/07/2007                                                *
.* Author         : Steve Armstrong                                           *
.*                                                                            *
.* Vars Required  : VARWIS14 - Variables required for WIES14                  *
.*                                                                            *
.* Files Required Open :                                                      *
.*        PATASFFD  PATASFA1 - Admission special funding arrangement file     *
.*        PATCODFD  PATCODE1 - Codes file                                     *
.*        PATECDFD  PATECDA2 - Episode disease coding file                    *
.*        PATECOFD  PATECOA2 - Episode operation coding file                  *
.*        PAT10OFD  PATI10O1 - ICD10 operations file                          *
.*        PATICUFD  PATICUA1 - ICU file                                       *
.*        PATTRNFD  PATTRAN2 - Transfer file                                  *
.*        PATW14FD  PATW14A1 - Victorian cost weights (for WIES14) file       *
.*                                                                            *
.* Parameters Req : Successful read on admission file                         *
.*                  Successful read on discharge file                         *
.*                  HIHLOS   - for Hopsipta in Home LOS (from RHIHDAYS)       *
.*                  NBRDAYS  - for LOS calculation (from NHOSPDAY)            *
.*                  PNSWRACE - User defined field for race                    *
.*                  WIS14DRG - DRG to calculate WIES14 for                    *
.*                                                                            *
.* Returns        : TOTWIS14 - Total WIES14 value                             *
.*                  WIS14DRG - DRG used to calculate WIES14                   *
.*                  WIS14LOS - LOS used to calculate WIES14                   *
.*                  WIS14WGT - Weight used to calculate WIES14                *
.*                  WIS14DSC - Description of DRG weight used                 *
.*                                                                            *
.* Modifications  :                                                           *
.*                                                                            *
.*        V12.00.01 30/05/2025  Don Nguyen     TSK 0955096                    *
.*                  Alphanumeric visit number changes                         *
.*        V10.02.01 28/03/2011  Mike Laming   CAR 240107                      *
.*                  Mods to PATECDaf & PATECOaf variables & Keys - file change*
.*        V9.09.00  17/07/2007  Steve Armstrong  CAR 145161                   *
.*                  Created for 2007/2008 WIES14 changes                      *
.******************************************************************************
+
PATWIS14  CALL      IN14W000                * Initialise variables
          BRANCH    EXIT,WIS14950
.
          CALL      C14TY000                * Determine and Check Care Type
          BRANCH    EXIT,WIS14950
.
          CALL      C14TR000                * Check for Contracted Patient
          BRANCH    EXIT,WIS14950
.
          CALL      M14RG000                * Modify DRG if necessary
.
          CALL      C14RG000                * Check for problem DRG
          BRANCH    EXIT,WIS14950
.
          CALL      M14CH000                * Check for Mechanical Ventilation
.
          CALL      T14AL000                * Check for Thalessaemia
.
          CALL      A14AA000                * Check for AAA
.
          CALL      A14SD000                * Check for ASD
.
          CALL      C14OL000                * Check for Colonoscopy
.
          CALL      B14SW000                * Calculate Base WIES14
.
          CALL      C14AB000                * Check for aboriginality
.
. ---- Calculate WIES14 value ----
.
          MOVE      TOTBWIES,TOTWIS14       * base WIES
          ADD       MVCOPAY,TOTWIS14        * mechv copayment
          ADD       THCOPAY,TOTWIS14        * thal  copayment
          ADD       AAACOPAY,TOTWIS14       * AAA   copayment
          ADD       ASDCOPAY,TOTWIS14       * ASD   copayment
          ADD       COLCOPAY,TOTWIS14       * colon copayment
          ADD       ATSIWIES,TOTWIS14       * ATSI  copayment
.
          MOVE      ZERO,EXIT
          GOTO      WIS14999
.
WIS14950  MOVE      ONE,EXIT
WIS14999  RETURN
+
.******************************************************************************
.*                                  IN14W000              Called by: PATWIS14 *
.*                         Initialise WIES14 variables                        *
.******************************************************************************
IN14W000  MOVE      ZERO,TOTWIS14
          MOVE      ZERO,WIS14LOS
          MOVE      ZERO,WIS14WGT
          MOVE      SP20,WIS14DSC
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATW14A1,"patw14af"
          TRAPCLR   IO
          BRANCH    OVRCD,IN14W950
.
IN14W900  MOVE      ZERO,EXIT
          GOTO      IN14W999
.
IN14W950  MOVE      ONE,EXIT
IN14W999  RETURN
+
.******************************************************************************
.*                                  C14TY000              Called by: PATWIS14 *
.*                 Check for valid Care Type & Incomplete Coding              *
.******************************************************************************
C14TY000  PACK     KEY5,ANSC,ANSC,ACARE     * use admission care type
          CALL     RDCODE1
          BRANCH   OVRCD,C14TY900
.
          MATCH    "6",TCINDC4              * Check Program Funding Source
          GOTO     C14TY900 IF NOT EQUAL
.
          MATCH    "0   ",THCSCOD           * check care types
          GOTO     C14TY900 IF EQUAL
          MATCH    "1   ",THCSCOD
          GOTO     C14TY900 IF EQUAL
          MATCH    "2   ",THCSCOD
          GOTO     C14TY900 IF EQUAL
          MATCH    "6   ",THCSCOD
          GOTO     C14TY900 IF EQUAL
          MATCH    "7   ",THCSCOD
          GOTO     C14TY900 IF EQUAL
          MATCH    "8   ",THCSCOD
          GOTO     C14TY900 IF EQUAL
          MATCH    "9   ",THCSCOD
          GOTO     C14TY900 IF EQUAL
          MATCH    "5   ",THCSCOD
          GOTO     C14TY900 IF EQUAL
          MATCH    "3   ",THCSCOD
          GOTO     C14TY900 IF EQUAL
          MATCH    "E   ",THCSCOD
          GOTO     C14TY900 IF EQUAL
          MATCH    "F   ",THCSCOD
          GOTO     C14TY900 IF EQUAL
          MATCH    "J   ",THCSCOD
          GOTO     C14TY900 IF EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     C14TY999
.
C14TY900  MOVE     ONE,EXIT                 * Not valid
C14TY999  RETURN
+
.******************************************************************************
.*                                  C14TR000              Called by: PATWIS14 *
.*                        Check for Contracted Patient                        *
.******************************************************************************
C14TR000  PACK     KEY25,AADMNO,ANSA,SP30
          CALL     RSPTASF1
C14TR100  CALL     RKPTASF1                 * Read adm. special funding file
          BRANCH   OVRCD,C14TR800           * still process
.
          MATCH    AADMNO,PTAFADMN
          GOTO     C14TR800 IF NOT EQUAL    * still process
.
          MATCH    ANSA,PTAFTYPE            * special fund. arrang. type = adm
          GOTO     C14TR100 IF NOT EQUAL
.
          MATCH    ANSB,PTAFCROL            * Ignore if contract role = 'B'
          GOTO     C14TR900 IF EQUAL
.
C14TR800  MOVE     ZERO,EXIT                * valid
          GOTO     C14TR999
.
C14TR900  MOVE     ONE,EXIT                 * invalid
C14TR999  RETURN
+
.******************************************************************************
.*                                  M14RG000              Called by: PATWIS14 *
.*                      Modify DRG if necessary for WIES14                    *
.******************************************************************************
M14RG000  CALL      P14IT000                * Peritoneal Dialysis
          CALL      R14IO000                * Radiotherapy
          CALL      B14EM000                * Bone Marrow Transplants
          CALL      H14TE000                * Hysterscopy Sterilisation
          CALL      M14TO000                * Mastoid Procedure
          CALL      E14RA000                * Extra Corporal Life Support
          CALL      D14PS000                * Diag. Proc. Sex incompat *I-53126
M14RG999  RETURN
+
.******************************************************************************
.*                                  P14IT000              Called by: M14RG000 *
.*                      Modify DRG if Peritoneal Dialysis                     *
.******************************************************************************
P14IT000  PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
P14IT100  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,P14IT999
.
          MATCH     AADMNO,PTEDADMN
          GOTO      P14IT999 IF NOT EQUAL
.
          MATCH     CMDISP,PTEDTYPE         * want principal diagnosis
          GOTO      P14IT100 IF NOT EQUAL
.
          MATCH     "Z49.2",PTEDCODE
          GOTO      P14IT100 IF NOT EQUAL
.
          MOVE      "L61Y",WIS14DRG         * modified for WIES14
P14IT999  RETURN
+
.******************************************************************************
.*                                  R14IO000              Called by: M14RG000 *
.*                         Modify DRG if Radiotherapy                         *
.******************************************************************************
R14IO000  PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
R14IO100  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,R14IO999
.
          MATCH     AADMNO,PTEDADMN         * same admission?
          GOTO      R14IO999 IF NOT EQUAL
.
          MATCH     "Z51.0",PTEDCODE        * radiotherapy?
          GOTO      R14IO100 IF NOT EQUAL
.
          MOVE      "R64Z",WIS14DRG         * modified for WIES14
R14IO999  RETURN
+
.******************************************************************************
.*                                  B14EM000              Called by: M14RG000 *
.*                    Modify DRG if Bone Marrow Transplant                    *
.******************************************************************************
B14EM000  MATCH     "A04Z",WIS14DRG
          GOTO      B14EM999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
B14EM100  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,B14EM900
.
          MATCH     AADMNO,PTEOADMN
          GOTO      B14EM900 IF NOT EQUAL
.
          MATCH     "13706-00",PTEOCODE
          IF        !@EQUAL
            MATCH     "13706-06",PTEOCODE
            IF        !@EQUAL
              MATCH     "13706-09",PTEOCODE
              IF        !@EQUAL
                MATCH     "13706-10",PTEOCODE
                GOTO      B14EM100 IF NOT EQUAL
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      "A04A",WIS14DRG         * new DRG = A04A
          GOTO      B14EM999
.
B14EM900  MOVE      "A04B",WIS14DRG         * new DRG = A04B
.
B14EM999  RETURN
+
.******************************************************************************
.*                                  H14TE000              Called by: M14RG000 *
.*                    Modify DRG if Hysterscopy Sterilisation                 *
.******************************************************************************
H14TE000  MATCH     "N09Z",WIS14DRG
          IF        !@EQUAL
            MATCH     "N10Z",WIS14DRG
            IF        !@EQUAL
              MATCH     "N11B",WIS14DRG
              IF        !@EQUAL
                MATCH     "O40Z",WIS14DRG
                IF        !@EQUAL
                  MATCH     "N08Z",WIS14DRG
                  IF        !@EQUAL
                    MATCH     "O05Z",WIS14DRG
                    GOTO      H14TE999 IF NOT EQUAL
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
H14TE100  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,H14TE999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      H14TE999 IF NOT EQUAL
.
          MATCH     "35688-01",PTEOCODE
          IF        !@EQUAL        
            GOTO      H14TE100 IF NOT EQUAL
          ENDIF
.
          MOVE      "N11C",WIS14DRG         * new DRG = N11C
          GOTO      H14TE999
.
H14TE999  RETURN
+
.******************************************************************************
.*                                  M14TO000              Called by: M14RG000 *
.*                    Modify DRG if Mastoid procedure                         *
.******************************************************************************
M14TO000  MATCH     "D06Z",WIS14DRG
          GOTO      M14TO999 IF NOT EQUAL
.
          MOVE      "D06B",WIS14DRG         * Other Sinus/mid/ear Proc.
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
M14TO100  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,M14TO999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      M14TO999 IF NOT EQUAL
.
          MATCH     "41545-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS14DRG       * Mastoid Procedure.
            GOTO      M14TO999
          ENDIF
.
          MATCH     "41551-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS14DRG       * Mastoid Procedure.
            GOTO       M14TO999
          ENDIF
.
          MATCH     "41554-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS14DRG       * Mastoid Procedure.
            GOTO      M14TO999
          ENDIF
.
          MATCH     "41557-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS14DRG       * Mastoid Procedure.
            GOTO      M14TO999
          ENDIF
.
          MATCH     "41557-03",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS14DRG       * Mastoid Procedure.
            GOTO      M14TO999
          ENDIF
.
          MATCH     "41560-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS14DRG       * Mastoid Procedure.
            GOTO      M14TO999
          ENDIF
.
          MATCH     "41563-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS14DRG       * Mastoid Procedure.
            GOTO      M14TO999
          ENDIF
.
          MATCH     "41564-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS14DRG       * Mastoid Procedure.
            GOTO      M14TO999
          ENDIF
.
          MATCH     "41566-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS14DRG       * Mastoid Procedure.
            GOTO      M14TO999
          ENDIF
.
          MATCH     "41566-01",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS14DRG       * Mastoid Procedure.
            GOTO      M14TO999
          ENDIF
.
          MATCH     "41566-02",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS14DRG       * Mastoid Procedure.
            GOTO      M14TO999
          ENDIF
.
          GOTO      M14TO100
.
M14TO999  RETURN
+
.******************************************************************************
.*                                  E14RA000              Called by: M14RG000 *
.*                    Modify DRG if Extra Corporeal Life Support              *
.******************************************************************************
E14RA000  MATCH     "A01Z",WIS14DRG
          GOTO      E14RA999 IF EQUAL
.
          MATCH     "A03Z",WIS14DRG
          GOTO      E14RA999 IF EQUAL
.
          MATCH     "A05Z",WIS14DRG
          GOTO      E14RA999 IF EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
E14RA100  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,E14RA999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      E14RA999 IF NOT EQUAL
.
          MATCH     "90225-00",PTEOCODE
          IF        @EQUAL
            MOVE      "A40Z",WIS14DRG   
            GOTO      E14RA999
          ENDIF
.
          MATCH     "38615-00",PTEOCODE
          IF        @EQUAL
            MOVE      "A40Z",WIS14DRG   
            GOTO      E14RA999
          ENDIF
.
          MATCH     "38615-01",PTEOCODE
          IF        @EQUAL
            MOVE      "A40Z",WIS14DRG   
            GOTO      E14RA999
          ENDIF
.
          MATCH     "38618-00",PTEOCODE
          IF        @EQUAL
            MOVE      "A40Z",WIS14DRG   
            GOTO      E14RA999
          ENDIF
.
          GOTO      E14RA100
.
E14RA999  RETURN
+
.******************************************************************************
.*                                  D14PS000              Called by: M14RG000 *
.*                    Modify DRG if Diagnosis or Procedure incompatible Sex   *
.******************************************************************************
D14PS000  MATCH     "901Z",WIS14DRG                                    *I-53126
          GOTO      D14PS999 IF NOT EQUAL
.
D14PS100  PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
D14PS200  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,D14PS999
.
          MATCH     AADMNO,PTEDADMN         * same admission?
          GOTO      D14PS999 IF NOT EQUAL
.
          MATCH     "E25.0",PTEDCODE        * is there a sex explin.
          GOTO      D14PS800 IF EQUAL       * yes
.
          MATCH     "E25.8",PTEDCODE        * is there a sex explin.
          GOTO      D14PS800 IF EQUAL       * yes
.
          MATCH     "E29.1",PTEDCODE        * is there a sex explin.
          GOTO      D14PS800 IF EQUAL       * yes
.
          MATCH     "E34.5",PTEDCODE        * is there a sex explin.
          GOTO      D14PS800 IF EQUAL       * yes
.
          MATCH     "F64.0",PTEDCODE        * is there a sex explin.
          GOTO      D14PS800 IF EQUAL       * yes
.
          MATCH     "Q56.0",PTEDCODE        * is there a sex explin.
          GOTO      D14PS800 IF EQUAL       * yes
.
          MATCH     "Q56.1",PTEDCODE        * is there a sex explin.
          GOTO      D14PS800 IF EQUAL       * yes
.
          MATCH     "Q56.2",PTEDCODE        * is there a sex explin.
          GOTO      D14PS800 IF EQUAL       * yes
.
          MATCH     "Q56.3",PTEDCODE        * is there a sex explin.
          GOTO      D14PS800 IF EQUAL       * yes
.
          MATCH     "Q56.4",PTEDCODE        * is there a sex explin.
          GOTO      D14PS800 IF EQUAL       * yes
.
          MATCH     "Q99.0",PTEDCODE        * is there a sex explin.
          GOTO      D14PS800 IF EQUAL       * yes
.
          MATCH     "Q99.1",PTEDCODE        * is there a sex explin.
          GOTO      D14PS800 IF EQUAL       * yes
.
          GOTO      D14PS200                * no
.
D14PS800  MOVE      "964Z",WIS14DRG
.
D14PS999  RETURN
+
.******************************************************************************
.*                                  C14RG000              Called by: PATWIS14 *
.*                            Check for Problem DRG                           *
.******************************************************************************
C14RG000  PACK     KEY4,WIS14DRG
          CALL     RDPT141                  * check WIES14 weights file
          BRANCH   OVRCD,C14RG900
.
          MATCH    "960Z",PT14DRGC
          GOTO     C14RG900 IF EQUAL
.
          MATCH    "961Z",PT14DRGC
          GOTO     C14RG900 IF EQUAL
.
          MATCH    "962Z",PT14DRGC
          GOTO     C14RG900 IF EQUAL
.
          MATCH    "963Z",PT14DRGC
          GOTO     C14RG900 IF EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     C14RG999
.
C14RG900  MOVE     ONE,EXIT
C14RG999  RETURN
+
.******************************************************************************
.*                                  M14CH000              Called by: PATWIS14 *
.*                Calculate Mechanical Ventialtion Co-payment                 *
.******************************************************************************
M14CH000  MOVE      ZERO,ADJMVDAY
          MOVE      ZERO,MVCOPAY
.
          PACK      KEY8,AADMNO
          CALL      RDPTICU1                * get mech. vent. hours
          BRANCH    OVRCD,M14CH999
.
          MATCH     "D",PT14MVEL            * check mech. vent. eligibility
          IF        @EQUAL
            IF        PTICUMEC > 6
              ASSIGN    ((PTICUMEC+12)/24),FORM12P4    * days receiving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7729)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
            GOTO      M14CH999
          ENDIF
.
          MATCH     "N",PT14MVEL            * check mech. vent. eligibility
          IF        @EQUAL
            IF        PTICUMEC > 6
              ASSIGN    ((PTICUMEC+12)/24),FORM12P4    * days receiving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7729)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
            GOTO      M14CH999
          ENDIF
.
          MATCH     "4",PT14MVEL
          IF        @EQUAL
            IF        PTICUMEC > 96
              ASSIGN    (((PTICUMEC+12)/24)-4),FORM12P4   * days recving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7729)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
          ENDIF
.
M14CH999  RETURN
+
.******************************************************************************
.*                                  T14AL000              Called by: PATWIS14 *
.*                      Calculate Thalessaemia Co-payment                     *
.******************************************************************************
T14AL000  MOVE      ZERO,THCOPAY
          MATCH     "T",PT14CPAY            * check WIES14 weights file
          IF        @EQUAL
            PACK      KEY16,AADMNO,SP20
            CALL      RSPTECD2              * Position on & read a patient
T14AL100    CALL      RKPTECD2                episode disease file record
            BRANCH    OVRCD,T14AL999
.
            MATCH     AADMNO,PTEDADMN
            GOTO      T14AL999 IF NOT EQUAL
.
            MATCH     "D56",PTEDCODE        * check ICD-10 code
            IF        !@EQUAL
              MATCH     "D57.2",PTEDCODE    * check ICD-10 code
              GOTO      T14AL100 IF NOT EQUAL
            ENDIF
            MOVE      "0.2648",THCOPAY      * eligible for thal. copayment
          ENDIF
T14AL999  RETURN
+
.******************************************************************************
.*                                  A14AA000              Called by: PATWIS14 *
.*                          Calculate AAA Co-payment                          *
.******************************************************************************
A14AA000  MOVE      ZERO,AAACOPAY
          MATCH     "AAA",PT14CPAY          * check WIES14 weights file
          GOTO      A14AA999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
A14AA100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,A14AA999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      A14AA999 IF NOT EQUAL
.
          MATCH     "33116-00",PTEOCODE     * check ICD-10 code
          GOTO      A14AA100 IF NOT EQUAL
.
          MOVE      "3.1421",AAACOPAY       * eligible for AAA copayment
A14AA999  RETURN
+
.******************************************************************************
.*                                  A14SD000              Called by: PATWIS14 *
.*                          Calculate ASD Co-payment                          *
.******************************************************************************
A14SD000  MOVE      ZERO,ASDCOPAY
          MATCH     "ASD",PT14CPAY          * check WIES14 weights file
          GOTO      A14SD999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
A14SD100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,A14SD999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      A14SD999 IF NOT EQUAL
.
          MATCH     "38742-00",PTEOCODE     * check ICD-10 code
          GOTO      A14SD100 IF NOT EQUAL
.
          MOVE      "2.4713",ASDCOPAY       * eligible for ASD copayment
A14SD999  RETURN
+
.******************************************************************************
.*                                  C14OL000              Called by: PATWIS14 *
.*                       Calculate Colonoscopy Co-payment                     *
.******************************************************************************
C14OL000  MOVE      ZERO,COLCOPAY
          MATCH     "Col",PT14CPAY          * check WIES14 weights file
          GOTO      C14OL999 IF NOT EQUAL
.
          CALL      LDCB1400                * load col. copayment codes
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
C14OL100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,C14OL999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      C14OL999 IF NOT EQUAL
.
          MOVE      ZERO,COUNT
C14OL200  ADD       ONE,COUNT
          COMPARE   TEN2,COUNT
          GOTO      C14OL100 IF EQUAL       * check next code on patecoaf
.
          MATCH     COLCODES[COUNT],PTEOCODE     * check ICD-10 code
          GOTO      C14OL200 IF NOT EQUAL
.
          MOVE      "0.1765",COLCOPAY       * eligible for Colono. copayment
C14OL999  RETURN
+
.******************************************************************************
.*                                  LDCB1400              Called by: C14OL000 *
.*                      Load Colonoscopy Copayment Codes                      *
.******************************************************************************
LDCB1400  MOVE      "30479-01",COLCODES[1]
          MOVE      "30479-02",COLCODES[2]
          MOVE      "90308-00",COLCODES[3]
          MOVE      "90312-01",COLCODES[4]
          MOVE      "32084-00",COLCODES[5]
          MOVE      "32084-01",COLCODES[6]
          MOVE      "32087-00",COLCODES[7]
          MOVE      "32090-00",COLCODES[8]
          MOVE      "32090-01",COLCODES[9]
          MOVE      "32093-00",COLCODES[10]
          MOVE      "32094-00",COLCODES[11]
LDCB1499  RETURN
+
.******************************************************************************
.*                                  B14SW000              Called by: PATWIS14 *
.*                            Calculate Base WIES14                           *
.******************************************************************************
B14SW000  CALL      D14SC000                * length of stay category
          CALL      C14ST000                * calculate inlier status
.
          MATCH     "L",W14NLIER            * low outlier
          CALL      BW14L000 IF EQUAL       * calculate base WIES14
.
          MATCH     "I",W14NLIER            * inlier
          CALL      BW14N000 IF EQUAL       * calculate base WIES14
.
          MATCH     "H",W14NLIER            * high outlier
          CALL      BW14H000 IF EQUAL       * calculate base WIES14
.
B14SW999  RETURN
+
.******************************************************************************
.*                                  D14SC000              Called by: B14SW000 *
.*                      Determine Length Of Stay Category                     *
.******************************************************************************
D14SC000  MOVE      NBRDAYS,WIS14LOS        * length of stay from NHOSPDAY
          MOVE      "M",LOSCAT              * set LOS category to multiday
          MATCH     ADATE,DDATE             * check for sameday
          IF        @EQUAL
            MOVE      ONE,SAMEDAY           * sameday
            MOVE      ONE,WIS14LOS          * LOS = 1
            MOVE      "S",LOSCAT            * set LOS category to sameday
            GOTO      D14SC999
          ELSE
            MOVE      ZERO,SAMEDAY          * not sameday
          ENDIF
.
          IF        SAMEDAY = 0 & WIS14LOS = 1
            MOVE      "O",LOSCAT            * set LOS category to oneday
            GOTO      D14SC999
          ENDIF
.
          IF        WIS14LOS > 1825
            MOVE      "1825",WIS14LOS       * maximum LOS is 5 years
          ENDIF
.
D14SC999  RETURN
+
.******************************************************************************
.*                                  C14ST000              Called by: B14SW000 *
.*                           Calculate Inlier Status                          *
.******************************************************************************
C14ST000  MOVE      "I",W14NLIER            * set status to inlier
          IF        WIS14LOS < PT14LIBP
            MOVE      "L",W14NLIER          * set status to low outlier
            GOTO      C14ST999
          ENDIF
.
          ASSIGN    (PT14HIBP+ADJMVDAY),FORM12P4   * high bound. + mech.v days
          IF        WIS14LOS > FORM12P4
            MOVE      "H",W14NLIER          * set status to high outlier
          ENDIF
C14ST999  RETURN
+
.******************************************************************************
.*                                  BW14L000              Called by: B14SW000 *
.*                     Calculate Base WIES14 for Low Outlier                  *
.******************************************************************************
BW14L000  MATCH     "S",LOSCAT            * sameday
          IF        @EQUAL
            MOVE      PT14TSDW,TOTBWIES             * base WIES14
            ASSIGN    (TOTBWIES/PT14TIMW),TOTIES14  * IES14
.
            MOVE      PT14TSDW,WIS14WGT             * for display
            MOVE      "Same Day Wgt ",WIS14DSC
            GOTO      BW14L999
          ENDIF
.
          MATCH     "O",LOSCAT            * oneday
          IF        @EQUAL
            MOVE      PT14TODW,TOTBWIES             * base WIES14
            ASSIGN    (TOTBWIES/PT14TIMW),TOTIES14  * IES14
.
            MOVE      PT14TODW,WIS14WGT             * for display
            MOVE      "One Day Wgt  ",WIS14DSC
            GOTO      BW14L999
          ENDIF
.
          MATCH     "M",LOSCAT            * multiday
          IF        @EQUAL
            ASSIGN    (PT14TODW+((WIS14LOS-1)*PT14TLOM)),TOTBWIES
.                                                    * base WIES14
            ASSIGN    (TOTBWIES/PT14TIMW),TOTIES14   * IES14
.
            MOVE      PT14TLOM,WIS14WGT             * for display
            MOVE      "Low Outlr Wgt",WIS14DSC
            GOTO      BW14L999
          ENDIF
BW14L999  RETURN
+
.******************************************************************************
.*                                  BW14N000              Called by: B14SW000 *
.*                      Calculate Base WIES14 for Inlier                      *
.******************************************************************************
BW14N000  MOVE      ONE,TOTIES14          * IES14 = 1
          MATCH     "S",LOSCAT            * sameday
          IF        @EQUAL
            MOVE      PT14TSDW,TOTBWIES   * base WIES14
.
            MOVE      PT14TSDW,WIS14WGT             * for display
            MOVE      "Same Day Wgt ",WIS14DSC
            GOTO      BW14N999
          ENDIF
.
          MATCH     "O",LOSCAT            * oneday
          IF        @EQUAL
            MOVE      PT14TODW,TOTBWIES   * base WIES14
.
            MOVE      PT14TODW,WIS14WGT             * for display
            MOVE      "One Day Wgt  ",WIS14DSC
            GOTO      BW14N999
          ENDIF
.
          MATCH     "M",LOSCAT            * multiday
          IF        @EQUAL
            MOVE      PT14TIMW,TOTBWIES   * base WIES14
.
            MOVE      PT14TIMW,WIS14WGT             * for display
            MOVE      "Inlier Wgt   ",WIS14DSC
            GOTO      BW14N999
          ENDIF
BW14N999  RETURN
+
.******************************************************************************
.*                                  BW14H000              Called by: B14SW000 *
.*                    Calculate Base WIES14 for High Outlier                  *
.******************************************************************************
BW14H000  ASSIGN    ((WIS14LOS-PT14HIBP)-ADJMVDAY),HIGHDAYS  * high outlr days
          COMPARE   ZERO,HIGHDAYS
          IF        @LESS
            MOVE      ZERO,HIGHDAYS         * highdays must be >= zero
          ENDIF
.
          COMPARE   HIGHDAYS,HIHLOS
          IF        @LESS
            MOVE      HIHLOS,HITHDAYS       * get lesser of highdays and
          ELSE                              *  HIHLOS
            MOVE      HIGHDAYS,HITHDAYS
          ENDIF
.
          ASSIGN    (HIGHDAYS-HITHDAYS)*PT14THOM,TOTBWIES    *high WIES
          ASSIGN    (HITHDAYS*PT14THIH)+TOTBWIES,TOTBWIES    *HITH WEIS
          ASSIGN    (PT14TIMW+TOTBWIES),TOTBWIES             *base WEIS14
          ASSIGN    (TOTBWIES/PT14TIMW),TOTIES14             * IES14
.
          MOVE      PT14THOM,WIS14WGT                        * for display
          MOVE      "High Outl Wgt",WIS14DSC
BW14H999  RETURN
+
.******************************************************************************
.*                                  C14AB000              Called by: PATWIS14 *
.*                          Check for Aboriginality                           *
.******************************************************************************
C14AB000  MOVE      ZERO,ATSIWIES                * set ATSI WIES to zero
          IF        PNSWRACE<1 | PNSWRACE>14
            GOTO      C14AB999              * Invalid parameter
          ENDIF
.
          LOAD      TCODE,PNSWRACE,CATP1,CATP2,CATP3,CATP4,CATP5:
                                   CATP6,CATA1,CATA2,CATA3,CATA4:
                                   CATA5,CATA6,CATA7,CATVA
          LOAD      ACODE,PNSWRACE,PUSR1,PUSR2,PUSR3,PUSR4,PUSR5:
                                   PUSR6,AUSR1,AUSR2,AUSR3,AUSR4:
                                   AUSR5,AUSR6,AUSR7,PMPXABRG
.
          MATCH     ACODE,SP3
          GOTO      C14AB999 IF EQUAL
.
          PACK      KEY5,TCODE,ACODE
          CALL      RDCODE1                 * Read a code file record
          BRANCH    OVRCD,C14AB999
.
          MOVE      THCSCOD,KEY1
          TYPE      KEY1
          GOTO      C14AB999 IF NOT EQUAL   * Non numeric, error
.
          MOVE      KEY1,FORM1
.
. ---- Calculate ATSI Loading if aboriginality = 5, 6, or 7 ----
.
          IF        FORM1=5 | FORM1=6 | FORM1=7
            MOVE      TOTBWIES,FORM12P4       * base WIES
            ADD       MVCOPAY,FORM12P4        * mechv copayment
            ADD       THCOPAY,FORM12P4        * thal  copayment
            ADD       AAACOPAY,FORM12P4       * AAA   copayment
            ADD       ASDCOPAY,FORM12P4       * ASD   copayment
            ADD       COLCOPAY,FORM12P4       * colon copayment
            ASSIGN    (0.3*FORM12P4),ATSIWIES
          ENDIF
.
C14AB999  RETURN
+
