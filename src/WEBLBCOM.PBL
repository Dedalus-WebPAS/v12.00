.******************************************************************************
.* LABELCOM - Common Routine for Displaying/Printing Screen Painted Labels    *
.*                                                                            *
.* Parameters - NOLABEL - no of labels to print                               *
.*              LABCOS  - no. printing across the page                        *
.*              LABSIZE - Wide or Small Format                                *
.*              PRGID                                                         *
.*              SCRID                                                         *
.* Returns: EXIT 0 = Labels printed ok OR zero labels requested for printing  *
.*               1 = Non screen painted labels to be printed                  *
.* Mods:                                                                      *
.*        V10.03.03   11/11/2013  Peter Vela     CAR 284858                   *
.*                    Fixed number of labels being printed LABELCOM           *
.*        V10.03.02   07/11/2013  Peter Vela     CAR 284858                   *
.*                    Added print wrist band labels to emergency LCOM6050     *
.*        V10.03.01   31/10/2012  Jill Parkinson CAR 275635                   *
.*                    Default LABCOS to 1 if not set up to stop looping       *
.*                    Added branch on ovrcd if RDSCMA1 fails                  *
.*        V9.09.01    09/08/2007  Peter Vela     CAR 147539                   *
.*                    Fixed 0 label printing                                  *
.*        V9.08.01    03/05/2007  Jill Habasque  CAR 139238                   *
.*                    Mods to print wrist band labels                         *
.*        V9.04.01    16/08/2005  Ebon Clements CAR 58448                     *
.*                    Removed Changes - Moved Label one column to the right   *
.*        10/08/2005  Peter Vela CAR 58448                                    *
.*                    Moved Label one column to the right                     *
.*        20/04/1998  Nick Read                                               *
.*                    New var LABSIZE to use instead of HELSIZE               *
.*        06/04/1998  Nick Read                                               *
.*                    Mods to print multiple screen painted labels across the *
.*                    page based on parameters HELPRNT, LABSIZE and HELPAGE   *
.*        02/01/1998  Steve Armstrong                                         *
.*                    Set for full screen save HLEF = 0                       *
.*        14/10/1997  Steve Armstrong                                         *
.*                    Mods for Wrist Band Labels.                             *
.*                    Mods to return EXIT as zero if there are zero labels    *
.*                    for printing.                                           *
.******************************************************************************
LABELCOM  CALL      CLRARR00                * Clear Print Array
          CALL      DSPBGRND                * display label background
          CALL      DSPFIELD                * display field values
          MOVE      NOLABEL,LABPRINT
          COMPARE   ZERO,LABCOS
          IF        @EQUAL
            MOVE      ONE,LABCOS            * added to stop loop if number   
.                                           * across the page is not set up  
          ENDIF
.
          MATCH     "33",SCRID              * printing inpatient/emr labels ?
          IF        @EQUAL
            SUB       TWO,LABPRINT
          ENDIF
.
          CALL      OPNPRINT
          MOVE      ONE,FORM3
.
          IF        LABSIZE=1
            MOVE      "37",TABPOS2
            MOVE      "73",TABPOS3
          ENDIF
.
          IF        LABSIZE=2
            MOVE      "42",TABPOS2
            MOVE      "83",TABPOS3
          ENDIF
.
.         get the label screen size
.
..          OPEN      SCRMASA1,"scrmasaf"
LCOM3000  MOVE      EIGHT,SCMATOP
          MOVE      TWENTY2,SCMABOT
          PACK      KEY10,PRGID,SCRID    
          CALL      RDSCMA1
          BRANCH    OVRCD,LCOM6500
.
LCOM4000  MOVE      SCMATOP,CVERT             * start at top of screen
.
.         If using the Eltron printer, then clear the image buffer
.         and initialise the first line number
.
          IF        USINGELT = 1
            PRINT     ANSN
            MOVE      ZERO,LINEONE
          ENDIF
.
LCOM5000  COMPARE   TWENTY3,CVERT
          GOTO      LCOM6000 IF NOT LESS
.
          MOVE      LABELDAT[CVERT],PRINTVAR
          MOVE      LABELATT[CVERT],ATT
          STRIP     PRINTVAR
.
. --------- stop if no background ---------------------------------------
.
          PACK      KEY12,PRGID,SCRID,CVERT,SP20
          CALL      RDSCSB1                 * positional read
          BRANCH    OVRCD,LCOM6000
.
.         If using the Eltron printer, get the first line to be printed
.         from the screen painter background file, and subtract one from this
.         to get the difference.  This figure will be subtracted from each
.         of the screen painter background records to get the 
.         actual line number to print on.
.
          IF        USINGELT = 1 & LINEONE = 0
            MOVE      SCSBLIN,LINEONE
            ASSIGN    (LINEONE - 1),LINEDIFF
          ENDIF
.
          REP       BARCODE,ATT             * change any barcoded fields
.
.         if using TBC_7, then compile with a printvar
.         If we are using the eltron printer, then format the print string.
.         The line number has to be converted to "dots" for the Eltron.
.        
          IFGE      $$VER,7
            IF        USINGELT = 1
              MOVE      SCSBLIN,LINEPRNT
              ASSIGN    ((LINEPRNT - LINEDIFF) * 24 - 24),LINEPRNT
              MOVE      LINEPRNT,LINEFRMT
              SQUEEZE   LINEFRMT
              STRIP     PRINTVAR              * remove trailing blanks
              PRINT    ANSA0,COMMA,*+,LINEFRMT,COMMA,ELTRON,PRINTVAR,ENDQUOTE,*-
            ELSE
              IF       LABCOS>1 & LABPRINT > 1
                  IF       LABCOS=2
                     PRINT      *1;            * two across the page
                     PRINTVAR   PRINTVAR,ATT;
                     PRINT      *TABPOS2;
                     PRINTVAR   PRINTVAR,ATT
                     PRINT      *N,*1;
                  ELSE
                     PRINT      *1;            * three across the page
                     PRINTVAR   PRINTVAR,ATT;
                     PRINT      *TABPOS2;
                     PRINTVAR   PRINTVAR,ATT;
                     PRINT      *TABPOS3;
                     PRINTVAR   PRINTVAR,ATT
                     PRINT     *N,*1;
                  ENDIF
              ELSE
                  IF        LABPRINT > 0
                    PRINTVAR  PRINTVAR,ATT  * printing only one across page
                    PRINT     *N,*1;
                  ENDIF
              ENDIF
            ENDIF
          XIF
.
.         if using TBC_6, then compile with a print
.        
          IFEQ      $$VER,6
            IF        USINGELT = 1
              MOVE      SCSBLIN,LINEPRNT
              ASSIGN    ((LINEPRNT - LINEDIFF) * 24 - 24),LINEPRNT
              MOVE      LINEPRNT,LINEFRMT
              SQUEEZE   LINEFRMT
              STRIP     PRINTVAR              * remove trailing blanks
              PRINT    ANSA0,COMMA,*+,LINEFRMT,COMMA,ELTRON,PRINTVAR,ENDQUOTE,*-
            ELSE
              IF        LABCOS>1 & LABPRINT > 1
                  IF        LABCOS=2
                      PRINT     PRINTVAR;     * print 2 across the page
                      PRINT     *TABPOS2;
                      PRINT     PRINTVAR; 
                      PRINT     *N;
                  ELSE
                      PRINT     PRINTVAR;     * print 3 across the page
                      PRINT     *TABPOS2;
                      PRINT     PRINTVAR; 
                      PRINT     *TABPOS3;
                      PRINT     PRINTVAR; 
                      PRINT     *N;
                  ENDIF
              ELSE
                  IF        LABPRINT > 0
                    PRINT     PRINTVAR
                  ENDIF
              ENDIF
            ENDIF
          XIF
          ADD       ONE,CVERT
          GOTO      LCOM5000
.
LCOM6000  IF        USINGELT = 1
            PRINT     "P1"
          ENDIF
.
          SUB       LABCOS,LABPRINT
          IF        LABPRINT>0
            IF        LABCOS>LABPRINT
              MOVE      LABPRINT,LABCOS
            ENDIF
            GOTO       LCOM4000
          ENDIF
.
LCOM6050  MATCH     "LABELINP",PRGID        * printing inpatient labels ?
          GOTO      LCOM6100 IF EQUAL   * no - finished printing
.
          MATCH     "LABELEMR",PRGID        * printing inpatient labels ?
          GOTO      LCOM6100 IF EQUAL   * no - finished printing
.
          GOTO      LCOM6500
.
LCOM6100  MATCH     "33",SCRID              * printing inpatient labels ?
          GOTO      LCOM6500 IF NOT EQUAL   * no - finished printing
.
.         A screen painted inpatient label exists and has been printed,
.         so check if there is a screen painted wrist label.
.         If not, don't prompt for wrist label
.
          MOVE      "02",SCRID              * set wrist band label screen id
          CALL      CKSR0000                * is wrist label screen painted ?
          BRANCH    EXIT,LCOM6500           * no - finished
.
          MOVE      TWO,NOLABEL           * set default number of labels to 2
.
          DISPLAY   *P1:7,*EF
          HLINE     *G37,7,1,80
          HLINE     *G37,23,1,80
          CALL      DSPBGRND              * display label background
          CALL      DSPFIELD              * display field values
.
          MOVE      NOLABEL,LABPRINT
          GOTO      LCOM3000              * yes
.
LCOM6500  CALL      CLSPRINT
.
          MOVE      ZERO,EXIT
LCOM9999  RETURN                  
.------------------------------------------------------------
. Clear Printing Array
.------------------------------------------------------------
CLRARR00  MOVE      SP80,LABELDAT[1]
          MOVE      SP80,LABELDAT[2]
          MOVE      SP80,LABELDAT[3]
          MOVE      SP80,LABELDAT[4]
          MOVE      SP80,LABELDAT[5]
          MOVE      SP80,LABELDAT[6]
          MOVE      SP80,LABELDAT[7]
          MOVE      SP80,LABELDAT[8]
          MOVE      SP80,LABELDAT[9]
          MOVE      SP80,LABELDAT[10]
          MOVE      SP80,LABELDAT[11]
          MOVE      SP80,LABELDAT[12]
          MOVE      SP80,LABELDAT[13]
          MOVE      SP80,LABELDAT[14]
          MOVE      SP80,LABELDAT[15]
          MOVE      SP80,LABELDAT[16]
          MOVE      SP80,LABELDAT[17]
          MOVE      SP80,LABELDAT[18]
          MOVE      SP80,LABELDAT[19]
          MOVE      SP80,LABELDAT[20]
          MOVE      SP80,LABELDAT[21]
          MOVE      SP80,LABELDAT[22]
          MOVE      SP80,LABELDAT[23]
          MOVE      SP80,LABELDAT[24]
.
          MOVE      SP80,LABELATT[1]
          MOVE      SP80,LABELATT[2]
          MOVE      SP80,LABELATT[3]
          MOVE      SP80,LABELATT[4]
          MOVE      SP80,LABELATT[5]
          MOVE      SP80,LABELATT[6]
          MOVE      SP80,LABELATT[7]
          MOVE      SP80,LABELATT[8]
          MOVE      SP80,LABELATT[9]
          MOVE      SP80,LABELATT[10]
          MOVE      SP80,LABELATT[11]
          MOVE      SP80,LABELATT[12]
          MOVE      SP80,LABELATT[13]
          MOVE      SP80,LABELATT[14]
          MOVE      SP80,LABELATT[15]
          MOVE      SP80,LABELATT[16]
          MOVE      SP80,LABELATT[17]
          MOVE      SP80,LABELATT[18]
          MOVE      SP80,LABELATT[19]
          MOVE      SP80,LABELATT[20]
          MOVE      SP80,LABELATT[21]
          MOVE      SP80,LABELATT[22]
          MOVE      SP80,LABELATT[23]
          MOVE      SP80,LABELATT[24]
.
          RETURN
