.******************************************************************************
.*  Include Name  : PATITMKY.PBL                                              *
.*  Description   : Keyin of item codes                                       *
.*  Author        : Ian Rutt                                                  *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF9 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMODE - is keyin mode                                  *
.*                  RANGE    - 1 = YES 0 = NO                                 *
.*                                                                            *
.*  Returns       : ITEMNO    - Keyed in code                                 *
.*                  IDESC    - Description                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*        V11.01.01 03/03/2020  J.Tan          TSK 0838262
.*                  Added Effective from and to date to MBS Item file
.*        V6.05.02  25/09/1998    Katie Nelson                                *
.*                  Added procedure HPTIKA00 to key word search               *
.*                                                                            *
.******************************************************************************
PATITMKY
PTITKY00  MATCH     SP9,CKYIDEF9                  * using a default ?
          GOTO      PTITKY10 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN9:      * no default used
                    *PECOL:EVERT,*V2LON,ITEMNO:
                    *PECOL:EVERT,*DV,ITEMNO
          GOTO      PTITKY20
.
PTITKY10  MOVE      CKYIDEF9,ITEMNO                 * default
          KEYIN     *PECOL:EVERT,*DV,ITEMNO:     * default used
                    *PECOL:EVERT,*V2LON,*RV,ITEMNO:
                    *PECOL:EVERT,*DV,ITEMNO
.
.         check what was entered
.
PTITKY20  CALL      ITMCK000
          BRANCH    EXIT,PTITKY25,PTITKY31,PTITKY41,PTITKY95
.                        "?"      valid    nothing  exitchar
          GOTO      PTITKY00
.
.         a "?" was input
.
PTITKY25  PROC      HPTIKA00
          BRANCH    OVRCD,PTITKY00
          IF        CKYIMODE=1
            CALL      REXISTS
            GOTO      PTITKY00
          ENDIF
PTITKY31  MOVE      FALSE,EXIT                    * valid input
          DISPLAY   *PECOL:EVERT,*V2LON,ITEMNO
          GOTO      PTITKY99
.
.         nothing was input
.
PTITKY41  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      PTITKY99 
.
.         EXITCHAR input
.
PTITKY95  MOVE      TWO,EXIT                * EXITCHAR
.
PTITKY99  RETURN
+
.*********************************************************************
.*                  ITMCK000                    Called by : PATITMKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
ITMCK000  ENDSET    ITEMNO
          APPEND    SP9,ITEMNO
          RESET     ITEMNO
.
          MATCH     EXITCHAR,ITEMNO
          GOTO      ITMCK600 IF EQUAL       * exit char
.
          MATCH     SP9,ITEMNO
          GOTO      ITMCK700 IF EQUAL       * nothing
.
          MATCH     QUEST,ITEMNO
          GOTO      ITMCK800 IF EQUAL       * ? mark
.
.         a code was entered so validate
. 
ITMCK500  COMPARE   ONE,RANGE
          IF        @EQUAL
            MOVE      TWO,EXIT
            GOTO      ITMCK9999
          ENDIF
. 
          PACK      KEY9,ITEMNO,SP70
          PACK      KEY17,KEY9,SP70
          CALL      PATITMRD
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,ITMCK900          * invalid code
          ELSE
            COMPARE   ZERO,OVRCD   
            GOTO      ITMCK900 IF EQUAL
          ENDIF  
.
          MOVE      TWO,EXIT                * valid code
          GOTO      ITMCK999
.
.         exitchar entered
.
ITMCK600  MOVE      FOUR,EXIT
          MOVE      SP9,ITEMNO
          MOVE      SP20,IDESC              * clear desc field
          GOTO      ITMCK999
.
.         nothing entered
.
ITMCK700  BRANCH    CKYIMAND,ITMCK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP9,ITEMNO
          MOVE      SP20,IDESC              * clear desc field
          GOTO      ITMCK999
.
.         "?" entered
.
ITMCK800  MOVE      ONE,EXIT
          GOTO      ITMCK999
.
.         invalid code entered
.
ITMCK900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
.
          IF        CKYIMODE = 0
            DISPLAY   *P1:24,*EL,*B,"Invalid Item Code. ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      ITMCK999
.
.         field is mandatory
.
ITMCK950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is Mandatory. ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
ITMCK999  RETURN
.
..          INCLUDE   PATITMDS
          INC       PATIKIKY
+
