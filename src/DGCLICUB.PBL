.************************************************************************
.*  Procedure: DGCLICUB  -  Send a Booking Pre-Admission                *
.*                                                                      *
.*  Author   : Mike Laming  (10/07/2001)                                *
.*                                                                      *
.*  Requires : BOKRC1FD (Booking Table - Operating)                     *
.*             BOKRX1FD (Booking Table - Extn.1)                        *
.*             PATMA1FD (Patient Master & Extension)                    *
.*             PMSPX2FD (Patient Master Extn.2)                         *
.*                                                                      *
.*             OPRCONFD (Operations Control File)                       *
.*             PATCONFD (Patient Control File)                          *
.*                                                                      *
.*  Mods     :                                                          *
.*                                                                      *
.************************************************************************
.*  V11.04.01 09/01/2024 Thanh T           TSK 0936263                  *
.*            Added OPEN/CLOSE PMSQPXA1 index                           *
.************************************************************************
.*  V10.03.01 17/04/2013  Davin S          CAR 280425                   *
.*            Changed IBCNUCIS - value 1 or greater will write trigger  *
.*  V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                  *
.*            Added setting of H7CITRID & H7CIINCL                      *
.************************************************************************
.*  V9.12.01   09/11/2009  Davin             CAR 208349                 *
.*             Fixed to check correct parameter to trigger CUB message  *
.*  V9.09.01   28/11/2007  Steve Armstrong   CAR 155887                 *
.*             Mods to handle HL7 messaging.                            *
.*             Mods to write directly to hl7inbaf instead of hl7bmtaf.  *
.************************************************************************
.*  V9.02.03   08/11/2001  Davin & MikeL                                *
.*             Mods to use message holding file                         *
.*  V9.02.02 : 10/09/2001  Mike Laming                                  *
.*             Remove all DB Segments except BOKRC1FD & BOKRX1FD        *
.*             This is because the Booking is done before any of the    *
.*             other Database segments are created.                     *
.*  V9.02.01 : 04/09/2001  Mike Laming                                  *
.*             Add NHIMAS-- and PATNID-- INCs for PMIGTNID usage        *
.*                                                                      *
.************************************************************************
          DEFPROC   DGCLICUB
.
          INC       BOKQUEFD/INC
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
          INC       PMSQPTFD/INC
          INC       WATQUEFD/INC
.
CISMFLAG  FORM      1             * CIS message flag
.                                    0 = don't send HL7 CIS message
.                                    1 = send HL7 CIS message
IBAMFLAG  FORM      1             * IBA proprietary message flag
.                                    0 = don't send IBA proprietary message
.                                    1 = send IBA proprietary message
.                     
MESSAGID  DIM       20
.
.
DGCLI000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,SIXTY8;*239,OPCNDGUB
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,HUND13;*1,OPCNH7BK
.
          MOVE      ZERO,CISMFLAG                * set default interface flag
          MATCH     "1",IBCNUCIS                 * using CIS interface ?
          IF        !@LESS
            MATCH     "1",OPCNH7BK               * yes - sending A05 ?
            IF        @EQUAL
              MOVE      ONE,CISMFLAG             * yes- set flag for CIS message
            ENDIF
          ENDIF
.
.         Check if we are sending any messages.
.
          IF        OPCNDGUB <> 1 & CISMFLAG <> 1
            GOTO      DGCLI999
          ENDIF
.
.         We are sending IBA Proprietary and/or CIS HL7 messages, so
.         first open the relevant holding tables
.
          MOVE      ZERO,IBAMFLAG
          IF        OPCNDGUB = 1
            OPEN      HL7INB01,"hl7inbaf"
            MOVE      ONE,IBAMFLAG
          ENDIF
          IF        CISMFLAG = 1
            OPEN      HL7CISA1,"hl7cisaf"
          ENDIF
          OPEN      PMSQPTA1,"pmsqptaf"
          OPEN      PMSQPXA1,"pmsqpxaf"
          OPEN      BOKQUEA1,"bokqueaf"
          OPEN      WATQUEA1,"watqueaf"
.
          CALL      DGMESSID                     * get the unique message id
.
.         Write a record to pmsqptaf (holding patient details)
.
          MOVE      MESSAGID,PMQPMESI
          MOVE      SP8,PMQPOURN
          PACK      PMQPSPAR,SP70,SP70
          CALL      WRPMQPT1
.
.         Write a record to bokqueaf (holding booking details)
.
          MOVE      MESSAGID,BKQUMESI
          PACK      BKQUSPAR,SP70,SP70
          CALL      WRBKQUE1
.
.         Write a record to watqueaf (holding waiting list details)
.
          MOVE      MESSAGID,WTQUMESI
          PACK      WTQUSPAR,SP70,SP70
          CALL      WRWTQUE1
.
          COMPARE   ONE,CISMFLAG                 * sending CIS HL7 A05 message?
          IF        @EQUAL
            MOVE      "A05",H7CIMETP
            MOVE      HL7TRGID,H7CITRID
            MOVE      HL7INCLD,H7CIINCL
            CALL      WRCIS000                   * yes - write hl7cisaf record
          ENDIF
.
          COMPARE   ONE,OPCNDGUB                 * sending proprietary message?
          IF        @EQUAL
            MOVE      "CUB",H7INMETP
            CALL      WRINB000                   * yes - write hl7inbaf record
          ENDIF
.
          CLOSE     BOKQUEA1
          CLOSE     HL7INB01
          CLOSE     HL7CISA1
          CLOSE     PMSQPTA1
          CLOSE     PMSQPXA1
          CLOSE     WATQUEA1
.
DGCLI999  GOTO      DGCLIEND                     * end procedure
.
.         I/O Routines
.
          INC       BRHLCOMN
          INC       CLPMSQPX
.
          INC       BOKQUEIO/INC
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       IBAOVRIO/INC
          INC       PMSQPTIO/INC
          INC       WATQUEIO/INC
.
DGCLIEND  ENDPROC
+
