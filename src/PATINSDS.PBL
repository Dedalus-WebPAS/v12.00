.
.       PATINSDS : Display Workers Company's
.
.       This include is used to display various options available, using
.       whatever file it needs, along with any special parameter supplied
.       by the calling program.
.
.     Modifications: 
.         V10.00.01 25/05/2010  Mike Laming       CAR 169939
.                   Added test for "Active" flag (Insurance Co.) at INSRLP1
.         .........  28/11/89 Graeme Williams
.                    Added the parameter CNEWDS to determine if the include
.                    is to use the new standards for "?" routines
.
.        Subroutine to display the available surgical INSRs
.
PATINSDS  MOVE       FIVE,CVERT
          MOVE       ONE,CCOL
          DISPLAY    *P1:3,*EF,*P20:4,*V2LON,*ULON,"INSURANCE COMPANIES"
.
.>>>>>>>  TRAP       ENDINSR0 IF F1
.
          MOVE       SP6,KEY6
          CALL       RDSINSR1
.
.         Loop over file
.
INSRLP1   CALL       RDKINSR1
          BRANCH     OVRCD OF ENDINSR1
.
          MATCH     "1",PTINACTV            * (1=inactive)            *I-169939
          GOTO      INSRLP1 IF EQUAL                                  *I-169939
.
          RESET      ICODE
          GOTO       INSRLP1 IF EOS
.
          MOVE       INAME,DIM30
          DISPLAY    *PCCOL:CVERT,*V2LON,ICODE,SP2,*HOFF,DIM30
          ADD        ONE,CVERT
          COMPARE    TWENTY3,CVERT
          GOTO       INSRLP1 IF LESS
.
          MOVE       FIVE,CVERT
          ADD        "40",CCOL
          COMPARE    "70",CCOL
          GOTO       INSRLP1 IF LESS
.
.>>>>>>>  TRAPCLR    F1
.
.         Check for new standards question, or old question
.
          BRANCH    CNEWDS,DSWCO200
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSE,ANS
          GOTO      ENDWCO2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATINSDS IF EQUAL
.
          GOTO      DSWCO400
.
.         Use the new standards question
.
DSWCO200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSC,ANS
          GOTO      ENDWCO2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATINSDS IF EQUAL
.
DSWCO400  MOVE       FIVE,CVERT
          MOVE       ONE,CCOL
          DISPLAY    *P1:5,*EF
.
.>>>>>>>  TRAP       ENDINSR0 IF F1
          GOTO       INSRLP1
.
ENDINSR0  NORETURN
.>>>>>>>  TRAPCLR    F1
.
.      End of rates
.
ENDINSR1  BRANCH    CNEWDS,ENDWCO2
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
ENDWCO2   RETURN
