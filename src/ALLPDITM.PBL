. *----------------------------------------------------------------------
. * Include Name  :   ALLPDITM.PBL
. *
. * Function      :   CGI variables for ALLPDITM - allpdiaf.dat
. *
. * Includes Needed : 
. *                   
. * Modifications   :
. * V11.03.01 09/01/2023  Bella Turco   TSK 0921957
. *           Added PTCNNRTD & NOMORE00 to RDTAB000 & PDTAB000 routines
. *                   V9.03.02 01/12/2003 Sylvek Litewka CAR 20222          
. *                            Modified section ALPD1500 to use common IO - 
. *                            RDPTICD1 when reading ICD Disease file.
. *                   V9.03.01 25/06/2003 Davin          
. *                            Created include
. *----------------------------------------------------------------------
.
.------------------------------------------------------------------------
.   Clear Parameters for Allied Health PDI Table
.------------------------------------------------------------------------
.
CPALPD00  MOVE      Z70,ALPDI001
          MOVE      Z70,ALPDI002
          MOVE      Z70,ALPDI003
          MOVE      Z70,ALPDI004
          MOVE      Z70,ALPDI005
          MOVE      Z70,ALPDI006
          MOVE      Z70,ALPDI007
          MOVE      Z70,ALPDI008
          MOVE      Z70,ALPDI009
          MOVE      Z70,ALPDI010
          MOVE      Z70,ALPDI011
.
CPALPD99  RETURN
.
.------------------------------------------------------------
. Add fields for allied health PDI Table
.------------------------------------------------------------
ALPD0000  BRANCH    FLDITMNO,ALPD0100,ALPD0200,ALPD0300,ALPD0400,ALPD0500:
                             ALPD0600,ALPD0700,ALPD0800,ALPD0900,ALPD1000:
                             ALPD1100,ALPD1200,ALPD1300,ALPD1400,ALPD1500:
                             ALPD1600,ALPD1700,ALPD1800
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      ALPD9999
.
.
ALPD0100  
.>>>>     MOVE      "LQ",CATEGORY           * Referring Department Code
.>>>>     MOVE      RDEPCODE,CODE
.>>>>     CALL      CODITM00
.>>>>     GOTO      ALPD9999
.
          WRITE     HTMLFILE;RDEPCODE;
          GOTO      ALPD9999
.
ALPD0200  WRITE     HTMLFILE;ALPDREAS;
          GOTO      ALPD9999
.
ALPD0300  WRITE     HTMLFILE;ALPDDESC;
          GOTO      ALPD9999
.
ALPD0400  WRITE     HTMLFILE;ALPDICD;
          GOTO      ALPD9999
.
ALPD0500  WRITE     HTMLFILE;ALPDACTV;
          GOTO      ALPD9999
.
ALPD0600  MATCH     ALPDCDAT,SP70
          GOTO      ALPD9999 IF EQUAL
          UNPACK    ALPDCDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      ALPD9999
.
ALPD0700  WRITE     HTMLFILE;ALPDCTIM; 
          GOTO      ALPD9999
.
ALPD0800  MATCH     SP70,ALPDCUID
          GOTO      ALPD9999 IF EQUAL
          PACK      KEY10,ALPDCUID
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      ALPDCUID,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM;
          GOTO      ALPD9999
.
ALPD0900  MATCH     ALPDUDAT,SP70
          GOTO      ALPD9999 IF EQUAL
          UNPACK    ALPDUDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      ALPD9999
.
ALPD1000  WRITE     HTMLFILE;ALPDUTIM;
          GOTO      ALPD9999
.
ALPD1100  MATCH     SP70,ALPDUUID
          GOTO      ALPD9999 IF EQUAL
          PACK      KEY10,ALPDUUID
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      ALPDUUID,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM;
          GOTO      ALPD9999
.
ALPD1200  CALL      PDTAB000
          GOTO      ALPD9999
.
ALPD1300  CALL      NEXTPD00
          GOTO      ALPD9999
.
ALPD1400  CALL      PREVPD00
          GOTO      ALPD9999
.
ALPD1500  MATCH     SP70,ALPDICD
          IF        !@EQUAL
            PACK      KEY9,ALPDICD,SP70
            CALL      RDPTICD1
            IF        OVRCD=1
              MOVE    SP70,DDESC
            ENDIF
            WRITE     HTMLFILE;DDESC;
          ELSE
            MOVE      SP70,DDESC
            WRITE     HTMLFILE;DDESC;
          ENDIF
          GOTO      ALPD9999
.
ALPD1600  CALL      RDTAB000                * referring department table
          GOTO      ALPD9999
.
ALPD1700  CALL      NEXTRD00                * next referring dept. group
          GOTO      ALPD9999
.
ALPD1800  CALL      PREVRD00                * previous referring dept. group
          GOTO      ALPD9999
.
ALPD9999  RETURN
.
.------------------------------------------------------------
. Table of Allied Health Predefined Diagnosis
.------------------------------------------------------------
PDTAB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY12,RDEPCODE,STARTKEY,SP70
          CALL      RSALPDI1
          CALL      RPALPDI1
.
PDTAB100  CALL      RKALPDI1
          BRANCH    OVRCD,PDTAB900
.
          MATCH     RDEPCODE,ALPDRDEP
          GOTO      PDTAB900 IF NOT EQUAL
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO     PDTAB100
          ENDIF
.
          CALL      PDROW000                * Display PDI Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      PDTAB100 IF NOT EQUAL
          GOTO      PDTAB999
.
PDTAB900  CALL      NOMORE00        * Display No More Records Message
.
PDTAB999  RETURN
.
.------------------------------------------------------------
.  Allied Health Predefined Diagnosis Reasons (TABLE ROW)
.------------------------------------------------------------
PDROW000  MATCH   "1",ALPDACTV
          IF      @EQUAL
             MOVE    "ACTIVE  ",ACTIDESC
          ELSE
             MOVE    "INACTIVE",ACTIDESC
          ENDIF
.
          MATCH   SP70,ALPDICD
          IF      @EQUAL
             MOVE    "&nbsp",ALPDICD
          ENDIF
.
          MATCH   SP70,ACTIDESC
          IF      @EQUAL
             MOVE    "&nbsp",ACTIDESC
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&rdepcode=",RDEPCODE:
                             "&alpdi002=",ALPDREAS,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             ALPDREAS,"</td>":
                             "<td>",ALPDDESC,"</td>":
                             "<td align=center>",ALPDICD,"</td>":
                             "<td align=center>",ACTIDESC,"</td>":
                             "</tr>"
.
PDROW999  RETURN
.
.-----------------------------------------------------------
. Link to Next Group
.-----------------------------------------------------------
NEXTPD00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,D3,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",RDEPCODE
          REP       " +",ALPDREAS
.
          WRITE     HTMLFILE;"allweb01.pbl":
                                 "?reportno=",F2:
                                 "&template=",D3:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&rdepcode=",RDEPCODE:
                                 "&alpdi002=",ALPDREAS;
.
          REP       "+ ",RDEPCODE
          REP       "+ ",ALPDREAS
.
NEXTPD99  RETURN
.
.-------------------------------------------------------------
. Link to Prev Group
.-------------------------------------------------------------
PREVPD00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",ALPDREAS
          REP       " +",RDEPCODE
.
          WRITE     HTMLFILE;"allweb01.pbl":
                                "?reportno=",F2:
                                "&template=",FLDPARA:
                                "&startnum=",KEY3:
                                "&norecord=",KEY2:
                                "&rdepcode=",RDEPCODE:
                                "&alpdi002=",ALPDREAS;
.
          REP       "+ ",ALPDREAS
          REP       "+ ",RDEPCODE
.
PREVPD99  RETURN
.
.------------------------------------------------------------
. Table of Allied Health Referring Department codes
.------------------------------------------------------------
RDTAB000  IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          MOVE      WBSESIT,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,RDTAB999
.
          MOVE      OSTCATG,KEY2          * source of referral category
.
          PACK      KEY5,KEY2,STARTKEY,SP70
          CALL      RDSCODE1
          IF        OVRCD=0
            CALL      RDPCODE1
          ENDIF
RDTAB100  CALL      RDKCODE1
          BRANCH    OVRCD,RDTAB900
          MATCH     KEY2,TCODE
          GOTO      RDTAB900 IF NOT EQUAL
          MATCH     SP70,ACODE
          GOTO      RDTAB100 IF EQUAL
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      RDTAB100
          ENDIF
.
          CALL      RDROW000                * Display Referring Dep.code Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      RDTAB100 IF NOT EQUAL
          GOTO      RDTAB999
.
RDTAB900  CALL      NOMORE00      * Display No More Records Message
.
RDTAB999  RETURN
.
.------------------------------------------------------------
.  Allied Health Department codes (TABLE ROW)
.------------------------------------------------------------
RDROW000  MATCH     SP70,TDESC
          IF        @EQUAL
            MOVE      "&nbsp;",TDESC
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap>":
                             "<img src=../images/MaintenanceIcon.gif ":
                             "class=ListIcon ":
                             "onclick=#"DepartmentLink('",ACODE,"');#"> ":
                             ACODE,"</td>":
                             "<td>",TDESC,"</td>":
                             "</tr>"
.
RDROW999 RETURN
.
.-----------------------------------------------------------
. Link to Next Referring Dept. group
.-----------------------------------------------------------
NEXTRD00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          WRITE     HTMLFILE;"allweb01.pbl":
                                 "?reportno=",F2:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2;
.
NEXTRD99  RETURN
.
.-------------------------------------------------------------
. Link to Prev Referring Dept. group
.-------------------------------------------------------------
PREVRD00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          WRITE     HTMLFILE;"allweb01.pbl":
                                 "?reportno=",F2:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2;
.
PREVRD99  RETURN
.
.------------------------------------------------------------
.   Set Parameters for allied health PDI table
.------------------------------------------------------------
SPALPD00  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPALPD01,SPALPD02,SPALPD03,SPALPD04,SPALPD05:
                       SPALPD06,SPALPD07,SPALPD08,SPALPD09,SPALPD10:
                       SPALPD11
.
          MOVE      ONE,EXIT
          GOTO      SPALPD99
.
SPALPD01  MOVE      QRYDATA,ALPDI001
          GOTO      SPALPD99
.
SPALPD02  MOVE      QRYDATA,ALPDI002
          GOTO      SPALPD99
.
SPALPD03  MOVE      QRYDATA,ALPDI003
          GOTO      SPALPD99
.
SPALPD04  MOVE      QRYDATA,ALPDI004
          GOTO      SPALPD99
.
SPALPD05  MOVE      QRYDATA,ALPDI005
          GOTO      SPALPD99
.
SPALPD06  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      ALPDI006,CCENT,CYEAR,CMON,CDAY
          GOTO      SPALPD99
.
SPALPD07  MOVE      QRYDATA,ALPDI007
          GOTO      SPALPD99
.
SPALPD08  MOVE      QRYDATA,ALPDI008
          GOTO      SPALPD99
.
SPALPD09  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      ALPDI009,CCENT,CYEAR,CMON,CDAY
          GOTO      SPALPD99
.
SPALPD10  MOVE      QRYDATA,ALPDI010
          GOTO      SPALPD99
.
SPALPD11  MOVE      QRYDATA,ALPDI011
          GOTO      SPALPD99
.
SPALPD99  RETURN
.------------------------------------------------------------
.  Update allied health PDI table
.------------------------------------------------------------
MVALPD00  MATCH     ALPDI001,Z70
          IF        !@EQUAL
            MOVE      ALPDI001,ALPDRDEP
          ENDIF
.
          MATCH     ALPDI002,Z70
          IF        !@EQUAL
            MOVE      ALPDI002,ALPDREAS
          ENDIF
.
          MATCH     ALPDI003,Z70
          IF        !@EQUAL
            MOVE      ALPDI003,ALPDDESC
          ENDIF
.
          MATCH     ALPDI004,Z70
          IF        !@EQUAL
            MOVE      ALPDI004,ALPDICD
          ENDIF
.
          MATCH     ALPDI005,Z70
          IF        !@EQUAL
            MOVE      ALPDI005,ALPDACTV
          ELSE
            MOVE      ZERO,ALPDACTV
          ENDIF
.
          MATCH     ALPDI006,Z70
          IF        !@EQUAL
            MOVE      ALPDI006,ALPDCDAT
          ENDIF
.
          MATCH     ALPDI007,Z70
          IF        !@EQUAL
            MOVE      ALPDI007,ALPDCTIM 
          ENDIF
.
          MATCH     ALPDI008,Z70
          IF        !@EQUAL
            MOVE      ALPDI008,ALPDCUID
          ENDIF
.
          MATCH     ALPDI009,Z70
          IF        !@EQUAL
            MOVE      ALPDI009,ALPDUDAT
          ENDIF
.
          MATCH     ALPDI010,Z70
          IF        !@EQUAL
            MOVE      ALPDI010,ALPDUTIM
          ENDIF
.
          MATCH     ALPDI011,Z70
          IF        !@EQUAL
            MOVE      ALPDI011,ALPDUUID
          ENDIF
.
MVALPD99  RETURN
