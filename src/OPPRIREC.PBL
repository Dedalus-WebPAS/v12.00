.******************************************************************************
.* System        :  Operating Rooms         Release 5.01                      *
.*                                                                            *
.* Include Name  :  OPPRIREC.PBL                                              *
.*                                                                            *
.* Function      :  Print Operation Records for a patient                     *
.*                                                                            *
.* Author        :  Justin Coates 03/04/1991                                  *
.*                                                                            *
.* Parameters    :  OPCNPREC  Sector 42 Position 68 - Format for printing     *
.*                  OPCNNCOP  Sector 42 Position 13 - Def. no of copies       *
.*                  CURSESS   The current session - DATE/TIME/CLINIC          *
.*                  Current OPRDEAFD & OPRDEBFD & PMI records                 *
.*                                                                            *
.* Modifications :                                                            *
.*        V11.03.01 06/03/2023  Ebon Clements     TSK 0909393                 *
.*                  Added hospital to oprsesaf(1-6) and oprdetfa(1&5) indexes *
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.*        V5.08.01  01/09/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description                                           *
.******************************************************************************
.*     V5.07.00     14/10/98 Jim Stathopoulos                                 *
.*                           507 Changes                                      *
.******************************************************************************
.*                  07/05/91 Graeme Williams                                  *
.*                           Changes scrub nurse from "SC" to "SN" for SAHB   *
.*                  13/09/91 Graeme Williams           SRF 109633 (Quote 7075)*
.*                           Print description instead of code for staff for  *
.*                           S.A.H.B.                                         *
.******************************************************************************
.
OPPRIREC  BRANCH    OPCNPREC,PRREC100
.
.         format for Wellington : OPCNPREC = 0
.
PRREC000  CALL      WELLN000                * print records for Wellington
          GOTO      PRREC999
.
.         format for Southland  : OPCNPREC = 1
.
PRREC100  CALL      SOUTH000                * print records for Southland
          GOTO      PRREC999
.
PRREC999  RETURN
+
+
.********************************************************************
.*                  WELLN000                   Called by : PRREC000 *
.*        Print the operation records for Wellington                *
.*        (will be format type A)                                   *
.********************************************************************
.
.         The number of copies to be printed will default to the system
.         parameter OPCNNCOP.
.
WELLN000  MOVE      OPCNNCOP,FORM3
          DISPLAY   *P1:24,*EL,"Input the number of copies to be printed : "
          KEYIN     *P44:24,*DV,FORM3:
                    *P44:24,*V2LON,*RV,FORM3:
                    *P44:24,*DV,FORM3
.
          COMPARE   ZERO,FORM3
          GOTO      WELLN999 IF EQUAL       * None wanted
          GOTO      WELLN000 IF LESS        * negative entered
.
.         print the required copies
.
WELLN200  CALL      HEADA000                * print header details
          CALL      BODYS000                * print operation report
.
.         Check if we have printed enough copies yet.
.
          SUB       ONE,FORM3
          COMPARE   ONE,FORM3               * Printed all our copies yet ?
          GOTO      WELLN200 IF NOT LESS    * No. Print another copy
.
WELLN999  RETURN
+
.********************************************************************
.*                  SOUTH000                   Called by : PRREC100 *
.*        Print the operation records for Southland                 *
.*        (will be format type B)                                   *
.********************************************************************
.
.         Check if usage details have been input yet.
.
SOUTH000  CALL      HAVEU000                * Do we have usage details yet ?
          BRANCH    EXIT,SOUTH999           * No.
.
.         The number of copies to be printed will default to the system
.         parameter OPCNNCOP.
.
SOUTH100  MOVE      OPCNNCOP,FORM3
          DISPLAY   *P1:24,*EL,"Input the number of copies to be printed : "
          KEYIN     *P44:24,*DV,FORM3:
                    *P44:24,*V2LON,*RV,FORM3:
                    *P44:24,*DV,FORM3
.
          COMPARE   ZERO,FORM3
          GOTO      SOUTH999 IF EQUAL       * None wanted
          GOTO      SOUTH100 IF LESS        * negative entered
.
.         print the required copies
.
SOUTH200  CALL      HEADB000                * print header details
          CALL      BODYS000                * print operation report
.
.         Check if we have printed enough copies yet.
.
          SUB       ONE,FORM3
          COMPARE   ONE,FORM3                * Printed all our copies yet ?
          GOTO      SOUTH200 IF NOT LESS     * No. Print another copy
.
SOUTH999  RETURN
.
.*********************************************************************
.*                  HAVEU000                    Called by : SOUTH000 *
.*        Check if this patient has usage details yet.               *
.*********************************************************************
.
HAVEU000  PACK      KEY11,OPDAUNIQ,OPDBTEAM
          CALL      RDOPDEC1                 * get operation details
          COMPARE   ZERO,OVRCD
          GOTO      HAVEU900 IF EQUAL        * we have operation details
.
          DISPLAY   *P1:24,*EL,"This patient has no usage details yet. ";
          CALL      HITENTER
          MOVE      ONE,EXIT
          GOTO      HAVEU999
.
HAVEU900  MOVE      ZERO,EXIT
.
HAVEU999  RETURN
+
.*********************************************************************
.*                  HEADA000                    Called by : WELLN200 *
.*        Print the Header details for Wellington format stationary  *
.*********************************************************************
.
.         Print the patients name, UR number and operation date
.
HEADA000  MOVE      PSNAME,DESC15           * Shorten the patients surname
          MOVE      PGNAME,HOMEPHON         * Shorten the patients given names
          UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * session date
          PRINT     *F,*N,*43,DESC15,SP1,HOMEPHON,SP1,PURNO:
                    *N,*30,CPCDATE
.
.         Print the attending doctor, date of birth, age, sex and ward.
.
          PACK      KEY6,ADOCTA,SP6
          CALL      GDOCD000
          MOVE      DESC20,HOMEPHON         * attending doctor
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * date of birth
          PRINT     *N,*43,HOMEPHON,SP1,CPCDATE,SP1,PSAGE,SP2,PSEX,SP4,AWARD
.
.         Print the details of the staff who were in the theatre.
.
          CALL      STFFA000                * Print staff details
.
.         Print the sessions start and end times
.
          MOVE      CURSESS,KEY22           * get current session details
          CALL      RDOPSES1                * Read in the session record
          PRINT     *N,*N,*24,OPSEACTS,*56,OPSEACTE
          PRINT     *N,*N,*N,*N,*N,*N
.
HEADA999  RETURN
+
.*********************************************************************
.*                  STFFA000                    Called by : HEADA000 *
.*        Print staff personnel and names for Wellington format      *
.*********************************************************************
.
. ------------------------------------------------------
. ------- get surgeon anaesthetist and assistant -------
. ------------------------------------------------------
.
STFFA000  MOVE      ZERO,FORM1         * doctor counter
          MOVE      ZERO,FORM2         * assistant counter
.
.         Because of problems with the data area in the IBAOPR43, the variables
.         used in this routine are not obvious. What they all mean are:
.
.         FORM1      =   Counter for loop over the doctor and nurse codes
.
.         Doctors:
.
.         OPERSURG   =   Operating surgeon code
.         NEWSCODE   =   Anaesthetist code
.         PREF01     =   1st assistant surgeon code
.         PREF02     =   2nd assistant surgeon code
.         PREF03     =   3rd assistant surgeon code
.         PREF04     =   4th assistant surgeon code
.
.         Nurses:
.
.         NURSCODE   =   Scrub nurse code
.         DOCTCODE   =   Circulating nurse code
.
          UNPACK    SP30,OPERSURG,NEWSCODE
          UNPACK    SP20,PREF01,PREF02
          UNPACK    SP20,PREF03,PREF04
.
STFFA222  ADD       ONE,FORM1
          LOAD      ACODE,FORM1,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                                OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
          MATCH     SP3,ACODE          * test for spaces
          GOTO      STFFA444 IF EQUAL
.
          PACK      KEY5,ANSO,ANSP,ACODE
          CALL      RDCODE1            * get indicator
          BRANCH    OVRCD,STFFA444
.
.         check for surgeon
.
          CMATCH    ANSS,TCINDC1       * test if this is the surgeon
          GOTO      STFFA333 IF NOT EQUAL
.
          LOAD      OPERSURG,FORM1,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                                   OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
          GOTO      STFFA444
.
.         check for anaesthetist
.
STFFA333  CMATCH    ANSN,TCINDC1       * test for the anaesthetist
          GOTO      STFFA366 IF NOT EQUAL
.
          LOAD      NEWSCODE,FORM1,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                                   OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
          GOTO      STFFA444
.
.         check for assistants
.
STFFA366  CMATCH    ANSA,TCINDC1       * test for assistants
          GOTO      STFFA444 IF NOT EQUAL
.
          LOAD      DOCTCODE,FORM1,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                                   OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
          ADD       ONE,FORM2
          STORE     DOCTCODE,FORM2,PREF01,PREF02,PREF03,PREF04
.
STFFA444  COMPARE   EIGHT,FORM1        * test end of list
          GOTO      STFFA222 IF LESS
.
.         We have the doctor codes sorted out, now print the doctors names.
.
          PACK      KEY6,OPERSURG      * get surgeon name
          CALL      GDOCD000
          PRINT     *N,*14,DESC20;
          PACK      KEY6,NEWSCODE      * get anaesthetist name
          CALL      GDOCD000
          PRINT     *45,DESC20,*71,OPDAANAE
.
          PACK      KEY6,PREF01       * get surgeon assistant 1 name
          CALL      GDOCD000
          PRINT     *N,*14,DESC20;
          PACK      KEY6,PREF02       * get surgeon assistant 2 name
          CALL      GDOCD000
          PRINT     *45,DESC20
          PACK      KEY6,PREF03       * get surgeon assistant 3 name
          CALL      GDOCD000
          PRINT     *N,*14,DESC20;
          PACK      KEY6,PREF04       * get surgeon assistant 4 name
          CALL      GDOCD000
          PRINT     *45,DESC20
.
. ------------------------------------------------
. ------- get scrub and circulating nurses -------
. ------------------------------------------------
.
          MOVE      ZERO,FORM1
          UNPACK    SP20,NURSCODE,DOCTCODE
.
STFFA666  ADD       ONE,FORM1
          LOAD      ACODE,FORM1,OPDBTYN1,OPDBTYN2,OPDBTYN3,OPDBTYN4,OPDBTYN5:
                                OPDBTYN6
          MATCH     SP3,ACODE          * test for spaces
          GOTO      STFFA700 IF EQUAL
.
          PACK      KEY5,ANSO,ANSN,ACODE
          CALL      RDCODE1            * get indicator
          BRANCH    OVRCD,STFFA700
.
.         check for scrub nurse
.
          CMATCH    ANSS,TCINDC1       * test scrub nurse
          GOTO      STFFA699 IF NOT EQUAL
.
          LOAD      NURSCODE,FORM1,OPDBNUC1,OPDBNUC2,OPDBNUC3,OPDBNUC4,OPDBNUC5:
                                   OPDBNUC6
          GOTO      STFFA700
.
.         test for circulating nurse
.
STFFA699  CMATCH    ANSC,TCINDC1       * test for circulating nurse
          GOTO      STFFA700 IF NOT EQUAL
.
          LOAD      DOCTCODE,FORM1,OPDBNUC1,OPDBNUC2,OPDBNUC3,OPDBNUC4,OPDBNUC5:
                                   OPDBNUC6
.
STFFA700  COMPARE   SIX,FORM1         * test end of list
          GOTO      STFFA666 IF LESS
.
.         We have the nurse codes, now print the nurse names.
.
          PACK      KEY6,NURSCODE,SP6
          CALL      GDOCD111
          PRINT     *N,*N,*18,DESC20;
          PACK      KEY6,DOCTCODE,SP6
          CALL      GDOCD111
          PRINT     *53,DESC20
.
STFFA999  RETURN
+
.*********************************************************************
.*                  GDOCD000                                         *
.*        This routine gets the staff name from either the doctors or*
.*        operating nurse file                                       *
.*        Para's  : KEY6          key to doct or nurse file          *
.*        Returns : DESC20        name formatted as I. Surname       *
.*********************************************************************
GDOCD000  MOVE      SP30,DESC20
          CALL      RDDOCT1            * read doctors file
          BRANCH    OVRCD,GDOCD999
          GOTO      GDOCD777           * consider it a CALL
.
.         get name from nurses file
.
GDOCD111  MOVE      SP30,DESC20
          CALL      RDOPNUR1
          BRANCH    OVRCD,GDOCD999
          MOVE      OPNRSNAM,DSNAME         * set surname
          MOVE      OPNRGNAM,DGNAME         * set given name
.
.         set up name into desc20
.
GDOCD777  MOVE      DGNAME,ANS              * get initial
          PACK      DESC20,ANS,DOT,DSNAME   * set up name
          RESET     DESC20
.
GDOCD999  RETURN
+
.*********************************************************************
.*                  HDTPB000                    Called by : HEADB000 *
.*        Print basic header details for Southland format   BODYS000 *
.*********************************************************************
HDTPB000  PRINT     *F,*N,*N,*N,*N
.
.         print surname and UR number
.
          PRINT     *N,*9,"Surname       : ",PSNAME:
                    *53,"UR No        : ",PURNO;
.
.         print Given Name and admission number
.
          PRINT     *N,*9,"Given         : ",PGNAME:
                    *53,"Admission No : ",AADMNO
.
HDTPB999  RETURN
.*********************************************************************
.*                  HEADB000                    Called by : SOUTH200 *
.*        Print header details for Southland format                  *
.*********************************************************************
HEADB000  CALL      HDTPB000
.
.         print date of birth and age
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          PRINT     *9,"D.O.B.        : ",CPCDATE:
                    *53,"Age          : ",PSAGE;
.
.         print sex and ward
.
          MOVE      SP20,SESS01             * clear value
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,SESS01
.* ******************************************   End of Changes         *C-49016
.
HEADB100  PRINT     *N,*9,"Sex           : ",SESS01:
                    *53,"Ward         : ",AWARD;
.
.         print session date and time
.
          UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          PRINT     *N,*9,"Session Date  : ",CPCDATE:
                    *53,"Session Time : ",OPDATIME;
.
.         print surgeon and assist surgeon
.
          CALL      STFFB000                * print staff details
.
.         print anaesthetic start time and anaesthetic end time
.
          PRINT     *N,*9,"Anaes. Start  : ",OPDAPRE3:
                    *53,"Anaes. End   : ",OPDAPOS1;
.
.         print surgery start time and surgery end time
.
          PACK      KEY11,OPDAUNIQ,OPDBTEAM
          CALL      RDOPDEC1                * get operation details
          PRINT     *N,*9,"Surgery Start : ",OPDCOTM1:
                    *53,"Surgery End  : ",OPDCOTM2;
.
.         print operation description
.  
          PRINT     *N,*9,"Operation : ",OPDADES1:
                    *N,*21,OPDADES2:
                    *N,*21,OPDADES3,*N,*N
.
HEADB999  RETURN
+
.*********************************************************************
.*                  BODYS000                    Called by : WELLN200 *
.*        Print operation report for each format type       SOUTH200 *
.*********************************************************************
BODYS000  MOVE      ZERO,CLNO               * zero the line counter
          PACK      KEY14,OPDAUNIQ,OPDBTEAM,SP20
          CALL      RSOPREC1                * read all text from records file
.
.         loop over comments file
.
BODYS444  CALL      RKOPREC1                * next read on comments file
          BRANCH    OVRCD,BODYS999          * end of file
.
          MATCH     OPDAUNIQ,OPORUNIQ
          GOTO      BODYS999 IF NOT EQUAL   * different operation
.
          MATCH     OPDBTEAM,OPORTEAM
          GOTO      BODYS999 IF NOT EQUAL   * different team
.
          MOVE      "8",FORM2               * set left margin
          LOAD      FORM2,OPCNPREC,NINE     * set margin
          PRINT     *FORM2,OPORLINE         * margin is "form2" spaces into page
          ADD       ONE,CLNO 
.
          COMPARE   FORTY,CLNO
          GOTO      BODYS444 IF LESS        * dont need a new page
.
.         print new heading depending upon sys. parameter
.
          BRANCH    OPCNPREC,BODYS510      
.
          CALL      HEADA000                * print page heading for WELLINGTON
          MOVE      ZERO,CLNO               * zero the line counter for new page
          GOTO      BODYS444
.
BODYS510  CALL      HDTPB000                * print page heading for SOUTHLAND
          MOVE      ZERO,CLNO               * zero the line counter for new page
          GOTO      BODYS444
.
BODYS999  RETURN
+
.*********************************************************************
.*                  STFFB000                    Called by : HEADB000 *
.*        Print staff personnel and names for Southland format       *
.*********************************************************************
.
.         Because of problems with the data area in the IBAOPR43, the variables
.         used in this routine are not obvious. What they all mean are:
.
.         Doctors:
.         OPERSURG   =   Operating surgeon code
.         PREF01     =   Assistant surgeon code
.         NEWSCODE   =   Anaesthetist code
.         PREF02     =   Supervising anaesthetist code
.
.         Nurses:
.         NURSCODE   =   Scrub nurse code
.         DOCTCODE   =   Circulating nurse code
.
. -------------------------------------------------------
. ------- get surgeon anaesthetist and assistants -------
. -------------------------------------------------------
.
STFFB000  MOVE      ZERO,FORM1         * doctor counter
          UNPACK    SP30,OPERSURG,NEWSCODE
          UNPACK    SP20,PREF01,PREF02
.
. ------- loop over the doctor types -------
.
STFFB200  ADD       ONE,FORM1               * get next type
          MOVE      SP3,ACODE               * set default value
          LOAD      ACODE,FORM1,OPDBTYD1,OPDBTYD2,OPDBTYD3,OPDBTYD4:
                                OPDBTYD5,OPDBTYD6,OPDBTYD7,OPDBTYD8
.
          MATCH     SP3,ACODE
          GOTO      STFFB450 IF EQUAL       * no code, check if any more 
.
          PACK      KEY5,ANSO,ANSP,ACODE
          CALL      RDCODE1                 * check if valid code
          BRANCH    OVRCD,STFFB450          * invalid code, check if any more
.
.         check if a surgeon
.
          PACK      DIM3,ANSS,SP2           * code for surgeon
          MATCH     DIM3,ACODE              * test if this is the surgeon
          GOTO      STFFB300 IF NOT EQUAL   * not a surgeon
.
.         operating surgeon code
.
          LOAD      OPERSURG,FORM1,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                                   OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
          GOTO      STFFB450                * check if any more codes to get
.
.         check if an anaesthetist
.
STFFB300  PACK      DIM3,ANSA,SP2           * set for anaesthetist
          MATCH     DIM3,ACODE              * test for the anaesthetist
          GOTO      STFFB350 IF NOT EQUAL   * not an anaesthetist
.
.         anaesthetist code
.
          LOAD      NEWSCODE,FORM1,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                                   OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
          GOTO      STFFB450                * check if any more codes to get
.
.         check if an assistant surgeon
.
STFFB350  PACK      DIM3,ANSA,ANSS,SP1      * set for ass. surgeon
          MATCH     DIM3,ACODE              * test for assistants
          GOTO      STFFB400 IF NOT EQUAL   * not assistant surgeon
.
          LOAD      PREF01,FORM1,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                                 OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
          GOTO      STFFB450                * check if any more codes to get
.
.         check if an assistant anaesthetist
.
STFFB400  PACK      DIM3,ANSS,ANSA,SP1      * set for ass. anaesthetist
          MATCH     DIM3,ACODE              * test for assistants
          GOTO      STFFB450 IF NOT EQUAL   * not assistant anaesthetist
.
          LOAD      PREF02,FORM1,OPDBDCC1,OPDBDCC2,OPDBDCC3,OPDBDCC4:
                                 OPDBDCC5,OPDBDCC6,OPDBDCC7,OPDBDCC8
.
.         see if any more doctor types to check
.
STFFB450  COMPARE   EIGHT,FORM1             * test end of list
          GOTO      STFFB200 IF LESS        * more to search for
.
. ------------------------------------------------
. ------- get scrub and circulating nurses -------
. ------------------------------------------------
.
STFFB500  MOVE      ZERO,FORM1              * nurse counter
          UNPACK    SP20,NURSCODE,DOCTCODE
.
. ------- loop over nurse types -------
.
STFFB600  ADD       ONE,FORM1               * increment counter
          MOVE      SP3,ACODE               * set default value
          LOAD      ACODE,FORM1,OPDBTYN1,OPDBTYN2,OPDBTYN3,OPDBTYN4,OPDBTYN5:
                                OPDBTYN6
.
          MATCH     SP3,ACODE
          GOTO      STFFB700 IF EQUAL       * no code, check if any more
.
          PACK      KEY5,ANSO,ANSN,ACODE
          CALL      RDCODE1                 * validate code
          BRANCH    OVRCD,STFFB700          * invalid code, check if any more
.
.         check for Scrub nurse
.
          PACK      DIM3,ANSS,ANSN,SP1      * set for scrub nurse
          MATCH     DIM3,ACODE 
          GOTO      STFFB650 IF NOT EQUAL   * not a scrub nurse
.
.         set up scrub nurse
.
          LOAD      NURSCODE,FORM1,OPDBNUC1,OPDBNUC2,OPDBNUC3,OPDBNUC4,OPDBNUC5:
                                   OPDBNUC6
          GOTO      STFFB700
.
.         check for Circulating nurse
.
STFFB650  PACK      DIM3,ANSC,ANSN,SP1      * set for circ nurse
          MATCH     DIM3,ACODE 
          GOTO      STFFB700 IF NOT EQUAL   * not a circ nurse
.
.         set up circulating nurse
.
          LOAD      DOCTCODE,FORM1,OPDBNUC1,OPDBNUC2,OPDBNUC3,OPDBNUC4,OPDBNUC5:
                                   OPDBNUC6
.
.         see if any more codes to check
.
STFFB700  COMPARE   SIX,FORM1
          GOTO      STFFB600 IF LESS        * still more to check
.
. --------------------------------
. ------- print the values -------
. --------------------------------
.
          MOVE      PREF01,KEY6
          CALL      DOCDB000                * get the doctors description
          MOVE      DESC27,DESC10           * only 10 character description
.
          MOVE      OPERSURG,KEY6
          CALL      DOCDB000                * get the doctors description
.
          PRINT     *N,*9,"Surgeon       : ",DESC27:
                    *53,"Ass. Surgeon : ",DESC10
.
          MOVE      PREF02,KEY6
          CALL      DOCDB000                * get the doctors description
          MOVE      DESC27,DESC10           * only 10 character description
.
          MOVE      NEWSCODE,KEY6
          CALL      DOCDB000                * get the doctors description
.
          PRINT     *9,"Anaesthetist  : ",DESC27:
                    *53,"Sup. Anaes.  : ",DESC10
.
          MOVE      DOCTCODE,KEY6
          CALL      NURDB000                * get the doctors description
          MOVE      DESC27,DESC10           * only 10 character description
.
          MOVE      NURSCODE,KEY6
          CALL      NURDB000                * get the doctors description
.
          PRINT     *9,"Scrub Nurse   : ",DESC27:
                    *53,"Circ. Nurse  : ",DESC10;
.
STFFB999  RETURN
+
.*********************************************************************
.*                  DOCDB000                    Called by : STFFB000 *
.*        Get the name for a doctor                                  *
.*********************************************************************
DOCDB000  MOVE      SP30,DESC27             * initialise description
          CALL      RDDOCT1                 * get the doctor record
          BRANCH    OVRCD,DOCDB999          * no record
.
          MOVE      DSNAME,PACSNAME         * surname
          MOVE      DGNAME,PACGNAME         * given name
          MOVE      SP10,PACTITLE           * title
          MOVE      TWO,PACFRMT             * surname, given name sequence
          CALL      PACNAME
.
          MOVE      PACFNAME,DESC27
.
DOCDB999  RETURN
.*********************************************************************
.*                  NURDB000                    Called by : STFFB000 *
.*        Get the name for a nurse                                   *
.*********************************************************************
NURDB000  MOVE      SP30,DESC27             * initialise description
          CALL      RDOPNUR1                * get the nurse record
          BRANCH    OVRCD,NURDB999          * no record
.
          MOVE      OPNRSNAM,PACSNAME       * surname
          MOVE      OPNRGNAM,PACGNAME       * given name
          MOVE      SP10,PACTITLE           * title
          MOVE      TWO,PACFRMT             * surname, given name sequence
          CALL      PACNAME
.
          MOVE      PACFNAME,DESC27
.
NURDB999  RETURN
