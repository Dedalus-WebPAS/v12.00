.
.       PATITMDS : Display Surgical Items Entries
.
.       This include is used to display various options available, using
.       whatever file it needs, along with any special parameter supplied
.       by the calling program.
.
.     Modifications:
.         V5.07.01  13/07/1999  Greg Horvat
.                   Replaced certain variables & with KEY variables
.                    28/11/89 Graeme Williams
.                    Added the parameter CNEWDS to determine if the include
.                    is to use the new standards for "?" routines
.
.        Subroutine to display the available surgical items
.
PATITMDS  MOVE       FIVE,CVERT
          MOVE       ONE,CCOL
          RESET      DIM1A
          DISPLAY    *P1:3,*EF,*P20:4,*V2LON,*ULON,"SURGICAL ITEMS"
.
          PACK       KEY9 WITH DIM1A,SP9
          CALL       RDSITEM1
.
.         Loop over file
.
ITEMLP1   CALL       RDKITEM1
          BRANCH     OVRCD OF ENDITEM1
.
          CMATCH     SP1,DIM1A
          GOTO       ITEMDS1 IF EQUAL
.
          CMATCH     DIM1A,ITEMNO
          GOTO       ENDITEM1 IF NOT EQUAL
.
ITEMDS1   MOVE       IDESC,KEY20
          DISPLAY    *PCCOL:CVERT,*V2LON,ITEMNO,SP2,*HOFF,KEY20
          ADD        ONE,CVERT
          COMPARE    TWENTY4,CVERT
          GOTO       ITEMLP1 IF LESS
.
          MOVE       FIVE,CVERT
          ADD        "40",CCOL
          COMPARE    "70",CCOL
          GOTO       ITEMLP1 IF LESS
.
.         Check for new standards question, or old question
.
          BRANCH    CNEWDS,DSITM200
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSE,ANS
          GOTO      ENDITM2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATITMDS IF EQUAL
.
          GOTO      DSITM400
.
.         Use the new standards question
.
DSITM200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSC,ANS
          GOTO      ENDITM2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATITMDS IF EQUAL
.
DSITM400  MOVE       FIVE,CVERT
          MOVE       ONE,CCOL
          DISPLAY    *P1:5,*EF
.
          GOTO       ITEMLP1
.
.      End of rates
.
ENDITEM1  BRANCH     CNEWDS,ENDITM2
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
ENDITM2   RETURN
