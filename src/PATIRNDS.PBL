+
.******************************************************************************
.
.         PATIRNDS : Display Invoice Ranges
.
.     Modifications: 28/11/89 Graeme Williams
.                    Added the parameter CNEWDS to determine if the include
.                    is to use the new standards for "?" routines
.                    11/03/94 Ian Rutt    
.                    Renamed to PATIRNDS for standards
.
.------------------------------------------------------------------------------
.
PATIRNDS  DISPLAY   *P1:3,*EF,*P20:4,*V2LON,*ULON,"INVOICE RANGE "
          MOVE      FIVE,CVERT
.
          PACK      KEY3,SP3
          CALL      RDSIRNG1
PDIR1000  CALL      RDKIRNG1
          BRANCH    OVRCD,PDIR9998
.
          MOVE      IRDESC,DIM30
          DISPLAY   *P1:CVERT,*V2LON,IRANGE,*HOFF,SP1,DIM30
.
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      PDIR1000 IF LESS
.
.         Check for new standards question, or old question
.
          BRANCH    CNEWDS,DSIRN200
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSE,ANS
          GOTO      PDIR9999 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATIRNDS IF EQUAL
.
          GOTO      DSIRN400
.
.         Use the new standards question
.
DSIRN200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSC,ANS
          GOTO      PDIR9999 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATIRNDS IF EQUAL
.
DSIRN400  MOVE      FIVE,CVERT
          DISPLAY   *P1:CVERT,*EF
          GOTO      PDIR1000
.
.         End of codes
.
PDIR9998  BRANCH    CNEWDS,PDIR9999
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
PDIR9999  MOVE      ZERO,OVRCD
          RETURN
.
