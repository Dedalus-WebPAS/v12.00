.----------------------------------------------------------------------
.    File          : PMSDBINV.PBL
.    
.    Function      : Include to Debtors Invoices for WEB
.
.    Modifications : 
.                    V10.14.01 20/03/2019  J.Tan            TSK 0857392
.                    Changed RCP Transaction count from DIM3 to DIM5
.                    V10.00.01 22/09/2010 J.Tan      CAR 230162
.                    Mods to display Insurance code for payment
.                    V9.11.02  06/03/2009 J.Tan      CAR 188205
.                    Mods to print 'Non-bank deposit' as 'Payment recieved'
.                    V9.11.01 04/08/2005  J.Tan        CAR 62750
.                    Mods not to use the redefined amount variables 
.                    V9.03.03 21/04/2004 J.Tan  
.                    Fixed non-banked amount for two same invoice in one receipt
.                    V9.03.02 03/02/2004 J.Tan   CAR 45047
.                    Mods to display Non-banked Cash total
.
.----------------------------------------------------------------------
DEBINV00  BRANCH    F1,DEBINV08
          WRITE     HTMLFILE;"<tr><td align=right>Debtor Number":
                             "<b>",DBFDDEB,"</b></td></tr>"
          GOTO      DEBINV99
.
DEBINV08  MOVE      ZERO,TOTINV
          MOVE      ZERO,TGSTEXCV
          MOVE      ZERO,TGSTAMNT
          MOVE      ZERO,CPAGENO
          MOVE      ZERO,TOTPAID     * payments
          MOVE      ZERO,PINVJOUR    * Debit Adjustment Journals
          MOVE      ZERO,JOURFLAG
          MOVE      ZERO,PAYMFLAG
.
          WRITE     HTMLFILE;"<tr>":
                            "<td class=HeadingCell>Date</td>":
                            "<td class=HeadingCell>Description</td>":
                            "<td class=HeadingCell>Quantity</td>":
                            "<td align=right class=HeadingCell>Price</td>":
                            "<td align=right class=HeadingCell>GST Exc.</td>":
                            "<td align=right class=HeadingCell>GST Amount</td>":
                            "<td align=right class=HeadingCell>Amount</td>":
                            "</tr>"
.
          MOVE      ZERO,FORM2
          MOVE      INVOICEN,KEY12
          REP       " 0",KEY12
          MOVE      "1",DBFDDTY
          MOVE      ZERO,SDBTYPE         * 1-invoice, 2-payment, 3-Journal
          PACK      KEY36,DBDBDEB,KEY12,DBFDDTY,SP70
          CALL      RSDBFD6
DEBINV10  CALL      RKDBFD6
          BRANCH    OVRCD,DEBINV80       * End of file
.
          MATCH     DBDBDEB,DBFDDEB
          GOTO      DEBINV80 IF NOT EQUAL   * Not same debtor
.
          MATCH     KEY12,DBFDINV
          GOTO      DEBINV80 IF NOT EQUAL   * Not same invoice 
.
          MOVE      SP70,KEY28
          COMPARE   ZERO,SDBTYPE
          GOTO      DEBINV18 IF EQUAL    * First record
.
          MOVE      DBFDDTY,KEY1
          REP       "324353",KEY1
          MOVE      ZERO,FORM1
          MOVE      KEY1,FORM1
          COMPARE   SDBTYPE,FORM1        * still same type?
          GOTO      DEBINV18 IF EQUAL
.
.         Payment or journal records are found,
.           display new heading if this is the first one.
.
          BRANCH    SDBTYPE,DEBINV12,DEBINV14
          GOTO      DEBINV18
.
DEBINV12  CALL      EINV0000             * Display end of invoice
.
          WRITE     HTMLFILE;"<tr>":
                            "<td class=HeadingCell>Date</td>":
                            "<td class=HeadingCell>Description</td>":
                            "<td class=HeadingCell>&nbsp;</td>":
                            "<td align=right class=HeadingCell>&nbsp;</td>":
                            "<td align=right class=HeadingCell>&nbsp;</td>":
                            "<td align=right class=HeadingCell>&nbsp;</td>":
                            "<td align=right class=HeadingCell>Amount</td>":
                            "</tr>"
          GOTO      DEBINV18
.
DEBINV14  CALL      PAYT0000       * print payment total
          GOTO      DEBINV18
.
DEBINV18  MOVE      ZERO,FORM1
          MOVE      DBFDDTY,FORM1
          BRANCH    FORM1,DEBINV20,DEBINV30,DEBINV32,DEBINV40,DEBINV50
          GOTO      DEBINV10
.
DEBINV20  PACK      KEY8,DBFDITM
          CALL      RDDBIT1        * Read Sales Item Details
          MOVE      DBITIDE,KEY28
.
          MOVE      "9999.99",DIM7
          MOVE      DBFDQTY,F4P2
          FEDIT     F4P2,DIM7
.
          MOVE      "999,999.99",DIM10A
          MOVE      DBFDPRI,F12P2
          FEDIT     F12P2,DIM10A
.
          MOVE      "999,999,999.99",DIM14A
          FEDIT     DBFDTOT,DIM14A
.
          MOVE      "999,999,999.99",DIM14B
          FEDIT     DBFDTAX,DIM14B
.
          MOVE      "99,999,999.99",TOTDIM13
          ASSIGN    DBFDTAX+DBFDTOT,F12P2
          FEDIT     F12P2,TOTDIM13
          IF        F12P2=0
            MOVE       SP70,TOTDIM13
          ENDIF
          ADD       F12P2,TOTINV
          ADD       DBFDTOT,TGSTEXCV
          ADD       DBFDTAX,TGSTAMNT
          GOTO      DEBINV60
.
DEBINV30  MOVE      "Credit ",KEY28
          MULT      "-1",DBFDPAID
          ADD       DBFDPAID,TOTPAID     * payments
          GOTO      DEBINV55
.
DEBINV32  MOVE      "Payment",KEY28
          MULT      "-1",DBFDPAID
          ADD       DBFDPAID,TOTPAID     * payments
          GOTO      DEBINV55
.
DEBINV40  MOVE      "Debit Adjustment",KEY28
          ADD       DBFDPAID,PINVJOUR    * Debit Adjustment Journals
          GOTO      DEBINV55
.
DEBINV50  MOVE      "Credit Adjustment",KEY28
          MULT      "-1",DBFDPAID
          ADD       DBFDPAID,PINVJOUR    * Credit Adjustment Journals
.
DEBINV55  UNPACK    SP70,DBITUNT,DIM10A,DIM14A,DIM14B,DIM7
.
          MOVE      "99,999,999.99",TOTDIM13
          ASSIGN    DBFDPAID,F12P2
          FEDIT     F12P2,TOTDIM13
          IF        F12P2=0
            MOVE       SP70,TOTDIM13
          ENDIF
.
DEBINV60  UNPACK    DBFDSDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
          WRITE     HTMLFILE;"<tr>":
                             "<td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                             "<td>&nbsp;",KEY28,"</td>":
                             "<td>&nbsp;",DIM7," ",DBITUNT,"</td>":
                             "<td align=right>&nbsp;",DIM10A,"</td>":
                             "<td align=right>&nbsp;",DIM14A,"</td>":
                             "<td align=right>&nbsp;",DIM14B,"</td>":
                             "<td align=right>",TOTDIM13,"</td></tr>"
          MOVE      DBFDDTY,KEY1
          REP       "324353",KEY1
          MOVE      KEY1,SDBTYPE
          GOTO      DEBINV10
.
DEBINV80  COMPARE   ONE,SDBTYPE
          GOTO      DEBINV82 IF NOT EQUAL
.
          CALL      EINV0000      * Display end of invoice, no payment/journal
          MOVE      INVOICEN,KEY12
          REP       " 0",KEY12
          MOVE      ONE,FORM1      * to check amount only
          CALL      CHKC0000             * Get cash pending for the invoice
          MOVE      INVOICEN,KEY12
          REP       " 0",KEY12
          COMPARE   ZERO,CASHTOT
          GOTO      DEBINV82 IF NOT EQUAL    * found a non-bank cash
.
          GOTO      DEBINV99
.
DEBINV82  CALL      PAYT0000      * display payment total
          CALL      JRNT0000      * display journal total
.
          MOVE      TOTINV,F12P2
          ADD       TOTPAID,F12P2
          ADD       PINVJOUR,F12P2
          MOVE      "999,999,999.99",DIM14GRN
          FEDIT     F12P2,DIM14GRN
.
          WRITE     HTMLFILE;"<tr><td>&nbsp;</td>":
                             "<td><b>Balance Payable</b></td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>";
.
          IF        F12P2<0
            WRITE     HTMLFILE;"<td align=right><font color=#FF0000><b>":
                               DIM14GRN,"</font></b></td></tr>"
          ELSE
            WRITE     HTMLFILE;"<td align=right><font color=#0000FF><b>":
                               DIM14GRN,"</font></b></td></tr>"
          ENDIF
.
DEBINV99  RETURN
.
.------------------------------------------------------------
.         Display end of invoice
.------------------------------------------------------------
EINV0000  MOVE      "999,999,999.99",DIM14
          FEDIT     TGSTEXCV,DIM14
          MOVE      "999,999,999.99",DIM14TAX
          FEDIT     TGSTAMNT,DIM14TAX
          MOVE      "999,999,999.99",DIM14GRN
          FEDIT     TOTINV,DIM14GRN
.
          WRITE      HTMLFILE;"<tr><td>&nbsp;</td>":
                               "<td><b>Invoice Total</b></td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td align=right><b>",DIM14,"</b></td>":
                               "<td align=right><b>",DIM14TAX,"</b></td>":
                               "<td align=right><b>",DIM14GRN,"</b></td></tr>"
EINV9999  RETURN
+
.
.------------------------------------------------------------
.         Display Payment total
.------------------------------------------------------------
PAYT0000  BRANCH    PAYMFLAG,PAYT9999
          COMPARE   ZERO,TOTPAID
          GOTO      PAYT2000 IF EQUAL
          MOVE      "999,999,999.99",DIM14
          FEDIT     TOTPAID,DIM14
.
          WRITE     HTMLFILE;"<tr><td>&nbsp;</td>":
                               "<td><b>Payment Total</b></td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td align=right><b>",DIM14,"</b></td></tr>"
          MOVE      ONE,PAYMFLAG
.
PAYT2000  MOVE      INVOICEN,KEY12
          REP       " 0",KEY12
          MOVE      ZERO,FORM1
          CALL      CHKC0000             * Get cash pending for the invoice
          MOVE      INVOICEN,KEY12
          REP       " 0",KEY12
.
PAYT9999  RETURN
+
.------------------------------------------------------------
.         Display Journal total
.------------------------------------------------------------
JRNT0000  BRANCH    JOURFLAG,JRNT9999
          COMPARE   ZERO,PINVJOUR
          GOTO      JRNT9999 IF EQUAL
          MOVE      "999,999,999.99",DIM14
          FEDIT     PINVJOUR,DIM14
.
          WRITE      HTMLFILE;"<tr><td>&nbsp;</td>":
                              "<td><b>Journal Total</b></td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td align=right><b>",DIM14,"</b></td></tr>"
          MOVE       ONE,JOURFLAG
JRNT9999  RETURN
+
.*******************************************************************************
.         Display Cash pending total
.*******************************************************************************
CHKC0000  MOVE      ZERO,CASHTOT
          MOVE      KEY12,SAVKEY12     * saved variable for invoice number
          OPEN      RCPDTEA3,"rcpdteaf"
          OPEN      RCPBNKA1,"rcpbnkaf"
          PACK      KEY29,SAVKEY12,SP70
          CALL      RSRCDTE3
CHKC1000  CALL      RKRCDTE3
          BRANCH    OVRCD,CHKC8000
.
          MATCH     SAVKEY12,RCDTINVN       * Same invoice number?
          GOTO      CHKC8000 IF NOT EQUAL
.
          PACK      KEY12,RCDTRECN,SP70
          CALL      RDRCBNK1
          BRANCH    OVRCD,CHKC1000
.
          MATCH     "0",RCBNSTAT        * Cash Not banked?
          GOTO      CHKC1000 IF NOT EQUAL
.
          MULT      "-1",RCDTAMNT
          ADD       RCDTAMNT,CASHTOT
.
          BRANCH    FORM1,CHKC1000
.
          REP       " 0",RCDTTDAT
          UNPACK    RCDTTDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
.
          MOVE      "P",KEY1
          MOVE      RCBNTTYP,F1
          LOAD      KEY1,F1,ANSH,ANSI,ANSG,ANSH
          MOVE      RCDTDPTY,KEY3
          CALL      FPAY0000                * Get payment desc.
.
          MOVE      "999,999,999.99",DIM14
          FEDIT     RCDTAMNT,DIM14
          WRITE     HTMLFILE;"<tr><td>Payment - ":
                             CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR:
                             "</td>":
                             "<td>&nbsp;</td>":
                             "<td>",KEY30,"</td>":
                             "<td align=right>&nbsp;",RCDTRECN,"</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td align=right><b>",DIM14,"</b></td></tr>"
          GOTO      CHKC1000
.
CHKC8000  ADD       CASHTOT,TOTPAID
CHKC9999  RETURN
+
.*******************************************************************************
.     Payment description
.*******************************************************************************
FPAY0000  MOVE      SP70,KEY30
          CLEAR     KEY30
          MATCH     SP70,KEY3
          GOTO      FPAY2000 IF NOT EQUAL   * Deposit
.
          MATCH     "H",KEY1
          IF        @EQUAL
            APPEND    "FROM HEALTH FUND ",KEY30
          ELSE
            MATCH     "I",KEY1
            IF        @EQUAL
              APPEND    "FROM INSURANCE  ",KEY30
            ELSE
              APPEND    "FROM PATIENT    ",KEY30
            ENDIF
          ENDIF
          APPEND    RCBNRCOD,KEY30
          GOTO      FPAY8000
.
FPAY2000  PACK      KEY5,ANSD,ANSP,KEY3,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            APPEND    "DEPOSIT - ",KEY30
            APPEND    TDESC,KEY30
          ENDIF
.
FPAY8000  APPEND    "(*)",KEY30
          APPEND    SP70,KEY30
          RESET     KEY30
FPAY9999  RETURN
+
.------------------------------------------------------------
.
