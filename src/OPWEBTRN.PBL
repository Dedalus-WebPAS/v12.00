.*****************************************************************************
.* Program Name:  OPWEBTRN.PBL                                               *
.*****************************************************************************
.* V11.03.02 03/08/2023  Ebon Clements     TSK 0935730                       *
.*           Fixed hospital in KEY22 pack before call to OPMVSBOK            *
.* V11.03.01 06/03/2023  Ebon Clements     TSK 0909393                       *
.*           Added hospital to oprsesaf(1-6) and oprdetfa(1&5) indexes       *
.* V10.15.01 19/12/2019  Peter Vela        TSK 0871644                       *
.*           Added Auto Add Operating Team Preferences TBTRN288              * 
.* V10.13.02 18/08/2019  Richa Phogat      TSK 0863284                       *
.*           Updated PVIDOCT with new Doctor for a pre-admission under       *  
.*           TBTRN149                                                        *
.* V10.13.01 24/09/2018  Don Nguyen        TSK 0863852                       *
.*           Added pre-admission check around HL7 trigger DGCLICCP           *
.* V10.12.02 06/07/2018  Don Nguyen        TSK 0857683                       *
.*           Added call to DGCLICCP - HL7 Broadcast Pre-Admission Change     *
.* V10.12.01 05/04/2018  Thanh T.          TSK 0854778                       *
.*           Clear BKREINDC and PTMIDMOV fields when perform transfer        *
.*           booking                                                         *
.* V10.11.02 03/11/2017  J.Tan             TSK 0838821                       *
.*           Mod FXNZ0000 to update Surgeon/Consultant                       *
.* V10.11.01 09/08/2017  Ebon Clements     TSK 0843373                       *
.*           Check record exists prior to update of oprsesaf                 *
.* V10.08.01 25/10/2016  Davin             TSK 0814504                       *
.*           Call extra diet details (UNUT0000) when updating booking date   *
.* V10.05.02 17/12/2014  Peter Vela        CAR 304784                        *
.*           Updated Premature Booking Reason and Authised By before UPBKRX11*
.* V10.05.01 24/11/2013  J.Tan             CAR 304284                        *
.*           Mods to update Extra MBS items                                  *
.* V10.03.06 29/08/2013  Don Nguyen        CAR 178866                        *
.*           Updated admission date in NBRS0000 for booking transfer         *
.* V10.03.05 19/08/2013  Don Nguyen        CAR 178866                        *
.*           Moved TRANREAS (transfer reason) and BKREADAT (Proc date) to    *
.*           relevant save vars in NBRS0000 (called by OPRWEB01).            *
.* V10.03.04 06/06/2013  Jill Parkinson    CAR 260982                        *
.*           Added setting of opdastat=0 and bkrestat=0 if currenta=1        *
.*           When transferring a booking as NOT part of the current admission*
.*           it needs to be created as 'booked'                              *
.* V10.03.03 18.11.2010  Jill Parkinson    CAR 233952                        *
.*           Changed to only update ADATE and PVIDATE if ASTAT=1             *
.* V10.03.02 25/02/2013  Ania P            CAR 281021                        *
.*           Removed unnecessary modification of constants.                  *
.* V10.03.01 21/02/2012  Jill Parkinson    CAR 259918                        *
.*           Added FXNZ0000 - update nzpspraf with new opdauniq value        *
.* V10.00.01 18.11.2010  Jill Parkinson    CAR 233952                        *
.*           Changed to only update ADATE and PVIDATE if ASTAT=1             *
.* V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                        *
.*           Added HL7TRGID & HL7INCLD setting for all broadcast messages    *
.*****************************************************************************
. 15/06/2010V10.00.01 J.Tan         CAR 217811                               *
.                     Fixed only update ALACDTE if No invoice printed        *
. 20/01/2010 V9.12.01 Ebon Clements CAR 208543                               *
.                     Added save of cancellation date for transferred        *
.                     bookings opdea031 TBTRN9999                            *
. 24/12/2008 V9.11.01 Peter Mc      CAR 178013                               *
.                     Preserve pre-admission status on theatre transfers     *
. 03/09/2008 V9.10.01 Davin         CAR 147323                               *
.                     Send hl7 message for new bookings (added call DGCLIS12)*
.                     Send hl7 message for upd bookings (added call DGCLIS14)*
. 20/12/2007 V9.09.02 Jill Habasque CAR 126282                               *
.                     Added update of created date/time for oprdetaf         *
. 07/11/2007 V9.09.01 Jill Habasque CAR 135913                               *
.                     Mods to compare newexdat to wtenevdt                   *
. 27/07/2006 V9.07.01 Jill Habasque CAR 109355                               *
.                     Add NBRS0000                                           *
. 07/04/2006 V9.05.01 Mike Laming   CAR 96147                                *
.                     Add Comments file OPRDEDFD to Transfer Booking at      *
.                     at TBTRN149                                            *
. 31/01/2006 V9.05.B01 Peter Vela    CAR 89644                               *
.                      Added updates to BKRXCRDT, BKRXCRTM, BKRXWEBC         *
.                      at TBTRN148                                           *
. 08/11/2005 V9.04.03 Jill Habasque CAR 51483                                *
.                     Changed to update attending doctor if required         *
. 08/11/2004 V9.04.02 Lina Vo       CAR 54962                                *
.                     Changed TBTRN00 to also write new record to bokdiaaf   *
.                     for full description                                   *
. 08/11/2004 V9.04.01 Lina Vo       CAR 54900                                *
.                     CANDEA00 will adjust times and case numbers so do not  *
.                     need to call OPMVSBOK to adjust current case           *
. 18/03/2003 V9.02.04 Jill Habasque SRF 36865                                *
.                     Added update of OPDATHET when transferring             *
. 13/03/2003 V9.02.03 Jill Habasque SRF 21715                                *
.                     Added update of BKREADAT when transferring             *
. 09/05/2002 V9.02.02 Ebon Clements                                          *
.                     Fixed value of ADATE it was being assigned 6 characters*
. 07/05/2002 V9.02.01 Ebon Clements                                          *
.                     Changed PATADMDT as is should be in correct format     *
.                     from setpar.                                           *
.*****************************************************************************
.                                 TBTRN000
.    Do Transfer and required calculations
.    Get patients operation details from original session
.    Get the next case number in the New Session.
.    Write Transfering patient to the New Session with the next case number
.    Recalculate start times and case numbers.
.    Move transferring patient to the specified case number then update
.    start times.  Check the suspended status of both sessions.
.*****************************************************************************
.
.    Workout the next case number in the new session.  If the session has
.    no records the next case number is one.
.
TBTRN000  MOVE      ZERO,FORM3         * zero the Next case number
          UNPACK    CASEKEYS,CURSESS,ANS,KEY3
          MOVE      ANS,CURSTAT
          MOVE      KEY3,CURCASE
          MOVE      CURSESS,SAVSESS
          MOVE      CURCASE,SAVCCAS
          MOVE      CURSTAT,SAVCSTA
          UNPACK    NEWSESSI,NEWSHOSP,NEWSDATE,NEWSTIME,NEWSCODE
.
          MATCH     Z70,PATADMDT
          IF        !@EQUAL
            MOVE      PATADMDT,NEWEXDAT
          ENDIF
          MOVE      SP70,NEWEXTIM
          MATCH     Z70,BKREC005
          IF        !@EQUAL
            MOVE      BKREC005,NEWEXTIM
          ENDIF
.
          PACK      KEY26,NEWSHOSP,NEWSDATE,NEWSTIME,NEWSCODE,Z70
          CALL      RSOPDEA5           * position fptr at end of session
TBTRN122  CALL      RPOPDEA5           * read backwards rec
          BRANCH    OVRCD,TBTRN144
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP20
          MATCH     KEY22,KEY26        * test for same session
          GOTO      TBTRN144 IF NOT EQUAL
.
          COMPARE   THREE,OPDASTAT
          GOTO      TBTRN122 IF EQUAL
          MOVE      OPDACASE,FORM3
.
.    Read the patients record and update it the new key (session) and
.    case number.   Update the session record concerning book time.
.    Original session OPSETIMB = OPSETIMB - ( OPDAEXPD + OPSEPREP )
.    NEW      session OPSETIMB = OPSETIMB + ( OPDAEXPD + OPSEPREP )
.
.         update OPRDEAFD record
.
TBTRN144  ADD       ONE,FORM3          * Increment case number
          PACK      KEY26,CURSESS,CURSTAT,CURCASE
          CALL      RDOPDEA1           * get patients original session details
.
          MOVE      OPDAUNIQ,OLDUNIQ
          MOVE      "2",AUDIFLAG       * before audit
          CALL      AUDEA000           * write audit
.
          PACK      KEY26,NEWSHOSP,NEWSDATE,NEWSTIME,NEWSCODE,ZERO,FORM3
          UNPACK    KEY26,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,ANS,OPDACASE
.
          MOVE      ZERO,OPDASTAT      * set operation to booked status
          MOVE      "42",FORM2              * get previous unique record
          MOVE      " 42",PRXCODE   * System Lock Sector 42
          CALL      GETSLK00        * Get System Lock of Sector 42
          READ      CONTROLF,FORM2;*32,OPCNUNIQ
          ADD       ONE,OPCNUNIQ            * set it to next unique number
          WRITAB    CONTROLF,FORM2;*32,OPCNUNIQ
          CALL      RELSLK00        * Release System Lock of Sector 42
          MOVE      OPCNUNIQ,OPDAUNIQ
.
          MOVE      OPDAADMN,OLDADMN
          COMPARE   ONE,CURRENTA
          IF        @EQUAL
            CALL      NRBK0000
            MOVE      BKREBOOK,OPDAADMN
            MOVE      OPDAADMN,NEWADMN
            MOVE      ZERO,OPDASTAT
          ENDIF
.
          MOVE      "004",TADTTYPE
          PROC      OPTHETAT                * write to theatre audit
.
          PACK      KEY10,OPDAUNIQ
          CALL      RAOPDEA3
          IF        OVRCD=1
            CALL      IBACLOCK
            PACK      OPDACRDT,CCC,CYY,CMM,CDD
            REP       " 0",OPDACRDT
.
            CLOCK     TIME,OPDACTIM
            MOVE      WBSEUID,OPDAWEBC
.
            CALL      WROPDEA1           * Update transfer record at next case
.
            MOVE      "001",TADTTYPE
            PROC      OPTHETAT                * write to theatre audit
.
            MOVE      ONE,HL7TRGID
            MOVE      "OPWEBTRN",HL7INCLD
            PROC      DGCLIS12                * send new booking message
.
            MOVE      OLDADMN,OPDAADMN
          ENDIF
.
          CALL      FXNZ0000                * fix unique number on nzpspraf
          CALL      FXMB0000                * fix extra MBS on oprmbsaf
.
          MATCH     SP70,OPDAPROC
          GOTO      TBTRN148 IF EQUAL
.
          PACK      KEY19,OPDAURNO,OPDAPROC,OPDACONT,SP70
          CALL      RDTREA1
          BRANCH    OVRCD,TBTRN148
.
          CALL      IBACLOCK                * get current date
          PACK      WTENEVDT,CCC,CYY,CMM,CDD
          REP       " 0",WTENEVDT           * date of addition
          PACK      WTENEVAL,NEWEXDAT
          MATCH     NEWEXDAT,WTENEVDT
          IF        !@LESS
            MOVE      NEWEXDAT,WTENEVDT
          ENDIF
          MOVE      ANSS,WTENETYP
          PACK      WTENSADI,OPDAUNIQ
          CALL      WSAD0000
.
TBTRN148  MOVE      OPDACASE,NEWSCASE
          PACK      CASEKEYS,NEWSHOSP,NEWSDATE,NEWSTIME,NEWSCODE,CURSTAT,FORM3
          MOVE      "3",AUDIFLAG       * after audit
          CALL      AUDEA000           * write audit
.
. ---- this is now done when CANDEA00 is called ----
.         update current session record
.
.         PACK      KEY19,CURSESS,SP20
.         CALL      RDOPSES1           * get original session book time
.         MOVE      "2",AUDIFLAG       * before audit
.         CALL      AUSES000           * write audit
.         SUB       OPDAEXPD,OPSETIMB  * subtract duration to session book time
.         SUB       OPSEPREP,OPSETIMB  * subtract patient prep time
.         CALL      UPOPSES1           * update book time
.         MOVE      "3",AUDIFLAG       * after audit
.         CALL      AUSES000           * write audit
.
.         update new session record
.
          PACK      KEY22,NEWSHOSP,NEWSDATE,NEWSTIME,NEWSCODE,SP20
          CALL      RDOPSES1           * get New session book time
          IF        OVRCD=0
            MOVE      "2",AUDIFLAG       * before audit
            CALL      AUSES000           * write audit
            ADD       OPDAEXPD,OPSETIMB  * Add duration to new session book time
            ADD       OPSEPREP,OPSETIMB  * Add patient prep time
            CALL      UPOPSES1           * update book time
            MOVE      "3",AUDIFLAG       * after audit
            CALL      AUSES000           * write audit
          ENDIF
.
          PACK      KEY10,OPDAUNIQ
          CALL      RDOPDEA3
          IF        OVRCD=0
            MOVE      OPSETHET,OPDATHET
            PACK      OPDAUDAT,CCC,CYY,CMM,CDD
            REP       " 0",OPDAUDAT
.
            CLOCK     TIME,OPDAUTIM
            MOVE      WBSEUID,OPDAWEBU
            MOVE      SAVCSTA,OPDASTAT
            IF        CURRENTA=1
              MOVE      ZERO,OPDASTAT
            ENDIF
.
            CALL      UPOPDEA3           * Update operating room
.
            MOVE      TWO,HL7TRGID
            MOVE      "OPWEBTRN",HL7INCLD
            PROC      DGCLIS14           * send update booking message
          ENDIF
.         update booking record if a pre-admission,
.
           PACK      KEY8,OLDADMN,SP70
           CALL      RDBKREC1               * Ensure on correct booking record
           IF        OVRCD = ZERO
             MOVE      BKREEDAT,OLDADATE             * save date for diet info
             MOVE      NEWEXDAT,BKREEDAT      * Update booking date
             MOVE      NEWEXTIM,BKREATIM      * Update booking time
             MOVE      OPDADATE,BKREADAT
             IF        UPDATDOC=1
               IF        OPCNCLSU=1
                 MOVE      OPDACLIN,BKREADOC
               ELSE
                 MOVE      OPDACLIN,KEY6      * Using Clinic File for Session
                 CALL      RDOPCLI1
                 MOVE      OPCLDOCT,BKREADOC
               ENDIF
             ENDIF
.
             IF        CURRENTA=1
               CALL      NBOK0000             * New booking number
             ELSE
               MOVE      SP70,BKREINDC
               CALL      UPBKREC1             * Update booking record
               CALL      UNUT0000             * Nutrition record update
               CALL      NBRS0000
.
               PACK      KEY8,OLDADMN,SP70
               CALL      RDBKRX11
.
               IF        OVRCD=0
                 CALL      IBACLOCK
                 PACK      BKRXLUPD,CCC,CYY,CMM,CDD
                 REP       " 0",BKRXLUPD
.
                 CLOCK     TIME,BKRXLUPT
                 MOVE      WBSEUID,BKRXWEBU
.
                 MATCH     "1",UPPBRAUB
                 IF        @EQUAL
                   MOVE      BOKRX045,BKRXEABR
                   MOVE      BOKRX046,BKRXAUBY
                   MOVE      BOKRX047,BKRXEAID
                 ENDIF
.
                 CALL      UPBKRX11             * Update booking ext record
.
               ENDIF
             ENDIF
           ENDIF
.
.         Write new booking diagnosis record
.
          MOVE      OLDADMN,BKDIBOOK       * set booking number
          MOVE      OLDUNIQ,BKDIUNIQ       * set unique number
          PACK      KEY18,BKDIBOOK,BKDIUNIQ,SP70
          CALL      RDBKDIA1
          IF        OVRCD=0
            MOVE      BKREBOOK,BKDIBOOK       * set booking number
            MOVE      OPDAUNIQ,BKDIUNIQ       * set unique number
            PACK      KEY18,BKDIBOOK,BKDIUNIQ,SP70
            CALL      RABKDIA1
            IF        OVRCD=1
              CALL      WRBKDIA1
            ENDIF
          ENDIF
.
.         Write any Comments to new booking
.
          MOVE      OPDAUNIQ,BKDIUNIQ      * set new unique number
          PACK      KEY16,OLDUNIQ,SP70     * set key to old unique number
          CALL      RSOPDED1
TBTRN149  CALL      RKOPDED1
          IF        OVRCD = 0
            MATCH     OLDUNIQ,OPDDUNIQ
            IF        @EQUAL
              PACK      SKEY16,OPDDUNIQ,OPDDTYPE,OPDDLINE,SP70
              PACK      KEY16,BKDIUNIQ,OPDDTYPE,OPDDLINE,SP70
              CALL      RAOPDED1
              IF        OVRCD = 1
                MOVE      BKDIUNIQ,OPDDUNIQ
                CALL      WROPDED1
              ENDIF
.                                           * now re-position
              MOVE      SKEY16,KEY16
              CALL      RDOPDED1
              GOTO      TBTRN149
            ENDIF
          ENDIF
.
          BRANCH    CURRENTA,TBTRN150
.
          UNPACK    OPDAADMN,KEY8
          CALL      RDMISS1          * Ensure on correct admission record
          IF        OVRCD = ZERO
            IF        ASTAT=1
              MOVE      BKREEDAT,ADATE
              MOVE      SP10,ATIME
              MOVE      NEWEXTIM,ATIME
              IF        AINVPRT=0
                MOVE      ADATE,ALACDTE
              ENDIF
              IF        UPDATDOC=1
                IF        OPCNCLSU=1
                  MOVE      OPDACLIN,ADOCTA
                ELSE
                  MOVE      OPDACLIN,KEY6      * Using Clinic File for Session
                  CALL      RDOPCLI1
                  MOVE      OPCLDOCT,ADOCTA
                ENDIF
              ENDIF
            ENDIF
            MOVE      SP70,PTMIDMOV
            CALL      UPMISS1        * Update Admission record
.
.          Update Patient Visit Record
.
            MOVE      OPDAADMN,KEY8
            CALL      RDVISA1
            IF        OVRCD = ZERO
              IF        ASTAT=1
                MOVE      BKREEDAT,PVIDATE        * update visit date
                IF        UPDATDOC=1
                  IF        OPCNCLSU=1
                    MOVE      OPDACLIN,PVIDOCT
                  ELSE
                    MOVE      OPDACLIN,KEY6      * Using Clinic File for Session
                    CALL      RDOPCLI1
                    MOVE      OPCLDOCT,PVIDOCT
                  ENDIF
                ENDIF
                CALL      UPVISA1     * Update Patient Visit record
              ENDIF
            ENDIF
.
.
.           Broadcast HL7 message for Pre-Admission Change (A08)
.
            IF        ASTAT=1
              PACK      KEY8,OPDAURNO,SP70
              CALL      RDMAST1
              BRANCH    OVRCD,TBTRN150
.
              MOVE      OPDAADMN,KEY8
              CALL      RDPMVX11
              IF        OVRCD=1
                CALL      CLPMSVX1
              ENDIF
.
              CALL      RDPTRES1            * PRA record on file ?
              IF        OVRCD=1
                CALL      CLPATRE1
              ENDIF
.
              CALL      CLPATTRN            * clear transfer details
              CALL      CLPATDSC            * clear discharge details
.
              CALL      PMIGTNID            * get national id for dgate write
              MOVE      NMPNUMB,PTNINMPI
              MOVE      THREE,HL7TRGID
              MOVE      "OPWEBTRN",HL7INCLD
              PROC      DGCLICCP            * Broadcast Pre-Admission Change
            ENDIF
          ENDIF
.
.    The transfer is done by sneaking the record at the
.    end of the file than moving it to the selected NEWSCASE by Calling
.    OPMVSBOK : This routine will update the start times.
.
TBTRN150  MOVE      FORM3,CASEFROM     * Source case number
          MOVE      NEWSCASE,CASETO    * destination case number
.
.    Save the details of the current case number. This is necessary, as the
.    OPMVSBOK routine alters the value of CURPOS and CURCASE.
.
          MOVE      CURCASE,CCITEM     * save the current case number
.
          PACK      KEY22,NEWSHOSP,NEWSDATE,NEWSTIME,NEWSCODE,SP20
          CALL      OPMVSBOK           * move booking within operation file
.
.    Restore the save case details
.
          MOVE      CCITEM,CURCASE     * restore the current case number
.
.    The Blank position left in original session must be consumed by shifting
.    all cases forward.  Thus get the next case and drag every other case up
.    basically the same as removing it from the session
. ---- this is now done when CANDEA00 is called ----
.
.          MOVE      CURCASE,CASEFROM   * set original spot as case from
.          MOVE      CURCASE,CASETO     * set original spot as case to
.          PACK      KEY19,CURSESS,SP20 * set the session
.          CALL      OPMVSBOK           * fix up case numbers
.
.    Check current and new session suspended status
.   
          PACK      KEY22,CURSESS,SP20
          CALL      OPCHKFUL           * check session is full
          BRANCH    EXIT,TBTRN220      * exit=0 if session is full
          CALL      OPSUSPND
          GOTO      TBTRN222
TBTRN220
          CALL      OPUNSPND           * unsuspend session KEY19
TBTRN222
          PACK      KEY22,NEWSHOSP,NEWSDATE,NEWSTIME,NEWSCODE,SP20
          CALL      OPCHKFUL           * check session is full
          BRANCH    EXIT,TBTRN288      * exit=0 if session is full
          CALL      OPSUSPND           * suspend session KEY19
.
TBTRN288  CALL      AAUPRT00
.
TBTRN300
.
.         Cancel old case and adjust existing case numbers and times
.
TBTRN999  MOVE      SAVSESS,CURSESS
          MOVE      SAVCCAS,CURCASE
          MOVE      SAVCSTA,CURSTAT
          MOVE      TRANREAS,OPDACANC
          MOVE      TRANREAS,REPLY
          MOVE      SP70,DIM8
          MATCH     Z70,OPDEA031
          IF        !@EQUAL
            MOVE      OPDEA031,DIM8
          ENDIF
          CALL      CANDEA00
          RETURN
+
.*****************************************************************************
. Update nzpspraf with the new opdauniq number                               *
.*****************************************************************************
FXNZ0000  COMPARE   FIVE,CFEETYP            * NZ Private only
          GOTO      FXNZ9999 IF NOT EQUAL
.
          OPEN      NZPSPRA1,"nzpspraf"
.
          PACK      KEY11,OPDAADMN,SP70
          CALL      RSNZSPR1
FXNZ1000  CALL      RKNZSPR1
          BRANCH    OVRCD,FXNZ9999
.
          MATCH     OPDAADMN,NZSPADMN
          GOTO      FXNZ9999 IF NOT EQUAL
.
          MATCH     NZSPUNTH,OLDUNIQ
          GOTO      FXNZ1000 IF NOT EQUAL
.
          PACK      NZSPUNTH,OPDAUNIQ,SP70
          IF        UPDATDOC=1
            IF        OPCNCLSU=1
              MOVE      OPDACLIN,NZSPSURG       * Update Surgeon/Consultant
            ENDIF
          ENDIF
.
          CALL      UPNZSPR1
          GOTO      FXNZ1000
.
FXNZ9999  RETURN
+
.*****************************************************************************
. Update oprmbsaf with the new opdauniq number                               *
.*****************************************************************************
FXMB0000  OPEN      OPRMBSA1,"oprmbsaf"
.
FXMB1000  PACK      KEY13,OLDUNIQ,SP70
          CALL      RSOPMBS1
          CALL      RKOPMBS1
          BRANCH    OVRCD,FXMB9999
.
          MATCH     OPMBUNIQ,OLDUNIQ
          GOTO      FXMB9999 IF NOT EQUAL
.
          PACK      OPMBUNIQ,OPDAUNIQ,SP70
          CALL      UPOPMBS1
          GOTO      FXMB1000
.
FXMB9999  RETURN
+
.*****************************************************************************
. Write NBRS "06" records if required                                        *
.*****************************************************************************
NBRS0000  MATCH     SP70,OPDAPROC
          GOTO      NBRS9999 IF EQUAL
.
          PACK      KEY19,OPDAURNO,OPDAPROC,OPDACONT,SP70
          CALL      RDTREA1
          BRANCH    OVRCD,NBRS9999
.
          CALL      SAVHIS00
.
          MOVE      BKREADAT,SAVDBFT        * Operation date
          MOVE      BKREBKDT,SAVBMDT        * Booking time
          MOVE      TRANREAS,SAVERDT3       * Transfer reason
          MOVE      BKREEDAT,WMDATE3        * Admisison date
          CALL      IBACLOCK
          MOVE      "06",WTWMBSCD
          MOVE      ONE,NBRFLAG
          CALL      WRTHIS00
.
NBRS9999  RETURN
+
.*****************************************************************************
. Not using the current admission, generate new bokrc1af and bokrx1af record *
.*****************************************************************************
NBOK0000  PACK      KEY8,NEWADMN,SP70
          CALL      RABKREC1
          IF        OVRCD=1
            MOVE      NEWADMN,BKREBOOK
            MOVE      ZERO,BKRESTAT
            CALL      WRBKREC1           * New booking number
          ENDIF
.
          PACK      KEY8,OLDADMN,SP70
          CALL      RDBKRX11
          IF        OVRCD=1
            MOVE      NEWADMN,BKRXBNUM
            CALL      RABKRX11
            IF        OVRCD=1
              CALL      WRBKRX11            * write record to booking details
            ENDIF
          ELSE
            MOVE      NEWADMN,BKRXBNUM
            CALL      WRBKRX11
          ENDIF
.              
          MATCH     SP70,OPDAPROC
          GOTO      NBOK9999 IF EQUAL
.
          PACK      KEY19,OPDAURNO,OPDAPROC,OPDACONT,SP70
          CALL      RDTREA1
          BRANCH    OVRCD,NBOK9999
.
          MOVE      TWO,WMSTAT
          MOVE      NEWADMN,WMBOOK
          CALL      UPTREA1
          CALL      NBRS0000
.
NBOK9999  RETURN
+
.*****************************************************************************
. Auto Add Operating Team Preferences
.*****************************************************************************
AAUPRT00  MATCH     "1",AUTOPREF
          GOTO      AAUPRT99 IF NOT EQUAL
.
          PACK      KEY22,NEWSHOSP,NEWSDATE,NEWSTIME,NEWSCODE,SP70
          CALL      RDOPSES1
          BRANCH    OVRCD,AAUPRT99
.
          PACK      KEY10,OPCNUNIQ,SP70
          CALL      RDOPDEA3
          BRANCH    OVRCD,AAUPRT99 
.
          MATCH     SP70,OPDACLIN
          GOTO      AAUPRT50 IF EQUAL
          GOTO      AAUPRT50 IF EOS
.
          PACK      DIM6,OPDACLIN,SP70
.
          PACK      KEY13,OPDAUNIQ,SP70
          CALL      RSOPMBS1
AAUPRT10  CALL      RKOPMBS1
          BRANCH    OVRCD,AAUPRT50
.
          MATCH     OPDAUNIQ,OPMBUNIQ
          GOTO      AAUPRT50 IF NOT EQUAL
.
          MATCH     SP70,OPMBMBSI
          GOTO      AAUPRT10 IF EQUAL
.
          PACK      DIM9,OPMBMBSI,SP70
          CALL      WRTPRF00
.
          GOTO      AAUPRT10
.
AAUPRT50  MATCH     SP70,OPSEANAE
          GOTO      AAUPRT99 IF EQUAL
          GOTO      AAUPRT99 IF EOS
.
          PACK      DIM6,OPSEANAE,SP70
.
          MATCH     SP70,OPDAANAE
          IF        !@EQUAL & !@EOS
            PACK      DIM9,OPDAANAE,SP70
            CALL      WRTPRA00
          ENDIF
.
AAUPRT99  RETURN
+
