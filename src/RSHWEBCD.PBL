.------------------------------------------------------------
. Set Up Schedule Record 
.   Includes   - RSHWEBDF
.                WEBCONFD/INC
.                WEBSCHFD/INC
.                WEBSCHIO/INC
.
.   Parameters - SCHDDATE Schedule for Date (Blank for Immediate)
.                SCHDTIME Schedule for Time  (Blank for Immediate)
.                SCHDDESC Description of Report or Process
.                SCHDPGID Program to be Executed
.                SCHDPRIN Printer (Optional)
.                SCHDCOPY No of Copies (Optional)
.                KEYSTARR Array of Keyins Required    DIM    100[99]
.                KEYSTCNT Number of Keyins            FORM   2
. Modifications:
.     V11.02.01 05/04/2022  Ebon Clements  Task 0911828
.               Added WBSCCDAT and WBSCCTIM to file clear - CLRSCS00
.     V11.01.01 26/04/2021  Ebon Clements  Task 0899288
.               Added WBSCHOSP and WBSCSITE to file clear - CLRSCS00
.     V10.13.02 04/12/2018  Peter Vela     Task 0859743
.               Increased length of KEYSTARR / work variable
.     V10.13.01 05/11/2018  Peter Vela     Task 0863035
.               Increased length of KEYSTARR / work variable
.     V9.11.01  02/04/2009  Jill Habasque  CAR 193772
.               Added trap around WRWBSC1
.     V9.03.01  02/07/2003  Lina Vo
.               Added WBSCEXCP field to CLRSCS00.  This field was not being
.               cleared and was causing scheduled jobs to be deleted in
.               RSHWEB01.
.------------------------------------------------------------
SCHPRO00  OPEN      WEBSCHA1,"webschaf"
          CALL      IBACLOCK 
          READ      CONTROLF,HUNDRED2;RSCNCCP,RSCNSLP,RSCNRCD,RSCNROD:
                                      RSCNCMD,RSCNCLP1,RSCNCLP2,RSCNCLP3:
                                      RSCNCLP4,RSCNCLP5,RSCNSPDL,RSCNCMDL
          CALL      NXTSCH00         * Get Next Report Schedule Number 
          MOVE      "1",WBSCSTAT     * Status 1 = Awaiting Processing
.
          MOVE      SCHDDATE,WBSCSCDT
          MOVE      SCHDTIME,WBSCSCTM 
          MOVE      "0",WBSCPERM 
          MOVE      SP70,WBSCRDAT 
          MOVE      SP70,WBSCREND 
          MOVE      SP70,WBSCACTV       * Active Process ID 
          MOVE      SP70,WBSCUNIX
          MOVE      SCHDDESC,WBSCDSRP   * Description of Report      
          MOVE      SCHDPGID,WBSCRPID   * Report Program ID
          MOVE      USERID,WBSCUSER     * User ID
          PACK      WBSCRDTE,CCC,CYY,CMM,CDD  * Request Date
          REP       " 0",WBSCRDTE
          MOVE      CTIMEIS,WBSCRTIM    * Request Time
          MOVE      SP70,WBSCSDAT       * Start Date
          MOVE      SP70,WBSCSTIM       * Start Time
          MOVE      ZERO,WBSCSFLG       * Start Flag
          MOVE      SCHDTYPE,WBSCOTYP   * Output Type 0=Text 1=Html
          MOVE      ZERO,WBSCNREC       * Expected no of records
          MOVE      ZERO,WBSCRPRO       * Records Processed
          MOVE      SP70,WBSCUTIM       * Last Update Time 
          MATCH     SP70,SCHDPRNT
          IF        @EQUAL
            MOVE      "S  ",WBSCPRIN    * Spool by Default
          ELSE
            MOVE      SCHDPRNT,WBSCPRIN * Printer
          ENDIF
          MOVE      SP70,WBSCSPOO       * Spool File
          MOVE      SCHDCOPY,WBSCNCOP   * No of Copies
.
          CALL      UPWBSC1             * Update Schedule
.
          CALL      MAKKEY00            * Make Key Strokes File
.
          CALL      UUWBSC1             * Unlock record
.
SAVPRO99  RETURN
.------------------------------------------------------------
.  * Create a Key Strokes File
.------------------------------------------------------------
MAKKEY00  PACK      KEY80,RSCNRCD,WBSCSCHD,KEYSTKNM 
          SQUEEZE   KEY80
          PREP      KEYSTKFL,KEY80     * Create Key Strokes File
.
.       Write Schedule Number as first key stroke
.
          WRITE     KEYSTKFL,SEQ;WBSCSCHD    * Schedule No : First Key Stroke
          COMPARE   ZERO,KEYSTCNT
          GOTO      MAKKEY90 IF EQUAL
.
          MOVE      ZERO,ARRCNT
MAKKEY10  ADD       ONE,ARRCNT
          MOVE      KEYSTARR[ARRCNT],KEY100     * Key Stroke Variable
          STRIP     KEY100
          MOVELPTR  KEY100,F2
          IF        F2=0
            WRITE     KEYSTKFL,SEQ;""      * Write Key Stroke Variable     
          ELSE
            SFORMAT   VAR,127
            SFORMAT   VAR,F2
            MOVE      KEY100,VAR
            WRITE     KEYSTKFL,SEQ;VAR      * Write Key Stroke Variable     
          ENDIF
          IF        ARRCNT<KEYSTCNT
            GOTO      MAKKEY10              * Get Next Key Stroke
          ENDIF         
.
MAKKEY90  CLOSE     KEYSTKFL
.      
MAKKEY99  RETURN
.------------------------------------------------------------
. Get Next Report Schedule Number
.------------------------------------------------------------
NXTSCH00  PACK      KEY8,Z70
          CALL      RSWBSC1
NXTSCH10  CALL      RPWBSC1
          BRANCH    OVRCD,NXTSCH90
          MOVE      WBSCSCHD,SCHDFLNO
          ADD       ONE,SCHDFLNO
          GOTO      NXTSCH95
.
NXTSCH90  MOVE      ONE,SCHDFLNO
.
NXTSCH95  CALL      CLRSCS00    * Clear All Fields   
          MOVE      SCHDFLNO,WBSCSCHD
          REP       " 0",WBSCSCHD
          MOVE      WBSCSCHD,KEY8
          CALL      RAWBSC1
          IF        OVRCD=1
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            CALL      WRWBSC1
            TRAPCLR   IO
            BRANCH    OVRCD,NXTSCH98
            CALL      RLWBSC1
          ELSE
            GOTO      NXTSCH98
          ENDIF
          GOTO      NXTSCH99
.
NXTSCH98  ADD       ONE,SCHDFLNO
          GOTO      NXTSCH95
.
NXTSCH99  RETURN
.------------------------------------------------------------
. Clear Server Schedule Table Variables 
.------------------------------------------------------------
CLRSCS00  MOVE      SP70,WBSCSCHD   * Schedule No  
          MOVE      SP70,WBSCSTAT   * Status  
          MOVE      SP70,WBSCSCDT   * Schedule Date 
          MOVE      SP70,WBSCSCTM   * Schedule Time 
          MOVE      SP70,WBSCACTV   * Active Process ID
          MOVE      SP70,WBSCUNIX   * Unix Command
          MOVE      SP70,WBSCDSRP   * Description of report
          MOVE      SP70,WBSCRPID   * Report Program ID
          MOVE      SP70,WBSCUSER   * Web User
          MOVE      SP70,WBSCRDTE   * Request Date
          MOVE      SP70,WBSCRTIM   * Request Time
          MOVE      SP70,WBSCSDAT   * Started Date
          MOVE      SP70,WBSCSTIM   * Started Time
          MOVE      ZERO,WBSCSFLG   * Start Flag :Set to 1 by Report Program
          MOVE      ZERO,WBSCOTYP   * Output Type : 0=Text 1=HTML
          MOVE      ZERO,WBSCNREC   * Expected No. of records
          MOVE      ZERO,WBSCNREC   * Expected No. of records
          MOVE      ZERO,WBSCRPRO   * Records Processed
          MOVE      SP70,WBSCUTIM   * Last Update Time
          MOVE      SP70,WBSCPRIN   * Printer
          MOVE      SP70,WBSCSPOO   * Spool File
          MOVE      ZERO,WBSCNCOP   * No of Copies
          MOVE      SP70,WBSCPREP   * Print Report 0=No 1=Yes
          MOVE      "00000000",WBSCEXCP   * Exclusion flags - Hols, Mon-Sun
          MOVE      SP70,WBSCHOSP   * Hospital ID (pathspaf)
          MOVE      SP70,WBSCSITE   * Outpatient Site (outsitaf)
          MOVE      SP70,WBSCCDAT   * Completed Date (CCYYMMDD)
          MOVE      SP70,WBSCCTIM   * Completed Time (HH:MM:SS)
          MOVE      SP70,WBSCSPAR   * Spare
.
CLRSCS99  RETURN
.
