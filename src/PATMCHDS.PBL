.******************************************************************************
.*                                  PATMCHDS                                  *
.*                                                                            *
.*     INClude to display all available Charge Codes in the Miscellaneous     *
.*     Charges file (PATMCHFD - patmchgf) for the specified Claim Code/       *
.*     Health Fund/Health Fund Table combination                              *
.*                                                                            *
.*     Author               : i chung                                         *
.*                                                                            *
.*     Date                 : 13/08/90                                        *
.*                                                                            *
.*     File Needed          : PATMCHG1                                        *
.*                                                                            *
.*     Input Requirement(s) : CLAIM     (Claim Code)                          *
.*                            HFUND     (Health Fund)                         *
.*                            PTMCHFTT  (Health Fund Table Type)  *C-233046   *
.*                            CMCNOHF   (System Parameter - IBA SCREEN 4)     *
.*                                                                            *
.*     Modifications :                                                        *
.*        V10.01.01 01/12/2010  Mike Laming   CAR 233046                      *
.*                  Mods to use "Table Type"  PTMCHFTT (replaces H/F Table)   *
.*        V9.03.01  09/09/2003  Lina Vo           CAR 40497                   *
.*                  Changed indexes in patmchgf.                              *
.*        V5.02.02  23/03/1995  Graeme Williams   SRF 135698                  *
.*                  Parameter for health fund is now HFUND instead of HCODE.  *
.*                  Most programs were using HFUND (as it is in release 6),   *
.*                  which is safer anyway, as HCODE is the field from the     *
.*                  health fund file, while HFTAB is not.                     *
.*                            V5.02.01  28/12/1994  Steve Armstrong           *
.*                                      Fixed bug in display of prompt on line*
.*                                      24                                    *
.*                          : V5.01.02  03/06/1992  Graeme Williams           *
.*                                      MCHARGE now 9 characters long         *
.*                                      SRF 114865                            *
.*                                                                            *
.******************************************************************************
PATMCHDS
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:4,*EF,*P20:4,*V2LON,*ULON,"CHARGE CODES"
.
          UNPACK    SP20,PTHFBCAT      * convert "H/F Table" to "Table Type"
          PACK      KEY14,HFUND,HFTAB,SP20
          CALL      RDFUND1            * "Table Type code" now in PTHFBCAT
.
          PACK      KEY29,CLAIM,HFUND,PTHFBCAT,SP70                    
          CALL      RDSMCHG1                     * position read PATMCHFD file
          CALL      RDKMCHG1                     * read next PATMCHFD record
          BRANCH    OVRCD,PATMC800               * end-of-file
.
          BRANCH    CLMFLG,PATMC150
          BRANCH    FNDFLG,PATMC150
.
          MATCH     MCLMCOD,CLAIM                * same Claim Code ?
          GOTO      PATMC800 IF NOT EQUAL        * no
          MATCH     MHFUND,HFUND                 * same Health Fund ?
          GOTO      PATMC800 IF NOT EQUAL        * no
          MATCH     PTMCHFTT,PTHFBCAT            * same Health Fund Table ?
          GOTO      PATMC800 IF NOT EQUAL        * no
.
.
. ------  display INClude heading and then loop entries  ------
.
.          DISPLAY   *P1:4,*EF,*P20:4,*V2LON,*ULON,"CHARGE CODES"
.
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          CALL      RDSMCHG1
.
PATMC100  CALL      RDKMCHG1
          BRANCH    OVRCD,PATMC900
          BRANCH    CLMFLG,PATMC150  
          BRANCH    FNDFLG,PATMC150
.
          MATCH     MCLMCOD,CLAIM                * same Claim Code ?
          GOTO      PATMC900 IF NOT EQUAL        * no
          MATCH     MHFUND,HFUND                 * same Health Fund ?
          GOTO      PATMC900 IF NOT EQUAL        * no
          MATCH     PTMCHFTT,PTHFBCAT            * same Health Fund Table ?
          GOTO      PATMC900 IF NOT EQUAL        * no
          GOTO      PATMC175
.
PATMC150  BRANCH    CLMFLG,PATMC160
          MATCH     MCLMCOD,CLAIM                * same Claim Code ?
          GOTO      PATMC100 IF NOT EQUAL        * no
.
PATMC160  BRANCH    FNDFLG,PATMC175
          MATCH     MHFUND,HFUND                 * same Health Fund ?
          GOTO      PATMC100 IF NOT EQUAL        * no
.
PATMC170  MATCH     PTMCHFTT,PTHFBCAT            * same Health Fund Table ?
          GOTO      PATMC100 IF NOT EQUAL        * no
.
PATMC175  DISPLAY   *PCCOL:CVERT,*V2LON,MCHARGE,*HOFF,SP1,MDESC
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      PATMC100 IF LESS
.
          MOVE      SIX,CVERT
          ADD       "40",CCOL
          COMPARE   "70",CCOL
          GOTO      PATMC100 IF LESS
.
PATMC180  DISPLAY   *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue, (",*V2LON:
                               "F",*HOFF,")irst screen or (",*V2LON,"N",*HOFF:
                               ")ext screen :"
.
PATMC200  KEYIN     *P47:24,*DV,UNDLN1,*P47:24,*V2LON,ANS
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          MATCH     SP1,ANS                     * null input ?
          GOTO      PATMC180 IF EQUAL           * yes
          REP       UPPLOW,ANS                  * no - replace lower case
          DISPLAY   *P47:24,*V2LON,ANS
.
          MATCH     ANSC,ANS                    * Continue ?
          GOTO      PATMC920 IF EQUAL           * yes
          MATCH     ANSF,ANS                    * First screen ?
          GOTO      PATMCHDS IF EQUAL           * yes
          MATCH     ANSN,ANS                    * Next screen ?
          GOTO      PATMC300 IF EQUAL           * yes
          BEEP                                  * invalid input - beep &
          GOTO      PATMC200                                      re-enter
.
PATMC300  MOVE      SIX,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
          GOTO      PATMC100
.
. ------  if missing entries show entries missing  ------
.
PATMC800  BRANCH    CMCNOHF,PATMC810
.
          COMPARE   ONE,CKYIMODE
          GOTO      PATMC999 IF EQUAL
. 
          DISPLAY   *P1:23,*EF,*B,*P1:23:
                    "No Charge Codes for this ",*V2LON,"Claim Code",*HOFF:
                    "/",*V2LON,"Health Fund",*HOFF,"/",*V2LON,"Health Fund ":
                    "Table",*HOFF," combination",*P1:24,"exist on file.  ";
          CALL      HITENTER
          DISPLAY   *P1:23,*EL
          GOTO      PATMC820
.
PATMC810  DISPLAY   *P1:24,*EL,*B:
                    "No Charge Codes for this ",*V2LON,"Claim Code",*HOFF:
                    " exist on file.  ";
          CALL      HITENTER
.
PATMC820  MOVE      ONE,OVRCD
          GOTO      PATMC999
.
. ------  end of Codes Table file  ------
.
PATMC900  DISPLAY   *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue, (",*V2LON:
                               "F",*HOFF,")irst screen :"
.
PATMC910  KEYIN     *P30:24,*DV,UNDLN1,*P30:24,*V2LON,ANS
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          MATCH     SP1,ANS                     * null input ?
          GOTO      PATMC920 IF EQUAL           * yes
          REP       UPPLOW,ANS                  * no - replace lower case
          DISPLAY   *P30:24,*V2LON,ANS
.
          MATCH     ANSC,ANS                    * Continue ?
          GOTO      PATMC920 IF EQUAL           * yes
.
          MATCH     ANSF,ANS                    * First screen ?
          GOTO      PATMCHDS IF EQUAL           * yes
          BEEP                                  * invalid input - beep &
          GOTO      PATMC910                                      re-enter
.
PATMC920  MOVE      ZERO,OVRCD
.
PATMC999  RETURN
+
