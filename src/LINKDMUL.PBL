. *****************************************************************************
. * Routine Name  : LINKDMUL                                                  *
. *                                                                           *
. * Function      : To utilise the multi database search feature.  It will    *
. *                 loop over each of the databases Visit files for the UR    *
. *                 and extract all the LINKED visits and display the         *
. *                 record.                                                   *
. *                                                                           *
. * Modifications :                                                           *
. *                                                                           *
. *                V12.00.01 30/05/2025  Don Nguyen   TSK 0955096             *
. *                          Alphanumeric visit number changes                *
. *                V10.08.01 02/05/2016  Ebon Clements  TSK 0268970           *
. *                          Change to use OUTCLIFD index 1 instead of index 2*
. *                V9.12.01  17/12/2009  Peter McMullen CAR 210130            *
. *                          Convert to newer Dr name format routine DOCNM000 *
. *                 V9.10.01  22/06/2009  Jill Habasque    CAR 179650         *
. *                          Mods to use outboka6 in MLOUT000                 *
. *                 V9.05.B01 20/02/2006  Ebon Clements     CAR 76341         *
. *                          Added referral and encounter file audit          *
. *                 V9.04.01 27/10/2005 Ebon Clements CAR 75461               *
. *                          Fixed display of clinic/doctor for outpatients   *
. *                 V9.04.00 14/10/2005 Ebon Clements CAR 79606               *
. *                          Fixed display of diagnosis in MLOUT000           *
. *                 V9.03.04 11/12/2003 Ebon Clements CAR 45549               *
. *                          Mods for OUTRF1FD - change from doctors file     *
. *                          to hcp file                                      *
. *                 V9.03.03 04/08/2003 Jill Habasque 42003                   *
. *                          Added match for outpatient site                  *
. *                 V9.03.02 22.07.2003 Jill Habasque CAR 21760               *
. *                          Changed key for bokdiaaf                         *
. *                 V9.03.01 09.07.2003 Jill Habasque                         *
. *                          Fixed multi database opens                       *
. *                 V9.02.00 09/10/2002 Sylvek Litewka  SRF 22692             *
. *                          Made changes to process (display) Allied Health  *
. *                          Referrals (patvisaf.pvitype=9, pvisyst=2) and    *
. *                          Patient Events (patvisaf.pvitype=9, pvisyst=1)   *
. *                                                                           *
. *                 V5.07.01 04.10.1999 B.G.Ackland                           *
. *                          Changes Made at Taranaki                         *
. *                 V5.07.00 22.09.1999 Pat Dirito                            *
. *                          Created Program - WEB Version of GETVIMUL        *
. *                                                                           *
. *****************************************************************************
          DEFPROC   LINKDMUL
.
          INC       AAEDE1FD/INC         * Aae details File
          INC       ALLCONFD/INC
          INC       BOKRC1FD/INC         * Booking File
          INC       OUTBOAFD/INC         * Slots File
          INC       OUTBB1FD/INC         * Booking File
          INC       OUTCLIFD/INC         * Clinic Id File
          INC       OUTCTYFD/INC         * Clinic Type File
          INC       OUTMA1FD/INC         * Outpatient Master File
          INC       OUTRF1FD/INC         * Referrals File
          INC       OUTSITFD/INC         * Site File
          INC       PATHOSFD/INC         * Database File
          INC       PATHSPFD/INC         * Hospital File
          INC       PATMI1FD/INC         * Admission File
          INC       PATVISFD/INC         * Visit File
          INC       PATWR1FD/INC         * Ward File
          INC       WATTR1FD/INC         * Waiting List File
          INC       OUTDIAFD/INC         * Outpatient - Diagnosis File
          INC       BOKDIAFD/INC         * Booking Diagnosis File
          INC       ALLPRRFD/INC         * Allied Health Problem Codes File
          INC       ALLREFFD/INC         * Allied Health Referal File
          INC       PMSEVTFD/INC         * Patient Event File
          INC       PATCODFD/INC         * Patient Codes File
.
SAVOSTF   DIM       3
DMSHOSPN  FORM      2
CLIHOSPN  FORM      2
DIAHOSPN  FORM      2
DIGHOSPN  FORM      2
DOCHOSPN  FORM      2
SITHOSPN  FORM      2
MISHOSPN  FORM      2
VISHOSPN  FORM      2
AHRHOSPN  FORM      2
APCHOSPN  FORM      2
PEVHOSPN  FORM      2
PCDHOSPN  FORM      2
HOSPCNT   FORM      1
.-----------------------------------------------------------------------------
LINKD000  MOVE      "99",DMSHOSPN
          MOVE      "99",CLIHOSPN
          MOVE      "99",DIAHOSPN
          MOVE      "99",DIGHOSPN
          MOVE      "99",DOCHOSPN
          MOVE      "99",SITHOSPN
          MOVE      "99",MISHOSPN
          MOVE      "99",VISHOSPN
          MOVE      "99",AHRHOSPN
          MOVE      "99",APCHOSPN
          MOVE      "99",PEVHOSPN
          MOVE      "99",PCDHOSPN
.
          PACK      KEY23,URNUMBER,EPSOFCAR,SP70
          CALL      RSECLNK1
LINKD100  CALL      RKECLNK1
          BRANCH    OVRCD,LINKD999
          MATCH     URNUMBER,ECLNURNO
          GOTO      LINKD999 IF NOT EQUAL   * Linked record not found
          MATCH     EPSOFCAR,DECLNEPN
          GOTO      LINKD999 IF NOT EQUAL   * Linked record not found
.
          CALL      CHKVS000    * Check Visits File
          BRANCH    EXIT,LINKD200
.
          CALL      CHKBOK00    * Check Clinic Sessions Bookings File A
          BRANCH    EXIT,LINKD200
.
          CALL      CHKIBK00    * Check Inpatient Bookings File
          BRANCH    EXIT,LINKD200
.
          CALL      CHKWAT00    * Check Waiting List File
          BRANCH    EXIT,LINKD200
.
          CALL      CHKREF00    * Check Outpatient Referral File
          BRANCH    EXIT,LINKD200
.
          GOTO      LINKD100    * Read next Linked Record
.
.   Record found! Display HTML Row
.
LINKD200  
          UNPACK    EVNTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY11,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          LOAD      VTYPE,EVNVTYPE,MVEVN01,MVEVN02,MVEVN03,MVEVN04,MVEVN05:
                                   MVEVN06,MVEVN07,MVEVN08,MVEVN09,MVEVN10:
                                   MVEVN11,MVEVN12,MVEVN13,MVEVN14,MVEVN15:
                                   MVEVN16,MVEVN17,MVEVN18
          IF        EVNVTYPE = 16 
            MOVE      " disabled ",DISABLD
          ELSE
            MOVE      SP70,DISABLD
          ENDIF
.
          MATCH     SP70,EVNTHCP
          GOTO      LINKD250 IF EQUAL
.
          MOVE      SP70,DOCTOR
          PACK      KEY10,EVNTHCP,SP70  * Get Doctor Formatted name
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
            CLEAR     DOCTOR
            APPEND    DOCFNAME,DOCTOR
          ENDIF          
          GOTO      LINKD270
.
LINKD250  COMPARE   TEN2,EVNVTYPE       * Outpatient booking
          GOTO      LINKD270 IF EQUAL
.
          COMPARE   TWO,EVNVTYPE        * Outpatient attendance
          GOTO      LINKD270 IF EQUAL
.
          MOVE      SP70,DOCTOR
          PACK      KEY6,EVNTDOC,SP70   * Get Doctor Formatted name
          CALL      RDDOCT1
          IF        OVRCD = 0
            MOVE      DTITL,FMTTITLE
            MOVE      DGNAME,FMTGNAME
            MOVE      DSNAME,FMTSNAME
            CALL      DOCNM000
            CLEAR     DOCTOR
            APPEND    DOCFNAME,DOCTOR
          ELSE
            MOVE      SP70,DOCTOR
          ENDIF
.
LINKD270  PACK      KEY3,EVNTHSPL,SP70   * Get Hospital
          CALL      RDPTHSP1
          IF        OVRCD = 0
            MOVE      PTHSNAME,KEY25
          ELSE
            MOVE      SP70,KEY25
          ENDIF
.
          MOVE      "#"",KEY1
          PACK      UNLINK,KEY1,EVNTHOSP,EVNTNUM,KEY1,SP70
.
          WRITE     HTMLFILE;"<tr><td nowrap>":
                             "<img src=../images/PatientFolder.gif hspace=4":
                             " border=0 align=absmiddle alt=#"",EVNTNUM,"#"":
                             " onClick='SelectVisit(#"",EVNVTYPE,"#",#"":
                             EVNTNUM,"#",#"",EVNTHOSP,"#")'>":
                             KEY11,"</td>":
                             "<td>",VTYPE,"</td><td>",EVNTDIAG,"</td>":
                             "<td>",DOCTOR,"</td><td>",KEY25,"</td>";
.
          BRANCH    LTABTYPE,LINKD300
          WRITE     HTMLFILE;"<td align=center><input type=checkbox":
                             " name=linkevnt value=",UNLINK,DISABLD,"></td>";
.
LINKD300  WRITE     HTMLFILE;"</tr>"
.
.          WRITE     HTMLFILE;"t.addRow(#"":
.                            "eocweb01.pbl?reportno=1&template=003&urnumber=":
.                             URNUMBER,"&epsofcar=",EVNTNUM,"#",#"":
.                             EVNTDATE,"#",#"":
.                             VISITYPE,"#",#"":
.                             EVNTDIAG,"#",#"":
.                             EVNTDOC,"#",#"":
.                             EVNTHOSP,"#");"
.
          GOTO      LINKD100    * Read next Linked Record
.
.
LINKD999  GOTO      ENDLINKD 
.------------------------------------------------------------
.  Check the Visit file for the correct record..
.  Parameters:
.              PTCNLEDT - control file variable starting date to read from.
.------------------------------------------------------------
CHKVS000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,SIXTY;*129,MRCNNOHS  * number of data directories
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,TEN;*244,CHOSPNUM
.
          OPEN      PATHSPA1,"pathspaf"
.
          MOVE      ZERO,HOSPCNT            * clear the counter
          COMPARE   ZERO,MRCNNOHS
          GOTO      CHKVS080 IF EQUAL
.
CHKVS050  MOVE      DECLNHOS,HOSPCNT
.
CHKVS080  PACK      XXDPATH,SP20,SP20,SP20
          LOAD      XXDPATH,HOSPCNT,HDPATH1,HDPATH2,HDPATH3,HDPATH4,HDPATH5,HDPATH6
          IF        HOSPCNT <> ZERO
            MATCH     SP30,XXDPATH
            GOTO      CHKVS999 IF EQUAL       * no DPATH for hospital
          ENDIF
.
          MOVE      "patvisaf",OPFILE
          MOVE      TWENTY,FILENUM
          CALL      OMDFN000                * open appropriate visit file
          BRANCH    EXIT,CHKVS999           * no visit file, try O/P file
.
          MOVE      ONE,FILENUM
          MOVE      "patmi1af",OPFILE
          CALL      OMDFN000                * open appropriate visit file
          BRANCH    EXIT,CHKVS999           * no visit file, try O/P file
.
          CALL      OPTWD000                * open the ward file
          BRANCH    EXIT,CHKVS999           * no visit file, try O/P file
.
          CALL      OPTDC000                * doctor
          BRANCH    EXIT,CHKVS999           * no visit file, try O/P file
.
          MOVE      "outsitaf",OPFILE       * op site
          MOVE      FORTY,FILENUM
          CALL      OMDFN000
          BRANCH    EXIT,CHKVS999
.
          CALL      OAEDA000                * a&e details
          BRANCH    EXIT,CHKVS999
.
          CALL      OPNAHR00           * Open the Allied Health Referral File
          BRANCH    EXIT,CHKVS999      * No Allied Health file, try O/P file
.
          CALL      OPNAPC00           * Open Allied Health Problem Codes File
          BRANCH    EXIT,CHKVS999      * No AHP Codes file, try O/P file
.
          CALL      OPNPEV00           * Open the Patient Events File
          BRANCH    EXIT,CHKVS999      * No Patient Events file, try O/P file
.
          CALL      OPNPCD00           * Open the Patient Codes File
          BRANCH    EXIT,CHKVS999      * No Patient Codes file, try O/P file
.
          PACK      KEY8,DECLNVIS,SP70
          CALL      RDVISA1
          BRANCH    OVRCD,CHKVS999
.
..        MATCH     PTCNLEDT,PVIDATE  * Check if Visit Date < Search date
..        GOTO      CHKVS990 IF LESS
.
          BRANCH    PVITYPE,CHKVS100,CHKVS100,CHKVS100:
                            CHKVS990,CHKVS990,CHKVS100:
                            CHKVS990,CHKVS990,CHKVS100
          GOTO      CHKVS990
.
.    Get extra details for each Visit Type and Set VISITYPE Variable
.
CHKVS100  PERFORM   PVITYPE,MLAAE000,MLOUT000,MLINP000:
                            MLDUM000,MLDUM000,MLCOM000:
                            MLDUM000,MLDUM000,MLOTH000
.
.  Set Display Variables
.
          MOVE      PVIBILL,EVNTNUM      * Event Number
          MOVE      PVIDATE,EVNTDATE     * Event Date
          MOVE      PVIDOCT,EVNTDOC      * Doctor
          MOVE      SP70,EVNTHCP         * HCP  
          MOVE      VISITYPE,EVNVTYPE    * Event Visit Type
          MOVE      DIAGNSIS,EVNTDIAG    * Event Diagnosis
          MOVE      DECLNHOS,EVNTHOSP    * Hospital Code
          MOVE      HOSPCODE,EVNTHSPL    * Hospital Code
.
          MOVE      ONE,EXIT      * Visit Record found exit
          GOTO      CHKVS999
.
CHKVS990  MOVE      ZERO,EXIT    * Check next File
CHKVS999  RETURN
.--------------------------------------------------------------------------
.  Loop over the Clinic Sessions Bookings File A for the patients UR number
.  Parameters:
.              PTCNLEDT - control file variable starting date to read from.
.--------------------------------------------------------------------------
CHKBOK00  MOVE      "outsitaf",OPFILE       * op site
          MOVE      FORTY,FILENUM
          CALL      OMDFN000
          BRANCH    EXIT,CHKBOK98
.
          MOVE      "outsitaf",OPFILE       * op site
          MOVE      FORTY1,FILENUM
          CALL      OMDFN000
          BRANCH    EXIT,CHKBOK98
.
.   Non Site Dependant ? control parameter!
.
          IF        OTCNNSID = 1
            MOVE      "out",OSTFILE
            PACK      OPFILE,OSTFILE,FILBOKA3   * Parameter for OPNFIL00
            GOTO      CHKBOK10
          ENDIF
.
          PACK      SAVOSTF,SP70
          MOVE      SP70,KEY6         * Current Site Code
          CALL      RDSSITA3
CHKBOK05  BRANCH    OTCNNSID,CHKBOK98
          CALL      RDKSITA3
          BRANCH    OVRCD,CHKBOK98
.
          MATCH     OSTFILE,SAVOSTF
          GOTO      CHKBOK05 IF EQUAL
          PACK      SAVOSTF,OSTFILE,SP70
.
CHKBOK10  PACK      OPFILE,OSTFILE,FILBOKA3   * Parameter for OPNFIL00
          MOVE      SEVEN,FILENUM
          CALL      OMDFN000                * open bookings file
          BRANCH    EXIT,CHKBOK05
.
          CALL      OOTMA000            * O/P Clinic Master File
          BRANCH    EXIT,CHKBOK05
.
          PACK      OPFILE,OSTFILE,FILDIAG1
          MOVE      "69",FILENUM
          CALL      OMDFN000                * open diagnosis file
          BRANCH    EXIT,CHKBOK05
.
          PACK      OPFILE,OSTFILE,FILCLIA1
          MOVE      THIRTY6,FILENUM
          CALL      OMDFN000                * op clinic id - index 1
          BRANCH    EXIT,CHKBOK05
.
          PACK      KEY36,URNUMBER,PTCNLEDT,SP70
          CALL      RDSBOKA3
CHKBOK20  CALL      RDKBOKA3
          BRANCH    OVRCD,CHKBOK05          * no more data
.
          MATCH     URNUMBER,OBAURNO
          GOTO      CHKBOK05 IF NOT EQUAL   * no more data
.
          MATCH     DECLNVIS,DOBAOUTN
          GOTO      CHKBOK20 IF NOT EQUAL   * Visit not found
.
          COMPARE   ONE,OBASTAT
          GOTO      CHKBOK98 IF NOT EQUAL   * not booked (must be booked)
.
          MOVE      SP3,OTMAHOSP            * Get Hospital Code
          PACK      KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT
          CALL      RDMASA1
          MOVE      OTMAHOSP,HOSPCODE       * Hospital Code Set
.
          PACK      OPDIDESC,SP70
          PACK      KEY8,DECLNVIS,SP70
          CALL      RDDIAG1
          MOVE      OPDIDESC,DIAGNSIS
.
          MOVE      TEN2,VISITYPE        *  "O/P Book  "
.
.  Set Display Variables
.
          MOVE      OBAOUTNO,EVNTNUM     * Event Number
          MOVE      OBADATE,EVNTDATE     * Event Date
          MOVE      DIAGNSIS,EVNTDIAG    * Event Diagnosis
          MOVE      VISITYPE,EVNVTYPE    * Event Visit Type
          MOVE      DECLNHOS,EVNTHOSP    * Hospital Code
          MOVE      HOSPCODE,EVNTHSPL    * Hospital Code
.
          MOVE      SP70,EVNTDOC         * Doctor
          MOVE      SP70,EVNTHCP         * HCP
          MOVE      OBACLIN,PVIDOCT
          MOVE      OBASITE,PVISITE
          MOVE      OBADATE,PVIDATE
          CALL      ODOC0000             * Doctor/ Clinic
.
          MOVE      ONE,EXIT     * Booking Record found exit
          GOTO      CHKBOK99
.
CHKBOK90  WRITE     HTMLFILE;"<tr>":
                             "<td align=center><br>":
                             "<b>Invalid read on Site code file!</td></tr>";
.
CHKBOK98  MOVE      ZERO,EXIT    * Check next File
CHKBOK99  RETURN
.
.*********************************************************************
. ODOC0000 - get doctor/clinic description
.*********************************************************************
ODOC0000  MOVE      SP6,OCADOCT
          MOVE      SP70,DOCTOR
          MOVE      PVIDOCT,EVNTDOC         * Doctor
.
          PACK      KEY20,PVISITE,PVIDOCT,PVIDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     PVISITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     PVIDOCT,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      SP30,OCADESC 
          ENDIF
          MOVE      OCADESC,DOCTOR     * Save the clinic description
          BRANCH    OVRCD,ODOC9999
.
          MATCH     SP6,OCADOCT
          IF        !@EQUAL
            PACK      KEY6,OCADOCT,SP70   * Get Doctor Formatted name
            CALL      RDDOCT1
            IF        OVRCD = 0
              MOVE      DTITL,FMTTITLE
              MOVE      DGNAME,FMTGNAME
              MOVE      DSNAME,FMTSNAME
              CALL      DOCNM000
              CLEAR     DOCTOR
              APPEND    DOCFNAME,DOCTOR
              MOVE      OCADOCT,EVNTDOC   * Doctor
            ELSE
              MOVE      SP70,DOCTOR
            ENDIF
          ENDIF
.
ODOC9999  RETURN
.------------------------------------------------------------
.  Check the Inpatients Booking File for the correct record..
.  Parameters:
.------------------------------------------------------------
CHKIBK00  CALL      OBKRC000           * Open Booking File
          BRANCH    EXIT,CHKIBK98      * No File
. 
          MOVE      "bokdiaaf",OPFILE
          MOVE      "68",FILENUM
          CALL      OMDFN000                * open appropriate visit file
.
          PACK      KEY8,DECLNVIS,SP70
          CALL      RDBKREC1
          BRANCH    OVRCD,CHKIBK98
.
          COMPARE   ZERO,BKRESTAT
          GOTO      CHKIBK98 IF NOT EQUAL   * only want bookings
.
          MATCH     PTCNLEDT,BKREEDAT   * Check if Due Date < Search date
          GOTO      CHKIBK98 IF LESS
.
          MOVE      SP3,PTWDHOSN      * Get the Hospital Code (from Ward)
          PACK      KEY6,BKREWARD,SP3
          CALL      RDWARD1
          MOVE      PTWDHOSN,HOSPCODE
.
          PACK      BKDIDES1,SP70        * Get the Diagnosis
          PACK      KEY18,DECLNVIS,SP70
          CALL      RSBKDIA1
          CALL      RKBKDIA1
          BRANCH    OVRCD,CHKIBK20
          MATCH     ECLNVISI,BKDIBOOK
          IF        @EQUAL
            MOVE      BKDIDES1,DIAGNSIS    * Event Diagnosis
          ENDIF
.
CHKIBK20  MOVE      TEN,VISITYPE         * "I/P Book  "
.
.  Set Display Variables
.
          MOVE      BKREBOOK,EVNTNUM     * Event Number
          MOVE      BKREEDAT,EVNTDATE    * Event Date
          MOVE      BKREADOC,EVNTDOC     * Doctor
          MOVE      SP70,EVNTHCP         * HCP
          MOVE      VISITYPE,EVNVTYPE    * Event Visit Type
          MOVE      DIAGNSIS,EVNTDIAG    * Event Diagnosis
          MOVE      DECLNHOS,EVNTHOSP    * Hospital Unique Code
          MOVE      HOSPCODE,EVNTHSPL    * Hospital Code
.
          MOVE      ONE,EXIT   * Booking Record Found Exit
          GOTO      CHKIBK99
.
CHKIBK98  MOVE      ZERO,EXIT
CHKIBK99  RETURN
.------------------------------------------------------------
.  Check the Waiting File for the correct record..
.  Parameters:
.------------------------------------------------------------
CHKWAT00  CALL      OWTWM000                * Open Wiating List File
          BRANCH    EXIT,CHKWAT98           * No file
.
          PACK      KEY20,ONE,URNUMBER,SP70
          CALL      RDSTREA2
CHKWAT10  CALL      RDKTREA2
          BRANCH    OVRCD,CHKWAT98          * no more details
.
          MATCH     URNUMBER,WMURNO
          GOTO      CHKWAT98 IF NOT EQUAL   * no more details
.
          MATCH     ECLNVISI,WMBOOK
          GOTO      CHKWAT10 IF NOT EQUAL
.
          COMPARE   ONE,WMSTAT              * Check Procedure Status
          GOTO      CHKWAT98 IF NOT EQUAL   * no more details
.
          MATCH     PTCNLEDT,WMDATE1   * Check if Date on list < Search date
          GOTO      CHKWAT10 IF LESS
.
          MOVE      NINE,VISITYPE        * "Waiting   "
.
.  Set Display Variables
.
          MOVE      WMBOOK,EVNTNUM       * Event Number
          MOVE      WMDATE1,EVNTDATE     * Event Date
          MOVE      WMREASON,EVNTDIAG    * Event Diagnosis
          MOVE      WMDOCTOR,EVNTDOC     * Doctor
          MOVE      SP70,EVNTHCP         * HCP
          MOVE      VISITYPE,EVNVTYPE    * Event Visit Type
          MOVE      DECLNHOS,EVNTHOSP    * Hospital Unique Code
          MOVE      WTWMIHSP,EVNTHSPL    * Hospital Code
.
          MOVE      ONE,EXIT   * Waiting Record Found Exit
          GOTO      CHKWAT99
.
CHKWAT98  MOVE      ZERO,EXIT
CHKWAT99  RETURN
.------------------------------------------------------------
.  Check the Outpatient Referral File for the correct record..
.  Parameters:
.------------------------------------------------------------
CHKREF00  CALL      OOTRF000
          BRANCH    EXIT,CHKREF98
. 
          PACK      KEY8,DECLNVIS,SP70
          CALL      RDOTRFL1
          BRANCH    OVRCD,CHKREF98
.
          MATCH     SP70,OTRLBDTE
          GOTO      CHKREF98 IF NOT EQUAL   * referral is booked
.
. ------- Do display if this is the Outpat external referral for this EOC ---
. ------- Don't display if this is an other external referral for this EOC ---
.
          COMPARE   ONE,OTRLTREF              Other referral ?
          GOTO      CHKREF98 IF EQUAL         Yes
.
          MOVE      TEN6,VISITYPE
          MOVE      ECRFREFN,KEY8
          MATCH     OTRLOUTN,KEY8
          GOTO      CHKREF10 IF EQUAL
.
          MOVE      OTRLOUTN,KEY8
          PROC      CHKOCREF  * Checks if Referral is an External Referral (EOC)
          BRANCH    EXIT,CHKREF98
          MOVE      TEN1,VISITYPE
.
.  Set Display Variables
.
CHKREF10  MOVE      OTRLOUTN,EVNTNUM     * Event Number
          MOVE      OTRLDATE,EVNTDATE    * Event Date
          MOVE      OTRLDIAG,EVNTDIAG    * Event Diagnosis
          MOVE      SP70,EVNTDOC         * Doctor
          MOVE      OTRLRDOC,EVNTHCP     * HCP
          MOVE      VISITYPE,EVNVTYPE    * Event Visit Type
          MOVE      DECLNHOS,EVNTHOSP    * Hospital Code
          MOVE      SP70,EVNTHSPL        * Hospital Code ????????????
.
          MOVE      ONE,EXIT   * Outpatient Referral Record Found Exit
          GOTO      CHKREF99
.
.
CHKREF98  MOVE      ZERO,EXIT
CHKREF99  RETURN
.---------------------------------------------------------------------
.         Dummy Routine For Visits Types 4,5
.---------------------------------------------------------------------
MLDUM000  MOVE      SP3,HOSPCODE
          RETURN
.---------------------------------------------------------------------
.         Get The Diagnosis from the A&E Details File
.---------------------------------------------------------------------
MLAAE000  MOVE      SP3,HOSPCODE
          MOVE      ONE,VISITYPE
.
          PACK      ADADIAG,SP70
          PACK      KEY8,PVIBILL,SP70
          CALL      RDDETA1
          MOVE      ADADIAG,DIAGNSIS
.
MLAAE999  RETURN
.---------------------------------------------------------------------
.         get the hospital code for outpatients
.         Returns : OVRCD = 1     invalid details
.---------------------------------------------------------------------
MLOUT000  MOVE      SP3,HOSPCODE
          MOVE      TWO,VISITYPE                 * patient type
          MOVE      SP20,EVNTDOC
          MOVE      SP70,EVNTHCP
          MOVE      SP70,DIAGNSIS
.
          IF        OTCNNSID = 1
            MOVE      "out",OSTFILE
          ELSE
            MOVE      PVISITE,KEY6
            CALL      RDSITA1
            BRANCH    OVRCD,MLOUT999
          ENDIF
.
          PACK      OPFILE,OSTFILE,FILBOKA2
          MOVE      "45",FILENUM
          CALL      OMDFN000                * open bookings file
          BRANCH    EXIT,MLOUT999
.
          PACK      OPFILE,OSTFILE,FILBOKA6
          MOVE      "76",FILENUM
          CALL      OMDFN000                * open bookings file
          BRANCH    EXIT,MLOUT999

          CALL      OOTMA000        * op clinic master
          BRANCH    EXIT,MLOUT999
.
          CALL      OOTBB000        * op booking B
          BRANCH    EXIT,MLOUT999
.
          PACK      OPFILE,OSTFILE,FILDIAG1
          MOVE      "69",FILENUM
          CALL      OMDFN000                * open diagnosis file
          BRANCH    EXIT,MLOUT999
.
          PACK      OPFILE,OSTFILE,FILCLIA1
          MOVE      THIRTY6,FILENUM
          CALL      OMDFN000                * op clinic id - index 2
          BRANCH    EXIT,MLOUT999
.
.         get the booking details
.
MLOUT050  MOVE      PVIBILL,KEY8
          CALL      RDBOKB1
          BRANCH    OVRCD,MLOUT999          * no details
.
          PACK      OPDIDESC,SP30,SP30
          PACK      KEY8,PVIBILL,SP10       * diagnosis
          CALL      RDDIAG1
          MOVE      OPDIDESC,DIAGNSIS
.
          PACK      KEY36,PVIBILL,SP70
          CALL      RDSBOKA6
          CALL      RDKBOKA6
          BRANCH    OVRCD,MLOUT999
.
          MATCH     DPVIBILL,DOBAOUTN
          GOTO      MLOUT999 IF NOT EQUAL
.
          PACK      KEY18,OBASITE,OBACLIN,OBADAY,OBASTRT
          CALL      RDMASA1
          BRANCH    OVRCD,MLOUT999          * invalid clinic master
.
          MOVE      OTMAHOSP,HOSPCODE
          CALL      ODOC0000                * Doctor/ Clinic
.
MLOUT999  RETURN
.---------------------------------------------------------------------
.         get the hospital code
.         Returns : OVRCD = 1     invalid details
.---------------------------------------------------------------------
MLINP000  MOVE      SP10,HOSPCODE
.
          MOVE      PVIBILL,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,MLINP999
.
          MOVE      ADIAG1,DIAGNSIS
.
          LOAD      VISITYPE,ASTAT,SEVEN,THREE,FIVE,SIX,FOUR
.
          IF        ASTAT = ONE
            PACK      KEY6,PTMIXWRD,SP10
          ELSE
            PACK      KEY6,AWARD,SP10
          ENDIF
          CALL      RDWARD1
          BRANCH    OVRCD,MLINP999
          MOVE      PTWDHOSN,HOSPCODE
.
MLINP999  RETURN
.---------------------------------------------------------------------
.         get the hospital code for community health
.---------------------------------------------------------------------
MLCOM000  MOVE      SP3,HOSPCODE
          MOVE      EIGHT,VISITYPE
          MOVE      SP20,DIAGNSIS
          MOVE      SP20,EVNTDOC
          MOVE      SP70,EVNTHCP
MLCOM999  RETURN
.*********************************************************************
.         open the required file
.*********************************************************************
OTRF0000  IF        FILENUM = 1
            CLOSE     PATMI1A1
            OPEN      PATMI1A1,PATH
          ENDIF
          IF        FILENUM = 5
            CLOSE     AAEDE1A1
            OPEN      AAEDE1A1,PATH
          ENDIF
          IF        FILENUM = 6
            CLOSE     OUTBOKA1
            OPEN      OUTBOKA1,PATH
          ENDIF
          IF        FILENUM = 7
            CLOSE     OUTBOKA3
            OPEN      OUTBOKA3,PATH
          ENDIF
          IF        FILENUM = 8
            CLOSE     OUTBB1A1
            OPEN      OUTBB1A1,PATH
          ENDIF
          IF        FILENUM = 15
            CLOSE     PATWR1A1
            OPEN      PATWR1A1,PATH
          ENDIF
          IF        FILENUM = 18
            CLOSE     PATVISA2
            OPEN      PATVISA2,PATH
          ENDIF
          IF        FILENUM = 20
            CLOSE     PATVISA1
            OPEN      PATVISA1,PATH
          ENDIF
          IF        FILENUM = 36
            CLOSE     OUTCLIA1
            OPEN      OUTCLIA1,PATH
          ENDIF
          IF        FILENUM = 37
            CLOSE     PATDO1A1
            OPEN      PATDO1A1,PATH
          ENDIF
          IF        FILENUM = 38
            CLOSE     OUTMA1A1
            OPEN      OUTMA1A1,PATH
          ENDIF
          IF        FILENUM = 40
            CLOSE     OUTSITA1
            OPEN      OUTSITA1,PATH
          ENDIF
          IF        FILENUM = 41
            CLOSE     OUTSITA3
            OPEN      OUTSITA3,PATH
          ENDIF
          IF        FILENUM = 45
            CLOSE     OUTBOKA2
            OPEN      OUTBOKA2,PATH
          ENDIF
          IF        FILENUM = 61
            CLOSE     OUTRF1A1
            OPEN      OUTRF1A1,PATH
          ENDIF
          IF        FILENUM = 62
            CLOSE     WATTR1A2
            OPEN      WATTR1A2,PATH
          ENDIF
          IF        FILENUM = 63
            CLOSE     BOKRC1A6
            OPEN      BOKRC1A6,PATH
          ENDIF
          IF        FILENUM = 65
            CLOSE     OUTRF1A2
            OPEN      OUTRF1A2,PATH
          ENDIF
          IF        FILENUM = 68
            CLOSE     BOKDIAA1
            OPEN      BOKDIAA1,PATH
          ENDIF
          IF        FILENUM = 69
            CLOSE     OUTDIAG1
            OPEN      OUTDIAG1,PATH
          ENDIF
          IF        FILENUM = 71
            CLOSE     BOKRC1A1
            OPEN      BOKRC1A1,PATH
          ENDIF
.
          IF        FILENUM = 72
            CLOSE     ALLREFA1
            OPEN      ALLREFA1,PATH
          ENDIF
.
          IF        FILENUM = 73
            CLOSE     ALLPRRA1
            OPEN      ALLPRRA1,PATH
          ENDIF
.
          IF        FILENUM = 74
            CLOSE     PMSEVTA1
            OPEN      PMSEVTA1,PATH
          ENDIF
.
          IF        FILENUM = 75
            CLOSE     PATCODE1
            OPEN      PATCODE1,PATH
          ENDIF
.
          IF        FILENUM = 76
            CLOSE     OUTBOKA6
            OPEN      OUTBOKA6,PATH
          ENDIF
.
OTRF9999  RETURN
.---------------------------------------------------------------------
.         get the diagnosis and visit type for Other types
.         (Patient Events and Allied Health Referrals)
.---------------------------------------------------------------------
MLOTH000  MOVE      SP3,HOSPCODE
          MOVE      ZERO,VISITYPE
          MOVE      SP70,DIAGNSIS
.
          MATCH     " 1",PVISYST            * Is it Patient Event?
          GOTO      MLOTH100 IF EQUAL       * Yes, process Patient Event
.
          MATCH     " 2",PVISYST            * Is it Allied Health Referral?
          GOTO      MLOTH200 IF EQUAL       * Yes, Process Allied Health Ref
          GOTO      MLOTH999
.
MLOTH100  MOVE      SP70,MVEVN17
          MOVE      TEN7,VISITYPE
          PACK      KEY8,PVIBILL
          CALL      RDPMEVT1
          BRANCH    OVRCD,MLOTH999
.
          MOVE      PMEVHOSN,HOSPCODE
          MOVE      PMEVPPRB,DIAGNSIS
          MATCH     SP3,PMEVEVNT
          IF        !@EQUAL
            MOVE      "EV",KEY2
            PACK      KEY5,KEY2,PMEVEVNT
            CALL      RDCODE1
            BRANCH    OVRCD,MLOTH999
            MOVE      TDESC,MVEVN17
          ENDIF

          GOTO      MLOTH999
.
MLOTH200  MOVE      TEN8,VISITYPE
          PACK      KEY8,PVIBILL
          CALL      RDALREF1
          BRANCH    OVRCD,MLOTH999
.
          MOVE      ALREHOSN,HOSPCODE
          PACK      KEY12,ALREDEPT,ALREPRO1
          CALL      RDALPRR1
          BRANCH    OVRCD,MLOTH999
          MOVE      ALPRDESC,DIAGNSIS
.
MLOTH999  RETURN
.*********************************************************************
.         open the O/P booking file
.*********************************************************************
OOTBB000  PACK      OPFILE,OSTFILE,FILBB1A1
          MOVE      EIGHT,FILENUM
          CALL      OMDFN000
OOTBB999  RETURN
.*********************************************************************
.         open the outpatient master file
.*********************************************************************
OOTMA000  PACK      OPFILE,OSTFILE,FILMA1A1
          MOVE      THIRTY8,FILENUM
          CALL      OMDFN000
OOTMA999  RETURN
.*********************************************************************
.         open the ward file
.*********************************************************************
OPTWD000  MOVE      "patwr1af",OPFILE
          MOVE      TEN5,FILENUM
          CALL      OMDFN000
OPTWD999  RETURN
.*********************************************************************
.         open the waiting list details file
.*********************************************************************
OWTWM000  MOVE      "wattr1af",OPFILE
          MOVE      SIXTY2,FILENUM
          CALL      OMDFN000
OWTWM999  RETURN
+
.*********************************************************************
.         open the booking file
.*********************************************************************
OBKRC000  MOVE      "bokrc1af",OPFILE
          MOVE      SIXTY3,FILENUM
          CALL      OMDFN000
          MOVE      SEVENTY1,FILENUM
          CALL      OMDFN000
OBKRC999  RETURN
.*********************************************************************
.         open the A&E details file
.*********************************************************************
OAEDA000  MOVE      "aaede1af",OPFILE
          MOVE      FIVE,FILENUM
          CALL      OMDFN000
OAEDA999  RETURN
.*********************************************************************
.         open the outpatient referral file
.*********************************************************************
OOTRF000  MOVE      "outrf1af",OPFILE
          MOVE      SIXTY1,FILENUM
          CALL      OMDFN000
          MOVE      SIXTY5,FILENUM
          CALL      OMDFN000
OOTRF999  RETURN
+
.*********************************************************************
.         open the doctor file
.*********************************************************************
OPTDC000  MOVE      "patdo1af",OPFILE
          MOVE      THIRTY7,FILENUM
          CALL      OMDFN000
OPTDC999  RETURN
.
.---------------------------------------------------------------------
.         Open the Allied Health Referral file
.---------------------------------------------------------------------
OPNAHR00  MOVE      "allrefaf",OPFILE
          MOVE      SEVENTY2,FILENUM
          CALL      OMDFN000
.
OPNAHR99  RETURN
.---------------------------------------------------------------------
.         Open the Allied Health Problem Codes file
.---------------------------------------------------------------------
OPNAPC00  MOVE      "allprraf",OPFILE
          MOVE      SEVENTY3,FILENUM
          CALL      OMDFN000
.
OPNAPC99  RETURN
.---------------------------------------------------------------------
.         Open the Patient Events file
.---------------------------------------------------------------------
OPNPEV00  MOVE      "pmsevtaf",OPFILE
          MOVE      SEVENTY4,FILENUM
          CALL      OMDFN000
.
OPNPEV99  RETURN
.---------------------------------------------------------------------
.         Open the Patient Codes file
.---------------------------------------------------------------------
OPNPCD00  MOVE      "patcodes",OPFILE
          MOVE      SEVENTY5,FILENUM
          CALL      OMDFN000
.
OPNPCD99  RETURN
.
.---------------------------------------------------------------------------
.
          INC       IBAOVRIO/INC
          INC       PATMI1IO/INC
          INC       PATVISIO/INC
          INC       BOKRC1IO/INC
          INC       OUTCLIIO/INC
          INC       OUTCTYIO/INC
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       OUTMA1IO/INC
          INC       OUTSITIO/INC
          INC       OUTRF1IO/INC
          INC       PATHOSIO/INC
          INC       PATHSPIO/INC
          INC       PATWR1IO/INC
          INC       WATTR1IO/INC
          INC       OUTDIAIO/INC
          INC       BOKDIAIO/INC
          INC       AAEDE1IO/INC
          INC       ALLPRRIO/INC
          INC       ALLREFIO/INC
          INC       PMSEVTIO/INC
          INC       PATCODIO/INC
          INC       PATDPATH
.
ENDLINKD  ENDPROC
