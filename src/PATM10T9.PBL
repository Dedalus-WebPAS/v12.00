.******************************************************************************
.* System         : Patient Management                                        *
.* Include        : PATM10T9.PBL                                              *
.* Name           : Map ICD10 to ICD9 codes & also get complex mappings if any*
.******************************************************************************
.* Date           : 26/10/1998                                                *
.* Author         : Greg Horvat                                               *
.*                                                                            *
.* Note           : Each complex mapping format does something different to   *
.*                  get the complex map code                                  *
.*                                                                            *
.* Requires       : PATCMPFD - Complex mapping file           (Open PATCMPA1) *
.*                  PATICDFD - ICD9  disease file             (Open PATICDD1) *
.*                           - ICD10 disease file             (Open PATI10D1) *
.*                  PATICOFD - ICD9  operation file           (Open PATICDO1) *
.*                             ICD10 operation file           (Open PATI10O1) *
.*                  PATECDFD - Patient episode disease file   (Open PATECDA2) *
.*                  PATECOFD - Patient episode operation file (Open PATECOA2) *
.*                                                                            *
.* Parameters     : AADMNO   - Admission no                                   *
.*                  PTCPCOD  - ICD10 code that you want the map equivalent for*
.*                                                                            *
.* Returns        : PTCP1MCD - 1st map code                                   *
.*                  PTCP2MCD - 2nd map code                                   *
.*                  PTCP3MCD - 3rd map code                                   *
.*                                                                            *
.* Programmer Note: If any changes are made to this include they also need to *
.*                  be made in COMDSOPS, rountine PATM10T9.  That routine is  *
.*                  the same as this include but it loops thru the procedure's*
.*                  tempfile rather than patecdaf & patecoaf.                 *
.*                                                                            *
.* Modifications  :                                                           *
.*        V12.00.01 19/05/2025  J.Tan          TSK 0955096                    *
.*                  Added alpanumeric visit number generation                 *
.*        V10.02.01 25/03/2011  Mike Laming   CAR 240107                      *
.*                  Mods to PATECDaf & PATECOaf variables & Keys              *
.*        V5.08.B01 09/03/2000  Greg Horvat                                   *
.*                  Changed to get the coding from the episode coding files   *
.*        V5.07.02  14/12/1999  Greg Horvat                                   *
.*                  Fixed obstetric & tendon injury complex mapping bugs      *
.*        V5.07.01  14/10/1999  Greg Horvat                                   *
.*                  Changed for new complex mapping file structure & added    *
.*                  more complex maps                                         *
.*                                                                            *
.******************************************************************************
+
PATM10T9
M10T9000  MOVE      ZERO,ECDFLAG            * Read patecdaf flag - no
          MOVE      ZERO,ECOFLAG            * Read patecoaf flag - no
          PACK      SECDKEY,PTEDADMN,PTEDUNQN,PTEDEPNO,PTEDCNT
          PACK      SECOKEY,PTEOADMN,PTEOUNQN,PTEOEPNO,PTEOCNT
          UNPACK    SP30,PTCP1MCD,PTCP2MCD,PTCP3MCD
.
          MATCH     SP9,PTCPCOD
          GOTO      M10T9900 IF EOS         * Blank ICD10 code
          GOTO      M10T9900 IF EQUAL       * Blank ICD10 code
.
          PACK      KEY9,PTCPCOD
          CALL      RDPT10D1                * Read an ICD10 disease file record
          BRANCH    OVRCD,M10T9300
.
          MOVE      PT0DMI9C,SPT0DMI9
          CALL      CIUCM000                * Check if using complex mapping
          BRANCH    EXIT,M10T9200
.
          PACK      KEY20,PT0DCMFT,PTCPCOD,SP20
          CALL      RDPTCMP1                * Read a complex mapping file record
          COMPARE   ONE,OVRCD
          GOTO      M10T9100 IF NOT EQUAL   * Record exists
.
          CALL      RKPTCMP1                * Read a complex mapping file record
          BRANCH    OVRCD,M10T9200
.
          COMPARE   PT0DCMFT,PTCPCMFT
          GOTO      M10T9200 IF NOT EQUAL   * Different complex map format
.
          MATCH     PTCPCOD,PTCP1PCD
          GOTO      M10T9200 IF NOT EQUAL   * Different ICD disease code
.
M10T9100  PERFORM   PTCPCMFT,LTDIS000,LTDIS000,TENDI000,RETUR000,SOCIN000:
.                            Obsteric Head inj Tend inj Pacemake Soc indu
                             RETUR000,RETUR000,TTTMD000
.                            Med augm 2-3 maps 2-3 maps
          COMPARE   ONE,EXIT
          GOTO      M10T9800 IF NOT EQUAL   * Valid complex map codes
.
M10T9200  PACK      KEY9,SPT0DMI9
          CALL      RD10T9D1                * Read an ICD9 disease file record
          IF        OVRCD=1
            MOVE      SP9,PTCP1MCD
          ELSE
            MOVE      DPCODE,PTCP1MCD
          ENDIF
          UNPACK    SP20,PTCP2MCD,PTCP3MCD
          GOTO      M10T9800
.
M10T9300  PACK      KEY9,PTCPCOD
          CALL      RDPT10O1                * Read an ICD10 operation file rec
          BRANCH    OVRCD,M10T9900
.
          MOVE      PT0OMI9C,SPT0OMI9
          CALL      CIUCM000                * Check if using complex mapping
          BRANCH    EXIT,M10T9500
.
          PACK      KEY20,PT0OCMFT,PTCPCOD,SP20
          CALL      RDPTCMP1                * Read a complex mapping file record
          COMPARE   ONE,OVRCD
          GOTO      M10T9400 IF NOT EQUAL   * Record exists
.
          CALL      RKPTCMP1                * Read a complex mapping file record
          BRANCH    OVRCD,M10T9500
.
          COMPARE   PT0OCMFT,PTCPCMFT
          GOTO      M10T9500 IF NOT EQUAL   * Different complex map format
.
          MATCH     PTCPCOD,PTCP1PCD
          GOTO      M10T9500 IF NOT EQUAL   * Different ICD operation code
.
M10T9400  PERFORM   PTCPCMFT,RETUR000,RETUR000,RETUR000,PACEM000,RETUR000:
.                            Obsteric Head inj Tend inj Pacemake Soc indu
                             MEDAU000,TTTMO000,RETUR000
.                            Med augm 2-3 maps 2-3 maps
          COMPARE   ONE,EXIT
          GOTO      M10T9800 IF NOT EQUAL   * Valid complex map codes
.
M10T9500  PACK      KEY9,SPT0OMI9
          CALL      RD10T9O1                * Read an ICD9 operation file record
          IF        OVRCD=1
            MOVE      SP9,PTCP1MCD
          ELSE
            MOVE      OPCODE,PTCP1MCD
          ENDIF
          UNPACK    SP20,PTCP2MCD,PTCP3MCD
.
M10T9800  MOVE      SECDKEY,KEY16
          PERFORM   ECDFLAG,RDPTECD2        * Read a pat eps dis file rec
.
          MOVE      SECOKEY,KEY16
          PERFORM   ECOFLAG,RDPTECO2        * Read a pat eps opr file rec
.
M10T9900  MOVE      ZERO,EXIT
M10T9999  RETURN
+
.******************************************************************************
.*                                  CIUCM000              Called by: M10T9000 *
.*                        Check If Usng Complex Mapping                       *
.******************************************************************************
CIUCM000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATCMPA1,"patcmpaf"
          TRAPCLR   IO
          BRANCH    OVRCD,CIUCM950          * Comp mapping file not found
.
          PACK      KEY20,SP20
          CALL      RSPTCMP1                * Position on & read a complex
          CALL      RKPTCMP1                  mapping file record
          BRANCH    OVRCD,CIUCM950
.
CIUCM900  MOVE      ZERO,EXIT
          GOTO      CIUCM999
.
CIUCM950  MOVE      ONE,EXIT
CIUCM999  RETURN
+
.******************************************************************************
.*                                  LTDIS000              Called by: M10T9000 *
.*                           Loop Thru Disease File                           *
.******************************************************************************
LTDIS000  CALL      LACMC000                * Load array with complex map codes
          MOVE      ONE,ECDFLAG             * Read patecdaf flag - yes
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
LTDIS100  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,LTDIS300
.
          MATCH     AADMNO,PTEDADMN
          GOTO      LTDIS300 IF NOT EQUAL   * Different admin no
.
          MATCH     PTCPCOD,PTEDCODE
          GOTO      LTDIS100 IF EQUAL       * Dont validate map code
.
          MOVE      ONE,FORM2
LTDIS200  MOVE      PTCPARY[FORM2],KEY9
          MATCH     SP9,KEY9
          GOTO      LTDIS100 IF EQUAL       * No complex map code found
.
          MATCH     PTEDCODE,KEY9
          GOTO      LTDIS400 IF EQUAL       * 2nd primary code found
.
          ADD       ONE,FORM2
          COMPARE   "21",FORM2
          GOTO      LTDIS200 IF LESS        * Counter < 21
          GOTO      LTDIS100
.
LTDIS300  MOVE      "xxxxxxxxx",KEY9        * Check with else code
LTDIS400  CALL      VCMDC000                * Validate complex map disease codes
          BRANCH    EXIT,LTDIS950
.
LTDIS900  MOVE      ZERO,EXIT
          GOTO      LTDIS999
.
LTDIS950  MOVE      ONE,EXIT
LTDIS999  RETURN
+
.******************************************************************************
.*                                  TENDI000              Called by: M10T9000 *
.*                Loop Thru Disease File For Tendon Injury Map                *
.******************************************************************************
TENDI000  MOVE      ONE,ECDFLAG             * Read patecdaf flag - yes
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
          CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,TENDI950
.
          MATCH     AADMNO,PTEDADMN
          GOTO      TENDI950 IF NOT EQUAL   * Different admin no
.
          MATCH     PTCPCOD,PTEDCODE
          GOTO      TENDI950 IF NOT EQUAL   * Different principal dx, exit
.
          CALL      RKPTECD2                * Read a pat eps dis file rec
          BRANCH    OVRCD,TENDI950
.
          MATCH     AADMNO,PTEDADMN
          GOTO      TENDI950 IF NOT EQUAL   * Different admin no
.
          PACK      KEY20,PT0DCMFT,PTCPCOD,PTEDCODE
          CALL      RDPTCMP1                * Read a complex mapping file record
          BRANCH    OVRCD,TENDI950
.
          MOVE      PTEDCODE,KEY9           * Complex map found
          CALL      VCMDC000                * Validate complex map disease codes
          BRANCH    EXIT,TENDI950
.
TENDI900  MOVE      ZERO,EXIT
          GOTO      TENDI999
.
TENDI950  MOVE      ONE,EXIT
TENDI999  RETURN
+
.******************************************************************************
.*                                  PACEM000              Called by: M10T9000 *
.*                 Loop Thru Operation File For Pacemaker Map                 *
.******************************************************************************
PACEM000  MOVE      ONE,ECOFLAG             * Read patecoaf flag - yes
          MOVE      SECOKEY,KEY16
          CALL      RDPTECO2                * Read a pat eps opr file rec
          BRANCH    OVRCD,PACEM950
.
          MATCH     PTCPCOD,PTEOCODE
          GOTO      PACEM950 IF NOT EQUAL   * Not the map code required
.
          CALL      RKPTECO2                * Read a opr eps dis file rec
          BRANCH    OVRCD,PACEM950
.
          MATCH     AADMNO,PTEOADMN
          GOTO      PACEM950 IF NOT EQUAL   * Different admin no
.
          PACK      KEY20,PT0OCMFT,PTCPCOD,SP20
          CALL      RSPTCMP1                * Position & read a complex mapping
PACEM100  CALL      RKPTCMP1                  file record
          BRANCH    OVRCD,PACEM950
.
          COMPARE   PT0OCMFT,PTCPCMFT
          GOTO      PACEM950 IF NOT EQUAL   * Different complex map format
.
          MATCH     PTCPCOD,PTCP1PCD
          GOTO      PACEM950 IF NOT EQUAL   * Different ICD operation code
.
          MATCH     PTEOCODE,PTCP2PCD
          GOTO      PACEM100 IF NOT EQUAL   * Different 2nd prim code & opr code
.
          MOVE      PTEOCODE,KEY9           * Complex map found
          CALL      VCMOC000                * Validate complex map operat codes
          BRANCH    EXIT,PACEM950
.
PACEM900  MOVE      ZERO,EXIT
          GOTO      PACEM999
.
PACEM950  MOVE      ONE,EXIT
PACEM999  RETURN
+
.******************************************************************************
.*                                  SOCIN000              Called by: M10T9000 *
.*        Loop Thru Disease & Operation Files For Social Induction Map        *
.******************************************************************************
SOCIN000  MOVE      ONE,ECDFLAG             * Read patecdaf flag - yes
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
          CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,SOCIN950
.
          MATCH     AADMNO,PTEDADMN
          GOTO      SOCIN950 IF NOT EQUAL   * Different admin no
.
          MATCH     PTCPCOD,PTEDCODE
          GOTO      SOCIN950 IF NOT EQUAL   * Not the map code required
.
          CALL      LACMC000                * Load array with complex map codes
          MOVE      ONE,ECOFLAG             * Read patecoaf flag - yes
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
SOCIN100  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,SOCIN950
.
          MATCH     AADMNO,PTEOADMN
          GOTO      SOCIN950 IF NOT EQUAL   * Different admin no
.
          MOVE      ONE,FORM2
SOCIN200  MOVE      PTCPARY[FORM2],KEY9
          MATCH     SP9,KEY9
          GOTO      SOCIN100 IF EQUAL       * No complex map code found
.
          MATCH     PTEOCODE,KEY9
          GOTO      SOCIN300 IF EQUAL       * 2nd primary code found
.
          ADD       ONE,FORM2
          COMPARE   "21",FORM2
          GOTO      SOCIN200 IF LESS        * Counter < 21
          GOTO      SOCIN100
.
SOCIN300  CALL      VCMDC000                * Validate complex map disease codes
          BRANCH    EXIT,SOCIN950
.
SOCIN900  MOVE      ZERO,EXIT
          GOTO      SOCIN999
.
SOCIN950  MOVE      ONE,EXIT
SOCIN999  RETURN
+
.******************************************************************************
.*                                  MEDAU000              Called by: M10T9000 *
.*             Loop Thru Disease File For Medical Augmentation Map            *
.******************************************************************************
MEDAU000  MOVE      ONE,ECDFLAG             * Read patecdaf flag - yes
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
          CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,MEDAU950
.
          MATCH     AADMNO,PTEDADMN
          GOTO      MEDAU950 IF NOT EQUAL   * Different admin no
.
          PACK      KEY20,PT0OCMFT,PTCPCOD,PTEDCODE
          CALL      RDPTCMP1                * Read a complex mapping file record
          BRANCH    OVRCD,MEDAU950
.
MEDAU900  MOVE      ZERO,EXIT
          GOTO      MEDAU999
.
MEDAU950  MOVE      ONE,EXIT
MEDAU999  RETURN
+
.******************************************************************************
.*                                  TTTMO000              Called by: M10T9000 *
.*                           2-3 Maps For Operations                          *
.******************************************************************************
TTTMO000  MOVE      SP9,KEY9
          CALL      VCMOC000                * Validate complex map operat codes
          BRANCH    EXIT,TTTMO950
.
TTTMO900  MOVE      ZERO,EXIT
          GOTO      TTTMO999
.
TTTMO950  MOVE      ONE,EXIT
TTTMO999  RETURN
+
.******************************************************************************
.*                                  TTTMD000              Called by: M10T9000 *
.*                            2-3 Maps For Diseases                           *
.******************************************************************************
TTTMD000  MOVE      SP9,KEY9
          CALL      VCMDC000                * Validate complex map disease codes
          BRANCH    EXIT,TTTMD950
.
TTTMD900  MOVE      ZERO,EXIT
          GOTO      TTTMD999
.
TTTMD950  MOVE      ONE,EXIT
TTTMD999  RETURN
+
RETUR000  RETURN                            * Dummy return label
+
.******************************************************************************
.*                                  LACMC000              Called by: LTDIS000 *
.*                      Load Array With Complex Map Codes          & SOCIN000 *
.******************************************************************************
LACMC000  MOVE      ONE,FORM2
          REPEAT
            MOVE      SP9,PTCPARY[FORM2]
            ADD       ONE,FORM2
          UNTIL     FORM2>=21
.
          MOVE      ONE,FORM2
          PACK      KEY20,PT0DCMFT,PTCPCOD,SP20
          CALL      RSPTCMP1                * Position & read a complex mapping
LACMC100  CALL      RKPTCMP1                  file record
          BRANCH    OVRCD,LACMC900
.
          COMPARE   PT0DCMFT,PTCPCMFT
          GOTO      LACMC900 IF NOT EQUAL   * Different complex map format
.
          MATCH     PTCPCOD,PTCP1PCD
          GOTO      LACMC900 IF NOT EQUAL   * Different ICD disease code
.
          MATCH     "xxxxxxxxx",PTCP2PCD
          GOTO      LACMC100 IF EQUAL       * Else code dont load
.
          MOVE      PTCP2PCD,PTCPARY[FORM2]
          ADD       ONE,FORM2
          COMPARE   "21",FORM2
          GOTO      LACMC100 IF LESS        * Counter < 21
.
LACMC900  MOVE      ZERO,EXIT
LACMC999  RETURN
+
.******************************************************************************
.*                                  VCMDC000              Called by: Lots     *
.*                     Validate Complex Map Disease Codes                     *
.******************************************************************************
VCMDC000  PACK      KEY20,PT0DCMFT,PTCPCOD,KEY9
          CALL      RDPTCMP1                * Read a complex mapping file record
          BRANCH    OVRCD,VCMDC950
.
          PACK      KEY9,PTCP1MCD
          CALL      RD10T9D1                * Read an ICD9 disease file record
          BRANCH    OVRCD,VCMDC950
.
          MATCH     SP9,PTCP2MCD
          GOTO      VCMDC900 IF EQUAL       * Blank 2nd map code
.
          PACK      KEY9,PTCP2MCD
          CALL      RD10T9D1                * Read an ICD9 disease file record
          BRANCH    OVRCD,VCMDC950
.
          MATCH     SP9,PTCP3MCD
          GOTO      VCMDC900 IF EQUAL       * Blank 3rd map code
.
          PACK      KEY9,PTCP3MCD
          CALL      RD10T9D1                * Read an ICD9 disease file record
          BRANCH    OVRCD,VCMDC950
.
VCMDC900  MOVE      ZERO,EXIT
          GOTO      VCMDC999
.
VCMDC950  MOVE      ONE,EXIT
VCMDC999  RETURN
+
.******************************************************************************
.*                                  VCMOC000              Called by: PACEM000 *
.*                    Validate Complex Map Operation Codes         & TTTMO000 *
.******************************************************************************
VCMOC000  PACK      KEY20,PT0OCMFT,PTCPCOD,KEY9
          CALL      RDPTCMP1                * Read a complex mapping file record
          BRANCH    OVRCD,VCMOC950
.
          PACK      KEY9,PTCP1MCD
          CALL      RD10T9O1                * Read an ICD9 operation file record
          BRANCH    OVRCD,VCMOC950
.
          MATCH     SP9,PTCP2MCD
          GOTO      VCMOC900 IF EQUAL       * Blank 2nd map code
.
          PACK      KEY9,PTCP2MCD
          CALL      RD10T9O1                * Read an ICD9 operation file record
          BRANCH    OVRCD,VCMOC950
.
          MATCH     SP9,PTCP3MCD
          GOTO      VCMOC900 IF EQUAL       * Blank 3rd map code
.
          PACK      KEY9,PTCP3MCD
          CALL      RD10T9O1                * Read an ICD9 operation file record
          BRANCH    OVRCD,VCMOC950
.
VCMOC900  MOVE      ZERO,EXIT
          GOTO      VCMOC999
.
VCMOC950  MOVE      ONE,EXIT
VCMOC999  RETURN
+
.

