. Program  : PRIUPURE
.
. Function : Routine to Write/Update Private Practice Un-reconciled invoice file
.            (priureaf) after Input Receipts/Journals/Credit Notes
.
. Parameter: KEY8 - Invoice number
.
.*******************************************************************************
. Modification :
.            V12.00.01 24/07/2025  J.Tan          TSK 0962500
.                      Mod Cash to check for U/R number
.            V11.03.01 04/05/2023 J.Tan    TSK 0847250
.                      Fixed updating payment amount
.            V11.03.00 09/03/2023 J.Tan    TSK 0847250
.                      Created
.*******************************************************************************
.
          DEFPROC   PRIUPURE
.
          INC       PRIHDBFD/INC
          INC       PRIUREFD/INC
          INC       RCPREGFD/INC
          INC       RCPDTEFD/INC
          INC       RCPBNKFD/INC
.
F12P2     FORM      12.2
FORM12    FORM      12
SAVKEY12  DIM       12
FORM12P2  FORM      12.2
.
UREC0000  
          OPEN      RCPDTEA1,"rcpdteaf"
          OPEN      RCPREGA1,"rcpregaf"
          OPEN      PRIUREA1,"priureaf"
          OPEN      RCPBNKA1,"rcpbnkaf"
          OPEN      RCPDTEA3,"rcpdteaf"
          OPEN      PRIHDBT2,"prihdbtf"
.
          CALL      RDPRIN1
          BRANCH    OVRCD,UREC9999
.
          MOVE      PRINUNIQ,KEY8
          CALL      RDPRHD2
          BRANCH    OVRCD,UREC9999
.
.         Check if invoice is reconciled after Receipts/Credit notes/Journals
.
          MOVE      ZERO,F12P2
          MOVE      PRINNUMB,FORM12
          MOVE      FORM12,SAVKEY12
          PACK      KEY29,SAVKEY12,SP70
          CALL      RSRCDTE3
UREC1000  CALL      RKRCDTE3
          BRANCH    OVRCD,UREC2000
.
          MATCH     SAVKEY12,RCDTINVN       * Same invoice number?
          GOTO      UREC2000 IF NOT EQUAL
.
          MATCH     PRINDEBT,RCDTURNO       * Same U/R?
          GOTO      UREC1000 IF NOT EQUAL
.
          PACK      KEY12,RCDTRECN,SP70
          CALL      RDRCBNK1
          BRANCH    OVRCD,UREC1000
          MATCH     "1",RCBNSTAT
          GOTO      UREC1000 IF EQUAL       * Receipt cancelled
.
          MOVE      RCDTREGI,KEY3
          CALL      RDREGA1
          BRANCH    OVRCD,UREC1000
.
          COMPARE   "2",REGIBACD
          GOTO      UREC1200 IF EQUAL       * Private Practice
.
          COMPARE   "96",REGIBACD
          GOTO      UREC1000 IF NOT EQUAL  * NOT IP Deposits
.
UREC1200  ADD       RCDTAMNT,F12P2       * payment
          GOTO      UREC1000
.
UREC2000  MOVE      PRINITOT,FORM12P2     * Invoice amount +
          SUB       F12P2,FORM12P2        * Payments
          ADD       PRINCNTT,FORM12P2     * Credit Notes +
          ADD       PRINJAMT,FORM12P2     * Journals = amount outstanding
          ADD       PRINGSTJ,FORM12P2     * Journal GST = amount outstanding
.
          CALL      RECN0000              * Update Unreconciled invoice
.
UREC9999  GOTO      NURE9999
+
.-------------------------------------------------------------------------------
.         If invoice balanced, removed record from Unreconciled invoice file
.         if invoice not balanced, write or update the Unreconciled invoice file
.-------------------------------------------------------------------------------
RECN0000  PACK      KEY8,PRINNUMB
          CALL      RDPRURE1
.
          CALL      IBACLOCK
          IF        FORM12P2=0
            IF        OVRCD=0
              CALL      DEPRURE1              * Delete Un-reconciled record
            ENDIF
          ELSE
            IF        OVRCD=0
              MOVE      F12P2,PRURPAYM        * Payment
              MOVE      PRINCNTT,PRURJRNL     * Credit Notes +
              ADD       PRINJAMT,PRURJRNL     * Journals = amount outstanding
              ADD       PRINGSTJ,PRURJRNL     * Journal GST = amount outstanding
              MOVE      FORM12P2,PRUROSTD     * Outstanding amount
.
              PACK      PRURCDAT,CCC,CYY,CMM,CDD
              REP       " 0",PRURCDAT
              MOVE      CTIMEIS,PRURCTIM
              MOVE      WBSEUID,PRURCUID        * WEB user id
              CALL      UPPRURE1              * Update outstanding amount
            ELSE
              CALL      SETU0000              * Setup Unreconciled inv.fields
              CALL      WRPRURE1
            ENDIF
          ENDIF
.
RECN9999  RETURN
+
.------------------------------------------------------------
.         Setup fields for the Unreconciled invoices file
.------------------------------------------------------------
SETU0000  CALL      CLPRIURE             * Clear variables
.
          MOVE      PRINNUMB,PRURINVN    * Invoice number
          MOVE      PRINDEBT,PRURDEBT    * Debtor number
          MOVE      PRINSCAN,PRURSCAN    * Scan flag
          MOVE      "0",PRURSTAT         * New status
.
          MOVE      PRINPRAC,PRURMPRA    * Medical practice
          MOVE      PRINDOCT,PRURSDOC    * Service doctor
          MOVE      PRINCLAM,PRURCLMN    * Claim code
          MOVE      PRHDHFND,PRURHFND    * Health fund code
.
          MOVE      PRINITOT,PRURINVT    * Invoice total
          MOVE      F12P2,PRURPAYM       * Payment
          MOVE      PRINCNTT,PRURJRNL    * Credit Notes +
          ADD       PRINJAMT,PRURJRNL    * Journals = amount outstanding
          ADD       PRINGSTJ,PRURJRNL    * Journal GST = amount outstanding
          MOVE      FORM12P2,PRUROSTD    * Outstanding amount
.
          CALL      IBACLOCK
          PACK      PRURCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PRURCDAT
          MOVE      CTIMEIS,PRURCTIM
          PACK      PRURCUID,WBSEUID,SP70            * WEB user id
.
          PACK      PRURUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PRURUDAT
          MOVE      CTIMEIS,PRURUTIM
          PACK      PRURUUID,WBSEUID,SP70            * WEB user id
.
SETU9999  RETURN
+
          INC       CLPRIURE
          INC       PRIHDBIO/INC
          INC       PRIUREIO/INC
          INC       RCPREGIO/INC
          INC       RCPDTEIO/INC
          INC       RCPBNKIO/INC
          INC       IBAOVRIO/INC
.
NURE9999  ENDPROC
.
