.**********************************************************************
.*                                                                    *
.*                 MRTLOCDS  : Display Locations                      *
.*                                                                    *
.*         This include displays all Internal, External, or both      *
.*         locations in the locations file.                           *
.*         MRTLOCA1 and MRTLOCA2 must be opened.                      *
.*                                                                    *
.*    AUTHOR           :       DIG     25/05/89                       *
.*                                                                    *
.*    VARIABLES NEEDED :       LOCATNS   FORM 1                       *
.*                             HOMEONLY  1=home locs only  0=all      *
.*                                                                    *
.*    MODS             :                                              *
.*        V10.02.01 04/07/2011  Ebon Clements  CAR 240724             *
.*                  Mods for MRT hospital/location - Added hospital   *
.*                  to location key                                   *
.*        V5.07.01  13/07/1999  Greg Horvat                           *
.*                  Replaced certain variables & with KEY variables   *
.*                             DIG     08/09/89                       *
.*                             Modified to extract all Internal,      *
.*                             External, or both Location Codes as    *
.*                             required.                              *
.*                                                                    *
.*                             28/11/89 Graeme Williams               *
.*                             Added the parameter CNEWDS to determine*
.*                             if the include is to use the new       *
.*                             standards for "?" routines             *
.**********************************************************************
MRTLOCDS  MOVE      ONE,COUNT
          BRANCH    LOCATNS,LOCDS050,LOCDS050
          MOVE      SP7,KEY7
          CALL      RSMRLOC1
          CALL      RKMRLOC1
          BRANCH    OVRCD,LOCDS200
          GOTO      LOCDS070
.
LOCDS050  PACK      KEY8,LOCATNS,SP4
          CALL      RSMRLOC2
          CALL      RKMRLOC2
          BRANCH    OVRCD,LOCDS200
          MOVE      MRLOINDC,FORM1
          COMPARE   LOCATNS,FORM1
          GOTO      LOCDS200 IF NOT EQUAL
.
.------ Display Category Heading, and then loop over entries ------
.
LOCDS070  DISPLAY   *P1:3,*EF,*P30:4,*V2LON,*ULON,"LOCATIONS";
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
.
....      TRAP      LOCDS300 IF F1
.
          BRANCH    LOCATNS,LOCDS080,LOCDS080
          MOVE      SP7,KEY7 
          CALL      RSMRLOC1
          GOTO      LOCDS100
.
LOCDS080  PACK      KEY8,LOCATNS,SP4
          CALL      RSMRLOC2
          GOTO      LOCDS110
.
LOCDS100  BRANCH    LOCATNS,LOCDS110,LOCDS110
          CALL      RKMRLOC1
          BRANCH    OVRCD,LOCDS400
          GOTO      LOCDS180
.
LOCDS110  CALL      RKMRLOC2
          BRANCH    OVRCD,LOCDS400
          MOVE      MRLOINDC,FORM1
          COMPARE   LOCATNS,FORM1
          GOTO      LOCDS400 IF NOT EQUAL
.
.-------- test for active records only -----
.
LOCDS180  COMPARE   "0",MRLOSTAT                     * 0 = Active
          GOTO      LOCDS100 IF NOT EQUAL
.
          IF        HOMEONLY=1
            MATCH     MRCNHMTY,MRLOTYPE             * desire home locats only
            GOTO      LOCDS100 IF NOT EQUAL
          ENDIF
.
          COMPARE   TWENTY3,CVERT
          GOTO      LOCDS185 IF LESS
.
          MOVE      FIVE,CVERT
          ADD       "40",CCOL
          COMPARE   "70",CCOL
          GOTO      LOCDS185 IF LESS
.
LOCDS183  MOVE      TWO,FORM1
          KEYIN     *P1:24,*EL,"Select item, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen, (":
                    *V2LON,"E",*HOFF,")xit ? ",*JR,*V2LON,KEY2;
.
          ENDSET    KEY2
          APPEND    SP2,KEY2
          RESET     KEY2
.
          TYPE      KEY2
          GOTO      LOCDS500 IF EQUAL
          REP       UPPLOW,KEY2
.
          REP       "F1E2N3",KEY2
          TYPE      KEY2
          GOTO      LOCDS183 IF NOT EQUAL
          MOVE      KEY2,FORM2
          BRANCH    FORM2,MRTLOCDS,LOCDS800,LOCDS184
          GOTO      LOCDS183
.
LOCDS184  MOVE      FIVE,CVERT                    * Next screen selected
          MOVE      ONE,CCOL
          MOVE      ONE,COUNT
          DISPLAY   *P1:5,*EF
.
LOCDS185  MOVE      MRLODESC,KEY30
          DISPLAY   *PCCOL:CVERT,*V2LON,COUNT,*HOFF,". ",MRLOHOSP," - ":
                     MRLOCODE,SP2:
                    *HOFF,KEY30;
          MOVE      MRLOCODE,DISPARRY[COUNT]
          ADD       ONE,COUNT
          ADD       ONE,CVERT
          GOTO      LOCDS100
.
.------ If missing category show code missing ------
.
LOCDS200  DISPLAY   *P1:3,*EF,*P30:4,*V2LON,*BLINKON:
                    "No Locations on file";
          GOTO      LOCDS400
.
LOCDS300  NORETURN
....      TRAPCLR   F1
.
.------ End of codes ------
.
LOCDS400  MOVE      ONE,FORM1
          KEYIN     *P1:24,*EL,"Select Item, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"E",*HOFF,")xit ? ",*JR,*V2LON,KEY2;
          ENDSET    KEY2
          APPEND    SP2,KEY2
          RESET     KEY2
.
          TYPE      KEY2
          GOTO      LOCDS500 IF EQUAL
          REP       UPPLOW,KEY2
.
          REP       "F1E2",KEY2
          TYPE      KEY2
          GOTO      LOCDS400 IF NOT EQUAL
          MOVE      KEY2,FORM2
          BRANCH    FORM2,MRTLOCDS,LOCDS800
          GOTO      LOCDS400
.
LOCDS500  MOVE      KEY2,FORM2                    * move to form field
.
          IF        FORM2=0
            BRANCH    FORM1,LOCDS400,LOCDS183
          ENDIF
.
          IF        (FORM2<0) | (FORM2>=COUNT)
            BEEP
            BRANCH    FORM1,LOCDS400,LOCDS183
          ENDIF
.
          MOVE      DISPARRY[FORM2],LOCATION      * get record selected
          MOVE      ZERO,EXIT
          GOTO      LOCDS900
.
LOCDS800  MOVE      ONE,EXIT                      * Exit
.
LOCDS900  RETURN
+
