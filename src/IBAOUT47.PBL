.*****************************************************************************
.*                                                                           *
.*              P R O G R A M  S U M M A R Y                                 *
.*              -------------  -------------                                 *
.*                                                                           *
.*     PROGRAM NAME:     IBAOUT47                                            *
.*                                                                           *
.*     FUNCTION:         NEXT AVAILABLE APPOINTMENTS LIST                    *
.*                                                                           *
.*     DATE WRITTEN:     18/07/88                                            *
.*                                                                           *
.*     AUTHOR:           N. SPIJKMAN                                         *
.*                                                                           *
.*     MODIFICATIONS:    _copy IBAPAT52                                      *
.*                       _no option for surname sequence                     *
.*      V10.05.01  15/07/2014  Ebon Clements     CAR 261630                  *
.*                 Removed port number from temp file name                   *
.*      V5.08.B01  13/03/2000  Steve Armstrong   5.08 Mods                   *
.*                 Mods for changes to OUTCLIFD (effective date)             *
.*****************************************************************************
.*      V5.07.B01  Nicole Harrington 507 rebound 095                         *
.*                 Mods to use HEAD132                                       *
.*****************************************************************************
. *                       V4.00 01/10/88 M.HAYSE  (I.B.A.) *
. *                             MISSING DATA               *
. *                       V5.01 05.05.89 S.Barcham         *
. *                             Fix 11263 I * C            *
. *                             SRF100436                  *
. *                       V5.02 03.06.89 S.Barcham         *
. *                             Split OUTBOK & OUTMAS      *
. *                             SRF100751                  *
. *                       V5.03 24.06.89 S.Barcham         *
. *                             Recompiled For OUTBB1FD    *
. *                       V5.04 04.07.89 E.Phan            *
. *                             Temp record length should  *
. *                             be 52 not 51.  Print skip- *
. *                             ping down 1 line at every  *
. *                             date print. Date scan not  *
. *                             starting at current date.  *
. *                             Fixed & fixed & fixed.     *
. *                             Added "TO" date to stop    *
. *                             scan.  SRF 211.            *
. *                    V5.01.01 16/07/89 K.Peak            *
. *                             Changed Adm & U/R to 8 chrs*
. *                    V5.01.02 06.12.89 D.Di.Paola        *
. *                             Fixed extraction routine   *
. *                             for when writing to temp   *
. *                             file. Also fixed various   *
. *                             other bugs....             *
. *                             SRF 103033                 *
. *                    V5.01.03 20/02/90 J.Tan  SRF 103726 *
. *                             Fixed display code descrip.*
. *                    V5.01.04 11/07/90 Paul Duncan       *
. *                          recomp for OUTBOA & OUTBOB aud*
. *                                                        *
. **********************************************************
.
          INC       STD001FD
.
          INC       IBASEQFD/INC
          INC       OUTBOAFD/INC       * Clinic booking file
          INC       OUTBB1FD/INC       * Clinic booking file
          INC       OUTCTYFD/INC       * Clinic type file
          INC       OUTCONFD/INC
          INC       OUTCLIFD/INC       * Clinic ID file
          INC       PATDO1FD/INC       * Doctor file
          INC       PATCODFD/INC       * Codes file - for visit type
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
CMDLINE   DIM       30
CMDKILL   DIM       30
CODE      DIM       2
CURDATE   DIM       8
CODECV    INIT      "CV"
.
DESCTYP1  DIM       30        * store start clinic type description
DESCTYP2  DIM       30        * store end clinic type description
DESCID    DIM       30        * store clinic ID description
.
ENDCTYP   DIM       6
.
.
YDATE     DIM       8
NEWDATE   DIM       8
NEWTIME   DIM       5
SPCDATE   DIM       8
SPCTIME   DIM       5
REVDATE   DIM       8
REVTIME   DIM       5
.
OUTTMP    DIM       8          /* Filename for temp file         */
OUTTMPXX  IFILE     SQL, FIXED=52, KEY="U1-12"
.
.  ----- Tempfile consists of following vars ------
.        -----------------------------------
.           Type     Length    Physical   Start
.           ----     ------    --------   -----
.OBACTYP     DIM       6          6        1
.OBACLIN     DIM       6          6        7
.NEWDATE     DIM       8          8       13
.NEWTIME     DIM       5          5       21
.SPCDATE     DIM       8          8       26
.SPCTIME     DIM       5          5       34
.REVDATE     DIM       8          8       39
.REVTIME     DIM       5          5       47
. ----- EOR 52 -----
. 
PRTFLAG   FORM      1
OUT       INIT      "out"
TMP       INIT      "tmp"
TODATE    DIM       8
.
SAVCTYP   DIM       6
SAVINDC   DIM       1
SITE      DIM       6
STRCTYP   DIM       6
.
PRGID     INIT      "IBAOUT47"
PRGNAM    INIT      "Next Available Appointments"
VERSION   INIT      "V12.00.00"
+
.         Start of program
.
          CALL      DISPHEAD
          DISPLAY   *P56:24,"Opening";
          MOVE      IDDD,SITE
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,OUTTMP
.
          CLEAR     CMDKILL
          APPEND    "iserase ",CMDKILL
          APPEND    OUTTMP,CMDKILL
          RESET     CMDKILL
.
          EXECUTE   CMDKILL,TASKID        * Delete old temp file
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    OUTTMP,CMDLINE
          APPEND    " 52 U1-12",CMDLINE
          RESET     CMDLINE
.
          PACK      CFNAME WITH UID,FILCTYA1
          DISPLAY   *P64:24,CFNAME
          OPEN      OUTCTYA1,CFNAME
.
          PACK      CFNAME WITH UID,FILBOKA2
          DISPLAY   *P64:24,CFNAME
          OPEN      OUTBOKA2,CFNAME
.
          PACK      CFNAME WITH UID,FILCLIA1
          DISPLAY   *P64:24,CFNAME
          OPEN      OUTCLIA1,CFNAME
.
          DISPLAY   *P64:24,"patcodes"
          OPEN      PATCODE1,"patcodes"
.
.       Main menu
.
START     DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Clinic Type Sequence"
.
REPEAT    KEYIN     *P1:7,*EL,"Please Select : ",*V2LON,OPTION
.
          COMPARE   ZERO,OPTION
          GOTO      ENDIT IF EQUAL
          BRANCH    OPTION TO GETCL100
          BEEP
          GOTO      REPEAT
.
.------------------------------------------------------------------------------
.         Enter Starting clinic type
.------------------------------------------------------------------------------
.
GETCL100  MOVE      SP6,STRCTYP
.
          DISPLAY   *P1:9,*EF,"Start Clinic Type :":
                    *P1:10,"End   Clinic Type :"
          KEYIN     *P21:9,*V2LON,STRCTYP
.
          ENDSET    STRCTYP
          APPEND    SP6,STRCTYP
          RESET     STRCTYP
.
          MATCH     SP6,STRCTYP
          GOTO      GETCL110 IF NOT EQUAL
.
          MOVE      SP6,ENDCTYP
          DISPLAY   *P21:9,*EL,*V2LON,"Start  "
          GOTO      GETCL200
.
GETCL110  PACK      KEY12 WITH SITE,STRCTYP
          CALL      RDCTYA1
          BRANCH    OVRCD TO GETCL120
          MOVE      OCTDESC,DESCTYP1
          GOTO      GETCL130
.
GETCL120  DISPLAY   *P30:24,*EL,*V2LON,"*** Invalid Clinic Type ***",*W2:
                    *P1:24,*EL
          GOTO      GETCL100
.
GETCL130  DISPLAY   *P21:9,*EL,*V2LON,STRCTYP,*P30:9,DESCTYP1
.
.------------------------------------------------------------------------------
.         Enter Ending clinic type
.------------------------------------------------------------------------------
.
GETCL200  MOVE      SP6,ENDCTYP
          KEYIN     *P21:10,*V2LON,ENDCTYP
.
          ENDSET    ENDCTYP
          APPEND    SP6,ENDCTYP
          RESET     ENDCTYP
.
          MATCH     SP6,ENDCTYP
          GOTO      GETCL210 IF NOT EQUAL
.
          MOVE      "ZZZZZZ",ENDCTYP
          DISPLAY   *P21:10,*EL,*V2LON,"Finish "
          GOTO      GETCL300
.
GETCL210  PACK      KEY12 WITH SITE,ENDCTYP
          CALL      RDCTYA1
          BRANCH    OVRCD TO GETCL220
          MOVE      OCTDESC,DESCTYP2
          GOTO      GETCL230
.
GETCL220  DISPLAY   *P30:24,*EL,*V2LON,"*** Invalid Clinic Type ***",*W2:
                    *P1:24,*EL
          GOTO      GETCL200
.
GETCL230  DISPLAY   *P21:10,*EL,*V2LON,ENDCTYP,*P30:10,DESCTYP2
.
.------------------------------------------------------------------------------
.         Keyin to date
.------------------------------------------------------------------------------
.
GETCL300  DISPLAY   *P1:12,*EL,"End Date : ",*P1:24,*EL,"Hit ",*V2LON:
                    "<ENTER>",*HOFF," to scan to end of file."
          UNPACK    SP6,CYEAR,CMON,CDAY
          MOVE      CCC,CCENT
          MOVE      TEN2,CCOL
          MOVE      TEN2,CVERT
          MOVE      ZERO,CDEFDTE
          MOVE      ZERO,CHIGHLT
.
          CALL      KEYDATE
          BRANCH    OVRCD,GETCL310
.
          PACK      TODATE,CCENT,CYEAR,CMON,CDAY
          GOTO      GETCL320
.
GETCL310  PACK      TODATE,SP8
.
. ------- Set up today's date -------
.
GETCL320  PACK      CURDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURDATE
.
. ------- Confirm parameters -------
.
GETCL390  MOVE      SP1,ANS
          KEYIN     *P1:24,*EL,"Ok to proceed (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH:
                    *V2LON,*DV,ANSN,*HOFF,*DV,SLASH:
                    *V2LON,*DV,ANSC,*HOFF:
                    ") ? ",*V2LON,ANS;
          CMATCH    ANSY,ANS
          GOTO      GETCL400 IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      GETCL100 IF EQUAL
          CMATCH    ANSC,ANS
          GOTO      START IF EQUAL
          BEEP
          GOTO      GETCL390
.
.------------------------------------------------------------------------------
.         Collate data into temp file in clinic type/clinic id sequence
.------------------------------------------------------------------------------
.
GETCL400  DISPLAY   *P1:12,*EF
          EXECUTE   CMDLINE,TASKID
          DISPLAY   *P56:24,"Opening ",OUTTMP;
          OPEN      OUTTMPXX,OUTTMP
          DISPLAY   *P1:12,*EF
.
          DISPLAY   *P1:24,"Clinic Type :":
                    *P20:24,"Clinic ID :":
                    *P40:24,"Scanning :";
.
          PACK      KEY12 WITH SITE,SP20
          CALL      RDSCTYA1
.
NEXTCTY   MOVE      SP6,OBACLIN
          MOVE      SP30,OCADESC
          CALL      RDKCTYA1
          BRANCH    OVRCD TO GETCL700        * No more records - start print
.
          MATCH     STRCTYP,OCTCTYP          * check if start clinic in range
          GOTO      NEXTCTY IF LESS
.
          MATCH     OCTCTYP,ENDCTYP          * check if finish clinic in range
          GOTO      GETCL700 IF LESS
.
          MATCH     OCTSITE,SITE
          GOTO      GETCL700 IF NOT EQUAL    * Different site - start print
.
.------- read bookings file to extract additional info -----
.
          PACK      KEY35,SITE,OCTCTYP,ZERO,CURDATE,SP30
          CALL      RDSBOKA2
.
GETCL500  MOVE      SP6,OBACLIN
          MOVE      SP30,OCADESC
          CALL      RDKBOKA2
          BRANCH    OVRCD,NEXTCTY           * No more records - start print
.
          MATCH     OBASITE,OCTSITE
          GOTO      NEXTCTY IF NOT EQUAL    * Different site - start print
.
          MATCH     OBACTYP,OCTCTYP
          GOTO      NEXTCTY IF NOT EQUAL    * Different CTY - start print
.
          COMPARE   ZERO,OBASTAT
          GOTO      NEXTCTY IF NOT EQUAL    * Not free - ignore record
.
          MATCH     CURDATE,OBADATE
          GOTO      NEXTCTY IF LESS         * No free slots after today - ignore
.
          MATCH     SP8,TODATE              * Is end date used ?
          GOTO      GETCL502 IF EQUAL       * No - scan till end of file
.
          MATCH     OBADATE,TODATE          * Have we gone past end date ?
          GOTO      NEXTCTY IF LESS         * Yes - get next clinic
.
GETCL502  DISPLAY   *P14:24,*V2LON,OBACTYP:
                    *P34:24,OBACLIN:
                    *P55:24,OBADATE,SP2,OBATIME;
.
GETCL505  MATCH     SP3,OBAVISIT
          GOTO      GETCL500 IF EQUAL        * No visit type - ignore record
.
          PACK      KEY5 WITH CODECV,OBAVISIT
          CALL      RDCODE1
          BRANCH    OVRCD TO GETCL500        * No visit type - ignore record
.
.------------------------------------------------------------------------------
.         Get visit type
.------------------------------------------------------------------------------
.
          MOVE      ONE,FORM1
.
GETCL510  MOVE      TCINDC1,SAVINDC
.
          MOVE      ONE,FORM1     *** NEW VISIT
          CMATCH    ANSN,SAVINDC
          GOTO      GOTVISIT IF EQUAL
.
          MOVE      TWO,FORM1           * Indicator for Special visit type
          CMATCH    ANSS,SAVINDC
          GOTO      GOTVISIT IF EQUAL
.
          MOVE      THREE,FORM1           * Indicator for Revisit type OR OTHER
          CMATCH    ANSR,SAVINDC
          GOTO      GOTVISIT IF EQUAL
          CMATCH    ANSO,SAVINDC
          GOTO      GOTVISIT IF EQUAL
.
          GOTO      GETCL500         * No indicator, ignore record
.
.------------------------------------------------------------------------------
.         Write data away to temp file
.------------------------------------------------------------------------------
.
GOTVISIT  PACK      KEY12 WITH OBACTYP,OBACLIN
          READ      OUTTMPXX,KEY12;*13,NEWDATE,NEWTIME:
                                       SPCDATE,SPCTIME:
                                       REVDATE,REVTIME
          GOTO      GETCL560 IF OVER
.
          LOAD      YDATE,FORM1,NEWDATE,SPCDATE,REVDATE
.
          MATCH     SP8,YDATE
          GOTO      GETCL500 IF NOT EQUAL
.
          STORE     OBADATE,FORM1,NEWDATE,SPCDATE,REVDATE
          STORE     OBATIME,FORM1,NEWTIME,SPCTIME,REVTIME
.
          UPDATE    OUTTMPXX;*13,NEWDATE,NEWTIME:
                                 SPCDATE,SPCTIME:
                                 REVDATE,REVTIME
          GOTO      GETCL500
.
GETCL560  UNPACK    SP30 INTO NEWDATE,SPCDATE,REVDATE
          UNPACK    SP30 INTO NEWTIME,SPCTIME,REVTIME
.
          STORE     OBADATE BY FORM1 INTO NEWDATE,SPCDATE,REVDATE
          STORE     OBATIME BY FORM1 INTO NEWTIME,SPCTIME,REVTIME
.
          WRITE     OUTTMPXX,KEY12;KEY12,NEWDATE,NEWTIME:
                                         SPCDATE,SPCTIME:
                                         REVDATE,REVTIME
          GOTO      GETCL500
.
.------------------------------------------------------------------------------
.         Read temp file & print
.------------------------------------------------------------------------------
.
GETCL700  DISPLAY   *P1:24,*EL,*P40:24,*V2LON,"*** Printing ***",*HOFF:
                    *P1:24,*EL,"Clinic Type :":
                    *P30:24,"Clinic ID :";
.
          MOVE      ZERO,CPAGENO
          MOVE      SP6,SAVCTYP
.
          PACK      KEY12 WITH SP20
          READ      OUTTMPXX,KEY12;;
.
          MOVE      FALSE,PRTFLAG
.
GETCL800  READKS    OUTTMPXX;OBACTYP,OBACLIN,NEWDATE,NEWTIME:
                                             SPCDATE,SPCTIME:
                                             REVDATE,REVTIME
          GOTO      GETCL900 IF OVER
.
          DISPLAY   *P15:24,*V2LON,OBACTYP:
                    *P45:24,OBACLIN;
.
          MATCH     SP6,SAVCTYP
          GOTO      GETCL815 IF EQUAL
.
          MATCH     SAVCTYP,OBACTYP
          GOTO      GETCL820 IF EQUAL
          CALL      PAGEND
GETCL815  CALL      PRHEAD
          CALL      PRLINE
          CALL      PAGEND
          GOTO      GETCL830
.
GETCL820  CALL      PRLINE
.
GETCL830  MOVE      TRUE,PRTFLAG                  * set print flag
          GOTO      GETCL800
.
GETCL900  BRANCH    PRTFLAG,GETCL950
          CALL      PRHEAD
GETCL950  CALL      PAGEND
          DISPLAY   *P40:24,*EL,*V2LON,"*** Generation completed ***",*W2;
          PRINT     *N,*N,*45,"*** END OF REPORT ***"
.
          EXECUTE   CMDKILL,TASKID        * Delete old temp file
          GOTO      START
.
+
.******************************************************************************
.
.         PRINTING SUBROUTINES
.
.------------------------------------------------------------------------------
.         Page Header
.------------------------------------------------------------------------------
.
PRHEAD    MOVE      ONE,CNOUNDLN
          CALL      IBACLOCK    
          PACK      KEY12 WITH SITE,OBACTYP
          CALL      RDCTYA1
.
.-------- print header -----
.
          CALL      HEAD132
.
.-------- print parameters ----
.
          PRINT     *N:
                    *40,"CLINIC TYPE : ",OBACTYP,SP3,OCTDESC,*N
.
.-------- print details ------
.
          CALL      PAGEND
          PRINT     *1,"|",*5,"CLINIC",*45,"|",*56,"NEW VISITS":
                    *74,"|",*83,"SPECIAL VISITS":
                    *103,"|",*108,"REVISITS/OTHER VISITS",*132,"|":
                    *N,"|",*5,"ID",*13,"DESCRIPTION",*45,"|",*52,"DATE":
                    *66,"TIME",*74,"|",*82,"DATE",*95,"TIME":
                    *103,"|",*110,"DATE",*124,"TIME",*132,"|"
          CALL      PAGEND
.
          MOVE      EIGHT,CLNO
          MOVE      OBACTYP,SAVCTYP
.
          RETURN
.
.------------------------------------------------------------------------------
.         Print a line
.------------------------------------------------------------------------------
.
PRLINE    COMPARE   "60",CLNO
          GOTO      PRLIN100 IF LESS
.
          CALL      PAGEND
          CALL      PRHEAD
.
PRLIN100  PACK      KEY20,SITE,OBACLIN,NEWDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     SITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OBACLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ENDIF
.
          UNPACK    NEWDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *1,"|",*5,OBACLIN,*14,OCADESC,*45,"|";
          MOVE      "49",CCOL
          PRINT     *CCOL,CPCDATE;
          PRINT     *62,"|",*66,NEWTIME;
.
          UNPACK    SPCDATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *74,"|";
          MOVE      "78",CCOL
          PRINT     *CCOL,CPCDATE;
          PRINT     *91,"|",*95,SPCTIME;
.
          UNPACK    REVDATE INTO CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *103,"|";
          MOVE      "107",CCOL
          PRINT     *CCOL,CPCDATE;
          PRINT     *120,"|",*124,REVTIME,*132,"|"
.
          ADD       ONE,CLNO
          RETURN
.
.------------------------------------------------------------------------------
.         Page Border
.------------------------------------------------------------------------------
.
PAGEND    PRINT     "*---------------------------------------------------":
                    "--------------------------------------------------------":
                    "-----------------------*"
          ADD       ONE,CLNO
          RETURN
.
+
.******************************************************************************
.
.         THE END IS NIGH ...
.
.------------------------------------------------------------------------------
.
ENDIT     EXECUTE   CMDKILL,TASKID        * Delete old temp file
.
          CHAIN     PGM
          STOP
.
          INC       IBASEQIO/INC
          INC       PATCODIO/INC      * codes file IO
          INC       PATDO1IO/INC      * doctor file IO
          INC       OUTBOAIO/INC      * clinic booking file IO
          INC       OUTCTYIO/INC      * clinic type file IO
          INC       OUTCLIIO/INC      * clinic ID file IO
          INC       WEBERRIO/INC
.
          INC       STD001IO
          INC       TFILENAM
.
