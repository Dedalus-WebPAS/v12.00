.***********************************************************************
.*                                                                     *
.*                                IHAPASDS                             *
.*                                ========                             *
.*                                                                     *
.*        Routine to display all security codes in the ihapassf file   *
.*                                                                     *
.*        AUTHOR           :  DIG                                      *
.*                                                                     *
.*        DATE             :  21/11/89                                 *
.*                                                                     *
.*        VARIABLES NEEDED :  DIM30   DIM   30                         *
.*                                                                     *
.*        FILES NEEDED     :  IHAPASS.INC   IBAIOIHA.INC               *
.*                                                                     *
.*        MODIFICATIONS    : V5.01 28/11/89 Graeme Williams            *
.*                                 Make routine conform to the new     *
.*                                 standard                            *
.*                                                                     *
.***********************************************************************
.
IHAPASDS  MOVE      SP4,KEY4
          CALL      RDSPASS1                 * see if any security codes exist
          CALL      RDKPASS1
          BRANCH    OVRCD,QPASS200
.
.------ Display Heading, and then loop over entries ------
.
          DISPLAY   *P1:4,*EF,*P30:4,*V2LON,*ULON,"SECURITY CODES"
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
.
.>>>>>>   TRAP      QPASS300 IF F1
.
          MOVE      SP4,KEY4
          CALL      RDSPASS1                  * position on security file
.
.------ Read through security file ------
.
QPASS100  CALL      RDKPASS1
          BRANCH    OVRCD,QPASS400
.
.------ Display security codes and descriptions ------
.
          MOVE      SECUSER,DIM30
          DISPLAY   *PCCOL:CVERT,*V2LON,SECODE,SP2,*HOFF,DIM30;
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      QPASS100 IF LESS
.
.------ Set up second column display positions ------
.
          MOVE      FIVE,CVERT
          ADD       "40",CCOL
          COMPARE   "70",CCOL
          GOTO      QPASS100 IF LESS
.
.>>>>>>   TRAPCLR   F1
.
.------ Next screen or Continue ? ------
.
          KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSC,ANS
          GOTO      QPASS400 IF EQUAL
.
.------ Reset to first column display positions ------
.
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
.
.>>>>>>   TRAP      QPASS300 IF F1
.
          GOTO      QPASS100
.
.------ No security codes on file ------
.
QPASS200  DISPLAY   *P1:4,*EF,*P30:4,*V2LON,*BLINKON:
                    "No security codes on file":
                    *P1:24,*EL,*B,*HOFF,"Hit <",*V2LON,"ENTER",*HOFF,">";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
          MOVE      ONE,OVRCD
          GOTO      QPASS999
.
QPASS300  NORETURN
.>>>>>>   TRAPCLR   F1
.
.------ End of codes ------
.
QPASS400  MOVE      ZERO,OVRCD
.
QPASS999  RETURN
+
