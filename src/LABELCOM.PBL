.******************************************************************************
.* LABELCOM - Common Routine for Displaying/Printing Screen Painted Labels    *
.*                                                                            *
.* Parameters - NOLABEL - no of labels to print                               *
.*              LABCOS  - no. printing across the page                        *
.*              LABSIZE - Wide or Small Format                                *
.*              PRGID                                                         *
.*              SCRID                                                         *
.* Returns: EXIT 0 = Labels printed ok OR zero labels requested for printing  *
.*               1 = Non screen painted labels to be printed                  *
.* Mods:                                                                      *
.*        09/06/1999  Andrew Peel  SRF 630678                                 *
.*                    Fixed to allow screen painted wrist band labels         * 
.*                    to be printed when no labels are required               *
.*        18/01/1999  Steve Armstrong                                         *
.*                    Mods to print wrist band labels on the same page,       *
.*                    straight after normal labels.                           *
.*        20/04/1998  Nick Read                                               *
.*                    New var LABSIZE to use instead of HELSIZE               *
.*        06/04/1998  Nick Read                                               *
.*                    Mods to print multiple screen painted labels across the *
.*                    page based on parameters HELPRNT, LABSIZE and HELPAGE   *
.*        02/01/1998  Steve Armstrong                                         *
.*                    Set for full screen save HLEF = 0                       *
.*        14/10/1997  Steve Armstrong                                         *
.*                    Mods for Wrist Band Labels.                             *
.*                    Mods to return EXIT as zero if there are zero labels    *
.*                    for printing.                                           *
.******************************************************************************
.
LABELCOM  CALL      CKSR0000                * screen painted label ?
          MOVE      EXIT,NOTSCPT            * save screen painted flag
          BRANCH    EXIT,LCOM1000           * not screen painted label
.
          MOVE      ZERO,HLEF
          CALL      GETSCR00                * save current screen
          DISPLAY   *P1:7,*EF
          HLINE     *G37,7,1,80
          HLINE     *G37,23,1,80
          CALL      DSPBGRND                * display label background
          CALL      DSPFIELD                * display field values
.
.         The label is screen painted, so check how many
.         labels are to be printed
.
          CALL      CKLB0000                * any labels to be printed ?
          MOVE      LABCOS,SAVLBCOS         * save no. of labels across page
.
          DISPLAY   *P1:24,*EL,*V2LON:
                    "***  Printing Labels  ***";
.
          CALL      OPNPRINT                * Open print file
          MOVE      ONE,FORM3
.
          IF        LABSIZE=1
              MOVE      "37",TABPOS2
              MOVE      "73",TABPOS3
          ENDIF
.
          IF        LABSIZE=2
              MOVE      "42",TABPOS2
              MOVE      "83",TABPOS3
          ENDIF
.                                           * EXIT returned from CKLB0000
          BRANCH    EXIT,LCOM6050           * no labels to be printed
          MOVE      NOLABEL,LABPRINT
.
          GOTO      LCOM3000                * yes labels to be printed
.
.         The label is not screen painted, so check how
.         many labels to be printed
.
LCOM1000  CALL      CKLB0000                * any labels to be printed ?
          BRANCH    EXIT,LCOM7500           * no - finished
          GOTO      LCOM8000                * yes - return to print normally
.
.         get the label screen size
.
LCOM3000  OPEN      SCRMASA1,"scrmasaf"
          MOVE      EIGHT,SCMATOP
          MOVE      TWENTY2,SCMABOT
          PACK      KEY10,PRGID,SCRID    
          CALL      RDSCMA1
LCOM4000  MOVE      SCMATOP,CVERT             * start at top of screen
.
.         If using the Eltron printer, then clear the image buffer
.         and initialise the first line number
.
          IF        USINGELT = 1
            PRINT     ANSN
            MOVE      ZERO,LINEONE
          ENDIF
.
LCOM5000  COMPARE   TWENTY3,CVERT
          GOTO      LCOM6000 IF NOT LESS
.
          GETVAR    PRINTVAR,ATT,ONE,CVERT
          STRIP     PRINTVAR
.
. --------- stop if no background ---------------------------------------
.
          PACK      KEY12,PRGID,SCRID,CVERT,SP20
          CALL      RDSCSB1                 * positional read
          BRANCH    OVRCD,LCOM6000
.
.         If using the Eltron printer, get the first line to be printed
.         from the screen painter background file, and subtract one from this
.         to get the difference.  This figure will be subtracted from each
.         of the screen painter background records to get the 
.         actual line number to print on.
.
          IF        USINGELT = 1 & LINEONE = 0
            MOVE      SCSBLIN,LINEONE
            ASSIGN    (LINEONE - 1),LINEDIFF
          ENDIF
.
          REP       BARCODE,ATT             * change any barcoded fields
.
.         if using TBC_7, then compile with a printvar
.         If we are using the eltron printer, then format the print string.
.         The line number has to be converted to "dots" for the Eltron.
.        
          IFGE      $$VER,7
            IF        USINGELT = 1
              MOVE      SCSBLIN,LINEPRNT
              ASSIGN    ((LINEPRNT - LINEDIFF) * 24 - 24),LINEPRNT
              MOVE      LINEPRNT,LINEFRMT
              SQUEEZE   LINEFRMT
              STRIP     PRINTVAR              * remove trailing blanks
              PRINT    ANSA0,COMMA,*+,LINEFRMT,COMMA,ELTRON,PRINTVAR,ENDQUOTE,*-
            ELSE
PUFF          IF       LABCOS>1 & LABPRINT > 1
                  IF       LABCOS=2
                     PRINT      *1;            * two across the page
                     PRINTVAR   PRINTVAR,ATT;
                     PRINT      *TABPOS2;
                     PRINTVAR   PRINTVAR,ATT
                     PRINT      *N,*1;
                  ELSE
                     PRINT      *1;            * three across the page
                     PRINTVAR   PRINTVAR,ATT;
                     PRINT      *TABPOS2;
                     PRINTVAR   PRINTVAR,ATT;
                     PRINT      *TABPOS3;
                     PRINTVAR   PRINTVAR,ATT
                     PRINT     *N,*1;
                  ENDIF
              ELSE
                  PRINTVAR  PRINTVAR,ATT  * printing only one across page
                  PRINT     *N,*1;
              ENDIF
            ENDIF
          XIF
.
.         if using TBC_6, then compile with a print
.        
          IFEQ      $$VER,6
            IF        USINGELT = 1
              MOVE      SCSBLIN,LINEPRNT
              ASSIGN    ((LINEPRNT - LINEDIFF) * 24 - 24),LINEPRNT
              MOVE      LINEPRNT,LINEFRMT
              SQUEEZE   LINEFRMT
              STRIP     PRINTVAR              * remove trailing blanks
              PRINT    ANSA0,COMMA,*+,LINEFRMT,COMMA,ELTRON,PRINTVAR,ENDQUOTE,*-
            ELSE
              UNPACK    PRINTVAR,WLINE1A
              IF        LABCOS>1 & LABPRINT > 1
                  IF        LABCOS=2
                      PRINT     WLINE1A;     * print 2 across the page
                      PRINT     *TABPOS2;
                      PRINT     WLINE1A; 
                      PRINT     *N;
                  ELSE
                      PRINT     WLINE1A;     * print 3 across the page
                      PRINT     *TABPOS2;
                      PRINT     WLINE1A; 
                      PRINT     *TABPOS3;
                      PRINT     WLINE1A; 
                      PRINT     *N;
                  ENDIF
              ELSE
                  PRINT     WLINE1A
              ENDIF
            ENDIF
          XIF
          ADD       ONE,CVERT
.
          GOTO      LCOM5000
.
LCOM6000  IF        USINGELT = 1
            PRINT     "P1"
          ENDIF
.
          SUB       LABCOS,LABPRINT
          IF        LABPRINT>0
              IF        LABCOS>LABPRINT
                  MOVE      LABPRINT,LABCOS
              ENDIF
              GOTO       LCOM4000
          ENDIF
.
.         If we are printing admission labels, then we need to see
.         if there is a wrist band label to be printed
.
LCOM6050  MATCH     "LABELINP",PRGID        * printing inpatient labels ?
          GOTO      LCOM6500 IF NOT EQUAL   * no - finished printing
.
          MATCH     "02",SCRID              * printed wrist band labels ?
          GOTO      LCOM6500 IF EQUAL       * yes - finished printing
.
.         A screen painted inpatient label exists and has been printed,
.         so check if there is a screen painted wrist label.
.         If not, don't prompt for wrist label
.
          MOVE      "02",SCRID              * set wrist band label screen id
          CALL      CKSR0000                * is wrist label screen painted ?
          BRANCH    EXIT,LCOM6500           * no - finished
.
.         See if user wants to print a wrist/arm band label
.
LCOM6100  KEYIN     *P1:24,*EL,"Print Wrist Band Labels (",*V2LON,*DV,ANSY:
                    *HOFF,"/",*V2LON,*DV,ANSN,*HOFF,") ? ":
                    *P33:24,*V2LON,ANS:
                    *P33:24,*DV,ANS
.
          PACK      ANS,ANS,SP1
          REP       UPPLOW,ANS
.
          MATCH     ANSN,ANS                * "N" entered ?
          GOTO      LCOM6500 IF EQUAL       * yes - finished
.
          MATCH     ANSY,ANS                * "Y" entered ?
          IF        @EQUAL
            MOVE      ONE,NOLABEL           * set default number of labels to 1
            MOVE      SAVLBCOS,LABCOS       * restore no. of labels across page
.
            DISPLAY   *P1:7,*EF
            HLINE     *G37,7,1,80
            HLINE     *G37,23,1,80
            CALL      DSPBGRND              * display label background
            CALL      DSPFIELD              * display field values
.
            CALL      CKLB0000              * any labels to be printed ?
            BRANCH    EXIT,LCOM6500         * no - finished
            MOVE      NOLABEL,LABPRINT
            GOTO      LCOM3000              * yes
          ENDIF
.
          BEEP                              * invalid input
          GOTO      LCOM6100
.
LCOM6500  CALL      CLSPRINT
LCOM7000  CALL      PUTSCR00
LCOM7500  MOVE      ZERO,EXIT
          GOTO      LCOM9999
.
LCOM8000  MOVE      ONE,EXIT
.
LCOM9999  RETURN                  
