.  ********************************************************************
.  *                                                                  *
.  *     Include Name   : IBASTPDS                                    *
.  *                                                                  *
.  *     Function       : Provides a routine to handle stationery     *
.  *                      type descriptions.                          *
.  *                      requires NSORTYPE FORM 3 to be defined      *
.  *                      & IBADSTYP include                          *
.  *                                                                  *
.  *                                                                  *
.  *     Programmer     : Peter Eddey  15/04/90                       *
.  *                                                                  *
.  ********************************************************************
.  * Mods     :                                                       *
.  *      V9.04.01   17/03/2005  Lina Vo         CAR 56405            *
.  *                 Added var IBASTP18 - CSSD Form                   *
.  ********************************************************************
.  *      V9.03.00   01/03/2004  Pat Dirito      CAR 43997            *
.  *                 Added var IBASTP17 - Allied Health               *
.  ********************************************************************
.  *      C2.00.02  27/06/2000  Charles Handaya                       *
.  *                Added new Stationery Types 15 & 16 for Receipt    *
.  *      C2.00.01  05/04/2000  Charles Handaya                       *
.  *                Added new Stationery Types 13 & 14 for Debtor     *
.  *      C2.00.00  23/02/2000  Davin                                 *
.  *                - Combined releases 5 & 6 (all stationery types)  *
.  ********************************************************************
.  *      V5.07.02  09/07/1999  Nicole Harrington                     *
.  *                Added IBASTYP7 - Emergency Form                   *
.  *      V5.07.01  31/03/1999  Davin                                 *
.  *                Added IBASTYP6 - Health Fund Claim Form           *
.  *      V5.07     06/10/98  Jim Stathopoulos                        *
.  *                507 Changes                                       *
.  ********************************************************************
.  *        V5.04.01  21/10/1996    Howellsy    EOC & ACC             *
.  *                  Added ACC Form. Stationery Type=5               *
.  *      C1.00.00  01/05/1995  Steve Armstrong                       *
.  *                Fixed bug in line 24 prompt after returning from  *
.  *                last screen to first screen                       *
.  *      V6.01.01  22/06/1994  Greg Horvat      V6.02 Upgrade        *
.  *                Added IBASTYP5 - Health Fund Claim Form           *
.  *                                                                  *
.  *                                                                  *
.  ********************************************************************
.
IBASTPDS  
          DISPLAY   *P1:3,*EF
          DISPLAY   *P20:6,*V2LON,*ULON,"Stationery Types Codes & Descriptions",*HOFF
.
IBSTP050  MOVE      ZERO,IBSTPSCR        * set screen number
          MOVE      ZERO,IBSTTYPE        * set order type
.
IBSTP100
          DISPLAY   *P1:7,*EF           
          MOVE      ONE,IBSTPCOL         * set column
          MOVE      SEVEN,IBSTPROW       * set row
          MOVE      ZERO,IBSTPFLG        * set screen full flag
          ADD       ONE,IBSTPSCR         * increment the screen number
.
IBSTP200
          ADD       ONE,IBSTTYPE         * increment the order type   
          ADD       ONE,IBSTPROW         * increment the row          
.
          COMPARE   FORTY,IBSTTYPE
          GOTO      IBSTP250 IF NOT LESS
.
          MOVE      IBASTYP0,NSORDESC    * Default description
          LOAD      NSORDESC,IBSTTYPE,IBASTYP1,IBASTYP2,IBASTYP3,IBASTYP4:
                                      IBASTYP5,IBASTYP6,IBASTYP7,IBASTYP8:
                                      IBASTYP9,IBASTP10,IBASTP11,IBASTP12:
                                      IBASTP13,IBASTP14,IBASTP15,IBASTP16:
                                      IBASTP17,IBASTP18
          GOTO      IBSTP260

. 
IBSTP250  MOVE      IBSTTYPE,NSORTYPE
          SUB       FORTY,NSORTYPE
          CALL      NSMORDES             * get the description
          BRANCH    OVRCD,IBSTP400
.
IBSTP260  DISPLAY   *PIBSTPCOL:IBSTPROW,*V2LON,IBSTTYPE,*HOFF,".  ":
                    NSORDESC
.
          COMPARE   TWENTY2,IBSTPROW    * test if column is full
          GOTO      IBSTP300 IF EQUAL
.
          GOTO      IBSTP200
.
IBSTP300
          BRANCH    IBSTPFLG,IBSTP500   * if screen is full
          MOVE      ONE,IBSTPFLG        * set screen full flag
          MOVE      SEVEN,IBSTPROW      * reset row
          MOVE      FORTY,IBSTPCOL      * reset column
          GOTO      IBSTP200
.
.-----  the following controls the display of prompts ---
.-----  depending on the number of screens or if their --
.-----  is another screen etc.                        ---
.
IBSTP400
          COMPARE   ONE,IBSTPSCR
          GOTO      IBSTP999 IF EQUAL
.
          DISPLAY   *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst screen ? "
IBSTP450
          KEYIN     *P30:24,*DV,UNDLN1:
                    *P30:24,*V2LON,ANS:
                    *P30:24,*DV,ANS
.
          REP       UPPLOW,ANS
          MATCH     ANS,ANSC
          GOTO      IBSTP999 IF EQUAL
.
          MATCH     ANS,ANSF
          GOTO      IBSTP050 IF EQUAL
.
          BEEP
          GOTO      IBSTP450
.
IBSTP500
.
.----  test if there is a "next" screen            -----
.
          COMPARE   FORTY,IBSTTYPE
          GOTO      IBSTP520 IF NOT LESS
.
          GOTO      IBSTP530
.
IBSTP520  ADD       ONE,IBSTTYPE
          MOVE      IBSTTYPE,NSORTYPE
          SUB       FORTY,NSORTYPE
          CALL      NSMORDES
          SUBTRACT  ONE,IBSTTYPE
          BRANCH    OVRCD,IBSTP400
.
IBSTP530  COMPARE   ONE,IBSTPSCR
          GOTO      IBSTP600 IF EQUAL
.
          DISPLAY   *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst screen, (",*V2LON,"N":
                    *HOFF,")ext screen ? "
IBSTP550
          KEYIN     *P45:24,*DV,UNDLN1:
                    *P45:24,*V2LON,ANS:
                    *P45:24,*DV,ANS
.
          REP       UPPLOW,ANS
          MATCH     ANS,ANSC
          GOTO      IBSTP999 IF EQUAL
.
          MATCH     ANS,ANSF
          GOTO      IBSTP050 IF EQUAL
.
          MATCH     ANS,ANSN
          GOTO      IBSTP100 IF EQUAL
.
          BEEP                
          GOTO      IBSTP550
.  
IBSTP600
          DISPLAY   *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"N",*HOFF,")ext screen ? "
IBSTP650
          KEYIN     *P29:24,*DV,UNDLN1:
                    *P29:24,*V2LON,ANS:
                    *P29:24,*DV,ANS
.
          REP       UPPLOW,ANS
          MATCH     ANS,ANSC
          GOTO      IBSTP999 IF EQUAL
.
          MATCH     ANS,ANSN
          GOTO      IBSTP100 IF EQUAL
.
          BEEP                
          GOTO      IBSTP650
.
IBSTP999
          MOVE      IBSTPTMP,IBSTTYPE    * restore the original order
.                                          type
          RETURN
