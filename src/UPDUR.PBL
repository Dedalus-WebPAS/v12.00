.******************************************************************************
.* SUBROUTINES    UPDUR        :  Update U/R number to the appropriate files  *
.*                UHKIF        :  Update Hong Kong National Interface file    *
.*                SVMAS000     :  Save master variables                       *
.*                                                                            *
.* Date          :  V5.01.00  09/09/1993                                      *
.* Modifications :                                                            *
.*       V12.00.01  26/05/2025  Bella Turco       TSK 0955096                 *
.*                  Alphanumeric changes                                      *
.*       V11.04.01  11.10.2024  Alina Bhari       TSK 0946636                 *
.*                  Fixed the issue with the demographics last updated date,  *
.*                  time and user stamp while updating the NHI information    *
.*                  - UPDUR                                                   *
.*       V11.02.01  18.07.2022  Jacob Jackson     TSK 0922155                 *
.*                  Remove PACK KEY17 before WRURAD1                          *
.*       V11.01.01  28.06.2021	DA Sarkies        TSK 0898410                 *
.*                  Changed SP70 to USERID at ll 448 & 426                    *
.*                  Changed SP70 to PATIME at ll 449 & 425                    *
.*                  Changed SP70 to PADATE at ll 450 & 424                    *
.******************************************************************************
.*       V10.15.01  16/12/2019  Davin             TSK 0882837                 *
.*                  Match prgid with HL7RECVR to stop A31 broadcast in UPDUR  *
.*       V10.07.01  04/01/2016 Ebon Clements      TSK 0294154                 *
.*                  Added Opt out of SMS to prev address file write           *
.*       V10.06.03  01/09/2015  Ebon Clements     CAR 319970                  *
.*                  Pack vars with SP70 prior to test UDRGP000 - Previous GP  *
.*       V10.06.02  24/08/2015  Ebon Clements     CAR 319970                  *
.*                  Added SP70 to PACK of KEY30 in UDRGP000 - Previous GP     *
.*       V10.06.01  18/05/2015  Patrick Adair     CAR 208170                  *
.*                  Removed PTCNUCGP test from  UDRGP000 - Previous GP        *
.*                  details will always be stored now                         *
.******************************************************************************
.*       V10.05.01  11/02/2015  Patrick Adair     CAR 208170                  *
.*                  Modified UDRGP000 to use Death Audit Table                *
.*                  Included new HCP save variables as SVMAS variables are    *
.*                  too small for new fields                                  *
.******************************************************************************
.*       V10.04.01  22/01/2014  Don Nguyen        CAR 295972                  *
.*                  Modified UDADR000 - KEY16 to KEY24. Updated key PACK      *
.******************************************************************************
.*       V10.03.02  19/07/2012  Steve Armstrong   CAR 268870                  *
.*                  Added check for change of DOB and Sex (as well as names)  *
.*                  in UDMAS000.                                              *
.*       V10.03.01  09/11/2011  Steve Armstrong   CAR 254774                  *
.*                  Mods to UDADR000 to update the previous address where a   *
.*                  record already exists for the day.                        *
.******************************************************************************
.*       V10.02.02  13/07/2011  Steve Armstrong   CAR 240722                  *
.*                  Further mods to cater for second given name as            *
.*                  part of PATGSRFD keys                                     *
.*       V10.02.01  22/06/2011  Steve Armstrong   CAR 240722                  *
.*                  Mods to cater for changes to PATGSRFD:                    *
.*                       - GSRSNAM & GSRGNAM extended to 30 chars.            *
.*                       - PTGSGNM2 added.                                    *
.*                       - all indexes changed in length.                     *
.******************************************************************************
.*       V10.01.01  02/02/2011  Steve Armstrong   CAR 237520                  *
.*                  Mods to remove Detente interface (PATDETNT)               *
.******************************************************************************
.*       V10.00.01  19/03/2010  Steve Armstrong   CAR 135505                  *
.*                  Added HL7TRGID & HL7INCLD setting for all broadcast       *
.*                  messages                                                  *
.******************************************************************************
.*       V9.06.01   08/06/2006  Steve Armstrong   CAR 108060                  *
.*                  Added call to PMIGTNID to set NHI variables before call   * 
.*                  to DGCLICUP.                                              *
.******************************************************************************
.*       V9.04.03   02/08/2005  Davin             CAR 64087                   *
.*                  Mods for PAS/CIS and proprietary interface changes -      * 
.*                  removed call to cisa31br                                  *
.*       V9.04.02   14/12/2004  Steve Armstrong CAR 55879                     *
.*                  Fixed BRANCH on CAUDM at UMAUD000 to goto UMAUD999        *
.*                  instead of returning control directly back to the calling *
.*                  routine.                                                  *
.*       V9.04.01   29/09/2004  Peter Vela    CAR 49647                       *
.*                  Fixed assignment of username/id in previous address file  *
.******************************************************************************
.*       V9.03.03   11/05/2004  Steve Armstrong   CAR 49679                   *
.*                  Added call to cisa31br routine for PAS-CIS interface.     *
.*       V9.03.02   10/03/2004  Guomin Fu     CAR 47828                       *
.*                  Fixed a bug when promting " Save as alias?", system saves *
.*                  alias anyway even when "Cancel" button is clicked on      *
.*                  Emergency Registration Screen.                            *
.*       V9.03.01   10/07/2003  Jill Habasque SRF 38462                       *
.*                  Changed to call UDADR00 for NHIWEB                        *
.*       V9.02.01   08/11/2001  Mike Laming                                   *
.*                  Rename DGCLIUPM to DGCLICUP                               *
.*       V9.00.01   07/09/2001  B.G.Ackland                                   *
.*                  Change patgsrng                                           *
.*       V5.10.B03  26/04/2001  Ebon Clements                                 *
.*                  Call UPDSUR to update sex and date of birth in the        *
.*                  surname file when updating a ur. (UDMAS00)                *
.*       V5.10.B02  11/04/2001  Ebon Clements                                 *
.*                  Made changes for new PATPA1FD layout. Moved spaces to     *
.*                  new fields that are not used                              *
.*       V5.10.B01  28/03/2001  Glenn Saunders                                *
.*                  Remove access to PTCNSNDX and associated processing.      *
.*                  Remove all references to PATSUR file (old surname file).  *
.*       V5.09.01   30/01/2001 Katie Nelson                                   *
.*                  Added flag to skip previous address update if in web      *
.*       V5.09.00   08/01/2001  Pat Dirito                                    *
.*                  Added new Previous Address variable. (SPCELL)             *
.*       V5.08.01   22/08/2000  Jill Habasque                                 *
.*                  Added flag to not display for WEB programs                *
.*       V5.07.B01  28/01/1999  Steve Armstrong                               *
.*                  Mods for DGCLIUPM to be called if update not from DGSRVPMI*
.*       V5.07.B00  17/11/1998  Jim Stathopoulos                              *
.*                  Removed the HCS Admission Data Audit Files                *
.*                  08/10/1998  Glenn Berry      5.07 changes                 *
.*****************************************************************************
.*                  V5.01.01  14/10/1993  Gabrielle                           *
.*                            Added CALCAGE                                   *
.*                            Fixed Outpatient Speciality Code                *
.*                  V5.01.02  29/10/1993  Gabrielle                           *
.*                            Fixed address when there is not a valid postcode*
.*                  V5.01.03  08/03/1994  Rob Leonard  SRF 440662             *
.*                            Fixed UDMAS0000 incorrectly calculating age     *
.*                  V5.01.04  24/03/1994  Whirlpool SRF 128432                *
.*                            Added P1 description for detente record 3       *
.*                  V5.01.05  27/06/1994  Rob Leonard  SRF 440839             *
.*                            Removed PTCNICAD check before writing prev add  *
.*                  V5.01.06  11/07/1994  ROD                                 *
.*                            added check on DOR for previous address         *
.*                            Modified to not read in NZHIS parameter PTCNNHII*
.*                            if we are in IBAOUT66 - Ugly                    *
.*                            12/07/1994  ROD                                 *
.*                            added check on change of Registered Gp/Prac     *
.*                  V5.03.07  27/11/1995  ROD                                 *
.*                            set up gsr vars if using new file in udmas000   *
.*                  V5.03.08  19/12/95 J.Tan                                  *
.*                            mods to update the soundex key in patgsrn       *
.*                  V5.04.01  14/01/1997  Steve Armstrong                     *
.*                            Mods for Datagate API's                         *
.* Parameters Required:                                                       *
.*                                                                            *
.*       AADMNO     must be passed with the pre/admission number              *
.*                                                                            *
.                                                                             *
.* Requirements :                                                             *
.*                                                                            *
.*       FD's :                                 External Subroutines :        *
.*                                                                            *
.*       INC       BOKRC1FD/INC                 INC       CALCAGE             *
.*       INC       HKIDATFD/INC                 INC       CHKMEDI             *
.*       INC       NZHISIFD/INC                 INC       CLPATMAS            *
.*       INC       OUTCLIFD/INC                 INC       DISPITEM            *
.*       INC       PATAK1FD/INC                 INC       KEYINPMI            *
.*       INC       PATAM1FD/INC                                               *
.*       INC       PATALRFD/INC                 INC       NZHISIIO            *
.*       INC       PATALGFD/INC                 INC       NZIBSRCH            *
.*       INC       PATCT1FD/INC                 INC       PATCOMN1            *
.*       INC       PATCCCFD/INC                 INC       PATCOMN2            *
.*       INC       PATCODKY                                                   *
.*       INC       PATCONFD/INC                 INC       PATLGADS            *
.*       INC       PATDNRFD/INC                 INC       PATLGPDS            *
.*       INC       PATDO1FD/INC                 INC       SCRCKFLD            *
.*       INC       PATDSCFD/INC                                               *
.*       INC       PATFN1FD/INC                                               *
.*       INC       PMSHCGFD/INC                                               *
.*       INC       PMSHCPFD/INC                                               *
.*       INC       PATGSRFD/INC                                               *
.*       INC       PATINVFD/INC                                               *
.*       INC       PATLGAFD/INC                                               *
.*       INC       PATLG1FD/INC                                               *
.*       INC       PATLOCFD/INC                                               *
.*       INC       PATMA1FD/INC                                               *
.*       INC       PATMI1FD/INC                                               *
.*       INC       PATMRGFD/INC                                               *
.*       INC       PATNHIFD/INC                                               *
.*       INC       PATNMPFD/INC                                               *
.*       INC       PATPA1FD/INC                                               *
.*       INC       PATPNIFD/INC                                               *
.*       INC       PATPR1FD/INC                                               *
.*       INC       PATRE1FD/INC                                               *
.*       INC       PATSDNFD/INC                                               *
.*       INC       PATSMWFD/INC                                               *
.*       INC       PATURAFD/INC                                               *
.*       INC       PATVISFD/INC                                               *
.*       INC       PMIVARS/INC                                                *
.*       INC       SCRPSTFD/INC                                               *
.*       INC       SCRSLTFD/INC                                               *
.*                                                                            *
.* Variables Required :                                                       *
.*                                                                            * 
.*       ACCEPT    FORM      1                                                *
.*       ACTION    DIM       1                                                *
.*       ADD       DIM       2                                                *
.*       AMM       DIM       2                                                *
.*       ATCHAR    INIT      "@"                                              *
.*       AYY       DIM       2                                                *
.*       CHECK     FORM      3                                                *
.*       CHECKA    FORM      3                                                *
.*       CMDLINE   DIM       80                                               *
.*       CODE      DIM       2                                                *
.*       COUNT     FORM      2                                                *
.*       CURDATE   DIM       8                                                *
.*       DIM20     DIM       20                                               *
.*       DIM8      DIM       8                                                *
.*       FIFTY9    FORM      "59"                                             *
.*       FORM3     FORM      3                                                *
.*       MODE      DIM       1                                                *
.*       MODE1     DIM       1                                                *
.*       OPERCODE  DIM       4                                                *
.*       PATRGMAS  FORM      1                                                *
.*       PATRGPAD  FORM      1                                                *
.*       PATRGPMI  FORM      1                                                *
.*       SURSYST   FORM      1                                                *
.*       SEC1      FORM      "00001"                                          *
.*       SECTOR    FORM      5                                                *
.*       SEVENTY7  FORM      "77"                                             *
.*       SP25      INIT      "                         "                      *
.*       TIMEIS    DIM       8                                                *
.*       VAR1      FORM      1                                                * 
.*       VAR2      FORM      2                                                * 
.*       VDATE     DIM       8                                                *
.*       VSTIM     DIM       5                                                *
.*       VSITE     DIM       6                                                *
.*       VCLIN     DIM       6                                                *
.*       VTIME     DIM       8                                                *
.*       VSLOT     DIM       3                                                *
.*       Z15       INIT      "zzzzzzzzzzzzzzz"                                * 
.*                                                                            * 
.* Open files   :                                                             * 
.*                                                                            *
.*          bokrc1af                                                          *
.*          outpreaf                                                          *
.*          patma1af                                                          *
.*          patmx1af                                                          *
.*          patmi1af                                                          *
.*          patpramf                                                          *
.*          patpraxf                                                          *
.*          patvisaf                                                          *
.*          patpa1af                                                          *
.*          patsurnf         (removed V.10.B01)                               *
.*          patgsrnf                                                          *
.*          patam1af                                                          *
.******************************************************************************
+
.******************************************************************************
.* UPDUR        :  Update U/R number to the appropriate files                 *
.*                                                                            *
.* Parameters   :  PATMA1FD    variables (new) and save (old) variables       *
.*                 PATPR1FD    variables                                      *
.*                 NMPNUMB     DIM    20  New National Id Number              *
.*                 URSYST      Originating System : 1 = A & E                 *
.*                                                  2 = Outpatients           *
.*                                                  3 = Inpatients            *
.*                                                  4 = Other Financial       *
.*                                                  5 = Day Centre            *
.* Returns      :  EXIT  0 = Ok to continue                                   *
.*                       1 = Error in PAS-CIS interface                       *
.******************************************************************************
.
UPDUR     OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TEN;*24,PTCNICAD,*244,CHOSPNUM
          READ      CONTROLF,TEN9;*208,CPMIAD
          READ      CONTROLF,FIFTY9;*216,PTPRVALS,*234,PTCNNMPI
          READ      CONTROLF,TWENTY1;*138,PTCNNHII
          READ      CONTROLF,HUND05;*1,PTCNACTI
.
          MATCH     ZEROUR,PURNO
          IF        @EQUAL
            CALL      UDMAS000               * Update master/pram file & surn
            CALL      UDPSN000               * Update previous surname
          ELSE
            SCAN      "WEB",PRGID
            IF        !@EQUAL
              CALL      UDADR000               * Update previous address file
            ENDIF
            RESET     PRGID
            SCAN      "NHIWEB",PRGID
            IF        @EQUAL
              CALL      UDADR000               * Update previous address file
            ENDIF
            RESET     PRGID
.
            CLOCK     TIME,PTMAUTIM
            MOVE      USERID,PTMAUUID
.
            CALL      UDPMI000               * Update PMI alterations file
            CALL      UDMAS000               * Update master/pram file & surn
            CALL      UDPSN000               * Update previous surname
            CALL      UDRGP000               * Update previous Reg GP file
.
            MATCH     "DGSRVPMI",PRGID
            IF        !@EQUAL
              MATCH     "HL7RECVR",PRGID
              IF        !@EQUAL
                CALL      PMIGTNID             * get national id for dgate write
                MOVE      NMPNUMB,PTNINMPI
                MOVE      ONE,HL7TRGID
                MOVE      "UPDUR   ",HL7INCLD
                PROC      DGCLICUP             * send details to Dgate  *C-90201
              ENDIF
            ENDIF
          ENDIF
.
.----     NB! UMAUD000 should be called last because 
.         it overrides the save variables.
.
          CALL      UMAUD000                 * Update Master Audit file
.
UPDUR900  MOVE      ZERO,EXIT
          GOTO      UPDUR999
.
UPDUR910  MOVE      ONE,EXIT
.
UPDUR999  RETURN
+
.******************************************************************************
.* UDMAS000 : Update the master file record       
.******************************************************************************
UDMAS000  CALL      IBACLOCK
          PACK      AGEDATE,CCC,CYY,CMM,CDD
          REP       " 0",AGEDATE
          CALL      CALCAGE 
          MOVE      PSAGEYRS,PSAGE
          MATCH     ZEROUR,PURNO
          IF        @EQUAL
            MOVE      AADMNO,PURNO
            CALL      UPPRAM1                  * Update and unlock master file
            MOVE      ZEROUR,PURNO             * Restore U/R Number
          ELSE
            MOVE      PURNO,KEY8
            CALL      RDAMAST1
            BRANCH    OVRCD,UDMAS700
            CALL      UPMAST1
            CALL      UPPMPX21
          ENDIF
.
          MATCH     PTMASNAM,SPTMASNM        * changes in surname?
          GOTO      UDMAS100 IF NOT EQUAL    * yes 
.
          MATCH     PMPXFNAM,SPMPXFNM        * changes in first given name?
          GOTO      UDMAS100 IF NOT EQUAL    * yes
.
          MATCH     PMPXSNAM,SPMPXSNM        * changes in second given name?
          GOTO      UDMAS100 IF NOT EQUAL    * yes
.
          MATCH     PBDATE,SPBDATE           * changes in dob?
          GOTO      UDMAS100 IF NOT EQUAL    * yes
.
          MATCH     PSEX,SPSEX               * changes in sex?
          GOTO      UDMAS100 IF NOT EQUAL    * yes
.
          MOVE      SPTMASNM,GSRSNAM       * New surname
          MOVE      SPMPXFNM,GSRGNAM       * New 1st given name
          MOVE      SPMPXSNM,PTGSGNM2      * New 2nd given name
          MOVE      SPBDATE,GSRDOB         * New DOB
          MOVE      SPSEX,GSRSEX           * New Sex
          MOVE      PURNO,GSRURNO
          MOVE      ZEROVISN,GSRBILL
          PACK      KEY115,GSRURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                           GSRSEX,SP70,SP70
          CALL      RDPTGSR1               * Read the record again
          BRANCH    OVRCD,UDMAS999  
          CALL      UPDSUR
          GOTO      UDMAS999
.
UDMAS100  MOVE      SPTMASNM,GSRSNAM         * New surname
          MOVE      SPMPXFNM,GSRGNAM         * New given names
          MOVE      SPMPXSNM,PTGSGNM2
          MOVE      PURNO,GSRURNO
.... CAR 47828
          MOVE      SPBDATE,GSRDOB           * New DOB
          MOVE      SPSEX,GSRSEX             * New Sex
....
          MOVE      ZEROVISN,GSRBILL
          CALL      SOUNDEX                  * Calculate new sound-ex key
          CALL      SOUNDX2
.
          CALL      UPDSUR
          GOTO      UDMAS999
.
UDMAS700  SCAN      "WEB",PRGID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,*B,*V2LON,"** U/R Number not on file **    ";
            CALL      HITENTER
          ENDIF
.
UDMAS999  RETURN
+
.******************************************************************************
.* UDADR000 : Update the previous address file        
.******************************************************************************
.
.         If the ACT PMI Interface is on, then the previous address will
.         be handled in IBAACT04, not here
.
UDADR000  BRANCH    PTCNACTI,UDADR999        * ACT PMI on
.
          MATCH     PADD1,SPADD1             * changes in address?
          GOTO      UDADR100 IF NOT EQUAL    * yes
.
          MATCH     PADD2,SPADD2             * changes in address?
          GOTO      UDADR100 IF NOT EQUAL    * yes
.
          MATCH     PSUBRB,SPSUBRB           * changes in address?
          GOTO      UDADR100 IF NOT EQUAL    * yes
.
          MATCH     PADD4,SPADD4             * changes in address?
          GOTO      UDADR100 IF NOT EQUAL    * yes
.
          MATCH     PPOST,SPPOST             * changes in address?
          GOTO      UDADR100 IF NOT EQUAL    * yes
.
          IF        PTCNDORP=1
            MATCH     PTMXDOFR,SPDOR         * change in DOR?
            GOTO      UDADR100 IF NOT EQUAL  * yes
          ENDIF
.
          GOTO      UDADR999                 * no changes in address
.
UDADR100  CALL      IBACLOCK
          CLOCK     TIME,PATIME
.
          PACK      PADATE,CCC,CYY,CMM,CDD
          REP       " 0",PADATE
.
          OPEN      PATPA1A1,"patpa1af"
.
          PACK      KEY24,PURNO,PADATE,PATIME
          CALL      RDPADD1
          BRANCH    OVRCD,UDADR500
          GOTO      UDADR600
.
UDADR500  MOVE      PURNO,PAURNO
          MOVE      SPADD1,PAADD1
          MOVE      SPADD2,PAADD2
          MOVE      SPADD4,PAADD4
          MOVE      SPSUBRB,PASUBR 
          MOVE      SPPOST,PAPOST
          MOVE      SPTELEP,PATELEP
          MOVE      SPTELEB,PATELEB
          MOVE      SPCELL,PTPAMOBL
          MOVE      SPDOR,PTPADOR
          PACK      PTPAEMAL,SP70,SP70
          MOVE      PADATE,PTPACDTE
          MOVE      PATIME,PTPACTIM
          MOVE      USERID,PTPAWEBC
          MOVE      SP70,PTPALUPD
          MOVE      SP70,PTPALUPT
          MOVE      USERID,PTPAWEBU                                    *C-49647
          MOVE      SPOSMS,PTPAOSMS
          MOVE      SP70,PTPASPAR
.
          CALL      WRPADD1
          GOTO      UDADR999
.
UDADR600  MOVE      PURNO,PAURNO
          MOVE      SPADD1,PAADD1
          MOVE      SPADD2,PAADD2
          MOVE      SPADD4,PAADD4
          MOVE      SPSUBRB,PASUBR
          MOVE      SPPOST,PAPOST
          MOVE      SPTELEP,PATELEP
          MOVE      SPTELEB,PATELEB
          MOVE      SPCELL,PTPAMOBL
          MOVE      SPDOR,PTPADOR
          PACK      PTPAEMAL,SP70,SP70
          MOVE      PADATE,PTPALUPD
          MOVE      PATIME,PTPALUPT
          MOVE      USERID,PTPAWEBU
          MOVE      SPOSMS,PTPAOSMS
          MOVE      SP70,PTPASPAR
.
          CALL      UPPADD1
.
UDADR999  RETURN
+
.******************************************************************************
.* UDRGP000 : Update the registered GP/Practice changes file  
.******************************************************************************
UDRGP000  PACK      KEY30,SAVEPHCP,SAVEPRAC,SAVEPCTR,SP70
.
          MATCH     KEY30,SP70
          GOTO      UDRGP999 IF EQUAL
.
          PACK      PMPXRHC1,PMPXRHC1,SP70
          PACK      PMPXRH1G,PMPXRH1G,SP70
          PACK      PMPXR1GC,PMPXR1GC,SP70
.
          MATCH     PMPXRHC1,SAVEPHCP
          GOTO      UDRGP100 IF NOT EQUAL
.
          MATCH     PMPXRH1G,SAVEPRAC
          GOTO      UDRGP100 IF NOT EQUAL
.
          MATCH     PMPXR1GC,SAVEPCTR
          GOTO      UDRGP100 IF NOT EQUAL
.
          GOTO      UDRGP999                     * no changes to  GP/Practice
.
UDRGP100  MOVE       "002",D3
          CALL       WDAU0000                    * Write to HCP details audit file
.
UDRGP999  RETURN
+
.******************************************************************************
.* UDPMI000  : Update the PMI alterations file      
.******************************************************************************
UDPMI000  MOVE      THREE,DETNTREC
.
          PACK      PUSR1,PUSR1,SP3
          MATCH     SP3,PUSR1
          IF        !@EQUAL
            MOVE      SP20,TDESC
            MOVE      "P1",DIM2
            PACK      KEY5,DIM2,PUSR1
            CALL      RDCODE1
          ENDIF
            
UDPMI100  BRANCH    CPMIAD,UDPMI200
          GOTO      UDPMI999
.
UDPMI200  CALL      IBACLOCK
.
          OPEN      PATURAD1,"paturadf"
.
          PACK      URDATE,CCC,CYY,CMM,CDD       * Date
          REP       " 0",URDATE
.
          MOVE      URSYST,SURSYST           * Save Originiating System
.
          MOVE      PURNO,URURNO             * UR Number
          MOVE      ANSC,URRTYPE             * Audit Record
.
          PACK      KEY17,URRTYPE,URDATE,URURNO
          CALL      RDAURAD1                 * Record Exist
          BRANCH    OVRCD,UDPMI400           * No.
.
          MOVE      SURSYST,URSYST           * Yes. Restore Originating System
          CALL      UPURAD1                  * Update UR.
.
          MOVE      URDATE,PCHGDTE
          GOTO      UDPMI999
.
UDPMI400  CALL      WRURAD1                 * Write Record
.
          MOVE      URDATE,PCHGDTE
.
UDPMI999  RETURN
+
.****************************************************************************
.*  UDPSN000  :  Update Previous Surname       
.****************************************************************************
UDPSN000  COMPARE   ONE,PTPRVALS             * Previous name to be an alias ?
          GOTO      UDPSN999 IF NOT EQUAL    * No.
.
          MATCH     SP20,PPSNAME             * Yes. Is there a previous name ?
          GOTO      UDPSN999 IF EQUAL        * No.
.
          MATCH     PPSNAME,PSNAME           * Yes. Previous name = Actual name?
          GOTO      UDPSN999 IF EQUAL        * Yes.
.
          MOVE      GSRSNAM,SSDXSNAM
          PACK      GSRSNAM,PPSNAME,SP70
          MOVE      PURNO,GSRURNO
          MOVE      ZEROVISN,GSRBILL
          MOVE      ZERO,GSRSYS
          CALL      SOUNDEX
.
          PACK      GSRSNAM,PPSNAME,SP70
          MOVE      PMPXFNAM,GSRGNAM
          MOVE      PMPXSNAM,PTGSGNM2
          MOVE      PBDATE,GSRDOB
          MOVE      PSEX,GSRSEX
          PACK      KEY115,GSRURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                           GSRSEX,SP70,SP70
          CALL      RDPTGSR1                                  * Record exist ?
          IF        OVRCD=1
            CALL      WRPTGSR1                                * No.Write new rec
          ENDIF
.
UDPSN999  RETURN
.******************************************************************************
.*  SVMAS000  :  Save master variables                                        *
.*                                                                            *
.*  Parameters   :  PATMA1FD    variables                                     * 
.******************************************************************************
SVMAS000  MOVE      PMICRO,SPMICRO
          MOVE      PUSR1,SPUSR1
          MOVE      PUSR2,SPUSR2
          MOVE      PUSR3,SPUSR3
          MOVE      PUSR4,SPUSR4
          MOVE      PUSR5,SPUSR5
          MOVE      PUSR6,SPUSR6
          MOVE      PUYN1,SPUYN1
          MOVE      PUYN2,SPUYN2
          MOVE      PUYN3,SPUYN3
.
          MOVE      PURNO,SPURNO
          MOVE      PTITL,SPTITL
          MOVE      PSNAME,SPSNAME
          MOVE      PGNAME,SPGNAME
          MOVE      PPSNAME,SPPSNAME
          MOVE      PSMOK,SPSMOK
          MOVE      PSEX,SPSEX
          MOVE      PMSTAT,SPMSTAT
          MOVE      PBDATE,SPBDATE
          MOVE      PSAGE,SPSAGE
          MOVE      PCONT,SPCONT
          MOVE      PREG,SPREG
          MOVE      PTYPE,SPTYPE
          MOVE      POCCUP,SPOCCUP
          MOVE      PMEDI,SPMEDI
          MOVE      PPENNO,SPPENNO
          MOVE      PPNDTE,SPPNDTE
          MOVE      PREPAT,SPREPAT
          MOVE      PCHGDTE,SPCHGDTE
          MOVE      PLOCDOC,SPLOCDOC
          MOVE      PCEASE,SPCEASE  
.
          MOVE      PNKNAME,SPNKNAME
          MOVE      PNKADD1,SPNKADD1
          MOVE      PNKADD2,SPNKADD2
          MOVE      PNKSUBR,SPNKSUBR
          MOVE      PNKADD4,SPNKADD4
          MOVE      PNKPOST,SPNKPOST
          MOVE      PNKTELP,SPNKTELP
          MOVE      PNKTELB,SPNKTELB
          MOVE      PNKRELP,SPNKRELP
.
          MOVE      PADD1,SPADD1
          MOVE      PADD2,SPADD2
          MOVE      PSUBRB,SPSUBRB
          MOVE      PADD4,SPADD4
          MOVE      PPOST,SPPOST
          MOVE      PTELEP,SPTELEP
          MOVE      PTELEB,SPTELEB
          MOVE      PTMXCELL,SPCELL
          MOVE      PTMXDOFR,SPDOR
          MOVE      PMPXRHC1,SPREGP
          MOVE      PMPXRH1G,SPGPPC
          MOVE      PMPXR1GC,SPGPPT
          MOVE      PTMXOSMS,SPOSMS
.
          MOVE      PHIGH1,SPHIGH1
          MOVE      PHIGH2,SPHIGH2
          MOVE      PHIGH3,SPHIGH3
          MOVE      PAUTOPY,SPAUTOPY
          MOVE      PDECDTE,SPDECDTE
          MOVE      PYRRES,SPYRRES
          MOVE      PTMASNAM,SPTMASNM
          MOVE      PMPXFNAM,SPMPXFNM
          MOVE      PMPXSNAM,SPMPXSNM
.
.-------- Save Community Card Details ---------------------------------
.
          MOVE      PTMXCARD,SPCARD
          MOVE      PTMXCEXP,SPCEXP
          MOVE      PTMXCXMP,SPCXMP
          MOVE      PTMXFMLY,SPFMLY
.
SVMAS999  RETURN
+
.****************************************************************************
.*  SVPRA000  :  Save PRA variables         
.****************************************************************************
SVPRA000  MOVE      PKNAME,SPKNAME
          MOVE      PKADD1,SPKADD1
          MOVE      PKADD2,SPKADD2
          MOVE      PKSUBR,SPKSUBR
          MOVE      PKADD4,SPKADD4
          MOVE      PKPOST,SPKPOST
          MOVE      PKTELEP,SPKTELP
          MOVE      PKTELEB,SPKTELB
          MOVE      PKRELP,SPKRELP
.
SVPRA999  RETURN
+
.*********************************************************************
.* UMAUD000 : Update Master Audit file
.*********************************************************************
.
UMAUD000  BRANCH    CAUDM,UMAUD999          * check if Audit is on
.
          MOVE      "B",ACTION              * set Audit Action to Before Change
          CALL      WRMAUD                  * write Audits
          MOVE      "C",ACTION              * set Audit Action to After Change
          CALL      SVMAS000                * Save Master variables
          MOVE      PMPXRHC1,SAVEPHCP       * Save variable for HCP Audit  
          MOVE      PMPXRH1G,SAVEPRAC       * Save variable for HCP Audit
          MOVE      PMPXR1GC,SAVEPCTR       * Save variable for HCP Audit
          CALL      SVPRA000                * Save PRA variables
          CALL      WRMAUD                  * write Audits
.
UMAUD999  RETURN
.
          INC       PMIGTNID                * get national number
.
