.------------------------------------------------------------
.  File          :  ALLMKIPR.PBL
.
.  Function      :  Update Keyword Index File for Allied Health Medications
.
.  Parameters    :  ALMECODE - Key to allied health medication table
.
.  Modifications :
.
.  V10.04.01  27/03/2014  Ebon Clements     CAR 298164
.             Remove left and right brackets from keywords
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Procedure to Update
.  a Key Word Index Table that can be used for searching 
.  Masterfiles. The procedure should be called from the 
.  Add, Update and Delete functions in the Masterfile 
.  Maintenance Function.
.  
.  A Key Word Index Table is Defined in the following Manner
.                              
.  ALMKCODE    DIM       20    * Procedure Code
.  ALMKKWRD    DIM       60    * Key Word
.  ALMKSPAR    DIM       30    * Spare
.
.  Index 1   ALMKCODE, ALMKKWRD
.  Index 2   ALMKKWRD, ALMKCODE
.
.  To convert this Procedure the following should be performed
.    Global Change PAT to the System ID eg :%s/PAT/PAT/g
.    Global Change MKI to the File   ID eg :%s/MKI/MKI/g
.    Global Change ALMK to the IO Call ID eg :%s/ALMK/ALMK/g
.    Modify the KEYINDEX defintion to combined Code Field Lengths eg 6
.    Global Change KEY80 to the Key for the Key Word Table eg KEY80
.    Change ALMECODE to the Code Fields eg DCODEhh
.    Change the WPGE0000 routine to call the HCPBW000 for each
.    string of key words to be indexed for the Coded Field
.    eg MOVE    DSNAME,KEYWORDS
.       CALL    HCPBW000
.------------------------------------------------------------
          DEFPROC   ALLMKIUP
.
          INC       ALLMKIFD/INC
.
KEYINDEX  DIM       20        * Code Index in Table
KEYWORDS  DIM       60        * String Containing the Key Words to be Indexed
SIXTY     FORM      "60"
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       50
KEYWRD02  DIM       50
KEYWRD03  DIM       50
KEYWRD04  DIM       50
KEYWRD05  DIM       50
KEYWRD06  DIM       50
KEYWRD07  DIM       50
KEYWRD08  DIM       50
KEYWRD09  DIM       50
KEYWRD10  DIM       50
.------------------------------------------------------------
. Generate Word Index
.------------------------------------------------------------
WPGE0000  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     ALLMKIA1,"allmkiaf"
          TRAPCLR  IO
          BRANCH   OVRCD,WPGE9999
.
          MOVE     ALMECODE,KEYINDEX
          CALL     WTPCL000           * Remove Current Key Word Index
          BRANCH   EXIT,WPGE9999
.
          MOVE     ALMECODE,KEY20     * Validate Record on File
          CALL     RDALMED1
          BRANCH   OVRCD,WPGE1900     * If Not then it Must be a Delete/Clear
.
          MOVE     ALMECODE,KEYINDEX
          MOVE     ALMEDESC,KEYWORDS
          REP      "( ) ",KEYWORDS    * Remove left and right brackets
.
          CALL     ALLPW000
.
WPGE1900  CLOSE    ALLMKIA1
.
WPGE9999  GOTO     ALLMKIEP
.------------------------------------------------------------
. Clear Current Key Words
.------------------------------------------------------------
WTPCL000  PACK     KEY80,KEYINDEX,SP70
          CALL     RSALMKI1
          CALL     RKALMKI1
          BRANCH   OVRCD,WTPCL999
          MATCH    KEYINDEX,ALMKCODE 
          GOTO     WTPCL999 IF NOT EQUAL
.
          PACK     KEY80,ALMKCODE,ALMKKWRD,SP70
          CALL     DEALMKI1
          GOTO     WTPCL000
.
WTPCL999  RETURN
.-------------------------------------------------
. Determine Words to Index
.-------------------------------------------------
ALLPW000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      ALLPW000 IF NOT EOS
            GOTO      ALLPW999               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWORDS,SP30      * reset form pointer
          MOVE      KEY60,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      ALLPW100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Save this Word
.
ALLPW100  MOVE      KEYWORDS,KEY60
          PACK      KEY60,KEY60,SP70
          REP       UPPLOW,KEY60             * Always Upper Case
          PACK      KEY80,KEYINDEX,KEY60
          UNPACK    KEY80,ALMKCODE,ALMKKWRD
          CALL      RDALMKI1
          IF        OVRCD=1
            CALL      WRALMKI1
          ENDIF
          GOTO      ALLPW190
.
.         Check for any more words
.
ALLPW190  SETLPTR   KEYWORDS,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY60,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY60,KEYWORDS
          GOTO      ALLPW000
.
ALLPW999  RETURN
.
          INC       ALLMKIIO/INC
          INC       IBAOVRIO/INC
.
ALLMKIEP  ENDPROC                          * End of Procedure
.
