.*****************************************************************************
.*                                                                           *
.*    Include    :  WATAHOCS.PBL                                             *
.*                                                                           *
.*    Function   :  Waiting List Ad-Hoc Patient Seletion                     *
.*                                                                           *
.*    Author     :  Whirlpool & IBAWAT12                                     *
.*                                                                           *
.*    Date       :  11/12/92                                                 *
.*                                                                           *
.*    Modications:  24/03/93     Whirlpool                                   *
.*                               Added parameter SESNDATE                    *
.*                               Added Validation of not available dates     *
.*                  08/04/93     Whirlpool                                   *
.*                               Added New 11 fields                         *
.*                  26/05/93     Whirlpool                                   *
.*                               Added Invalid Security Message              *
.*                  12/07/93     Whirlpool    SRF  123516                    *
.*                               change to make more efficient               *
.*                  23/07/93     Whirlpool    SRF  440243                    *
.*                               change to make user defined field parameter *
.*                               driven                                      *
.*                  28/10/1993    Justin Coates  QUOTE 440017  SRF 440204    *
.*                  allow for new GP practice file                           *
.*                  22/11/1993   Rob Leonard  SRF 440445                     *
.*                               Added two new procedure status              *
.*                  08/12/1993   Rob Leonard  SRF 440542                     *
.*                               Fixed bug in matching of gp practice code   *
.*                  16/12/1993   Glenn Berry / Rob Lowe                      * 
.*                               Fixed Default GP paractice code field.      *
.*                  05/01/1994   Rob Leonard  SRF 440591                     *
.*                               Fixed Consultant defaulting to prev entered *
.*                               GP code. Fix checking of suspended Proc Stat*
.*                  16/03/1994   ROD          SRF 440693                     * 
.*                               Add check on do not admit before date.      *
.*                  08/04/1994   Paul          SRF 440793                    * 
.*                               Remove stat 8 = not performed on option 10. *
.*                  01/07/1994   Rob Leonard   SRF 440475                    *
.*                               Add option to print                         *
.*                                                                           *
.*****************************************************************************
.*                                                                           *
.*    Parameters :  SESNDATE     - DIM  8                                    *
.*                               - Spaces if not a theatre booking           *
.*                                 or not in OPR40                           *
.*                               - SESSION DATE if a theatre booking         *
.*                                                                           *
.*****************************************************************************
.*                                                                           *
.*    Returns    :   EXIT = 0      - valid patient selected                  *
.*                                 - Their patient master file details       *
.*                                 - The waiting list treament record        *
.*                   EXIT = 1      - Exit selected                           *
.*                                                                           *
.*****************************************************************************
.*                                                                           *
.*    Includes                                                               *
.*    ---------                                                              *
.*                                                                           *
.*    WATAHSVR.INC       * Data Variables for this include                   *
.*                                                                           *
.*    WATTR1FD.INC       * Waiting list treatment file                       *
.*    WATPROFD.INC       * Procedures                                        *
.*    WATSUSFD.INC       * Suspended Procedures                              *
.*    PATMA1FD.INC       * Patient Master File                               *
.*    PMSHCGFD.INC       * GP Practice File                                  *
.*    PMSHCPFD.INC       * GP File                                           *
.*    PMSHCLFD.INC       * GP/Practice Link File                             *
.*    PATDO1FD.INC       * Doctor File                                       *
.*    PATCODFD.INC       * Codes File                                        *
.*    CONPURFD.INC       * Purchaser file                                    *
.*    CONCWLFD.INC       * W/L contract file                                 *
.*                                                                           *
.*    PMSHCGDS           * ? routine for GP practice procedures              *
.*    PMSHCPDS           * ? routine for GP'S                                *
.*    WATDSPRO           * ? routine for WL Procedures                       *
.*    PATDOCDS           * ? routine for Doctors file                        *
.*    PATCODDS           * ? routine for Patient Codes file                  *
.*    CONPURDS           * ? routine for Purchaser file                      *
.*                                                                           *
.*    PATCODKY           * Codes file keyin                                  *
.*    PATDOCKY           * Doctor file keyin                                 *
.*    KYCONPUR           * Purchaser file keyin                              *
.*                                                                           *
.*****************************************************************************
.
WATAHOCS  CALL      CLER0000
          CALL      IMTH0000           * Initialise Match variables
          CALL      SCRN0000           * parameter screen + keyin params
          CALL      SIPC0000           * select to update
          BRANCH    EXIT,WTAHS999      * (E)xit selected
          BRANCH    TOTLFLAG,WTAHS500
.
          CALL      XTRA0000           * extract details and select a patient
          IF        EXIT = 1
            MOVE      ZERO,EXIT     
            GOTO      WATAHOCS
          ENDIF
          GOTO      WTAHS999
.
WTAHS500  CALL      TALY0000
          GOTO      WATAHOCS
.
WTAHS999  RETURN         
.
.****************************************************************************
.*                              IMTH0000                                    *
.*                     Initialise the match variables                       *
.****************************************************************************
.
IMTH0000  MOVE      ZERO,COUNT
          WHILE     COUNT < 29
            ADD       ONE,COUNT
            STORE     ONE,COUNT,MATCHF1,MATCHF2,MATCHF3,MATCHF4,MATCHF5,MATCHF6:
                          MATCHF7,MATCHF8,MATCHF9,MATCHF10,MATCHF11,MATCHF12:
                          MATCHF13,MATCHF14,MATCHF15,MATCHF16,MATCHF17,MATCHF18:
                          MATCHF19,MATCHF20,MATCHF21,MATCHF22,MATCHF23,MATCHF24:
                          MATCHF25,MATCHF26,MATCHF27,MATCHF28,MATCHF29
          DO
IMTH9999  RETURN
.
.****************************************************************************
.*                              SCRN0000                                    *
.*                     Display the screen layout                            *
.****************************************************************************
.
SCRN0000
.
.-----    Get user defined field details ------
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,THIRTY7;*223,WTCNAHUD
          MOVE      SP20,TDESC
          PACK      KEY5,WTCNAHUD,SP5
          CALL      RDCODE1
          RESET     TDESC
          SETLPTR   TDESC,17
.
.------   Display the screen ----
.
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON," 1. ",*HOFF:
                    "U/R Number       : ",*P35:4,"to":
                    *P1:5,*V2LON," 2. ",*HOFF:
                    "Procedure Code   :",*P35:5,"to":
                    *P1:6,*V2LON," 3. ",*HOFF:
                    "Est. Proc. Time  :",*P35:6,"to":
                    *P1:7,*V2LON," 4. ",*HOFF:
                    "Consultant       :",*P35:7,"to":
                    *P1:8,*V2LON," 5. ",*HOFF:
                    "Patient Class    :",*P35:8,"to":
                    *P1:9,*V2LON," 6. ",*HOFF:
                    "Guar. Adm Date   :",*P35:9,"to":
                    *P1:10,*V2LON," 7. ",*HOFF:
                    "Do Not Admit Bef :",*P35:10,"to":
                    *P1:11,*V2LON," 8. ",*HOFF:
                    "Date on List     :",*P35:11,"to":
                    *P1:12,*V2LON," 9. ",*HOFF:
                    "Scheduled Admiss :",*P35:12,"to":
                    *P1:13,*V2LON,"10. ",*HOFF:
                    "Procedure status :",*P35:13,"to":
                    *P1:14,*V2LON,"11. ",*HOFF:
                    "List Type        :",*P35:14,"to":
                    *P1:15,*V2LON,"12. ",*HOFF:
                    "Estimated Stay   :",*P35:15,"to":
                    *P1:16,*V2LON,"13. ",*HOFF:
                    "Date on system   :",*P35:16,"to":
                    *P1:17,*V2LON,"14. ",*HOFF:
                    "Removal Date     :",*P35:17,"to":
                    *P49:5,*V2LON,"15. ",*HOFF:
                    "Removal Reason   : ":  
                    *P49:6,*V2LON,"16. ",*HOFF:
                    "Priority Code    : ": 
                    *P49:7,*V2LON,"17. ",*HOFF:
                    "Resident Status  : ": 
                    *P49:8,*V2LON,"18. ",*HOFF:
                    "Speciality       : ": 
                    *P49:9,*V2LON,"19. ",*HOFF:
                    "District of Res  :":
                    *P49:10,*V2LON,"20. ",*HOFF:
                    *+,TDESC,":":
                    *P49:11,*V2LON,"21. ",*HOFF:
                    "Admin Category   :":
                    *P49:12,*V2LON,"22. ",*HOFF:
                    "Short Stay       :":
                    *P49:13,*V2LON,"23. ",*HOFF:
                    "Elec. Adm Type   :":
                    *P49:14,*V2LON,"24. ",*HOFF:
                    "Purchaser        :":
                    *P49:15,*V2LON,"25. ",*HOFF:
                    "Referring GP     :":
                    *P49:16,*V2LON,"26. ",*HOFF:
                    "GP Practice Code :":
                    *P49:17,*V2LON,"27. ",*HOFF:
                    "Registered GP    :":
                    *P49:18,*V2LON,"28. ",*HOFF:
                    "Deceased Patient : ",*V2LON,"No ":
                    *P49:19,*V2LON,"29. ",*HOFF:
                    "Total (Y/N)      : ",*V2LON,"No "

SCRN9999  RETURN
.
.*****************************************************************************
.*                               PRCE0000                                    *
.*                          Keyin the procedure range                        *
.*****************************************************************************
.
PRCE0000
          MOVE      EVERT,ROW
          MOVE      ECOL,COL
          MOVE      ZERO,FTFLAG
          CALL      KPRO0000
.
          MOVE      ONE,FTFLAG
          MOVE      "38",ECOL
          CALL      KPRO0000
.
PRCE9999  RETURN
.
.*****************************************************************************
.*                               KPRO0000                                    *
.*                       keyin the procedure type                            *
.****************************************************************************
.------------------------------------------------------------------------------
. ----- Keyin procedure code range -----
.------------------------------------------------------------------------------
KPRO0000  
          KEYIN     *PECOL:EVERT,*DV,UNDLN9:
                    *PECOL:EVERT,*V2LON,DIM9:
                    *PECOL:EVERT,*DV,DIM9
.
          PACK      DIM9,DIM9,SP9
          MATCH     DIM9,SP9
          IF        @EQUAL
            IF        FTFLAG = 0
              DISPLAY   *PECOL:EVERT,*V2LON,"Start"
              MOVE      SP9,FROMPROC
            ELSE
              DISPLAY   *PECOL:EVERT,*V2LON,"Finish"
              MOVE      "zzzzzzzzz",TOPROC
            ENDIF
            GOTO      KPRO9999
          ENDIF
.
          MATCH     QUEST,DIM9
          GOTO      KPRO5000 IF EQUAL
.
          PACK      KEY9,DIM9
          CALL      RDPROA1
          IF        OVRCD = 1
            DISPLAY   *P1:24,*B,*EL,"Procedure is not on file.  ";
            CALL      HITENTER
            GOTO      KPRO0000
          ENDIF
.
          IF        FTFLAG = 1
            MATCH     FROMPROC,DIM9
            IF        @LESS
              DISPLAY   *P1:24,*B,"Code must be >= first code.  ";
              CALL      HITENTER
              GOTO      KPRO0000
            ENDIF
            MOVE      DIM9,TOPROC
          ELSE
            MOVE      DIM9,FROMPROC

          ENDIF
          GOTO        KPRO9999
.
KPRO5000  CALL        GETSCR00
KPRO5100  CALL        WATDSPRO
KPRO5200  DISPLAY     *P1:24,*EL,"Procedure Code : " 
          KEYIN       *P18:24,*DV,UNDLN9:
                      *P18:24,*V2LON,DIM9:
                      *P18:24,*DV,DIM9
.
          PACK      DIM9,DIM9,SP9
          MATCH     DIM9,SP9
          IF        @EQUAL
            IF        FTFLAG = 0
              CALL      PUTSCR00
              DISPLAY   *PECOL:EVERT,*V2LON,"Start"
              MOVE      SP9,FROMPROC
            ELSE
              CALL      PUTSCR00
              DISPLAY   *PECOL:EVERT,*V2LON,"Finish"
              MOVE      "zzzzzzzzz",TOPROC
            ENDIF
            GOTO      KPRO9999
          ENDIF
.
          MATCH     QUEST,DIM9
          GOTO      KPRO5100 IF EQUAL
.
          PACK      KEY9,DIM9
          CALL      RDPROA1
          IF        OVRCD = 1
            DISPLAY   *P1:24,*B,*EL,"Procedure is not on file.  ";
            CALL      HITENTER
            GOTO      KPRO5200
          ENDIF
.
          IF        FTFLAG = 1
            MATCH     FROMPROC,DIM9
            IF        @LESS
              DISPLAY   *P1:24,*B,"Code must be >= first code.  ";
              CALL      HITENTER
              GOTO      KPRO5200
            ENDIF
            MOVE      DIM9,TOPROC
          ELSE
            MOVE      DIM9,FROMPROC

          ENDIF
          CALL        PUTSCR00
          DISPLAY     *PECOL:EVERT,*V2LON,DIM9
              
KPRO9999  RETURN
.
.*****************************************************************************
.*                               CONS0000                                    *
.*                         keyin the cosultant range                         *
.*****************************************************************************
.
CONS0000  MOVE      ZERO,EXIT 
          MOVE      SP10,CKYIDEF6              * No default for range
          CALL      PATDOCKY
          IF        EXIT = 1
            DISPLAY   *PECOL:EVERT,*V2LON,"Start"
            MOVE      SP6,DOCCODE
            MOVE      ZERO,EXIT
            GOTO      CONS5000
          ENDIF
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      CONS9999
          ENDIF
          MOVE      DOCCODE,FROMCONS
.
CONS5000  MOVE      ZERO,EXIT 
          MOVE      "38",ECOL
          CALL      PATDOCKY
          IF        EXIT = 1
            DISPLAY   *PECOL:EVERT,*V2LON,"Finish"
            MOVE      "zzzzzz",TOCONS
            MOVE      ZERO,EXIT
            GOTO      CONS9999
          ENDIF
          IF        EXIT = 2
            MOVE      ONE,EXIT
            GOTO      CONS9999
          ENDIF
          MOVE      DOCCODE,TOCONS
          MATCH     FROMCONS,TOCONS
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"Consultant must be >= first consultant.  ";
            CALL      HITENTER
            GOTO      CONS5000
          ENDIF
        
CONS9999  RETURN
.
.*****************************************************************************
.*                               KTIM0000                                    *
.*                         Keyin the estimated procedure times               *
.*****************************************************************************
.
KTIM0000
          KEYIN     *P24:EVERT,*V2LON,FROMTIME:
                    *P24:EVERT,*DV,FROMTIME
.
          
          KEYIN     *P38:EVERT,*V2LON,TOTIME:
                    *P38:EVERT,*DV,TOTIME
.
          IF        TOTIME = 0
            MOVE      "99999",TOTIME
            DISPLAY   *P38:EVERT,*V2LON,TOTIME
          ENDIF
.
          IF        FROMTIME > TOTIME
            DISPLAY   *P1:24,*B,*EL,"Time must be >= first time.  ";
            CALL      HITENTER
            GOTO      KTIM0000
          ENDIF
.
KTIM9999  RETURN
.
.*****************************************************************************
.*                               GADD0000                                    *
.*                         Keyin a date range                                *
.*****************************************************************************
.
KDAT0000
          MOVE      EVERT,CVERT
          MOVE      ECOL,CCOL
          UNPACK    SP30,SDATE,ENDATE,CDAY,CMON,CYEAR
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    CQUEST,KDAT0000
.
          PACK      SDATE,CCENT,CYEAR,CMON,CDAY
          MATCH     SP2,CDAY 
          IF        @EQUAL
            DISPLAY   *PCCOL:CVERT,*V2LON,"Start"
            MOVE      SP8,SDATE
            GOTO      KDAT5000
          ENDIF
          REP       " 0",SDATE
          CALL      PACDATE
          DISPLAY   *PCCOL:CVERT,*V2LON,CPCDATE
.
KDAT5000  MOVE      "38",CCOL
          MOVE      CCC,CCENT
          CALL      KEYDATE
          BRANCH    CQUEST,KDAT5000
.
          PACK      ENDATE,CCENT,CYEAR,CMON,CDAY
          MATCH     SP2,CDAY  
          IF        @EQUAL
            DISPLAY   *PCCOL:CVERT,*V2LON,"Finish"
            MOVE      "zzzzzzzz",ENDATE
            GOTO      KDAT9999
          ENDIF
          REP       " 0",SDATE
          CALL      PACDATE
          DISPLAY   *PCCOL:CVERT,*V2LON,CPCDATE
.
          MATCH     SDATE,ENDATE
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"Date must be >= from date.  ";
            CALL      HITENTER
            GOTO      KDAT5000
          ENDIF
.
KDAT9999  RETURN 
.
.*****************************************************************************
.*                               KDEC0000                                    *
.*                         Keyin if they want deceased                       *
.*****************************************************************************
.
KDEC0000  ADD       ONE,DCEASEFL       * if 0 then 1 else if 1 then 0
          LOAD      DCEASEFL,DCEASEFL,ONE,ZERO
KDEC1111
          BRANCH    DCEASEFL,KDEC4444
          DISPLAY   *PECOL:EVERT,*V2LON,"No ";
          GOTO      KDEC9999
KDEC4444
          DISPLAY   *PECOL:EVERT,*V2LON,"Yes";
KDEC9999
          RETURN               
+
.*****************************************************************************
.*                               KPCL0000                                    *
.*                         Keyin the patient classifaction                   *
.*****************************************************************************
.
KPCL0000
          MOVE      "P ",CODE
          CALL      PATCODKY                * keyin the speciality
          BRANCH    EXIT,KPCL1000,KPCL9000
          MOVE      ACODE,FROMPATC
          GOTO      KPCL1500
KPCL1000  MOVE      SP3,FROMPATC
          DISPLAY   *PECOL:EVERT,*V2LON,"Start"
.
KPCL1500  MOVE      "38",ECOL
          MOVE      "P ",CODE
          CALL      PATCODKY                * keyin the speciality
          BRANCH    EXIT,KPCL2000,KPCL9000
          MOVE      ACODE,TOPATC
          MATCH     FROMPATC,TOPATC
          IF        @LESS
            DISPLAY   *P1:24,*B,"Classifaction must >= first classifaction.  ";
            CALL      HITENTER
            GOTO      KPCL1500
          ENDIF
          GOTO      KPCL9999
KPCL2000  MOVE      "zzz",TOPATC
          DISPLAY   *PECOL:EVERT,*V2LON,"Finish"
          MOVE      ZERO,EXIT
          GOTO      KPCL9999
.
KPCL9000  MOVE      ONE,EXIT
KPCL9999  RETURN
.
.*****************************************************************************
.*                               KLTY0000                                    *
.*                         Keyin the patient List Type                       *
.*****************************************************************************
.
KLTY0000
          MOVE      "WU",CODE
          CALL      PATCODKY                * keyin the speciality
          BRANCH    EXIT,KLTY1000,KLTY9000
          MOVE      ACODE,FROMLSTT
          GOTO      KLTY1500
KLTY1000  MOVE      SP3,FROMLSTT
          DISPLAY   *PECOL:EVERT,*V2LON,"Start"
.
KLTY1500  MOVE      "38",ECOL
          MOVE      "WU",CODE
          CALL      PATCODKY                * keyin the speciality
          BRANCH    EXIT,KLTY2000,KLTY9000
          MOVE      ACODE,TOLSTT
          MATCH     FROMLSTT,TOLSTT
          IF        @LESS
            DISPLAY   *P1:24,*B,"List Type must >= first List Type.  ";
            CALL      HITENTER
            GOTO      KLTY1500
          ENDIF
          GOTO      KLTY9999
KLTY2000  MOVE      "zzz",TOLSTT
          DISPLAY   *PECOL:EVERT,*V2LON,"Finish"
          MOVE      ZERO,EXIT
          GOTO      KLTY9999
.
KLTY9000  MOVE      ONE,EXIT
KLTY9999  RETURN
.
+
.------------------------------------------------------------------------------
. ----- Keyin UR range -----
.------------------------------------------------------------------------------
KURR0000  MOVE      "       0",KSTUR
          MOVE      EVERT,SVERT
          MOVE      ECOL,SCOL  
          MOVE      EVERT,CVERT
          MOVE      ECOL,CCOL
          MOVE      ONE,SRCHOPT
          MOVE      ONE,SRCHSYS
          MOVE      ZERO,REFRESH
          CALL      GETSCR00
          CALL      KURN
          IF        REFRESH = 0
            CALL      FRESCR00
          ENDIF
.
          IF        EXIT=1
            DISPLAY   *PECOL:EVERT,*V2LON,"Start"
            GOTO      KURR0300
          ENDIF
.
          MOVE      CPPURNO,KSTUR
.
KURR0300  MOVE      "99999999",KTOUR
.          
          
          DISPLAY   *P42:EVERT,"to"
          MOVE      EVERT,CVERT
          MOVE      "45",CCOL
          MOVE      TWO,SRCHOPT
          MOVE      ONE,SRCHSYS
          MOVE      ZERO,REFRESH
          CALL      GETSCR00
          CALL      KURN
          IF        REFRESH = 0
            CALL      FRESCR00
          ENDIF
.
          IF        EXIT=1
            DISPLAY   *P45:EVERT,*V2LON,"Finish"
            GOTO      KURR0400
          ENDIF
          MOVE      CPPURNO,KTOUR
.
KURR0400  
          MATCH     KSTUR,KTOUR
          GOTO      KURR0500 IF LESS
          GOTO      KURR9999
.
KURR0500  DISPLAY   *P1:24,*B,"End U/R must be > start U/R. ";
          CALL      HITENTER
          MOVE      SVERT,EVERT
          MOVE      SCOL,ECOL
          GOTO      KURR0000
.
KURR0700  DISPLAY   *P1:24,*EL,*B,"U/R doesn't exist. ";
          CALL      HITENTER
          MOVE      SVERT,EVERT
          MOVE      SCOL,ECOL
          GOTO      KURR0300
.
KURR9999  RETURN                    
.
.------------------------------------------------------------------------------
. ----- Keyin Procedure status -----
.------------------------------------------------------------------------------
KPST0000  DISPLAY   *P1:24,*V2LON,"1",*HOFF,"=Unschd, ":
                    *V2LON,"2",*HOFF,"=Schd, ":
                    *V2LON,"3",*HOFF,"=Pre-Admtd, ":
                    *V2LON,"4",*HOFF,"=Admtd, ":
                    *V2LON,"5",*HOFF,"=Dschgd, ":
                    *V2LON,"6",*HOFF,"=Rmvd, ":
                    *V2LON,"7",*HOFF,"=Perfmd "
...                    *V2LON,"8",*HOFF,"=Not Perfmd";
.
          KEYIN     *PECOL:EVERT,*V2LON,KSTATUS1;
          IF        KSTATUS1 > 7
            BEEP
            GOTO      KPST0000
          ENDIF
.
          IF        KSTATUS1 > 0
            MOVE      KSTATUS1,WMSTAT
            CALL      VSTA0000           * validate procedure status
            BRANCH    EXIT,KPST0000
            DISPLAY   *PECOL:EVERT,*V2LON,DIM10
          ELSE
            DISPLAY   *PECOL:EVERT,*V2LON,"Start     "
            MOVE      ZERO,KSTATUS1
          ENDIF

          KEYIN     *P38:EVERT,*V2LON,KSTATUS2;
          IF        KSTATUS2 > 7
            BEEP
            GOTO      KPST0000
          ENDIF
.
          IF        KSTATUS2 > 0
            MOVE      KSTATUS2,WMSTAT
            CALL      VSTA0000           * validate procedure status
            BRANCH    EXIT,KPST0000
            DISPLAY   *P38:EVERT,*V2LON,DIM10
          ELSE
            DISPLAY   *P38:EVERT,*V2LON,"Finish    "
            MOVE      SEVEN,KSTATUS2
          ENDIF
.
          COMPARE   KSTATUS1,KSTATUS2
          GOTO      KPST9000 IF LESS
          GOTO      KPST9999
KPST9000
          DISPLAY   *P1:24,*B,*EL,"End Code must be greater than Start Code.  ";
          CALL      HITENTER
          GOTO      KPST0000
.
KPST9999  RETURN
.
.------------------------------------------------------------------------------
. ----- Keyin estimated stay range -----
.------------------------------------------------------------------------------
KEST0000  MOVE      ZERO,KSTSTAY
          KEYIN     *PECOL:EVERT,*V2LON,*RV,KSTSTAY,*PECOL:EVERT,*DV,KSTSTAY;
.
          MOVE      "99999",KTOSTAY
          KEYIN     *P38:EVERT,*V2LON,*RV,KTOSTAY,*P38:EVERT,*DV,KTOSTAY;
.
          COMPARE   KSTSTAY,KTOSTAY
          GOTO      KEST3500 IF LESS
          GOTO      KEST9999
KEST3500
          DISPLAY   *P1:24,*EL,*B,"End value greater than Start value.  ";
          CALL      HITENTER
          GOTO      KEST0000
.
KEST9999  RETURN
+
.******************************************************************************
.*        This toggles the switch if they like to total the search records.   *
.******************************************************************************
TOTL0000  ADD       ONE,TOTLFLAG       * if 0 then 1 else if 1 then 0
          LOAD      TOTLFLAG,TOTLFLAG,ONE,ZERO
TOTL1111
          BRANCH    TOTLFLAG,TOTL4444
          DISPLAY   *PECOL:EVERT,*V2LON,"No ";
          GOTO      TOTL9999
TOTL4444
          DISPLAY   *PECOL:EVERT,*V2LON,"Yes";
TOTL9999
          RETURN               
+
.*****************************************************************************
.*                               SIPC0000                                    *
.*                         Select Item Post Cancel                           *
.*****************************************************************************
.
SIPC0000  MOVE      ZERO,EXIT
          CALL      MAINQST
          IF        CCITEM < 15
            MOVE    "24",ECOL
            ASSIGN    (CCITEM + 3),EVERT
          ELSE
            MOVE    "72",ECOL
            ASSIGN    ((CCITEM - 14)+4) ,EVERT
          ENDIF
          BRANCH    CCITEM,SIPC0100,SIPC0200,SIPC0300,SIPC0400,SIPC0500:
                           SIPC0600,SIPC0700,SIPC0800,SIPC0900,SIPC1000:
                           SIPC1100,SIPC1200,SIPC1300,SIPC1400,SIPC1500:
                           SIPC1600,SIPC1700,SIPC1800,SIPC1900,SIPC2000:
                           SIPC2100,SIPC2200,SIPC2300,SIPC2400,SIPC2500:
                           SIPC2600,SIPC2700,SIPC2800,SIPC2900
          COMPARE   "-1",CCITEM
          GOTO      SIPC9000 IF EQUAL
          COMPARE   ZERO,CCITEM 
          GOTO      SIPC9999 IF EQUAL
          BEEP
          GOTO      SIPC0000
.
SIPC0100  CALL      KURR0000                * keyin U/R Number
          MOVE      ZERO,MATCHF1
          GOTO      SIPC0000
SIPC0200
          CALL      PRCE0000                * keyin the procedure code
          MOVE      ZERO,MATCHF2
          GOTO      SIPC0000
SIPC0300
          CALL      KTIM0000                * Keyin estimated procedure times
          MOVE      ZERO,MATCHF3
          GOTO      SIPC0000
SIPC0400
          CALL      CONS0000                * keyin the consultant code
          BRANCH    EXIT,SIPC9999
          MOVE      ZERO,MATCHF4
          GOTO      SIPC0000
.
SIPC0500  
          CALL      KPCL0000                * keyin patient class
          BRANCH    EXIT,SIPC9999
          MOVE      ZERO,MATCHF5
          GOTO      SIPC0000
.
SIPC0600 
          CALL      KDAT0000                * keyin the Guar. Adm Date
          MOVE      SDATE,FROMGADD
          MOVE      ENDATE,TOGADD
          MOVE      ZERO,MATCHF6
          GOTO      SIPC0000

SIPC0700 
          CALL      KDAT0000                * keyin do not admit before date
          MOVE      SDATE,FROMDNAB
          MOVE      ENDATE,TODNAB
          MOVE      ZERO,MATCHF7 
          GOTO      SIPC0000
.
SIPC0800 
          CALL      KDAT0000                * keyin Date on list            
          MOVE      SDATE,FROMDONL
          MOVE      ENDATE,TODONL
          MOVE      ZERO,MATCHF8 
          GOTO      SIPC0000
SIPC0900                             
          CALL      KDAT0000                * Scheduled Admission date      
          MOVE      SDATE,FROMDSAD
          MOVE      ENDATE,TODSAD
          MOVE      ZERO,MATCHF9 
          GOTO      SIPC0000
SIPC1000
          CALL      KPST0000                * Procedure status
          MOVE      ZERO,MATCHF10
          GOTO      SIPC0000
SIPC1100
          CALL      KLTY0000                * List Type
          MOVE      ZERO,MATCHF11
          GOTO      SIPC0000
SIPC1200
          CALL      KEST0000                * Estimated Stay
          MOVE      ZERO,MATCHF12
          GOTO      SIPC0000
SIPC1300
          CALL      KDAT0000                * Date on the system
          MOVE      SDATE,FROMDDOS
          MOVE      ENDATE,TODDOS
          MOVE      ZERO,MATCHF13
          GOTO      SIPC0000
.
.
SIPC1400
          CALL      KDAT0000                * Removal Date                  
          MOVE      SDATE,FROMDREM
          MOVE      ENDATE,TODREM
          MOVE      ZERO,MATCHF14
          CALL      SSTAT000                * Set the status
          GOTO      SIPC0000
.
.
SIPC1500
          MOVE      "WR",CODE
          CALL      PATCODKY                * keyin Removal Reason
          BRANCH    EXIT,SIPC1510,SIPC9000
          MOVE      ACODE,REMREAS 
          MOVE      ZERO,MATCHF15
          CALL      SSTAT000                * Set the status
          GOTO      SIPC0000
SIPC1510  MOVE      SP3,REMREAS 
          MOVE      ONE,MATCHF15
          GOTO      SIPC0000
.
.
SIPC1600
          MOVE      "TP",CODE
          CALL      PATCODKY                * keyin Priority Code
          BRANCH    EXIT,SIPC1610,SIPC9000
          MOVE      ACODE,PRIORITY
          MOVE      ZERO,MATCHF16
          GOTO      SIPC0000
SIPC1610  MOVE      SP3,PRIORITY
          MOVE      ONE,MATCHF16
          GOTO      SIPC0000
.
SIPC1700 
          MOVE      "T ",CODE
          CALL      PATCODKY                * keyin Resident Status
          BRANCH    EXIT,SIPC1710,SIPC9000
          MOVE      ACODE,RESISTAT
          MOVE      ZERO,MATCHF17
          GOTO      SIPC0000
SIPC1710  MOVE      SP3,RESISTAT
          MOVE      ONE,MATCHF17
          GOTO      SIPC0000
SIPC1800
          MOVE      "A ",CODE
          CALL      PATCODKY                * keyin the speciality
          BRANCH    EXIT,SIPC1810,SIPC9000
          MOVE      ACODE,SPECIALT
          MOVE      ZERO,MATCHF18
          GOTO      SIPC0000
SIPC1810  MOVE      SP3,SPECIALT
          MOVE      ONE,MATCHF18
          GOTO      SIPC0000
SIPC1900
          MOVE      "DA",CODE
          CALL      PATCODKY                * keyin the District of residence
          BRANCH    EXIT,SIPC1910,SIPC9000
          MOVE      ACODE,DISOFRES
          MOVE      ZERO,MATCHF19
          GOTO      SIPC0000
SIPC1910  MOVE      SP3,DISOFRES
          MOVE      ONE,MATCHF19
          GOTO      SIPC0000
SIPC2000
          MOVE      WTCNAHUD,CODE
          CALL      PATCODKY                * keyin the the Research Group
          BRANCH    EXIT,SIPC2010,SIPC9000
          MOVE      ACODE,RESGROUP
          MOVE      ZERO,MATCHF20
          GOTO      SIPC0000
SIPC2010  MOVE      SP3,RESGROUP
          MOVE      ONE,MATCHF20
          GOTO      SIPC0000
SIPC2100
          MOVE      "CL",CODE
          CALL      PATCODKY                * keyin the Administrative cat.
          BRANCH    EXIT,SIPC2110,SIPC9000
          MOVE      ACODE,ADMINCAT
          MOVE      ZERO,MATCHF21
          GOTO      SIPC0000
SIPC2110  MOVE      SP3,ADMINCAT
          MOVE      ONE,MATCHF21
          GOTO      SIPC0000
SIPC2200
          MOVE      "WS",CODE
          CALL      PATCODKY                * keyin the "Short Stay"
          BRANCH    EXIT,SIPC2210,SIPC9000
          MOVE      ACODE,SHRTSTAY
          MOVE      ZERO,MATCHF22
          GOTO      SIPC0000
SIPC2210  MOVE      SP3,SHRTSTAY
          MOVE      ONE,MATCHF22
          GOTO      SIPC0000
SIPC2300
          MOVE      "CC",CODE
          CALL      PATCODKY                * keyin the Elective admission type
          BRANCH    EXIT,SIPC2310,SIPC9000
          MOVE      ACODE,EADMTYPE
          MOVE      ZERO,MATCHF23
          GOTO      SIPC0000
SIPC2310  MOVE      SP3,EADMTYPE
          MOVE      ONE,MATCHF23
          GOTO      SIPC0000
.
SIPC2400
          GOTO      SIPC0000
.
SIPC2500
          CALL      KYGP0000                * Keyin GP code
          MOVE      DIM8,REFERGP
          MATCH     SP8,REFERGP
          IF        @EQUAL
            MOVE      ONE,MATCHF25
          ELSE
           MOVE      ZERO,MATCHF25
          ENDIF 
          GOTO      SIPC0000
SIPC2600
          CALL      KGPP0000                * keyin GP practice
          MOVE      ZERO,MATCHF26
          MATCH     SP6,GPPRAC
          IF        @EQUAL
            MOVE      ONE,MATCHF26
          ELSE
           MOVE      ZERO,MATCHF26
          ENDIF 
          GOTO      SIPC0000
SIPC2700
          MOVE      SP8,DIM8
          CALL      KYGP0000                * Keyin GP code
          MOVE      DIM8,REGISGP
          MATCH     SP8,REGISGP
          IF        @EQUAL
            MOVE      ONE,MATCHF27
          ELSE
           MOVE      ZERO,MATCHF27
          ENDIF 
          GOTO      SIPC0000
SIPC2800
          CALL      KDEC0000                * keyin deceased indicator
          MOVE      ZERO,MATCHF28
          GOTO      SIPC0000
SIPC2900
          CALL      TOTL0000                * Total only flag
          GOTO      SIPC0000
.
SIPC9000  MOVE      ONE,EXIT
                           
SIPC9999  RETURN
.
.*****************************************************************************
.*                               UINDX000                                    *
.*               Determine which index on the waiting list file              *
.*****************************************************************************
.
UINDX000  CALL      CMTCH0000               * check for key defaluts
.
.  0 = Yes 1 = No
.  Do we have a procedure status
          IF        MATCHF10 = 0
.  Do we have a U/R
            IF        MATCHF1 = 0
              PACK      KEY20,KSTATUS1,KSTUR,FROMPROC,SP30
              MOVE      TWO,USEINDEX
              GOTO      UINDX999
            ELSE
              PACK      KEY32,KSTATUS1,FROMCONS,FROMLSTT,PRIORITY,KSTUR,FROMPROC,SP30
              MOVE      FIVE,USEINDEX       * assume index 5    
              GOTO      UINDX999
            ENDIF
          ENDIF
.
.  Do we have a U/R
.
          IF        MATCHF1 = 0
.  Do we have a Date on List
            IF        MATCHF8 = 0
              PACK      KEY27,KSTUR,FROMDONL,FROMPROC,SP30
              MOVE      FOUR,USEINDEX
            ELSE
              PACK      KEY19,KSTUR,FROMPROC,SP30
              MOVE      ONE,USEINDEX
            ENDIF
          ELSE
.  Do we have a Date on List
            IF        MATCHF8 = 0
              PACK      KEY27,FROMDONL,SP30
              MOVE      THREE,USEINDEX
            ELSE
.  Index 1 as default
              PACK      KEY19,SP30
              MOVE      ONE,USEINDEX
            ENDIF
          ENDIF
.
UINDX999  RETURN
.
.*****************************************************************************
.*                               RANGE000                                    *
.*                     Check for end of index Range                          *
.*****************************************************************************
.
RANGE000
          MOVE      ZERO,EXIT
          BRANCH    USEINDEX,RANGE100,RANGE200,RANGE300,RANGE100,RANGE200
.
RANGE100  BRANCH    MATCHF1,RANGE999
          MATCH     WMURNO,KTOUR
          IF        @LESS
            MOVE      ONE,EXIT
          ENDIF
          GOTO      RANGE999

RANGE200  BRANCH    MATCHF10,RANGE999

          IF        WMSTAT > KSTATUS2
            MOVE      ONE,EXIT
          ENDIF
          GOTO      RANGE999
RANGE300
          BRANCH    MATCHF8,RANGE999
          MATCH     WMDATE1,TODONL
          IF        @LESS
            MOVE      ONE,EXIT
          ENDIF
          GOTO      RANGE999
.
RANGE999  RETURN
.
.*****************************************************************************
.*                               CMTCH000                                    *
.*                        Check key fields for defauls & reset               *
.*****************************************************************************
.
CMTCH000  BRANCH    MATCHF1,CMTCH100        * Check U/R
          MATCH     "       0",KSTUR
          GOTO      CMTCH100 IF NOT EQUAL
          MATCH     "99999999",KTOUR
          GOTO      CMTCH100 IF NOT EQUAL
          MOVE      ONE,MATCHF1
.
CMTCH100  BRANCH    MATCHF10,CMTCH200       * Check Procedure Status
          IF        KSTATUS1 = 0 & KSTATUS2 = 9
            MOVE      ONE,MATCHF10
          ENDIF
.
CMTCH200  BRANCH    MATCHF4,CMTCH300        * Check Consultant
          MATCH     SP6,FROMCONS
          GOTO      CMTCH300 IF NOT EQUAL
          MATCH     "zzzzzz",TOCONS
          GOTO      CMTCH300 IF NOT EQUAL
          MOVE      ONE,MATCHF4
.
CMTCH300  BRANCH    MATCHF11,CMTCH400       * Check List Type
          MATCH     SP3,FROMLSTT
          GOTO      CMTCH400 IF NOT EQUAL
          MATCH     "zzz",TOLSTT
          GOTO      CMTCH400 IF NOT EQUAL
          MOVE      ONE,MATCHF11
 
CMTCH400  BRANCH    MATCHF18,CMTCH999       * Check Date on list
          MATCH     SP8,FROMDONL
          GOTO      CMTCH9999 IF NOT EQUAL
          MATCH     "zzzzzzzz",TODONL
          GOTO      CMTCH9999 IF NOT EQUAL
          MOVE      ONE,MATCHF18
.
CMTCH999  RETURN
.
.
.*****************************************************************************
.*                              POSITION                                     *
.*                 Open and position in the correct index                    *
.*****************************************************************************
.
POSITION
         BRANCH     USEINDEX,POSIT100,POSIT200,POSIT300,POSIT400,POSIT500
.
POSIT100
          OPEN      WATTR1A1,"wattr1af"
          CALL      RDSTREA1
          GOTO      POSIT999
POSIT200
          OPEN      WATTR1A2,"wattr1af"
          CALL      RDSTREA2
          GOTO      POSIT999
POSIT300
          OPEN      WATTR1A3,"wattr1af"
          CALL      RDSTREA3
          GOTO      POSIT999
POSIT400
          OPEN      WATTR1A4,"wattr1af"
          CALL      RDSTREA4
          GOTO      POSIT999
POSIT500
          OPEN      WATTR1A5,"wattr1af"
          CALL      RDSTREA5
          GOTO      POSIT999
.
POSIT999  RETURN
.
.*****************************************************************************
.*                              READNEXT                                     *
.*                 Read next record based on USEINDEX                        *
.*****************************************************************************
.
READNEXT
         BRANCH     USEINDEX,READN100,READN200,READN300,READN400,READN500
.

READN100
          CALL      RDKTREA1
          GOTO      READN999
READN200
          CALL      RDKTREA2
          GOTO      READN999
READN300
          CALL      RDKTREA3
          GOTO      READN999
READN400
          CALL      RDKTREA4
          GOTO      READN999
READN500
          CALL      RDKTREA5
          GOTO      READN999
.
READN999  RETURN
.***************************************************************************
.                   Set procedure status if removal reason/date keyed
.***************************************************************************
.
SSTAT000  MOVE      SIX,KSTATUS1
          MOVE      SIX,KSTATUS2
          MOVE      STAT6,DIM10
          DISPLAY   *P24:13,*V2LON,DIM10,*P38:13,DIM10
          MOVE      ZERO,MATCHF10
SSTAT999  RETURN

+
.*********************************************************************
.                   KYGP0000
.*********************************************************************
.
KYGP0000 
          KEYIN     *PECOL:EVERT,*EL,*DV,UNDLN8:
                    *PECOL:EVERT,*V2LON,DIM8:
                    *PECOL:EVERT,*DV,DIM8
.
          PACK      DIM8,DIM8,SP8
          MATCH     SP8,DIM8
          GOTO      KYGP9999 IF EQUAL
.
KYGP0010  MATCH     QUEST,DIM8
          GOTO      KYGP0050 IF EQUAL
.
          PACK      KEY10,DIM8,SP70
          CALL      RDPMHCP1
          IF        OVRCD = 1
            DISPLAY   *P1:24,*B,*EL,"Registered Gp is not on file.  ";
            CALL      HITENTER
            MOVE      SP8,DIM8
            GOTO      KYGP0000
          ELSE
            MOVE      ZERO,EXIT
          ENDIF
          GOTO        KYGP9999
.
KYGP0050  MOVE      ZERO,HLEF
          DISPLAY   *PECOL:EVERT,*EL
          CALL      GETSCR00
KYGP0055  CALL      PMSHCPDS
          MOVE      PMHCHCPC,DIM8
          IF        EXIT=1
            CALL      PUTSCR00
            GOTO      KYGP9999
          ENDIF
.
          PACK      KEY10,DIM8,SP70
          CALL      RDPMHCP1
          IF        OVRCD = 1
            DISPLAY   *P1:24,*B,*EL,"Registered GP is not on file.  ";
            CALL      HITENTER
            GOTO      KYGP0055
          ENDIF
.
KYGP0075  MOVE      ZERO,EXIT
          CALL      PUTSCR00
          DISPLAY   *PECOL:EVERT,*V2LON,DIM8
      
KYGP0080  
.
KYGP9999  MOVE      ZERO,EXIT
          RETURN
.
.*********************************************************************
.                           KGPP0000
.*********************************************************************
.
KGPP0000  MOVE      SP6,GPPRAC
          CALL      GDFGPP00
          BRANCH    EXIT,KGPP9999
.
          MATCH     SP6,GPPRAC
          GOTO      KGPP1000 IF EQUAL
.
KGPP0500  KEYIN     *PECOL:EVERT,*DV,GPPRAC:
                    *PECOL:EVERT,*V2LON,*RV,GPPRAC:
                    *PECOL:EVERT,*DV,GPPRAC

          GOTO      KGPP1500
         
KGPP1000  KEYIN     *PECOL:EVERT,*V2LON,GPPRAC:
                    *PECOL:EVERT,*DV,GPPRAC
.
KGPP1500  PACK      GPPRAC,GPPRAC,SP6
          MATCH     SP6,GPPRAC
          GOTO      KGPP9999 IF EQUAL
.
KGPP2000  MATCH     QUEST,GPPRAC
          GOTO      KGPP5000 IF EQUAL
.
          PACK      KEY10,GPPRAC,SP70
          PACK      KEY12,KEY10,SP1,ONE,SP70
          CALL      RDPMHCG1
          IF        OVRCD = 1
            DISPLAY   *P1:24,*B,*EL,"GP practice is not on file.  ";
            CALL      HITENTER
            GOTO      KGPP0000
          ENDIF
          BRANCH      ANYPRACF,KGPP2500
          PACK        KEY10,REFERGP,SP70
          PACK        KEY20,KEY10,GPPRAC,SP70
          CALL        RDPMHCL1
          IF          OVRCD = 1
            DISPLAY   *P1:24,*B,*EL,"The GP is not with this practice.  ";
            CALL      HITENTER
            GOTO      KGPP0000
          ENDIF
KGPP2500  DISPLAY     *PECOL:EVERT,*V2LON,GPPRAC
          GOTO        KGPP9999
.
.
KGPP5000  MOVE      ZERO,HLEF
          DISPLAY   *PECOL:EVERT,*EL
          CALL      GETSCR00
KGPP5100  MOVE      REFERGP,KEY8
          CALL      PMSHCGDS
          MOVE      PMHGPRAC,GPPRAC
          IF        EXIT=1
            CALL      PUTSCR00
            GOTO      KGPP9999
          ENDIF
.
          PACK      KEY10,GPPRAC,SP70
          PACK      KEY12,KEY10,SP1,ONE,SP70
          CALL      RDPMHCG1
          IF        OVRCD = 1
            DISPLAY   *P1:24,*B,*EL,"GP practice is not on file.  ";
            CALL      HITENTER
            GOTO      KGPP5100
          ENDIF
.
          MATCH     SP8,REFERGP                   * validate on link file?
          GOTO      KGPP6500 IF EQUAL
.
          PACK        KEY10,REFERGP,SP70
          PACK        KEY20,KEY10,GPPRAC,SP70
          CALL        RDPMHCL1
          IF          OVRCD = 1
            DISPLAY   *P1:24,*B,*EL,"The GP is not with this practice.  ";
            CALL      HITENTER
            GOTO      KGPP5100
          ENDIF
KGPP6500  CALL        PUTSCR00
          DISPLAY     *PECOL:EVERT,*V2LON,GPPRAC
.
KGPP9999  MOVE      ZERO,EXIT
          RETURN
.
.****************************************************************************
.* Called by: KGPP0000           GDFPPP00                                   *
.*                        Gets the default practice                         *
.****************************************************************************
.
GDFGPP00  MOVE      ZERO,EXIT
          PACK      REFERGP,REFERGP,SP8
          MATCH     SP8,REFERGP            * If no registered Gp then we don't
.                                             enter the practice
          IF        @EQUAL
            MOVE      ONE,ANYPRACF
            GOTO      GDFGPP99             * any practice is valid
          ELSE
            MOVE      ZERO,ANYPRACF
          ENDIF
         
.
.------ Test if the GP is in more than one practice ---------
.
          MOVE      ZERO,FORM1
          PACK      KEY20,REFERGP,SP20
          CALL      RSPMHCL1
GDFGPP02  CALL      RKPMHCL1
          IF        OVRCD = 1
            IF        FORM1 = 0
              DISPLAY   *P1:24,*B,"Registered GP is not with a practice.  ";
              CALL      HITENTER
              GOTO      GDFGPP90
            ENDIF
            MOVE      PMHLHCPR,GPPRAC
            GOTO      GDFGPP99
          ENDIF
          MATCH     PMHLHCPC,REFERGP
          IF        @EQUAL
            ADD       ONE,FORM1
            IF        FORM1 > 1
              MOVE      SP6,GPPRAC
              GOTO      GDFGPP99
            ELSE
              MOVE      PMHLHCPR,GPPRAC
            ENDIF
          ELSE
            IF      FORM1 = 0 
              DISPLAY   *P1:24,*B,"Registered GP is not with a practice.  ";
              CALL      HITENTER
              GOTO      GDFGPP90
            ELSE
              PACK      KEY20,REFERGP,SP20
              CALL      RSPMHCL1
              CALL      RKPMHCL1
              MOVE      PMHLHCPR,GPPRAC
              GOTO      GDFGPP99
            ENDIF
          ENDIF
          GOTO      GDFGPP02
.
GDFGPP90  MOVE      ONE,EXIT
GDFGPP99  RETURN
.
.   Clear the variables 
.
CLER0000
          MOVE      SP20,ADMINCAT 
          MOVE      SP20,DISOFRES
          MOVE      SP20,DOCCODE  
          MOVE      SP20,EADMTYPE 
          MOVE      SP20,ENDATE   
          MOVE      SP20,FROMCONS 
          MOVE      SP20,FROMDNAB 
          MOVE      SP20,FROMGADD 
          MOVE      SP20,FROMPATC 
          MOVE      SP20,FROMPROC 
          MOVE      ZERO,FROMTIME 
          MOVE      SP20,GPPRAC    
          MOVE      SP20,REFERGP   
          MOVE      SP20,REGISGP   
          MOVE      SP20,RESGROUP  
          MOVE      SP20,SHRTSTAY  
          MOVE      SP20,SPECIALT  
          MOVE      SP20,TOCONS   
          MOVE      SP20,TODNAB   
          MOVE      SP20,TOGADD  
          MOVE      SP20,TOPATC   
          MOVE      SP20,TOPROC    
          MOVE      ZERO,TOTIME
          MOVE      ZERO,DCEASEFL
          MOVE      ZERO,TOTLFLAG
          MOVE      ZERO,KSTSTAY
          MOVE      ZERO,KTOSTAY
          MOVE      ZERO,KSTATUS1
          MOVE      ZERO,KSTATUS2
          UNPACK    SP30,KTOUR,KSTUR
          UNPACK    SP30,FROMDONL,TODONL,FROMDSAD
          UNPACK    SP30,TODSAD,FROMLSTT,TOLSTT,REMREAS,PRIORITY,RESISTAT
          UNPACK    SP30,FROMDREM,TODREM,FROMDDOS
          UNPACK    SP30,TODREM
.
CLER9999  RETURN
+
.**************************************************************************
.*                                TALY0000                                *
.*        Tally the procedures that suit the search criteria.             *
.**************************************************************************
TALY0000  DISPLAY   *P1:24,*EL,*V2LON,*BLINKON,"Searching...";
.
          MOVE      ZERO,FORM8         * zero the number of records counter
          PACK      KEY27,FROMDONL,SP30         * start date on list
          CALL      RDSTREA3
TALY2222
          CALL      RDKTREA3           * read by date on list index
          BRANCH    OVRCD,TALY6666
.
          CALL      MTCH0000           * match record against search criteria
          BRANCH    EXIT,TALY2222      * exit=1 if match failed
          ADD       ONE,FORM8          * increment the found record counter
          GOTO      TALY2222
TALY6666
          DISPLAY   *P1:24,*EL,"Total Records : ",*V2LON,FORM8,SP5;
          CALL      HITENTER
.
TALY9999  RETURN
+
.******************************************************************************
.*                                GOPT0000                         BD=GAYLORD *
.*        GOPT is gets the item selected or a command option                  *
.*        MASK  set the replace codes                                         *
.*        CCOL  x position for keyin                                          *
.******************************************************************************
GOPT0000  DISPLAY   *PCCOL:24,*EL,UNDLN2;
          KEYIN     *PCCOL:24,*JR,*ZF,*V2LON,REPLY;
.
          ENDSET    REPLY
          APPEND    SP2,REPLY
          RESET     REPLY
          REPLACE   UPPLOW,REPLY
.
          TYPE      REPLY              * a number was entered
          GOTO      GOPT5555 IF EQUAL
.
          REPLACE   MASK,REPLY 
          TYPE      REPLY
          GOTO      GOPT9000 IF EQUAL
GOPT4444
          BEEP
          GOTO      GOPT0000
GOPT5555
          MOVE      REPLY,CCITEM
          COMPARE   CCITEM,ZERO
          GOTO      GOPT4444 IF NOT LESS
          MOVE      ZERO,EXIT          * valid number exit=0
          GOTO      GOPT9999
GOPT9000
          MOVE      REPLY,EXIT
.
GOPT9999  RETURN
+
.*****************************************************************************
.*        Validate procedure status                                          *
.*        load procedure description into DIM10                              *
.*****************************************************************************
VSTA0000  COMPARE   WMSTAT,EIGHT         * test number is within range
          GOTO      VSTA8888 IF LESS 
          MOVE      SP20,TDESC
          LOAD      TDESC,WMSTAT,STAT1,STAT2,STAT3,STAT4:
                                 STAT5,STAT6,STAT7,STAT8
          MOVE      TDESC,DIM10
          MOVE      ZERO,EXIT
          GOTO      VSTA9999
.
VSTA8888  DISPLAY   *P1:24,*B,*EL,"Invalid Procedure Status.  ";
          CALL      HITENTER
          MOVE      ONE,EXIT
.
VSTA9999  RETURN
+
.******************************************************************************
.*                                SREC0000                                    *
.*                           Show valid record                                *
.******************************************************************************
SREC0000  MOVE      SP10,DATE10
          MOVE      SP10,DATE20
          MOVE      SP10,DATE30
.
          MATCH     SP8,WMDATE1                  * date on list
          GOTO      SREC3333 IF EQUAL
          UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      DATE10,CPCDATE,SP10
          REP       " 0",DATE10
SREC3333
          MATCH     SP8,WMDATE2                  * last review date
          GOTO      SREC4444 IF EQUAL
          UNPACK    WMDATE2,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      DATE20,CPCDATE,SP10
          REP       " 0",DATE20
SREC4444
          MATCH     SP8,WMDATE3
          GOTO      SREC5555 IF EQUAL
          UNPACK    WMDATE3,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      DATE30,CPCDATE,SP10
          REP       " 0",DATE30
.
SREC5555  BRANCH    PROPTION,SREC9999       * test if printing record
.
          MOVE      WMDESC,KEY26
          LOAD      DESCSTAT,WMSTAT,STAT1,STAT2,STAT3,STAT4:
                                    STAT5,STAT6,STAT7,STAT8
          PACK      WTSPDTCK,CCC,CYY,CMM,CDD
          REP       " 0",WTSPDTCK
          CALL      WATONSUS
          IF        EXIT = ZERO
            MOVE      "SUSPENDED",DESCSTAT
          ENDIF
          MOVE      PGNAME,ANS
          PACK      KEY13,ANS,DOT,PSNAME
          DISPLAY   *P1:YPOS,*V2LON,COUNT,*HOFF,*P5:YPOS,WMURNO:
                    *P15:YPOS,KEY13,*P29:YPOS,KEY26,*P56:YPOS,DATE10:
                    *P67:YPOS,DESCSTAT;
SREC9999  RETURN
+
.******************************************************************************
.*                                XTRA0000                                    *
.*                 extract records from Procedure file                        *
.*                 check if valid ( check parameters)                         *
.*   Modified 21/06/1990 Bill Dew : This is the second biggest load of shit   *
.*   I've found in WAT12.  Didnt the ancient programmers consider planning    *
.*   their routines.  I suppose good programmers are born not taught.         *
.*                                                                            *
.*   This routine attempts to extract the records which satisfy the search    *
.*   criteria and stores each record key into a array.                        *
.*   I DO BELIEVE BILL DEW IS A TOTAL WANKER!                                 *
.******************************************************************************
XTRA0000  MOVE      ZERO,EXIT
          MOVE      ZERO,PROPTION      * signal SREC to display data to screen
.
XTRA0200  DISPLAY   *P1:3,*EF,*V2LON:
                    *ULON,*P1:4,"Cnt",*P5:4,"  U/R No",*P15:4,"Name         ":
                    *P29:4,"Description               ":
                    *P56:4,"On List   ",*P67:4,"Status      ";
.
          BRANCH    EXTRFLAG,XTRA8888
XTRA0300
          MOVE      ZERO,EXTRFLAG      * set search thru wattr1af
          DISPLAY   *P1:5,*EF,*P1:24,*BLINKON,"Searching...";
          MOVE      ZERO,COUNT         * zero the number of records counter
          MOVE      ZERO,SECCOUNT      * Zero the number of invalid security
          MOVE      ZERO,DSACNUM
          MOVE      FIVE,YPOS
          CALL      UINDX000           * select index and pack the key 
          CALL      POSITION
XTRA2222
          CALL      READNEXT           * read next record
          BRANCH    OVRCD TO XTRA8700
.
          CALL      RANGE0000          * Check index ranges
          BRANCH    EXIT,XTRA8700
.
          PACK      KEY8,WMURNO
          CALL      RDMAST1
          BRANCH    OVRCD,XTRA2222
.
          CALL      MTCH0000           * match record against search criteria
          BRANCH    EXIT,XTRA2222      * exit=1 if match failed
.
          CALL      CKWLS000           * Check Waiting List Security
          IF        EXIT = 1
            ADD       ONE,SECCOUNT
            GOTO      XTRA2222
          ENDIF
.
.------  Validate not available dates ------
.
          PACK      SESNDATE,SESNDATE,SP8
          MATCH     SP8,SESNDATE
          IF          !@EQUAL
            PACK        WTWMNAFR,WTWMNAFR,SP8
            MATCH       SP8,WTWMNAFR
            IF          !@EQUAL
              MATCH       WTWMNAFR,SESNDATE
              GOTO        XTRA3333 IF LESS
.
              MATCH       SESNDATE,WTWMNATO
              GOTO        XTRA3333 IF LESS
              
              GOTO        XTRA2222
            ENDIF
          ENDIF
.
.------- Validate Do Not Admit Before Date --------
.
XTRA3333  PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MATCH     WTWMDABD,KEY8
          GOTO      XTRA2222 IF LESS              * ignore
.
. -----   Check if Suspended & If so check 1st indicator to see if to disp. ----
.
XTRA4444  MOVE      SP8,WTSPDTCK
          CALL      WATONSUS           * Check if Currently Suspended
          IF        EXIT=0
            PACK      KEY5,ANSW,ANSN,WTSUREAS
            CALL      RDCODE1
            BRANCH    OVRCD,XTRA2222
            MOVE      ZERO,FORM1
            MOVE      TCINDC1,FORM1
            BRANCH    FORM1,XTRA2222   * Check if to display
          ENDIF
.
          ADD       ONE,COUNT          * increment the found record counter
          CALL      SREC0000           * show screen a single record 
.
          PACK      DIM19,WMURNO,WMCODE,WMCNT
.
. ---- Prepare the keys for the "select" later on
.
          STORE     DIM19,COUNT,DIMC01,DIMC02,DIMC03,DIMC04,DIMC05,DIMC06:
                                DIMC07,DIMC08,DIMC09,DIMC10,DIMC11,DIMC12:
                                DIMC13,DIMC14,DIMC15,DIMC16,DIMC17
          ADD       ONE,YPOS
          COMPARE   TEN7,COUNT         * test for a full page of records
          GOTO      XTRA2222 IF NOT EQUAL
.
. ---- The screen is full, select on of the records for detailed display,
. ---- or search further on new screen
.
XTRA5555  BRANCH    PRINTOPT,XTRA6000         * include option to print ?
          MOVE      COUNT,DSACNUM             * save number of records on screen
          DISPLAY   *P1:24,*EL,"Select Line, ":
                    "(",*V2LON,"F",*HOFF,")irst screen, ":
                    "(",*V2LON,"N",*HOFF,")ext screen, ":
                    "(",*V2LON,"E",*HOFF,")xit :";
.
          MOVE      "N1E2F3",MASK
          MOVE      "54",CCOL
          CALL      GOPT0000
          BRANCH    EXIT,XTRA6666,XTRA9000,XTRA0300
          GOTO      XTRA7000
.
. ---- Include option to print
.
XTRA6000  MOVE      COUNT,DSACNUM
          DISPLAY   *P1:24,*EL,"Select Line, ":
                    "(",*V2LON,"F",*HOFF,")irst screen, ":
                    "(",*V2LON,"N",*HOFF,")ext screen, ":
                    "(",*V2LON,"P",*HOFF,")rint, ":
                    "(",*V2LON,"E",*HOFF,")xit :";
.
          MOVE      "N1E2F3P4",MASK
          MOVE      "63",CCOL
          CALL      GOPT0000
          BRANCH    EXIT,XTRA6666,XTRA9000,XTRA0300,XTRA9500
          GOTO      XTRA7000
.
. ---- Further search, so clean the screen and read next record
.
XTRA6666  COMPARE   TEN7,COUNT
          GOTO      XTRA5555 IF NOT EQUAL
          MOVE      ZERO,COUNT
          MOVE      FIVE,YPOS
          DISPLAY   *P1:YPOS,*EF,*P1:24,*BLINKON,"Searching...";
          GOTO      XTRA2222
XTRA6688
          CALL      LLIB0000
          GOTO      XTRA5555
.
. ---- Select one of the records on the screen
. ---- load the key19 and read Procedure file
.
XTRA7000  COMPARE   ZERO,CCITEM        * test for zero selection 
          GOTO      XTRA7999 IF EQUAL
          COMPARE   CCITEM,COUNT       * test within range displayed on screen
          GOTO      XTRA7999 IF LESS
          LOAD      KEY19,CCITEM,DIMC01,DIMC02,DIMC03,DIMC04,DIMC05,DIMC06:
                                 DIMC07,DIMC08,DIMC09,DIMC10,DIMC11,DIMC12:
                                 DIMC13,DIMC14,DIMC15,DIMC16,DIMC17
          CALL      RDTREA1
          BRANCH    OVRCD,XTRA8000
          UNPACK    KEY19,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,XTRA8100
          MOVE      ZERO,EXIT
          GOTO      XTRA9999
XTRA7999
          BEEP
          GOTO      XTRA5555
XTRA8000
          DISPLAY   *P1:24,*EL,*B,"Record not on Waiting Lists file.  ";
          CALL      HITENTER
          GOTO      XTRA5555
XTRA8100
          DISPLAY   *P1:24,*EL,*B,"No Master file details for this Patient.  ";
          CALL      HITENTER
          GOTO      XTRA5555
XTRA8700
          COMPARE   ZERO,COUNT
          GOTO      XTRA5555 IF NOT EQUAL
          IF        DSACNUM <> 0
            DISPLAY   *P1:24,*EL,*B,"No More records on file. ";
            CALL      HITENTER
            GOTO      XTRA5555
          ENDIF
            
          IF        SECCOUNT = 0
            DISPLAY   *P1:24,*EL,*B,"No records found.  ";
          ELSE
            DISPLAY   *P1:24,*EL,*B,"Invalid Security.  ";
          ENDIF
          CALL      HITENTER
          GOTO      XTRA9000
XTRA8888
          CALL      DSAC0000           * display screen array contents
          GOTO      XTRA5555
XTRA9000
          MOVE      ONE,EXIT           * go back to previous screen
          GOTO      XTRA9999
.
XTRA9500 
          CALL      PRNT0000           * print number of records
          GOTO      XTRA5555
.
XTRA9999  RETURN
+
+
.******************************************************************************
.*                                MTCH0000                                    *
.*           MATCH records against the specified search fields                *
.******************************************************************************
MTCH0000  BRANCH    MATCHF2 TO MTCH0100             * Procedure Code
          MATCH     FROMPROC,WMCODE
          GOTO      MTCH9990 IF LESS
          MATCH     WMCODE,TOPROC
          GOTO      MTCH9990 IF LESS
.
MTCH0100  BRANCH    MATCHF3 TO MTCH0200             * Est Procedure Time
          COMPARE   FROMTIME,WMTIME
          GOTO      MTCH9990 IF LESS
          COMPARE   WMTIME,TOTIME
          GOTO      MTCH9990 IF LESS
.
MTCH0200  BRANCH    MATCHF4 TO MTCH0250             * consultant       
          MATCH     FROMCONS,WMDOCTOR
          GOTO      MTCH9990 IF LESS
          MATCH     WMDOCTOR,TOCONS
          GOTO      MTCH9990 IF LESS
.
MTCH0250  BRANCH    MATCHF17,MTCH0300               * Resident Status
          MATCH     RESISTAT,WMPTYPE
          GOTO      MTCH9990 IF NOT EQUAL
.
MTCH0300  BRANCH    MATCHF18,MTCH0400               * Speciality   
          MATCH     SPECIALT,WMPCAT 
          GOTO      MTCH9990 IF NOT EQUAL
.
MTCH0400  BRANCH    MATCHF25 TO MTCH0500             * Refering Gp    
          MATCH     REFERGP,WTWMRFGP
          GOTO      MTCH9990 IF NOT EQUAL
.
MTCH0500  BRANCH    MATCHF26 TO MTCH0600             * GP practice Code  n
          MATCH     GPPRAC,WTWMGPPC
          GOTO      MTCH9990 IF NOT EQUAL
.
MTCH0600  BRANCH    MATCHF27 TO MTCH0700             * Registered Gp
          MATCH     REGISGP,PMPXRHC1
          GOTO      MTCH9990 IF NOT EQUAL
.
MTCH0700  BRANCH    MATCHF5 TO MTCH0800             * Patient class
          MATCH     FROMPATC,WTWMCLSS
          GOTO      MTCH9990 IF LESS
          MATCH     WTWMCLSS,TOPATC
          GOTO      MTCH9990 IF LESS

MTCH0800  GOTO      MTCH0900                        * purchaser 
.
MTCH0900  BRANCH    MATCHF19 TO MTCH1000            * district of Residence
          MATCH     DISOFRES,PTMXDOFR
          GOTO      MTCH9990 IF NOT EQUAL
.
MTCH1000  BRANCH    MATCHF20 TO MTCH1100            * Research Group
          UNPACK    WTCNAHUD,DIM1,DIM1 
          MOVE      DIM1,FORM1
          BRANCH    FORM1,MTCH1010,MTCH1020,MTCH1030,MTCH1040
          GOTO      MTCH1100
.
MTCH1010  MATCH     RESGROUP,WMUDEF1 
          GOTO      MTCH9990 IF NOT EQUAL
          GOTO      MTCH1100
.
MTCH1020  MATCH     RESGROUP,WMUDEF2 
          GOTO      MTCH9990 IF NOT EQUAL
          GOTO      MTCH1100
.
MTCH1030  MATCH     RESGROUP,WMUDEF3 
          GOTO      MTCH9990 IF NOT EQUAL
          GOTO      MTCH1100
.
MTCH1040  MATCH     RESGROUP,WMUDEF4 
          GOTO      MTCH9990 IF NOT EQUAL
          GOTO      MTCH1100
.
MTCH1100  BRANCH    MATCHF21 TO MTCH1200            * Admin. Category          
          MATCH     ADMINCAT,WTWMACAT
          GOTO      MTCH9990 IF NOT EQUAL
.
MTCH1200  BRANCH    MATCHF22 TO MTCH1300            * Short Stay    
          MATCH     SHRTSTAY,WMNOTICE
          GOTO      MTCH9990 IF NOT EQUAL
.
MTCH1300  BRANCH    MATCHF23,MTCH1400               * Elec adm type
          MATCH     EADMTYPE,WTWMEATY
          GOTO      MTCH9990 IF NOT EQUAL
.
MTCH1400  BRANCH    MATCHF6,MTCH1500               * Guar adm date
          MATCH     FROMGADD,WTWMGADD
          GOTO      MTCH9990 IF LESS
          MATCH     WTWMGADD,TOGADD
          GOTO      MTCH9990 IF LESS
.
MTCH1500  BRANCH    MATCHF7,MTCH2200               * Do not admit before
          MATCH     FROMDNAB,WTWMDABD
          GOTO      MTCH9990 IF LESS
          MATCH     WTWMDABD,TODNAB
          GOTO      MTCH9990 IF LESS
.
MTCH2200  IF        PCEASE = 1             
            IF        DCEASEFL = 0
              GOTO      MTCH2300
            ENDIF
          ENDIF
.
MTCH2300  BRANCH    MATCHF1,MTCH2400         * U/R Number
          MATCH     KSTUR,WMURNO
          GOTO      MTCH9990 IF LESS
          MATCH     WMURNO,KTOUR
          GOTO      MTCH9990 IF LESS
.
MTCH2400  BRANCH    MATCHF10,MTCH2500         * procedure Status
          COMPARE   KSTATUS1,WMSTAT
          GOTO      MTCH9990 IF LESS
          COMPARE   WMSTAT,KSTATUS2
          GOTO      MTCH9990 IF LESS
.
MTCH2500  BRANCH    MATCHF8,MTCH2600         * Date on List
          MATCH     FROMDONL,WMDATE1
          GOTO      MTCH9990 IF LESS
          MATCH     WMDATE1,TODONL
          GOTO      MTCH9990 IF LESS
.
MTCH2600  BRANCH    MATCHF9,MTCH2700         * Scheduled admission date
          MATCH     FROMDSAD,WMDATE3
          GOTO      MTCH9990 IF LESS
          MATCH     WMDATE3,TODSAD
          GOTO      MTCH9990 IF LESS
.
MTCH2700  BRANCH    MATCHF11,MTCH2800         * List Type                  
          MATCH     FROMLSTT,WMUNIT 
          GOTO      MTCH9990 IF LESS
          MATCH     WMUNIT,TOLSTT
          GOTO      MTCH9990 IF LESS
.
MTCH2800  BRANCH    MATCHF12,MTCH2900         * Estimated stay             
          COMPARE   KSTSTAY,WMSTAY 
          GOTO      MTCH9990 IF LESS
          COMPARE   WMSTAY,KTOSTAY
          GOTO      MTCH9990 IF LESS
.
MTCH2900  BRANCH    MATCHF16,MTCH3000         * Priority                   
          MATCH     PRIORITY,WMPTY
          GOTO      MTCH9990 IF NOT EQUAL
.
MTCH3000  BRANCH    MATCHF15,MTCH3100         * Removal Reason             
          MATCH     REMREAS,WMREMOVE
          GOTO      MTCH9990 IF NOT EQUAL
.
MTCH3100  BRANCH    MATCHF14,MTCH3200         * Removal Date               
          MATCH     FROMDREM,WMDATE4 
          GOTO      MTCH9990 IF LESS
          MATCH     WMDATE4,TODREM
          GOTO      MTCH9990 IF LESS
.
MTCH3200  BRANCH    MATCHF13,MTCH3300         * Date on system             
          MATCH     FROMDDOS,WMKEYDT 
          GOTO      MTCH9990 IF LESS
          MATCH     WMKEYDT,TODDOS
          GOTO      MTCH9990 IF LESS
.
MTCH3300  BRANCH    MATCHF28,MTCH3400
          PACK      KEY8,WMURNO
          CALL      RDMAST1
          BRANCH    OVRCD,MTCH9990
.
          COMPARE   DCEASEFL,PCEASE
          GOTO      MTCH9990 IF NOT EQUAL
.
MTCH3400  MOVE      ZERO,EXIT
          GOTO      MTCH9999
.
MTCH9990  MOVE      ONE,EXIT
.
MTCH9999  RETURN
+
.******************************************************************************
.*                                DSAC0000                                    *
.*   This stops the program looping thru the wattr1af a second time because   *
.*   its better to read whats stored in memory.                               *
.******************************************************************************
DSAC0000  MOVE      ZERO,COUNT        * zero the number of records counter
          MOVE      FIVE,YPOS
DSAC2222
          ADD       ONE,COUNT         * increment record counter
          LOAD      KEY19,COUNT,DIMC01,DIMC02,DIMC03,DIMC04,DIMC05,DIMC06:
                                DIMC07,DIMC08,DIMC09,DIMC10,DIMC11,DIMC12:
                                DIMC13,DIMC14,DIMC15,DIMC16,DIMC17
          CALL      RDTREA1
          CALL      SREC0000           * show screen a single record 
          ADD       ONE,YPOS           * increment line counter
          COMPARE   COUNT,DSACNUM      * test for a full page of records
          GOTO      DSAC2222 IF NOT EQUAL
          RETURN
+
.*******************************************************************************
LLIB0000  KEYIN     *P56:24,CCITEM
          LOAD      KEY19,CCITEM,DIMC01,DIMC02,DIMC03,DIMC04,DIMC05,DIMC06:
                                 DIMC07,DIMC08,DIMC09,DIMC10,DIMC11,DIMC12:
                                 DIMC13,DIMC14,DIMC15,DIMC16,DIMC17
.
          DISPLAY   *P1:24,*EL,"Confirm Delete ",KEY19," 0=yes 1=No : ";
          KEYIN     EXIT
          BRANCH    EXIT,LLIB9999
          CALL      DETREA1
.
LLIB9999  RETURN
.
.*****************************************************************************
.*  CKWLS000  :  Check Waiting List security level                           *
.*****************************************************************************
CKWLS000  MOVE      FALSE,EXIT
.
          COMPARE   ONE,WTCNWLSC                  * using security?
          GOTO      CKWLS999 IF NOT EQUAL
.
          OPEN      WATSEAA1,"watseaaf"
          OPEN      WATSEIA2,"watseiaf"
.
          PACK      KEY4,PASSCODE
          CALL      RDWTSEA1
          COMPARE   ZERO,OVRCD                    * ALL Access
          GOTO      CKWLS999 IF EQUAL
.
          PACK      KEY13,PASSCODE,WMDOCTOR,WMUNIT
          CALL      RDWTSEI2
          BRANCH    OVRCD,CKWLS900                * no access
          GOTO      CKWLS999
.
CKWLS900  MOVE      ONE,EXIT                      * user doesn't have access
.
CKWLS999  RETURN
.
+
