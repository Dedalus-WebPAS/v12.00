.******************************************************************************
.*  Procedure Name: OTHISENQ                                                  *
.*                                                                            *
.*  Function      : Display Outpatient History                                *
.*                                                                            *
.*        V10.07.01 30/10/2015  Ebon Clements  CAR 268970                     *
.*                  Change to use OUTCLIFD index 1 instead of index 2         *
.*        V9.02.00  03/12/2002  Lina Vo                                       *
.*                  Changed outhisaf to open with site prefix.                *
.*        V5.09.B02 01/02/2001  Greg Horvat                                   *
.*                  Removed a shit lot of variables from this procedure cause *
.*                  IBAOUT66 couldnt compile                                  *
.*        V5.09.B01 05/01/2001  Greg Horvat                                   *
.*                  Replaced certain variables with KEY?? variables cause     *
.*                  OUT66 couldnt compile                                     *
.*        V5.08.03  22/05/2000  Steve Downing                                 *
.*                  Fixed Next U/R prompt                                     *
.*        V5.08.02  19/05/2000  Steve Downing                                 *
.*                  Fixed display of patient history header                   *
.*        V5.08.01  19/05/2000  Steve Downing                                 *
.*                  Fixed display of priority descriptions                    *
.*        V5.08.00  16/03/2000  Steve Armstrong     5.08 Mods                 *
.*                  Mods for changes to OUTCLIFD (effective date)             *
.*        V5.07.00  01/06/1999  Caleb Sharman  SRF 645586                     *
.*                  Recompiled for OTHISENQ                                   *
.******************************************************************************
          DEFPROC   OTHISENQ
.
          INC       OUTHISFD/INC
          INC       PATCODFD/INC
.
DIM4      DIM       4
DIM8      DIM       8
DIM14     DIM       14
DIM15     DIM       15
DIM16     DIM       16
DIM20     DIM       20
DIM79     DIM       79
DDAT1     DIM       10
DDAT2     DIM       10
SAVOUTNO  DIM       8
NUMITEM   FORM      2
ARRYITEM  DIM       28[24]
FIRSTREC  FORM      1
SCREEN    FORM      2
PREVITEM  FORM      2[99]
COUNTER   FORM      5
FORM2     FORM      2
.
CHGDESC   DIM       15 
CHGDES1   INIT      "Booked         "
CHGDES2   INIT      "Rescheduled    "
CHGDES3   INIT      "Cancelled      "
.
CODESC    INIT      "SC"
.
OTHISENQ  PACK       CFNAME,UID,FILHISA1
          OPEN       OUTHISA1,CFNAME    
.
          OPEN       PATCODE1,"patcodes"
.
          MOVE      SP10,SAVOUTNO
          CALL      DHIS0000                      * batch screen processing
.
          COMPARE   ONE,EXIT
          GOTO      OTHIS999 IF EQUAL
.
          MOVE      ZERO,EXIT
.
OTHIS999  GOTO      ENDOTHIS
.*************************************************************************
.*  DHIS0000  :  Display the Outpatient History 
.*************************************************************************
DHIS0000  DISPLAY   *P1:7,*EF;
          MOVE      ONE,SCREEN                    * init screen no.
          MOVE      ONE,NUMITEM                   * init Item no.
          MOVE      "8",CVERT
          DISPLAY   *P1:7,*V2LON,*ULON,"Event No.":
                    *P11:7,"Event Date":
                    *P22:7,"Visit Type  ":
                    *P35:7,"Clinic",SP6:
                    *P48:7,"Date Changed":
                    *P61:7,"Priority   ":
                    *P74:7," Rank "
.
          PACK      KEY28,VURNO,Z70        * position at end  
          CALL      RSOTHIS1
DHIS1000  CALL      RPOTHIS1
          BRANCH    OVRCD,DHIS5000                * no more records
.
          MATCH     VURNO,OTHIURNO
          GOTO      DHIS5000 IF NOT EQUAL 
.
. check if enough room for display
.
DHIS1100  COMPARE   TEN3,NUMITEM  
          GOTO      DHIS3000 IF LESS
.
. we need a new screen
.
DHIS1200  CALL      QNXT0000                     * ask question with Next scrn
          BRANCH    EXIT,DHIS2500:               * Page Forward selected
                         DHIS2000:               * Page Back selected
                         DHIS6500:               * Exit
                         DHIS7000                 * Next U/R
.
. previous screen was selected
.
DHIS2000  PACK      KEY28,ARRYITEM[1]
          CALL      RSOTHIS1                      * pos on 1st key on screen
.
. read back 13 records until 1st key on previous screen is reached
.
          MOVE      ZERO,COUNTER                  * init counter
DHIS2200  CALL      RKOTHIS1                      * read back 1 record
          ADD       ONE,COUNTER                   * increment counter
          COMPARE   TEN2,COUNTER    * check if read far enough
          GOTO      DHIS2200 IF NOT EQUAL
.
. we have read back far enough
.
          DISPLAY   *P1:8,*EF                     * erase screen
          MOVE      "8",CVERT                     * init screen row
          MOVE      ONE,NUMITEM                   * init screen disp counter
          SUB       ONE,SCREEN                    * update screen no.
          GOTO      DHIS3000
.
. Next screen was selected
.
DHIS2500  ADD       ONE,SCREEN                    * increment screen no.
          DISPLAY   *P1:8,*EF                     * erase screen
          MOVE      "8",CVERT                     * init screen row
          MOVE      ONE,NUMITEM                   * init screen disp counter
.
. display record
.
DHIS3000  PACK      KEY36,OTHIURNO,OTHIEDAT,Z70
          CALL      RDSBOKA3
DHIS3100  CALL      RDPBOKA3
          MATCH     OBAURNO,OTHIURNO
          GOTO      DHIS5000 IF NOT EQUAL
.
          PACK      KEY20,OBASITE,OBACLIN,OBADATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OBASITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OBACLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          BRANCH    OVRCD,DHIS3200          * Record missing
.
.         We have the clinic record. Now set up the description
.
          CALL      CLDSC000                * Get the clinic description
          GOTO      DHIS3300                * Continue on with description
.
.         Missing clinic record. Set up an appropriate description
.
DHIS3200  CLEAR     OCADESC                 * Clear the description
          APPEND    "** Unknown Clinic Id ",OCADESC
          APPEND    OBACLIN,OCADESC         * Add unknown code to description
          APPEND    " **",OCADESC           * Trailing section of description
          RESET     OCADESC
.
DHIS3300  UNPACK    OTHIEDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DDAT1
.
          MOVE      SP20,KEY8
          MATCH     SP3,OTHIPRIO
          IF        !@EQUAL
            PACK      KEY5,ANSP,ANSR,OTHIPRIO,SP10
            CALL      RDCODE1
            PACK      DIM8,TDESC
          ENDIF
.
          MATCH     OTHIEVNT,SAVOUTNO
          IF        @EQUAL
            DISPLAY   *P1:CVERT,SP8;
          ELSE
            DISPLAY   *P1:CVERT,OTHIEVNT;
          ENDIF
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
.         Get the visit type 
.
          MOVE      SP20,KEY12
          PACK      KEY5,ANSC,ANSV,OBAVISIT
          CALL      RDCODE1
          PACK      KEY12,TDESC             * default to visit type
.
          IF        OBASTAT=1
            MOVE      "O/P Book",KEY12
          ELSE
            IF        OBASTAT=4
              MOVE      "O/P Att",KEY12
            ENDIF
          ENDIF
.
.         Check if any referral letter exists
.
          OPEN      OUTRF1A1,"outrf1af"
          MOVE      OBAOUTNO,KEY8
          CALL      RDOTRFL1
          IF        OVRCD=0
            MOVE      "O/P Ref.",KEY12
          ENDIF
.
          DISPLAY   *P11:CVERT,CPCDATE:
                    *P22:CVERT,KEY12
.
          PACK      KEY12,OCADESC           * clinic desc
          DISPLAY   *P35:CVERT,KEY12:
                    *P48:CVERT,DDAT1:
                    *P61:CVERT,OTHIPRIO,SP1,DIM8:
                    *P74:CVERT,OTHIRANK
.
          MOVE      OTHIEVNT,SAVOUTNO
.
.    save each record as it is displayed (for selection purposes)
.
          PACK      ARRYITEM[NUMITEM],OTHIURNO,OTHIEVNT,OTHIEDAT:
                                      OTHIUCNT,SP20
.
          ADD       ONE,CVERT                     * update row pos
          ADD       ONE,NUMITEM                   * increment counter
          GOTO      DHIS1000                      * get next record
.
. we have no more records to display
.
DHIS5000  CALL      QLST0000
          BRANCH    EXIT,DHIS2500:                * shouldn't get here
                         DHIS2000:                * Page Back selected
                         DHIS6500:                * Exit
                         DHIS7000                 * Next U/R
.
. Exit Selected
.
DHIS6500  MOVE      ZERO,EXIT
          GOTO      DHIS9999
.
DHIS7000  MOVE      ONE,EXIT
          GOTO      DHIS9999
.
DHIS9999  RETURN              
+
. *****************************************************************************
. * CLDSC000: Get the description for the current clinic id record            *
. * RETURNS    : OCADESC = Clinic description or Doctor's description         *
. *****************************************************************************
CLDSC000  MATCH     SP6,OCADOCT         * Do we have a doctors code ?
          GOTO      CLDSC500 IF EQUAL   * No. Use the description provided.
.
          MOVE      OCADOCT,KEY6        * Set up key for doctors file
          CALL      RDDOCT1             * Read the doctors code
          BRANCH    OVRCD,CLDSC999      * No doctors record found. Blank desc.
.
.         Pack up the doctors name into a "nice" format
.
          CALL      CONCATDR            * Routine found in OUTDSCLN
          PACK      OCADESC,OCACLIN,SP1,HYPHEN,SP1,DIM40
          GOTO      CLDSC999
.
CLDSC500  PACK      KEY30,OCADESC,SP20,SP20
          PACK      OCADESC,OCACLIN,SP1,HYPHEN,SP1,KEY30
.
CLDSC999  RETURN
+
.**************************************************************************
.*  QNXT0000  :  ask line 24 question with (N)ext                         *
.**************************************************************************
QNXT0000  
. check which screen we are on. If 1st dont' display (P)revious
.
          BRANCH    SCREEN,QNXT0500
.
. screen is not 1st screen so display "(P)revious"
.
          DISPLAY   *P1:24,*EL:
                    "(",*V2LON,"F",*HOFF,")orward, ":
                    "(",*V2LON,"B",*HOFF,")ack, ":
                    "(",*V2LON,"N",*HOFF,")ext U/R, ":
                    "(",*V2LON,"E",*HOFF,")xit ? "
          MOVE      "41",CCOL
          GOTO      QNXT1000                      * get keyin
.
. screen is 1st screen so don't display "(P)revious"
. 
QNXT0500  DISPLAY   *P1:24,*EL:
                    "(",*V2LON,"F",*HOFF,")orward, ":
                    "(",*V2LON,"N",*HOFF,")ext U/R, ":
                    "(",*V2LON,"E",*HOFF,")xit ? "
          MOVE      "33",CCOL
.
. now get keyin
.
QNXT1000  KEYIN     *PCCOL:24,*DV,UNDLN1:
                    *PCCOL:24,*V2LON,DIM2:
                    *PCCOL:24,*DV,DIM2
.
          TYPE      DIM2
          GOTO      QNXT1500 IF EQUAL
.
          ENDSET    DIM2
          APPEND    SP2,DIM2
          RESET     DIM2
.
          CMOVE     DIM2,ANS
          REP       UPPLOW,ANS                  * Change to lower case
          REP       "F1B2E3N4",ANS
.
          TYPE      ANS                          * valid letter entered?
          GOTO      QNXT1500 IF NOT EQUAL        * No.
.
          MOVE      ANS,FORM1
          BRANCH    FORM1,QNXT2000,QNXT3000,QNXT4000,QNXT5000
QNXT1500  BEEP 
          GOTO      QNXT1000
.
QNXT2000  MOVE      ONE,EXIT                      * Add selected
          GOTO      QNXT9999
.
. Previous was selected so check if it was asked
.
QNXT3000  BRANCH    SCREEN,QNXT5200
          MOVE      TWO,EXIT                     * page Back selected
          GOTO      QNXT9999
.
QNXT4000  MOVE      THREE,EXIT                   * page Forward selected
          GOTO      QNXT9999
.
QNXT5000  MOVE      FOUR,EXIT                    * next U/R selected
          GOTO      QNXT9999
.
. Previous was selected but wasn't asked so error
.
QNXT5200  BEEP
          GOTO      QNXT1000
.
QNXT9999  RETURN
+
.**************************************************************************
.*  QLST0000  :  ask line 24 question without (N)ext                      *
.**************************************************************************
QLST0000  
. check which screen we are on. If 1st dont' display (P)revious
.
          BRANCH    SCREEN,QLST0500
.
. screen is not 1st screen so display "(P)revious"
.
          DISPLAY   *P1:24,*EL:
                    "(",*V2LON,"B",*HOFF,")ack, ":
                    "(",*V2LON,"N",*HOFF,")ext U/R, ":
                    "(",*V2LON,"E",*HOFF,")xit ? "
            MOVE      "30",CCOL                     * keyin position
          GOTO      QLST1000                      * get keyin
.
. screen is 1st screen so don't display "(P)revious"
. 
QLST0500  DISPLAY   *P1:24,*EL:
                    "(",*V2LON,"N",*HOFF,")ext U/R, ":
                    "(",*V2LON,"E",*HOFF,")xit ? "
          MOVE      "22",CCOL                     * keyin position
.
. now get keyin
.
QLST1000  KEYIN     *PCCOL:24,*DV,UNDLN1:
                    *PCCOL:24,*V2LON,DIM2:
                    *PCCOL:24,*DV,DIM2
.
          TYPE      DIM2
          GOTO      QLST1500 IF EQUAL
.
          ENDSET    DIM2
          APPEND    SP2,DIM2
          RESET     DIM2
.
          CMOVE     DIM2,ANS
          REP       UPPLOW,ANS              
          REP       "B1E2N3",ANS
.
          TYPE      ANS                          * valid letter entered?
          GOTO      QLST1500 IF NOT EQUAL        * No.
.
          MOVE      ANS,FORM1
          BRANCH    FORM1,QLST2000,QLST3000,QLST4000
.
QLST1500  BEEP 
          GOTO      QLST1000
.
. Previous was selected so check if it was asked
.
QLST2000  BRANCH    SCREEN,QLST5200
          MOVE      TWO,EXIT                     * page Back selected
          GOTO      QLST9999
.
QLST3000  MOVE      THREE,EXIT                      * Delete selected
          GOTO      QLST9999
.
QLST4000  MOVE      FOUR,EXIT                      * Delete selected
          GOTO      QLST9999
.
. Previous was selected but wasn't asked so error
.
QLST5200  BEEP
          GOTO      QLST1000
.
QLST9999  RETURN
.
.
          INC       OUTHISIO/INC
          INC       IBAOVRIO/INC
          INC       PATCODIO/INC
.
ENDOTHIS  ENDPROC
