.----------------------------------------------------------------------
. Program       : DTTMADDD
.
. Function      : Date Arithmetic functions. This code performs one of
.                 two functions: 1. Convert a date to seconds (integer).
.                 2. Convert an integer (seconds) to a date.
.
. Modifications :
.
. V10.04.01  14/02/2014  Ania P                      CAR 275582
.            Created Program
.
.----------------------------------------------------------------------

.INTEGER   FORM     10
.SS        FORM      2
.MM        FORM      2
.HH        FORM      2
.DTTMDATE  DATE      6 

.   Days to month vars
.------------------------
.I031      INIT      "031"
.I059      INIT      "059"
.I060      INIT      "060"
.I090      INIT      "090"
.I091      INIT      "091"
.I120      INIT      "120"
.I121      INIT      "121"
.I151      INIT      "151"
.I152      INIT      "152"
.I181      INIT      "181"
.I182      INIT      "182"
.I212      INIT      "212"
.I213      INIT      "213"
.I243      INIT      "243"
.I244      INIT      "244"
.I273      INIT      "273"
.I274      INIT      "274"
.I304      INIT      "304"
.I305      INIT      "305"
.I334      INIT      "334"
.I335      INIT      "335"
.I365      INIT      "365"
.I366      INIT      "366"

.------------------------------------------------------------------------------
.                                DATE2INT
. Convert date to integer (seconds)  - Zero time is 00:00 01 Jan 2001
. Requires CLOCK vars to be populated: CYY/CMM/CDD & CTIMEIS
. Returns INTEGER
.------------------------------------------------------------------------------
DATE2INT  CLEAR     INTEGER
          CALL      CLEARALL
.
          UNPACK    CTIMEIS,D2,D1,CTIMEIS
          MOVE      D2,F2
          ASSIGN    (INTEGER+(F2*60*60)),INTEGER           * + hrs
.
          UNPACK    CTIMEIS,D2,D1,CTIMEIS
          MOVE      D2,F2
          ASSIGN    (INTEGER+(F2*60)),INTEGER              * + mins
.
          UNPACK    CTIMEIS,D2,D1,CTIMEIS
          MOVE      D2,F2
          ASSIGN    (INTEGER+F2),INTEGER                   * + secs
.
          SUB       ONE,CDD                                * h/m/s now used up
.                                                            in the day
          ASSIGN    (INTEGER+(CDD*24*60*60)),INTEGER       * + days
.
          SUB       ONE,CMM                                * days now used up
.                                                            in month
          ASSIGN    (CYY%4),F1                             * 0 = leap year
.
.
.         Find days since December 31st
.         -----------------------------
          IF        F1=0
            LOAD      F3,CMM,I031,I060,I091,I121,I152:
                      I182,I213,I244,I274,I305,I335,I366
          ELSE
            LOAD      F3,CMM,I031,I059,I090,I120,I151:
                      I181,I212,I243,I273,I304,I334,I365
          ENDIF
.
          ASSIGN    (INTEGER+(F3*24*60*60)),INTEGER        * + mths
.
          SUB       ONE,CYY                                * mths now used up
.                                                            in year
.
.         Determine how many lots of 4 year periods are in CYY.
.         Each 4 year period has 365+365+365+366 (1461) days.
.         -----------------------------------------------------
.
          ASSIGN    (CYY%4),F1                             * remainder years
          ASSIGN    (CYY-F1)/4,CYY                         * lots of 4 years
          ASSIGN    (INTEGER+(CYY*1461*24*60*60)),INTEGER  * lots of 4 yrs->sec
          ASSIGN    (INTEGER+(F1*365*24*60*60)),INTEGER    * leftover years->sec
.
          RETURN
.
.------------------------------------------------------------------------------
.                                INT2DATE
. Convert integer (seconds) to date - Zero time is 00:00 01 Jan 2001
. Requires INTEGER to be populated
. Returns DTTMDATE & CTIMEIS
.------------------------------------------------------------------------------
INT2DATE  CALL      CLEARALL
.
          ASSIGN    INTEGER%60,TIMESS                      * remainder seconds
          ASSIGN    (INTEGER-TIMESS)/60,INTEGER            * convert to minutes
.
          ASSIGN    INTEGER%60,TIMEMM                      * remainder minutes
          ASSIGN    (INTEGER-TIMEMM)/60,INTEGER            * convert to hours
.
          ASSIGN    INTEGER%24,TIMEHH                      * remainder hours
          ASSIGN    (INTEGER-TIMEHH)/24,INTEGER            * convert to days
.
          PACK      CTIMEIS,TIMEHH,COLON,TIMEMM,COLON,TIMESS       
          REP       " 0",CTIMEIS                           
.
          ADD       INTEGER,DTTMDATE                       * Add remaining days
.
          RETURN
.
.------------------------------------------------------------------------------
.                                  CLEARALL
.------------------------------------------------------------------------------

CLEARALL  CLEAR     F1
          CLEAR     F2
          CLEAR     F3
          CLEAR     TIMESS
          CLEAR     TIMEMM
          CLEAR     TIMEHH
          MOVE      "010101",DTTMDATE
          MOVE      " ",D1
          MOVE      "  ",D2
.
          RETURN
