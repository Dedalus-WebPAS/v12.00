.**************************************************************************
.*                             DPRTENQR              Called by:           *
.*              Enquiry of all available departments                      *
.*                                                                        *
.**************************************************************************
.
DSEQ0000  MOVE      ONE,CPAGENO
          MOVE      SEVEN,CVERT
          MOVE      FIVE,CCOL
          MOVE      ZERO,FIELD
.
          DISPLAY   *P40:4,*EF
          DISPLAY   *P4:6,"Code",SP2,"Department"
          PACK      KEY5,SP5
          CALL      RDSICOD1                      * positional read
DSEQ1000  CALL      RDKICOD1                      * read next record
          BRANCH    OVRCD,DSEQ5000
.
          MATCH     SP3,PRXCODE                   * correct department ?
          GOTO      DSEQ1000 IF NOT EQUAL
.
          ADD       ONE,CVERT
          COMPARE   TWENTY2,CVERT
          IF        @EQUAL
            COMPARE   FIFTY,CCOL                  * page full ?
            IF        @EQUAL
              GOTO      DSEQ4000
            ELSE
              MOVE      FIFTY,CCOL
              MOVE      SEVEN,CVERT
              DISPLAY   *P48:6,"Code",SP2,"Department"
              GOTO      DSEQ1000
            ENDIF
          ELSE
            GOTO      DSEQ3000
          ENDIF
.
DSEQ3000  ADD       ONE,FIELD
          DISPLAY   *PCCOL:CVERT,*V2LON,PRXCATA,SP3,*HOFF,PRXDESC
          PACK      TMPKEY[FIELD],PRXCATA,PRXCODE
          GOTO      DSEQ1000
.
.  new page needed
.
DSEQ4000  BRANCH    CPAGENO,DSEQ4500
.
.  middle pages  :  Next & First options
.
DSEQ4100  MOVE      "1",CALLPOSN
          CALL      KEYB0000
          BRANCH    EXIT,DSEQ9999:              * exit
                         DSEQ9999:              * search again
                         DSEQ7000:              * next
                         DSEQ8000               * previous
          GOTO      KEYB0000
.
.  first page  :  Next option
.
DSEQ4500  MOVE      "2",CALLPOSN
          CALL      KEYB0000
          BRANCH    EXIT,DSEQ9999:              * exit
                         DSEQ9999:              * search again
                         DSEQ7000:              * next
                         DSEQ5900               * previous
          GOTO      KEYB0000
.
.  end of data 
.
DSEQ5000  BRANCH    CPAGENO,DSEQ5500
.
.  middle pages  :  First option
.
DSEQ5100  MOVE      "3",CALLPOSN
          CALL      KEYB0000
          BRANCH    EXIT,DSEQ9999:              * exit
                         DSEQ9999:              * select again
                         DSEQ5900:              * next 
                         DSEQ8000               * previous
          GOTO      KEYB0000
.
.  first page  :  No options
.
DSEQ5500  MOVE      "4",CALLPOSN
          CALL      KEYB0000
          BRANCH    EXIT,DSEQ9999:              * exit
                         DSEQ9999:              * search again
                         DSEQ5900:              * next
                         DSEQ5900               * previous
          GOTO      KEYB0000
.
DSEQ5900  BEEP
DSEQ5910  BRANCH    CALLPOSN,DSEQ4100,DSEQ4500,DSEQ5100,DSEQ5500
.
.   number entered
.
DSEQ7000  MOVE      EIGHT,CVERT
          MOVE      FIVE,CCOL
          ADD       ONE,CPAGENO
          DISPLAY   *P1:CVERT,*EF
          MOVE      ZERO,FIELD
          GOTO      DSEQ3000
.
.   previous screen selected
.
DSEQ8000  MOVE      TMPKEY[1],KEY5
          CALL      RDICOD1                     * reread first record on screen
          MOVE      ZERO,FORM2                  * initialise count
          WHILE     FORM2 <= TWENTY8
            CALL      RDPICOD1                  * read previous record
            ADD       ONE,FORM2                 * increment count
          DO
.
          MOVE      EIGHT,CVERT
          MOVE      FIVE,CCOL
          SUB       ONE,CPAGENO
          MOVE      ZERO,FIELD
          DISPLAY   *P1:CVERT,*EF
          GOTO      DSEQ3000
.
DSEQ9999  RETURN



KEYB0000  DISPLAY   *P1:24,*EF,"e(",*V2LON,ANSX,*HOFF,")it, ":
                    "(",*V2LON,ANSS,*HOFF,")elect Department";
          MOVE      TWENTY6,CCOL
          IF        CALLPOSN = 1|CALLPOSN = 2
            DISPLAY   ", (",*V2LON,ANSN,*HOFF,")ext";
            ADD       EIGHT,CCOL
          ENDIF
          IF        CALLPOSN = 1|CALLPOSN = 3
            DISPLAY   ", p(",*V2LON,ANSR,*HOFF,")evious";
            ADD       TEN2,CCOL
          ENDIF
.
          DISPLAY   " :  ";
          ADD       FIVE,CCOL
.
KEYB1000  KEYIN     *PCCOL:24,*DV,UNDLN1:
                    *PCCOL:24,*V2LON,*JR,DIM1:
                    *PCCOL:24,*DV,DIM1
          UNPACK    DIM1,ANS
          REP       UPPLOW,ANS
.
          MATCH     ANSX,ANS                     * exit selected ?
          GOTO      KEYB9100 IF EQUAL
.
          MATCH     ANSS,ANS                     * search again selected ?
          GOTO      KEYB9200 IF EQUAL
.
          MATCH     ANSN,ANS                     * next selected ?
          GOTO      KEYB9300 IF EQUAL
.
          MATCH     ANSR,ANS                     * previous selected ?
          GOTO      KEYB9400 IF EQUAL
.
KEYB1500  BEEP
          GOTO      KEYB1000
.
KEYB9100  MOVE      ONE,EXIT                     * exit
          GOTO      KEYB9999
.
KEYB9200  MOVE      TWO,EXIT                     * search again
          GOTO      KEYB9999
.
KEYB9300  MOVE      THREE,EXIT                   * next
          GOTO      KEYB9999
.
KEYB9400  MOVE      FOUR,EXIT                    * previous
          GOTO      KEYB9999
.
KEYB9999  RETURN
+













