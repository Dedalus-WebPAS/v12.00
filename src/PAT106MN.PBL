.******************************************************************************
.*  File          :  PAT106MN.PBL                                             *
.*                                                                            *
.*  Function      :  ICD10 Ed.6 Coding Maintenance Routines for PATWEB88      *
.*                                                                            *
.*  Parameters    :                                                           *
.*                                                                            *
.*  Modifications :                                                           *
.*                                                                            *
.         V11.02.01 19/07/2022  Bella Turco    TSK 0921957                    *
.                   Added NOMORE00 - Display "No more records" at end of list *
.                   Added PTCNNRTD - Displays number of rows set by parameter *
.                   value                                                     *
.         V10.03.01 22/11/2011  Mike Laming    CAR 253821                     *
.                   Added ICD10EDN variable to pass Edition no. to PATWEB88   *
.         V9.10.01  06/06/2008  Jill Habasque  CAR 166095                     *
.                   Changed all references from ODESC to PT0ODESC             *
.                   and DDESC to PT0DDESC                                     *
.         V9.09.00  20/03/2008 Mike Laming     CAR 163918                     *
.                   Created - cloned from PAT105MN.PBL and then all the "5"s  *
.                   changed to "6"s                                           *
.******************************************************************************
.
.------------------------------------------------------------------------------
. ICD10 Edition 6 Diagnosis Coding Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------------------------
ICD6DI00  RESET     PTICD001
          MOVE      "6",ICD10EDN
.                                           * make sure files are open
          OPEN      PATI16D1,"pat10d6f"     * Open ICD10 Ed.6 Diseases
          OPEN      PATK6DA1,"patkcd6f"     * ICD10 Ed.6 Key Word Diag.
          OPEN      PATK6DA2,"patkcd6f"     * ICD10 Ed.6 Key Word Diag.
.
          RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      ICD6DI40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      ICD6DI10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      ICD6DI20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      ICD6DI30 IF EQUAL
.
          MATCH     "V",UPDTTYPE    * Validate Formaction
          IF        @EQUAL
            CALL      VAL6CD00
            GOTO      ICD6DI99
          ENDIF
.
          GOTO      ICD6DI93
.
ICD6DI10  CALL      CHKDIS00  * Checks mandatory fields & checks for blank
          BRANCH    EXIT,ICD6DI99
          PACK      KEY9,PTICD001,SP70  * ADD FORMACTION
          CALL      RDPT16D1
          COMPARE   ZERO,OVRCD
          GOTO      ICD6DI92 IF EQUAL
          CALL      DISSAV00      * Moves input variables to file variables
          CALL      WRPT16D1      * Writes away the data
          PROC      PATK6DUP      * Update Keyword table
.
          CALL      ADDCAN00      * Add Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICD6DI99
.
ICD6DI20  PACK      KEY9,PTICD001,SP70  * UPDATE FORMACTION
          CALL      RDPT16D1
          BRANCH    OVRCD,ICD6DI91
          CALL      CHKDIS00      * Checks mandatory fields
          BRANCH    EXIT,ICD6DI99
          CALL      DISSAV00      * Moves input variables to file variables
          CALL      UPPT16D1      * Writes away the data
          PROC      PATK6DUP      * Update Key word Table
.
          CALL      UPDCAN00      * Update Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICD6DI99
.
ICD6DI30  PACK      KEY9,PTICD001,SP70   * DELETE FORMACTION
          CALL      RDPT16D1
          BRANCH    OVRCD,ICD6DI91
          CALL      DEPT16D1      * Deletes the data
          PROC      PATK6DUP      * Update Key word Table
.
          CALL      DELCAN00      * Delete Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICD6DI99
.
ICD6DI40  PACK      KEY9,PTICD001,SP70
          CALL      RDPT16D1
          IF        OVRCD=1
            CALL      CLRDIS00    * Clear all the file variables
          ENDIF
.
          MOVE      ONE,CANCERCD
          PACK      KEY9,PTICD001,SP70
          CALL      RDPTCAN1
          IF        OVRCD=1
            MOVE     ZERO,CANCERCD
            MOVE     ZERO,PTCCLATR
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "ICD10 Edition 6 Diagnosis Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ICD6DI99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      ICD6DI99
.
ICD6DI91  MOVE      "ICD10 Edition 6 Diagnosis Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD6DI99
.
ICD6DI92  MOVE      "ICD10 Edition 6 Diagnosis Code exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD6DI99
.
ICD6DI93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD6DI99
.
ICD6DI99  RETURN
+
.------------------------------------------------------------------------------
. ICD10 Edition 6 Operation Coding Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------------------------
ICD6OP00  RESET     PTICO001
          MOVE      "6",ICD10EDN
.
          OPEN      PATI16O1,"pat10o6f"     * Open ICD10 Ed.6 Operations
          OPEN      PATK6OA1,"patkco6f"     * ICD10 Ed.6 Key Word Proc.
          OPEN      PATK6OA2,"patkco6f"     * ICD10 Ed.6 Key Word Proc.
.
          RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      ICD6OP40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      ICD6OP10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      ICD6OP20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      ICD6OP30 IF EQUAL
.
          MATCH     "V",UPDTTYPE    * Validate formaction
          IF        @EQUAL
            CALL      VAL6CD00
            GOTO      ICD6OP99
          ENDIF
.
          GOTO      ICD6OP93
.
ICD6OP10  CALL      CHKICD00  * Checks mandatory fields & checks for blank
          BRANCH    EXIT,ICD6OP99
          PACK      KEY9,PTICO001,SP70  * ADD FORMACTION
          CALL      RDPT16O1
          COMPARE   ZERO,OVRCD
          GOTO      ICD6OP92 IF EQUAL
          CALL      ICDSAV00      * Moves input variables to file variables
          CALL      WRPT16O1      * Writes away the data
          PROC      PATK6OUP      * Update Keyword table
          MOVE      ZERO,EXIT
          GOTO      ICD6OP99
.
ICD6OP20  PACK      KEY9,PTICO001,SP70  * UPDATE FORMACTION
          CALL      RDPT16O1
          BRANCH    OVRCD,ICD6OP91
          CALL      CHKICD00      * Checks mandatory fields
          BRANCH    EXIT,ICD6OP99
          CALL      ICDSAV00      * Moves input variables to file variables
          CALL      UPPT16O1      * Writes away the data
          PROC      PATK6OUP      * Update Key word Table
          MOVE      ZERO,EXIT
          GOTO      ICD6OP99
.
ICD6OP30  PACK      KEY9,PTICO001,SP70   * DELETE FORMACTION
          CALL      RDPT16O1
          BRANCH    OVRCD,ICD6OP91
          CALL      DEPT16O1      * Deletes the data
          PROC      PATK6OUP      * Update Key word Table
          MOVE      ZERO,EXIT
          GOTO      ICD6OP99
.
ICD6OP40  PACK      KEY9,PTICO001,SP70
          CALL      RDPT16O1
          IF        OVRCD=1
            CALL      CLRICD00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "ICD10 Edition 6 Procedure Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ICD6OP99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      ICD6OP99
.
ICD6OP91  MOVE      "ICD10 Edition 6 Procedure Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD6OP99
.
ICD6OP92  MOVE      "ICD10 Edition 6 Procedure Code exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD6OP99
.
ICD6OP93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD6OP99
.
ICD6OP99  RETURN
+
.------------------------------------------------------------------------------
. Online ICD10 Edition 6 Code Validation
.------------------------------------------------------------------------------
VAL6CD00  MOVE      SP70,VALDNAME
          MATCH     SP70,VALDCODE
          GOTO      VAL6CD90 IF EQUAL
.
          MOVE      "Invalid Code Entered",VALDNAME
          BRANCH    VALDTYPE,VAL6CD10,VAL6CD20
          MOVE      "Invalid Validation Type",VALDNAME
          GOTO      VAL6CD90
.
VAL6CD10  MOVE      VALDCODE,KEY9
          CALL      RDPT16O1
          IF        OVRCD=0
            MOVE      PT0ODESC,VALDNAME
          ENDIF
          GOTO      VAL6CD90
.
VAL6CD20  MOVE      VALDCODE,KEY9
          CALL      RDPT16D1
          IF        OVRCD=0
            MOVE      PT0DDESC,VALDNAME
          ENDIF
          GOTO      VAL6CD90
.
VAL6CD90  CALL      OPENHTML
          BRANCH    EXIT,VAL6CD99
          WRITE     HTMLFILE;"<h3 align=center>Checking Database</h3>":
                             "<script type=#"text/javascript#">":
                             "{ opener.ValidateName.value=#"",VALDNAME,"#";":
                             "  opener.ValidateCode.value=#"",VALDCODE,"#";"
.
          MATCH     "Invalid",VALDNAME
          IF        @EQUAL
            WRITE     HTMLFILE;"  alert(#"Invalid Code Entered#");";
          ENDIF
.
          WRITE     HTMLFILE;"  close(); } </script>"
          CALL      CLOSHTML
.
VAL6CD99  RETURN
+
.-----------------------------------------------------------
. ICD10 Edition 6 Procedure Code: Search Table
.-----------------------------------------------------------
SPT6OA00  WRITE     HTMLFILE;"<tr align=center>":
                             "<td class=HeadingCell colspan=2>":
                             "<b>You Searched for ":
                               KEYWORDS,"</td>":
                             "</tr>":
                             "<tr>":
                             "<td class=HeadingCell width=15%><b>Code</b></td>":
                             "<td class=HeadingCell><b>Description</b></td>":
                             "</tr>"
.
          CALL      GETWRD00
.
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD       * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
....      UNPACK    PTCNGDV6,KEY4,KEY5
....      MATCH     "9999",KEY4          * is ICD10 Ed.6 in use yet?
....      GOTO      SPT6OA90 IF EQUAL
.
          STRIP     KEYWRD01
          PACK      KEY24,KEYWRD01,SP70
          CALL      RSPT6O2
.
SPT6OA10  CALL      RKPT6O2
          BRANCH    OVRCD,SPT6OA90
.
          CALL      SPT6OB00
          BRANCH    OVRCD,SPT6OA10,SPT6OA90
.
          MOVE      PTCOITM,KEY9
          CALL      RDPT16O1
          BRANCH    OVRCD,SPT6OA10
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      SPT6OA10
          ENDIF
.
          BRANCH    LNK2PRT,SPT6OA20,SPT6OA30
          GOTO      SPT6OA99
.
SPT6OA20  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:showICD02":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&ptico001=",OPCODE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             OPCODE,"</td>":
                             "<td>",PT0ODESC,"</td>":
                             "</tr>"
.
          GOTO      SPT6OA80
.
SPT6OA30  WRITE     HTMLFILE;"<tr valign=top>":
                              "<td><a href=#"#" ":
                              "OnClick=#"updateParent('",PT0ODESC:
                              "','",OPCODE,"')#">":
                              "<img src=../images/MaintenanceIcon.gif hspace=4":
                              " border=0 align=absmiddle></a>":
                              OPCODE,"</td>":
                              "<td>",PT0ODESC,"</td>":
                             "</tr>"
.
SPT6OA80  ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      SPT6OA10 IF NOT EQUAL
          GOTO      SPT6OA99
.
SPT6OA90  CALL      NOMORE00           * Display No More Records Message
.
SPT6OA99  RETURN
.------------------------------------------------------------
. Check ICD10 Edition 6 Procedure Record
.------------------------------------------------------------
SPT6OB00  MATCH     KEYWRD01,PTCOKWD
          GOTO      SPT6OB92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY24,PTCOKWD,PTCOITM
            CALL      SPT6OC00
            BRANCH    EXIT,SPT6OB91
            MOVE      SAVKEY24,KEY24
            CALL      RDPT6O2
          ENDIF
.
. Insert Other Check for Valid Record Here i.e. Suspended or In Active
.   eg    COMPARE    ONE,INACTIVE
.         GOTO       SPT6OB91 IF EQUAL
.
          GOTO      SPT6OB90
.
SPT6OB90  MOVE      ZERO,OVRCD
          GOTO      SPT6OB99
SPT6OB91  MOVE      ONE,OVRCD
          GOTO      SPT6OB99
SPT6OB92  MOVE      TWO,OVRCD
SPT6OB99  RETURN
.------------------------------------------------------------
. Check for Matching ICD10 Edition 6 Procedure Words
.------------------------------------------------------------
SPT6OC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY9,PTCOITM,SP70
.
SPT6OC10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      SPT6OC99 IF EQUAL     * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY24,PTCOITM,KEYWRDXX
          CALL      RDPT6O1               * Check for Exact Match
          COMPARE   ZERO,OVRCD            * Ok so Continue Loop
          GOTO      SPT6OC10 IF EQUAL
.
          CALL      RKPT6O1               * Not Exact so Read Next Record
          BRANCH    OVRCD,SPT6OC90        * No Match So Exit No Match
          PACK      KEY9,PTCOITM,SP70
          MATCH     SAVKEY9,KEY9        * Match Key Fields
          GOTO      SPT6OC90 IF NOT EQUAL * No Match So Exit
          STRIP     KEYWRDXX
          MATCH     PTCOKWD,KEYWRDXX         * Check Key Word
          GOTO      SPT6OC90 IF NOT EQUAL * No Match So Exit
          GOTO      SPT6OC10              * Check Next Search Word
.
SPT6OC90  MOVE      ONE,EXIT
SPT6OC99  RETURN
+
.-----------------------------------------------------------
. ICD10 Edition 6 Diagnosis Code: Search Table
.-----------------------------------------------------------
SPT6DA00  WRITE     HTMLFILE;"<tr align=center>":
                             "<td class=HeadingCell colspan=2>":
                             "<b>You Searched for ":
                               KEYWORDS,"</td></tr><tr>":
                               "<td class=HeadingCell width=15%><b>Code</b>":
                               "</td>":
                               "<td class=HeadingCell><b>Description</b></td>":
                             "</tr>"
.
          CALL      GETWRD00
.
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
....      UNPACK    PTCNGDV6,KEY4,KEY5
....      MATCH     "9999",KEY4          * is ICD10 Ed.6 in use yet?
....      GOTO      SPT6DA90 IF EQUAL
.
          STRIP     KEYWRD01
          PACK      KEY24,KEYWRD01,SP70
          CALL      RSPT6D2
.
SPT6DA10  CALL      RKPT6D2
          BRANCH    OVRCD,SPT6DA90
.
          CALL      SPT6DB00
          BRANCH    OVRCD,SPT6DA10,SPT6DA90
.
          MOVE      PTCDITM,KEY9
          CALL      RDPT16D1
          BRANCH    OVRCD,SPT6DA10
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      SPT6DA10
          ENDIF
.
          BRANCH    LNK2PRT,SPT6DA20,SPT6DA30
          GOTO      SPT6DA99
.
SPT6DA20  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:showICD03":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&pticd001=",DPCODE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             DPCODE,"</td>":
                             "<td>",PT0DDESC,"</td>":
                             "</tr>"
.
          GOTO      SPT6DA80
.
SPT6DA30  WRITE     HTMLFILE;"<tr valign=top>":
                              "<td><a href=#"#" ":
                              "OnClick=#"updateParent('",PT0DDESC:
                              "','",DPCODE,"')#">":
                              "<img src=../images/MaintenanceIcon.gif hspace=4":
                              " border=0 align=absmiddle></a>":
                              DPCODE,"</td>":
                              "<td>",PT0DDESC,"</td>":
                             "</tr>"
.
SPT6DA80  ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      SPT6DA10 IF NOT EQUAL
          GOTO      SPT6DA99
.
SPT6DA90  CALL      NOMORE00      * Display No More Records Message
.
SPT6DA99  RETURN
.------------------------------------------------------------
. Check ICD10 Edition 6 Diagnosis Record
.------------------------------------------------------------
SPT6DB00  MATCH     KEYWRD01,PTCDKWD
          GOTO      SPT6DB92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY24,PTCDKWD,PTCDITM
            CALL      SPT6DC00
            BRANCH    EXIT,SPT6DB91
            MOVE      SAVKEY24,KEY24
            CALL      RDPT6D2
          ENDIF
.
. Insert Other Check for Valid Record Here i.e. Suspended or In Active
.   eg    COMPARE    ONE,INACTIVE
.         GOTO       SPT6DB91 IF EQUAL
.
          GOTO      SPT6DB90
.
SPT6DB90  MOVE      ZERO,OVRCD
          GOTO      SPT6DB99
SPT6DB91  MOVE      ONE,OVRCD
          GOTO      SPT6DB99
SPT6DB92  MOVE      TWO,OVRCD
SPT6DB99  RETURN
.------------------------------------------------------------
. Check for Matching ICD10 Edition 6 Diagnosis Words
.------------------------------------------------------------
SPT6DC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY9,PTCDITM,SP70
.
SPT6DC10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      SPT6DC99 IF EQUAL     * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY24,PTCDITM,KEYWRDXX
          CALL      RDPT6D1               * Check for Exact Match
          COMPARE   ZERO,OVRCD            * Ok so Continue Loop
          GOTO      SPT6DC10 IF EQUAL
.
          CALL      RKPT6D1               * Not Exact so Read Next Record
          BRANCH    OVRCD,SPT6DC90        * No Match So Exit No Match
          PACK      KEY9,PTCDITM,SP70
          MATCH     SAVKEY9,KEY9        * Match Key Fields
          GOTO      SPT6DC90 IF NOT EQUAL * No Match So Exit
          STRIP     KEYWRDXX
          MATCH     PTCDKWD,KEYWRDXX         * Check Key Word
          GOTO      SPT6DC90 IF NOT EQUAL * No Match So Exit
          GOTO      SPT6DC10              * Check Next Search Word
.
SPT6DC90  MOVE      ONE,EXIT
SPT6DC99  RETURN
