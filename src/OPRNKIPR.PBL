.------------------------------------------------------------
.  File          :  OPRNKIPR.PBL
.
.  Function      :  Update Keyword Index File for Nurse Table
.
.  Parameters    :  OPNRCODE - Key to the Nurse Table
.
.  Modifications :
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Procedure to Update
.  a Key Word Index Table that can be used for searching 
.  Masterfiles. The procedure should be called from the 
.  Add, Update and Delete functions in the Masterfile 
.  Maintenance Function.
.  
.  A Key Word Index Table is Defined in the following Manner
.                              
.  PTDKxxxx    DIM       xx    * Code Field Segment 1
.  PTDKyyyy    DIM       xx    * Code Field Segment n
.  PMHKKKWD     DIM       15    * Key Word
.  PTDKSPA     DIM       10    * Spare
.
.  Index 1   PTDKxxxx, PTDKyyyy, PMHKKKWD
.  Index 2   PMHKKKWD, PTDKxxxx, PTDKyyyy
.
.  To convert this Procedure the following should be performed
.    Global Change PAT to the System ID eg :%s/PAT/PAT/g
.    Global Change CKI to the File   ID eg :%s/CKI/CKI/g
.    Global Change PTDK to the IO Call ID eg :%s/PTDK/PTDK/g
.    Modify the KEYINDEX defintion to combined Code Field Lengths eg 6
.    Global Change KEY21 to the Key for the Key Word Table eg KEY21
.    Change xxxxxxx to the Code Fields eg DCODEhh
.    Change the HCGE0000 routine to call the NURBW000 for each
.    string of key words to be indexed for the Coded Field
.    eg MOVE    DSNAME,KEYWORDS
.       CALL    NURBW000
.       MOVE    DGNAME,KEYWORDS
.       CALL    NURBW000
.       MOVE    TDESC,KEYWORDS
.       CALL    NURBW000
.------------------------------------------------------------
          DEFPROC   OPRNKIUP
.
          INC       OPRNKIFD/INC
.
KEYINDEX  DIM       6         * Code Index in Table
KEYWORDS  DIM       60        * String Containing the Key Words to be Indexed
SIXTY     FORM      "60"
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
.------------------------------------------------------------
. Generate Word Index
.------------------------------------------------------------
HCGE0000  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     OPRNKIA1,"oprnkiaf"
          TRAPCLR  IO
          BRANCH   OVRCD,HCGE9999
.
          MOVE     OPNRCODE,KEYINDEX
          CALL     NURCL000           * Remove Current Key Word Index
          BRANCH   EXIT,HCGE9999
.
          MOVE     OPNRCODE,KEY6          * Validate Record on File
          CALL     RDOPNUR1
          BRANCH   OVRCD,HCGE1900     * If Not then it Must be a Delete/Clear
.
          MOVE     OPNRCODE,KEYINDEX
          MOVE     OPNRSNAM,KEYWORDS
          CALL     NURBW000
.
          MOVE     OPNRGNAM,KEYWORDS
          CALL     NURBW000
.
HCGE1900  CLOSE    OPRNKIA1
.
HCGE9999  GOTO     OPRNKIEP
.------------------------------------------------------------
. Clear Current Key Words
.------------------------------------------------------------
NURCL000  PACK     KEY21,KEYINDEX,SP70
          CALL     RSOPNK1
          CALL     RKOPNK1
          BRANCH   OVRCD,NURCL999
          MATCH    KEYINDEX,OPNKITM
          GOTO     NURCL999 IF NOT EQUAL
.
          PACK     KEY21,OPNKITM,OPNKKWD,SP70
          CALL     DEOPNK1
          GOTO     NURCL000
.
NURCL999  RETURN
.-------------------------------------------------
. Determine Words to Index
.-------------------------------------------------
NURBW000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      NURBW000 IF NOT EOS
            GOTO      NURBW999               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWORDS,SP30      * reset form pointer
          MOVE      KEY60,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      NURBW100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Save this Word
.
NURBW100  MOVE      KEYWORDS,KEY15
          PACK      KEY15,KEY15,SP70
          REP       UPPLOW,KEY15             * Always Upper Case
          PACK      KEY21,KEYINDEX,KEY15
          UNPACK    KEY21,OPNKITM,OPNKKWD
          CALL      RDOPNK1
          IF        OVRCD=1
            CALL      WROPNK1
          ENDIF
          GOTO      NURBW190
.
.         Check for any more words
.
NURBW190  SETLPTR   KEYWORDS,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY60,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY60,KEYWORDS
          GOTO      NURBW000
.
NURBW999  RETURN
.
          INC       OPRNKIIO/INC
          INC       IBAOVRIO/INC
.
OPRNKIEP  ENDPROC                          * End of Procedure
.
