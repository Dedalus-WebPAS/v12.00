.******************************************************************************
.* System         : ALL                                                       *
.*                                                                            *
.* Include Name   : CLACDBDS.PBL                                              *
.*                                                                            *
.* Function       : Display all the codes for a categories for                *
.*                    Clinical Audits Codes file                              *
.*                                                                            *
.* Author         : Justin Coates           04/05/1992                        *
.*                                                                            *
.* Parameters     : CODE          which category to search for                *
.*                                                                            *
.* Modifications  :                                                           *
.******************************************************************************
CLACDBDS  MOVE      FIVE,CVERT              * line counter
          MOVE      ONE,CCOL                * start on LHS of screen
          PACK      KEY5,CODE,SP3           * key of blanks
          CALL      RDCACOD1                * positional read
.
          DISPLAY   *P1:3,*EF,*V2LON,*P30:4,"CATEGORY : ",*V2LON,CODE:
                    *P1:5,*ULON,"Code ",*P8:5,"          Description         "
.
. -------------------------------------
. ------- next read on item file ------
. -------------------------------------
.
BCACD100  CALL      RKCACOD1
          BRANCH    OVRCD,BCACD999          * end of file
.
          MATCH     CODE,CACDCATG
          GOTO      BCACD999 IF NOT EQUAL   * different category
.
. ----------------------------------
. ------- increment counters -------
. ----------------------------------
.
BCACD200  ADD       ONE,CVERT               * inc line counter
.
          COMPARE   "23",CVERT
          GOTO      BCACD300 IF LESS        * will fit on the page
.
          ADD       "40",CCOL               * inc column counter
          MOVE      SIX,CVERT               * reset row position
.
          COMPARE   "80",CCOL  
          GOTO      BCACD400 IF NOT LESS    * need a new page
.
          DISPLAY   *V2LON,*ULON,*P41:5," Code ":
                    *P48:5,"          Description         "
.
. -------------------------------------------
. ------- display all the information -------
. -------------------------------------------
.
BCACD300  DISPLAY   *PCCOL:CVERT,*V2LON,SP1,CACDCODE,*HOFF,SP4,CACDDESC
          GOTO      BCACD100
.
. -------------------------------
. ------- new page needed -------
. -------------------------------
.
BCACD400  DISPLAY   *P1:24,"( )ontinue, ( )irst, ( )ext ? ",*EL:
                    *V2LON,*P2:24,ANSC,*P14:24,ANSF,*P23:24,ANSN
.
          KEYIN     *P31:24,*DV,UNDLN1:
                    *P31:24,*V2LON,ANS
.
          REP       UPPLOW,ANS
.
          MATCH     ANSC,ANS
          GOTO      BCACD999 IF EQUAL       * Continue selected
.
          MATCH     ANSF,ANS
          GOTO      CLACDBDS IF EQUAL       * First selected
.
. --------------------------------------
. ------- (N)ext screen selected -------
. --------------------------------------
.
BCACD700  DISPLAY   *P41:5,*EF;
          MOVE      SIX,CVERT               * first display row 
          MOVE      ONE,CCOL                * get back to LHS of screen
          GOTO      BCACD300                * display the data
.
BCACD999  RETURN
+
