.---------------------------------------------------------------------------
.   System    :   Supply and Inventory               
.   Include   :   SINCIEKY
.   Routine   :   KSIIEA00 Keyin Catalog Code for Warehouse SIWAWAR
.   Parameter :   CKEYTYP 0 = Keyin , 1 = Accept, 2 = Either
.   Return    :   EXIT = 1 null entry, 2 EXITCHAR entered
.-------------------------------------------------------------------------------
KSIIEA00 TOPIC     "NATOCODE "
          READ      CONTROLF,SIXTY3;*67,INCOCATE,*141,INCNNATO
          MOVE      SP70,NATOCODE 
          MOVE      SP70,SIIECAT 
          COMPARE   ZERO,INCOCATE
          GOTO      KSIIEA01 IF NOT EQUAL
.
          KEYIN     *PCCOL:CVERT,*V2LON,NATOCODE
.
          ENDSET    NATOCODE 
          APPEND    SP70,NATOCODE 
          RESET     NATOCODE 
          REP       UPPLOW,NATOCODE
.
          IF        ETOEXIT=0
            MATCH     SP70,NATOCODE 
            IF        @EQUAL
              MOVE      ONE,EXIT
              GOTO      KSIIAA99
            ENDIF
          ELSE
            MATCH     ANSE,NATOCODE 
            IF        @EQUAL
              MOVE      TWO,EXIT
              GOTO      KSIIAA99
            ENDIF
          ENDIF
.
          CMATCH    EXITCHAR,NATOCODE 
          IF        @EQUAL
            MOVE      TWO,EXIT
            GOTO      KSIIEA99
          ENDIF
.
          CMATCH    QUEST,NATOCODE 
          GOTO      KSIIEA10 IF NOT EQUAL
          GOTO      KSIIEA02
.
KSIIEA01 
          MOVE      SP70,NATOCODE 
          KEYIN     *PCCOL:CVERT,*V2LON,*JR,NATOCODE
.
          ENDSET    NATOCODE 
          APPEND    SP70,NATOCODE 
          RESET     NATOCODE 
          REP       UPPLOW,NATOCODE
.
          IF        ETOEXIT=0
            MATCH     SP70,NATOCODE 
            IF        @EQUAL
              MOVE      ONE,EXIT
              GOTO      KSIIAA99
            ENDIF
          ENDIF
.
          ENDSET    NATOCODE 
          CMATCH    EXITCHAR,NATOCODE 
          IF        @EQUAL
            MOVE      TWO,EXIT
            GOTO      KSIIEA99
          ENDIF
.
          IF        ETOEXIT=1
            MATCH     ANSE,NATOCODE 
            IF        @EQUAL
              MOVE      TWO,EXIT
              GOTO      KSIIAA99
            ENDIF
          ENDIF
.
          CMATCH    QUEST,NATOCODE 
          GOTO      KSIIEA02 IF EQUAL
          RESET     NATOCODE 
.
          GOTO      KSIIEA10
.
KSIIEA02  MOVE      CCITEM,SCCITEM
          CALL      HSIIEA00               * Normal Catalog/Warehouse Lookup
          BRANCH    EXIT,KSIIEA00,KSIIEA00
.
          MOVE      SCCITEM,CCITEM
.
          MATCH     SP70,NATOCODE 
          GOTO      KSIIEA00 IF EQUAL
.
          GOTO      KSIIEA80
KSIIEA10
          IF        INCOCATE<>0
            UNPACK    NATOCODE,KEY3,SIIECAT
          ELSE
            UNPACK    NATOCODE,SIIECAT,KEY3
          ENDIF
.
          PACK      KEY12,SIWAWAR,SIIECAT,SP70
          MATCH     KEY3,SP3          * Check if too large for Catalog - Nato?
          IF        @EQUAL  
            CALL      RDSIIE2 
          ELSE
            IF        INCNNATO<>1
              MOVE      "Entry too long for a Catalog Code - ",DISPMSG
              CALL      MESSAGE1
              GOTO      KSIIEA00
            ELSE
              MOVE      ONE,OVRCD     * Simulate for Nato Lookup
            ENDIF
          ENDIF
.
. ------- OVRCD=1 if not found on catalog file or if > 7 chars keyed in and
. ------- Nato Codes are being used.
          IF        OVRCD=1
            CALL      CKNATA00               * Check Nato File
            BRANCH    EXIT,KSIIEA00
          ELSE
            IF        CKEYTYP=1
              MOVE      "Catalog Code Already Exists - ",DISPMSG
              CALL      MESSAGE1
              GOTO      KSIIEA00
            ENDIF
          ENDIF
.
. ------- Sinciaaf record must exist as in modify mode
KSIIEA80  PACK      KEY12,SIWAWAR,SIIECAT
          CALL      RDSIIE2 
          IF        OVRCD=1 & CKEYTYP<>1 & CKEYTYP<>2
            MOVE      "Catalog does not exist in Warehouse - ",DISPMSG
            CALL      MESSAGE1
            GOTO      KSIIEA00
          ENDIF
          PACK      KEY7,SIIECAT
          CALL      RDSIIA1
          MOVE      ZERO,EXIT
          GOTO      KSIIEA99
.
KSIIEA99  DISPLAY   *PCCOL:CVERT,*V2LON,SIIECAT,SP3
          MOVE      ZERO,ETOEXIT
         TOPIC    PRGID
          RETURN
.---------------------------------------------------------------------------
. Check if SIIANAT is from the Nato File.
.-------------------------------------------------------------------------------
CKNATA00  MOVE      ZERO,EXIT
.
          MOVE      NATOCODE,KEY10
.
          SQUEEZE   KEY10
          PACK      KEY17,KEY10,SP70
          RESET     KEY10
          MOVE      KEY17,KEY10
.
          IF        INCNNATO=0
            IF        CKEYTYP<>1 & CKEYTYP<>2
              MOVE      "Catalog Does Not Exist - ",DISPMSG
              CALL      MESSAGE1
              MOVE      ONE,EXIT
            ENDIF
            GOTO      CKNATA99 
          ENDIF
.
          MOVE      SP70,SINANAT
          CALL      RSSINA2
          CALL      RKSINA2
          MATCH     SINANAT,KEY10
          IF        OVRCD=1 | !@EQUAL
            IF CKEYTYP<>1 & CKEYTYP<>2
              MOVE      "Catalog or Nato Code Does Not Exist - ",DISPMSG
              CALL      MESSAGE1
              MOVE      ONE,EXIT
              GOTO      CKNATA99
            ENDIF
            IF        INCOCATE<>0
              UNPACK    NATOCODE,KEY3,SIIECAT
            ELSE
              UNPACK    NATOCODE,SIIECAT,KEY3
            ENDIF
          ELSE 
            IF        @EQUAL & (CKEYTYP=1 | CKEYTYP=2)
              MOVE      "Nato Code Already Exists - ",DISPMSG
              CALL      MESSAGE1
              MOVE      ONE,EXIT
              GOTO      CKNATA99
            ENDIF
            MOVE      SINACAT,SIIECAT
          ENDIF
.
CKNATA99  RETURN
.---------------------------------------------------------------------------
.   System    :   Supply and Inventory               
.   Include   :   SINCIEKY
.   Routine   :   HSIIEA00 Catalog by Warehouse Lookup
.   Return    :           
.-------------------------------------------------------------------------------
HSIIEA00
          MOVE      ZERO,CSCRSIZ
          IF        HTOP<3
            MOVE      "10",HTOP
            MOVE      "24",HBOT
            MOVE      "01",HLEF
            MOVE      "80",HRIG
          ENDIF
HSIIEA04
          CALL      GSIIE000
          BRANCH    EXIT,HSIIEA99
          MOVE      STDCOL00,STDCOL01
          ADD       " 7",STDCOL01
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON,"Catalog"            
.
          MOVE      STDCOL01,STDCOL02
          ADD       "9",STDCOL02
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"Description",SP20,SP20,SP8
.
          MOVE      STDCOL02,STDCOL03
          ADD       "60",STDCOL03
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
          GOTO      HSIIEA50
.
HSIIEA08
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          MOVE      SP70,SIIECAT 
          IF        CSEARCH=0
            PACK      KEY12,SIWAWAR,SCHFLD01,SP70
          ELSE
            IF        CSEARCH=1
              PACK      KEY12,SIWAWAR,SIIECAT,SP70
            ELSE
              PACK      KEY12,SIWAWAR,SP70
            ENDIF
          ENDIF
          CALL      RSSIIE2 
          CALL      RPIEA000
HSIIEA10  CALL      RKIEA000
          BRANCH    OVRCD,HSIIEA15
.
          BRANCH    CSEARCH,HSIIEA12
          CALL      HSIIEA60
          BRANCH    OVRCD,HSIIEA10
.
HSIIEA12  ADD       ONE,XITEMNO
          ADD       ONE,STDROW
          PACK      KEY12,SIIEWAR,SIIECAT,SP70
          STORE     KEY12,XITEMNO,SST1,SST2,SST3,SST4,SST5:
                                SST6,SST7,SST8,SST9,SST10:
                                SST11,SST12,SST13,SST14,SST15:
                                SST16,SST17
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,XITEMNO,DOT
          DISPLAY   *PSTDCOL01:STDROW,SIIECAT 
          PACK      KEY7,SIIECAT
          CALL      RDSIIA1
          DISPLAY   *PSTDCOL02:STDROW,SIIADES 
          COMPARE   MAXITEM,XITEMNO
          GOTO      HSIIEA10 IF NOT EQUAL
HSIIEA15
          MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
HSIIEA20
          CALL      SSIIE000
          BRANCH    EXIT,HSIIEA30,HSIIEA35,HSIIEA40,HSIIEA45,HSIIEA04:
                              HSIIEA91,HSIIEA50
          GOTO      HSIIEA92
.
. ------- Page Up
HSIIEA30
          CALL      RKIEA000
          BRANCH    OVRCD,HSIIEA85
          CALL      RPIEA000
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HSIIEA10
.
. ------- Page Down
HSIIEA35
          MOVE      SST1,KEY12
          CALL      RDSIIE2
          CALL      RPIEA000
          BRANCH    OVRCD,HSIIEA85
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
          MOVE      SP70,KEY7
HSIIEA36
          CALL      RPIEA000
          BRANCH    OVRCD,HSIIEA38
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HSIIEA36 IF NOT EQUAL
HSIIEA38
          MOVE      ZERO,XITEMNO
          GOTO      HSIIEA10
.
. ------- Home
HSIIEA40
          PACK      KEY12,SIWAWAR,SP70 
          CALL      RSSIIE2
          CALL      RKIEA000
          BRANCH    OVRCD,HSIIEA85
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HSIIEA08
.
. ------- End
HSIIEA45
          CALL      RKIEA000
          BRANCH    OVRCD,HSIIEA85
          SUSPEND
          CLIP      1,1,CRIG,24
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,XITEMNO
          PACK      KEY12,SIWAWAR,Z70                                          
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
          CALL      RSSIIE2
HSIIEA46
          CALL      RPIEA000
          BRANCH    OVRCD,HSIIEA48
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HSIIEA46 IF NOT EQUAL
HSIIEA48
          MOVE      ZERO,XITEMNO
          GOTO      HSIIEA10
.
HSIIEA50  
          MOVE      ZERO,CSEARCH
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          UNPACK    SP70,SCHFLD01,SCHFLD02,SCHFLD03,SCHFLD04,SCHFLD05
          UNPACK    SP70,SCHFLD06,SCHFLD07,SCHFLD08,SCHFLD09,SCHFLD10
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          DISPLAY   *PSTDCOL00:STDROW,*V2LON,*HON,"Sch"
.
.---- keyin catalog code ----
.
          KEYIN     *PSTDCOL01:STDROW,*V2LON,SCHFLD01;
          IF        @EOS
            DISPLAY   *PSTDCOL01:STDROW,SP10
          ENDIF
          RESET     SCHFLD01

          KEYIN     *PSTDCOL02:STDROW,*V2LON,SCHFLD02;
          RESET     SCHFLD02
.
          MATCH     SCHFLD01,SP70
          IF        @EQUAL
            MATCH     SCHFLD02,SP70
            IF        @EQUAL
              MOVE      ONE,CSEARCH
            ELSE
              CMATCH    "*",SCHFLD02
              IF        @EQUAL
                UNPACK    SCHFLD02,KEY1,KEY60
                PACK      SCHFLD02,KEY60
                REP       UPPLOW,SCHFLD02
                MOVE      TWO,CSEARCH
              ENDIF
            ENDIF
          ENDIF
.
          PACK      KEY8,SP70
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HSIIEA08
.
HSIIEA55  
          MOVE      SP70,KEY12
          MOVE      SP70,SIIECAT 
          LOAD      KEY12,CCITEM,SST1,SST2,SST3,SST4,SST5:
                                SST6,SST7,SST8,SST9,SST10:
                                SST11,SST12,SST13,SST14,SST15:
                                SST16,SST17
          UNPACK    KEY12,SIIEWAR,SIIECAT
          CALL      VSIIE000               * view catalog
          GOTO      HSIIEA20
.
HSIIEA60  MOVE      ZERO,OVRCD
          MATCH     SP70,SCHFLD01
          GOTO      HSIIEA61 IF EQUAL
          MATCH     SCHFLD01,SIIECAT 
          GOTO      HSIIEA72 IF NOT EQUAL
.
HSIIEA61  MOVE      ZERO,OVRCD
          MATCH     SP70,SCHFLD02
          GOTO      HSIIEA72 IF EQUAL
          IF        CSEARCH=2
            MOVE      SIIADES,KEY60
            REP       UPPLOW,KEY60
            SCAN      SCHFLD02,KEY60
            GOTO      HSIIEA72 IF NOT EQUAL
          ELSE
            MATCH     SCHFLD02,SIIADES 
            GOTO      HSIIEA72 IF NOT EQUAL
          ENDIF
          GOTO      HSIIEA75
.
HSIIEA72  MOVE      ONE,OVRCD
HSIIEA75  RETURN
.
HSIIEA85
          LOAD      KEY12,XITEMNO,SST1,SST2,SST3,SST4,SST5:
                                SST6,SST7,SST8,SST9,SST10:
                                SST11,SST12,SST13,SST14,SST15:
                                SST16,SST17
          UNPACK    KEY12,SIIEWAR,SIIECAT
          CALL      RDSIIE2
HSIIEA90
          BEEP
          GOTO      HSIIEA20
HSIIEA91
          MOVE      SP70,SIIEWAR 
          MOVE      SP70,KEY12
          MOVE      SP70,SIIECAT 
          GOTO      HSIIEA96
HSIIEA92
          MOVE      SP70,KEY12
          MOVE      SP70,SIIECAT 
          LOAD      KEY12,CCITEM,SST1,SST2,SST3,SST4,SST5:
                                SST6,SST7,SST8,SST9,SST10:
                                SST11,SST12,SST13,SST14,SST15:
                                SST16,SST17
          UNPACK    KEY12,SIIEWAR,SIIECAT
          CALL      RDSIIE2
HSIIEA96
          PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
HSIIEA99
          PACK      NATOCODE,SIIECAT,SP70
          RETURN
.------------------------------------------------------------
.
.----- Routines required for "V"iew Screen -----
.
.------------------------------------------------------------
. Routine   : VSIIE000
.
. Function  : Initialisation for Help Screen Display.
.              - Save Screen
.              - Display Box Details
.
. Called by : All Computer Generated Help Screens
.------------------------------------------------------------
VSIIE000  
          MOVE      HTOP,SHTOP         * restore screen vars
          MOVE      HLEF,SHLEF
          MOVE      HRIG,SHRIG
          MOVE      HBOT,SHBOT
.
.---- display sub screen ----
.
          ADD       "1",HTOP
          SUB       "1",HRIG
          ADD       "1",HLEF
          ASSIGN    HTOP+10,HBOT
          IF        HBOT>SHBOT
            ASSIGN    SHBOT-1,HBOT
          ENDIF
.
          CALL      GETSCR00
          BRANCH    EXIT,GSIIE999
.
          MOVE      HTOP,XTOP
          MOVE      HLEF,XLEF
          MOVE      HBOT,XBOT
          MOVE      HRIG,XRIG
.
          ADD       ONE,XTOP       * Cxxx Clip Box Size
          ADD       ONE,XLEF
          SUB       THREE,XBOT
          SUB       ONE,XRIG
.
          SUSPEND
.
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          ADD       ONE,XBOT
          DISPLAY   *PHRIG:XBOT,*G34,*PHLEF:XBOT,*G33;
          HLINE     *G37,XBOT,XLEF,XRIG
          ADD       ONE,XBOT
.
          DISPLAY   *PXLEF:XBOT," Hit Any key to continue";
          SUB       TWO,XBOT
.
....      CLIP      1,1,XRIG,XBOT  * Start Inside of Box Displays
.
          PACK      KEY7,SIIECAT,SP70
          CALL      RDSIIA1                 * read catalog
          IF        OVRCD=0
            MOVE      XLEF,F2
            ADD       "40",F2
            MOVE      XTOP,SECTOR
            DISPLAY   *PXLEF:SECTOR," Catalog      : ",*V2LON,SIIECAT
.
            ADD       ONE,SECTOR
            DISPLAY   *PXLEF:SECTOR," Description  : ",*V2LON,SIIADES
.
            ADD       ONE,SECTOR
            IF        SIIANON=0
              MOVE      "Stock     ",KEY10
            ELSE
              MOVE      "Non-Stock ",KEY10
            ENDIF
            DISPLAY   *PXLEF:SECTOR," Catalog Type : ",*V2LON,KEY10,*HOFF:
                      *PF2:SECTOR," Subjective     : ",*V2LON,SIIASUB
.
            ADD       ONE,SECTOR
            DISPLAY   *PXLEF:SECTOR," Issue Unit   : ",*V2LON,SIIAISS,*HOFF:
                      *PF2:SECTOR," Pref Supplier  : ",*V2LON,SIIAPSC
.
            ADD       ONE,SECTOR
            DISPLAY   *PXLEF:SECTOR," NATO Number  : ",*V2LON,SIIANAT,*HOFF:
                      *PF2:SECTOR," Stock Catagory : ",*V2LON,SIIASTI
.
            ADD       ONE,SECTOR
            DISPLAY   *PXLEF:SECTOR," Average Cost : ",*V2LON,SIIAAVC
          ENDIF
.
          FLUSH     HLEF,HTOP,HRIG,HBOT
          KEYIN     *NOEDIT,*+,*EOFF,ANS
.
.---- restore help screen ----
.
          PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
.
          MOVE      SHTOP,HTOP         * restore screen vars
          MOVE      SHLEF,HLEF
          MOVE      SHRIG,HRIG
          MOVE      SHBOT,HBOT
.
VSIIE999  RETURN
.------------------------------------------------------------
. Routine   : GSIIE000
.
. Function  : Initialisation for Help Screen Display.
.              - Save Screen
.              - Display Box Details
.
. Called by : All Computer Generated Help Screens
.------------------------------------------------------------
GSIIE000  
          MOVE      ZERO,EXIT
          MOVE      ONE,CSEARCH
.
          CALL      GETSCR00
          BRANCH    EXIT,GSIIE999
.
          MOVE      HTOP,CTOP
          MOVE      HLEF,CLEF
          MOVE      HBOT,CBOT
          MOVE      HRIG,CRIG
.
          ADD       TWO,CTOP       * Cxxx Clip Box Size
          ADD       ONE,CLEF
          SUB       THREE,CBOT
          SUB       ONE,CRIG
          MOVE      HBOT,MAXITEM
          SUB       HTOP,MAXITEM
          SUB       "4",MAXITEM
.
          SUSPEND
.
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          ADD       ONE,CBOT
          DISPLAY   *PHRIG:CBOT,*G34,*PHLEF:CBOT,*G33;
          HLINE     *G37,CBOT,CLEF,CRIG
          ADD       ONE,CBOT
.
          CLIP      1,1,CRIG,24    * Start Inside of Box Displays
.
          DISPLAY   *PCLEF:CBOT,"e",*V2LON,"X",*HOFF,"it, ex":
                                    *V2LON,"P",*HOFF,"and, ":
                                    *V2LON,"U",*HOFF,"p, ":
                                    *V2LON,"D",*HOFF,"own, ":
                                    *V2LON,"S",*HOFF,"earch, ":
                                    *V2LON,"H",*HOFF,"ome, ":
                                    *V2LON,"E",*HOFF,"nd, ":
                                    *V2LON,"R",*HOFF,"educe ":
                                    *V2LON,"V",*HOFF,"iew ";
.exit, expand, up, down, search, home, end, reduce, view, more
          SUB       TWO,CBOT
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
          SUSPEND
.
          MOVE      HTOP,MINITEM
          ADD       ONE,MINITEM
          MOVE      MINITEM,STDROW
.
          MOVE      HLEF,STDCOL00
          ADD       ONE,STDCOL00
          DISPLAY   *PSTDCOL00:STDROW,*V2LON,*ULON,"Item"
.
GSIIE999  RETURN
.
.------------------------------------------------------------
. Function    : Select Option Function for Help Screens
.
. Called by   : All Computer Generated Help Screens
.
. Exit Values : 0 = Item Selected      
.               1 = Page Up
.               2 = Page Down            
.               3 = Home
.               4 = End      
.               5 = Display Screen from start
.               6 = Exit
.               7 = Search
.               8 = View
.------------------------------------------------------------
SSIIE000  MOVE      ZERO,CCITEM
          MOVE      SP2,CCITEMD
.
          KEYIN     *PCLEF:CBOT,*NOEDIT,*+,*EOFF,ANS
          GOTO      SSIIE300 IF PAGEDOWN
          GOTO      SSIIE350 IF PAGEUP
          GOTO      SSIIE400 IF HOME
          GOTO      SSIIE450 IF END
          GOTO      SSIIE500 IF RIGHT
          GOTO      SSIIE550 IF LEFT 
          GOTO      SSIIE600 IF UP
          GOTO      SSIIE650 IF DOWN
          RESET     ANS
          GOTO      SSIIE250 IF EOS
          TYPE      ANS
          GOTO      SSIIE220 IF NOT EQUAL
          MOVE      ANS,CCITEM
.
          MOVE      XITEMNO,F3
          DIV       TEN,F3
          COMPARE   CCITEM,F3          * Less than ten items on screen 
          GOTO      SSIIE210 IF LESS   * so no need to enter second character
.
          KEYIN     *PCLEF:CBOT,*NOEDIT,*+,*EOFF,ANS
          RESET     ANS
          GOTO      SSIIE210 IF EOS
          MULT      TEN,CCITEM
          MOVE      ANS,F1
          ADD       F1,CCITEM
.
SSIIE210  
          COMPARE   ONE,CCITEM
          GOTO      SSIIE900 IF LESS       * Invalid Entry
          COMPARE   CCITEM,XITEMNO
          GOTO      SSIIE900 IF LESS       * Invalid Entry
          MOVE      ZERO,EXIT
          GOTO      SSIIE999               * Item Selected
.
SSIIE220 
          REP       UPPLOW,ANS
          MATCH     SP70,ANS           * Next Item
          GOTO      SSIIE650 IF EQUAL  
          MATCH     "X",ANS             * Exit
          GOTO      SSIIE910 IF EQUAL
          MATCH     "D",ANS             * Page Down
          GOTO      SSIIE300 IF EQUAL
          MATCH     "U",ANS             * Page Up
          GOTO      SSIIE350 IF EQUAL
          MATCH     "H",ANS             * Home
          GOTO      SSIIE400 IF EQUAL
          MATCH     "E",ANS             * End
          GOTO      SSIIE450 IF EQUAL
          MATCH     "S",ANS             * Search
          GOTO      SSIIE480 IF EQUAL
          MATCH     "P",ANS             * Left  (Expand Display)
          GOTO      SSIIE500 IF EQUAL
          MATCH     "R",ANS             * Right (Reduce Display)
          GOTO      SSIIE550 IF EQUAL
          MATCH     "V",ANS             * View Catalog
          GOTO      SSIIE700 IF EQUAL
          GOTO      SSIIE900               * Invalid Entry
.
SSIIE250  
          MOVE      DITEM,CCITEM
          COMPARE   CCITEM,XITEMNO
          GOTO      SSIIE900 IF LESS       * Invalid Entry
          MOVE      ZERO,EXIT
          GOTO      SSIIE999               * Item Selected
.         
. Page Down
.
SSIIE300  MOVE      ONE,EXIT
          GOTO      SSIIE999
.
. Page Up
.
SSIIE350  MOVE      TWO,EXIT
          GOTO      SSIIE999
.
. Home
.
SSIIE400  MOVE      THREE,EXIT
          GOTO      SSIIE999
.
. End
.
SSIIE450  MOVE      FOUR,EXIT
          GOTO      SSIIE999
.
. Search
.
SSIIE480  MOVE      SEVEN,EXIT
          GOTO      SSIIE999
.
. Expand Display
.
SSIIE500  BRANCH    CSCRSIZ,SSIIE900
          MOVE      ONE,CSCRSIZ
          SUSPEND
          CALL      PUTSCR00
          MOVE      HTOP,STOP     * Save Screen Size
          MOVE      HLEF,SLEF
          MOVE      HBOT,SBOT
          MOVE      HRIG,SRIG
          MOVE      "3",HTOP      * Full Screen Display Size
          MOVE      "1",HLEF
          MOVE      "24",HBOT
          MOVE      "80",HRIG
          MOVE      FIVE,EXIT     * Display Help Screen from Start
          GOTO      SSIIE999
.
.
. Reduce Display
.
SSIIE550  COMPARE   ZERO,CSCRSIZ
          GOTO      SSIIE900 IF EQUAL
          MOVE      ZERO,CSCRSIZ
          CALL      PUTSCR00
          MOVE      STOP,HTOP     * Restore Saved Screen Size
          MOVE      SLEF,HLEF
          MOVE      SBOT,HBOT
          MOVE      SRIG,HRIG
          MOVE      FIVE,EXIT     * Display Help Screen from Start
          GOTO      SSIIE999
.
. Up Arrow. Highlight Last Item
.
SSIIE600  DISPLAY   *PSTDCOL00:DROW,*V2LON,SP1,DITEM,DOT
          COMPARE   DITEM,ONE
          GOTO      SSIIE610 IF NOT EQUAL
          COMPARE   XITEMNO,ZERO
          GOTO      SSIIE620 IF EQUAL
          MOVE      XITEMNO,DITEM
          MOVE      CTOP,DROW
          ADD       XITEMNO,DROW  
          ADD       ONE,DITEM
.
SSIIE610  SUB       ONE,DITEM
          SUB       ONE,DROW
SSIIE620  DISPLAY   *PSTDCOL00:DROW,*HON,*V2LON,SP1,DITEM,DOT
          GOTO      SSIIE000
.
. Down Arrow. Highlight Next Item
.
SSIIE650  DISPLAY   *PSTDCOL00:DROW,*V2LON,SP1,DITEM,DOT
          COMPARE   DITEM,XITEMNO
          GOTO      SSIIE660 IF NOT EQUAL
          MOVE      ZERO,DITEM
          MOVE      CTOP,DROW
          SUB       ONE,DROW
.
SSIIE660  COMPARE   XITEMNO,ZERO
          GOTO      SSIIE670 IF EQUAL
          ADD       ONE,DITEM
          ADD       ONE,DROW
SSIIE670  DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
          GOTO      SSIIE000
.
SSIIE700  
          MOVE      DITEM,CCITEM
          COMPARE   CCITEM,XITEMNO
          GOTO      SSIIE900 IF LESS       * Invalid Entry
          MOVE      EIGHT,EXIT
          GOTO      SSIIE999               * Item Selected
.
SSIIE900  BEEP
          GOTO      SSIIE000
.
SSIIE910  MOVE      SIX,EXIT
.
SSIIE999  RETURN
.-------------------------------------------------------
. Read Forward
.-------------------------------------------------------
RKIEA000  CALL      RKSIIE2
          BRANCH    OVRCD,RKIEA999
.
          MATCH     SIWAWAR,SIIEWAR
          IF        !@EQUAL
            MOVE      ONE,OVRCD
          ELSE
            MOVE      SP70,SIIADES
            PACK      KEY7,SIIECAT,SP70
            CALL      RDSIIA1
            IF        CSEARCH=0 | CSEARCH=2
              CALL      HSIIEA60
              BRANCH    OVRCD,RKIEA000
            ENDIF
          ENDIF
.
RKIEA999  RETURN
.-------------------------------------------------------
. Read Backward
.-------------------------------------------------------
RPIEA000  CALL      RPSIIE2
          BRANCH    OVRCD,RKIEA999
.
          MATCH     SIWAWAR,SIIEWAR
          IF        !@EQUAL
            MOVE      ONE,OVRCD
          ELSE
            MOVE      SP70,SIIADES
            PACK      KEY7,SIIECAT
            CALL      RDSIIA1
            MOVE      ZERO,OVRCD
            IF        CSEARCH=0 | CSEARCH=2
              CALL      HSIIEA60
              BRANCH    OVRCD,RPIEA000
            ENDIF
          ENDIF
.
RPIEA999  RETURN
