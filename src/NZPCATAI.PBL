.******************************************************************************
.
.        NZPCATAI.PBL  -  THIS IS FOR ALREADY INVOICED
.        ------------     -----------------------------
.
.        USED BY IBAAAE02, IBAAAE20, IBABIL09, IBABIL20, IBABIL21, IBAHOS32,
.                IBAHOS94, IBAPAT32, IBAPAT34, IBAOUT20, IBAOUT66, IBAPRI12
.                PATWEB76
.
.        VERSION :
.------------------------------------------------------------------------------
.         V12.00.01 19/05/2025  J.Tan          TSK 0955096
.                   Added Alphanumeric visit number changes
.         V11.05.01 25/11/2024 Thanh T         TSK 0946085
.                   Changed DXUNR000 to show statement reference data when
.                   the Display Statement Reference (nzprivate) parameter
.                   is turned on
.         V11.04.03 29/04/2024  Thanh T        TSK 0946985
.                   Changed X5WEB000 to show statement reference data when
.                   the Display Statement Reference (nzprivate) parameter
.                   is turned on.
.         V11.04.02 20/02/2024  J.Tan          TSK 0942976
.                   Mod DWLS0000 - displaying Win/Loss for Multi contract proc.
.         V11.04.01 17/01/2024  J.Tan          TSK 0941108
.                   Mod D8SCR0000 - display procedure order
.------------------------------------------------------------------------------
.         V11.03.02 12/01/2023  J.Tan          TSK 0900145
.                   Mod Win/Loss for each procedure
.         V11.03.01 22/12/2022  J.Tan         TSK 0920195
.                   Mod unreleased cash (DXUNR000) to display payment type
.         V10.13.01 16/01/2019  J.Tan         TSK 0869117
.                   Removed checking for zero Win/Loss for Invoice NOT Extracted
.         V10.07.02 29/01/2016  J.Tan         CAR 303942
.                   Mods for Payment types
.                  V10.07.01  18/11/2015  J.Tan         CAR 303942
.                  Added new payment types
.                  V10.05.01 19/09/2014 J.Tan  CAR 305963
.                  Mods NOT to print Inc/Excl for No Contract or FFS patient
.                  V10.04.03 07/05/2014 J.Tan  CAR 300260
.                  Mods checking previous record after finding Inactive Item
.                  V10.04.02  22/04/2014 J.Tan  CAR 300260
.                  Mods checking Active/Inactive Misc.items
.                  V10.04.01  24/01/2014 J.Tan  CAR 296422
.                  Mods to set TCLAIM=2 for Misc.Item not incl.in Win/Loss Calc.
.                  V10.03.02 31/01/2013 J.Tan  CAR 280296
.                  Removed printing 'Less Payment'
.                  V10.03.01 19/07/2012 J.Tan  CAR 257771
.                  Mods SX invoice to print Theatre duration column
.                  V10.03.03 06/03/2012 J.Tan  CAR 250477
.                  Mods for Pack items
.                  V10.03.02 02/02/2012 J.Tan  CAR 235425
.                  Fixed printing Credit notes
.                  V10.03.01 09/11/2011 J.Tan  CAR 235425
.                  Mods to print procedure code and full item description
.                  V10.02.01 02/05/2011 J.Tan  CAR 239526
.                  Fixed Invoice breakdown for total amount of zero
.                  V10.01.01 08/12/2010 J.Tan  CAR 229956
.                  Fixed printing item group on multiple pages
.                  V10.00.01 27/04/2010 J.Tan  CAR 218779
.                  Fixed printing date of misc.items
.                  V9.12.04 19/02/2010 J.Tan  CAR 216119
.                  Fixed printing GST Credit Note for inv.with credited by item
.                  V9.12.03 01/10/2009 J.Tan  CAR 204960
.                  Mods to display Win/Loss after invoiced for Contract Adm.
.                  V9.12.02 24/08/2009 J.Tan  CAR 186102
.                  Mods to display Win/Loss after invoiced
.                  V9.12.01 11/06/2009 J.Tan  CAR 198713
.                  Fixed receipt amount, invoice total for invoice tail
.                  V9.11.03 29/01/2009 J.Tan  CAR 188549
.                  Fixed display/print rounding for invoice with cash/journal
.                  V9.11.02 20/01/2009 J.Tan  CAR 187842
.                  Fixed MPGEFLAG and COUNT for printing
.                  V9.11.01 14/01/2009 J.Tan  CAR 187462
.                  Mods Summary inv.not to print zero total amt of misc.items
.                  V9.10.02 07/08/2008 J.Tan  CAR 174542
.                  Fixed printing Theatre charges
.                  V9.10.01 23/07/2008 J.Tan  CAR 169840
.                  Fixed printing Credit notes procedure and GST
.                  V9.09.03 03/10/2007 J.Tan  CAR 151472
.                  Fixed printing procedure with breakdown to print no date
.                  V9.09.02 21/09/2007 J.Tan  CAR 149966
.                  Mods printing SX Summary invoice
.                  V9.09.01 05/07/07 J.Tan   CAR 140282
.                  Mods for Invoice breakdown
.                  V9.08.01 13/03/07 J.Tan   CAR 120005
.                  Mods for SX Rounding
.--------------------------------------------------------------------------
.**********************************************************************
.*                            SXSCR000                                *
.*                     Display the Initial Screen                     *
.**********************************************************************
SXSCR000  MATCH     "PATWEB76",PRGID
          GOTO      SXSCR999 IF NOT EQUAL
.
          MATCH     SP10,DIM10
          GOTO      SXSCR100 IF EQUAL
.
          WRITE     HTMLFILE;"<tr><td colspan=12 align=center><b>":
                             "Last Letter : ",REMLSTL,"  Sent : ",DIM10:
                             "</b></td></tr>"
.
SXSCR100  IF        PINVTYP=1
            IF        INVTYPE=2
              WRITE     HTMLFILE;"<tr bgcolor=#CCCCCC>":
                                 "<td colspan=12 align=right><b>":
                                 "Funder Invoice</b></td></tr>"
            ELSE
              WRITE     HTMLFILE;"<tr bgcolor=#CCCCCC>":
                               "<td colspan=12 align=right><b>":
                               "Funder Invoice</b></td></tr>"
            ENDIF
          ENDIF
.
          IF        PINVTYP=2
            IF        INVTYPE=2
              WRITE     HTMLFILE;"<tr bgcolor=#CCCCCC>":
                                 "<td colspan=12 align=right><b>":
                                 "Patient Invoice</b></td></tr>"
            ELSE
              WRITE     HTMLFILE;"<tr bgcolor=#CCCCCC>":
                                 "<td colspan=12 align=right><b>":
                                 "Patient Invoice</b></td></tr>"
            ENDIF
          ENDIF
.
.         Detailed/Summary invoice
.
          WRITE     HTMLFILE;"<tr>":
                             "<td class=HeadingCell align=center>":
                             "Date</td>":
                             "<td class=HeadingCell>Description</td>":
                             "<td class=HeadingCell>Procedure</td>":
                             "<td class=HeadingCell>Dur</td>":
                             "<td class=HeadingCell>Inc/Excl</td>":
                             "<td class=HeadingCell>Contract</td>":
                             "<td class=HeadingCell>Item</td>":
                             "<td class=HeadingCell>Qty</td>":
                            "<td align=right class=HeadingCell>":
                             "Unit $</td>":
                            "<td align=right class=HeadingCell>":
                             "GST Exc.</td>":
                            "<td align=right class=HeadingCell>":
                             "GST</td>":
                            "<td align=right class=HeadingCell>":
                             "Total</td>":
                            "</tr>"
          GOTO      SXSCR999
.
SXSCR999  RETURN
+
.**********************************************************************
.         SX Accommodation line
.**********************************************************************
X1WEB000  PACK      KEY8,TFCENT,TFYEAR,TFMONTH,TFDAY
          REP       " 0",KEY8
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          MOVE      TFMONTH,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
          WRITE     HTMLFILE;"<tr>":
                             "<td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          PACK      KEY8,TTCENT,TTYEAR,TTMONTH,TTDAY
          REP       " 0",KEY8
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          MOVE      TTMONTH,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
.
          MOVE      TPATAMTT,FORM12P2
          DIV       TPATAMTI,FORM12P2
          MOVE      FORM12P2,FORM41         * move it to form 4.1
.
          MOVE      SP70,KEY6
          MOVE      FORM41,KEY6
          UNPACK    KEY6,KEY4,KEY2
          MATCH     ".5",KEY2               * check if the last 2 chars
          GOTO      X1WEB300 IF NOT EQUAL
.
          WRITE     HTMLFILE;" to ",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                             "<td>",FORM41," Days ";
          GOTO      X1WEB500
.
X1WEB300  WRITE     HTMLFILE;" to ",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                             "<td>",TNODAYS," Days ";
.
X1WEB500  WRITE     HTMLFILE;TDDESC,"</td>";
.
          MOVE      SP70,NZSPPROC
          PACK      KEY11,PINVADM,PTDTSUNQ,SP70
          CALL      RDNZSPR1
.
          MOVE      "Incl   ",KEY7
          IF        PTDTCCU=1
            MOVE      "Patient",KEY7
          ENDIF
          IF        PTDTCCU=2
            MOVE      "Extra  ",KEY7
          ENDIF
.
          MOVE      PTDTGSTM,GSTAMNT
          MOVE      TPATAMTT,TOTAMNT
          ADD       GSTAMNT,TOTAMNT
          MOVE      GSTAMNT,GSTAMNT6
          ADD       GSTAMNT,TOTGST
.
.         If No Contract (FFS), field for Inc/Excl will be blank
.
          MATCH     SP70,PTDTCNTR
          IF        @EQUAL
            MOVE      SP70,KEY7
          ENDIF
.
          WRITE     HTMLFILE;"<td>&nbsp;",NZSPPROC,"</td>":
                             "<td>&nbsp;</td>":
                             "<td>",KEY7,"</td>":
                             "<td>&nbsp;",PTDTCNTR,"</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>";
.
          WRITE     HTMLFILE;"<td align=right>",TPATAMTI,"</td>":
                             "<td align=right>",TPATAMTT,"</td>":
                             "<td align=right>",GSTAMNT6,"</td>":
                             "<td align=right>",TOTAMNT,"</td></tr>"
X1WEB999  RETURN
+
.**********************************************************************
.         SX Cash line
.**********************************************************************
X5WEB000  PACK      KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KEY8
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
.
          WRITE     HTMLFILE;"<tr><td>",DIM8," - ":
                              CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>";
.
X5WEB020  MOVE      TTYPEDEB,KEY1
          MOVE      TDEPTYP,KEY3
          UNPACK    TDCODE,KEY6             * Health Fund or Insurance code
          IF        PINVSYS=1|PINVSYS=2
            PACK      KEY12,TRECEIPT,SP70
            CALL      RDRCBNK1
            IF        OVRCD<>1
              MOVE      ANSP,KEY1
              MOVE      RCBNTTYP,F1
              LOAD      KEY1,F1,ANSH,ANSI,ANSG,ANSH
              UNPACK    RCBNRCOD,KEY6           * Health Fund or Insurance code
            ENDIF
          ENDIF

          CALL      PAYD0000                * Get payment desc.
.
          MOVE      SP70,DIM14
          PACK      RCDTRECN,TRECEIPT,SP70
          PACK      KEY15,RCDTRECN,SP70
          CALL      RSRCBDT1
          CALL      RKRCBDT1
          BRANCH    OVRCD,X5WEB200
.
          MATCH     RCDTRECN,RCBDRECN
          GOTO      X5WEB200 IF NOT EQUAL
.
          MOVE      ZERO,F1
          MOVE      RCBDPTYP,F1
          MOVE      F1,F2
          CALL      CSHDS000                * Get payment type desc.
          IF        F2=2
            MOVE      RCBDDRAW,RCBDPAYD     * Cheque - use the drawer as payee
          ENDIF
.
X5WEB100  MATCH     "1",RCCNSTRF
          GOTO      X5WEB150 IF NOT EQUAL
.
          MATCH     "4",RCBDPTYP
          GOTO      X5WEB150 IF NOT EQUAL
.
          STRIP     RCBDPAYD
          STRIP     RCBDSTRF
.
          WRITE     HTMLFILE;"<td><b>Receipt ## </b>",TRECEIPT:
                             "<b> Payer: </b>",RCBDPAYD:
                             "<b> Stmt Ref: </b>",RCBDSTRF:
                             "</td>";
          GOTO      X5WEB400
.
X5WEB150  WRITE     HTMLFILE;"<td><b>Receipt ## </b>",TRECEIPT:
                             "<b> Payer: </b>",RCBDPAYD,"</td>";
          GOTO      X5WEB400
.
X5WEB200  WRITE     HTMLFILE;"<td>",KEY25," RECEIPT ## ",TRECEIPT,"</td>";
.
X5WEB400  WRITE     HTMLFILE;"<td>",DIM14,"</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td align=right>",FORM7P2,"</td></tr>"
X5WEB999  RETURN
+
.**********************************************************************
.         Display Cash Pending
.**********************************************************************
X5CSH000  CALL      PAYE0000                  * Get payee details
          MOVE      RCBDPAYD,KEY50
.
          WRITE     HTMLFILE;"<tr><td>Payment - ":
                             CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR:
                             "</td>":
                             "<td><b>Receipt ##</b>",RCDTRECN," <b>Payer: </b>":
                             KEY50,"</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td align=right><b>":
                             RCDTAMNT,"</b></td></tr>"
          MOVE      ZERO,CASHFLAG           * A Cash Record has been found
X5CSH999  RETURN
+
.**********************************************************************
.         Display Cash total
.**********************************************************************
XCSHT000  WRITE     HTMLFILE;"<tr bgcolor=#CCCCCC><td>&nbsp;</td>":
                             "<td><b>Payment Total</b></td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td align=right><b>",PAYMTTOT,"</td></tr>"
XCSHT999  RETURN
+
.**********************************************************************
.         SX Journal line
.**********************************************************************
X6WEB000  PACK      KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KEY8
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
.
          WRITE     HTMLFILE;"<tr><td>",DIM8," - ":
                              CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                              "<td>",DIM25,TRECEIPT,"</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp",DIM6,"</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>";
.
          IF        TRECTYPE=6
            IF        PTDTGSTM<>0
              MOVE      PTDTGSTM,FORM72
              ADD       PTDTGSTM,TPATAMTT
              WRITE     HTMLFILE;"<td>&nbsp;</td>":
                                 "<td align=right>",TPATAMTT,"</td>";
            ELSE
              WRITE     HTMLFILE;"<td align=right>",TPATAMTT,"</td>":
                                 "<td>&nbsp;</td>";
            ENDIF
          ELSE
            WRITE     HTMLFILE;"<td>&nbsp",TRECEIPT,"</td>":
                               "<td>&nbsp",DIM6,"</td>";
          ENDIF
          WRITE     HTMLFILE;"<td align=right>",TPATAMTT,"</td></tr>"
          RETURN
.
X6WEB999  RETURN
+
.**********************************************************************
.*                            D8SCR000                                *
.*                       Routine to display Procedures                *
.**********************************************************************
D8SCR000  COMPARE   THREE,PINVSYS
          GOTO      D8SCR999 IF NOT EQUAL
.
          MOVE      "8",DTRECTYP
          MOVE      PINVADM,DPINVADM
          PACK      KEY23 WITH PINVNO,DTRECTYP,PINVADM,SP30
          CALL      RDSDTRN5
D8SCR100  CALL      RDKDTRN5
          BRANCH    OVRCD,D8SCR999
.
          MATCH     PINVNO,TREF
          GOTO      D8SCR999 IF NOT EQUAL
.
          COMPARE   EIGHT,TRECTYPE          * Procedure record
          GOTO      D8SCR999 IF NOT EQUAL
.
          MOVE      TFDAY,CDAY
          MOVE      TFMONTH,CMON
          MOVE      TFCENT,CCENT
          MOVE      TFYEAR,CYEAR
          CALL      PACDATE
          REP       " 0",CPCDATE
.
          PACK      KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KEY8
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
.
          MOVE      SP70,NZSPPROC
          PACK      KEY11,PINVADM,PTDTSUNQ,SP70
          CALL      RDNZSPR1
.
          MATCH     "1",PTDTCONT
          GOTO      D8SCR700 IF NOT EQUAL
.
          MOVE      TPATAMTT,FORM12P2
          ADD       PTDTLSPT,FORM12P2
          ADD       PTDTLSRB,FORM12P2
          IF        PTDTDTYP=1
            MOVE      TPATAMTP,FORM12P2      * Patient Inv.from Funder contr.
            ADD       TPATAMTP,GROSSTOT
          ELSE
            IF        PTDTDTYP=2
              MOVE      TREBATE,FORM12P2     * Funder Inv.for Funder contr.amt
              ADD       TREBATE,GROSSTOT
             ENDIF
          ENDIF
          GOTO      D8SCR800
.
D8SCR700  ADD       TPATAMTT,GROSSTOT
          MOVE      ZERO,FORM12P2
          MOVE      PTDTLSRB,FORM12P2
          ADD       PTDTLSPT,FORM12P2        * fixed fee
.
.         Check if this is a fixed fee
.
          COMPARE   ZERO,FORM12P2
          GOTO      D8SCR710 IF NOT EQUAL
          COMPARE   ZERO,PTDTADPT
          GOTO      D8SCR800 IF EQUAL      * no Adjustment fee
.         GOTO      D8SCR900
.
D8SCR710
          MOVE      ONE,F2                 * Set to check amt only
          MOVE      FORM12P2,FORM12D2      * save fix fee
          CALL      DIBL0000               * Check for Invoice breakdown
          IF        F1=0
            MOVE      FORM12D2,TPATAMTT
            GOTO      D8SCR800               * No Invoice breakdown
          ENDIF
.
          MOVE      ZERO,F2
          CALL      DIBL0000               * Display Invoice breakdown
          GOTO      D8SCR900
.
D8SCR800  WRITE     HTMLFILE;"<tr>":
                             "<td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                             "<td>&nbsp;",TDDESC,"</td>":
                             "<td>&nbsp;",NZSPPROC,"</td>";
.
           IF        NZSPPIND=1 & NZSPPRIM<>2
             WRITE     HTMLFILE;"<td>&nbsp;",NZSPTDUR,"</td>";
           ELSE
             WRITE     HTMLFILE;"<td>&nbsp;</td>";
           ENDIF
.
           WRITE    HTMLFILE;"<td>&nbsp;</td>":
                             "<td>&nbsp;",PTDTCNTR,"</td>":
                             "<td>&nbsp;",TITEMNO,"</td>":
                             "<td>&nbsp;</td>":
                             "<td align=right>&nbsp;</td>":
                             "<td align=right>",TPATAMTT,"</td>":
                             "<td align=right>&nbsp;</td>":
                             "<td align=right>",TPATAMTT,"</td></tr>"
.
D8SCR900  MATCH     "1",PTDTCONT
          GOTO      D8SCR100 IF EQUAL
.
          COMPARE   ZERO,PTDTADPT
          GOTO      D8SCR100 IF EQUAL     * no adjustment fee
.
          MOVE      "Fixed Fee Adjustment        ",TDDESC
          WRITE     HTMLFILE;"<tr>":
                             "<td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                             "<td>&nbsp;",TDDESC,"</td>":
                             "<td>&nbsp;",NZSPPROC,"</td>";
.
           IF        NZSPPIND=1 & NZSPPRIM<>2
             WRITE     HTMLFILE;"<td>&nbsp;",NZSPTDUR,"</td>";
           ELSE
             WRITE     HTMLFILE;"<td>&nbsp;</td>";
           ENDIF
.
           WRITE    HTMLFILE;"<td>&nbsp;</td>":
                             "<td>&nbsp;",PTDTCNTR,"</td>":
                             "<td>&nbsp;",TITEMNO,"</td>":
                             "<td>&nbsp;</td>":
                             "<td align=right>&nbsp;</td>":
                             "<td align=right>",PTDTADPT,"</td>":
                             "<td align=right>&nbsp;</td>":
                             "<td align=right>",PTDTADPT,"</td></tr>"
          GOTO      D8SCR100
.
D8SCR999  RETURN
+
.**********************************************************************
.*                            D2SCR000                                *
.*                       Routine to display Theatre fees              *
.**********************************************************************
D2SCR000  IF        PINVSYS = 3
            PACK      KEY23 WITH PINVADM,PINVNO,TWO,SP30
          ELSE
            PACK      KEY31 WITH PINVADM,PINVNO,ONE,SP30
            PACK      KEY22 WITH PINVADM,PINVNO,SP30
          ENDIF
          IF        CRDNFLAG=1
            MOVE      "2",KEY1
          ENDIF
.
          CALL      RDSDTRAN
D2SCR100  CALL      RDKDTRAN
          BRANCH    OVRCD OF D2SCR999
          MATCH     PINVNO,TREF
          GOTO      D2SCR999 IF NOT EQUAL
.
          IF        TRECTYPE<>2
            IF        TRECTYPE=3 & (PINVSYS=1 | PINVSYS=2)
.
. ----- Go thru all misc items to get theatre items for A&E & outpatients -----
.
              GOTO      D2SCR100
            ELSE
              GOTO      D2SCR999
            ENDIF
          ENDIF
.
          MOVE      TFDAY,CDAY
          MOVE      TFMONTH,CMON
          MOVE      TFCENT,CCENT
          MOVE      TFYEAR,CYEAR
          CALL      PACDATE
          REP       " 0",CPCDATE
.
          MOVE      SP70,NZSPPROC
          PACK      KEY11,PINVADM,PTDTSUNQ,SP70
          CALL      RDNZSPR1
.
          PACK      KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KEY8
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
          WRITE     HTMLFILE;"<tr>":
                             "<td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                             "<td>&nbsp;",TDDESC,"</td>":
                             "<td>&nbsp;",NZSPPROC,"</td>";

           IF        NZSPPIND=1 & NZSPPRIM<>2
             WRITE     HTMLFILE;"<td>&nbsp;",NZSPTDUR,"</td>";
           ELSE
             WRITE     HTMLFILE;"<td>&nbsp;</td>";
           ENDIF
.
           WRITE    HTMLFILE;"<td>&nbsp</td>":
                             "<td>&nbsp;",PTDTCNTR,"</td>":
                             "<td>&nbsp;",TITEMNO,"</td>":
                             "<td>&nbsp</td>";
.
          MOVE      TPATAMTT,FORM12P2
          ADD       TPATAMTT,GROSSTOT
          WRITE     HTMLFILE;"<td align=right>&nbsp;</td>":
                             "<td align=right>",FORM12P2,"</td>";
          ADD       PTDTGSTM,FORM12P2
          ADD       PTDTGSTM,TOTGST
          WRITE     HTMLFILE;"<td align=right>&nbsp;</td>":
                             "<td align=right>",FORM12P2,"</td></tr>"
          GOTO      D2SCR100
.
D2SCR999  RETURN
+
.**********************************************************************
.*                            D3SCR000                                *
.*                     Display Misc.Items                             *
.**********************************************************************
D3SCR000  MOVE      ZERO,MISCTOT
          MOVE      ZERO,ROUNDAMT
.
          MOVE      ZERO,TRNFLAG
          MOVE      ONE,CSTYPEF
          MOVE      SP70,SAVGCOD
.
          IF        PINVSYS=3 | PINVSYS=2
            MOVE      ONE,CSTYPEF
            IF        PINVSYS=2
              PACK      KEY26 WITH PINVADM,PINVNO,ONE,SP30
            ELSE
              PACK      KEY26 WITH PINVADM,PINVNO,THREE,SP30
            ENDIF
          ELSE
            PACK      KEY22 WITH PINVADM,PINVNO,SP30
          ENDIF
.
          CALL      RDSDTRAN
D3SCR100  CALL      RDKDTRAN
          BRANCH    OVRCD OF D3SCR820
.
          MATCH     PINVNO,TREF
          GOTO      D3SCR820 IF NOT EQUAL
.
          IF        PINVSYS<>1
            BRANCH    TRECTYPE,D3SCR820,D3SCR820,D3SCR200,D3SCR200
            IF        TRECTYPE=7
              MATCH     TMISGRP,PTCNROUN           * Check for rounding
              IF        @EQUAL
                ADD       TPATAMTT,GROSSTOT
                ADD       TPATAMTT,ROUNDAMT
              ENDIF
            ENDIF
            GOTO      D3SCR100
          ENDIF
.
D3SCR200  COMPARE   ZERO,TRNFLAG
          GOTO      D3SCR300 IF EQUAL              * first record
.
          MATCH     SP70,TMISGRP
          GOTO      D3SCR300 IF EQUAL
.
          MATCH     SAVGCOD,TMISGRP
          GOTO      D3SCR300 IF EQUAL
.
.         Display/Print total item for the group
.
          MOVE      SP70,TDESC
          MATCH     SP70,SAVGCOD
          IF        @EQUAL
            MOVE      "No Group Code",TDESC
          ELSE
            PACK      KEY5,ANSF,ANSI,SAVGCOD,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              CLEAR     TDESC
              APPEND    "Invalid Group ",TDESC
              APPEND    SAVGCOD,TDESC
              APPEND    SP70,TDESC
              RESET     TDESC
            ENDIF
          ENDIF
.
          MOVE      MISCTOT,FORM12P2
          WRITE     HTMLFILE;"<tr><td>&nbsp;</td>":
                             "<td>",TDESC,"</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>";
.
          IF        INVTYPE=1
            WRITE     HTMLFILE;"<td align=right>",MISCTOT,"</td>":
                               "<td align=right>&nbsp;</td>":
                               "<td align=right>",FORM12P2,"</td>":
                               "</tr>"
          ELSE
            WRITE     HTMLFILE;"<td align=right>&nbsp;</td>":
                               "<td align=right>&nbsp;</td>":
                               "<td align=right>",FORM12P2,"</td>":
                               "</tr>"
          ENDIF
          MOVE      ZERO,MISCTOT
.
.         Display miscellaneous item
.
D3SCR300  ADD       TPATAMTT,GROSSTOT
          ADD       PTDTGSTM,TOTGST
          MOVE      TMISGRP,SAVGCOD
.
          ADD       TPATAMTT,MISCTOT
          MOVE      ONE,TRNFLAG
          BRANCH    INVTYPE,D3SCR100           * Summary invoice
.
          MOVE      SP70,NZSPPROC
          PACK      KEY11,PINVADM,PTDTSUNQ,SP70
          CALL      RDNZSPR1
.
          MOVE      "Incl   ",KEY7
          IF        PTDTCCU=1
            MOVE      "Patient",KEY7
          ENDIF
          IF        PTDTCCU=2
            MOVE      "Extra  ",KEY7
          ENDIF
.
          MOVE      MISCTOT,FORM12P2
          MOVE      TFDAY,CDAY
          MOVE      TFMONTH,CMON
          MOVE      TFCENT,CCENT
          MOVE      TFYEAR,CYEAR
          CALL      PACDATE
          REP       " 0",CPCDATE
.
          PACK      KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KEY8
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
          WRITE     HTMLFILE;"<tr>":
                             "<td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                             "<td>&nbsp;",TDDESC,PTDTDES2,"</td>":
                             "<td>&nbsp;",NZSPPROC,"</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;",KEY7,"</td>":
                             "<td>&nbsp;",PTDTCNTR,"</td>":
                             "<td>&nbsp;",TITEMNO,"</td>";
.
          MOVE      TPATAMTT,FORM12P2
          IF        TSERVS<>0
            MOVE      TPATAMTP,FORM12P2           * Unit price
            ADD       TREBATE,FORM12P2
            MULT      TSERVS,FORM12P2
            IF        FORM12P2<>TPATAMTT
              DIV       TSERVS,TPATAMTP
              DIV       TSERVS,TREBATE
            ENDIF
            MOVE      TPATAMTP,FORM12P2           * Unit price
            ADD       TREBATE,FORM12P2
          ENDIF
.
          WRITE     HTMLFILE;"<td>&nbsp;",TSERVS,"</td>":
                             "<td align=right>",FORM12P2,"</td>";
          MULT      TSERVS,FORM12P2
.
          WRITE     HTMLFILE;"<td align=right>",FORM12P2,"</td>":
                             "<td>&nbsp;</td>";
.
          ADD       PTDTGSTM,FORM12P2
          WRITE     HTMLFILE;"<td>&nbsp;</td>":
                             "</tr>";
          GOTO      D3SCR100
.
.        Display item group code
.
D3SCR820  COMPARE   ZERO,TRNFLAG
          GOTO      D3SCR900 IF EQUAL
.
          MOVE      SP70,TDESC
          MATCH     SP70,SAVGCOD
          IF        @EQUAL
            MOVE      "No Group Code",TDESC
          ELSE
            PACK      KEY5,ANSF,ANSI,SAVGCOD,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              CLEAR     TDESC
              APPEND    "Invalid Group ",TDESC
              APPEND    SAVGCOD,TDESC
              APPEND    SP70,TDESC
              RESET     TDESC
            ENDIF
          ENDIF
.
          MOVE      MISCTOT,FORM12P2
          WRITE     HTMLFILE;"<tr><td>&nbsp;</td>":
                             "<td>",TDESC,"</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>";
.
          IF        INVTYPE=1
            WRITE     HTMLFILE;"<td align=right>",MISCTOT,"</td>":
                               "<td align=right>&nbsp;</td>":
                               "<td align=right>",FORM12P2,"</td>":
                               "</tr>"
            MOVE      ZERO,MISCTOT
          ELSE
            WRITE     HTMLFILE;"<td align=right>&nbsp;</td>":
                               "<td align=right>&nbsp;</td>":
                               "<td align=right>",FORM12P2,"</td>":
                               "</tr>"
          ENDIF
.
D3SCR900  MOVE      ZERO,CSTYPEF
D3SCR999  RETURN
+
.**********************************************************************
.*                            S8TYP000                                *
.*                       Routine to print Procedures                  *
.**********************************************************************
S8TYP000  COMPARE   THREE,PINVSYS
          GOTO      S8TYP999 IF NOT EQUAL
.
          IF        CRDNFLAG=1
            MOVE      "8",KEY1
          ENDIF
          MOVE      ONE,CSTYPEF
          MOVE      "8",DTRECTYP
          MOVE      PINVADM,DPINVADM
          PACK      KEY26 WITH PINVADM,PINVNO,DTRECTYP,SP30
          CALL      RDSDTRAN
S8TYP100  CALL      RDKDTRAN
          BRANCH    OVRCD,S8TYP999
.
          MATCH     PINVNO,TREF
          GOTO      S8TYP999 IF NOT EQUAL
.
          COMPARE   EIGHT,TRECTYPE          * Procedure record
          GOTO      S8TYP999 IF NOT EQUAL
.
          MOVE      TFDAY,CDAY
          MOVE      TFMONTH,CMON
          MOVE      TFCENT,CCENT
          MOVE      TFYEAR,CYEAR
          CALL      PACDATE
          REP       " 0",CPCDATE
.
          PACK      KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KEY8
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
.
          ADD       TPATAMTT,TOTAMNT
          MATCH     "1",PTDTCONT
          GOTO      S8TYP300 IF EQUAL       * funder contribution
.
          MOVE      ZERO,FORM12P2
          MOVE      PTDTLSRB,FORM12P2
          ADD       PTDTLSPT,FORM12P2        * fixed fee
.
.         Check if this is a fixed fee
.
          COMPARE   ZERO,FORM12P2
          GOTO      S8TYP120 IF NOT EQUAL
          COMPARE   ZERO,PTDTADPT
          GOTO      S8TYP300 IF EQUAL      * No adjustment fee
.
S8TYP120  MOVE      ONE,F2                 * Set to check amt only
          MOVE      FORM12P2,FORM12D2      * save fix fee
          CALL      DIBL0000               * Check for Invoice breakdown
          IF        F1=0
            MOVE      FORM12D2,TPATAMTT
            GOTO      S8TYP300             * No Invoice breakdown
          ENDIF
.
          MOVE      ZERO,F2
          CALL      DIBL0000               * Display Invoice breakdown
          GOTO      S8TYP500
.
S8TYP300  MOVE      TPATAMTT,FORM7P2
.
          MATCH     "1",PTCNPITM           * print long item desc?
          GOTO      S8TYP320 IF EQUAL
          MATCH     "1",PTCNPPRC           * print procedure code?
          GOTO      S8TYP400 IF NOT EQUAL
.
          PRINT     *3,CPCDATE,*14,TITEMNO,*34,TDDESC,*70,FORM7P2
          ADD       ONE,COUNT
          GOTO      S8TYP500
.
S8TYP320  PRINT     *3,CPCDATE,*14,TITEMNO,*34,TDDESC,*64,PTDTDES2:
                    *115,FORM7P2                         * Details
          ADD       ONE,COUNT
          GOTO      S8TYP500
.
S8TYP400  PRINT     *3,CPCDATE,*18,TITEMNO,*30,TDDESC,*70,FORM7P2  * Details
          ADD       ONE,COUNT
.
S8TYP500  MATCH     "1",PTDTCONT
          GOTO      S8TYP100 IF EQUAL
.
          COMPARE   ZERO,PTDTADPT           * Check for adjustment fee
          GOTO      S8TYP100 IF EQUAL
.
          MOVE      "Fixed Fee Adjustment        ",TDDESC
          MOVE      PTDTADPT,TPATAMTT  * Adjustment fixed fee
.
          COMPARE   MAXLINE,COUNT
          CALL      TAIL IF NOT LESS
.
          MOVE      PTDTADPT,FORM7P2
          MATCH     "1",PTCNPITM            * print long item desc?
          GOTO      S8TYP600 IF EQUAL
          MATCH     "1",PTCNPPRC            * print procedure code?
          GOTO      S8TYP800 IF NOT EQUAL
.
          PRINT     *3,CPCDATE,*14,TITEMNO,*34,TDDESC,*70,FORM7P2  * Details
          ADD       ONE,COUNT
          GOTO      S8TYP100
.
S8TYP600  PRINT     *3,CPCDATE,*14,TITEMNO,*34,TDDESC,*64,PTDTDES2:
                    *115,FORM7P2            * Details
          ADD       ONE,COUNT
          GOTO      S8TYP100
.
S8TYP800  PRINT     *3,CPCDATE,*18,TITEMNO,*30,TDDESC,*70,FORM7P2  * Details
          ADD       ONE,COUNT
          GOTO      S8TYP100
.
S8TYP999  RETURN
+
.**********************************************************************
.           Print Accommodation
.**********************************************************************
S1TYP000  PACK      KEY31 WITH PINVADM,PINVNO,ONE,SP30
          PACK      KEY23 WITH PINVADM,PINVNO,ONE,SP30
          PACK      KEY22 WITH PINVADM,PINVNO,SP30
          MOVE      ZERO,INVBFLAG
.
          CALL      RDSDTRAN
S1TYP100  CALL      RDKDTRAN
          BRANCH    OVRCD,S1TYP999               * Check for end of File
.
          MATCH     PINVNO,TREF                  * Check if Same Invoice
          GOTO      S1TYP999 IF NOT EQUAL
.
          COMPARE   ONE,TRECTYPE                 * Check for Accom. Record
          GOTO      S1TYP999 IF NOT EQUAL
.
          MOVE      TPATAMTI,FORM6P2
.
          IF        TNODAYS=1
            MOVE      "DAY",DAYFORMT
          ELSE
            MOVE      "DAYS",DAYFORMT
          ENDIF
.
          PACK      KEY27,SP70
          CLEAR     KEY27
          APPEND    "@",KEY27
          APPEND    FORM6P2,KEY27
          APPEND    " for",KEY27
.
          MOVE      TPATAMTT,FORM12P2
          DIV       TPATAMTI,FORM12P2
          MOVE      FORM12P2,FORM41
.
          MOVE      SP70,KEY6
          UNPACK    FORM41,KEY6
          UNPACK    KEY6,KEY4,KEY2
          MATCH     ".5",KEY2               * check if the last 2 chars
          GOTO      S1TYP300 IF NOT EQUAL
.
          MOVE      "DAYS",DAYFORMT
          MOVE      TNODAYS,FORM41
          ADD       "0.5",FORM41
          APPEND    FORM41,KEY27
          GOTO      S1TYP500
.
S1TYP300  APPEND    TNODAYS,KEY27
S1TYP500  APPEND    SP1,KEY27
          APPEND    DAYFORMT,KEY27
          APPEND    SP70,KEY27
          RESET     KEY27
.
          COMPARE   MAXLINE,COUNT
          CALL      TAIL IF NOT LESS
.
          MOVE      TPATAMTT,FORM7P2
          MATCH     "1",PTCNPITM                         * print long item desc?
          GOTO      S1TYP580 IF NOT EQUAL
.
          PRINT     *3,TDDESC,*33,KEY27,*115,FORM7P2      * Details
          GOTO      S1TYP800
.
S1TYP580  PRINT     *3,TDDESC,*33,KEY27,*70,FORM7P2      * Details
.
S1TYP800  ADD       TPATAMTT,TOTAMNT
          ADD       PTDTGSTM,GSTAMNT
          ADD       ONE,COUNT
          GOTO      S1TYP100
.
S1TYP999  RETURN
+
.**********************************************************************
.*                            S2TYP000                                *
.*                   Process Procedure Records (ie. TRECTYPE = 2) SX  *
.**********************************************************************
S2TYP000  IF        CRDNFLAG=1
            MOVE      "2",KEY1
          ENDIF
          PACK      KEY23 WITH PINVADM,PINVNO,TWO,SP30
          PACK      KEY26 WITH PINVADM,PINVNO,TWO,SP30
          CALL      RDSDTRAN
S2TYP100  CALL      RDKDTRAN
          BRANCH    OVRCD,S2TYP999               * Check for end of File
.         
          MATCH     PINVNO,TREF                  * Check if Same Invoice
          GOTO      S2TYP999 IF NOT EQUAL
.             
          COMPARE   TWO,TRECTYPE
          GOTO      S2TYP999 IF NOT EQUAL
.
          IF        CRDNFLAG=1
            ADD       PTDTGSTM,GSTAMNT
            ADD       PTDTGSTM,TOTGST
          ENDIF
.
          IF        PTDTDTYP=2
            MOVE      TREBATE,FORM7P2              * Funder contribution
          ELSE
            MOVE      TPATAMTT,FORM7P2
          ENDIF
          ADD       FORM7P2,TOTAMNT
          MOVE      TFDAY,CDAY
          MOVE      TFMONTH,CMON
          MOVE      TFCENT,CCENT
          MOVE      TFYEAR,CYEAR
          CALL      PACDATE
          REP       " 0",CPCDATE
.
          COMPARE   MAXLINE,COUNT                * Check for room on Page
          CALL      TAIL IF NOT LESS
.
          MATCH     "1",PTCNPITM            * print long item desc?
          GOTO      S2TYP600 IF EQUAL
          MATCH     "1",PTCNPPRC            * print procedure code?
          GOTO      S2TYP800 IF NOT EQUAL
.
          PRINT     *3,CPCDATE,*14,TITEMNO,*34,TDDESC,*70,FORM7P2  * Details
          ADD       ONE,COUNT
          GOTO      S2TYP100
.
S2TYP600  PRINT     *3,CPCDATE,*14,TITEMNO,*34,TDDESC,*64,PTDTDES2:
                    *115,FORM7P2  * Details
          ADD       ONE,COUNT
          GOTO      S2TYP100
.
S2TYP800  PRINT     *3,CPCDATE,*18,TITEMNO,*30,TDDESC,*70,FORM7P2  * Details
          ADD       ONE,COUNT
          GOTO      S2TYP100
.
S2TYP999  RETURN
+
.
.**********************************************************************
.*                            S3TYP000                                *
.*             Process Miscellaneous Records (ie. TRECTYPE = 3)       *
.**********************************************************************
S3TYP000  MOVE      ZERO,ROUNDAMT
          MOVE      ZERO,TRNFLAG
          IF        CRDNFLAG<>1
            MOVE      PTINGSTA,GSTAMNT
            MOVE      PTINGSTA,TOTGST
          ENDIF
.
          IF        PINVSYS=3 | PINVSYS=2
            MOVE      ONE,CSTYPEF
            IF        PINVSYS=2
              PACK      KEY26 WITH PINVADM,PINVNO,ONE,SP30
            ELSE
              PACK      KEY26 WITH PINVADM,PINVNO,THREE,SP30
            ENDIF
          ELSE
            PACK      KEY22 WITH PINVADM,PINVNO,SP30
          ENDIF
.
          MOVE      ONE,CSTYPEF
          MOVE      SP70,SAVGCOD
.
          CALL      RDSDTRAN
S3TYP100  CALL      RDKDTRAN
          BRANCH    OVRCD,S3TYP600               * Check for end of File
.
          MATCH     PINVNO,TREF                  * Check if Same Invoice
          GOTO      S3TYP600 IF NOT EQUAL
.
          MOVE      TFDAY,CDAY
          MOVE      TFMONTH,CMON
          MOVE      TFCENT,CCENT
          MOVE      TFYEAR,CYEAR
          CALL      PACDATE
          REP       " 0",CPCDATE
.
          IF        PINVSYS<>1
            BRANCH    TRECTYPE,S3TYP600,S3TYP600,S3TYP200
            IF        TRECTYPE=7
              MATCH     TMISGRP,PTCNROUN           * Check for rounding
              IF        @EQUAL
                ADD       TPATAMTT,TOTAMNT
                ADD       TPATAMTT,ROUNDAMT
              ENDIF
            ENDIF
            GOTO      S3TYP100
          ENDIF
.
S3TYP200  COMPARE   ZERO,TRNFLAG
          GOTO      S3TYP400 IF EQUAL              * first record
.
          MATCH     SP70,TMISGRP
          GOTO      S3TYP400 IF EQUAL
.
          MATCH     SAVGCOD,TMISGRP
          GOTO      S3TYP400 IF EQUAL
.
.         Display/Print total item for the group
.
          COMPARE   MAXLINE,COUNT
          CALL      TAIL IF NOT LESS
.
          MOVE      SP70,TDESC
          MATCH     SP70,SAVGCOD
          IF        @EQUAL
            MOVE      "No Group Code",TDESC
          ELSE
            PACK      KEY5,ANSF,ANSI,SAVGCOD,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              CLEAR     TDESC
              APPEND    "Invalid Group",TDESC
              APPEND    SAVGCOD,TDESC
              APPEND    SP70,TDESC
              RESET     TDESC
            ENDIF
          ENDIF
.
          MOVE      TOTMISC,FORM7P2
.
.         Summary invoice - do not print if total of misc.item is zero
.
          IF        INVTYPE=1
            COMPARE   ZERO,FORM7P2
            GOTO      S3TYP400 IF EQUAL
          ENDIF
.
          MATCH     "1",PTCNPITM              * print long item desc?
          GOTO      S3TYP220 IF EQUAL
          MATCH     "1",PTCNPPRC              * print procedure code?
          GOTO      S3TYP300 IF NOT EQUAL
.
          PRINT     *34,TDESC,*70,FORM7P2
          GOTO      S3TYP320
.
S3TYP220  PRINT     *34,TDESC,*115,FORM7P2
          GOTO      S3TYP320
.
S3TYP300  PRINT     *30,TDESC,*70,FORM7P2
.
S3TYP320  ADD       ONE,COUNT
          MOVE      ZERO,TOTMISC
.
S3TYP400  ADD       TPATAMTT,TOTAMNT
          MOVE      TMISGRP,SAVGCOD
          MOVE      TPATAMTT,FORM7P2
          ADD       TPATAMTT,TOTMISC
          IF        CRDNFLAG=1
            ADD       PTDTGSTM,GSTAMNT
            ADD       PTDTGSTM,TOTGST
          ENDIF
.
.         COMPARE   ZERO,PTDTCCU
.         GOTO      S3TYP100 IF EQUAL        * Include in Contract
.
          MOVE      ONE,TRNFLAG
          BRANCH    INVTYPE,S3TYP100         * Summary invoice
.
          COMPARE   MAXLINE,COUNT
          CALL      TAIL IF NOT LESS
.
          MOVE      TFDAY,CDAY
          MOVE      TFMONTH,CMON
          MOVE      TFCENT,CCENT
          MOVE      TFYEAR,CYEAR
          CALL      PACDATE
          REP       " 0",CPCDATE
.
          MOVE      SP70,NZSPPROC
          PACK      KEY11,PINVADM,PTDTSUNQ,SP70
          CALL      RDNZSPR1
.
          MOVE      TPATAMTT,FORM7P2
          UNPACK    TDDESC,KEY20
.
          MATCH     "1",PTCNPITM            * print long item desc?
          GOTO      S3TYP460 IF EQUAL
          MATCH     "1",PTCNPPRC            * print procedure code?
          GOTO      S3TYP480 IF NOT EQUAL
.
          PRINT     *3,CPCDATE,*14,NZSPPROC,*24,TITEMNO:
                    *34,KEY20,*54,TSERVS,*60,FORM7P2
          GOTO      S3TYP500
.
S3TYP460  PRINT     *3,CPCDATE;
          MATCH     "1",PTCNPPRC            * print procedure code?
          IF        @EQUAL
            PRINT     *14,NZSPPROC;
          ENDIF
          PRINT     *24,TITEMNO:
                    *34,TDDESC,*64,PTDTDES2,*100,TSERVS,*105,FORM7P2
          GOTO      S3TYP500
.
S3TYP480  PRINT     *3,CPCDATE,*18,TITEMNO:
                    *30,KEY20,*52,TSERVS,*60,FORM7P2
.
S3TYP500  ADD       ONE,COUNT
          IF        INVTYPE=2
            CALL      XPAC0000                * print contents of Pack item
          ENDIF
          GOTO      S3TYP100
.
S3TYP600  COMPARE   ZERO,TRNFLAG
          GOTO      S3TYP700 IF EQUAL
.
          COMPARE   MAXLINE,COUNT
          CALL      TAIL IF NOT LESS
.
          MOVE      SP70,TDESC
          MATCH     SP70,SAVGCOD
          IF        @EQUAL
            MOVE      "No Group Code",TDESC
          ELSE
            PACK      KEY5,ANSF,ANSI,SAVGCOD,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              CLEAR     TDESC
              APPEND    "Invalid Group",TDESC
              APPEND    SAVGCOD,TDESC
              APPEND    SP70,TDESC
              RESET     TDESC
            ENDIF
          ENDIF
.
.         Printing item group in bold
.
          MOVE      TOTMISC,FORM7P2
.
.         Summary invoice - do not print if total of misc.item is zero
.
          IF        INVTYPE=1
            COMPARE   ZERO,FORM7P2
            GOTO      S3TYP700 IF EQUAL
          ENDIF
.
          MATCH     "1",PTCNPITM              * print long item desc?
          GOTO      S3TYP620 IF EQUAL
          MATCH     "1",PTCNPPRC              * print procedure code?
          GOTO      S3TYP680 IF NOT EQUAL
.
          PRINT     *34,TDESC,*70,FORM7P2
          ADD       ONE,COUNT
          GOTO      S3TYP700
.
S3TYP620  PRINT     *34,TDESC,*115,FORM7P2
          ADD       ONE,COUNT
          GOTO      S3TYP700
.
S3TYP680
.         PRINT     *3,ESCCHR,CHARG,TDESC,ESCCHR,CHARH:
          PRINT     *30,TDESC:
                    *70,FORM7P2    * Details
          ADD       ONE,COUNT
.
S3TYP700  MOVE      TOTAMNT,FORM7P2
          SUB       ROUNDAMT,FORM7P2
.
          MOVE      MAXLINE,FORM2
          SUB       TWO,FORM2
          COMPARE   FORM2,COUNT
          CALL      TAIL IF NOT LESS
.
          MATCH     "1",PTCNPITM              * print long item desc?
          GOTO      S3TYP800 IF NOT EQUAL
.
          PRINT     *N,*115,"----------":
                    *N,*3,"Total Charges",*115,FORM7P2
          GOTO      S3TYP820
.
S3TYP800  PRINT     *N,*70,"----------":
                    *N,*3,"Total Charges",*70,FORM7P2
.
S3TYP820  ADD       THREE,COUNT
.
          MOVE      MAXLINE,FORM2
          SUB       ONE,FORM2
          COMPARE   FORM2,COUNT
          CALL      TAIL IF NOT LESS
.         IF        CRDNFLAG=1
.           MULT      "-1",GSTAMNT
.         ENDIF
.
          IF        CRDNFLAG=1
            MATCH     "1",PTINCRST           * Credit all
            IF        @EQUAL
              MOVE      PTINGSTA,GSTAMNT
              MULT      "-1",GSTAMNT
              MOVE      PTINGSTA,TOTGST
            ENDIF
          ENDIF
          MOVE      GSTAMNT,FORM7P2
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,NINTY8;*130,PTCNAGST
          OPEN      IBAGSTA1,"ibagstaf"
          PACK      KEY6,PTCNAGST,SP70
          CALL      RDIBGS1
          IF        OVRCD=1
            MOVE      "GST                     ",IBGSDESC
          ENDIF
.
          MATCH     "1",PTCNPITM              * print long item desc?
          GOTO      S3TYP860 IF NOT EQUAL
.
          PRINT     *3,"Add ",IBGSDESC,*115,FORM7P2:
                    *N,*115,"----------"
          GOTO      S3TYP880
.
S3TYP860  PRINT     *3,"Add ",IBGSDESC,*70,FORM7P2:
                    *N,*70,"----------"
.
S3TYP880  ADD       TWO,COUNT
.
          MOVE      MAXLINE,FORM2
          SUB       ONE,FORM2
          COMPARE   FORM2,COUNT
          CALL      TAIL IF NOT LESS
.
          MOVE      TOTAMNT,FORM12P2
          SUB       ROUNDAMT,FORM12P2
          ADD       GSTAMNT,FORM12P2
          MOVE      FORM12P2,FORM7P2
.
          MATCH     "1",PTCNPITM              * print long item desc?
          GOTO      S3TYP900 IF NOT EQUAL
.
          PRINT     *3,"TOTAL CHARGE (INCLUSIVE OF GST)",*115,FORM7P2:
                    *N
          GOTO      S3TYP920
.
S3TYP900  PRINT     *3,"TOTAL CHARGE (INCLUSIVE OF GST)",*70,FORM7P2:
                    *N
.
S3TYP920  ADD       TWO,COUNT
          MOVE      ZERO,CSTYPEF
.
S3TYP999  RETURN
+
.**********************************************************************
.* XPAC0000 : Print Content of Pack item
.**********************************************************************
XPAC0000  MOVE      TITEMNO,KEY9
          CALL      FNDNZM00
          BRANCH    EXIT,XPAC9999
.
          COMPARE   ONE,NZMCCHGT
          GOTO      XPAC9999 IF NOT EQUAL   * not exploding charge
.
          OPEN      NZPMXCA1,"nzpmxcaf"
          PACK      DIM37,NZMCHOSP,NZMCCLMC,NZMCCNTR,NZMCFTAB,NZMCMCHG:
                          NZMCEFDT,SP70
.
          PACK      KEY40,DIM37,SP70
          CALL      RSNZMXC1
XPAC1000  CALL      RKNZMXC1
          BRANCH    OVRCD,XPAC9999
.
          PACK      KEY37,NZMXHOSP,NZMXCLMC,NZMXCNTR,NZMXFTAB,NZMXMCHG:
                          NZMXEFDT,SP70
          MATCH     DIM37,KEY37
          GOTO      XPAC9999 IF NOT EQUAL
.
          COMPARE   MAXLINE,COUNT
          CALL      TAIL IF NOT LESS
.
          MOVE      NZMXITMN,KEY9
          CALL      FNDNZM00                  * Read Misc.item file to get desc.
          BRANCH    EXIT,XPAC1000
.
          PACK      KEY20,NZMCDESC
          MOVE      ZERO,FORM3
          MOVE      NZMXQNTY,FORM3
          IF        TSERVS<>0
            MULT      TSERVS,FORM3
          ENDIF
.
          MATCH     "1",PTCNPITM              * print long item desc?
          GOTO      XPAC6000 IF EQUAL
          MATCH     "1",PTCNPPRC              * print procedure code?
          GOTO      XPAC7000 IF NOT EQUAL
.
          PRINT     *3,"     Incl.";
.         PRINT     *14,NZSPPROC;
          PRINT     *24,NZMXITMN:
                    *34,KEY20,*56,FORM3
          GOTO      XPAC8000
.
XPAC6000  PRINT     *3,"     Incl.";
.         MATCH     "1",PTCNPPRC            * print procedure code?
.         IF        @EQUAL
.           PRINT     *14,NZSPPROC;
.         ENDIF
          UNPACK    NZMCDESC,KEY30,KEY35
          PRINT     *24,NZMXITMN:
                    *34,KEY30,*64,KEY35,*102,FORM3
          GOTO      XPAC8000
.
XPAC7000  PRINT     *3,"     Incl.",*18,NZMXITMN:
                    *30,KEY20,*54,FORM3
.
XPAC8000  ADD       ONE,COUNT
          GOTO      XPAC1000
.
XPAC9999  RETURN
+
.******************************************************************************
.         Get NZ Misc.Item
.******************************************************************************
FNDNZM00  MOVE      ZERO,EXIT
.
          OPEN      NZPMCHA1,"nzpmchaf"
          PACK      MISCDATE,TFCENT,TFYEAR,TFMONTH,TFDAY
          REP       " 0",MISCDATE
          MOVE      PMVXMHOS,KEY3
          MOVE      ZERO,F1
.
FNDNZM07  PACK      KEY29,KEY3,NZSPCLMN,NZSPCNTR,SP8,KEY9,SP70
          PACK      KEY37,KEY29,MISCDATE
          CALL      RDNZMCH1
          IF        OVRCD=0
            MATCH     "1",NZMCACFL                * Inactive?
            GOTO      FNDNZM99 IF NOT EQUAL       * No
          ENDIF
.
FNDNZM08  CALL      RPNZMCH1
          BRANCH    OVRCD,FNDNZM10
          PACK      KEY29A,NZMCHOSP,NZMCCLMC,NZMCCNTR,NZMCFTAB,NZMCMCHG,SP70
          MATCH     KEY29,KEY29A
          GOTO      FNDNZM10 IF NOT EQUAL
.
          MATCH     "1",NZMCACFL                * Inactive?
          GOTO      FNDNZM08 IF EQUAL           * Yes
.
          MATCH     NZMCEFDT,MISCDATE       * effective date => serv. date ?
          GOTO      FNDNZM10 IF LESS        * yes
.
          MATCH     SP70,NZMCEEDT
          IF        !@EQUAL
            MATCH     MISCDATE,NZMCEEDT
            GOTO      FNDNZM10 IF LESS
          ENDIF
          GOTO      FNDNZM99
.
.         No record with health fund details - try a blank health fund
.
FNDNZM10  PACK      KEY29,KEY3,NZSPCLMN,SP6,SP8,KEY9,SP70
          PACK      KEY37,KEY29,MISCDATE
          CALL      RDNZMCH1
          IF        OVRCD=0
            MATCH     "1",NZMCACFL                * Inactive?
            GOTO      FNDNZM99 IF NOT EQUAL       * No
          ENDIF
.
FNDNZM12  CALL      RPNZMCH1
          BRANCH    OVRCD,FNDNZM20
          PACK      KEY29A,NZMCHOSP,NZMCCLMC,NZMCCNTR,NZMCFTAB,NZMCMCHG,SP70
          MATCH     KEY29,KEY29A
          GOTO      FNDNZM20 IF NOT EQUAL
.
          MATCH     "1",NZMCACFL                * Inactive?
          GOTO      FNDNZM12 IF EQUAL           * Yes
.
          MATCH     NZMCEFDT,MISCDATE       * effective date => serv. date ?
          GOTO      FNDNZM20 IF LESS        * yes
.
          MATCH     SP70,NZMCEEDT
          IF        !@EQUAL
            MATCH     MISCDATE,NZMCEEDT
            GOTO      FNDNZM20 IF LESS
          ENDIF
          GOTO      FNDNZM99
.
.         No record with health fund details - try default claim, blank hf
.
FNDNZM20  IF        IBCNMHOS=1
            MOVE      PTHSDCLM,NZMCCLMC
          ELSE
            MOVE      PTCNDCLM,NZMCCLMC
          ENDIF
          PACK      KEY29,KEY3,NZMCCLMC,SP14,KEY9
          PACK      KEY37,KEY29,MISCDATE,SP70
          CALL      RDNZMCH1
          IF        OVRCD=0
            MATCH     "1",NZMCACFL                * Inactive?
            GOTO      FNDNZM99 IF NOT EQUAL       * No
          ENDIF
.
FNDNZM22  CALL      RPNZMCH1
          BRANCH    OVRCD,FNDNZM30
          PACK      KEY29A,NZMCHOSP,NZMCCLMC,NZMCCNTR,NZMCFTAB,NZMCMCHG,SP70
          MATCH     KEY29,KEY29A
          GOTO      FNDNZM30 IF NOT EQUAL
.
          MATCH     "1",NZMCACFL                * Inactive?
          GOTO      FNDNZM22 IF EQUAL           * Yes
.
          MATCH     NZMCEFDT,MISCDATE       * effective date => serv. date ?
          GOTO      FNDNZM30 IF LESS        * yes
.
          MATCH     SP70,NZMCEEDT
          IF        !@EQUAL
            MATCH     MISCDATE,NZMCEEDT
            GOTO      FNDNZM30 IF LESS
          ENDIF
.
          GOTO      FNDNZM99
.
.         No record for the hosp.- try with blank hosp
.
FNDNZM30  BRANCH    F1,FNDNZM90
          ADD       ONE,F1
          MOVE      SP70,KEY3
          GOTO      FNDNZM07
.
FNDNZM90  MOVE      ONE,EXIT
          GOTO      FNDNZM99
.
FNDNZM99  RETURN
+
.**********************************************************************
.*                            S5TYP000                                *
.*             Process Cash Receipt Records (ie. TRECTYPE = 5)        *
.**********************************************************************
S5TYP000  MOVE      ZERO,PAYMTTOT
          MOVE      ZERO,TRNFLAG
          IF        PINVSYS=3 | PINVSYS=2
            MOVE      ONE,CSTYPEF
            IF        PINVSYS=2
              PACK      KEY26 WITH PINVADM,PINVNO,TWO,SP30
            ELSE
              PACK      KEY26 WITH PINVADM,PINVNO,FIVE,SP30
            ENDIF
          ELSE
            PACK      KEY22 WITH PINVADM,PINVNO,SP30
          ENDIF
.
          CALL      RDSDTRAN
S5TYP100  CALL      RDKDTRAN
          BRANCH    OVRCD,S5TYP200               * Check for end of File
.
          MATCH     PINVNO,TREF                  * Check if Same Invoice
          GOTO      S5TYP200 IF NOT EQUAL
.
          IF        PINVSYS=3 | PINVSYS=2
            COMPARE   FIVE,TRECTYPE                * Check for a Cash Receipt
            GOTO      S5TYP200 IF NOT EQUAL
          ELSE
            COMPARE   FIVE,TRECTYPE                * Check for a Cash Receipt
            GOTO      S5TYP100 IF NOT EQUAL
          ENDIF
.
          ADD       TPATAMTT,PAYMTTOT
          MOVE      ZERO,CASHFLAG
.         BRANCH    INVTYPE,S5TYP100               * Summary invoice
.
          IF        TRNFLAG=0
.           COMPARE   MAXLINE,COUNT                * Check for room on Page
.           CALL      TAIL IF NOT LESS
.           PRINT     *1,"LESS PAYMENT"
.           ADD       ONE,COUNT
            ADD       ONE,TRNFLAG
          ENDIF
.
          COMPARE   MAXLINE,COUNT                * Check for room on Page
          CALL      TAIL IF NOT LESS
.
          PACK      RCDTRECN,TRECEIPT,SP70
          CALL      PAYE0000                  * Get payee details
          MOVE      RCBDPAYD,KEY50
.
          MOVE      TPATAMTT,FORM7P2
          MOVE      RCBDPAYD,KEY50
.
          UNPACK    RCBDPAYD,KEY39
          MATCH     "1",PTCNPITM              * print long item desc?
          GOTO      S5TYP160 IF NOT EQUAL
.
          PRINT     *3,"RECEIPT ##",TRECEIPT,*25,"Payer:",RCBDPAYD,*115,FORM7P2
          GOTO      S5TYP180
.
S5TYP160  PRINT     *3,"RECEIPT ##",TRECEIPT,*25,"Payer:",KEY39,*70,FORM7P2
.
S5TYP180  ADD       ONE,COUNT
          GOTO      S5TYP100
.
S5TYP200  PACK      KEY12,SP4,PINVNO
          CALL      CSHP0000                * Display cash pending if any
.
          BRANCH    CASHFLAG,S5TYP900
.
          MOVE      PAYMTTOT,FORM7P2
          ADD       CASHTOT,FORM7P2
.
          MOVE      MAXLINE,FORM2
          SUB       ONE,FORM2
          COMPARE   FORM2,COUNT
          CALL      TAIL IF NOT LESS
.
          MATCH     "1",PTCNPITM              * print long item desc?
          GOTO      S5TYP260 IF NOT EQUAL
.
          PRINT     *115,"----------":
                    *N,*3,"Total Payments",*115,FORM7P2
          GOTO      S5TYP280
.
S5TYP260  PRINT     *70,"----------":
                    *N,*3,"Total Payments",*70,FORM7P2
.
S5TYP280  ADD       TWO,COUNT
.
S5TYP900  MOVE      ZERO,CSTYPEF
S5TYP999  RETURN
+
.**********************************************************************
.*                            S6TYP000                                *
.*                Process Journal Records (ie. TRECTYPE = 6)          *
.**********************************************************************
S6TYP000  MOVE      ZERO,JOURTOT
          MOVE      ZERO,TRNFLAG
          IF        PINVSYS = 3
            PACK      KEY23 WITH PINVADM,PINVNO,SIX,SP30
          ELSE
            PACK      KEY31 WITH PINVADM,PINVNO,THREE,SP30
            PACK      KEY22 WITH PINVADM,PINVNO,SP30
          ENDIF
.
          CALL      RDSDTRAN
S6TYP100  CALL      RDKDTRAN
          BRANCH    OVRCD,S6TYP600               * Check for end of File
.
          MATCH     PINVNO,TREF                  * Check if Same Invoice
          GOTO      S6TYP600 IF NOT EQUAL
.
          IF        PINVSYS<>3
            COMPARE   SIX,TRECTYPE                 * Check for a Journal
            GOTO      S6TYP100 IF NOT EQUAL
          ELSE
            COMPARE   SIX,TRECTYPE                 * Check for a Journal
            GOTO      S6TYP600 IF NOT EQUAL
          ENDIF
.
          ADD       PTDTGSTM,TPATAMTT
.
          IF        TRNFLAG=0
            PRINT     *N
            ADD       ONE,COUNT
          ENDIF
          MATCH     TTYPE,HJNL
          IF        @EQUAL
            MOVE      SP70,TDDESC
            MOVE      "Discount from Receipt",TDDESC
          ENDIF
          MOVE      TPATAMTT,FORM7P2
          COMPARE   MAXLINE,COUNT
          CALL      TAIL IF NOT LESS
          PRINT     *3,TDDESC,*57,FORM7P2
          ADD       ONE,COUNT
.
S6TYP400  ADD       TPATAMTT,JOURTOT
          MOVE      ONE,TRNFLAG
          GOTO      S6TYP100
.
S6TYP600  COMPARE   ZERO,TRNFLAG
          GOTO      S6TYP999 IF EQUAL              * No journals
          MOVE      JOURTOT,FORM7P2
.
          COMPARE   MAXLINE,COUNT
          CALL      TAIL IF NOT LESS
.
          MATCH     "1",PTCNPITM              * print long item desc?
          GOTO      S6TYP660 IF NOT EQUAL
.
          PRINT     *3,"Total Journals",*115,FORM7P2
          GOTO      S6TYP680
.
S6TYP660  PRINT     *3,"Total Journals",*70,FORM7P2
.
S6TYP680  ADD       ONE,COUNT
.
S6TYP999  RETURN
+
.**********************************************************************
.*                            SXEND000                                *
.*                     Print End of the Invoice - NZ Billing          *
.**********************************************************************
SXEND000  MOVE      TOTAMNT,GROSSTOT
          MOVE      CASHTOT,CSAMT
          MULT      "-1",CSAMT
.
          MOVE      GROSSTOT,NETTOT
          ADD       GSTAMNT,NETTOT 
          ADD       PAYMTTOT,NETTOT
          ADD       CASHTOT,NETTOT
          ADD       JOURTOT,NETTOT
          ADD       CREDTOT,NETTOT
          MOVE      NETTOT,FORM7P2
.
          MOVE      MAXLINE,FORM2
          SUB       "2",FORM2
          COMPARE   FORM2,COUNT               * Check For A Full Page
          CALL      TAIL IF NOT LESS
.
          MATCH     "1",PTCNPITM              * print long item desc?
          GOTO      SXEND600 IF NOT EQUAL
.
          PRINT     *115,"----------":
                    *N,*3,"BALANCE PAYABLE",*115,FORM7P2:
                    *N,*115,"=========="
          GOTO      SXEND800
.
SXEND600  PRINT     *70,"----------":
                    *N,*3,"BALANCE PAYABLE",*70,FORM7P2:
                    *N,*70,"=========="
.
SXEND800  ADD       THREE,COUNT
.
          WHILE     COUNT<MAXLINE
            PRINT     *N;
            ADD       ONE,COUNT
          DO
          CALL      SINVT000                 * set up for templating
          MOVE      ZERO,MPGEFLAG
          ADD       ONE,CPAGENO
          MOVE      FORM7P2,NETTOT           * Balance payable
          MOVE      GROSSTOT,INVCTOTL
          ADD       GSTAMNT,INVCTOTL         * Invoice total
          MOVE      PAYMTTOT,FORM62
          ADD       CASHTOT,FORM62           * Receipt amount
          CALL      PRTINVTL                 * print templated invoice tail
          PRINT     *N;
SXEND999  RETURN
+
.**********************************************************************
.         Display Invoice breakdown
.**********************************************************************
DIBL0000  MOVE      ZERO,F1
          MOVE      ZERO,FORM12P2
          OPEN      NZPIVBA1,"nzpivbaf"
          PACK      KEY22,DPINVADM,PINVNO,PTDTSUNQ,SP70
          CALL      RSNZIVB1
DIBL1000  CALL      RKNZIVB1
          BRANCH    OVRCD,DIBL9999
.
          MATCH     DPINVADM,NZIVADMN
          GOTO      DIBL9999 IF NOT EQUAL
          MATCH     PINVNO,NZIVINVN
          GOTO      DIBL9999 IF NOT EQUAL
          MATCH     PTDTSUNQ,NZIVSUNQ
          GOTO      DIBL9999 IF NOT EQUAL
.
          MOVE      SP70,TDESC
          PACK      KEY5,ANSF,ANSI,NZIVBRCD
          CALL      RDCODE1
.
          BRANCH    F2,DIBL4000                * calculate amt only
.
          BRANCH    PRINTFLG,DIBL2000
.
          WRITE     HTMLFILE;"<tr>":
.                            "<td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;",TDESC,"</td>":
                             "<td>&nbsp;</td>";
.
           IF        NZSPPIND=1 & NZSPPRIM<>2
             WRITE     HTMLFILE;"<td>&nbsp;",NZSPTDUR,"</td>";
           ELSE
             WRITE     HTMLFILE;"<td>&nbsp;</td>";
           ENDIF
.
           WRITE    HTMLFILE;"<td>&nbsp;</td>":
                             "<td>&nbsp;",PTDTCNTR,"</td>":
                             "<td>&nbsp;",TITEMNO,"</td>":
                             "<td>&nbsp;</td>":
                             "<td align=right>&nbsp;</td>":
                             "<td align=right>",NZIVAMNT,"</td>":
                             "<td align=right>&nbsp;</td>":
                             "<td align=right>",NZIVAMNT,"</td></tr>"
          GOTO      DIBL4000
.
DIBL2000  MOVE      NZIVAMNT,FORM7P2
.
          COMPARE   MAXLINE,COUNT
          CALL      TAIL IF NOT LESS
.
          MATCH     "1",PTCNPITM              * print long item desc?
          GOTO      DIBL2400 IF EQUAL
          MATCH     "1",PTCNPPRC              * print procedure code?
          GOTO      DIBL2600 IF NOT EQUAL
.
          PRINT     *14,TITEMNO,*34,TDESC,*70,FORM7P2  * Details
          ADD       ONE,COUNT
          GOTO      DIBL4000
.
DIBL2400  PRINT     *14,TITEMNO,*34,TDESC,*115,FORM7P2  * Details
          ADD       ONE,COUNT
          GOTO      DIBL4000
.
DIBL2600  PRINT     *18,TITEMNO,*30,TDESC,*70,FORM7P2  * Details
          ADD       ONE,COUNT
.
DIBL4000  MOVE      ONE,F1
          ADD       NZIVAMNT,FORM12P2
          GOTO      DIBL1000
.
DIBL9999  RETURN
+
.**********************************************************************
.         Display Win/Loss
.**********************************************************************
DWLS0000  COMPARE   THREE,PINVSYS
          GOTO      DWLS9999 IF NOT EQUAL
.
          MATCH     "1",PTINCRST
          GOTO      DWLS9999 IF EQUAL         * Fully Credited
.
          MOVE      ZERO,CPRCNUMB
          MOVE      ZERO,COUNTA
          MOVE      ZERO,F1
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND03;*246,PTCNLJCD,PTCNWJCD
.
          CALL      GPRI0000                  * Get Primary Funder
.
          MOVE      ONE,F2
          WHILE     F2<=9
            MOVE      SP70,CNTRCODE[F2]
            MOVE      SP70,CPRCCODE[F2]
            ADD       ONE,F2
          DO
          CALL      MCPR0000                  * set no of contract procedures
.
          COMPARE   ZERO,CPRCNUMB
          GOTO      DWLS9999 IF EQUAL
.
          MOVE      SP70,DIM6
          MOVE      "6",DTRECTYP
          MOVE      PINVADM,DPINVADM
          OPEN      NZPRDTA6,"nzprdtaf"
          PACK      KEY26 WITH PINVADM,PINVNO,SP30
          CALL      RSNZRDT6
DWLS1000  CALL      RKNZRDT6
          BRANCH    OVRCD,DWLS9999
.
          MATCH     PINVNO,TREF
          GOTO      DWLS9999 IF NOT EQUAL
.
          MATCH     SP70,DIM6
          IF        @EQUAL
            MOVE      PTDTCNTR,DIM6       * Save the Contract funder/HF
          ENDIF
.
          MATCH     SP70,PTDTBTCH
          GOTO      DWLS4000 IF EQUAL     * Not extracted yet
.
          COMPARE   SIX,TRECTYPE          * Journal record
          GOTO      DWLS1000 IF NOT EQUAL
.
          MOVE      TPATAMTT,FORM12P2
.
          MATCH     PTCNLJCD,TTYPE              * Loss
          GOTO      DWLS2000 IF EQUAL
.
          MATCH     PTCNWJCD,TTYPE              * Win
          GOTO      DWLS1000 IF NOT EQUAL
.
DWLS2000  ADD       ONE,COUNTA
          MOVE      CNTRCODE[COUNTA],DIM6
          MOVE      CPRCCODE[COUNTA],KEY9         * Contract Procedure code
          GOTO      DWLS8000
.
.         Invoice hasn't been extracted, so work out Win/Loss from Contract and
.         FFS amount
.
DWLS4000  ADD       ONE,COUNTA
          MOVE      CNTRCODE[COUNTA],DIM6
          MOVE      CPRCCODE[COUNTA],KEY9         * Contract Procedure code
.
          CALL      FCNT0000                  * Get total Contract amount
          CALL      GFFS0000                  * Calculate FFS amount
          SUB       TFFSAMNT,FORM12P2         * Subtracted by total FFS amount
.
DWLS8000  MATCH     SP70,PTINHFND
          IF        !@EQUAL
            MATCH     DIM6,PTINHFND
            GOTO      DWLS9000 IF NOT EQUAL
          ENDIF
.
          IF        CPRCNUMB>1
            COMPARE   ZERO,FORM12P2
            GOTO      DWLS9000 IF EQUAL
          ENDIF
.
          WRITE     HTMLFILE;"<tr bgcolor=#99FFFF><td>&nbsp;</td>";
.
          IF        FORM12P2<0
            WRITE     HTMLFILE;"<td><b>Loss on this Contract - </b>":
                               DIM6," -",KEY9,"</td>";
          ELSE
            WRITE     HTMLFILE;"<td><b>Win on this Contract - </b>":
                               DIM6," -",KEY9,"</td>";
          ENDIF
.
          WRITE     HTMLFILE;"<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td align=right>",FORM12P2,"</td></tr>"
.
DWLS9000  COMPARE   CPRCNUMB,COUNTA
          GOTO      DWLS4000 IF LESS
.
DWLS9999  RETURN
+
.**********************************************************************
.         SX - Display total
.**********************************************************************
SITOT000  MOVE      GROSSTOT,TOTAMNT
          ADD       TOTGST,TOTAMNT
          WRITE     HTMLFILE;"<tr bgcolor=#CCCCCC><td>&nbsp;</td>":
                             "<td><b>Invoice Total</b></td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td align=right><b>",FORM12P2,"</td>":
                             "<td align=right><b>",TOTGST,"</td>":
                             "<td align=right><b>",TOTAMNT,"</td></tr>"
SITOT999  RETURN
+
.**********************************************************************
.         Display End of invoice
.**********************************************************************
SENDI000  WRITE     HTMLFILE;"<tr><td>&nbsp;</td>":
                             "<td><b>Balance Payable</b></td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>";
          IF        NETTOT<0
            WRITE     HTMLFILE;"<td align=right><font color=#FF0000><b>":
                               NETTOT,"</font></b></td></tr>"
          ELSE
            WRITE     HTMLFILE;"<td align=right><font color=#0000FF><b>":
                               NETTOT,"</font></b></td></tr>"
          ENDIF
.
SENDI999  RETURN
+
.**********************************************************************
.         Credit Note
.**********************************************************************
SXCRD000  BRANCH    COMPFLG,SXCRD400,SXCRD300
          GOTO      SXCRD999
SXCRD300  MOVE      PMCNAMNT,FORM12P2   * Total credit
          SUB       PMCNGSTM,FORM12P2   * exclude GST
.
          WRITE     HTMLFILE;"<tr bgcolor=#CCCCCC><td>&nbsp;</td>":
                             "<td><b>Credit Note No.",PMCNCRNO,"</b></td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td align=right><b>",FORM12P2,"</td>":
                             "<td align=right><b>",PMCNGSTM,"</td>";
.
          IF        F1=0
            MATCH     "1",PTINCRST
            IF        @EQUAL
              SUB       PTINROUN,PMCNAMNT     * SX includes rounding
            ENDIF
          ENDIF
          WRITE     HTMLFILE;"<td align=right><b>":
                               PMCNAMNT,"</b></td></tr>"
.
          GOTO      SXCRD999
.
SXCRD400  MOVE      PMCNAMNT,FORM72
          MATCH     "1",PTCNPITM              * print long item desc?
          GOTO      SXCRD480 IF NOT EQUAL
.
          PRINT     *3,"CREDIT NOTE NO.",PMCNCRNO,*115,FORM72
          GOTO      SXCRD480
.
SXCRD460  PRINT     *3,"CREDIT NOTE NO.",PMCNCRNO,*70,FORM72
.
SXCRD480  ADD       ONE,COUNT
SXCRD999  RETURN
+
.******************************************************************************
.         Check if invoice has multiple Contract procedures
.******************************************************************************
MCPR0000  MOVE      ZERO,CPRCNUMB
.
          MOVE      "8",KEY1
          PACK      KEY23,PINVADM,PINVNO,KEY1,SP70
          CALL      RDSDTRN4
MCPR1000  CALL      RDKDTRN4
          BRANCH    OVRCD,MCPR9999
          MATCH     PINVADM,TADMNO
          GOTO      MCPR9999 IF NOT EQUAL
          MATCH     PINVNO,TREF                  * Check if Same Invoice
          GOTO      MCPR9999 IF NOT EQUAL
          COMPARE   EIGHT,TRECTYPE
          GOTO      MCPR9999 IF NOT EQUAL
.
          MATCH     SP70,PTDTCPRC
          GOTO      MCPR1000 IF EQUAL
.
.         Check if Contract Procedure code already exist in the array
.
          CALL      PARY0000
.
          GOTO      MCPR1000
.
MCPR9999  RETURN
+
.******************************************************************************
.         Check if this Contract Procedure code already exists in the array
.******************************************************************************
PARY0000  MOVE      ZERO,F1
          COMPARE   ZERO,CPRCNUMB
          GOTO      PARY8000 IF EQUAL
.
PARY1000  ADD       ONE,F1
          MATCH     CPRCCODE[F1],PTDTCPRC
          GOTO      PARY9999 IF EQUAL           * already exists
.
          COMPARE   CPRCNUMB,F1
          GOTO      PARY1000 IF LESS            * check next code in the array
.
.         Add the contract procedure code to the array.
.
PARY8000  ADD       ONE,F1
          MOVE      PTDTCNTR,CNTRCODE[F1]
          MOVE      PTDTCPRC,CPRCCODE[F1]
          ADD       ONE,CPRCNUMB
.
PARY9999  RETURN
+
.******************************************************************************
.         Get total contract amount
.******************************************************************************
FCNT0000  MOVE      ZERO,FORM12P2
          OPEN      PATDTRA4,"patdtraf"
          MOVE      "8",KEY1
          PACK      KEY23,PINVADM,PINVNO,KEY1,SP70
          CALL      RDSDTRN4
FCNT1000  CALL      RDKDTRN4
          BRANCH    OVRCD,FCNT9999
          MATCH     PINVADM,TADMNO
          GOTO      FCNT9999 IF NOT EQUAL
          MATCH     PINVNO,TREF                  * Check if Same Invoice
          GOTO      FCNT9999 IF NOT EQUAL
.
          MATCH     KEY9,PTDTCPRC
          GOTO      FCNT1000 IF NOT EQUAL
.
          MATCH     SP70,PTINHFND
          IF        @EQUAL
            MOVE      PTDTCNTR,PTINHFND
          ENDIF
.
          COMPARE   EIGHT,TRECTYPE
          GOTO      FCNT9999 IF NOT EQUAL * Not a procedure record
.
          ADD       PTDTLSPT,FORM12P2
          ADD       PTDTLSRB,FORM12P2
          ADD       PTDTADPT,FORM12P2
          GOTO      FCNT1000
.
FCNT9999  RETURN
+
.******************************************************************************
.         Calculate FFS
.******************************************************************************
GFFS0000  MOVE      ZERO,TFFSAMNT
          OPEN      NZPRDTA4,"nzprdtaf"
          OPEN      NZPSPRA1,"nzpspraf"
          PACK      KEY23,PINVADM,PINVNO,SP70
          CALL      RSNZRDT4
GFFS1000  CALL      RKNZRDT4
          BRANCH    OVRCD,GFFS9999
.
          MATCH     PINVADM,TADMNO
          GOTO      GFFS9999 IF NOT EQUAL
          MATCH     TREF,PINVNO
          GOTO      GFFS9999 IF NOT EQUAL
.
          MATCH     KEY9,PTDTCPRC
          GOTO      GFFS1000 IF NOT EQUAL
.
          BRANCH    TRECTYPE,GFFS1020:       * Accommodation record
                             GFFS1100:       * Theatre
                             GFFS1200        * Misc.Charge
          GOTO      GFFS1000
.
.         Accommodation for Primary procedure only
.
GFFS1020  MATCH     PTDTCPRC,PRIMMBS
          GOTO      GFFS1000 IF NOT EQUAL
.
          GOTO      GFFS2000
.
GFFS1100  PACK      KEY11,PINVADM,PTDTSUNQ,SP70
          CALL      RDNZSPR1
          BRANCH    OVRCD,GFFS1000
          GOTO      GFFS2000
.
GFFS1200  BRANCH    PTDTCCU,GFFS1000,GFFS1000
          COMPARE   TWO,TCLAIM
          GOTO      GFFS1000 IF EQUAL        * Not included in Win/Loss Calc.
.
          PACK      KEY11,PINVADM,PTDTSUNQ,SP70
          CALL      RDNZSPR1
          IF        OVRCD=0
            PACK      KEY5,ANSC,ANSL,NZSPCLMN
            CALL      RDCODE1
            IF        OVRCD=0
              CMATCH    "P",TCINDC7
              IF        @EQUAL
                MATCH     SP70,PTDTCNTR
                GOTO      GFFS1000 IF NOT EQUAL
                GOTO      GFFS2000
              ENDIF
            ENDIF
          ENDIF
.
GFFS2000  ADD       TPATAMTT,TFFSAMNT
          GOTO      GFFS1000
.
GFFS9999  RETURN
+
.******************************************************************************
.         Get primary funder
.******************************************************************************
GPRI0000  MOVE      ZERO,EXIT
          MOVE      SP70,PRIMFUND
          MOVE      SP70,PRIMMBS
.
          MOVE      PINVADM,KEY8
          OPEN      NZPSPRA2,"nzpspraf"
          PACK      KEY17 WITH PINVADM,SP70
          CALL      RSNZSPR2
GPRI1000  CALL      RKNZSPR2
          BRANCH    OVRCD OF GPRI9999
.
          MATCH     NZSPADMN,KEY8
          GOTO      GPRI9999 IF NOT EQUAL
.
          COMPARE   ONE,NZSPPIND
          GOTO      GPRI1000 IF NOT EQUAL        * Not performed
.
          COMPARE   ONE,NZSPPRIM
          GOTO      GPRI1000 IF NOT EQUAL        * Not a primary
.
          MATCH     SP70,NZSPCNTR
          GOTO      GPRI9999 IF EQUAL          * not a contract
.
          MOVE      NZSPCNTR,PRIMFUND
          MOVE      NZSPCPRC,PRIMMBS
.
GPRI9999  RETURN
+
.*******************************************************************************
.         Print Unreleased cash
.*******************************************************************************
SXUNR000  CALL      PAYE0000                  * Get payee details
          MOVE      RCBDPAYD,KEY50
          MOVE      RCBDPAYD,KEY39
.
          MATCH     "1",PTCNPITM              * print long item desc?
          GOTO      SXUNR400 IF NOT EQUAL
.
          PRINT     *3,"RECEIPT ##",RCDTRECN:
                    *30,"Payer:",KEY39,*116,FORM62
          GOTO      SXUNR999
.
SXUNR400  PRINT     *3,"RECEIPT ##",RCDTRECN:
                    *30,"Payer:",KEY39,*71,FORM62
SXUNR999  RETURN
+
.*******************************************************************************
.         Display Unreleased cash
.*******************************************************************************
DXUNR000  CALL      PAYE0000                  * Get payee details
          MOVE      RCBDPAYD,KEY50
          MOVE      SP70,DIM14
.
          MOVE      ZERO,F1
          MOVE      RCBDPTYP,F1
          MOVE      F1,F2
          CALL      CSHDS000                * Get payment type desc.
.
DXUNR100  MATCH     "1",RCCNSTRF
          GOTO      DXUNR150 IF NOT EQUAL
.
          MATCH     "4",RCBDPTYP
          GOTO      DXUNR150 IF NOT EQUAL
.
          STRIP     RCBDPAYD
          STRIP     RCBDSTRF
.
          WRITE     HTMLFILE;"<tr><td>Payment - ":
                             CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                             "<td><b>Receipt ##</b> ",RCDTRECN:
                             " <b>Payer:</b> ",KEY50:
                             " <b>Stmt Ref:</b> ",RCBDSTRF,"</td>";
.
          GOTO      DXUNR200
.
DXUNR150  WRITE     HTMLFILE;"<tr><td>Payment - ":
                             CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                             "<td><b>Receipt ##</b>",RCDTRECN:
                             " <b>Payer:</b> ",KEY50,"</td>";
.
DXUNR200  WRITE     HTMLFILE;"<td>&nbsp;",DIM14,"</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>":
                             "<td>&nbsp;</td>";
.
          IF        RCDTDISC<>0
            WRITE     HTMLFILE;"<td align=right><font color=#000080><b>":
                            RCDTAMNT,"</font></b></td></tr>";
          ELSE
            WRITE     HTMLFILE;"<td align=right>",RCDTAMNT,"</td></tr>"
          ENDIF
.
DXUNR999  RETURN
