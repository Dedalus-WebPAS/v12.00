+
.        AAECATBI :
.
.        This include produces the body of the next invoice for the current
.        Emergency patient. PROGFLAG is used to determine if the invoice is
.        to be displayed, printed, or just have the total calculated.
.
.        It refers to a number of subroutines that are not included in this
.        include. The main includes where these may be found (other than
.        the I/O includes) are :
.
.               AAEINVS  : Include that controls the printing of Invoices
.******************************************************************************
.* Mods     :                                                                 *
.----------------------------------------------------------------------------
. V12.00.01 14/05/2025  Alina Bhari    TSK 0955096                            *
.           Added alpanumeric visit number generation                         *
. V11.05.02 20/01/2025  J.Tan          TSK 0955356
.           Mod to set FININVN,FINADMN,FINURNO and FGSTFLAG
. V11.05.01 29/11/2024  Thanh T        TSK 0939466
.           Added Distance in kms field
.----------------------------------------------------------------------------
.*       V11.02.01 03/03/2022  J.Tan           TSK 0902652                    *
.*                 Mod for Patient Invoice new functionality (PTCNPPIN=1)     *
.*       V11.01.02 09/08/2021  J.Tan           TSK 0905305                    *
.*                 Mod for Emergency Care Classification 2021/2022 (ABFAECCL) *
.*       V11.01.01 18/02/2021  J.Tan           TSK 0888639                    *
.*                 Added PMMITMNO - Theatre Team no                           *
.*       V11.00.01 24/06/2020  J.Tan          TSK 0886178                     *
.*                 Mod to write to patinpaf - invoice pending details         *
.*       V10.15.01 29/11/2019  J.Tan           TSK 0857392                    *
.*                 Mod for IBAHMS58 - ABF Comparison report                   *
.*       V10.14.02 28/06/2019  J.Tan           TSK 0857392                    *
.*                 Mod for ABF invoice                                        *
.*       V10.14.01 20/03/2019  J.Tan            TSK 0857392                   *
.*                 Changed RCP Transaction count from DIM3 to DIM5            *
.*       V10.13.03 23/10/2018  Tracey Nguyen    TSK 0859743                   *
.*                 Added PMMIHOSI - Hopsital Indicator on PMSMTIFD            *
.*       V10.13.02 19/09/2018  J.Tan            TSK 0863727                   *
.*                 Added Restrictive Override code                            *
.*       V10.13.01 21/08/2017  J.Tan        TSK 0859742                       *
.*                 Added ECLIPSE new fields from pmsmtiaf and aaedtr          *
.*       V10.11.01 31/08/2017  J.Tan        TSK 0837479                       *
.*                 Added PMMICTYP - Consumption Type                          *
.*       V10.05.02 14/01/2015  J.Tan        CAR 310973                        *
.*                 Fixed Inv.date after Checking Previous inv.in EMROPOCK     *
.*       V10.05.01 04/08/2014  Peter Vela   CAR 302164                        *
.*                 Added PMMIRBRS - Rebate Reason CAT Rr                      *
.*                 Added ATDTRBRS - Rebate Reason CAT Rr                      *
.*       V10.04.01 17/01/2014  J.Tan        CAR 295008                        *
.*                 Mods to set PMMIDTCR,PMMITMCR,PMMIIDCR                     *
.*                 20/01/2014  J.Tan        CAR 249828                        *
.*                 Added PMMIPMBS - Patient MBS Team and Counter              *
.*       V10.03.04 30/07/2012  J.Tan        CAR 268318                        *
.*                 Mods to display Procedure rate and percentage              *
.*       V10.03.03 25/07/2012  J.Tan        CAR 266612                        *
.*                 Fixed variables for printing items quantity and rates      *
.*       V10.03.02 23/05/2012  J.Tan        CAR 260029                        *
.*                 Fixed Transaction number for DTR file                      *
.*       V10.03.01 26/04/2012  J.Tan        CAR 264231                        *
.*                 Fixed printing Invoice total                               *
.*       V10.02.01 13/10/2011  J.Tan        CAR 253138                        *
.*                 Fixed printing claim details twice                         *
.*       V10.01.02 14/12/2010  Mike Laming  CAR 233046                        *
.*                 Mods to Misc.Charges PATMCHFD - incl. new routine PATMCHRD *
.*       V10.01.01 30/11/2010 J.Tan         CAR 233043                        *
.*                 Mods to use maxline lines on body from parameter           *
.*       V10.00.04 08/10/2010 J.Tan         CAR 231497                        *
.*                 Fixed printing service doctor for HEC                      *
.*       V10.00.03 23/09/2010 J.Tan         CAR 230664                        *
.*                 Mods for HEC invoice layout - PTCNINVL=18                  *
.*       V10.00.02 18/08/2010  J.Tan        CAR 222031                        *
.*                 Mods to set PMMIDUPD and PMMITUPD                          *
.*       V10.00.01 29/04/2010  J.Tan        CAR 220887                        *
.*                 Checking for Active/Inactive of Misc.charge                *
.*       V9.12.02  13/07/2009  J.Tan        CAR 199759                        *
.*                 Mods displaying deposits and printing Pendlebury Invoices  *
.*       V9.12.01  27/05/2009  J.Tan        CAR 197652                        *
.*                 Mods to allocate item with no doctor to the first doctor   *
.*       V9.11.01  28/04/2009  Mike Laming  CAR 195368                        *
.*                 Add max line count for PTCNINVL=16 (Pendlebury) - 4 spots  *
.*       V9.10.04  22/08/2008  J.Tan        CAR 170219                        *
.*                 Mods to print item description for Cabrini layout          *
.*       V9.10.03  04/08/2008  J.Tan        CAR 175246                        *
.*                 Mods to allocate facility fee to first doctor (Cabrini)    *
.*       V9.10.02  21/05/2008  J.Tan        CAR 164036                        *
.*                 Mods for Cabrini invoice layout                            *
.*       V9.10.01  02/05/2008 J.Tan      CAR 167579                           *
.*                 Mods to Update the Emergency fees                          *
.*       V9.09.08  27/03/2008 J.Tan      CAR 161684                           *
.*                 Mods checking for 'Print Service Date/Time for Procedures  *
.*       V9.09.07  08/04/2008 J.Tan      CAR 165485                           *
.*                 Fixed I*C on patipenf file                                 *
.*       V9.09.06  04/04/2008 J.Tan      CAR 165485                           *
.*                 Mods to allow delete facility fee for Public Hospital      *
.*       V9.09.05  04/03/2008 J.Tan      CAR 157583                           *
.*                 Fixed Invoice Total with Out of Pocket displayed           *
.*       V9.09.04  31/10/2007 J.Tan      CAR 151803                           *
.*                 Mods for Emergency Triage fee                              *
.*                 Mods to display Out of Pocket                              *
.*       V9.09.03  02/07/2007 Peter Vela CAR 142173                           *
.*                 Added time into procedure item Emr Billing                 *
.*       V9.09.02  29/06/2007 Peter Vela CAR 142173                           *
.*                 Added doctor surname to PSJOG000                           *
.*       V9.09.01  07/05/2007 Peter Vela CAR 142173                           *
.*                 Initialised new pmsmtiaf variables                         *
.*                 07/06/2007 J.Tan      CAR 142173                           *
.*                 Mods for Emergency events and added GST                    *
.*       V9.08.06  07/05/2007 J.Tan  CAR 140282                               *
.*                 Mods to initialise batch no of aaedtref                    *
.*       V9.04.05  06/10/2005 J.Tan  CAR 78676                                *
.*                 Fixed printing RCH invoice                                 *
.*       V9.04.04  29/09/2005 J.Tan  CAR 77671                                *
.*                 Fixed print invoice amounts                                *
.*       V9.04.03  05/10/2004  J.Tan    CAR 53798                             *
.*                 Mods default charging claim code for multi hospital        *
.*       V9.04.02  09/09/2004 J.Tan  CAR 53226                                *
.*                 Fixed checking for "000000" pmsmtiaf trans number          *
.*       V9.04.01  20/08/2004 J.Tan  CAR 52835                                *
.*                 Initialise hospital ID for patfinsf                        *
.*       V9.03.10  13/08/2004 J.Tan  CAR 52540                                *
.*                 Fixed I*D on aaedtref (checking 2nd index)                 *
.*       V9.03.09  01/07/2004 J.Tan  CAR 51112                                *
.*                 Fixed displaying unrelease non banked deposit              *
.*       V9.03.08  18/06/2004 Ebon Clements   CAR 50833                       *
.*                 Fixed I * D on write to aaedtref in ADGSA300               *
.*       V9.03.07  11/06/2004 J.Tan  CAR 50035                                *
.*                 Mods to set mechanical vent var from pmsmtiaf              *
.*       V9.03.06  31/05/2004 J.Tan  CAR 50111                                *
.*                 Fixed printing RCH invoice                                 *
.*       V9.03.05  13/05/2004 J.Tan  CAR 49827                                *
.*                 Mods to display non-banked deposit                         *
.*       V9.03.04  03/02/2004 J.Tan  CAR 46269                                *
.*                 Mods for deleting items from next invoice screen.          *
.*       V9.03.03  11/12/2003 J.Tan   CAR 44093                               *
.*                  Removed patcash file                                      *
.*       V9.03.02  20/10/2003  J.Tan    CAR 44172                             *
.*                 Mods to read misc.theatre item file (pmsmitaf)             *
.*       V9.03.01  17/09/2003  Lina Vo        CAR 40497                       *
.*                 Changed index and read of patmchgf to include effective    *
.*                 date.                                                      *
.*       V9.02.07  08/04/2003  J.Tan
.*                 Mods for WEB billing
.*       V9.02.06  06/11/2002  Dean Cramer Defect 23687                       *
.*                 SVM format adjustment                                      *
.*       V9.02.05  01/07/2002  Dean Cramer                                    *
.*                 Add back blank lines after total removed in error (STVM)   *
.*       V9.02.04  21/06/2002  Dean Cramer                                    *
.*                 Mods for RCH format                                        *
.*       V9.02.03  04/06/2002  J.Tan                                          *
.*                 Mods to remove tail18                                      *
.*       V9.02.02  28/05/2002  J.Tan                                          *
.*                 Fixed I*D on aaedtref                                      *
.*       V9.02.01  22/05/2002  J.Tan                                          *
.*                 Mods for casepayment and added parameter not to print      *
.*                 claim details                                              *
.*       V9.02.00  06/05/2002  J.Tan                                          *
.*                 Mods for STV and RCH                                       *
.*       V5.08.03  28/06/2000  J.Tan                                          *
.*                 Recompiled for PATCALFN - patfigaf file                    *
.*            V5.08.02  18/05/2000  J.Habasque                                *
.*                      Fixed printing of dates for layout 6                  *
.*            V5.08.01  16/05/2000  J.Tan                                     *
.*                      Mods to print invoice on landscape                    *
.*            V5.07.01  13/07/1999  Nicole Harrington                         *
.*                      Added web flags for displays/keyins and added writes  * 
.*                      to HTMLFILE                                           *
.*            V5.07     06/10/98  Jim Stathopoulos                            *
.*                      507 Changes                                           *
.******************************************************************************
.*        V5.05.01  14/10/1997  Steve Armstrong    SRF 643393                 *
.*                  Removed invoice message for THC                           *
.**************************************************************************
.        V5.00  13/02/89 J.Tan
.               Reposition record after updating DTRO as Invoice no.changed
.        V5.01  14/04/89 K.Peak
.               Fixed $ amount display  SRF  100124                         
.        V5.02  10/05/89 K.Peak
.               Fixed minimum amount bug SRF 100489                         
.        V5.01.01  25/08/89 Graeme Williams
.                  Added GST
.        V5.01.02  23/03/92 J.Tan    SRF 113203
.                  Fixed printing invoice amount
.        V5.01.03  01/05/92 Graeme Williams        SRF 114418
.                  Corrected set up of FINDATE
.        V5.01.04  12/06/92 Graeme Williams        SRF 114865
.                  Changes for 9 character misc items
.        V5.01.05  04/08/1994  Rob Leonard  Quote 8064
.                  Changes in printing of body layout for Taranaki.
.        V5.02.01  28/02/1995  Paul Howells  SRF 134181
.                  Fixed phone number for Taranaki.                  
.        V5.02.02  02/03/1995  Greg Horvat       SRF 134196  Quote 8277
.                  Changed fees invoiced to print miscell. charges by claim
.                  code
.        V5.03.01  28/07/1995  Greg Horvat       SRF 610987
.                  Changed Port Mac's invoice to print the attending doctor &
.                  provider number in the body of the invoice
.
.        AAECATBI :
.        This include produces the body of the next invoice for the current
.        outpatient. PROGFLAG is used to determine if the invoice is to be
.        displayed, printed, or just have the total calculated.
.
.        It refers to a number of subroutines that are not included in this
.        include. The main includes where these may be found (other than
.        the I/O includes) are :
.
.               PATCALFN : Include to update the Fees Invoiced Statistics
.
.               PATINVS  : Include that controls the printing of Invoices
.
.               PATINVTn : Include that contains the code that is dependent
.                          on the invoice layout (eg. Heading and Tail of
.                          the invoice, any right-hand-side, etc).
.
.---------------------------------------------------------------------------
.
.        IF PROGFLAG = 1  THEN WORK OUT THE AMOUNT TO BE INVOICED
.
.        IF PROGFLAG = 2  THEN DISPLAY NEXT INVOICE
.
.        IF PROGFLAG = 3  THEN PRINT NEXT INVOICE
.
.        IF PROGFLAG = 4  THEN UPDATE PATFINS WITH AMOUNTS TO BE INVOICED
.
.        IPATFLAG=2 for Print Patient Invoice Using new Patient Inv.(PTCNPPIN=1)
.
.        PREBFLAG=1 for Print Rebate Invoice (PTCNPPIN=1)
.
.--------------------------------------------------------------------------
.
.         This is the main section of this include - it does the actual
.         work of calculating the body of the invoice
.
AAETBI    
.
ALINE000  MOVE      ZERO,GROSSTOT
          MOVE      ZERO,NETTOT
          MOVE      ZERO,TOTGST
          MOVE      ZERO,HFTHEA
          MOVE      ZERO,HFMISC
          MOVE      ONE,NUMINVS              * assume one invoice needed
          MOVE      ZERO,F3
          MOVE      ZERO,ABFFLAG
          MOVE      ZERO,PRINTABF
          MOVE      ZERO,PPAYABLE            * Patient Portion
          MOVE      ZERO,TOTPGST
          MOVE      ZERO,PATLNTOT
          MOVE      ZERO,PATLNGST
          MOVE      ZERO,PREBFLAG
          MOVE      ADANUMB,DADANUMB
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TEN;*240,PTCNINVN
          READ      CONTROLF,EIGHTY9;*205,EMCNDPOC
          READ      CONTROLF,HUND29;*89,PTCNPPIN
          MOVE      PTCNINVN,MAXLINE
.
          MATCH     "1",PTCNPPIN      * New Patient Invoice functionality
          IF        @EQUAL
            OPEN      EMRHTRA1,"emrhtraf"
            CALL      CPTI0000        * Check if we are printing Rebate Invoice
          ENDIF
.
          MOVE      SP70,PMVXMHOS
          OPEN      PMSVX1A1,"pmsvx1af"
          PACK      KEY8,ADANUMB
          CALL      RDPMVX11
.
.         Check if Using ABF
.
          MOVE      ZERO,PATFEE              * Total patient amount
          MOVE      ZERO,HFAMT               * Total rebate amount
.
          READ      CONTROLF,HUND28;*105,PTCNUABF
          MATCH     "1",PTCNUABF
          GOTO      ALINE020 IF NOT EQUAL
.
          PACK      KEY5,ANSC,ANSL,ADACOMP
          CALL      RDCODE1
          BRANCH    OVRCD,ALINE020
.
          MATCH     "A",TCINDC2              * ABF Funding ?
          GOTO      ALINE020 IF NOT EQUAL
.
          MOVE      ONE,ABFFLAG              * set ABF flag
.
.    ABF Patient-Display zero amount for Procedure record,but do not Print Proc.
.
.     PROGFLAG=1 (Calc.to be invoiced),PRABFINV=1 - ABF Charge
.                (Calc.to be invoiced),PRABFINV=2 - Item Charge only
.                (Calc.to be invoiced),PRABFINV=0 - ABF + Item chrgs
.
.     PROGFLAG=2 (Display Invoice) - ABF + Items charges + Procedures (zero amt)
.
.     PROGFLAG=3 (Print Invoice) - PRABFINV=0 for Item charges only(No Proc.)
.     PROGFLAG=3 (Print Invoice) - PRABFINV=1 for ABF charge
.                           (do not print procedure code and delete Procedure)
.     PROGFLAG=4 (Update FI) - ABF + Item charges
.
.     If we are not displaying invoice (progflag<>2), check for ABF charges flag
.
          IF        PROGFLAG=1 | PROGFLAG=3
            IF        PROGFLAG=1
              COMPARE   TWO,PRABFINV
              GOTO      ALINE080 IF EQUAL    * Calculate Item charges only
.
              IF        PRABFINV=1
                CALL      ABFI0000           * Check if ABF invoice raised
                MOVE      ONE,ABFFLAG        * set ABF flag back for non-disc.
                GOTO      ALINE640
              ENDIF
            ELSE
              COMPARE   ONE,PRABFINV
              GOTO      ALINE080 IF NOT EQUAL  * Print Item charges only
            ENDIF
          ENDIF
.
          CALL      ABFI0000                 * Check if ABF invoice raised
.
.         Restore invoice fields for PROGFLAG=3 - Print invoice
.
          IF        PROGFLAG=3
            MOVE      ADANUMB,PINVADM
            MOVE      ADAURNO,PINVUR
            MOVE      CNEXTINV,PINVNO
            PACK      PINVDTE,CCC,CYY,CMM,CDD
            REP       " 0",PINVDTE
            MOVE      ONE,PINVSYS
          ENDIF
.
          MOVE      ONE,ABFFLAG              * set ABF flag back for non-disc.
          GOTO      ALINE080
.
ALINE020  BRANCH   CHOSTYP,ALINE080          * Public hospital (STV)
.
          MOVE      ZERO,OPOCFLAG
          CALL      EMROPOCK                 * Recalc.Out of Pocket if necessary
          PACK      PINVDTE,CCC,CYY,CMM,CDD
          REP       " 0",PINVDTE
.
          READ      CONTROLF,THIRTY;*95,HESYST
.
          OPEN      EMRVISA1,"emrvisaf"
          MOVE      ADANUMB,KEY8
          CALL      RDEMVIS1
          IF        OVRCD=0
            CALL      UPDCH000               * Update facility fees
          ENDIF
.
.        Search for any items to be printed
.
.
ALINE080  MOVE      ZERO,KEYFLAG
          MATCH     "1",EMCNGINV
          GOTO      ALINE100 IF NOT EQUAL
.
          CALL      CFDOC000          * get the first Service doct.
          MOVE      KEY10,DIM10       * Save the first doctor
.
          MATCH     SP70,SERVDOCT
          GOTO      ALINE100 IF EQUAL
          MATCH     "ALL",SERVDOCT
          GOTO      ALINE100 IF EQUAL
.
.         KEYFLAG =1 for a selected doctor, KEYFLAG=2 for the first doctor
.         KEYFLAG =2 to include facility fees
.       
          MOVE      ONE,KEYFLAG       * breakup by service doctor
          MATCH     SP70,KEY10
          GOTO      ALINE100 IF EQUAL
          MATCH     KEY10,SERVDOCT
          IF        @EQUAL
            MOVE      TWO,KEYFLAG     * first doctor
          ENDIF
.
.         Printing Rebate invoice, get item details from emrhtraf file
.
ALINE100  IF        PREBFLAG=1
           CALL       ARINV0000
          ENDIF
.
          IF        KEYFLAG=1
            OPEN      PMSMTIA6,"pmsmtiaf"
            PACK      KEY24,ADANUMB,SERVDOCT,SP20
            CALL      RSPMMTI6
          ELSE
            PACK      KEY14,ADANUMB,SP20
            CALL      RSPMMTI1
          ENDIF
.
ALINE120  IF          KEYFLAG=1
            CALL        RKPMMTI6
          ELSE
            CALL        RKPMMTI1
          ENDIF
          BRANCH    OVRCD,ALINE640
.
          MOVE      PMMITRAN,SAVTRNNO
.
          MATCH     PMMIVISN,DADANUMB
          GOTO      ALINE640 IF NOT EQUAL
.
.          ABF invoice - display zero amount for Procedure record
.
          IF        ABFFLAG=1
            MATCH     "8",PMMIRTYP
            IF        !@EQUAL
              IF        PROGFLAG=1 | PROGFLAG=3
.
.      If we are printing ABF invoice, do not include Item record
.
                COMPARE   ONE,PRABFINV
                GOTO      ALINE120 IF EQUAL
              ENDIF
.
            ELSE
              MOVE      ZERO,PMMIAMTT
              MOVE      ZERO,PMMIAMTP
              MOVE      ZERO,PMMIRBAT
              MOVE      ZERO,ATPATAMT
              MOVE      ZERO,ATPATPOR
              MOVE      ZERO,ATREBPOR
              MOVE      ONE,PMMISERV
.
              IF        PROGFLAG=3 | PROGFLAG=4
.
.       If we are printing Normal invoice(ie Item only) do not include Procedure
.
                COMPARE   ZERO,PRABFINV
                GOTO      ALINE120 IF EQUAL
              ENDIF
.
            ENDIF
          ENDIF
.
.         Check if invoicing by Item group
.
          COMPARE   ZERO,KEYFLAG
          GOTO      ALINE202 IF EQUAL
.
.         If printing ALL invoice, facility will be included
.         If this is the first doctor, then include facility fees
.
          MATCH     SP70,PMMIITEM
          GOTO      ALINE200 IF EQUAL
.
.         If item has no doctor, allocate the item to first doctor
.
          MATCH     SP70,PMMIEDAD
          GOTO      ALINE201 IF NOT EQUAL
.
ALINE200  MOVE      DIM10,PMMIEDAD    * allocate facility fee to first doct.
.
ALINE201  IF         KEYFLAG=1
            MATCH     PMMIEDAD,SERVDOCT
            GOTO      ALINE640 IF NOT EQUAL
          ELSE
            IF        KEYFLAG=2
              MATCH     PMMIEDAD,SERVDOCT
              GOTO      ALINE120 IF NOT EQUAL
            ENDIF
          ENDIF
.
ALINE202  CALL      MVDE0000              * move pmsmtiaf to aaedtref file
          MOVE      ZERO,PATLNTOT
          MOVE      ZERO,PATLNGST
.
          MOVE      ATPATPOR,PATLNTOT
          IF        ATSERVS<>0
            MULT      ATSERVS,PATLNTOT
          ENDIF
.
.         Check if multiple invoices are required. This code will set NUMINVS
.         to "2" if this record requires two invoices, and will leave NUMINVS
.         unchanged otherwise.
.
          LOAD      NUMINVS,ATNINVS,NUMINVS,TWO
.
.         Print Patient Invoice :
.         if no of invoice = 1, skip item with rebate portion
.         if no of invoice = 2, skip item with zero patient portion
.
          IF        IPATFLAG=2
            IF        ATRECTYP=5
              IF        ATPATPOR<>0
                MOVE      TWO,ATNINVS   * set no of invoice to 2 for Procedure?
              ENDIF
            ENDIF
            LOAD      IPASS,ATNINVS,ZERO,ONE
            IF        ATNINVS=2
              COMPARE   ZERO,ATPATPOR
              GOTO      ALINE120 IF EQUAL
            ELSE
              COMPARE   ZERO,ATREBPOR
              GOTO      ALINE120 IF NOT EQUAL
            ENDIF
            GOTO      ALINE203
          ENDIF
.
.         Check what pass we are doing.
.                Pass 0 = Only one invoice -> all items will appear
.                Pass 1 = Patient portion only
.                Pass 2 = Rebate portion only
.
          MATCH     "1",EMCNDPOC
          GOTO      ALINE204 IF EQUAL        * Display out of pocket amt
.
ALINE203  
          BRANCH    IPASS,ALINE210,ALINE250
.
.         Process this miscellaneous item
.
ALINE204  MOVE      ATPATAMT,LINETOT
          ADD       LINETOT,GROSSTOT
.
          ADD       ATREBPOR,HFMISC
.
          MOVE      ATPATPOR,FORM12P2
          IF        PMMISERV<>0
            MULT      PMMISERV,FORM12P2
          ENDIF
          ADD       FORM12P2,PATFEE
.
          MOVE      ATREBPOR,FORM12P2
          IF        PMMISERV<>0
            MULT      PMMISERV,FORM12P2
          ENDIF
          ADD       FORM12P2,HFAMT
          GOTO      ALINE290
.
.         Patient portion only wanted
.
ALINE210  MATCH     "1",PTCNPPIN
          IF        !@EQUAL
            COMPARE   ZERO,ATPATPOR
            GOTO      ALINE120 IF EQUAL        * no patient portion -> ignore
          ENDIF
.
          MOVE      ATPATPOR,LINETOT
          ADD       LINETOT,GROSSTOT
.
          GOTO      ALINE290
.
.         Rebate portion only wanted.
.
ALINE250  COMPARE   ZERO,ATPATPOR
          GOTO      ALINE270 IF EQUAL        * no patient portion -> print
.
          COMPARE   ZERO,ATREBPOR
          GOTO      ALINE120 IF EQUAL          * no rebate portion -> ignore
.
ALINE270  MOVE      ATREBPOR,LINETOT
          ADD       LINETOT,GROSSTOT
          ADD       ATREBPOR,HFMISC
.
.         Process this item
.
ALINE290  MOVE      ZERO,LINEGST
          MOVE      ZERO,PATLNGST
.
          IF        IBCNUGST=2 & ATDTGSTA=1
            CALL      EGST0000               * Get Item GST rate
            MOVE      LINETOT,LINEGST
            MULT      IBGEAMNT,LINEGST       * GST amount
            DIV       "100.00",LINEGST       * Divide by 100 to get wanted fig.
            ADD       LINEGST,TOTGST
            ADD       LINEGST,PATFEE
.
            IF        PATLNTOT<>0
              MOVE      PATLNTOT,GSTWORK
              MULT      IBGEAMNT,GSTWORK      * Item GST Amount
              DIV       "100.00",GSTWORK
              MOVE      GSTWORK,PATLNGST
            ENDIF
          ENDIF
.
          IF        INVDFLAG=1
            MOVE      SP70,KEY2
            MOVE      PMMIITEM,KEY9
            MOVE      LINETOT,FORM12D2
            CALL      WAAE0000              * Write to invoice details file
          ENDIF
.
          ADD       PATLNTOT,PPAYABLE
          ADD       PATLNGST,PPAYABLE
          ADD       PATLNGST,TOTPGST
.
          BRANCH    PROGFLAG OF ALINE120,ALINE300,ALINE500,ALINE620
.
.        Display miscellaneous item
.
ALINE300  UNPACK    ATDDATE WITH XCENT,XYEAR,XMON,XDAY
          MOVE      XDAY,TFDAY
          MOVE      XMON,TFMONTH
          MOVE      XYEAR,TFYEAR
          MOVE      XCENT,TFCENT
          PACK      KEY10,TFDAY,SLASH,TFMONTH,SLASH,TFCENT,TFYEAR
          REP       " 0",KEY10
.
          MOVE     XMON,F2
          LOAD     MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG,NSEP:
                              NOCT,NNOV,NDEC
.
          MOVE     LINETOT,FORM12P2
          ADD      LINEGST,FORM12P2
.
          WRITE    HTMLFILE;"<tr>":
                   "<td>&nbsp",XDAY,SP1,MTHNAM3,SP1,XCENT,XYEAR,"</td>":
                   "<td><table width=100%><tr><td>":
                   "&nbsp",ATDDESC;
.
          MATCH     "8",PMMIRTYP
          IF        @EQUAL
.
            MATCH     "1",PTCNUESF        * Using Store and Forward?
            IF        @EQUAL
              WRITE     HTMLFILE;"</td><td align=right>":
                               "<img src=../images/MaintenanceIcon.gif ":
                               " class=ListIcon onclick='DispStoreFrwd(#"":
                               PMMIURNO,"#",#"":
                               PMMIVISN,"#",#"",PMMITRAN,"#")'>":
                               "<img src=../images/CommentIcon.gif ":
                               " class=ListIcon onclick='DispProc(#"":
                               PMMIURNO,"#",#"":
                               PMMIVISN,"#",#"",PMMITRAN,"#")'>";
              
            ELSE
              WRITE     HTMLFILE;"</td><td align=right>":
                               "<img src=../images/CommentIcon.gif ":
                               " class=ListIcon onclick='DispProc(#"":
                               PMMIURNO,"#",#"":
                               PMMIVISN,"#",#"",PMMITRAN,"#")'>";
            ENDIF
          ENDIF
.
          WRITE    HTMLFILE;"</td></tr></table></td>";
.
          ADD       ONE,F3
          MOVE      F3,DIM3
          REP       " 0",DIM3
.
          MATCH     "1",PTCNPPIN
          GOTO      ALINE320 IF NOT EQUAL
.
          COMPARE   TWO,IPATFLAG
          GOTO      ALINE320 IF EQUAL     * displaying patient invoice
.
.         We are displaying Rebate invoice, check if patient invoice has been
.         raised
.
          CALL      CPAI0000       * Check if Patient invoice raised
          COMPARE   ZERO,EXIT
          GOTO      ALINE320 IF EQUAL
.
.      Patient invoice has been raised for this item,disable Delete item option
.
          WRITE    HTMLFILE;"<td>":
                            "<input type=hidden id=srvpr",DIM3,">";
.
          WRITE    HTMLFILE;ATITEMNO,"</td>";
          MOVE     ZERO,EXIT
.
          GOTO     ALINE380
.
ALINE320  WRITE    HTMLFILE;"<td>":
                            "<input type=hidden id=srvpr",DIM3,">":
                            "<img src=../images/DeleteIcon.gif ";
.
          WRITE    HTMLFILE;" class=ListIcon onclick='DeleteItem(#"":
                     ADANUMB,"#",#"",PMMITRAN,"#")'>":
                     ATITEMNO,"</td>";
.
.
          COMPARE   TWO,IPATFLAG
          GOTO      ALINE400 IF EQUAL    * No out of Pocket for Pat.Inv.display
.
ALINE380  MATCH     "1",EMCNDPOC
          IF        !@EQUAL
            WRITE    HTMLFILE;"<td>&nbsp",ATRECEPT,"</td>";
          ENDIF
.
          WRITE    HTMLFILE;"<td align=right>&nbsp",LINETOT,"</td>":
                   "<td align=right>&nbsp",LINEGST,"</td>";
.
          WRITE    HTMLFILE;"<td align=right>&nbsp",FORM12P2,"</td>";
.
          MATCH     "1",EMCNDPOC
          IF        @EQUAL
            MOVE     PMMIRBAT,FORM12P2
            IF       PMMISERV<>0
              MULT     PMMISERV,FORM12P2
            ENDIF
            WRITE    HTMLFILE;"<td align=right>&nbsp",FORM12P2,"</td>";
.
            MOVE     PMMIAMTP,FORM12P2
            ADD      LINEGST,FORM12P2
            IF       PMMISERV<>0
              MULT     PMMISERV,FORM12P2
            ENDIF
            WRITE    HTMLFILE;"<td align=right>&nbsp",FORM12P2,"</td>";
          ENDIF
.
ALINE390  WRITE    HTMLFILE;"</tr>";
          GOTO     ALINE120
.
ALINE400  MOVE     PATLNTOT,FORM12P2
          ADD      PATLNGST,FORM12P2
.
          WRITE    HTMLFILE;"<td>&nbsp",ATRECEPT,"</td>";
.
          WRITE    HTMLFILE;"<td align=right>&nbsp",PATLNTOT,"</td>":
                   "<td align=right>&nbsp",PATLNGST,"</td>";
.
          WRITE    HTMLFILE;"<td align=right>&nbsp",FORM12P2,"</td>";
.
          WRITE    HTMLFILE;"</tr>";
          GOTO      ALINE120
.
.        Print miscellaneous item
.
ALINE500  MOVE      LINETOT,XLINETOT
          MOVE      LINEGST,XLINEGST
.
          MOVE     SP70,DIM20
          OPEN     PMSHCPA1,"pmshcpaf"
          MOVE     SP70,PMHCPRV1
          PACK     KEY10,ATDTEDAD,SP70
          CALL     RDPMHCP1
          IF       OVRCD<>1
            MOVE     PMHCTITL,FMTTITLE
            MOVE     PMHCGNAM,FMTGNAME
            MOVE     PMHCSNAM,FMTSNAME
            CALL     DOCNM000
          ENDIF
.
.         For ABF - Printing ABF invoice, delete Procedure record
.                 - Printing Normal invoice, do not include Procedure record
.
          IF        ABFFLAG=1
            MATCH     "8",PMMIRTYP
            IF        @EQUAL
              IF        PRABFINV=1
                MOVE      PMMITRAN,KEY6
                PACK      KEY14,ATNUMB,KEY6
                CALL      DEPMMTI1
                CALL      RSPMMTI1
              ENDIF
              GOTO      ALINE120
            ENDIF
          ENDIF
.
.        Do any miscellaneous processing
.
          MOVE      ATSERVS,NODAYS         * Quantity
          MOVE      ATPATPOR,LSTRATE
          IF        IPATFLAG<>2
            ADD       ATREBPOR,LSTRATE
          ENDIF
          MOVE      ATDDESC,DIM20 
          MOVE      ATDDATE,DFDATE
          MOVE      ATITEMNO,KEY9
          MOVE      TWO,RECFLAG
.
.         the following fields are used for printing (PRLN000)
.
          MOVE      ATITEMNO,PMMIITEM
          PACK      PMMIDESC,ATDDESC,SP70
          PACK      PMMIDES2,SP70,SP70
          MOVE      ATSERVS,PMMISERV
          MOVE      ATDTSVTM,PMMISVTM
          MOVE      ATSPARE1,PMMIINCT
          MOVE      "3",PMMIRTYP
          LOAD      PMMIRTYP,ATRECTYP,THREE,FIVE,SIX,FIVE,EIGHT,NINE
          MOVE      SP70,PMMIMVBR
.
          CALL      PRLN0000               * Print item
          ADD       LINEGST,LINETOT        * add GST back
.
.         Check if we need to update the number of visits that are charged
.
          IF        (AECNFTYP = 1) & (ATPATPOR <> 0.00)
.
.           The visit charge record will be the only one with a blank item no.
.
            MATCH     SP9,ATITEMNO
            IF        @EQUAL
              PROC      AEKWC000             * update the visits charged
            ENDIF
          ENDIF
.
.         If we are generating multiple invoices, and this item contains both
.         a patient and rebate portion, then we need to split into two items,
.         one for each portion
.
          MOVE      ATREBPOR,SAVHF     * save rebate portion
          BRANCH    IPASS,ALINE510
          GOTO      ALINE580
.
.         We have an item during the patient portion invoice. Check if it
.         has a rebate portion as well
.
ALINE510  COMPARE   ZERO,ATREBPOR
          GOTO      ALINE580 IF EQUAL
.
          COMPARE   TWO,IPATFLAG
          GOTO      ALINE570 IF EQUAL
.
.         Split this item into two records. Start by making a new item for
.         rebate portion
.
          MOVE      ZERO,ATPATPOR            * no patient portion on new item
          MOVE      ATREBPOR,ATPATAMT        * rebate portion is entire amount
.
          MOVE      ATREBPOR,PMMIRBAT        * rebate portion
          MOVE      ZERO,PMMIAMTP            * no patient portion on new item
          MOVE      ATREBPOR,PMMIAMTT        * rebate portion is entire amount
.
ALINE540  CALL      NXTTRAN1                 * generate a new transaction no.
          MOVE      ZERO,PMMITRAN
          MOVE      ATTRANS,PMMITRAN
          PACK      KEY14,ATNUMB,ATTRANS,SP70
          CALL      RAPMMTI1
          BRANCH    OVRCD,ALINE560
          GOTO      ALINE540
.
ALINE560  MOVE      "0",PMMIMVBR
          MOVE      SP70,PMMICNTR
          MOVE      SP70,PMMISUNQ
          MOVE      SP70,PMMIINCT
          MOVE      SP70,PMMISUBN
          MOVE      SP70,PMMIEDAD
          MOVE      SP70,PMMISVTM
          MOVE      SP70,PMMICCON
          MOVE      ZERO,PMMISCHF
          MOVE      SP70,PMMIITYP
.
          MOVE      SP70,PMMIDUPD
          MOVE      SP70,PMMITUPD
          MOVE      SP70,PMMIWUSR
          PACK      PMMIDTCR,CCC,CYY,CMM,CDD
          REP       " 0",PMMIDTCR
          CALL      IBACLOCK
          MOVE      CTIMEIS,PMMITMCR
          MOVE      PASSCODE,PMMIIDCR
          MOVE      SP70,PMMITMNO
          MOVE      SP70,PMMIPMBS
          MOVE      SP70,PMMICTYP
.
          PACK      PMMIACOI,SP70       * After Care Override Indicator
          PACK      PMMIDSOV,SP70       * Duplicate Service Override
          PACK      PMMISTXT,SP70       * Service Text
          PACK      PMMILSPN,SP70       * Location Specific Practice No(LSPN)
          PACK      PMMIMPOV,SP70       * Multi Procedure Override
          PACK      PMMINMPT,SP70       * Number of Patients Seen
          PACK      PMMISDCD,SP70       * Self Deem Code (Cat Sd)
          PACK      PMMITDUR,SP70       * Time Duration (Mins)
          PACK      PMMIROVR,SP70       * Restricted Override Code (Cat Ro)
          PACK      PMMIHOSI,SP70       * Hospital Indicator         *T0859743
.
          MOVE      SP70,PMMIDKSM
          PACK      PMMISPAR,SP70,SP70
          CALL      WRPMMTI1                 * post new item
.
          MOVE      SAVTRNNO,ATTRANS         * restore trans number
.
ALINE570  MOVE      ZERO,ATREBPOR            * no rebate portion on this item
          MOVE      ATPATPOR,ATPATAMT        * patient portion is entire amount
.
.        write record to DTR and remove it from pmsmtiaf
.
ALINE580  IF        IBCNUGST=2 & ATDTGSTA=1
            CALL      EGST0000               * Get Item GST rate
            MOVE      ATPATAMT,ATDTGSTM
            MULT      IBGEAMNT,ATDTGSTM      * Item GST Amount
            DIV       "100.00",ATDTGSTM
          ENDIF
.
          OPEN      AAEDTRE2,"aaedtref"
.
          MOVE      CNEXTINV TO ATINVNO
          MOVE      ONE TO ATINVPRT
          MOVE      PINVDTE,ATDTEFFD
          REP       " 0",ATDTEFFD
.
ALINE582  CALL      NXTTRAN1                 * generate a new transaction no.
.
          PACK      ATCHGDTE,CCC,CYY,CMM,CDD
          REP       " 0",ATCHGDTE
          PACK      KEY31,ATRECTYP,ATCHGDTE,ATNUMB,ATTRANS,ATINVNO
          CALL      RDADTRE2
          COMPARE   ZERO,OVRCD
          GOTO      ALINE582 IF EQUAL
.
          PACK      KEY22,ATNUMB,ATINVNO,ATTRANS
          CALL      RDADTRE1
          COMPARE   ZERO,OVRCD
          GOTO      ALINE582 IF EQUAL
.
          PACK      KEY22,ATNUMB,ATINVNO,ATTRANS
          CALL      WRDTRE1
.
          COMPARE   TWO,IPATFLAG
          GOTO      ALINE600 IF NOT EQUAL
.
         COMPARE    ZERO,SAVHF
         GOTO       ALINE600 IF EQUAL    * No rebate portion
.
.        If we are printing New Patient Invoice functionality, write the Rebate
.        portion to emrhtraf file (HF debtor transaction)
.
          CALL      CLEMRHTR
          CALL      MRHT0000            * Move DTR fields to emrhtraf
.
          MOVE      CNEXTINV,EMHTPINV    * Patient invoice number
          MOVE      ATTRANS,EMHTPTRN     * Transaction number
.
          MOVE      SP70,EMHTINVN
          MOVE      ATTRANS,EMHTTRNS
.
          MOVE      SAVHF,EMHTRPOR       * Restore rebate portion
          MOVE      ZERO,EMHTPPOR        * zero patient portion
          MOVE      SAVHF,EMHTAMTT       * Rebate portion is full amount
          MULT      EMHTSERV,EMHTAMTT
.
          MOVE      ZERO,EMHTGSTM
          IF        IBCNUGST=2
            CALL      EGST0000             * Get item GST rate
            MOVE      EMHTAMTT,EMHTGSTM
            MULT      IBGEAMNT,EMHTGSTM    * GST amount
            DIV       "100.00",EMHTGSTM    * Divide by 100 to get wanted fig.
          ENDIF
          GOTO      ALINE586
.
ALINE584  CALL      NXTTRAN1      * Get the next transaction number
          MOVE      ATTRANS,EMHTTRNS
.
ALINE586  PACK      KEY22,EMHTNUMB,EMHTINVN,EMHTTRNS
          CALL      RAEMHTR1
          COMPARE   ZERO,OVRCD
          GOTO      ALINE584 IF EQUAL
.
          CALL      WREMHTR1
.
ALINE600  PACK      KEY14,ATNUMB,SAVTRNNO
          CALL      DEPMMTI1
          CALL      RSPMMTI1
.
.
.        Update the Fees Invoiced Summary file
.
ALINE620  MOVE      SP3,ADACOMP
          PACK      KEY8,ATNUMB
          CALL      RDDETA1
          IF        OVRCD=1
            OPEN      PMSEVTA1,"pmsevtaf"
            CALL      RDPMEVT1
            IF        @EQUAL
              MOVE      PMEVCCOD,ADACOMP
              MOVE      PMEVACON,ADADOCT
            ENDIF
          ENDIF
.
          MOVE      ATPATAMT,FINAMT
          MOVE      ANSA,FINTYPE
.
          MOVE      ANSM,ANS
          IF        PTCNMFEE=1
            MATCH     ATMISGRP,SP3
            IF        @EQUAL
              PACK      FINCODE,ANSM,ADACOMP,ATITEMNO,SP20
.                               Misc. Charge, Claim Code, Item Code
            ELSE
.             MOVE      "-",DIM1
.             PACK      FINCODE,ANSM,ADACOMP,DIM1,ATMISGRP,SP20
              PACK      FINCODE,ANSM,ADACOMP,ATMISGRP,SP20
.                               Misc. Charge, Claim Code, Minus, Group Code
            ENDIF
          ELSE
            PACK      FINCODE,ANSM,ATMISGRP,ATITEMNO,SP20
.                             Misc. Charge, Group Code, Item Code
          ENDIF
.
          UNPACK    SP70,FININVN,FINADMN,FINURNO
          IF        PROGFLAG=3
            PACK      FININVN,CNEXTINV,SP70
            MOVE      ADANUMB,FINADMN
            MOVE      ADAURNO,FINURNO
          ENDIF
.
          MOVE      PINVDTE,FINDATE
          REP       " 0",FINDATE
          MOVE      ONE,FINSYS
          MOVE      SP6,FINSITE
          MOVE      ZERO,FGSTFLAG
          CALL      GHSP0000          * Get hospital ID
          CALL      PATCALF
.
          IF        IBCNUGST=2
            IF        LINEGST<>0
              MOVE      ONE,FGSTFLAG
              MOVE      PINVDTE,FINDATE
              REP       " 0",FINDATE
              MOVE      LINEGST,FINAMT
              CALL      PATCALF
              MOVE      ZERO,FGSTFLAG
            ENDIF
          ENDIF
.
          GOTO      ALINE120
.
.        End of this invoice's details
.
ALINE640  MOVE      GROSSTOT TO NETTOT
          IF        IBCNUGST=2
            ADD       TOTGST,NETTOT
          ENDIF
          MOVE      GROSSTOT,XGROSTOT
          MOVE      TOTGST,XTOTGST
.
.         Calculate the Goods & Services tax if needed
.
          COMPARE   ONE,IBCNUGST
          CALL      GTGSA000 IF EQUAL         * Calculate GST
.
          BRANCH    PROGFLAG OF ALINE900,ALINE740,ALINE700,ALINE800
.
.         Printing invoice. Check if GST needs to be added
.
ALINE700  COMPARE   ONE,IBCNUGST
          CALL      ADGSA000 IF EQUAL         * Add GST to invoice
.
          GOTO     ALINE900
.
.         Display invoice. Check if GST needs to be added
.
ALINE740  COMPARE   ONE,IBCNUGST
          CALL      DSGSA000 IF EQUAL         * Display GST on invoice
.
.         Check if there is room for the invoice total
.
          GOTO      ALINE900
.
.         Just updating the fee's invoiced file. Add GST if needed
.
ALINE800  COMPARE   ONE,IBCNUGST
          CALL      ADGSA800 IF EQUAL         * Add GST to fee's invoiced file
.
ALINE900   BRANCH    PROGFLAG OF ALINE990,ALINE920,ALINE980,ALINE990
.
.         Display invoice total and check if there was any cash received
.
ALINE920  MOVE      GROSSTOT,FORM12P2
          ADD       TOTGST,FORM12P2
          WRITE     HTMLFILE;"<tr>":
                              "<td>&nbsp</td>":
                              "<td>&nbsp</td>":
                             "<td>&nbsp</td>";
.
          COMPARE   TWO,IPATFLAG
          GOTO      ALINE960 IF EQUAL    * No out of Pocket for Pat.Inv.display
.
          MOVE      EMCNDPOC,DIM1
          IF        PREBFLAG=1
            MOVE      SP70,DIM1         * no out of pocket for print Rebate Inv.
          ENDIF
.
          MATCH     "1",DIM1
          IF        !@EQUAL
            WRITE     HTMLFILE;"<td>&nbsp</td>";
          ENDIF
.
          WRITE      HTMLFILE;"<td>&nbsp</td>":
                              "<td>&nbsp</td>":
                              "<td>&nbsp</td>";
.
          MATCH     "1",DIM1
          IF        @EQUAL
            WRITE     HTMLFILE;"<td>&nbsp;</td>":
                               "<td>&nbsp;</td>";
          ENDIF
.
          WRITE     HTMLFILE;"</tr>"
.
          WRITE     HTMLFILE;"<tr>":
                              "<td>&nbsp</td>":
                              "<td><b>&nbsp Invoice Total</b></td>":
                              "<td>&nbsp</td>";
.
          MATCH     "1",DIM1
          IF        !@EQUAL
            WRITE     HTMLFILE;"<td>&nbsp</td>";
          ENDIF
.
          WRITE     HTMLFILE;"<td align=right>&nbsp",GROSSTOT,"</td>":
                              "<td align=right>&nbsp",TOTGST,"</td>":
                              "<td align=right>&nbsp",FORM12P2,"</td>";
.
          MATCH     "1",DIM1
          IF        @EQUAL
            WRITE     HTMLFILE;"<td align=right>&nbsp",HFAMT,"</td>";
            IF        OPOCFLAG=1
              WRITE     HTMLFILE;"<td align=right><font color=#FF0000><b>":
                                 PATFEE,"</font></b></td>"
            ELSE
              WRITE     HTMLFILE;"<td align=right>":
                                 PATFEE,"</td>"
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"</tr>"
.
          SUB       CSAMT,NETTOT
          SUB       NBNKAMNT,NETTOT
          CALL      DSCH0000               * Display cash payment
.
.         Display balance payable
.
          WRITE     HTMLFILE;"<tr>":
                             "<td>&nbsp</td>":
                             "<td><b>&nbsp Balance Payable </b></td>":
                             "<td>&nbsp</td>";
          MATCH     "1",DIM1
          IF        !@EQUAL
            WRITE     HTMLFILE;"<td>&nbsp</td>";
          ENDIF
.
          WRITE     HTMLFILE;"<td>&nbsp</td>":
                             "<td>&nbsp</td>":
                             "<td align=right>&nbsp",NETTOT,"</td>";
.
          MATCH     "1",DIM1
          IF        @EQUAL
            WRITE     HTMLFILE;"<td>&nbsp;</td>":
                               "<td>&nbsp;</td>";
          ENDIF
.
          WRITE     HTMLFILE;"</tr>"
.
          WRITE     HTMLFILE;"<input type=hidden name=ppayable ":
                                "value='",PPAYABLE,"'>"
          GOTO      ALINE999
.
.         Display item for Patient invoice (IPATFLAG=2)
.
ALINE960  WRITE     HTMLFILE;"<td>&nbsp</td>";
.
          WRITE     HTMLFILE;"<td>&nbsp</td>":
                              "<td>&nbsp</td>":
                              "<td>&nbsp</td>";
.
          WRITE     HTMLFILE;"</tr>"
.
          WRITE     HTMLFILE;"<tr>":
                              "<td>&nbsp</td>":
                              "<td><b>&nbsp Invoice Total</b></td>":
                              "<td>&nbsp</td>";
.
          MOVE      PPAYABLE,FORM12P2
          SUB       TOTPGST,FORM12P2
.
          WRITE     HTMLFILE;"<td>&nbsp</td>";
          WRITE     HTMLFILE;"<td align=right>&nbsp",FORM12P2,"</td>":
                              "<td align=right>&nbsp",TOTPGST,"</td>":
                              "<td align=right>&nbsp",PPAYABLE,"</td>";
.
          WRITE     HTMLFILE;"</tr>"
.
          MOVE      PPAYABLE,NETTOT
.
          SUB       CSAMT,NETTOT
          SUB       NBNKAMNT,NETTOT
          CALL      DSCH0000               * Display cash payment
.
.         Display balance payable
.
          WRITE     HTMLFILE;"<tr>":
                             "<td>&nbsp</td>":
                             "<td><b>&nbsp Balance Payable </b></td>":
                             "<td>&nbsp</td>";
          WRITE     HTMLFILE;"<td>&nbsp</td>";
          WRITE     HTMLFILE;"<td>&nbsp</td>":
                             "<td>&nbsp</td>":
                             "<td align=right>&nbsp",NETTOT,"</td>";
.
          WRITE     HTMLFILE;"</tr>"
.
          WRITE     HTMLFILE;"<input type=hidden name=ppayable ":
                                "value='",PPAYABLE,"'>"
          RETURN
.
ALINE980  MOVE     XGROSTOT,FORM6P2
          ADD      XTOTGST,FORM6P2
.
          MOVE     "INVOICE TOTAL",IVTOTDES
          MOVE     THREE,RECFLAG
          CALL     PRLN0000                     * Print invoice total
.
.         Print the tail end of the invoice
.
          MOVE      XGROSTOT,INVCTOTL
          ADD       XTOTGST,INVCTOTL
          CALL      ENDPRT00
          GOTO      ALINE999
.
.         Add GST to invoice amount
.
ALINE990  COMPARE   ONE,IBCNUGST
          GOTO      ALINE999 IF NOT EQUAL
.
          ADD       GSTAMNT,NETTOT
          ADD       GSTAMNT,GROSSTOT
ALINE999  RETURN
+
.*******************************************************************************
.        Write to Invoice detail file
.*******************************************************************************
WAAE0000  CALL      CLPATINP              * clear patinpaf vars
.
          MOVE      KEY2,PTIPRTYP         * record type
          MATCH     SP70,PTIPRTYP
          GOTO      WAAE1000 IF NOT EQUAL * must be ABF Charge
.
          MATCH     SP70,PMMIMGRP
          IF        !@EQUAL
            MOVE      PMMIMGRP,PTIPBAND     * Item Group (Cat FI)
            PACK      KEY5,ANSF,ANSI,PTIPBAND
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TCINDC2,PTIPMHDP  * misc. charge group (Cat.FI, Ind.2)
            ENDIF
          ENDIF
          CALL      AMITM000             * misc. item record
.
          MATCH     "2",PMMIRTYP
          IF        @EQUAL
            MOVE      "2",PTIPTTYP       * Theatre
          ENDIF
          MATCH     "3",PMMIRTYP
          IF        @EQUAL
            MOVE      "3",PTIPTTYP       * Miscellaneous item
          ENDIF
          MATCH     "8",PMMIRTYP
          IF        @EQUAL
            MOVE      "5",PTIPTTYP       * Procedure
          ENDIF
          MOVE      PMMIRBAT,PTIPFREB    * estimated fund rebate
.
          GOTO      WAAE4000
.
WAAE1000  MOVE      ONE,PTIPTRNN         * ABF charge
          MOVE      "4",PTIPTTYP
          MOVE      FORM12D2,PTIPGREV
.
WAAE4000  MOVE      EMVIADMN,PTIPADMN       * admission number
          MOVE      EMVIURNO,PTIPURNO       * u/r number
          MOVE      EMVICOMP,PTIPCLAM       * claim type
          MOVE      EMVIFUND,PTIPHFND       * health fund
          MOVE      EMVITABL,PTIPFTBL       * health fund table
          MOVE      EMVIDATE,PTIPADAT       * visit Date
          MOVE      EMVIDDAT,PTIPDDAT       * Discharge Date (ccyymmdd)
          MOVE      EMVIDOCT,PTIPADOC       * Attending Doctor Code
          MOVE      EMVIREFD,PTIPRDOC       * Referring Doctor
          PACK      PTIPITEM,KEY9,SP70      * Item code
.
          CALL      IBACLOCK
          PACK      PTIPCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTIPCDAT           * date record created
          MOVE      CTIMEIS,PTIPCTIM
          REP       " 0",PTIPCTIM           * time record created
          PACK      PASSCODE,PASSCODE,SP70
          MATCH     SP70,PASSCODE
          IF        !@EQUAL
            MOVE      PASSCODE,PTIPOPER     * operator id
          ELSE
            MOVE      "CMDL",PTIPOPER
          ENDIF
          IF        UGSTFLAG=1
            MOVE      "U",PTIPGSTU          * Unknown GST
          ENDIF
.
          PACK      KEY16,PTIPADMN,PTIPRTYP,PTIPTRNN
          CALL      WRPTINP1
.
WAAE9999  RETURN
+
.******************************************************************************
.*                                  AMITM000                                  *
.*                      Set up Item fields to patinpaf                        *
.******************************************************************************
AMITM000  MATCH     ANSA,PTIPMHDP
          IF        @EQUAL
            PACK      PTIPRTYP,ANSA,SP3     * record type
            MOVE      FORM12D2,PTIPAREV     * accommodation revenue
            ADD       ONE,ACCCOUNT
            MOVE      ACCCOUNT,PTIPTRNN     * transaction number
            GOTO      AMITM999
          ENDIF
.
          MATCH     ANSI,PTIPMHDP
          IF        @EQUAL
            PACK      PTIPRTYP,ANSI,SP3     * record type
            MOVE      FORM12D2,PTIPIREV     * ICU/CCU revenue
            ADD       ONE,ICUCOUNT
            MOVE      ICUCOUNT,PTIPTRNN     * transaction number
            GOTO      AMITM999
          ENDIF
.
          MATCH     ANST,PTIPMHDP
          IF        @EQUAL
            PACK      PTIPRTYP,ANST,SP3     * record type
            MOVE      FORM12D2,PTIPTREV     * theatre revenue
            ADD       ONE,THTCOUNT
            MOVE      THTCOUNT,PTIPTRNN     * transaction number
            GOTO      AMITM999
          ENDIF
.
          MATCH     ANSP,PTIPMHDP
          IF        @EQUAL
            PACK      PTIPRTYP,ANSP,SP3     * record type
            MOVE      LINETOT,PTIPPREV      * prosthesis revenue
            ADD       ONE,PRSCOUNT
            MOVE      PRSCOUNT,PTIPTRNN     * transaction number
            GOTO      AMITM999
          ENDIF
.
          MATCH     ANSD,PTIPMHDP
          IF        @EQUAL
            PACK      PTIPRTYP,ANSD,SP3     * record type
            MOVE      LINETOT,PTIPHREV      * pharmacy revenue
            ADD       ONE,PHRCOUNT
            MOVE      PHRCOUNT,PTIPTRNN     * transaction number
            GOTO      AMITM999
          ENDIF
.
          MATCH     ANSL,PTIPMHDP
          IF        @EQUAL
            PACK      PTIPRTYP,ANSL,SP3     * record type
            MOVE      LINETOT,PTIPLREV      * labour ward revenue
            ADD       ONE,LBWCOUNT
            MOVE      LBWCOUNT,PTIPTRNN     * transaction number
            GOTO      AMITM999
          ENDIF
.
          PACK      PTIPRTYP,ANSM,SP3       * record type
          MOVE      LINETOT,PTIPMREV        * miscellaneous charges revenue
          ADD       ONE,MISCOUNT
          MOVE      MISCOUNT,PTIPTRNN       * transaction number
.
AMITM999  RETURN
+
.*******************************************************************************
.        Check for ABF Invoice raised
.*******************************************************************************
ABFI0000  MOVE      "0",EMCNDPOC             * ABF has NO Out of pocket
          MATCH     "IBAHMS58",PRGID
          GOTO      ABFI2000 IF EQUAL        * Calc.ABF charges for comparison
.
          OPEN      PATINVR3,"patinvrf"
          PACK      KEY16,ADANUMB,SP70
          CALL      RSPTINV3
ABFI1000  CALL      RKPTINV3
          BRANCH    OVRCD,ABFI2000
.
          MATCH     PINVADM,ADANUMB
          GOTO      ABFI2000 IF NOT EQUAL
.
          REP       " 0",PTINCRST
          MATCH     "0",PTINCRST
          GOTO      ABFI1000 IF NOT EQUAL  * Credit note
.
          COMPARE   THREE,PTINCMIX
          GOTO      ABFI9000 IF EQUAL      * found an ABF Invoice
          GOTO      ABFI1000

ABFI2000  PROC      ABFAECCL               * Emergency ABF Care Class
          PROC      ABFAEINV               * Emergency ABF Price weight
.
ABFI9000  MOVE      ZERO,EXIT
          GOTO      ABFI99999
.
ABFI9100  MOVE      ONE,EXIT
          GOTO      ABFI99999
.
ABFI9999  RETURN
+
.*******************************************************************************
.        Display Cash and Non-banked deposits
.*******************************************************************************
DSCH0000  OPEN      COMDEPA2,"comdepaf"
          OPEN      RCPDTEA1,"rcpdteaf"
          OPEN      RCPDTEA6,"rcpdteaf"
          OPEN      RCPBNKA1,"rcpbnkaf"
          MOVE      AADMNO,DAADMNO
          MOVE      "0",CMDESTAT
          PACK      KEY26,AADMNO,CMDESTAT,SP70
          CALL      RSCMDEP2
DSCH1000  CALL      RKCMDEP2
          BRANCH    OVRCD,DSCH2000
.
          MATCH     DAADMNO,CMDEADMN
          GOTO      DSCH2000 IF NOT EQUAL
          MATCH     "0",CMDESTAT
          GOTO      DSCH2000 IF NOT EQUAL
.
          PACK      KEY17,CMDERECN,CMDETCNT
          CALL      RDRCDTE1
          BRANCH    OVRCD,DSCH1000
.
          PACK      KEY12,RCDTRECN
          CALL      RDRCBNK1
          BRANCH    OVRCD,DSCH1000
.
          MOVE      SP70,DIM4
          UNPACK    CMDETDAT,CCENT,CYEAR,CMON,CDAY
          CALL      DSRE0000               * Display record
.
          GOTO      DSCH1000
.
.         Check for any non-banked deposits
.
DSCH2000  MOVE      ZERO,NBNKAMNT
          PACK      KEY25,AADMNO,SP70
          CALL      RSRCDTE6
DSCH3000  CALL      RKRCDTE6
          BRANCH    OVRCD,DSCH9999
.
          MATCH     DAADMNO,RCDTVIST
          GOTO      DSCH9999 IF NOT EQUAL
.
          MATCH     SP70,RCDTINVN          * Invoice number must be blank
          GOTO      DSCH3000 IF NOT EQUAL
          MATCH     SP70,RCDTRELD
          GOTO      DSCH3000 IF NOT EQUAL   * has been released
.
          PACK      KEY12,RCDTRECN
          CALL      RDRCBNK1
          BRANCH    OVRCD,DSCH3000
          MATCH     "1",RCBNSTAT            * check if cancelled
          GOTO      DSCH3000 IF EQUAL
.
          MOVE      " (*)",DIM4             * flag for non-banked deposit
          UNPACK    RCDTTDAT,CCENT,CYEAR,CMON,CDAY
          CALL      DSRE0000               * Display record
.
          GOTO      DSCH3000
.
DSCH9999  RETURN
+
.*******************************************************************************
.         Display Cash record
.*******************************************************************************
DSRE0000  MOVE      CMON,FORM2
          LOAD      MTHNAM3,FORM2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG:
                                  NSEP,NOCT,NNOV,NDEC
          PACK      KEY4,CCENT,CYEAR
          REP       " 0",KEY4
.
          WRITE     HTMLFILE;"<tr><td>Payment - ":
                             CDAY,SP1,MTHNAM3,SP1,KEY4,"</td>";
.
          MATCH     SP70,RCDTDPTY
          GOTO      DSRE2000 IF NOT EQUAL        * must be deposit
.
          MOVE      "P",KEY1
          MOVE      RCBNTTYP,F1
          LOAD      KEY1,F1,ANSH,ANSI,ANSG,ANSH
.
          MATCH     "H",KEY1
          IF        @EQUAL
            WRITE     HTMLFILE;"<td>FROM HEALTH FUND RECEIPT ##",RCDTRECN,DIM4:
                               "</td>";
          ELSE
            MATCH     "I",KEY1
            IF        @EQUAL
              WRITE     HTMLFILE;"<td>FROM INSURANCE RECEIPT ##",RCDTRECN,DIM4:
                                 "</td>";
            ELSE
              WRITE     HTMLFILE;"<td>FROM PATIENT RECEIPT ##",RCDTRECN,DIM4:
                                 "</td>";
            ENDIF
          ENDIF
          WRITE      HTMLFILE;"<td>&nbsp</td>";
.
          MATCH     "1",EMCNDPOC
          IF        !@EQUAL
            WRITE     HTMLFILE;"<td>&nbsp</td>";
          ENDIF
.
          WRITE      HTMLFILE;"<td>&nbsp</td>":
                              "<td>&nbsp</td>":
                              "<td align=right>",RCDTAMNT,"</td>"
.
          MATCH     "1",EMCNDPOC
          IF        @EQUAL
            WRITE     HTMLFILE;"<td>&nbsp;</td>":
                               "<td>&nbsp;</td>";
          ENDIF
.
          WRITE     HTMLFILE;"<tr>"
.
          GOTO      DSRE9999
.
DSRE2000  MOVE      SP70,KEY20
          PACK      KEY5,ANSD,ANSP,RCDTDPTY,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      TDESC,KEY20
          ENDIF
.
          WRITE     HTMLFILE;"<td>DEPOSIT - ",KEY20,"  ##",RCDTRECN,DIM4:
                             "</td>":
                             "<td>&nbsp</td>";
.
          MATCH     "1",EMCNDPOC
          IF        !@EQUAL
            WRITE     HTMLFILE;"<td>&nbsp</td>";
          ENDIF
.
          WRITE      HTMLFILE;"<td>&nbsp</td>":
                              "<td>&nbsp</td>":
                              "<td align=right>",RCDTAMNT,"</td>"
.
          MATCH     "1",EMCNDPOC
          IF        @EQUAL
            WRITE     HTMLFILE;"<td>&nbsp;</td>":
                               "<td>&nbsp;</td>";
          ENDIF
.
          WRITE     HTMLFILE;"<tr>"
.
DSRE9999  RETURN
+
. *****************************************************************************
.         Get the first doctor
. *****************************************************************************
CFDOC000  MOVE      SP70,KEY10
          COMPARE   NINE,PVITYPE
          GOTO      CFDOC800 IF EQUAL      * Emergency event
.
.         Emergency visit - check history file
.
          OPEN      EMRHISA1,"emrhisaf"
          PACK      KEY22,ADANUMB,SP70
          CALL      RDEMHIS1
CFDOC100  CALL      RKEMHIS1
          BRANCH    OVRCD,CFDOC9999
.
          MATCH     DADANUMB,EMHIVIS
          GOTO      CFDOC9999 IF NOT EQUAL
.
          MATCH     SP70,EMHIDOC
          GOTO      CFDOC100 IF EQUAL
.
          MOVE      EMHIDOC,KEY10
          GOTO      CFDOC999
.
.         Emergency Event
.
CFDOC800  OPEN      PMSEVTA1,"pmsevtaf"
          MOVE      ADANUMB,KEY8
          CALL      RDPMEVT1
          BRANCH    OVRCD,CFDOC999
.
          MOVE      PMEVACON,KEY10
CFDOC999  RETURN
+
.******************************************************************************
.* EGST0000 : Get percentage of Item GST                                      *
.******************************************************************************
EGST0000  MOVE     ZERO,IBGEAMNT            * Initialise GST percentage amount
          COMPARE  TWO,IBCNUGST
          GOTO     EGST9999 IF NOT EQUAL    * not using Aust.GST
.
          BRANCH   ATDTGSTA,EGST6000        * GST payable
          GOTO     EGST9000                 * GST free
.
EGST6000  OPEN     IBAGEDA1,"ibagedaf"
          MOVE     ZERO,IBGEAMNT             * zero GST percentage
          PACK     KEY8,CCC,CYY,CMM,CDD
          REP      " 0",KEY8
          PACK     KEY14,ATDTGSTC,KEY8
          CALL     RDIBGE1
          COMPARE  ZERO,OVRCD
          GOTO     EGST9000 IF EQUAL         * found GST amount for current date
.
          CALL     RPIBGE1                   * read previous record
          BRANCH   OVRCD,EGST8000
.
          MATCH    ATDTGSTC,IBGECODE         * Same code ?
          GOTO     EGST9000 IF EQUAL
.
EGST8000  MOVE     ZERO,IBGEAMNT
.
EGST9000  CLOSE    IBAGEDA1
EGST9999  RETURN
+
.******************************************************************************
.* GTGSA000 : Calculate the GST amount for the current invoice                *
.*                                                                            *
.* CGSTPERC is a percentage figure, such as 12.5 %. To use this figure, we    *
.* will need to divide by 100 at some point. The formula we are using is      *
.*           GROSSTOT * CGSTPERC / 100.00                                     *
.******************************************************************************
GTGSA000 MOVE      GROSSTOT,GSTWORK          * Move gross total to work var
         MULT      CGSTPERC,GSTWORK          * Multiply work var by percentage
         DIV       "100.00",GSTWORK          * Divide by 100 to get wanted fig.
         MOVE      GSTWORK,GSTAMNT           * Move work var to proper variable
GTGSA999 RETURN
.******************************************************************************
.* DSGSA000 : Display the GST amount on the screen, after a subtotal          *
.******************************************************************************
DSGSA000 COMPARE   ZERO,GSTAMNT              * Is there any GST to display ?
         GOTO      DSGSA999 IF EQUAL         * No. Don't do anything
.
.        Display the current invoice total as the sub-total
.
         WRITE     HTMLFILE;"<tr>":
                            "<td>&nbsp</td>":
                            "<td>&nbsp Sub-Total</td>":
                            "<td>&nbsp</td>";
.
         MATCH     "1",EMCNDPOC
         IF         !@EQUAL
           WRITE     HTMLFILE;"<td>&nbsp</td>";
         ENDIF
.
         WRITE     HTMLFILE;"<td>&nbsp</td>":
                            "<td>&nbsp</td>":
                            "<td align=right>&nbsp",GROSSTOT,"</td>";
.
         MATCH     "1",EMCNDPOC
         IF        @EQUAL
           WRITE     HTMLFILE;"<td>&nbsp;</td>":
                              "<td>&nbsp;</td>";
         ENDIF
.
         WRITE     HTMLFILE;"<tr>"
.
.        Get the description of the GST item, and display on the screen
.
         MOVE      "GOODS & SERVICES TAX          ",MDESC
         CALL      ACLM0000        * Check Hospital claim code charging
         PACK      KEY29,PTCNDCLM,SP6,SP3,CGSTITEM,CURRDATE,SP10
         CALL      ACGMC000                  * Get misc charge code
.
         MOVE      GSTAMNT,LINETOT           * Set up for display
         WRITE     HTMLFILE;"<tr>":
                            "<td>&nbsp ",CDATE,"</td>":
                            "<td>&nbsp ",MDESC,"</td>":
                            "<td>&nbsp ",CGSTITEM,"</td>";
.
         MATCH     "1",EMCNDPOC
         IF        !@EQUAL
           WRITE     HTMLFILE;"<td>&nbsp</td>";
         ENDIF
.
         WRITE     HTMLFILE;"<td>&nbsp</td>":
                            "<td>&nbsp</td>":
                            "<td align=right>&nbsp ",LINETOT,"</td>";
.
         MATCH     "1",EMCNDPOC
         IF        @EQUAL
           WRITE     HTMLFILE;"<td>&nbsp;</td>":
                              "<td>&nbsp;</td>";
         ENDIF
.
         WRITE     HTMLFILE;"<tr>"
.
.        Increment the invoice amount
.
         ADD       LINETOT,GROSSTOT
         ADD       LINETOT,NETTOT
.
DSGSA999 RETURN
.
.*******************************************************************************
.         Get the default charging claim code for multi hospital 
.*******************************************************************************
ACLM0000  READ      CONTROLF,ZERO;*43,IBCNMHOS
          IF        IBCNMHOS=1
            OPEN      PATHSPA1,"pathspaf"
            MOVE      PMVXMHOS,KEY3
            CALL      RDPTHSP1
            IF        OVRCD=0
              MOVE      PTHSDCLM,PTCNDCLM     * default claim code charging
            ENDIF
          ENDIF
ACLM9999  RETURN
.------------------------------------------------------------------------
.                   ACGMC000
.        Subroutine to get miscellaneous charge
.------------------------------------------------------------------------
ACGMC000  MOVE      ZERO,EXIT
          CALL      PATMCHRD                * read Misc.Charges file 
          COMPARE   ZERO,EXIT
          GOTO      ACGMC900 IF EQUAL
.
          GOTO      ACGMC990
.
ACGMC900  MOVE      ZERO,OVRCD
          GOTO      ACGMC999
.
ACGMC990  MOVE      ONE,OVRCD
ACGMC999  RETURN
+
.******************************************************************************
.* ADGSA000 : Print the GST amount on the invoice, and post to dtran file     *
.* ADGSA800 : Just post amount to the Fee's invoiced file                     *
.******************************************************************************
ADGSA000 COMPARE   ZERO,GSTAMNT              * Is there any GST to print ?
         GOTO      ADGSA999 IF EQUAL         * No. Don't do anything
....     CALL      ACGMC000                  * Get misc charge code
.
         IF        IBCNUGST=2 & PTCNINVB=1
           PRINT     *54,"SUB-TOTAL",*119,XGROSTOT;
         ELSE
           PRINT     *54,"SUB-TOTAL",*69,XGROSTOT;
         ENDIF
         CALL      PEOL0000
.
.        Get the description of the GST item, and print on the invoice
.
         MOVE      "GOODS & SERVICES TAX          ",MDESC
.
         PACK      KEY29,PTCNDCLM,SP6,SP3,CGSTITEM,CURRDATE,SP10
         CALL      ACGMC000                  * Get misc charge code
.
         MOVE      GSTAMNT,LINETOT           * Set up for print
         MOVE      LINETOT,XLINETOT
.
         IF        IBCNUGST=2 & PTCNINVB=1
           PRINT     *1,CDATE,*18,MDESC,*119,XLINETOT;
         ELSE
           PRINT     *1,CDATE,*18,MDESC,*69,XLINETOT;
         ENDIF
         CALL      PEOL0000      * Print the right hand side of the invoice
.
.        Post this GST item to the dtran file
.
ADGSA300 MOVE      ADANUMB,ATNUMB
         MOVE      CNEXTINV,ATINVNO
         MOVE      ADANUMB,KEY8
         CALL      TRVISA1
         MOVE      PVITRAN,ATTRANS
         MOVE      MDESC,ATDDESC
         MOVE      GSTAMNT,ATPATAMT
         PACK      ATDDATE,CCC,CYY,CMM,CDD
         REP       " 0",ATDDATE
         PACK      ATITEMNO,CGSTITEM,SP20
         MOVE      "DB",ATTYPE
         MOVE      ZERO,ATPAYTYP
         PACK      ATRECEPT,SP20
         MOVE      ONE,ATINVPRT
         MOVE      ONE,ATRECTYP
         MOVE      GSTAMNT,ATPATPOR
         MOVE      ZERO,ATREBPOR
         MOVE      SP3,ATMISGRP
         MOVE      SP3,ATDEPTYP
         MOVE      SP8,ATBATCHN
         MOVE      SP70,ATDTBTCH
         MOVE      SP70,ATDTSUBN
         MOVE      SP70,ATDTEDAD
         MOVE      SP70,ATDTSVTM
         MOVE      ZERO,ATDTSCHF
         MOVE      SP70,ATDTITYP
         MOVE      SP70,ATDTRBRS
         MOVE      ZERO,ATSPARE1
         MOVE      ZERO,ATSPARE2
         UNPACK    SP70,ATDTCRST,ATSPARE
         MOVE      SP70,ATDTBTCH
         PACK      ATDTEFFD,CCC,CYY,CMM,CDD
         REP       " 0",ATDTEFFD
         MOVE      SP70,ATDTPCOD
         PACK      ATDTACOI,SP70       * After Care Override Indicator
         PACK      ATDTDSOV,SP70       * Duplicate Service Override
         PACK      ATDTSTXT,SP70       * Service Text
         PACK      ATDTLSPN,SP70       * Location Specific Practice No(LSPN)
         PACK      ATDTMPOV,SP70       * Multi Procedure Override
         PACK      ATDTNMPT,SP70       * Number of Patients Seen
         PACK      ATDTSDCD,SP70       * Self Deem Code (Cat Sd)
         PACK      ATDTTDUR,SP70       * Time Duration (Mins)
         PACK      ATDTROVR,SP70       * Restricted Override Code (Cat Ro)
.
ADGSA400 OPEN      AAEDTRE2,"aaedtref"
         PACK      ATCHGDTE,CCC,CYY,CMM,CDD
         REP       " 0",ATCHGDTE
         PACK      KEY31,ATRECTYP,ATCHGDTE,ATNUMB,ATTRANS,ATINVNO
         CALL      RDADTRE2
         COMPARE   ZERO,OVRCD
         GOTO      ADGSA440 IF EQUAL
.
         PACK      KEY22,ATNUMB,ATINVNO,ATTRANS
         CALL      RDADTRE1
         BRANCH    OVRCD,ADGSA460
.
ADGSA440 MOVE      ADANUMB,KEY8
         CALL      TRVISA1
         MOVE      PVITRAN,ATTRANS
         GOTO      ADGSA400
.
ADGSA460 PACK      KEY22,ATNUMB,ATINVNO,ATTRANS
         CALL      WRDTRE1
.
.        Add GST to the invoice total
.
         ADD       GSTAMNT,GROSSTOT
         ADD       GSTAMNT,NETTOT
.
.        Update the Fee's invoiced file with GST
.
ADGSA800  MOVE      SP3,ADACOMP
          PACK      KEY8,ATNUMB
          CALL      RDDETA1
          IF        OVRCD=1
            OPEN      PMSEVTA1,"pmsevtaf"
            CALL      RDPMEVT1
            IF        @EQUAL
              MOVE      PMEVCCOD,ADACOMP
              MOVE      PMEVACON,ADADOCT
            ENDIF
          ENDIF
.
          MOVE      GSTAMNT,FINAMT
          MOVE      ANSA,FINTYPE
.
          IF        PTCNMFEE=1
            PACK      FINCODE,ANSM,ADACOMP,CGSTITEM,SP20
.                             Misc. Charge, Claim Code, Item Code
          ELSE
            PACK      FINCODE,ANSM,SP3,CGSTITEM,SP20
.                             Misc. Charge, Group Code, Item Code
          ENDIF
.
          UNPACK    SP70,FININVN,FINADMN,FINURNO
          IF        PROGFLAG=3
            PACK      FININVN,CNEXTINV,SP70
            MOVE      ADANUMB,FINADMN
            MOVE      ADAURNO,FINURNO
          ENDIF
.
         MOVE      PINVDTE,FINDATE   * Invoice date
         REP       " 0",FINDATE
         MOVE      ONE,FINSYS
         MOVE      SP6,FINSITE
         MOVE      ZERO,FGSTFLAG
         CALL      GHSP0000          * Get hospital ID
         CALL      PATCALF
.
ADGSA999 RETURN
.
. *****************************************************************************
.        Get hospital ID
. *****************************************************************************
GHSP0000 MOVE      SP70,FINHOSP
         IF        IBCNMHOS=1
           MOVE      PMVXMHOS,FINHOSP
         ENDIF
         OPEN      EMRVISA1,"emrvisaf"
         OPEN      EMRSITA1,"emrsitaf"
         MOVE      ATNUMB,KEY8
         CALL      RDEMVIS1
         IF        OVRCD=0
           MOVE      EMVISITE,KEY3
           CALL      RDEMSIT1
           IF        OVRCD=0
             MOVE      EMSTHSNO,FINHOSP
           ENDIF
         ENDIF
GHSP9999 RETURN
+
. *****************************************************************************
. * MVDE0000 : Move fields from pmsmitaf to aaedtref file                     *
. *****************************************************************************
MVDE0000  MOVE      PMMIVISN,ATNUMB
          MOVE      PMMIVISN,DATNUMB
          MOVE      "00000000",ATINVNO
          MOVE      PMMITRAN,ATTRANS
          MOVE      PMMIDESC,ATDDESC
          MOVE      PMMIAMTT,ATPATAMT
          MOVE      PMMITDAT,ATDDATE
          MOVE      PMMIITEM,ATITEMNO
          MOVE      "DB",ATTYPE
          MOVE      ZERO,ATPAYTYP
          MOVE      PMMIREFN,ATRECEPT
          MOVE      ZERO,ATINVPRT
          MOVE      ZERO,ATSPARE1
          MOVE      ZERO,ATSPARE2
          MOVE      ONE,ATRECTYP
          MATCH     "8",PMMIRTYP
          IF        @EQUAL
            MOVE      "5",ATRECTYP
            MOVE      PMMIINCT,ATSPARE1   * Print Date/Time procedure on Inv.
          ENDIF
          MOVE      PMMIMGRP,ATMISGRP
          MOVE      SP10,ATDEPTYP
          MOVE      PMMIBTCH,ATBATCHN
          MOVE      PMMIAMTP,ATPATPOR
          MOVE      PMMIRBAT,ATREBPOR
.
          MOVE      "1",ATNINVS
          IF        IPATFLAG=2
              MOVE      PMMIINVN,ATNINVS
          ENDIF
          MOVE      PMMISERV,ATSERVS
          MOVE      PMMIGSTA,ATDTGSTA
          MOVE      PMMIGSTC,ATDTGSTC
          MOVE      PMMIGSTM,ATDTGSTM
          UNPACK    SP70,ATDTEFFD,ATDTCRST,ATSPARE
          MOVE      SP70,ATDTBTCH
          MOVE      PMMISUBN,ATDTSUBN
          MOVE      PMMIEDAD,ATDTEDAD
          MOVE      PMMISVTM,ATDTSVTM
          MOVE      PMMISCHF,ATDTSCHF
          MOVE      PMMIITYP,ATDTITYP
          MOVE      PMMIRBRS,ATDTRBRS
          MOVE      PMMIACOI,ATDTACOI   * After Care Override Indicator
          MOVE      PMMIDSOV,ATDTDSOV   * Duplicate Service Override
          MOVE      PMMISTXT,ATDTSTXT   * Service Text
          MOVE      PMMILSPN,ATDTLSPN   * Location Specific Practice No(LSPN)
          MOVE      PMMIMPOV,ATDTMPOV   * Multi Procedure Override
          MOVE      PMMINMPT,ATDTNMPT   * Number of Patients Seen
          MOVE      PMMISDCD,ATDTSDCD   * Self Deem Code (Cat Sd)
          MOVE      PMMITDUR,ATDTTDUR   * Time Duration (Mins)
          MOVE      PMMIROVR,ATDTROVR   * Restrictive Override (Cat Ro)
.
MVDE9999  RETURN
+
. *****************************************************************************
. * MRHT0000 : Move DTR fields to HF debtor transaction (emrhtraf) file       *
. *****************************************************************************
MRHT0000  MOVE      DATNUMB,EMHTNUMB       * Admission Number
          MOVE      DATTRANS,EMHTTRNS      * Transaction Number
          MOVE      ATDDATE,EMHTDATE       * Date
          MOVE      ATITEMNO,EMHTITEM      * Item Number
          MOVE      ATDDESC,EMHTDESC       * Description
          MOVE      "1",EMHTINVP           * Patient Invoice printed(0=No,1=Yes)
          MOVE      DATRECTY,EMHTRTYP      * Record Type
          MOVE      ATSERVS,EMHTSERV       * Number of Services
          MOVE      ATMISGRP,EMHTMISG      * Misc.Group (Cat FI)
          MOVE      ATDTGSTA,EMHTGSTA      * Is GST applicable to this time ?
          MOVE      ATDTGSTC,EMHTGSTC      * GST Payable code
          MOVE      ATNINVS,EMHTNINV       * No of invoice to be printed

MRHT9999  RETURN
+
. *****************************************************************************
. * MHDT0000 : Move field from emrhtraf file to DTR for printing              *
. *****************************************************************************
MHDT0000  MOVE      EMHTNUMB,ATNUMB   *  Emergency Number
          MOVE      EMHTINVN,ATINVNO  *  Health Fund Invoice Number
          MOVE      EMHTTRNS,ATTRANS  *  Health Fund Transaction Number
          MOVE      EMHTDESC,ATDDESC  *  Description
          MOVE      EMHTAMTT,ATPATAMT *  Total Amount
          MOVE      EMHTDATE,ATDDATE  *  Date (ccyymmdd)
          MOVE      EMHTITEM,ATITEMNO *  Item Number
          MOVE      EMHTTTYP,ATTYPE   *  Transaction Type
          MOVE      EMHTPAYT,ATPAYTYP *  Payment Method
          MOVE      EMHTRECP,ATRECEPT *  Receipt Number
          MOVE      EMHTINVP,ATINVPRT *  Invoice Printed? (0=No,1=Yes)
          MOVE      EMHTRTYP,ATRECTYP *  Record Type
          MOVE      EMHTMISG,ATMISGRP *  Miscellaneous Group
          MOVE      EMHTDTYP,ATDEPTYP *  Deposit Type
          MOVE      EMHTBATC,ATBATCHN *  Batch No for Misc.Item
          MOVE      EMHTPPOR,ATPATPOR *  Patient Portion
          MOVE      EMHTRPOR,ATREBPOR *  Rebate Portion
          MOVE      EMHTNINV,ATNINVS  *  No of Invoices to be printed
          MOVE      EMHTSERV,ATSERVS  *  Number of services
          MOVE      EMHTEFFD,ATDTEFFD *  Date Item assigned to Fees Inv.
          MOVE      EMHTPDTM,ATSPARE1 *  Print Date/Time procedure on Inv.
          MOVE      EMHTGSTA,ATDTGSTA *  GST Applicable
          MOVE      EMHTGSTC,ATDTGSTC *  GST Payable Code
          MOVE      EMHTGSTM,ATDTGSTM *  GST Amount
          MOVE      EMHTCRST,ATDTCRST *  Credit Note Status
          MOVE      EMHTBTCH,ATDTBTCH *  Batch Details (NZ Priv Extract)
          MOVE      EMHTSUBN,ATDTSUBN *  Sub Item Number
          MOVE      EMHTEDAD,ATDTEDAD *  ED Attending Doctor
          MOVE      EMHTSVTM,ATDTSVTM *  Service Time (HH:MM:SS)
          MOVE      EMHTSCHF,ATDTSCHF *  Schedule Fee AMA=0, MBS=0.75, 0.85
          MOVE      EMHTITYP,ATDTITYP *  Item Type (0=MBS, 1=AMA)
          MOVE      EMHTRBRS,ATDTRBRS *  Rebate Reason (Cat Rr)
          MOVE      EMHTPCOD,ATDTPCOD *  Payment Code (Cat Py)
          MOVE      EMHTACOI,ATDTACOI *  After Care Overrided Indicator
          MOVE      EMHTDSOV,ATDTDSOV *  Duplicate Service Override
          MOVE      EMHTSTXT,ATDTSTXT *  Service Text
          MOVE      EMHTLSPN,ATDTLSPN *  Location Specific Practice No
          MOVE      EMHTMPOV,ATDTMPOV *  Multi Procedure Overrid 0=No 1=Yes
          MOVE      EMHTNMPT,ATDTNMPT *  Number of Patients Seen
          MOVE      EMHTSDCD,ATDTSDCD *  Deem Code (Cat Sd)
          MOVE      EMHTTDUR,ATDTTDUR *  Time Duration (Mins)
          MOVE      EMHTROVR,ATDTROVR *  Restrictive Override (Cat Ro)
.
MHDT9999  RETURN
+
. *****************************************************************************
. Check if Patient invoice has been raised (ie record exist in HF Debtors)
. *****************************************************************************
CPTI0000  MOVE      ZERO,PREBFLAG
          COMPARE   TWO,IPATFLAG
          GOTO      CPTI9999 IF EQUAL          * printing Patient invoice
.
.         Check if there is a rebate invoice to be printed
.
          PACK      KEY22 WITH DADANUMB,SP70
          CALL      RSEMHTR1
CPTI1000  CALL      RKEMHTR1
          BRANCH    OVRCD,CPTI9999
.
          MATCH     DADANUMB,EMHTNUMB
          GOTO      CPTI9999 IF NOT EQUAL
.
          MATCH     SP70,EMHTINVN              * Blank Rebate Invoice no?
          GOTO      CPTI9999 IF NOT EQUAL
.
.         Rebate invoice has not been raised, check if the patient invoice
.         has been credited
.
          OPEN      PATINVR1,"patinvrf"
          MOVE      EMHTPINV,KEY8
          CALL      RDINV1
          BRANCH    OVRCD,CPTI1000
.
          MATCH     "1",PTINCRST
          GOTO      CPTI1000 IF EQUAL     * fully credited
.
.         found a Patient invoice with Rebate invoice NOT raised yet
.
          MOVE      ONE,PREBFLAG          * flag for printing Rebate patient
.
CPTI9999  RETURN
+
. *****************************************************************************
.         Check if this item has the Patient invoice raised
. *****************************************************************************
CPAI0000  PACK      KEY22 WITH DATNUMB,SP70
          CALL      RSEMHTR1
CPAI1000  CALL      RKEMHTR1
          BRANCH    OVRCD,CPAI9000
.
          MATCH     DADANUMB,EMHTNUMB
          GOTO      CPAI9000 IF NOT EQUAL
.
          MATCH     SP70,EMHTINVN           * Blank HF invoice ?
          GOTO      CPAI9000 IF NOT EQUAL
.
          MATCH     EMHTITEM,PMMIITEM
          GOTO      CPAI1000 IF NOT EQUAL
.
          MATCH     EMHTTRNS,PMMITRAN
          GOTO      CPAI1000 IF NOT EQUAL
.
.         We have found the item that has blank health fund invoice
.
          MOVE      ONE,EXIT
          GOTO      CPAI9999
.
CPAI9000  MOVE      ZERO,EXIT
CPAI9999  RETURN
+
. *****************************************************************************
.         Printing rebate invoice
. *****************************************************************************
ARINV000  PACK      KEY22 WITH DADANUMB,SP70
ARINV100  CALL      RSEMHTR1
ARINV200  CALL      RKEMHTR1
          BRANCH    OVRCD,ARINV999
.
          MATCH     DADANUMB,EMHTNUMB
          GOTO      ARINV999 IF NOT EQUAL
.
          MATCH     SP70,EMHTINVN              * Blank Rebate Invoice no?
          GOTO      ARINV999 IF NOT EQUAL
.
.         Check if the Patient invoice has been credited
.
          MOVE      EMHTPINV,KEY8
          CALL      RDINV1
          BRANCH    OVRCD,ARINV200
.
          MATCH     "1",PTINCRST
          GOTO      ARINV200 IF EQUAL          * fully credited
.
          CALL      MHDT0000                * move emrhtraf to aaedtraf file
.
          MOVE      EMHTAMTT,LINETOT
          ADD       LINETOT,GROSSTOT
          ADD       EMHTAMTT,HFMISC
.
          MOVE      EMHTGSTM,LINEGST
          IF        IBCNUGST=2
            ADD       LINEGST,TOTGST
          ENDIF
.
          IF        INVDFLAG=1
            MOVE      SP70,KEY2
            MOVE      EMHTITEM,KEY9
            MOVE      LINETOT,FORM12D2
            CALL      WAIN0000              * Write to invoice details file
          ENDIF
.
          PACK      SKEY22,EMHTNUMB,EMHTINVN,EMHTTRNS,SP70
.
.         process Rebate invoice

          BRANCH    PROGFLAG OF ARINV200,ARINV300,ARINV500,ARINV620
.
.        Display miscellaneous item
.
ARINV300  UNPACK    ATDDATE WITH XCENT,XYEAR,XMON,XDAY
          MOVE      XDAY,TFDAY
          MOVE      XMON,TFMONTH
          MOVE      XYEAR,TFYEAR
          MOVE      XCENT,TFCENT
          PACK      KEY10,TFDAY,SLASH,TFMONTH,SLASH,TFCENT,TFYEAR
          REP       " 0",KEY10
.
          MOVE     XMON,F2
          LOAD     MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG,NSEP:
                              NOCT,NNOV,NDEC
.
          MOVE     LINETOT,FORM12P2
          ADD      LINEGST,FORM12P2
.
          WRITE    HTMLFILE;"<tr>":
                   "<td>&nbsp",XDAY,SP1,MTHNAM3,SP1,XCENT,XYEAR,"</td>":
                   "<td><table width=100%><tr><td>":
                   "&nbsp",ATDDESC;
.
          WRITE    HTMLFILE;"</td></tr></table></td>";
.
          ADD       ONE,F3
          MOVE      F3,DIM3
          REP       " 0",DIM3
.
          WRITE    HTMLFILE;"<td>":
                            "<input type=hidden id=srvpr",DIM3,">";
          WRITE    HTMLFILE;EMHTITEM,"</td>";
.
          WRITE    HTMLFILE;"<td>&nbsp",EMHTRECP,"</td>";
.
          WRITE    HTMLFILE;"<td align=right>&nbsp",LINETOT,"</td>":
                   "<td align=right>&nbsp",LINEGST,"</td>";
.
          WRITE    HTMLFILE;"<td align=right>&nbsp",FORM12P2,"</td>";
.
          WRITE    HTMLFILE;"</tr>";
          GOTO      ARINV200
.
.        Print miscellaneous item
.
ARINV500  MOVE      LINETOT,XLINETOT
          MOVE      LINEGST,XLINEGST
.
          MOVE     SP70,DIM20
          OPEN     PMSHCPA1,"pmshcpaf"
          MOVE     SP70,PMHCPRV1
          PACK     KEY10,ATDTEDAD,SP70
          CALL     RDPMHCP1
          IF       OVRCD<>1
            MOVE     PMHCTITL,FMTTITLE
            MOVE     PMHCGNAM,FMTGNAME
            MOVE     PMHCSNAM,FMTSNAME
            CALL     DOCNM000
          ENDIF
.
.        Do any miscellaneous processing
.
          MOVE      ATSERVS,NODAYS         * Quantity
          MOVE      ATREBPOR,LSTRATE
          MOVE      ATDDESC,DIM20
          MOVE      ATDDATE,DFDATE
          MOVE      ATITEMNO,KEY9
          MOVE      TWO,RECFLAG
.
.         the following fields are used for printing (PRLN000)
.
          MOVE      ATITEMNO,PMMIITEM
          PACK      PMMIDESC,ATDDESC,SP70
          PACK      PMMIDES2,SP70,SP70
          MOVE      ATSERVS,PMMISERV
          MOVE      ATDTSVTM,PMMISVTM
          MOVE      ATSPARE1,PMMIINCT
          MOVE      "3",PMMIRTYP
          LOAD      PMMIRTYP,ATRECTYP,THREE,FIVE,SIX,FIVE,EIGHT,NINE
          MOVE      SP70,PMMIMVBR
.
          CALL      PRLN0000               * Print item
          ADD       LINEGST,LINETOT        * add GST back
.
.        write record to DTR 
.
          IF        IBCNUGST=2 & ATDTGSTA=1
            CALL      EGST0000               * Get Item GST rate
            MOVE      ATPATAMT,ATDTGSTM
            MULT      IBGEAMNT,ATDTGSTM      * Item GST Amount
            DIV       "100.00",ATDTGSTM
          ENDIF
.
          OPEN      AAEDTRE2,"aaedtref"
.
          MOVE      CNEXTINV TO ATINVNO
          MOVE      ONE TO ATINVPRT
          MOVE      PINVDTE,ATDTEFFD
          REP       " 0",ATDTEFFD
.
ARINV582  CALL      NXTTRAN1                 * generate a new transaction no.
.
          PACK      ATCHGDTE,CCC,CYY,CMM,CDD
          REP       " 0",ATCHGDTE
          PACK      KEY31,ATRECTYP,ATCHGDTE,ATNUMB,ATTRANS,ATINVNO
          CALL      RDADTRE2
          COMPARE   ZERO,OVRCD
          GOTO      ARINV582 IF EQUAL
.
          PACK      KEY22,ATNUMB,ATINVNO,ATTRANS
          CALL      RDADTRE1
          COMPARE   ZERO,OVRCD
          GOTO      ARINV582 IF EQUAL
.
          PACK      KEY22,ATNUMB,ATINVNO,ATTRANS
          CALL      WRDTRE1
.
.         update invoice and transaction number on emrhtraf
.
          MOVE      ATTRANS,EMHTTRNS
          MOVE      CNEXTINV,EMHTINVN        * Update Rebate invoice number
          CALL      UPEMHTR1
.
.        Update the Fees Invoiced Summary file
.
ARINV620  MOVE      SP3,ADACOMP
          PACK      KEY8,ATNUMB
          CALL      RDDETA1
          IF        OVRCD=1
            OPEN      PMSEVTA1,"pmsevtaf"
            CALL      RDPMEVT1
            IF        @EQUAL
              MOVE      PMEVCCOD,ADACOMP
              MOVE      PMEVACON,ADADOCT
            ENDIF
          ENDIF
          MOVE      ATPATAMT,FINAMT
          MOVE      ANSA,FINTYPE
.
          MOVE      ANSM,ANS
          IF        PTCNMFEE=1
            MATCH     ATMISGRP,SP3
            IF        @EQUAL
              PACK      FINCODE,ANSM,ADACOMP,ATITEMNO,SP20
.                               Misc. Charge, Claim Code, Item Code
            ELSE
.             MOVE      "-",DIM1
.             PACK      FINCODE,ANSM,ADACOMP,DIM1,ATMISGRP,SP20
              PACK      FINCODE,ANSM,ADACOMP,ATMISGRP,SP20
.                               Misc. Charge, Claim Code, Minus, Group Code
            ENDIF
          ELSE
            PACK      FINCODE,ANSM,ATMISGRP,ATITEMNO,SP20
.                             Misc. Charge, Group Code, Item Code
          ENDIF
.
          UNPACK    SP70,FININVN,FINADMN,FINURNO
          IF        PROGFLAG=3
            PACK      FININVN,CNEXTINV,SP70
            MOVE      ADANUMB,FINADMN
            MOVE      ADAURNO,FINURNO
          ENDIF
.
          MOVE      PINVDTE,FINDATE
          REP       " 0",FINDATE
          MOVE      ONE,FINSYS
          MOVE      SP6,FINSITE
          MOVE      ZERO,FGSTFLAG
          CALL      GHSP0000          * Get hospital ID
          CALL      PATCALF
.
          IF        IBCNUGST=2
            IF        LINEGST<>0
              MOVE      ONE,FGSTFLAG
              MOVE      PINVDTE,FINDATE
              REP       " 0",FINDATE
              MOVE      LINEGST,FINAMT
              CALL      PATCALF
              MOVE      ZERO,FGSTFLAG
            ENDIF
          ENDIF
.
          IF        PROGFLAG=3
            MOVE      SKEY22,KEY22
            GOTO      ARINV100                 * printing rebate invoice
          ENDIF
          GOTO      ARINV200
.
ARINV9999 RETURN
+
. *****************************************************************************
.       Write to invoice details file
. *****************************************************************************
WAIN0000  CALL      CLPATINP              * clear patinpaf vars
.
          MATCH     SP70,EMHTMISG
          IF        !@EQUAL
            MOVE      EMHTMISG,PTIPBAND     * Item Group (Cat FI)
            PACK      KEY5,ANSF,ANSI,PTIPBAND
            CALL      RDCODE1
            IF        OVRCD<>1
              MOVE      TCINDC2,PTIPMHDP  * misc. charge group (Cat.FI, Ind.2)
            ENDIF
          ENDIF
          CALL      AMITM000             * misc. item record
.
          MATCH     "1",EMHTRTYP
          IF        @EQUAL
            MOVE      "3",PTIPTTYP       * Miscellaneous item
          ENDIF
          MATCH     "5",EMHTRTYP
          IF        @EQUAL
            MOVE      "5",PTIPTTYP       * Procedure
          ENDIF
          MOVE      EMHTRPOR,PTIPFREB    * estimated fund rebate
.
          MOVE      EMVIADMN,PTIPADMN       * admission number
          MOVE      EMVIURNO,PTIPURNO       * u/r number
          MOVE      EMVICOMP,PTIPCLAM       * claim type
          MOVE      EMVIFUND,PTIPHFND       * health fund
          MOVE      EMVITABL,PTIPFTBL       * health fund table
          MOVE      EMVIDATE,PTIPADAT       * visit Date
          MOVE      EMVIDDAT,PTIPDDAT       * Discharge Date (ccyymmdd)
          MOVE      EMVIDOCT,PTIPADOC       * Attending Doctor Code
          MOVE      EMVIREFD,PTIPRDOC       * Referring Doctor
          PACK      PTIPITEM,KEY9,SP70      * Item code
.
          CALL      IBACLOCK
          PACK      PTIPCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTIPCDAT           * date record created
          MOVE      CTIMEIS,PTIPCTIM
          REP       " 0",PTIPCTIM           * time record created
          PACK      PASSCODE,PASSCODE,SP70
          MATCH     SP70,PASSCODE
          IF        !@EQUAL
            MOVE      PASSCODE,PTIPOPER     * operator id
          ELSE
            MOVE      "CMDL",PTIPOPER
          ENDIF
          IF        UGSTFLAG=1
            MOVE      "U",PTIPGSTU          * Unknown GST
          ENDIF
.
          PACK      KEY16,PTIPADMN,PTIPRTYP,PTIPTRNN
          CALL      WRPTINP1
.
WAIN9999  RETURN
+
. *****************************************************************************
.
          INC       ABFAECCL
          INC       ABFAEINV
          INC       AAECHRG
          INC       EMROPOCK
          INC       CLEMRHTR              * Clear EMR HF debtors transaction
+
. *****************************************************************************
. * AEKWC000 : Update the number of days charged in the PATKWCFD file         *
. *****************************************************************************
.
          DEFPROC   AEKWC000
.
          INC       PATKWCFD/INC
          INC       PATVKCFD/INC
.
KWCFIL    INIT      "patkwc"
.
AEKWC000  CALL      CHKCD000                 * check the card number
          BRANCH    EXIT,AEKWC999            * no card, or invalid card
.
          CALL      OPNKW000                 * open the appropriate file
          BRANCH    EXIT,AEKWC999            * file not found
.
.         Check if we have a card number
.
          MATCH     SP20,PTVKCARD            * do we have a card number ?
          GOTO      AEKWC100 IF EQUAL        * no, treat as a special case
.
.         Key details if card number is known
.
          MOVE      PTVKCARD,PTKWCARD        * card number
          MOVE      SP8,PTKWURNO             * no U/R number
          GOTO      AEKWC300
.
.         Key details if card number is not known
.
AEKWC100  MOVE      SP20,PTKWCARD            * card number
          MOVE      ADAURNO,PTKWURNO         * U/R number
.
.         Read in the card file details if possible
.
AEKWC300  PACK      KEY28,PTKWCARD,PTKWURNO  * key for card details
          CALL      RDPTKWC1                 * get the card details
          BRANCH    OVRCD,AEKWC500           * no record, write a new one
.
          ADD       ONE,PTKWVISI             * no. of outpatient visits
.
          CALL      UPPTKWC1
          GOTO      AEKWC999
.
.         Write a new record to the kiwi card file
.
AEKWC500  MOVE      ONE,PTKWVISI             * no. of outpatient visits
          MOVE      ZERO,PTKWDAYS            * no. of inpatient days
.
          CALL      WRPTKWC1
.
AEKWC999  GOTO      ENDAEKWC
.
. *****************************************************************************
. * CHKCD000 : Validate card number.                                          *
. *****************************************************************************
.
CHKCD000  MOVE      ATNUMB,KEY8
          OPEN      PATVKCA1,"patvkcaf"
          CALL      RDPTVKC1
          CLOSE     PATVKCA1
          BRANCH    OVRCD,CHKCD900           * no card details
.
          MATCH     SP20,PTVKCARD            * no card number ?
          GOTO      CHKCD800 IF EQUAL        * no, we still want to post details
.
          CMATCH    ANSC,PTVKCARD            * community service card ?
          GOTO      CHKCD900 IF NOT EQUAL    * no, don't post to PATKWCFD
.
CHKCD800  MOVE      ZERO,EXIT
          GOTO      CHKCD999
.
.         Invalid card details
.
CHKCD900  MOVE      ONE,EXIT
.
CHKCD999  RETURN
+
. *****************************************************************************
. * OPNKW000 : Open the appropriate kiwicard file                             *
. *****************************************************************************
.
OPNKW000  UNPACK    ATDDATE,CCENT,CYEAR,KEY4
.
          MATCH     PTCNBCUT,KEY4
          IF        ! @LESS
            MOVE      CYEAR,FORM2            * year of kiwicard
          ELSE
            MOVE      CYEAR,FORM2
            SUB       ONE,FORM2              * calculate start of kiwicard year
            IF        FORM2 < 0
              MOVE     "99",FORM2            * allow for the year 2000
            ENDIF
          ENDIF
.
          PACK      CFNAME,KWCFIL,FORM2      * name of kiwicard file
          REP       " 0",CFNAME
.
          MOVE      ZERO,OVRCD               * assume file exists
          TRAP     OVERCOND IF IO            * trap for error openning file
          OPEN     PATKWCA1,CFNAME           * open kiwicard file
          TRAPCLR  IO
.
          BRANCH   OVRCD,OPNKW900            * open failed
.
          MOVE     ZERO,EXIT
          GOTO     OPNKW999
.
OPNKW900  MOVE     ONE,EXIT
.
OPNKW999  RETURN
+
.
          INC       PATKWCIO/INC
          INC       PATVKCIO/INC
          INC       IBAOVRIO/INC
.
ENDAEKWC  ENDPROC
