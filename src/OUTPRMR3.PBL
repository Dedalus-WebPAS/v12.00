.
.        OUTPRMR3  
.        --------
.
.        THIS INCLUDE PRINTS THE OUTPATIENTS MR-3 DETAILS
.
.        Requires : OUTBOK, PATCODES, PATMAS & PATRESP variables
.
.        MODIFICATIONS:  
.**********************************************************************
. V12.00.01 19/05/2025 Thanh T         TSK 0955096
.           Changed for alphanumeric visit number
.**********************************************************************
.         V11.00.02 02/04/2020  J.Tan             TSK 0868813
.                   Changed PKNAME to DIM80
.         V10.02.01 04/04/2011 Jill Parkinson CAR 239574
.                   Removed open of ibaprntf
.         V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2
.                   - Add Sex Description routine SEXDSC00 with Code "R" -
.                   "Intersex" added
.         V5.08.01  01/09/2000  Tonii                                         
.                   Mods to accept Unknown and Indeterminate as valid patient
.                   sex description                                         
.         V5.08.01  06/06/2000  Jill Habasque                                 
.                   Changed from key10 to key14 for the health fund file      
.            V5.07.00 16/10/1998  Jim Stathopoulos
.                     507 Changes
.**********************************************************************
.            V5.04.01 23/05/1997  Steve Armstrong   SRF 642902 
.                     Added open on IBAPRNT1 
.**********************************************************************
.                        V5.01 01/06/89 Graeme Williams
.                              Allow for codes being blank
.                              SRF 100860
.                        V5.02 09/06/89 J.Tan
.                              Allow for marital status being blank
.                              SRF 100995
.                        V5.03 03/07/89 DIG
.                              Changed to get source of referral from 
.                              OUTSITAF file
.                     V5.01.01 17/07/89 K.Peak
.                              Changed Adm & U/R to 8 characters
.
LINUPMR3 CALL      OPNPRINT
.
         PRINT     *F,*N,*N,*N,*N,*N,*N,*N:
                   *N,*9,"XXXXXXXXXXXXXXXXXXXX":           * Surname 
                      *30,"XXXX":                          * Title
                      *37,"XXXXXXXXXXXXXXXXXXXXXXXXX":     * Given Name
                      *66,"99999999*":                     * U/R Number
                   *N,*N,*9,"XXXXXXXXXXXXXXXXXXXXXXXXX":   * Address Line 1
                   *N,*9,"XXXXXXXXXXXXXXXXXXXXXXXXX":      * Address Line 2
                      *64,"XXXX",*70,"XXXXXXXXXX":         * Postcode & Phone
                   *N,*9,"XXXXXXXXXXXXXXX":                * Suburb
                   *N,*N,*13,"XXXX",*41,"X":               * Sex & HDP
                      *54,"XXXXXXXXXXXXXXXXXXXX":          * Country desc
                   *N,*13,"99",*33,"99/99/9999":           * Age & Birthdate
                      *54,"99999999999":                   * Pension Number
                   *N,*14,"XXXXXXXXXXXXXXXXXXXX",*41,"XX": * Religion & HDP
                   *N,*14,"XXXXXXXXXXXXXX":                * Occupation
                      *48,"XXXXXXXXXXXXXXXXXXXX",*77,"XX": * Marital Stat & HDP
                   *N,*N,*18,"XXXXXXXXXXXXXXXXXXXX":       * Next Of Kin Name
                      *53,"XXXXXXXXXX":                    * NOK Relationship
                      *68,"XXXXXXXXXXXX":                  * NOK Phone (Priv)
                   *N,*18,"XXXXXXXXXXXXXXXXXXXXXXXXX":     * NOK Address Line 1
                   *N,*18,"XXXXXXXXXXXXXXXXXXXXXXXXX":     * NOK Address Line 2
                      *69,"XXXX":                          * NOK Postcode
                   *N,*18,"XXXXXXXXXXXXXXX":               * NOK Suburb
                   *N,*N,*18,"XXXXXXXXXXXXXXXXXXXX":       * Res Name
                      *53,"XXXXXXXXXX":                    * Res Relationship
                      *68,"XXXXXXXXXXXX":                  * Res Phone (Priv)
                   *N,*18,"XXXXXXXXXXXXXXXXXXXXXXXXX":     * Res Address Line 1
                   *N,*18,"XXXXXXXXXXXXXXXXXXXXXXXXX":     * Res Address Line 2
                      *69,"XXXX":                          * Res Postcode
                   *N,*18,"XXXXXXXXXXXXXXX":               * Res Suburb
                   *N,*N,*18,"XXXXXXXXXXXXXXXXXXXX":       * Employer's Name
                      *68,"XXXXXXXXXXXX":                  * Emp Phone (Priv)
                   *N,*18,"XXXXXXXXXXXXXXXXXXXXXXXXX":     * Emp Address Line 1
                   *N,*18,"XXXXXXXXXXXXXXXXXXXXXXXXX":     * Emp Address Line 2
                      *69,"XXXX":                          * Emp Postcode
                   *N,*18,"XXXXXXXXXXXXXXX":               * Emp Suburb
                   *N,*N,*9,"XXXXXXXXXXXXXXXXXXXX":        * Insurance Status
                      *41,"XX":                            * Ins Stat HDP 
                      *45,"XXXXXXXXXXXXXXXXXXXXXXXXXXXX":  * Local Med Officer
                   *N,*45,"XXXXXXXXXXXXXXXXXXXXXXXXXXXX":  * LMO Address Line 1
                   *N,*9,"XXXXXXXXXXXXXXXXXXXXXXXX":       * Health Fund Name
                      *34,"XXXXXXXXXX":                    * Fund Membership No.
                      *45,"XXXXXXXXXXXXXXXXXXXXXXXXXXX":   * LMO Address Line 2
                   *N,*N,*9,"99999999999",*25,"XX/XX":     * Pen.No. & Exp Date
                   *N,*N,*9,"9999999999":                  * Medicare Number
                   *N,*N,*N,*75,"XX"                       * Source of Referral
.
        CALL       CLSPRINT
.
        RETURN
.
.
SETLMR3 MOVE      SP8,DESCSEX
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXDESC,DESCSEX
.* ******************************************   End of Changes         *C-49016
.
NOTMALX MOVE      PSEX,HDPSEX
        REP       DSEXREP,HDPSEX                                       *C-49016
.
.       Initialise descriptions for codes file entries
.
        MOVE      SP20,DESCNTRY
        MOVE      SP20,DESCRELG
        MOVE      SP20,DESCMSTA
        MOVE      SP20,DESCINSU
        MOVE      SP4,HDPRELG
        MOVE      SP4,HDPMSTA
        MOVE      SP4,HDPINSU
        MOVE      SP4,SRCR4
.
        MATCH     SP3,PCONT
        GOTO      SETMR3A IF EQUAL
.
        PACK      KEY5,CODEC,PCONT
        CALL      RDCODE1
        MOVE      TDESC,DESCNTRY
.
SETMR3A UNPACK    PBDATE INTO CCENT,CYEAR,CMON,CDAY
        CALL      PACDATE
        MOVE      CPCDATE,DESCBDAT
.
        MATCH     SP3,PREG
        GOTO      SETMR3B IF EQUAL
.
        PACK      KEY5 WITH CODER,PREG
        CALL      RDCODE1
        MOVE      TDESC,DESCRELG
        MOVE      THCSCOD TO HDPRELG
.
SETMR3B MATCH     SP3,PMSTAT
        GOTO      SETMR3C IF EQUAL
.
        PACK      KEY5 WITH CODEM,PMSTAT
        CALL      RDCODE1
        BRANCH    OVRCD OF SETMR3C
        MOVE      TDESC,DESCMSTA
        MOVE      THCSCOD TO HDPMSTA
.
SETMR3C MATCH     SP3,OBAINS
        GOTO      NOINS1 IF EQUAL
.
        PACK      KEY5,ANSU,ONE,OBAINS
        CALL      RDCODE1
        BRANCH    OVRCD TO NOINS1
        MOVE      TDESC,DESCINSU
        MOVE      THCSCOD TO HDPINSU
.
NOINS1  MATCH     SP3,OBASRCR
        GOTO      NOTWC IF EQUAL
.
        PACK      KEY5 WITH OSTCATG,OBASRCR
        CALL      RDCODE1
        BRANCH    OVRCD OF NOTWC
        MOVE      THCSCOD,SRCR4
.
NOTWC   MOVE      OBAURNO,PURNO
        PACK      KEY8 WITH PURNO
        CALL      RDMAST1
        BRANCH    OVRCD TO ERRPMI
.
        MOVE      SP30,HFNAME
        PACK      KEY14 WITH PFUNDH,PFNDTB,SP10
        CALL      RDFUND1
.
        MOVE      HFNAME TO DESCHFND   /* Move DIM 30 to DIM 25 */
.
        PACK      DESCPENN,SP5
        MATCH     SP4,PPNDTE
        GOTO      CHKCASE IF EQUAL
.
        UNPACK    PPNDTE INTO CCENT,CYEAR,CMON
        PACK      DESCPENN WITH CMON,SLASH,CYEAR
.
CHKCASE MOVE      SP1 TO DESCASEN
        LOAD      DESCASEN,PCASE,CCNOTES
.
        MOVE      OBAOUTNO,PKADMN
        MOVE      PKADMN,KEY8    
        CALL      RDRESP1
        BRANCH    OVRCD OF NORESP5
.
. If claim type is workcare, use person financially resp. in employer fields
.
        UNPACK    SP5,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
        PACK      KEY5,ANSC,ANSL,OBACOMP
        CALL      RDCODE1
        MOVE      FIVE TO FORM1
.
CHKLPXX LOAD      ANS USING FORM1 WITH TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
        CMATCH    ANSW TO ANS
        GOTO      WORKC IF EQUAL
        SUB       ONE FROM FORM1
        BRANCH    FORM1 TO CHKLPXX,CHKLPXX,CHKLPXX,CHKLPXX
        GOTO      NORESP5
.
WORKC   MOVE      PKNAME TO EMNAME
        MOVE      PKTELEP TO EMTELEP
        MOVE      PKADD1 TO EMADD1
        MOVE      PKADD2 TO EMADD2
        MOVE      PKPOST TO EMPOST
        MOVE      PKSUBR TO EMSUBR
.
        RETURN
.
NORESP5 MOVE      ZEROVISN,PKADMN
        PACK      PKNAME,SP70,SP10
        MOVE      SP30,PKRELP
        MOVE      SP20,PKTELEP
        MOVE      SP30,PKADD1
        MOVE      SP30,PKADD2
        MOVE      SP5,PKPOST
        MOVE      SP30,PKSUBR
        MOVE      SP30,EMNAME
        MOVE      SP20,EMTELEP
        MOVE      SP30,EMADD1
        MOVE      SP30,EMADD2
        MOVE      SP5,EMPOST
        MOVE      SP30,EMSUBR
        RETURN
.
PRTMR3  CALL      OPNPRINT
.
        UNPACK    PKNAME,KEY34
        PRINT     *N,*N,*N,*N,*N,*N,*N:
                  *N,*9,PSNAME,*30,PTITL,*37,PGNAME,*66,OBAURNO,DESCASEN:
                  *N,*N,*9,PADD1:
                  *N,*9,PADD2,*64,PPOST,*71,PTELEP:
                  *N,*9,PSUBRB:
                  *N,*N,*13,DESCSEX,*41,HDPSEX,*54,DESCNTRY:
                  *N,*13,PSAGE,*33,DESCBDAT,*54,PPENNO:
                  *N,*14,DESCRELG,*41,HDPRELG:
                  *N,*14,POCCUP,*48,DESCMSTA,*77,HDPMSTA:
                  *N,*N,*18,PNKNAME,*53,PNKRELP,*68,PNKTELP:
                  *N,*18,PNKADD1:
                  *N,*18,PNKADD2,*69,PNKPOST:
                  *N,*18,PNKSUBR:
                  *N,*N,*18,KEY34,*53,PKRELP,*68,PKTELEP:
                  *N,*N,*18,PKADD1:
                  *N,*18,PKADD2,*69,PKPOST:
                  *N,*18,PKSUBR:
                  *N,*18,EMNAME,*68,EMTELEP:
                  *N,*18,EMADD1:
                  *N,*18,EMADD2,*69,EMPOST:
                  *N,*18,EMSUBR:
                  *N,*N,*9,DESCINSU,*41,HDPINSU:
                  *N,*45,PLOCDOC:
                  *N,*9,DESCHFND,*34,PFNDMM:
                  *N,*N,*9,PPENNO,*25,DESCPENN:
                  *N,*N,*9,PMEDI,*N,*N,*N,*75,SRCR4               *was obasrcr
        CALL      CLSPRINT
        RETURN
.
.
ERRPMI  DISPLAY    *P1:24,*B,*EL,*V2LON,*BLINKON,"*** ERROR ***":
                   *HOFF,"  PMI record missing for this U/R : ",*V2LON:
                   OBAURNO,*HOFF,"  Please call IBA ... ";
        KEYIN      ANS;
        RETURN
