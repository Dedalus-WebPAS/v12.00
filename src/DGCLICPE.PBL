.************************************************************************
.*  Include  : DGCLICPE  -  Send Patient Pre-Admission Details          *
.*                                                                      *
.*  Author   : Steve Armstrong  (15/11/2001) Copied from DGCLICAD       *
.*                                                                      *
.*  Mods     :                                                          *
.*                                                                      *
.************************************************************************
.*  V11.04.01 09/01/2024 Thanh T           TSK 0936263                  *
.*            Added OPEN/CLOSE PMSQPXA1 index                           *
.************************************************************************
.*  V11.00.01 03/12/2020  Davin             TSK 0890602                 *
.*            Allow broadcast of A05 from HL7RECVR when ptcngosm=1      *
.************************************************************************
.*  V10.03.02 14/11/2013  Steve Armstrong  CAR 294040                   *
.*            Mods to only write to watqueaf for proprietary messages.  *
.*  V10.03.01 17/04/2013  Davin S          CAR 280425                   *
.*            Changed IBCNUCIS - value 1 or greater will write trigger  *
.************************************************************************
.*  V10.02.02 01/08/2011  Steve Armstrong   CAR 247602                  *
.*            Mods to not populate watqueaf for HL7 messages.           *
.*  V10.02.01 05/06/2011  Steve Armstrong   CAR 240692                  *
.*            Mods to populate H7CIRTAG & H7CISTAG                      *
.************************************************************************
.*  V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                  *
.*            Added setting of H7CITRID & H7CIINCL                      *
.************************************************************************
.*  V9.04.01  07/07/2005  Davin            CAR 64087                    *
.*            Mods to handle CIS interface.                             *
.*            Mods to write directly to holding tables instead of       *
.*            hl7bmtaf.                                                 *
.************************************************************************
.*  V9.02.01   28/11/2001  Davin                                        *
.*             Mods to use message holding file                         *
.************************************************************************
          DEFPROC   DGCLICPE
.
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
          INC       PMSQPTFD/INC
          INC       PMSQVIFD/INC
          INC       WATQUEFD/INC
.
CISMFLAG  FORM      1             * CIS message flag
.                                    0 = don't send HL7 CIS message
.                                    1 = send HL7 CIS message
IBAMFLAG  FORM      1             * IBA proprietary message flag
.                                    0 = don't send IBA proprietary message
.                                    1 = send IBA proprietary message
MESSAGID  DIM       20
.
DGCLI000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,HUND05;*164,PTCNDGPE
          READ      CONTROLF,HUND10;*108,PTCNA14M
          READ      CONTROLF,HUND28;*207,PTCNGOSM
.
          MOVE      ZERO,CISMFLAG                * set default interface flag
          MATCH     "1",IBCNUCIS                 * using CIS interface ?
          IF        !@LESS
            COMPARE   ONE,PTCNA14M               * yes - sending A14 ?
            IF        @EQUAL
              MOVE      ONE,CISMFLAG             * yes- set flag for CIS message
            ENDIF
          ENDIF
.
.         Check if we are sending any messages.
.
          IF        PTCNDGPE <> 1 & CISMFLAG <> 1
            GOTO      DGCLI999
          ENDIF
.
          MATCH     "HL7RECVR",PRGID             * trigger from receiver ?
          GOTO      DGCLI500 IF NOT EQUAL        * no - continue
.
          COMPARE   ONE,HL7TRGID                 * original A05 trigger ?
          GOTO      DGCLI500 IF EQUAL            * yes - continue
.
          MATCH     "1",PTCNGOSM                 * allow broadcast from SIU ?
          GOTO      DGCLI999 IF NOT EQUAL        * no - finished
.
.         We are sending IBA Proprietary and/or CIS HL7 messages, so
.         first open the relevant holding tables
.
DGCLI500  MOVE      ZERO,IBAMFLAG
          IF        PTCNDGPE = 1
            OPEN      HL7INB01,"hl7inbaf"
            MOVE      ONE,IBAMFLAG
          ENDIF
          IF        CISMFLAG = 1
            OPEN      HL7CISA1,"hl7cisaf"
          ENDIF
          OPEN      PMSQPTA1,"pmsqptaf"
          OPEN      PMSQPXA1,"pmsqpxaf"
          OPEN      PMSQVIA1,"pmsqviaf"
          OPEN      WATQUEA1,"watqueaf"
.
          CALL      DGMESSID                     * get the unique message id
.
.         Write a record to pmsqptaf (holding patient details)
.
          MOVE      MESSAGID,PMQPMESI
          MOVE      SP8,PMQPOURN
          PACK      PMQPSPAR,SP30,SP30,SP20
          CALL      WRPMQPT1
.
.         Save admission fields to transfer fields as no transfer record
.         exists for preadmissions.
.
          MOVE      PTMIXWRD,TWARD
          MOVE      PTMIEBED,TBED
          MOVE      ATYPE,TATYPE
          MOVE      ADOCTA,TADOCT
          MOVE      ACLSSFT,TDEPT
.
.         Write a record to pmsqviaf (holding visit details)
.
          MOVE      MESSAGID,PMQVMESI
          MOVE      ONE,PMQVTRAN
          MOVE      SP3,MHLRCODE
          PACK      PMQVSPAR,SP30,SP30,SP30
          CALL      WRPMQVI1
.
.         Write a record to watqueaf (holding W/L details) only where
.         proprietary messages are turned on
.
          IF        IBAMFLAG = 1
            MOVE      MESSAGID,WTQUMESI
            PACK      WTQUSPAR,SP30,SP30
            CALL      WRWTQUE1
          ENDIF
.
          COMPARE   ONE,CISMFLAG                 * sending CIS HL7 A14 message?
          IF        @EQUAL
            MOVE      "A14",H7CIMETP
            MOVE      HL7TRGID,H7CITRID
            MOVE      HL7INCLD,H7CIINCL
            MATCH     "HL7REGEN",PRGID
            IF        @EQUAL
              MOVE      HL7RCTAG,H7CIRTAG
              MOVE      HL7SFTAG,H7CISTAG
            ELSE
              MOVE      SP20,H7CIRTAG
              MOVE      SP3,H7CISTAG
            ENDIF
            CALL      WRCIS000                   * yes - write hl7cisaf record
          ENDIF
.
          COMPARE   ONE,PTCNDGPE                 * sending proprietary message?
          IF        @EQUAL
            MOVE      "CPE",H7INMETP
            CALL      WRINB000                   * yes - write hl7inbaf record
          ENDIF
.
          CLOSE     HL7INB01
          CLOSE     HL7CISA1
          CLOSE     PMSQPTA1
          CLOSE     PMSQPXA1
          CLOSE     PMSQVIA1
          CLOSE     WATQUEA1
.
DGCLI999  GOTO      DGCLIEND                     * end procedure
.
.         I/O Routines
.
          INC       BRHLCOMN
          INC       CLPMSQPX
.
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       IBAOVRIO/INC
          INC       PMSQPTIO/INC
          INC       PMSQVIIO/INC
          INC       WATQUEIO/INC
.
DGCLIEND  ENDPROC
+
