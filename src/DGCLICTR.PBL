.************************************************************************
.*  Include  : DGCLICTR  -  Send Patient Transfer Details               *
.*                                                                      *
.*  Author   : Steve Armstrong (17/04/1997)                             *
.*                                                                      *
.*  Mods     :                                                          *
.*                                                                      *
.*  V12.00.01 30/05/2025  Don Nguyen        TSK 0955096                 *
.*            Alphanumeric visit number changes around TADMN            *
.************************************************************************
.*  V11.04.01 09/01/2024 Thanh T           TSK 0936263                  *
.*            Added OPEN/CLOSE PMSQPXA1 index                           *
.************************************************************************
.*  V10.03.01 17/04/2013  Davin S          CAR 280425                   *
.*            Changed IBCNUCIS - value 1 or greater will write trigger  *
.*  V10.02.01 05/06/2011  Steve Armstrong   CAR 240692                  *
.*            Mods to populate H7CIRTAG & H7CISTAG                      *
.************************************************************************
.*  V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                  *
.*            Added setting of H7CITRID & H7CIINCL                      *
.************************************************************************
.*  V9.04.01  07/07/2005  Davin            CAR 64087                    *
.*            Mods to handle CIS interface.                             *
.*            Separated out CTR routine from dgclitrn routine.          *
.*            Mods to write directly to holding tables instead of       *
.*            hl7bmtaf.                                                 *
.************************************************************************
.*  V9.02.01  05/11/2001  Davin & MikeL                                 *
.*            Mods to use message holding file                          *
.*            Rename dgclitrn TO dgclictr                               *
.*  V5.       02/10/2000  Steve Downing                                 *
.*            Added doctor surname and given names                      *
.*            24/08/2000  Jill Habasque  SRF 646985                     *
.*            Changed definition of LRBEND from DIM 9 to DIM 3(dgclitrn)*
.*            22/12/1998  Davin                                         *
.*            Y2K mods                                                  *
.*            21/10/1998  Davin                                         *
.*            Mods for 507 changes                                      *
.*            13/10/1997  Steve Armstrong   WHL                         *
.*            Mods for new Master extension file variables              *
.*            (PTMXOPER - PTMXPOST).                                    *
.*            Also fixed birth date from ddmmyyyy to ddmmccyy.          *
.*                                                                      *
.************************************************************************
          DEFPROC   DGCLICTR
.
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
          INC       PMSQPTFD/INC
          INC       PMSQVIFD/INC
.
CISMFLAG  FORM      1             * CIS message flag
.                                    0 = don't send HL7 CIS message
.                                    1 = send HL7 CIS message
IBAMFLAG  FORM      1             * IBA proprietary message flag
.                                    0 = don't send IBA proprietary message
.                                    1 = send IBA proprietary message
MESSAGID  DIM       20
BEDSTRNG  DIM       3
WRDSTRNG  DIM       3
SVTRNKEY  DIM       30
.
DGCLI000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,SEVENTY9;*241,PTCNDGTR
          READ      CONTROLF,HUND10;*102,PTCNA02M
.
          MOVE      ZERO,CISMFLAG                * set default interface flag
          MATCH     "1",IBCNUCIS                 * using CIS interface ?
          IF        !@LESS
            COMPARE   ONE,PTCNA02M               * yes - sending A02 ?
            IF        @EQUAL
              MOVE      ONE,CISMFLAG             * yes- set flag for CIS message
            ENDIF
          ENDIF
.
.         Check if we are sending any messages.
.
          IF        PTCNDGTR <> 1 & CISMFLAG <> 1
            GOTO      DGCLI999
          ENDIF
.
.         We are sending IBA Proprietary and/or CIS HL7 messages, so
.         first open the relevant holding tables
.
          MOVE      ZERO,IBAMFLAG
          IF        PTCNDGTR = 1
            OPEN      HL7INB01,"hl7inbaf"
            MOVE      ONE,IBAMFLAG
          ENDIF
          IF        CISMFLAG = 1
            OPEN      HL7CISA1,"hl7cisaf"
          ENDIF
          OPEN      PMSQPTA1,"pmsqptaf"
          OPEN      PMSQPXA1,"pmsqpxaf"
          OPEN      PMSQVIA1,"pmsqviaf"
.
          CALL      DGMESSID                     * get the unique message id
.
.         Write a record to pmsqptaf (holding patient details)
.
          MOVE      MESSAGID,PMQPMESI
          MOVE      SP8,PMQPOURN
          PACK      PMQPSPAR,SP30,SP30,SP20
          CALL      WRPMQPT1
.
.         Write a record to pmsqviaf (holding visit details)
.
          MOVE      MESSAGID,PMQVMESI
          MOVE      ONE,PMQVTRAN
          MOVE      SP3,MHLRCODE
          PACK      PMQVSPAR,SP30,SP30,SP1
          CALL      WRPMQVI1
.
.>>>>
.         Write a pmsqviaf record with the last transfer record details.
.         (for "L" prefixed variables in final DataGate message).
.
          PACK      SVTRNKEY,TADMN,TDATE,TTIME,TWARD,TBED
          MOVE      TADMN,DIM8VISN
          CALL      GTPLN000                     * get previous location
          MOVE      WRDSTRNG,TWARD               * load ward/bed fields
          MOVE      BEDSTRNG,TBED
.
.         Write qvi-2 record with previous location
.
          MOVE      MESSAGID,PMQVMESI
          MOVE      TWO,PMQVTRAN
          MOVE      SP8,MHLRCODE
          PACK      PMQVSPAR,SP30,SP30,SP1
          CALL      WRPMQVI1
.
.         Re-read original transfer file record
.
          MOVE      SVTRNKEY,KEY30
          CALL      RDTRAN2                      * restore current tran record
          BRANCH    OVRCD,DGCLI500
.>>>>
.
DGCLI500  COMPARE   ONE,CISMFLAG                 * sending CIS HL7 A02 message?
          IF        @EQUAL
            MOVE      "A02",H7CIMETP
            MOVE      HL7TRGID,H7CITRID
            MOVE      HL7INCLD,H7CIINCL
            MATCH     "HL7REGEN",PRGID
            IF        @EQUAL
              MOVE      HL7RCTAG,H7CIRTAG
              MOVE      HL7SFTAG,H7CISTAG
            ELSE
              MOVE      SP20,H7CIRTAG
              MOVE      SP3,H7CISTAG
            ENDIF
            CALL      WRCIS000                   * yes - write hl7cisaf record
          ENDIF
.
          COMPARE   ONE,PTCNDGTR                 * sending proprietary message?
          IF        @EQUAL
            MOVE      "CTR",H7INMETP
            CALL      WRINB000                   * yes - write hl7inbaf record
          ENDIF
.
          CLOSE     HL7INB01
          CLOSE     HL7CISA1
          CLOSE     PMSQPTA1
          CLOSE     PMSQPXA1
          CLOSE     PMSQVIA1
.
DGCLI999  GOTO      DGCLIEND                     * end procedure
.
.*****************************************************************************
.*                         GTPLN000                                          *
.*             Get the previous location                                     *
.* Returns:  WRDSTRNG - previous ward code.                                  *
.*           BEDSTRNG - previous bed code.                                   *
.*****************************************************************************
.
GTPLN000  MOVE      TWARD,WRDSTRNG               * initialise ward/bed strings
          MOVE      TBED,BEDSTRNG
.
          PACK      KEY30,DIM8VISN,TDATE,TTIME,TWARD,TBED
          CALL      RDSTRAN2                     * current tran record found ?
GTPLN500  CALL      RDPTRAN2                     * previous tran record found ?
          BRANCH    OVRCD,GTPLN999               * no - finish
.
          MATCH     DIM8VISN,TADMN               * same admission still ?
          GOTO      GTPLN999 IF NOT EQUAL        * no - finish
.
.         Previous tran record found, so check if the ward/bed has changed
.
          MATCH     WRDSTRNG,TWARD               * same ward ?
          IF        @EQUAL
            MATCH     BEDSTRNG,TBED              * yes - same bed ?
            GOTO      GTPLN500 IF EQUAL          * yes - get previous record
          ENDIF
.
          MOVE      TWARD,WRDSTRNG               * load ward/bed fields
          MOVE      TBED,BEDSTRNG
.
GTPLN999  RETURN
+
.         I/O Routines
.
          INC       BRHLCOMN
          INC       CLPMSQPX
.
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       IBAOVRIO/INC
          INC       PMSQPTIO/INC
          INC       PMSQVIIO/INC
.
DGCLIEND  ENDPROC
+
