.******************************************************************************
.* Include Name  : PATCKEXC.PBL                                               *
.* Description   : Check if an item is an exclusion list item                 *
.* Author        : Greg Horvat                                                *
.******************************************************************************
.* Requires      : A valid item file record (patitemn)                        *
.* Returns       : Exit, 0 - Ok to continue                                   *
.*                       1 - Keyin the item again                             *
.* Modifications :                                                            *
.*        V6.09.01  20/02/2002  Steve Downing                                 *
.*                  Modified to not perform check in IBAADT39                 *
.*                                                                            *
.******************************************************************************
+
PATCKEXC
CHCKEXCL  MATCH     "IBAADT39",PRGID
          GOTO      CHCKEX90 IF EQUAL      * Not used in IBAADT39
          
          COMPARE   ONE,PTITEXCL
          GOTO      CHCKEX90 IF NOT EQUAL   * Not an exclusion list item
.
          DISPLAY   *P1:24,*EL,*B,"Warning: This is an Exclusion List Item. ":
                    "Ok to Continue (",*V2LON,"Y",*HOFF,"/",*V2LON,"N":
                    *HOFF,") ? ";
CHCKEX10  KEYIN     *P65:24,*V2LON,ANS;
          REP       UPPLOW,ANS
.
          MATCH     ANSY,ANS
          GOTO      CHCKEX90 IF EQUAL       * Ok to continue
.
          MATCH     ANSN,ANS
          GOTO      CHCKEX95 IF EQUAL       * Keyin the item again
.
          BEEP
          GOTO      CHCKEX10
.
CHCKEX90  MOVE      ZERO,EXIT               * Ok to continue
          GOTO      CHCKEX99
.
CHCKEX95  MOVE      ONE,EXIT                * Keyin the item again
.
CHCKEX99  DISPLAY   *P1:24,*EL;
          RETURN
