.******************************************************************************
.* System         : Clinical Audits                                           *
.*                                                                            *
.* Include Name   : CLASITDS.PBL                                              *
.*                                                                            *
.* Function       : Display the codes from the SITE file (CLASITFD)           *
.*                                                                            *
.* Author         : Justin Coates           30/03/1992                        *
.*                                                                            *
.* Index used     : CLASITA1,"clasitaf"                                       *
.*                                                                            *
.* Modifications  :                                                           *
.******************************************************************************
.
CLASITDS  MOVE      FIVE,CVERT              * line counter
          MOVE      ONE,CCOL                * start on LHS of screen
          MOVE      SP6,KEY6                * key of blanks
          CALL      RSCASIT1                * positional read
.
          DISPLAY   *P1:3,*EF,*V2LON,*P30:4,"SITE CODES":
                    *P1:5,*ULON," Code ",*P8:5,"          Description         "
.
. -------------------------------------
. ------- next read on item file ------
. -------------------------------------
.
HCAST100  CALL      RKCASIT1  
          BRANCH    OVRCD,HCAST995          * end of file
.
. ----------------------------------
. ------- increment counters -------
. ----------------------------------
.
HCAST200  ADD       ONE,CVERT               * inc line counter
.
          COMPARE   "23",CVERT
          GOTO      HCAST300 IF LESS        * will fit on the page
.
          ADD       "40",CCOL               * inc column counter
          MOVE      SIX,CVERT               * reset row position
.
          COMPARE   "80",CCOL  
          GOTO      HCAST400 IF NOT LESS    * need a new page
.
          DISPLAY   *V2LON,*ULON,*P41:5," Code ",*P48:5:
                    "          Description         "
.
. -------------------------------------------
. ------- display all the information -------
. -------------------------------------------
.
HCAST300  DISPLAY   *PCCOL:CVERT,*V2LON,CASTSITE,*HOFF,SP1,CASTDESC
          GOTO      HCAST100
.
. -------------------------------
. ------- new page needed -------
. -------------------------------
.
HCAST400  DISPLAY   *P1:24,"( )ontinue, ( )irst, ( )ext ? ",*EL:
                    *V2LON,*P2:24,ANSC,*P14:24,ANSF,*P23:24,ANSN
.
          KEYIN     *P31:24,*DV,UNDLN1:
                    *P31:24,*V2LON,ANS
.
          REP       UPPLOW,ANS
.
          MATCH     ANSC,ANS
          GOTO      HCAST999 IF EQUAL       * Continue selected
.
          MATCH     ANSF,ANS
          GOTO      CLASITDS IF EQUAL       * First selected
.
. --------------------------------------
. ------- (N)ext screen selected -------
. --------------------------------------
.
HCAST700  DISPLAY   *P41:5,*EF;
          MOVE      SIX,CVERT               * first display row 
          MOVE      ONE,CCOL                * get back to LHS of screen
          GOTO      HCAST300                * display the data
.
HCAST995  
.
HCAST999  RETURN
+
