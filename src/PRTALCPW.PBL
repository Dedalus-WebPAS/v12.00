.******************************************************************************
.* System         : ALL                                                       *
.*                                                                            *
.* Include Name   : PRTALCPW.PBL                                              *
.*                                                                            *
.* Function       : To set up the values to allow alert care pathway fields   *
.*                   to be printed on forms.                                  *
.*                   This will allow up to 10 alerts to be                    *
.*                   printed.  An alert can be printed if TCINDC3 = "P" for   *
.*                   the required alert CODE.                                 *
.*                                                                            *
.* Author         : Davin Sloan (copied from PATPAOFS)                        *
.*                                                                            *
.* Parameters     : FORM5         which alert number to print                 *
.*                                 1, 2, 3 = Alert  1 Pathway Flag, User, Date*
.*                                 4, 5, 6 = Alert  2 Pathway Flag, User, Date*
.*                                 7, 8, 9 = Alert  3 Pathway Flag, User, Date*
.*                                10,11,12 = Alert  4 Pathway Flag, User, Date*
.*                                13,14,15 = Alert  5 Pathway Flag, User, Date*
.*                                16,17,18 = Alert  6 Pathway Flag, User, Date*
.*                                19,20,21 = Alert  7 Pathway Flag, User, Date*
.*                                22,23,24 = Alert  8 Pathway Flag, User, Date*
.*                                25,26,27 = Alert  9 Pathway Flag, User, Date*
.*                                28,29,30 = Alert 10 Pathway Flag, User, Date*
.*                                                                            *
.* Modifications  : V10.08.00  10/08/2016  Davin        TSK 0321234           *
.*----------------------------------------------------------------------------*
.* Modifications  :                                                           *
.*----------------------------------------------------------------------------*
.* V11.03.01 08/09/2023  Thanh T.           TSK 0912156                       *
.*           Changed GALTP000 to select only active alert when ind 3 = 'A'    *
.*           of H1-H9 category.                                               *
.* V10.08.00 10/08/2016  Davin              TSK 0321234                       *
.*           Created include                                                  *
.******************************************************************************
.
          DEFPROC   PRTALCPW
.
          INC       PATALRFD/INC
          INC       WEBSECFD/INC
.
PRNTALR   FORM      2             * the alert number to print
.
. ------- determine if printing a care pathway flag, user id, or date ----
.
PACPF000  OPEN      PATALRT1,"patalrtf"
          OPEN      WEBSECA1,"websecaf"
          BRANCH    FORM5,PACPF100,PACPF400,PACPF700:      * alert 1
                          PACPF100,PACPF400,PACPF700:      * alert 2
                          PACPF100,PACPF400,PACPF700:      * alert 3
                          PACPF100,PACPF400,PACPF700:      * alert 4
                          PACPF100,PACPF400,PACPF700:      * alert 5
                          PACPF100,PACPF400,PACPF700:      * alert 6
                          PACPF100,PACPF400,PACPF700:      * alert 7
                          PACPF100,PACPF400,PACPF700:      * alert 8
                          PACPF100,PACPF400,PACPF700:      * alert 9
                          PACPF100,PACPF400,PACPF700       * alert 10
          GOTO      PACPF999
.
. ------- print an alert Care Pathway Activated Flag -------
.
PACPF100  ASSIGN    (FORM5+2)/3,FORM2       * get the alert number
          CALL      GALTP000                * get the correct alert to print
          MOVE      SP3,D3
          MATCH     "1",PTALCPAC
          IF        @EQUAL
            MOVE      "Yes",D3
          ENDIF
          MATCH     "0",PTALCPAC
          IF        @EQUAL
            MOVE      "No ",D3
          ENDIF
          PACK      VARIABLE,D3,SP30,SP30,SP30
          GOTO      PACPF999
.
. ------- print an alert User Id Who Last Updated Care Pathway Flag -------
.
PACPF400  ASSIGN    (FORM5+1)/3,FORM2       * get the alert number
          CALL      GALTP000                * get the correct alert to print
          MATCH     SP70,PTALUCPF
          IF        @EQUAL
            PACK      VARIABLE,SP70,SP30,SP30,SP30
            GOTO      PACPF999
          ENDIF
          PACK      KEY10,PTALUCPF,SP70
          CALL      RDWBSE1
          IF        OVRCD=1
            PACK      VARIABLE,PTALUCPF,SP30,SP30,SP30
          ELSE
            PACK      VARIABLE,WBSENAM,SP30,SP30,SP30
          ENDIF
          GOTO      PACPF999
.
. ------- print an alert Last Date Care Pathway Activated  -------
.
PACPF700  ASSIGN    FORM5/3,FORM2           * get the alert number
          CALL      GALTP000                * get the correct alert to print
          MATCH     SP70,PTALDCPU
          IF        @EQUAL
            PACK      VARIABLE,SP70,SP30,SP30,SP30
            GOTO      PACPF999
          ENDIF
          UNPACK    PTALDCPU,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      VARIABLE,CPCDATE,SP30,SP30,SP30
          GOTO      PACPF999
+
.*********************************************************************
.         loop over alerts to get the correct one to print
.         Para's  : FORM2         the alert number to print (1-10)
.*********************************************************************
GALTP000  MOVE      FORM2,PRNTALR           * the alert to print
          MOVE      ZERO,FORM2              * clear counter
          PACK      KEY16,PURNO,SP70
          CALL      RSPTALR1
.
GALTP100  CALL      RKPTALR1
          BRANCH    OVRCD,GALTP900          * patient has no alerts
          MATCH     PURNO,PTALURNO
          GOTO      GALTP900 IF NOT EQUAL   * patient has no alerts
.
          PACK      KEY5,PTALCATG,PTALCODE
          CALL      RDCODE1
          BRANCH    OVRCD,GALTP100          * invalid alert code
.
          MATCH     ANSP,TCINDC3
          GOTO      GALTP500 IF EQUAL       * Printable alert
.
          MATCH     ANSA,TCINDC3            * Check active alert
          GOTO      GALTP100 IF NOT EQUAL
.
. Check active alert
.
          MATCH     SP70,PTALEDAT
          GOTO      GALTP300 IF EQUAL       * no alert inactive date
.
          MATCH     PTALEDAT,CURRDATE
          GOTO      GALTP100 IF NOT LESS    * alert end date passed ?
.
GALTP300  MATCH     SP70,PTALDTIN
          GOTO      GALTP500 IF EQUAL       * no alert inactive date
.
          MATCH     PTALDTIN,CURRDATE
          GOTO      GALTP100 IF NOT LESS    * alert inactive date passed ?
.
GALTP500  ADD       ONE,FORM2               * add to number of printable alerts
          COMPARE   FORM2,PRNTALR
          GOTO      GALTP100 IF NOT EQUAL   * not the required printable alert
          GOTO      GALTP999                * have got the alert to print
.
GALTP900  MOVE      SP70,PTALCPAC
          MOVE      SP70,PTALUCPF
          MOVE      SP70,PTALDCPU
.
GALTP999  RETURN
+
          INC       IBAOVRIO/INC
          INC       PATALRIO/INC
          INC       WEBSECIO/INC
.
PACPF999  ENDPROC
+
