. *----------------------------------------------------------------------
. * Include Name  :   PMSIDWVR.PBL
. *
. * Function      :   EDWARD ISP Audit File Writes
. *----------------------------------------------------------------------
.  Modifications Done :
.
.  V11.03.02  16/11/2023  Sunny         TSK 0916075
.             Changed WISH7000 - AHPRA ID Type Change 
.  V11.03.01  12/10/2023  Sunny         TSK 0916075
.             Changed WISH2200 - Stafflink ID change  
.             Added WISH7000 - AHPRA ID change  
.  V10.14.03  05/09/2019  Ebon Clements TSK 0881147
.             Added SISPSPC1 - HCP Speciality 1
.  V10.14.02  01/03/2019  Ebon Clements TSK 0871097
.             Added HCP Gender trigger
.  V10.14.01  28/02/2019  Ebon Clements TSK 0871097
.             Allow multiple add units to a doctor records per day
.  V10.13.01  15/01/2018  Ebon Clements TSK 0868909
.             Fixed linked provider number update - WISL2000
.             Added practice fax and email writes
.  V10.13.00  09/01/2018  Ebon Clements TSK 0868909
.             Created include
. *----------------------------------------------------------------------
.
.----------------------------------------------------------------------
. Save PMSHCPFD file variables for EDWARD ISP Audit
.----------------------------------------------------------------------
SISH0000  MOVE      PMHCTITL,SISPTITL       * HCP Title
          MOVE      PMHCGNAM,SISPGNAM       * HCP Given Names
          MOVE      PMHCSNAM,SISPSNAM       * HCP Surname
          MOVE      PMHCSTTS,SISPSTTS       * HCP rec status
          MOVE      PMHCPRV1,SISPPRV1       * Provider Number 1 - Reg. No. NZ
          MOVE      PMHCPRV2,SISPPRV2       * Provider Number 2 - Pref Reg NZ 
          MOVE      PMHCPRV3,SISPPRV3       * Provider Number 3
          MOVE      PMHCPRV4,SISPPRV4       * Provider Number 4
          MOVE      PMHCPRV5,SISPPRV5       * Provider Number 5
          MOVE      PMHCNHTA,SIPSNHTA       * NEHTA Identifier
          MOVE      PMHCMPGN,SISPMPGN       * MPG Certificate Number
          MOVE      PMHCEDAT,SISPEDAT       * End Date (ccyymmdd)
          MOVE      PMHCGNDR,SISPGNDR       * HCP Gender (M,F,I,U)
          MOVE      PMHCSPC1,SISPSPC1       * HCP Speciality 1 (Cat DT)
          MOVE      PMHCUDF3,SISPUDF3       * AHPRA ID (Cat R7)
.
SISH9999  RETURN
.
.----------------------------------------------------------------------
. Write EDWARD ISP Audit for HCP
.----------------------------------------------------------------------
WISH0000  PACK      PMHCTITL,PMHCTITL,SP70
          PACK      SISPGNAM,SISPGNAM,SP70
          PACK      PMHCSNAM,PMHCSNAM,SP70
.
          MATCH     PMHCTITL,SISPTITL       * HCP Title
          IF        @EQUAL
            MATCH     PMHCGNAM,SISPGNAM     * HCP Given Names
            IF        @EQUAL
              MATCH     PMHCSNAM,SISPSNAM   * HCP Surname
              GOTO      WISH1000 IF EQUAL
            ENDIF
          ENDIF
.
          PACK      PMIWCODE,PMHCHCPC,SP70                  * HCP Code
          MOVE      "01 ",PMIWTYPE                          * HCP Name Change
          PACK      PMIWOLDV,SISPTITL,SISPGNAM,SISPSNAM:
                             SP70,SP70,SP70                 * Old Value
          PACK      PMIWNEWV,PMHCTITL,PMHCGNAM,PMHCSNAM:
                             SP70,SP70,SP70                 * New Value
          CALL      WRIDW000
.
WISH1000  PACK      PMHCSTTS,PMHCSTTS,SP70
          MATCH     PMHCSTTS,SISPSTTS       * HCP rec status
          GOTO      WISH2000 IF EQUAL
.
          PACK      PMIWCODE,PMHCHCPC,SP70                  * HCP Code
          MOVE      "10 ",PMIWTYPE                          * HCP Made Inactive
          PACK      PMIWOLDV,SISPSTTS,SP70,SP70,SP70,SP70   * Old Value
          PACK      PMIWNEWV,PMHCSTTS,SP70,SP70,SP70,SP70   * New Value
          CALL      WRIDW000
.
WISH2000  PACK      PMHCPRV1,PMHCPRV1,SP70
          MATCH     PMHCPRV1,SISPPRV1       * Provider Number 1 - Reg. No. NZ
          GOTO      WISH2100 IF EQUAL
.
          PACK      PMIWCODE,PMHCHCPC,SP70                  * HCP Code
          MOVE      "11 ",PMIWTYPE                          * Provider No Change
          PACK      PMIWOLDV,SISPPRV1,SP70,SP70,SP70,SP70   * Old Value
          PACK      PMIWNEWV,PMHCPRV1,SP70,SP70,SP70,SP70   * New Value
          CALL      WRIDW000
.
WISH2100  PACK      PMHCPRV2,PMHCPRV2,SP70
          MATCH     PMHCPRV2,SISPPRV2       * Provider Number 2
          GOTO      WISH2200 IF EQUAL
.
          PACK      PMIWCODE,PMHCHCPC,SP70                  * HCP Code
          MOVE      "24 ",PMIWTYPE                          * Stafflink Change
          PACK      PMIWOLDV,SISPPRV2,SP70,SP70,SP70,SP70   * Old Value
          PACK      PMIWNEWV,PMHCPRV2,SP70,SP70,SP70,SP70   * New Value
          CALL      WRIDW000
.
WISH2200  PACK      PMHCPRV3,PMHCPRV3,SP70
          MATCH     PMHCPRV3,SISPPRV3       * Provider Number 3
          GOTO      WISH2300 IF EQUAL
.
          PACK      PMIWCODE,PMHCHCPC,SP70                  * HCP Code
          MOVE      "11 ",PMIWTYPE                          * Provider No Change
          PACK      PMIWOLDV,SISPPRV3,SP70,SP70,SP70,SP70   * Old Value
          PACK      PMIWNEWV,PMHCPRV3,SP70,SP70,SP70,SP70   * New Value
          CALL      WRIDW000
.
WISH2300  PACK      PMHCPRV4,PMHCPRV4,SP70
          MATCH     PMHCPRV4,SISPPRV4       * Provider Number 4
          GOTO      WISH2400 IF EQUAL
.
          PACK      PMIWCODE,PMHCHCPC,SP70                  * HCP Code
          MOVE      "11 ",PMIWTYPE                          * Provider No Change
          PACK      PMIWOLDV,SISPPRV4,SP70,SP70,SP70,SP70   * Old Value
          PACK      PMIWNEWV,PMHCPRV4,SP70,SP70,SP70,SP70   * New Value
          CALL      WRIDW000
.
WISH2400  PACK      PMHCPRV5,PMHCPRV5,SP70
          MATCH     PMHCPRV5,SISPPRV5       * Provider Number 5
          GOTO      WISH2500 IF EQUAL
.
          PACK      PMIWCODE,PMHCHCPC,SP70                  * HCP Code
          MOVE      "11 ",PMIWTYPE                          * Provider No Change
          PACK      PMIWOLDV,SISPPRV5,SP70,SP70,SP70,SP70   * Old Value
          PACK      PMIWNEWV,PMHCPRV5,SP70,SP70,SP70,SP70   * New Value
          CALL      WRIDW000
.
WISH2500  PACK      PMHCNHTA,PMHCNHTA,SP70
          MATCH     PMHCNHTA,SIPSNHTA       * NEHTA Identifier 
          GOTO      WISH3000 IF EQUAL
.
          PACK      PMIWCODE,PMHCHCPC,SP70                  * HCP Code
          MOVE      "16 ",PMIWTYPE                          * Provider No Change
          PACK      PMIWOLDV,SIPSNHTA,SP70,SP70,SP70,SP70   * Old Value
          PACK      PMIWNEWV,PMHCNHTA,SP70,SP70,SP70,SP70   * New Value
          CALL      WRIDW000
.
WISH3000  PACK      PMHCMPGN,PMHCMPGN,SP70
          MATCH     PMHCMPGN,SISPMPGN       * MPG Certificate Number
          GOTO      WISH4000 IF EQUAL
.
          PACK      PMIWCODE,PMHCHCPC,SP70                  * HCP Code
          MOVE      "12 ",PMIWTYPE                          * Nat Reg No Change
          PACK      PMIWOLDV,SISPMPGN,SP70,SP70,SP70,SP70   * Old Value
          PACK      PMIWNEWV,PMHCMPGN,SP70,SP70,SP70,SP70   * New Value
          CALL      WRIDW000
.
WISH4000  PACK      PMHCEDAT,PMHCEDAT,SP70
          MATCH     PMHCEDAT,SISPEDAT       * End Date (ccyymmdd)
          GOTO      WISH5000 IF EQUAL
.
          PACK      PMIWCODE,PMHCHCPC,SP70                  * HCP Code
          MOVE      "14 ",PMIWTYPE                          * End Date Added    
          MATCH     SP70,SISPEDAT
          IF        !@EQUAL
            MATCH     SP70,PMHCEDAT
            IF        @EQUAL
              MOVE      "15 ",PMIWTYPE                      * End Date Removed  
            ENDIF
          ENDIF
          PACK      PMIWOLDV,SISPEDAT,SP70,SP70,SP70,SP70   * Old Value
          PACK      PMIWNEWV,PMHCEDAT,SP70,SP70,SP70,SP70   * New Value
          CALL      WRIDW000
.
WISH5000  PACK      PMHCGNDR,PMHCGNDR,SP70
          MATCH     PMHCGNDR,SISPGNDR       * HCP Gender (M,F,I,U)
          GOTO      WISH6000 IF EQUAL
.
          PACK      PMIWCODE,PMHCHCPC,SP70                  * HCP Code
          MOVE      "22 ",PMIWTYPE                          * End Date Added
          PACK      PMIWOLDV,SISPGNDR,SP70,SP70,SP70,SP70   * Old Value
          PACK      PMIWNEWV,PMHCGNDR,SP70,SP70,SP70,SP70   * New Value
          CALL      WRIDW000
.
WISH6000  PACK      PMHCSPC1,PMHCSPC1,SP70
          MATCH     PMHCSPC1,SISPSPC1       * HCP Speciality 1 (Cat DT)
          GOTO      WISH7000 IF EQUAL
.
          PACK      PMIWCODE,PMHCHCPC,SP70                  * HCP Code
          MOVE      "23 ",PMIWTYPE                          * End Date Added
          PACK      PMIWOLDV,SISPSPC1,SP70,SP70,SP70,SP70   * Old Value
          PACK      PMIWNEWV,PMHCSPC1,SP70,SP70,SP70,SP70   * New Value
          CALL      WRIDW000
.
WISH7000  PACK      PMHCUDF3,PMHCUDF3,SP70
          MATCH     PMHCUDF3,SISPUDF3       * AHPRA ID (Cat R7)
          GOTO      WISH9999 IF EQUAL
.
          PACK      PMIWCODE,PMHCHCPC,SP70                  * HCP Code
          MOVE      "25 ",PMIWTYPE                          * AHPRA ID Change
          PACK      PMIWOLDV,SISPUDF3,SISPMPGN,SP70,SP70,SP70,SP70   * Old Val
          PACK      PMIWNEWV,PMHCUDF3,PMHCMPGN,SP70,SP70,SP70,SP70   * New Val
          CALL      WRIDW000
.
WISH9999  RETURN
.
.-----------------------------------------------------------
. Write EDWARD ISP Audit Record
.------------------------------------------------------------
WRIDW000  MATCH     SP70,PTCNNSSI           * no edward source system
          GOTO      WRIDW999 IF EQUAL
.
          MATCH     "0000000000",PTCNNSSI   * no edward source system
          GOTO      WRIDW999 IF EQUAL
.
          CALL      IBACLOCK
          PACK      PMIWCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMIWCDAT
          MOVE      "00:00:00",PMIWCTIM
          PACK      PMIWCUID,USERID
          PACK      PMIWSPAR,SP70,SP70
.
WRIDW100  PACK      KEY41,PMIWCDAT,PMIWCTIM,PMIWCODE,PMIWTYPE,SP70
          CALL      RAPMIDW1
          IF        OVRCD=1
            CALL      WRPMIDW1
          ELSE
            MATCH     "08 ",PMIWTYPE        * Unit Added
            GOTO      WRIDW900 IF EQUAL
.
            MATCH     "09 ",PMIWTYPE        * Unit Deleted
            GOTO      WRIDW900 IF EQUAL
.
            CALL      UPPMIDW1
          ENDIF
          GOTO      WRIDW999 
.
WRIDW900  UNPACK    PMIWCTIM,HOURA,D1,MINA,D1,SECA
          PACK      XDATTIME,PMIWCDAT,HOURA,MINA,SECA,SP70
          CALL      ADSC0000             * Add one second
          UNPACK    XDATTIME,PMIWCDAT,HOURA,MINA,SECA
          PACK      PMIWCTIM,HOURA,COLON,MINA,COLON,SECA
          GOTO      WRIDW100
.
WRIDW999  RETURN
.
.*****************************************************************************
.*                            ADSC0000                                       *
.*               Add one second to date/time                                 *
.* Requires: XDATTIME - Date/time value (ccyymmddhhmmss)                     *
.* Returns : XDATTIME - Date/time value (ccyymmddhhmmss) incremented by 1 sec*
.*****************************************************************************
ADSC0000  UNPACK    XDATTIME,DIM8,HOURTM,MINTIME,SECTIME
          MATCH     "59",SECTIME
          IF        !@EQUAL
            MOVE      SECTIME,FORM2              * just increment seconds
            ADD       ONE,FORM2
            MOVE      FORM2,SECTIME
            REP       " 0",SECTIME
            PACK      XDATTIME,DIM8,HOURTM,MINTIME,SECTIME
            GOTO      ADSC9999
          ENDIF
.
.         The seconds were "59", so reset these to "00" and increment the
.         minutes
.
          MOVE      "00",SECTIME
          MATCH     "59",MINTIME
          IF        !@EQUAL
            MOVE      MINTIME,FORM2              * just increment minutes
            ADD       ONE,FORM2
            MOVE      FORM2,MINTIME
            REP       " 0",MINTIME
            PACK      XDATTIME,DIM8,HOURTM,MINTIME,SECTIME
            GOTO      ADSC9999
          ENDIF
.
.         The minutes were "59", so reset these to "00" and increment the
.         hours
.
          MOVE      "00",SECTIME
          MOVE      "00",MINTIME
          MATCH     "23",HOURTM
          IF        !@EQUAL
            MOVE      HOURTM,FORM2              * just increment hours
            ADD       ONE,FORM2
            MOVE      FORM2,HOURTM
            REP       " 0",HOURTM
            PACK      XDATTIME,DIM8,HOURTM,MINTIME,SECTIME
            GOTO      ADSC9999
          ENDIF
.
.         The hours were "59", so reset these to "00" and increment the
.         date
.
          MOVE      "00",SECTIME
          MOVE      "00",MINTIME
          MOVE      "00",HOURTM
.
.         We need to increment the date by 1 day
.
          UNPACK    DIM8,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON                       * get julian day
          ADD       ONE,JULDAY                   * increment julian day
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON                       * get new date
          PACK      XDATTIME,CC,YY,MM,DD,ZERO6
          REP       " 0",XDATTIME
.
ADSC9999  RETURN
.
.----------------------------------------------------------------------
. Save PMSHCGFD file variables for EDWARD ISP Audit
.----------------------------------------------------------------------
SISG0000  MOVE      PMHGPRAC,SISGPRAC       * HCP Practice Code
          MOVE      PMHGCNTR,SISGCNTR       * Count
          MOVE      PMHGADD1,SISGADD1       * HCP Practice Address 1
          MOVE      PMHGADD2,SISGADD2       * HCP Practice Address 2
          MOVE      PMHGADD3,SISGADD3       * HCP Practice Address 3
          MOVE      PMHGADD4,SISGADD4       * HCP Practice Address 4
          MOVE      PMHGPOST,SISGPOST       * HCP Practice Post Code
          MOVE      PMHGSTTS,SISGSTTS       * Status 0=active, 1=inactive
          MOVE      PMHGPTEL,SISGPTEL       * HCP Practice Telephone Number
          MOVE      PMHGPEML,SISGPEML       * HCP Practice Email Address  
          MOVE      PMHGPFAX,SISGPFAX       * HCP Practice Fax Number
.
SISG9999  RETURN
.
.----------------------------------------------------------------------
. Write EDWARD ISP Audit for Practice
.----------------------------------------------------------------------
WISG0000  PACK      PMHGPRAC,PMHGPRAC,SP70
          PACK      PMHGCNTR,PMHGCNTR,SP70
          MATCH     PMHGPRAC,SISGPRAC       * HCP Practice Code
          IF        @EQUAL
            MATCH     PMHGCNTR,SISGCNTR     * Count
            GOTO      WISG1000 IF EQUAL
          ENDIF
.
          PACK      PMIWCODE,SP10,PMHGPRAC,PMHGCNTR,SP70    * Practice/count
          MOVE      "13 ",PMIWTYPE                          * Add Practice
          PACK      PMIWOLDV,SP70,SP70,SP70,SP70         * Old Value
          PACK      PMIWNEWV,PMHGPRAC,PMHGCNTR,SP70:
                             SP70,SP70,SP70              * New Value
          CALL      WRIDW000
          GOTO      WISG9999
.
WISG1000  PACK      PMHGADD1,PMHGADD1,SP70
          PACK      PMHGADD2,PMHGADD2,SP70
          PACK      PMHGADD3,PMHGADD3,SP70
          PACK      PMHGADD4,PMHGADD4,SP70
          PACK      PMHGPOST,PMHGPOST,SP70

          MATCH     PMHGADD1,SISGADD1       * HCP Practice Address 1
          GOTO      WISG1500 IF NOT EQUAL
.
          MATCH     PMHGADD2,SISGADD2       * HCP Practice Address 2
          GOTO      WISG1500 IF NOT EQUAL
.
          MATCH     PMHGADD3,SISGADD3       * HCP Practice Address 3
          GOTO      WISG1500 IF NOT EQUAL
.
          MATCH     PMHGADD4,SISGADD4       * HCP Practice Address 4
          GOTO      WISG1500 IF NOT EQUAL
.
          MATCH     PMHGPOST,SISGPOST       * HCP Practice Post Code
          GOTO      WISG1500 IF NOT EQUAL
.
          GOTO      WISG2000
.
WISG1500  PACK      PMIWCODE,SP10,PMHGPRAC,PMHGCNTR,SP70   * Practice/count 
          MOVE      "02 ",PMIWTYPE                         * Prac Address Change
          PACK      PMIWOLDV,SISGADD1,SISGADD2,SISGADD3:
                             SISGADD4,SISGPOST,SP70         * Old Value
          PACK      PMIWNEWV,PMHGADD1,PMHGADD2,PMHGADD3:
                             PMHGADD4,PMHGPOST,SP70         * New Value
          CALL      WRIDW000
.
WISG2000  PACK      PMHGSTTS,PMHGSTTS,SP70
          MATCH     PMHGSTTS,SISGSTTS       * Status 0=active, 1=inactive
          GOTO      WISG3000 IF EQUAL
.
          PACK      PMIWCODE,SP10,PMHGPRAC,PMHGCNTR,SP70   * Practice/count
          MOVE      "07 ",PMIWTYPE                         * Prac inactivated
          PACK      PMIWOLDV,SISGSTTS,SP70,SP70,SP70,SP70  * Old Value
          PACK      PMIWNEWV,PMHGSTTS,SP70,SP70,SP70,SP70  * New Value
          CALL      WRIDW000
.
WISG3000  PACK      PMHGPTEL,PMHGPTEL,SP70
          MATCH     PMHGPTEL,SISGPTEL       * HCP Practice Telephone Number
          GOTO      WISG4000 IF EQUAL
.
          PACK      PMIWCODE,SP10,PMHGPRAC,PMHGCNTR,SP70   * Practice/count
          MOVE      "03 ",PMIWTYPE                         * Prac telephone
          PACK      PMIWOLDV,SISGPTEL,SP70,SP70,SP70,SP70  * Old Value
          PACK      PMIWNEWV,PMHGPTEL,SP70,SP70,SP70,SP70  * New Value
          CALL      WRIDW000
.
WISG4000  PACK      PMHGPEML,PMHGPEML,SP70
          MATCH     PMHGPEML,SISGPEML       * HCP Practice Email Address
          GOTO      WISG5000 IF EQUAL
.
          PACK      PMIWCODE,SP10,PMHGPRAC,PMHGCNTR,SP70   * Practice/count
          MOVE      "18 ",PMIWTYPE                         * Prac Email
          PACK      PMIWOLDV,SISGPEML,SP70,SP70,SP70,SP70  * Old Value
          PACK      PMIWNEWV,PMHGPEML,SP70,SP70,SP70,SP70  * New Value
          CALL      WRIDW000
.
WISG5000  PACK      PMHGPFAX,PMHGPFAX,SP70
          MATCH     PMHGPFAX,SISGPFAX       * HCP Practice Fax Number
          GOTO      WISG9999 IF EQUAL
.
          PACK      PMIWCODE,SP10,PMHGPRAC,PMHGCNTR,SP70   * Practice/count
          MOVE      "17 ",PMIWTYPE                         * Prac Fax
          PACK      PMIWOLDV,SISGPFAX,SP70,SP70,SP70,SP70  * Old Value
          PACK      PMIWNEWV,PMHGPFAX,SP70,SP70,SP70,SP70  * New Value
          CALL      WRIDW000
.
WISG9999  RETURN
.
.----------------------------------------------------------------------
. Save PMSHCLFD file variables for EDWARD ISP Audit
.----------------------------------------------------------------------
SISL0000  MOVE      PMHLLPDT,SISLLPDT       * Date HCP Left Practice (ccyymmdd)
          MOVE      PMHLPRV1,SISLPRV1       * Provider Number 1
          MOVE      PMHLHCPC,SISLHCPC       * HCP Code (pmshcpaf)
          MOVE      PMHLHCPR,SISLHCPR       * HCP Practice (pmshcgaf)
          MOVE      PMHLSTAT,SISLSTAT       * Status (0=Active,1=Not Active)
.
SISL9999  RETURN
.
.----------------------------------------------------------------------
. Write EDWARD ISP Audit for Practice
.----------------------------------------------------------------------
WISL0000  PACK      PMHLHCPC,PMHLHCPC,SP70
          PACK      PMHLHCPR,PMHLHCPR,SP70
          MATCH     PMHLHCPC,SISLHCPC       * HCP Code (pmshcpaf)
          IF        @EQUAL
            MATCH     PMHLHCPR,SISLHCPR     * HCP Practice (pmshcgaf)
            GOTO      WISL1000 IF EQUAL
          ENDIF
.
          PACK      PMIWCODE,PMHLHCPC,PMHLHCPR,SP70      * HCP Practice
          MOVE      "05 ",PMIWTYPE                       * HCP Add to Prac. 
          PACK      PMIWOLDV,SP70,SP70,SP70,SP70         * Old Value
          PACK      PMIWNEWV,PMHLHCPC,SISLHCPR,SP70:
                             SP70,SP70,SP70              * New Value
          CALL      WRIDW000
          GOTO      WISL9999

WISL1000  PACK      PMHLLPDT,PMHLLPDT,SP70
          MATCH     PMHLLPDT,SISLLPDT       * Date HCP Left Practice (ccyymmdd)
          GOTO      WISL2000 IF EQUAL
.
          PACK      PMIWCODE,PMHLHCPC,PMHLHCPR,SP70      * HCP Practice
          MOVE      "06 ",PMIWTYPE                       * HCP Date left prac.
          PACK      PMIWOLDV,SISLLPDT,SP70,SP70,SP70,SP70      * Old Value
          PACK      PMIWNEWV,PMHLLPDT,SP70,SP70,SP70,SP70      * New Value
          CALL      WRIDW000
.
WISL2000  PACK      PMHLPRV1,PMHLPRV1,SP70
          MATCH     PMHLPRV1,SISLPRV1       * Provider Number 1
          GOTO      WISL3000 IF EQUAL
.
          PACK      PMIWCODE,PMHLHCPC,PMHLHCPR,SP70      * HCP Practice
          MOVE      "04 ",PMIWTYPE                       * Provider No change 
          PACK      PMIWOLDV,SISLPRV1,SP70,SP70,SP70,SP70      * Old Value
          PACK      PMIWNEWV,PMHLPRV1,SP70,SP70,SP70,SP70      * New Value
          CALL      WRIDW000
.
WISL3000  PACK      PMHLSTAT,PMHLSTAT,SP70
          MATCH     PMHLSTAT,SISLSTAT       * Status (0=Active,1=Not Active)
          GOTO      WISL9999 IF EQUAL
.
          PACK      PMIWCODE,PMHLHCPC,PMHLHCPR,SP70      * HCP Practice
          MOVE      "06 ",PMIWTYPE                       * Inactivate from prac
          PACK      PMIWOLDV,SISLSTAT,SP70,SP70,SP70,SP70      * Old Value
          PACK      PMIWNEWV,PMHLSTAT,SP70,SP70,SP70,SP70      * New Value
          CALL      WRIDW000
.
WISL9999  RETURN
.
.----------------------------------------------------------------------
. Save control file variables for EDWARD ISP Audit
.----------------------------------------------------------------------
SISC0000  MOVE      CHADD1,SISCADD1         * Hospital Address 1
          MOVE      CHADD2,SISCADD2         * Hospital Address 2
          MOVE      CHPOST,SISCPOST         * Hospital Postcode
          MOVE      PTCNHDPS,SISCHDPS       * Hospital state for HDP extracts
          MOVE      CHTELEB,SISCTELB        * Hospital Telephone Number
          MOVE      PTCNHFAX,SISCHFAX       * Hospital Fax Number
.
SISC9999  RETURN
.
.----------------------------------------------------------------------
. Write EDWARD ISP Audit for controlf file
.----------------------------------------------------------------------
WISC0000  PACK      CHADD1,CHADD1,SP70
          PACK      CHADD2,CHADD2,SP70
          PACK      CHPOST,CHPOST,SP70

          MATCH     CHADD1,SISCADD1         * Hospital Address 1
          GOTO      WISC1500 IF NOT EQUAL
.
          MATCH     CHADD2,SISCADD2         * Hospital Address 2
          GOTO      WISC1500 IF NOT EQUAL
.
          COMPARE   PTCNHDPS,SISCHDPS       * Hospital State
          GOTO      WISC1500 IF NOT EQUAL
.
          MATCH     CHPOST,SISCPOST         * Hospital Postcode
          GOTO      WISC1500 IF NOT EQUAL
.
          GOTO      WISC2000
.
WISC1500  PACK      PMIWCODE,SP70                       * Blank for control file
          MOVE      "19 ",PMIWTYPE                      * Hosp Address Change
          PACK      PMIWOLDV,SISCADD1,SISCADD2,SISCHDPS:
                             SISCPOST,SP70         * Old Value
          PACK      PMIWNEWV,CHADD1,CHADD2,PTCNHDPS:
                             CHPOST,SP70         * New Value
          CALL      WRIDW000
.
WISC2000  PACK      CHTELEB,CHTELEB,SP70
          MATCH     CHTELEB,SISCTELB        * Hospital Telephone Number
          GOTO      WISC3000 IF EQUAL
.
          PACK      PMIWCODE,SP70                       * Blank for control file
          MOVE      "20 ",PMIWTYPE                      * Hosp Phone No. Change
          PACK      PMIWOLDV,SISCTELB,SP70,SP70,SP70,SP70    * Old Value
          PACK      PMIWNEWV,CHTELEB,SP70,SP70,SP70,SP70     * New Value
          CALL      WRIDW000
.
WISC3000  PACK      PTCNHFAX,PTCNHFAX,SP70
          MATCH     PTCNHFAX,SISCHFAX       * Hospital Fax Number
          GOTO      WISC9999 IF EQUAL
.
          PACK      PMIWCODE,SP70                       * Blank for control file
          MOVE      "21 ",PMIWTYPE                      * Hosp Fax No. Change
          PACK      PMIWOLDV,SISCHFAX,SP70,SP70,SP70,SP70    * Old Value
          PACK      PMIWNEWV,PTCNHFAX,SP70,SP70,SP70,SP70     * New Value
          CALL      WRIDW000
.
WISC9999  RETURN
