.******************************************************************************
.*  Include Name  : PATFLTKY.PBL                                              *
.*  Description   : Keyin of a Financial letter codes file entry              *
.*  Author        : ROD   04/03/91                                            *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CODE     - category of code                               *
.*                  CKYIDEF3 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory  (0=No  1=Yes)              *
.*                  CKYIMODE - is keyin input mode (0=No  1=Yes)              *
.*                                                                            *
.*  Returns       : LCODE    - Keyed in code                                  *
.*                  PTFLTEXT - Description                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  Required      : You must display the description yourself.                *
.*                                                                            *
.******************************************************************************
PATFLTKY
LCODE010  MATCH     SP3,CKYIDEF3                  * using a default ?
          GOTO      LCODE050 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN3:      * no default used
                    *PECOL:EVERT,*V2LON,*JR,LCODE:
                    *PECOL:EVERT,*DV,LCODE
          GOTO      LCODE080
.
LCODE050  MOVE      CKYIDEF3,LCODE
          KEYIN     *PECOL:EVERT,*DV,LCODE:    * default used
                    *PECOL:EVERT,*V2LON,*JR,*RV,LCODE:
                    *PECOL:EVERT,*DV,LCODE
.
.         check what was entered
.
LCODE080  CALL      LETCK000
          BRANCH    EXIT,LCODE100,LCODE705,LCODE805,LCODE905
.                         "?"      valid    nothing exitchar
          GOTO      LCODE010
.
.         a "?" was input
.
LCODE100  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
LCODE105  MOVE      ONE,CNEWDS
          CALL      PATFLTDS
.
.         now ask for code while leaving displayed codes on screen
.
LCODE150  MATCH     SP3,CKYIDEF3                  * using a default
          GOTO      LCODE200 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Letter Number : ":
                    *P17:24,*DV,UNDLN3:      * no default used
                    *P17:24,*V2LON,*JR,LCODE:
                    *P17:24,*DV,LCODE
          GOTO      LCODE300
.
LCODE200  MOVE      CKYIDEF3,LCODE
          KEYIN     *P1:24,*EL,*DV,"Letter Number : ":
                    *P17:24,*DV,LCODE:     * default used
                    *P17:24,*V2LON,*JR,*RV,LCODE:
                    *P17:24,*DV,LCODE
.
.         repeat the validation of the code
.         check what was entered
.
LCODE300  CALL      LETCK000
          BRANCH    EXIT,LCODE105,LCODE700,LCODE800,LCODE900
.                         "?"      valid    nothing exitchar
.
          GOTO      LCODE150
.
.         a valid code was entered 
. 
LCODE700  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,LCODE
.
LCODE705  MOVE      FALSE,EXIT              * valid input
          GOTO      LCODE999 
.
.         nothing was input
.
LCODE800  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,LCODE
.
LCODE805  PACK      PTFLTEXT,SP30,SP30,SP30 * clear desc field
          MOVE      TRUE,EXIT               * Nothing or Spaces entered
          GOTO      LCODE999 
.
.         EXITCHAR input
.
LCODE900  CALL      PUTSCR00                * free the screen saved
.
LCODE905  MOVE      TWO,EXIT                * EXITCHAR
.
LCODE999  RETURN
+
.***************************************************************************
.*                              LETCK000             Called by: PATFLTKY   *
.*                        check what was entered                           *
.*          Returns:  EXIT  = 0   invalid                                  *
.*                          = 1   "?"                                      *
.*                          = 2   valid                                    *
.*                          = 3   nothing                                  *
.*                          = 4   exitchar                                 *
.***************************************************************************
.
LETCK000  ENDSET    LCODE
          APPEND    SP3,LCODE
          RESET     LCODE
.
          SCAN      EXITCHAR,LCODE               * exitchar input ?
          GOTO      LETCK600 IF EQUAL            * yes
.
          RESET     LCODE
          SCAN      QUEST,LCODE                  * ? input ?
          GOTO      LETCK800 IF EQUAL            * yes
.
          RESET     LCODE
          MATCH     SP3,LCODE                    * anything input ?
          GOTO      LETCK700 IF EQUAL            * no
.
          TYPE      LCODE                        * numeric input ?
          GOTO      LETCK900 IF NOT EQUAL        * no
.
. a code was entered so validate
. 
LETCK500  MOVE      LCODE,LETTFORM
          PACK      KEY6,LETTFORM,SP2,ZERO
          CALL      RDPTFL1
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,LETCK900
          ELSE
            COMPARE   ZERO,OVRCD
            GOTO      LETCK920 IF EQUAL
          ENDIF
.
          MOVE      TWO,EXIT
          GOTO      LETCK999
.
. exitchar entered
.
LETCK600  MOVE      FOUR,EXIT
          GOTO      LETCK999
.
. nothing entered
.
LETCK700  BRANCH    CKYIMAND,LETCK950             * mandatory
          MOVE      THREE,EXIT
          PACK      PTFLTEXT,SP30,SP30,SP30     * clear desc field
          GOTO      LETCK999
.
. "?" entered
.
LETCK800  MOVE      ONE,EXIT
          GOTO      LETCK999
.
. invalid code entered
.
LETCK900  DISPLAY   *P1:24,*EL,*B,"Invalid Letter Number.  ";
          CALL      HITENTER
          MOVE      ZERO,EXIT
          GOTO      LETCK999
.
LETCK920  CALL      REXISTS 
          MOVE      ZERO,EXIT
          GOTO      LETCK999
.
LETCK950  BEEP
          MOVE      ZERO,EXIT
.
LETCK999  RETURN
.
          INC       PATFLTDS
.
+
