.******************************************************************************
.* System         : Theatre System          Release 6                         *
.* Include Name   : OPPSTBOK.PBL                                              *
.* Description    : Post the booking details to the BOKRC1FD                  *
.* Date           : 08/11/1990                                                *
.* Author         : Paul Marshall                                             *
.******************************************************************************
.* Parameters     : Booking details                                           *
.*                  Booking number                                            *
.*                  OPCNDFAD      Sector 42 position 92                       *
.*                  SURINCH       surgeon in charge of session                *
.* Returns        : EXIT = 1      booking record already exists               *
.******************************************************************************
.* Modifications  :                                                           *
.*        V10.05.01 19/09/2014 Jill Parkinson CAR 304288                      *
.*                  Added re-read of websecaf in DWLPC00 so correct wbseuid   *
.*                  is in context                                             *
.*        V10.03.01 21/10/2013  Ania P            CAR 263655                  *
.*                  Added DWLPC000.                                           *
.*        V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                  *
.*                  Added HL7TRGID & HL7INCLD setting for all broadcast msgs  *
.******************************************************************************
.*        V9.12.01  27/10/2009 Davin         CAR 207399                       *
.*                  Added code to default watmesaf comments to oprdedaf       *
.*                  (comment type 011)                                        *
.*        V9.11.01  17/02/2009 Peter Vela    CAR 183976                       *
.*                  Moved spaces to BKRXPOEC before WRBKRX11                  *
.*        V9.05.B01 31/01/2006 Peter Vela    CAR 89644                        *
.*                  Moved SP70 to BKRXLUPD,BKRXLUPT,BKRXWEBU at OPOSB400      *
.*        V9.02.06  04/04/2003 Jill Habasque SRF 37938                        *
.*                  Fixed BKRXCRDT pack                                       *
.*        V9.02.05  10/07/2002 Ebon Clements SRF 18421                        *
.*                  Changed OPOSB910 to pack KEY8 with BKREBOOK not AADMNO    *
.*                  because the admission file is not read in ADDBOK00        *
.*        V9.02.04  09/05/2002 Ebon Clements                                  *
.*                  Fixed value given to ADATE should be 8 chars not 6        *
.*        V9.02.03  22/04/2002 Jill Habasque                                  *
.*                  Added return on DWLC0000                                  *
.*        V9.02.02  13/12/2001 Ebon Clements                                  *
.*                  Do not set BKREADAT to todays date if it is not blank     *
.*                  as it is set to the session date OPSEDATE in OPRWEB01     *
.*        V9.02.01  28/08/2001   Mike Laming    RCH/SVHM                      *
.*                  Add API for Datagate Pre-Admission Booking DGCLICUB       *
.*        V?.00.00  18/02/91 Graeme Williams                                  *
.*                  Added booking date audit file                             *
.*                  25/10/1991    Justin Coates                               *
.*                  Check if to default attending doctor.                     *
.*                  25/10/92 J.Tan  SRF 117585                                * 
.*                  Added a system parameter for booking date audit file      *
.*                  13/01/93 J.Tan  SRF 119861                                *
.*                  Changed the indicator for booking date audit              *
.*                  26/10/1993    Gabrielle      HCOA Mods/Bugs                *
.*                  Recompiled for "priority 1" fixes for HCOA                 *
.*        V6.01.01  10/06/94 J.Tan   SRF 129846                               *
.*                  Mods expected due time to link with adm.time for pre-adm. *
.*        V6.01.02  27/06/94      Gabrielle     SRF 130153                    *
.*                  Update ALACDTE when Admission date after altering the     *
.*                  expected due date                                         *
.*        V6.02.01  03/11/94 J.Tan   SRF 133235                               *
.*                  Mods to update visit date as admission date changed       *
.*        V9.00.00  10/06/01 Bronko Sosic                                     *
.*                  Added a write to the booking extention file and           *
.*                  DWLC0000 which writes comments away to the waiting list   *
.******************************************************************************
OPPSTBOK  PACK      KEY8,BKREBOOK
.
          CALL      RABKREC1
          BRANCH    OVRCD,OPOSB200
          GOTO      OPOSB900
.
OPOSB200  MATCH     SP70,BKREADAT
          IF        @EQUAL
            CALL      IBACLOCK
            PACK      BKREADAT,CCC,CYY,CMM,CDD
            REP       " 0",BKREADAT           * date of booking
          ENDIF
.
.         see if to default attending doctor to surgeon in charge
.
          COMPARE   ONE,OPCNDFAD
          GOTO      OPOSB400 IF NOT EQUAL   * not using default facility
.
          MATCH     SP6,BKREADOC
          GOTO      OPOSB400 IF NOT EQUAL   * entered attending doctor
.
          MOVE      SURINCH,BKREADOC        * set attending doctor
.
OPOSB400  CALL      WRBKREC1
          CALL      WRCB0000     * write booking change audit
.
          MOVE      BKREBOOK,BKRXBNUM
          PACK      KEY8,BKRXBNUM,SP70
          CALL      RABKRX11 
          IF        OVRCD=1
            MOVE      USERID,BKRXWEBC   *  WEB User Id rec. updater
            PACK      BKRXCRDT,CCC,CYY,CMM,CDD
            REP       " 0",BKRXCRDT
            CLOCK     TIME,BKRXCRTM
.           MOVE      BKRXCRDT,BKRXLUPT
.           MOVE      BKRXCRTM,BKRXLUPT
.           MOVE      BKRXWEBC,BKRXWEBU
            MOVE      SP70,BKRXLUPD
            MOVE      SP70,BKRXLUPT
            MOVE      SP70,BKRXWEBU
            MOVE      SP70,BKRXPOEC
            CALL      WRBKRX11 
.                                           * Datagate API             *I-90201
            MOVE      ONE,HL7TRGID
            MOVE      "OPWEBPST",HL7INCLD
            PROC      DGCLICUB              * Pass Pre-Admit Booking   *I-90201
.
            IF        USEWATMT=0            
              CALL      DWLC0000            * Default waiting list comments
            ELSE
              CALL      DWLPC000            * Default W/L Preadm comments
            ENDIF
          ENDIF
.
.         see if to post to booking audit file
.
          COMPARE   ONE,OPCNBKBD
          GOTO      OPOSB460 IF NOT EQUAL   * no booking date audit
          OPEN      BOKBUDA1,"bokbudaf"     * open booking date audit file
          CALL      RABKBUD1                * see if record exists
          COMPARE   ZERO,OVRCD
          GOTO      OPOSB450 IF EQUAL       * record exist
.
          CALL      WRBKBUD1                * write to booking date audit file
.
OPOSB450  CLOSE     BOKBUDA1
.
OPOSB460  MOVE      ZERO,EXIT
          GOTO      OPOSB910
.
OPOSB900  MOVE      ONE,EXIT
.
OPOSB910  PACK      KEY8,BKREBOOK
          CALL      RDMISS1
          IF        OVRCD = ZERO
            IF        ASTAT = 1
              MOVE      BKREEDAT,ADATE
              MOVE      BKREEDAT,ALACDTE
              MOVE      SP10,ATIME
              UNPACK    BKREATIM,ATIME
              CALL      UPLMISS1
              MOVE      BKREBOOK,PVIBILL
              MOVE      PVIBILL,KEY8
              CALL      RDVISA1
              IF        OVRCD = ZERO
                MOVE      BKREEDAT,PVIDATE        * update visit date
                CALL      UPVISA1
              ENDIF
            ENDIF
          ENDIF
.
OPOSB999  RETURN
.-----------------------------------------------------------------------------
.        Default waiting list comments
.-----------------------------------------------------------------------------
DWLC0000  MATCH       SP70,WATTRKEY
          GOTO        DWLC9999 IF EQUAL
.
          PACK        KEY21,WATTRKEY,SP70
          CALL        RSWTPAC1
DWLC1000  CALL        RKWTPAC1
          BRANCH      OVRCD,DWLC5000
.
          PACK        KEY20,WTPCURNO,WTPCPCOD,WTPCPCNT,SP70
          MATCH       WATTRKEY,KEY20
          IF          !@EQUAL
            GOTO        DWLC5000
          ENDIF
.
          MOVE        "002",KEY3
          PACK        KEY16,OPDAUNIQ,KEY3,SP1,WTPCLINE,SP70
          CALL        RDOPDED1
          IF          OVRCD=1
            MOVE        OPDAUNIQ,OPDDUNIQ
            MOVE        "002",OPDDTYPE
            PACK        OPDDLINE,SP1,WTPCLINE
            MOVE        WTPCCOMM,OPDDDATA
            CALL        WROPDED1
          ENDIF
          GOTO        DWLC1000
.
DWLC5000  PACK        KEY21,WATTRKEY,SP70
          CALL        RDSMESA1
DWLC6000  CALL        RDKMESA1
          BRANCH      OVRCD,DWLC9999
.
          PACK        KEY20,WAURNO,WAPROC,WACNT,SP70
          MATCH       WATTRKEY,KEY20
          IF          !@EQUAL
            GOTO        DWLC9999
          ENDIF
.
          MOVE        "011",KEY3
          PACK        KEY16,OPDAUNIQ,KEY3,SP1,WACNT2,SP70
          CALL        RDOPDED1
          IF          OVRCD=1
            MOVE        OPDAUNIQ,OPDDUNIQ
            MOVE        "011",OPDDTYPE
            PACK        OPDDLINE,SP1,WACNT2
            MOVE        WATEXT,OPDDDATA
            CALL        WROPDED1
          ENDIF
          GOTO        DWLC6000
.
DWLC9999  RETURN
.
.------------------------------------------------------------------------------
.        Default waiting list comments & medication notes
.------------------------------------------------------------------------------
DWLPC000  MATCH     SP70,WATTRKEY
          GOTO      DWLPC999 IF EQUAL
.
          MOVE      "002",KEY3                      * Only comment type 002
          PACK      KEY16,OPDAUNIQ,KEY3,Z70
          CALL      RSOPDED1
          CALL      RPOPDED1
.
          MATCH     OPDAUNIQ,OPDDUNIQ
          IF        !@EQUAL
            MOVE      ONE,TOTALCOM                  * No comments exist yet
          ELSE
            MOVE      OPDDLINE,TOTALCOM             * If some comments already
            ADD       ONE,TOTALCOM                  * exist, increment last 
          ENDIF                                     * comment number by 1
.
          MOVE      TOTALCOM,DIM3
          PACK      KEY19,WATTRKEY,SP70
.
......... Read/write comment header ...........................................
.
          PACK      KEY28,KEY19,KEY3,SP70
          CALL      RSWTMHD1
DWLPC100  CALL      RKWTMHD1
          BRANCH    OVRCD,DWLPC999
.
          PACK      KEY19,WTMHURNO,WTMHPROC,WTMHPCNT,SP70
          MATCH     WATTRKEY,KEY19
          GOTO      DWLPC999 IF NOT EQUAL
.
          MATCH     "002",WTMHTYPE
          GOTO      DWLPC999 IF NOT EQUAL          * Only interested in type 002
.		  
          MATCH     "1",WTMHDELT                      
          GOTO      DWLPC100 IF EQUAL              * Skip deleted comments
.
          PACK      KEY10,WTMHUSER,SP70
          CALL      RDWBSE1
          IF        OVRCD=0
            PACK      KEY30,WBSENAM,SP70
          ELSE
            PACK      KEY30,WTMHUSER,SP70
          ENDIF
.
          UNPACK    WTMHDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY70,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,SP1,WTMHTIME,SP1:
                    HYPHEN,SP1,KEY30,SP70
.
          MOVE      OPDAUNIQ,OPDDUNIQ
          MOVE      "002",OPDDTYPE
          MOVE      DIM3,OPDDLINE
          PACK      OPDDDATA,KEY70
          CALL      WROPDED1
          ADD       ONE,TOTALCOM
          MOVE      TOTALCOM,DIM3
.
......... Read/write individual comment lines for this note ..................
.
          PACK      KEY31,WTMHURNO,WTMHPROC,WTMHPCNT,WTMHTYPE,WTMHNOTE,SP70
          CALL      RSWTMTX1
DWLPC200  CALL      RKWTMTX1
          BRANCH    OVRCD,DWLPC100
.
          MATCH     WTMHURNO,WTMTURNO
          GOTO      DWLPC100 IF NOT EQUAL
.
          MATCH     WTMHPROC,WTMTPROC
          GOTO      DWLPC100 IF NOT EQUAL
.
          MATCH     WTMHPCNT,WTMTPCNT
          GOTO      DWLPC100 IF NOT EQUAL
.
          MATCH     WTMHTYPE,WTMTTYPE
          GOTO      DWLPC100 IF NOT EQUAL
.
          MATCH     WTMHNOTE,WTMTNOTE
          GOTO      DWLPC100 IF NOT EQUAL
.
          MOVE      OPDAUNIQ,OPDDUNIQ
          MOVE      "002",OPDDTYPE
          MOVE      DIM3,OPDDLINE
          MOVE      WTMTCMNT,OPDDDATA
          CALL      WROPDED1
          ADD       ONE,TOTALCOM
          MOVE      TOTALCOM,DIM3
          GOTO      DWLPC200
.
DWLPC999  PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
.
          RETURN
.
