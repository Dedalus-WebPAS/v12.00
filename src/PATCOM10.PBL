.----------------------------------------------------------------------
.         Common codes for Auto/Bulk Merge PMI-NHI 
.
. Modifications
.----------------------------------------------------------------------
. V10.11.03 21/09/2017  Thanh T.       TSK 0821710
.           Audit Amission/outpatient visit comments
. V10.11.02 18/09/2017  Thanh T.       TSK 0821710
.           Audit Amission/outpatient visit comments
. V10.11.01 13/09/2017  Thanh T.       TSK 0821710
.           Audit Amission/outpatient visit comments
.----------------------------------------------------------------------
.
PATCOM10  MOVE      OLDURNO,KEY8
          CALL      CKURCM00
          COMPARE   ZERO,COMTFLAG           * No comments, exit
          GOTO      PATCOM18 IF EQUAL
.
          CALL      MXURCM00                * get maximum note number
          CALL      MRURCM00              
.
PATCOM18  CLOSE     PMSUCHA1
          CLOSE     PMSUCDA1
.
PATCOM19  RETURN
.
.----------------------------------------------------------------------
. check if UR comment exists
.----------------------------------------------------------------------
CKURCM00  MOVE      ZERO,COMTFLAG
          PACK      KEY17,KEY8,URCMTTYP,Z70      * Key for start of comment
          CALL      RSPMUCH1
CKURCM20  CALL      RPPMUCH1
          BRANCH    OVRCD,CKURCM99
.
          MATCH     KEY8,PMUHURNO
          GOTO      CKURCM99 IF NOT EQUAL
.
          MATCH     URCMTTYP,PMUHCTYP
          GOTO      CKURCM99 IF NOT EQUAL
.
          MOVE      ONE,COMTFLAG
.
CKURCM99  RETURN
.
.----------------------------------------------------------------------
. get the maximum note number of the comment header
.----------------------------------------------------------------------
MXURCM00  MOVE      ZERO,MAXNOTEN
          PACK      KEY17,NEWURNO,URCMTTYP,Z70      * Key for start of comment
          CALL      RSPMUCH1
          CALL      RPPMUCH1
          BRANCH    OVRCD,MXURCM99
.
          MATCH     NEWURNO,PMUHURNO
          GOTO      MXURCM99 IF NOT EQUAL
.
          MATCH     URCMTTYP,PMUHCTYP
          GOTO      MXURCM99 IF NOT EQUAL
.
          MOVE      PMUHCNUM,MAXNOTEN
.
MXURCM99  RETURN
.
.----------------------------------------------------------------------
. Move the old comments to the end of the new comments with the new 
. note number and the old comment will be deleted.
.----------------------------------------------------------------------
MRURCM00  PACK      KEY17,OLDURNO,URCMTTYP,SP70
MRURCM10  CALL      RSPMUCH1
          CALL      RKPMUCH1
          BRANCH    OVRCD,MRURCM99
.
          MATCH     OLDURNO,PMUHURNO
          GOTO      MRURCM99 IF NOT EQUAL
.
          MATCH     URCMTTYP,PMUHCTYP
          GOTO      MRURCM99 IF NOT EQUAL
.
          PACK      SAVKEY17,PMUHURNO,PMUHCTYP,PMUHCNUM,SP70
          MOVE      SAVKEY17,KEY17
.
. Active comment header found
.
          ADD       ONE,MAXNOTEN    
          CALL      MRURCT00
.
          MOVE      NEWURNO,PMUHURNO
          MOVE      MAXNOTEN,PMUHCNUM
          PACK      KEY17,PMUHURNO,PMUHCTYP,PMUHCNUM
          CALL      UPPMUCH1
.
          MOVE      SAVKEY17,KEY17
          GOTO      MRURCM10
.
MRURCM99  RETURN
.
.---------------------------------------------------------------------
. Write new comment text details
.---------------------------------------------------------------------
MRURCT00  PACK      KEY20,PMUHURNO,PMUHCTYP,PMUHCNUM,SP70
MRURCT10  CALL      RSPMUCD1
          CALL      RKPMUCD1
          BRANCH    OVRCD,MRURCT99
.
          MATCH     PMUHURNO,PMUCURNO
          GOTO      MRURCT99 IF NOT EQUAL
.
          MATCH     PMUHCTYP,PMUCCTTY
          GOTO      MRURCT99 IF NOT EQUAL
.
          MATCH     PMUHCNUM,PMUCCNUM
          GOTO      MRURCT99 IF NOT EQUAL
.
          PACK      SAVKEY20,PMUCURNO,PMUCCTTY,PMUCCNUM,PMUCLNUM,SP70
.
          MOVE      NEWURNO,PMUCURNO
          MOVE      MAXNOTEN,PMUCCNUM
          PACK      KEY20,PMUCURNO,PMUCCTTY,PMUCCNUM,PMUCLNUM
          CALL      UPPMUCD1
.
          MOVE      SAVKEY20,KEY20
          GOTO      MRURCT10
.
MRURCT99  RETURN
.
