.------------------------------------------------------------
. Standard Coded Field Option 
.
.  Parameters    CODE & CATAGORY
.
.  Tag Parameter 0=Code Description
.                1=Code
.                2=Selection List
.                3=Category Description
.------------------------------------------------------------
LCDITM00  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,LCDITM10,LCDITM20,LCDITM30,LCDITM40
          CALL      LGETCD00
          WRITE     HTMLFILE;LTDESC;
          GOTO      LCDITM99
.
LCDITM10  WRITE     HTMLFILE;CODE;
          GOTO      LCDITM99
.
LCDITM20  CALL      LCDSEL00
          GOTO      LCDITM99
.
LCDITM30  CALL      LGETCT00
          GOTO      LCDITM99
.
LCDITM40  CALL      LGETCD00
          WRITE     HTMLFILE;LACODE,LTCINDC1,LTCINDC2,LTCINDC3,LTCINDC4:
                                    LTCINDC5,LTCINDC6,LTCINDC7,LTCINDC8:
                                    LTCINDC9,LTCNDC10,LTCNDC11,LTHCSCOD:
                                    LPCDNHCP,LTCFORM6;
          GOTO      LCDITM99
.
LCDITM99  RETURN
.------------------------------------------------------------
. Get Code Description
.------------------------------------------------------------
LGETCD00  MOVE      SP70,LTDESC
          MATCH     SP70,CODE
          GOTO      LGETCD99 IF EQUAL
          PACK      KEY5,CATEGORY,CODE
          CALL      RDLGCOD1
          IF        OVRCD=1
            MOVE      SP70,LTDESC
          ENDIF
LGETCD99  RETURN
.------------------------------------------------------------
. Get Category Description
.------------------------------------------------------------
LGETCT00  MOVE      SP70,LTDESC
          PACK      KEY5,CATEGORY,SP70
          CALL      RDLGCOD1
          IF        OVRCD=1
            MOVE      SP70,LTDESC
          ENDIF
          WRITE     HTMLFILE;LTDESC;
LGETCT99  RETURN
.------------------------------------------------------------
. Add Selection Options to Form (code value)
.------------------------------------------------------------
LCDSEL00  MOVE      ONE,EXIT
          PACK      KEY25,CATEGORY,SP70
          CALL      RSLGCOD2
LCDSEL10  CALL      RKLGCOD2
          BRANCH    OVRCD,LCDSEL99
          MATCH     CATEGORY,LTCODE
          GOTO      LCDSEL99 IF NOT EQUAL
          MATCH     SP70,LACODE
          GOTO      LCDSEL10 IF EQUAL
          MATCH     "I",LPCOACTV
          GOTO      LCDSEL10 IF EQUAL
          PACK      KEY10,LTCINDC1,LTCINDC2,LTCINDC3,LTCINDC4,LTCINDC5:
                          LTCINDC6,LTCINDC7,LTCINDC8,LTCINDC9,LTCNDC10
          PACK      KEY15,QUOTE,LACODE,KEY10,QUOTE
          MATCH     CODE,LACODE
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY15," selected>":
                               LTDESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY15,">",LTDESC,"</option>"
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      LCDSEL10
LCDSEL90  IF        EXIT=1
            MOVE      ZERO,EXIT
            WRITE     HTMLFILE;"<option>No Active Codes for Category - ":
                               CATEGORY,"</option>"
          ENDIF
LCDSEL99  RETURN
.------------------------------------------------------------
. Add Selection Options to Form (code and indicator value)
.------------------------------------------------------------
LCDIND00  PACK      KEY25,CATEGORY,SP70
          CALL      RSLGCOD2
LCDIND10  CALL      RKLGCOD2
          BRANCH    OVRCD,LCDIND99
          MATCH     CATEGORY,LTCODE
          GOTO      LCDIND99 IF NOT EQUAL
          MATCH     SP70,LACODE
          GOTO      LCDIND10 IF EQUAL
          MATCH     "I",LPCOACTV
          GOTO      LCDIND10 IF EQUAL
.
          MOVE      ZERO,FORM2
LCDIND20  ADD       ONE,FORM2
          COMPARE   SIX,FORM2 
          GOTO      LCDIND40 IF NOT LESS     
.
          LOAD      KEY1,FORM2,LTCINDC1,LTCINDC2,LTCINDC3,LTCINDC4,LTCINDC5
.
          MATCH     "W",KEY1
          IF        @EQUAL
            PACK      KEY6,QUOTE,LACODE,KEY1,QUOTE
            GOTO      LCDIND30
          ENDIF
.
          MATCH     "M",KEY1
          IF        @EQUAL
            PACK      KEY6,QUOTE,LACODE,KEY1,QUOTE
            GOTO      LCDIND30
          ENDIF
.
          MATCH     "V",KEY1 
          IF        @EQUAL
            PACK      KEY6,QUOTE,LACODE,KEY1,QUOTE
            GOTO      LCDIND30
          ENDIF
          GOTO      LCDIND20
.
LCDIND30  MATCH     CODE,LACODE
          IF        @EQUAL 
            WRITE     HTMLFILE;"<option value=",KEY6," selected>":
                               LTDESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY6,">",LTDESC,"</option>"
          ENDIF
          GOTO      LCDIND10
.
LCDIND40  PACK      KEY6,QUOTE,LACODE,SP1,QUOTE
          MATCH     CODE,LACODE
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY6," selected>":
                               LTDESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY6,">",LTDESC,"</option>"
          ENDIF
          GOTO      LCDIND10
LCDIND99  RETURN
.
