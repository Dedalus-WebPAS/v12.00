+
.******************************************************************************
.
.         PATGOVDS.PBL
.         ------------
.         This include displays entries in Patient Government file.
.
.     Modifications: 28/11/89 Graeme Williams
.                    Added the parameter CNEWDS to determine if the include
.                    is to use the new standards for "?" routines
.
.------------------------------------------------------------------------------
.         Display Category Heading, and then loop over entries
.------------------------------------------------------------------------------
.
PATGOVDS  DISPLAY   *P1:3,*EF,*P20:4,*V2LON,*ULON,"GOVERNMENT CODES"
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
.
          PACK      KEY6,SP6
          CALL      RDSGOVR1
PDSG1000  CALL      RDKGOVR1
          BRANCH    OVRCD,PDSG2000
.
          DISPLAY   *PCCOL:CVERT,*V2LON,GCODE,SP1,*HOFF,GNAME
.
          ADD       ONE,CVERT
          COMPARE   TWENTY4,CVERT
          GOTO      PDSG1000 IF LESS
.
          MOVE      FIVE,CVERT
          ADD       "40",CCOL
          COMPARE   "70",CCOL
          GOTO      PDSG1000 IF LESS
.
.         Check for new standards question, or old question
.
          BRANCH    CNEWDS,DSGOV200
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSE,ANS
          GOTO      PDSG3000 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATGOVDS IF EQUAL
.
          GOTO      DSGOV400
.
.         Use the new standards question
.
DSGOV200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSC,ANS
          GOTO      PDSG3000 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATGOVDS IF EQUAL
.
DSGOV400  MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
          GOTO      PDSG1000
.
.         End of codes
.
PDSG2000  BRANCH    CNEWDS,PDSG3000
.
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
.
PDSG3000  MOVE      ZERO,OVRCD
          RETURN
.
