.******************************************************************************
.*  Include Name  : PATDRGKY.PBL                                              *
.*  Description   : Keyin of identification code                              *
.*  Author        : Ian Rutt                                                  *
.*                                                                            *
.*  Parameters    : The varibles required for KEYPER                        *
.*                  CKYIMODE - 1 for insert, 0 otherwise                      *
.*                  CKYIDEF2 - Default Value (Blank if no default)            * 
.*                                                                            *
.*  Returns       : IMON,IYEAR,ICENT     (forms)                              *
.*                  CMON,CYEAR,CCENT     (dim)                                *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.******************************************************************************
PATDRGKY
PTDRKY00  MATCH     SP2,CKYIDEF2
          CALL      IBACLOCK
          MOVE      CCC,CCENT
          IF        @EQUAL
            UNPACK    SP4,CYEAR,CMON           
          ENDIF
          MOVE      ZERO,CHIGHLT 
          CALL      KEYPER
          MOVE      CCOL,ECOL
          MOVE      CVERT,EVERT
.
.         check what was entered
.
PTDRKY20  CALL      DRGCK000
          BRANCH    EXIT,PTDRKY25,PTDRKY31,PTDRKY41,PTDRKY95
.                        "?"      valid    nothing  exitchar
          GOTO      PTDRKY00
.
.         a "?" was input
.
PTDRKY25  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
PTDRKY26  CALL      PATDRGDS
          BRANCH    CNEWDS,PTDRKY27 
.
          CALL      PUTSCR00                * redisplay screen
          GOTO      PTDRKY00                * rekey code
.
.         now ask for code while leaving displayed codes on screen
.
PTDRKY27  
          DISPLAY   *P1:24,*EL,"Date : "
          MOVE      "24",CVERT
          MOVE      "8",CCOL
          UNPACK    SP4,CYEAR,CMON
          MOVE      ZERO,CHIGHLT
          CALL      KEYPER
.          DISPLAY   *PECOL:EVERT,IMON,SLASH,ICENT,IYEAR
.
.         repeat the validation of the code
.         check what was entered
.
PTDRKY29  CALL      DRGCK000
          BRANCH    EXIT,PTDRKY26,PTDRKY30,PTDRKY40,PTDRKY90
.                        "?"      valid    nothing  exitchar
.
          GOTO      PTDRKY27
.
.         a valid code was entered 
. 
PTDRKY30  CALL      PUTSCR00                      * restore screen
          REP       " 0",CMON
          REP       " 0",CCENT
          REP       " 0",CYEAR
          DISPLAY   *PECOL:EVERT,*V2LON,CMON,SLASH,CCENT,CYEAR;
.
PTDRKY31  MOVE      FALSE,EXIT                    * valid input
          GOTO      PTDRKY99 
.
.         nothing was input
.
PTDRKY40  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON;
.
PTDRKY41  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      PTDRKY99 
.
.         EXITCHAR input
.
PTDRKY90  CALL      FRESCR00                * free the screen saved
.
PTDRKY95  MOVE      TWO,EXIT                * EXITCHAR
.
PTDRKY99  RETURN
+
.*********************************************************************
.*                  DRGCK000                    Called by : PATDRGKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
DRGCK000  BRANCH    CQUEST,DRGCK800
.
          BRANCH    OVRCD,DRGCK700
.
.         a code was entered so validate
. 
DRGCK500  PACK      KEY6,ICENT,IYEAR,IMON,SP6
          REP       " 0",KEY6
          CALL      RDDRGA1
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,DRGCK900          * invalid code
          ELSE
            COMPARE   ZERO,OVRCD   
            GOTO      DRGCK900 IF EQUAL
          ENDIF  
.
          MOVE      TWO,EXIT                * valid code
          GOTO      DRGCK999
.
.         exitchar entered
.
DRGCK600  MOVE      FOUR,EXIT
          GOTO      DRGCK999
.
.         nothing entered
.
DRGCK700  BRANCH    CKYIMAND,DRGCK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          GOTO      DRGCK999
.
.         "?" entered
.
DRGCK800  MOVE      ONE,EXIT
          GOTO      DRGCK999
.
.         invalid code entered
.
DRGCK900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
.
          IF        CKYIMODE = 0
            DISPLAY   *P1:24,*EL,*B,"Invalid Date Range. ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      DRGCK999
.
.         field is mandatory
.
DRGCK950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is Mandatory. ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
DRGCK999  RETURN
.
          INCLUDE   PATDRGDS
+
