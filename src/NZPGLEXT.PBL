.
.        V11.03.01 12/01/2023  J.Tan          TSK 0900145
.                  Mod Win/Loss for each procedure, setup PTDTCPRC
.        V11.01.01 11/05/2021 J.Tan   TSK 0902898
.                  Fixed to extract Adjus.fees (PTDTADPT) with no Rev.Breakdown
.        V10.04.04 13/05/2014 J.Tan   CAR 300955
.                  Fixed to extract Adjustment fees (PTDTADPT)
.        V10.04.03 07/05/2014 J.Tan  CAR 300260
.                  Mods checking previous record after finding Inactive Item
.        V10.04.02 22/04/2014 J.Tan          CAR 300260
.                  Mods checking Active/Inactive Misc.Charge
.        V10.04.01 15/04/2014 J.Tan   CAR 299278
.                  Mods checking effective date for Pack items
.        V10.03.03 23/10/2012 J.Tan   CAR 274919
.                  Mods to Credit Note flags
.        V10.03.02 14/09/2012 J.Tan   CAR 273034
.                  Fixed Invoice and Credit Note flags
.        V10.03.01 07/03/2010 J.Tan   CAR 250477
.                  Mods for Pack items
.        V10.02.01 12/10/2010 J.Tan   CAR 249242
.                  Mods to set Credit Note status
.        V10.00.02 30/09/2010 J.Tan   CAR 230735
.                  Fixed Adjustment fees
.        V10.00.01 12/05/2010 J.Tan   CAR 221434
.                  Mods checking for zero amt for Accomm. or Theatre only
.         V9.10.02 19/06/2008 J.Tan   CAR 170222
.                  Mods to use admission and invoice no fields from invoice file
.         V9.10.01 11/06/2008 J.Tan   CAR 170222
.                  Mods to save credit notes no to nzexrcno for GST
.         V9.08.00 25/07/2007 J.Tan   CAR 140282
.                  Created new include to Extract Credit Note for SX
.
.*******************************************************************************
.         Routine for NZ Private G/L Extract Credit Note
.*******************************************************************************
.
          DEFPROC   NZPGLEXT
.
          INC       NZPEXTFD/INC
          INC       NZPRVBFD/INC
          INC       PATRE1FD/INC
          INC       NZPMCHFD/INC
          INC       NZPMXCFD/INC
.
SAVFMHOS  DIM       3
SAVFMSUB  DIM       1
SAVFMCOD  DIM       13
KEY29A    DIM       29
RECCNT    FORM      3
DIM37     DIM       37
SKEY37    DIM       37
.
          OPEN      NZPEXTA2,"nzpextaf"
          OPEN      NZPRVBA1,"nzprvbaf"
          OPEN      PATRE1A1,"patre1af"
          OPEN      OPRDETA2,"oprdetaf"
          OPEN      NZPMCHA1,"nzpmchaf"
          OPEN      NZPMXCA1,"nzpmxcaf"
.
GLXT0000  CALL      CLNZPEXT
.
          MOVE      PINVADM,KEY8
          CALL      RDRESP1
          IF        OVRCD=1
            PACK      PKNAME,SP70,SP10               * PRFA Name
          ENDIF
          MOVE      PINVDTE,NZEXTDAT
          REP       " 0",NZEXTDAT
.
          MOVE      "3",NZEXSYST
          PACK      NZEXBNAM,PKNAME,SP70        * PRFA
          MOVE      PMVXMHOS,NZEXHCOD           * Hospital code
          MOVE      PINVUR,NZEXURNO
          MOVE      PINVADM,NZEXADMN
          MOVE      PINVNO,NZEXINVN
.
          BRANCH    UPGSTFLG,GLXT8800          * Update GST credit note only
.
          COMPARE   FOUR,UPDATETY              * Credit by item?
          GOTO      GLXT0800 IF NOT EQUAL      * No, must be Credit all
.
.         Credit note by item - Check GST on the item
.
          COMPARE   ZERO,PTDTGSTM
          GOTO      GLXT0800 IF EQUAL
.
          MOVE      "8",NZEXRTYP
          CLEAR     NZEXDESC
          APPEND    "GST",NZEXDESC
          APPEND    SP70,NZEXDESC
          RESET     NZEXDESC
          MOVE      PTCNAGST,NZEXGSTC           * GST Code
          MOVE      PTDTGSTM,GLAMOUNT
          MOVE      "000000",NZEXTRNO
          PACK      NZEXRCNO,PTCNCRNO,SP70      * Save the credit note number
          CALL      SFLG0000                    * Set Invoice/Credit Note flag
          CALL      WEXT0000                    * Write to Extract file
.
          MOVE      TPATAMTT,GLAMOUNT           * Item GST Amount
          MOVE      SP70,NZEXGSTC
          GOTO      GLXT0900
.
GLXT0800  IF        TRECTYPE=1 | TRECTYPE=2
            COMPARE   ZERO,GLAMOUNT
            GOTO      GLXT9999 IF EQUAL
          ENDIF
.
GLXT0900  MOVE      TTRANSN1,NZEXTRNO
          BRANCH    TRECTYPE,GLXT1000,GLXT2000,GLXT3000,GLXT9999:
                             GLXT9999,GLXT6000,GLXT7000,GLXT8000
          GOTO      GLXT9999
.
GLXT1000  MOVE      "1",NZEXRTYP
          CLEAR     NZEXDESC
          APPEND    "Accommodation",NZEXDESC
          APPEND    SP70,NZEXDESC
          RESET     NZEXDESC
          PACK      NZEXRREF,PTDTCPRC,SP70       * Procedure code
          GOTO      GLXT9000
.
GLXT2000  MOVE      "2",NZEXRTYP
          CLEAR     NZEXDESC
          APPEND    "Theatre Charge",NZEXDESC
          APPEND    SP70,NZEXDESC
          RESET     NZEXDESC
          PACK      NZEXRREF,PTDTCPRC,SP70       * Procedure code
          GOTO      GLXT9000
.
GLXT3000  MOVE      "3",NZEXRTYP
          CLEAR     NZEXDESC
          APPEND    "FI ",NZEXDESC
          APPEND    TMISGRP,NZEXDESC
          APPEND    SP1,NZEXDESC
          APPEND    TITEMNO,NZEXDESC
          APPEND    SP70,NZEXDESC
          RESET     NZEXDESC
          PACK      NZEXRREF,PTDTCPRC,SP70       * Procedure code
          GOTO      GLXT9000
.
GLXT6000  MOVE      "6",NZEXRTYP
          CLEAR     NZEXDESC
          APPEND    TTYPE,NZEXDESC
          APPEND    SP1,NZEXDESC
          APPEND    TDDESC,NZEXDESC
          APPEND    SP70,NZEXDESC
          RESET     NZEXDESC
          PACK      NZEXRREF,PTDTCPRC,SP70       * Procedure code
          PACK      NZEXTDAT,TFCENT,TFYEAR,TFMONTH,TFDAY
          REP       " 0",NZEXTDAT
          ADD       PTDTGSTM,GLAMOUNT
          GOTO      GLXT9000
.
GLXT7000  MOVE      "7",NZEXRTYP
          CLEAR     NZEXDESC
          APPEND    "Rounding",NZEXDESC
          APPEND    SP70,NZEXDESC
          RESET     NZEXDESC
          GOTO      GLXT9000
.
GLXT8000  MOVE      "4",NZEXRTYP
          CLEAR     NZEXDESC
          APPEND    "Procedure",NZEXDESC
          APPEND    SP70,NZEXDESC
          RESET     NZEXDESC
          PACK      NZEXRREF,PTDTCPRC,SP70     * Procedure code
          PACK      NZEXPREF,TMISGRP,SP70      * Save item group
.
.         Check Adjustment fees
.
          IF        PTDTADPT<>0
            MOVE      PTDTADPT,GLAMOUNT
            MULT      "-1",GLAMOUNT
            PACK      NZEXRCNO,PTCNCRNO,SP70   * Save the credit note number
            CALL      SFLG0000                 * Set Invoice/Credit Note flag
            CALL      WEXT0000                 * Write to Extract file
            MOVE      ZERO,GLAMOUNT
          ENDIF
.
.         If this is a Fixed fee for Procedure, check for Revenue breakdown
.
          MOVE      ZERO,FORM12P2
          MOVE      PTDTLSPT,FORM12P2
          ADD       PTDTLSRB,FORM12P2
          COMPARE   ZERO,FORM12P2
          GOTO      GLXT8100 IF NOT EQUAL      * a fixed fee
.
          COMPARE   ZERO,PTDTADPT
          GOTO      GLXT9000 IF EQUAL          * Adjustment fee
          GOTO      GLXT9999
.
GLXT8100  CALL      REVB0000                   * Check for Revenue breakdown
.         BRANCH    EXIT,GLXT9999              * Found the breakdown
.
.         Check if the sum of Revenue breakdown is equal to total fixed fee
.
          COMPARE   ZERO,EXIT
          GOTO      GLXT8200 IF NOT EQUAL
.
.         No revenue breakdown
.
          IF        PTDTADPT<>0
            MOVE      FORM12P2,GLAMOUNT
            MULT      "-1",GLAMOUNT
          ENDIF
          GOTO      GLXT9000                   * no revenue breakdown
.
GLXT8200  COMPARE   FORM12P2,F12P2
          GOTO      GLXT9999 IF EQUAL
.
          SUB       F12P2,FORM12P2
          MOVE      FORM12P2,GLAMOUNT          * adjustment fees
          MULT      "-1",GLAMOUNT
          GOTO      GLXT9000
.
GLXT8800  MOVE      "8",NZEXRTYP
          CLEAR     NZEXDESC
          APPEND    "GST",NZEXDESC
          APPEND    SP70,NZEXDESC
          RESET     NZEXDESC
          MOVE      PTCNAGST,NZEXGSTC           * GST Code
.
          MOVE      "000000",NZEXTRNO           * flag for Credit Note
.
.         Write a record to G/L Extract file
.
GLXT9000  PACK      NZEXRCNO,PTCNCRNO,SP70      * Save the credit note number
          CALL      SFLG0000                    * Set Invoice/Credit Note flag
          CALL      WEXT0000                    * Write to Extract file
          GOTO      GLXT9999
+
.******************************************************************************
.         Check if Revenue breakdown exist
.******************************************************************************
REVB0000  MOVE      ZERO,EXIT
          MOVE      ZERO,F12P2
          PACK      KEY22,NZEXADMN,NZEXINVN,PTDTSUNQ,SP70
          CALL      RSNZRVB1
REVB1000  CALL      RKNZRVB1
          BRANCH    OVRCD,REVB9999
.
          MATCH     NZEXADMN,NZRVADMN
          GOTO      REVB9999 IF NOT EQUAL     * Different admission
          MATCH     NZEXINVN,NZRVINVN
          GOTO      REVB9999 IF NOT EQUAL     * Different invoice
          MATCH     PTDTSUNQ,NZRVSUNQ
          GOTO      REVB9999 IF NOT EQUAL     * Different Uniq.number
.
          ADD       NZRVAMNT,F12P2
          MOVE      NZRVAMNT,GLAMOUNT
          MULT      "-1",GLAMOUNT
.
          PACK      NZEXPREF,NZRVBRCD,SP70      * Save revenue breakdown
          PACK      NZEXRCNO,PTCNCRNO,SP70      * Save the credit note number
          CALL      SFLG0000                    * Set Invoice/Credit Note flag
.
          CLEAR     NZEXDESC
          APPEND    "FI ",NZEXDESC
          APPEND    NZRVBRCD,NZEXDESC
          APPEND    SP1,NZEXDESC
          APPEND    TITEMNO,NZEXDESC
          APPEND    SP70,NZEXDESC
          RESET     NZEXDESC
          CALL      WEXT0000                    * Write to Extract file
.
          MOVE      ONE,EXIT
          GOTO      REVB1000

REVB9999  RETURN
+
.******************************************************************************
.         Write a record to G/L Extract file
.******************************************************************************
WEXT0000  MOVE      SP70,NZEXGLBT                * Spaces G/L Batch for Cr.Notes
          MOVE      GLAMOUNT,NZEXDAMT            * Debit Amount
          MOVE      GLAMOUNT,NZEXCAMT            * Credit Amount
          MULT      "-1",NZEXCAMT
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MOVE      KEY8,NZEXCDAT                * date created
          CLOCK     TIME,NZEXCTIM                * time created
          MOVE      PASSCODE,NZEXCUID            * user id
          MOVE      SP70,NZEXSPAR
.
          CALL      NZPAC000                   * Check if it's a pack item
          BRANCH    EXIT,WEXT9000
.
          PACK      KEY30,NZEXADMN,NZEXINVN,NZEXGLBT,NZEXHCOD,NZEXTRNO,SP70
          CALL      WRNZEXT2                     * Write a record
.
WEXT9000  MOVE      ZERO,EXIT
          GOTO      WEXT9999
.
WEXT9999  RETURN
+
.******************************************************************************
.         Check for Pack item
.******************************************************************************
NZPAC000  MOVE      ZERO,RECCNT
          COMPARE   "3",NZEXRTYP
          GOTO      NZPAC900 IF NOT EQUAL       * Not a misc.item
.
.         PACK      MISCDATE,NZEXTDAT,SP70
          PACK      MISCDATE,TFCENT,TFYEAR,TFMONTH,TFDAY
          REP       " 0",MISCDATE
          PACK      KEY9,TITEMNO,SP70
          CALL      GETNZM00
          BRANCH    EXIT,NZPAC900
.
          COMPARE   ONE,NZMCCHGT
          GOTO      NZPAC900 IF NOT EQUAL       * Not a pack item
.
          COMPARE   ZERO,NZMCPATP
          GOTO      NZPAC900 IF NOT EQUAL
.
          PACK      SKEY37,NZMCHOSP,NZMCCLMC,NZMCCNTR,NZMCFTAB,NZMCMCHG:
                          NZMCEFDT,SP70
          CALL      TPAC0000                    * Check total amount of pack itm
          BRANCH    EXIT,NZPAC9000
.
          PACK      KEY37,SKEY37,SP70
          CALL      RDNZMCH1                    * Reposition to pack item
          BRANCH    OVRCD,NZPAC900
.
          PACK      KEY40,SKEY37,SP70
          CALL      RSNZMXC1
NZPAC100  CALL      RKNZMXC1
          BRANCH    OVRCD,NZPAC800
.
          PACK      DIM37,NZMXHOSP,NZMXCLMC,NZMXCNTR,NZMXFTAB,NZMXMCHG:
                              NZMXEFDT,SP70
.
          MATCH     DIM37,SKEY37
          GOTO      NZPAC800 IF NOT EQUAL
.
          PACK      KEY9,NZMXITMN,SP70
          CALL      GETNZM00
          BRANCH    EXIT,NZPAC100               * invalid misc.item
.
          CLEAR     NZEXDESC
          APPEND    "FI ",NZEXDESC
          APPEND    NZMCCGRP,NZEXDESC
          APPEND    SP1,NZEXDESC
          APPEND    NZMXITMN,NZEXDESC
          APPEND    SP70,NZEXDESC
          RESET     NZEXDESC
.
          MOVE      ONE,FORM3                    * default quantity to one
          MOVE      NZMXQNTY,FORM3
          IF        TSERVS<>0
            MULT      TSERVS,FORM3
          ENDIF
          MOVE      NZMCPATP,FORM12P2
          MULT      FORM3,FORM12P2
          MULT      "-1",FORM12P2
.
          MOVE      FORM12P2,NZEXDAMT            * Debit Amount
          MOVE      FORM12P2,NZEXCAMT            * Credit Amount
          MULT      "-1",NZEXCAMT
.
          PACK      KEY30,NZEXADMN,NZEXINVN,NZEXGLBT,NZEXHCOD,NZEXTRNO,SP70
          CALL      WRNZEXT2                     * Write a record
          MOVE      ONE,RECCNT
          GOTO      NZPAC100
.
NZPAC800  MOVE      ONE,EXIT
          BRANCH    RECCNT,NZPAC999
.
NZPAC900  MOVE      ZERO,EXIT
NZPAC999  RETURN
+
.******************************************************************************
.         Get total amount of items in the pack
.******************************************************************************
TPAC0000  MOVE      ZERO,FORM12P2
          PACK      KEY40,SKEY37,SP70
          CALL      RSNZMXC1
TPAC1000  CALL      RKNZMXC1
          BRANCH    OVRCD,TPAC8000
.
          PACK      DIM37,NZMXHOSP,NZMXCLMC,NZMXCNTR,NZMXFTAB,NZMXMCHG:
                              NZMXEFDT,SP70
.
          MATCH     DIM37,SKEY37
          GOTO      TPAC8000 IF NOT EQUAL
.
          PACK      KEY9,NZMXITMN,SP70
          CALL      GETNZM00
          BRANCH    EXIT,TPAC1000               * invalid misc.item
.
          MOVE      ONE,FORM3                   * default quantity to one
          MOVE      NZMXQNTY,FORM3
          IF        TSERVS<>0
            MULT      TSERVS,FORM3
          ENDIF
.
          MOVE      NZMCPATP,F12P2
          MULT      FORM3,F12P2
          ADD       F12P2,FORM12P2
          GOTO      TPAC1000
.
TPAC8000  MOVE      ZERO,EXIT
          MULT      "-1",FORM12P2
          COMPARE   FORM12P2,GLAMOUNT
          GOTO      TPAC9999 IF EQUAL
          MOVE      ONE,EXIT
.
TPAC9999  RETURN
+
.******************************************************************************
.         Get NZ Misc.Item
.******************************************************************************
GETNZM00  MOVE      ZERO,EXIT
          PACK      KEY11,PINVADM,PTDTSUNQ,SP70
          CALL      RDNZSPR1
          IF        OVRCD=1
            PACK      NZSPCLMN,ACLAIM,SP70
            PACK      NZSPCNTR,AFUNDH,SP70
          ENDIF
.
          MOVE      PMVXMHOS,KEY3
          MOVE      ZERO,F1
.
GETNZM07  PACK      KEY29,KEY3,NZSPCLMN,NZSPCNTR,SP8,KEY9,SP70
          PACK      KEY37,KEY29,MISCDATE
          CALL      RDNZMCH1
          IF        OVRCD=0
            MATCH     "1",NZMCACFL                * Inactive?
            GOTO      GETNZM99 IF NOT EQUAL       * No
          ENDIF
.
GETNZM08  CALL      RPNZMCH1
          BRANCH    OVRCD,GETNZM10
          PACK      KEY29A,NZMCHOSP,NZMCCLMC,NZMCCNTR,NZMCFTAB,NZMCMCHG,SP70
          MATCH     KEY29,KEY29A
          GOTO      GETNZM10 IF NOT EQUAL
.
          MATCH     "1",NZMCACFL                * Inactive?
          GOTO      GETNZM08 IF EQUAL           * Yes
.
          MATCH     NZMCEFDT,MISCDATE       * effective date => serv. date ?
          GOTO      GETNZM10 IF LESS        * yes
.
          MATCH     SP70,NZMCEEDT
          IF        !@EQUAL
            MATCH     MISCDATE,NZMCEEDT
            GOTO      GETNZM10 IF LESS
          ENDIF
          GOTO      GETNZM99
.
.         No record with health fund details - try a blank health fund
.
GETNZM10  PACK      KEY29,KEY3,NZSPCLMN,SP6,SP8,KEY9,SP70
          PACK      KEY37,KEY29,MISCDATE
          CALL      RDNZMCH1
          IF        OVRCD=0
            MATCH     "1",NZMCACFL                * Inactive?
            GOTO      GETNZM99 IF NOT EQUAL       * No
          ENDIF
.
GETNZM12  CALL      RPNZMCH1
          BRANCH    OVRCD,GETNZM20
          PACK      KEY29A,NZMCHOSP,NZMCCLMC,NZMCCNTR,NZMCFTAB,NZMCMCHG,SP70
          MATCH     KEY29,KEY29A
          GOTO      GETNZM20 IF NOT EQUAL
.
          MATCH     "1",NZMCACFL                * Inactive?
          GOTO      GETNZM12 IF EQUAL           * Yes
.
          MATCH     NZMCEFDT,MISCDATE       * effective date => serv. date ?
          GOTO      GETNZM20 IF LESS        * yes
.
          MATCH     SP70,NZMCEEDT
          IF        !@EQUAL
            MATCH     MISCDATE,NZMCEEDT
            GOTO      GETNZM20 IF LESS
          ENDIF
          GOTO      GETNZM99
.
.         No record with health fund details - try default claim, blank hf
.
GETNZM20  IF        IBCNMHOS=1
            MOVE      PTHSDCLM,NZMCCLMC
          ELSE
            MOVE      PTCNDCLM,NZMCCLMC
          ENDIF
          PACK      KEY29,KEY3,NZMCCLMC,SP14,KEY9
          PACK      KEY37,KEY29,MISCDATE,SP70
          CALL      RDNZMCH1
          IF        OVRCD=0
            MATCH     "1",NZMCACFL                * Inactive?
            GOTO      GETNZM99 IF NOT EQUAL       * No
          ENDIF
.
GETNZM22  CALL      RPNZMCH1
          BRANCH    OVRCD,GETNZM30
          PACK      KEY29A,NZMCHOSP,NZMCCLMC,NZMCCNTR,NZMCFTAB,NZMCMCHG,SP70
          MATCH     KEY29,KEY29A
          GOTO      GETNZM30 IF NOT EQUAL
.
          MATCH     "1",NZMCACFL                * Inactive?
          GOTO      GETNZM22 IF EQUAL           * Yes
.
          MATCH     NZMCEFDT,MISCDATE       * effective date => serv. date ?
          GOTO      GETNZM30 IF LESS        * yes
.
          MATCH     SP70,NZMCEEDT
          IF        !@EQUAL
            MATCH     MISCDATE,NZMCEEDT
            GOTO      GETNZM30 IF LESS
          ENDIF
          GOTO      GETNZM99
.
.         No record for the hosp.- try with blank hosp
.
GETNZM30  BRANCH    F1,GETNZM90
          ADD       ONE,F1
          MOVE      SP70,KEY3
          GOTO      GETNZM07
.
GETNZM90  MOVE      ONE,EXIT
          GOTO      GETNZM99
.
GETNZM99  RETURN
+
.******************************************************************************
.         Set Invoice/Credit Note flag
.******************************************************************************
SFLG0000  MOVE      SP70,NZEXCRST
          MATCH     SP70,NZEXRCNO
          GOTO      SFLG9999 IF EQUAL           * Not a credit note
.
.
          MATCH     "1",PTDTCRST
          GOTO      SFLG1000 IF EQUAL
          MATCH     "2",PTDTCRST              * Credit by item
          GOTO      SFLG9999 IF NOT EQUAL
.
SFLG1000  PACK      NZEXCRST,PTDTCRST,SP70    * Credit note status
SFLG9999  RETURN
+
          INC       CLNZPEXT
          INC       NZPEXTIO/INC
          INC       NZPRVBIO/INC
          INC       PATRE1IO/INC
          INC       NZPMCHIO/INC
          INC       NZPMXCIO/INC
          INC       IBAOVRIO/INC
.
GLXT9999  CLOSE     NZPEXTA1
          ENDPROC
