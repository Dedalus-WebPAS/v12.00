.*****************************************************************************
.* System    :   Patient Management System                                   
.* Program   :   FA873251                                                    
.* Desc      :   Fix U/R Comment Header Occupation Code
.----------------------------------------------------------------------------
. Date      :   25/06/2019                                                  
. Author    :   Thanh T.                                                    
. Function  :   This program will loop through pmsuchaf looking for PMUHOCCG  
.               that does not match the WBSEOCG of websecaf for user
.               PMUHCUID, if it is different, then update PMUHOCCG of 
.               pmsuchaf with correct WBSEOCG of websecaf                     
.----------------------------------------------------------------------------
. V10.14.00 :   25/06/2019  Thanh T.             TSK 0873251
.----------------------------------------------------------------------------
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PMSUCHFD/INC
          INC       WEBSECFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
COUNTER   FORM      8
.
.
. PROGRAM CONSTANTS
. -----------------
PIPE      INIT      "|"
NOTETYPE  INIT      "001"
.
PRGID     INIT      "FA873251"
PRGNAM    INIT      "Fix U/R Comment Header Occupation Code"
VERSION   INIT      "V12.00.00"
+
.*****************************************************************************
.*                      Controlling Logic (Mainline code)                    *
.*****************************************************************************
.
MAIN0000  CALL      INIT0000               * initialisation and open files
.
MAIN0100  CALL      OPTN0000               * select options
          BRANCH    EXIT,MAIN9999          * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,MAIN1000       * proceed with fixit
.
MAIN1000  CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,MAIN2000:        * yes
                          MAIN0100:        * no
                          MAIN0100         * cancel
.
MAIN2000  CALL      PROC0000               * process
.
MAIN9999  CHAIN     PGM                    * chain back to program
+
.*****************************************************************************
.*  The initialisation routine is used to display headings and open files.   *
.*****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          DISPLAY   *P56:24,*EL,"Opening "
          DISPLAY   *P64:24,"websecaf";
          OPEN      WEBSECA1,"websecaf"
.
          DISPLAY   *P64:24,"pmsuchaf";
          OPEN      PMSUCHA1,"pmsuchaf"
.
INIT9999  RETURN
+
.*****************************************************************************
.*                        get user options or exit                           *
.*    Returns:  EXIT    = FALSE (0)  Run fixit                               *
.*                        TRUE  (1)  Exit option selected                    *
.*              COPTION  1 = Run Fixit (Report Only)                         *
.*                       2 = Run Fixit                                       *
.*****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Fixit (Report Only)":
                    *P1:6,*V2LON,TWO,*HOFF,". Run Fixit" 
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ":
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000:            * run fixit (report only)
                            OPTN9000             * run fixit
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.*****************************************************************************
.* Loop through pmsuchaf looking for PMUHOCCG that does not match the        *
.* WBSEOCG of websecaf for user PMUHCUID, if it is different, then update    *
.* PMUHOCCG of pmsuchaf with correct WBSEOCG of websecaf                     *
.*****************************************************************************
.
PROC0000  DISPLAY   *P1:24,*EL,"Processing:";
          MOVE      ZERO,COUNTER                 * initialise record counter
.
          MOVE      ZERO,CPAGENO                 * initialise print variables
          MOVE      ZERO,CNOUNDLN
          CALL      IBACLOCK
.
          CALL      HEAD0000                     * print report header
.
          MOVE      SP70,KEY17
PROC0500  CALL      RSPMUCH1                     * position at start of file
PROC1000  CALL      RKPMUCH1                     * read next record
          BRANCH    OVRCD,PROC9000               * eof - finished
.
          DISPLAY   *P13:24,*EL,*V2LON,PMUHURNO
.
          MATCH     NOTETYPE,PMUHCTYP
          GOTO      PROC1000 IF NOT EQUAL
.
          PACK      KEY10,PMUHCUID
          CALL      RDWBSE1                 * Occupational Group
          BRANCH    OVRCD,PROC9000   
.      
          MATCH     WBSEOCG,PMUHOCCG
          GOTO      PROC1000 IF EQUAL
.
          CALL      DISP0000                     * print record for update
.
.         Now update pmsuchaf with the WBSEOCG for websecaf
.
          IF        COPTION = 2
            MOVE      WBSEOCG,PMUHOCCG
            CALL      UPPMUCH1
          ENDIF
.
          GOTO      PROC1000                     * get next record
.
PROC9000  CALL      LINE0000
.
          IF        COPTION = 1
            PRINT     *N,COUNTER,*HOFF," records found.  "
          ELSE
            PRINT     *N,COUNTER,*HOFF," records updated.  "
          ENDIF
          PRINT     *N,*1,"*** End of Report ***"
.
          DISPLAY   *P1:23,*EF,"Fixit completed.";
          IF        COPTION = 1
            DISPLAY   *P1:24,*V2LON,COUNTER,*HOFF," records found.  ";
          ELSE
            DISPLAY   *P1:24,*V2LON,COUNTER,*HOFF," records updated.  ";
          ENDIF
          CALL      HITENTER
.
PROC9999  RETURN
+
.****************************************************************************
.*                  Valid record so print it                                *
.****************************************************************************
.
DISP0000  COMPARE   FIFTY5,CLNO                  * page full ?
          CALL      HEAD0000 IF NOT LESS         * yes
.
          PRINT     *1,PIPE,*5,PMUHURNO,*16,PIPE,*18,PMUHCNUM:
                    *24,PIPE,*26,PMUHOCCG,*39,PIPE:    
                    *41,WBSEOCG,*132,PIPE
.
          ADD       ONE,CLNO                     * increment line count
          ADD       ONE,COUNTER
.
DISP9999  RETURN
+
.****************************************************************************
.*                       Print page heading                                 *
.****************************************************************************
.
HEAD0000  CALL      HEAD132                      * display page header
.
          CALL      LINE0000                     * draw line across page
.
          PRINT     *1,PIPE,*3,"U/R Number",*16,PIPE,*18,"Notes":
                    *24,PIPE,*26,"Old PMUHOCCG",*39,PIPE:
                    *41,"New PMUHOCCG",*132,PIPE
.
          CALL      LINE0000                     * draw line across page
.
          MOVE      SIX,CLNO                     * increment line count
.
HEAD9999  RETURN
+
.****************************************************************************
.*                      Draw line across page                     HEAD0000  *
.****************************************************************************
.
LINE0000  PRINT     "*-----------------------------------------------":
                    "------------------------------------------------":
                    "-----------------------------------*"
.
LINE9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       WEBSECIO/INC
          INC       PMSUCHIO/INC
