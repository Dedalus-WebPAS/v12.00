.******************************************************************************
.*  Include Name  : PATMSCKY.PBL                                              *
.*  Description   : Keyin of Miscellanous Charge codes or Item's              *
.*  Author        : Ian Rutt                                                  *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF9 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMODE - is keyin mode                                  *
.*                  CLAIM    - Claim Code                                     *
.*                  HFUND    - Health Fund Code                               *
.*                  HFTAB    - Health Fund Table                              * 
.*                                                                            *
.*  Returns       : MCHARGE  - Keyed in code                                  *
.*                  MDESC   - Description                                     *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Modification  :                                                           *
.*        V11.00.01 24/02/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*        V10.00.01 30/04/2010  J.Tan     CAR 220887                          *
.*                  Mods checking for Active/Inactive of Misc.charge          *
.*                  V9.08.01 08/02/2007 Neelkamal Pyla  CAR 127560            *
.*                  Commented PATMSCDS                                        *
.*                  V9.03.01 15/09/2003 Lina Vo   CAR40497                    *
.*                  Changed index and read of patmchgf to include effective   *
.*                  date.                                                     *
.*                  V9.00.00 20/11/2001 J.Tan                                 *
.*                  Fixed key for miscellaneous charges file                  *
.*                                                                            *
.******************************************************************************
PATMSCKY
PTMSKY00  MATCH     SP9,CKYIDEF9                  * using a default ?
          GOTO      PTMSKY10 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN2:      * no default used
                    *PECOL:EVERT,*V2LON,MCHARGE:
                    *PECOL:EVERT,*DV,MCHARGE
          GOTO      PTMSKY20
.
PTMSKY10  MOVE      CKYIDEF9,MCHARGE                 * default
          KEYIN     *PECOL:EVERT,*DV,MCHARGE:     * default used
                    *PECOL:EVERT,*V2LON,*RV,MCHARGE:
                    *PECOL:EVERT,*DV,MCHARGE
.
.         check what was entered
.
PTMSKY20  CALL      MSCCK000
          BRANCH    EXIT,PTMSKY25,PTMSKY31,PTMSKY41,PTMSKY95
.                        "?"      valid    nothing  exitchar
          GOTO      PTMSKY00
.
.         a "?" was input
.
PTMSKY25  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
PTMSKY26  
.         CALL      PATMSCDS
.         BRANCH    CNEWDS,PTMSKY27 
.
.         CALL      PUTSCR00                * redisplay screen
          GOTO      PTMSKY00                * rekey code
.
.         now ask for code while leaving displayed codes on screen
.
PTMSKY27  MATCH     SP9,CKYIDEF9                  * using a default
          GOTO      PTMSKY28 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Misc. Charge Code : ":
                    *P21:24,*DV,UNDLN2:      * no default used
                    *P21:24,*V2LON,MCHARGE:
                    *P21:24,*DV,MCHARGE
          GOTO      PTMSKY29
.
PTMSKY28  MOVE      CKYIDEF9,MCHARGE
          KEYIN     *P1:24,*EL,*DV,"Misc. Charge Code : ":
                    *P21:24,*DV,MCHARGE:     * default used
                    *P21:24,*V2LON,*RV,MCHARGE:
                    *P21:24,*DV,MCHARGE
.
.         repeat the validation of the code
.         check what was entered
.
PTMSKY29  CALL      MSCCK000
          BRANCH    EXIT,PTMSKY26,PTMSKY30,PTMSKY40,PTMSKY90
.                        "?"      valid    nothing  exitchar
.
          GOTO      PTMSKY27
.
.         a valid code was entered 
. 
PTMSKY30  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,MCHARGE
.
PTMSKY31  MOVE      FALSE,EXIT                    * valid input
          GOTO      PTMSKY99 
.
.         nothing was input
.
PTMSKY40  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,MCHARGE
.
PTMSKY41  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      PTMSKY99 
.
.         EXITCHAR input
.
PTMSKY90  CALL      FRESCR00                * free the screen saved
.
PTMSKY95  MOVE      TWO,EXIT                * EXITCHAR
.
PTMSKY99  RETURN
+
.*********************************************************************
.*                  MSCCK000                    Called by : PATMSCKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
MSCCK000  ENDSET    MCHARGE
          APPEND    SP9,MCHARGE
          RESET     MCHARGE
          MOVE      MCHARGE,SAVCHG
.
          MATCH     EXITCHAR,MCHARGE
          GOTO      MSCCK600 IF EQUAL       * exit char
.
          MATCH     SP9,MCHARGE
          GOTO      MSCCK700 IF EQUAL       * nothing
.
          MATCH     QUEST,MCHARGE
          GOTO      MSCCK800 IF EQUAL       * ? mark
.
.         a code was entered so validate
. 
MSCCK500  PACK      KEY17,MCHARGE,CURRDATE,SP9
          CALL      PATITMRD
          IF        OVRCD = 0 
            MOVE      IDESC,MDESC
            MOVE      TWO,EXIT
            GOTO      MSCCK999
          ENDIF 
...        BRANCH    CLMFLG,MSCCK510
...
...        BRANCH    CMCNOHF,MSCCK510
...
...        COMPARE   ZERO,CMCNOHF
...        GOTO      MSCCK510 IF EQUAL
...
...        BRANCH    FNDFLG,MSCCK510
.
MSCCK501  UNPACK    SP20,PTHFBCAT           * change "H/F Table" to "Table Type"
          PACK      KEY14,HFUND,HFTAB,SP20
          CALL      RDFUND1                 * "Table Type code" now in PTHFBCAT
.
          MOVE      MCHARGE,KEY9            * (MCHARGE re-use in PATMCHRD)
          PACK      KEY29,CLAIM,HFUND,PTHFBCAT,KEY9,CURRDATE
.
          CALL      RDMCHG1
          BRANCH    CKYIMODE,MSCCK550
.
.         validate update keyin mode (code should already exist)
.
          CALL      PATMCHRD                * read Misc.Charges file 
          COMPARE   ZERO,EXIT
          GOTO      MSCCK900 IF NOT EQUAL
.
MSCCK540  MOVE      TWO,EXIT                * valid code
          GOTO      MSCCK999
.
.         validate insert keyin mode (code shouldn't exist)
.
MSCCK550  COMPARE   ZERO,OVRCD   
          GOTO      MSCCK900 IF EQUAL
.
          MOVE      TWO,EXIT                * valid code
          GOTO      MSCCK999
.
..MSCCK510  PACK      KEY26,CLAIM,HFUND,HFTAB,MCHARGE,SP20
..          CALL      RDSMCHG1
..
..MSCCK520  CALL      RDKMCHG1 
..          BRANCH    OVRCD,MSCCK900
..
..          BRANCH    CLMFLG,MSCCK530
..          MATCH     SAVCLM,MCLMCOD
..          GOTO      MSCCK520 IF NOT EQUAL
..
..MSCCK530  BRANCH    CMCNOHF,MSCCK550
..          BRANCH    FNDFLG,MSCCK540
..          MATCH     SAVFND,MHFUND
..          GOTO      MSCCK520 IF NOT EQUAL
..
..MSCCK540  BRANCH    TABFLG,MSCCK550
..          MATCH     SAVTAB,MHFTAB
..          GOTO      MSCCK520 IF NOT EQUAL
..
..MSCCK550  MATCH     SAVCHG,MCHARGE
..          GOTO      MSCCK520 IF NOT EQUAL
..          MOVE      TWO,EXIT
..          GOTO      MSCCK999   
.
.         exitchar entered
.
MSCCK600  MOVE      FOUR,EXIT
          MOVE      SP9,MCHARGE
          MOVE      SP20,MDESC              * clear desc field
          GOTO      MSCCK999
.
.         nothing entered
.
MSCCK700  BRANCH    CKYIMAND,MSCCK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP9,MCHARGE
          MOVE      SP20,MDESC              * clear desc field
          GOTO      MSCCK999
.
.         "?" entered
.
MSCCK800  MOVE      ONE,EXIT
          GOTO      MSCCK999
.
.         invalid code entered
.
MSCCK900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
.
          IF        CKYIMODE = 0
            DISPLAY   *P1:24,*EL,*B,"Invalid Miscellaneous Charge Code. ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      MSCCK999
.
.         field is mandatory
.
MSCCK950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is Mandatory. ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
MSCCK999  RETURN
.
.         INCLUDE   PATMSCDS
+
