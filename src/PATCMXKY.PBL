.******************************************************************************
.*  Include Name  : PATCMXKY.PBL                                              *
.*  Description   : Keyin of a Casemix Code                                   *
.*  Author        : Jeni Tan                                                  *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  ECOL1    - DRG & MBS column display                       *
.*                                                                            *
.*  Returns       : CMXCODE - Keyed in code                                   *
.*                  CMXDESC - Keyed in code description                       *
.*                  CMXTYPE - Casemix type                                    *
.*                              1 - DRG                                       *
.*                              2 - MBS                                       *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                       = 3   ZERO input for previous field                  *
.*                                                                            *
.*  On return     : You must display the description yourself                 *
.*                                                                            *
.*  Vars Used     : PATCMXVR.INC                                              *
.*                                                                            *
.*  Modifications :                                                           *
.*        V9.08.01  08/05/2007  Mike Laming   CAR 137125 HDP 2007             *
.*                  Add DRG Version to Key fields for reads to PATDGWaf       *
.*        V5.07.01  28/03/2000 J.Tan                                          *
.*                  Fixed clearing screen for screen painted                  *
.*        V5.08.B01 25/02/2000  Greg Horvat      SRF 637565                   *
.*                  Changes for 4 character DRGs                              *
.*                  V6.04.01 15/04/97 J.Tan                                   *
.*                  Fixed setting up descriptions                             *
.*                                                                            *
.******************************************************************************
PATCMXKY
KCSMX000  MOVE      SP10,CMXCODE
          MOVE      ZERO,CMXTYPE
          PACK      CMXDESC,SP30,SP30
          MOVE      ZERO,FORM1
          MOVE      SP1,DIM1
.....          DISPLAY   *PECOL:EVERT,*EL,*PECOL1:EVERT,"1=DRG, 2=MBS, 3=Adm.type";
          DISPLAY   *PECOL:EVERT,SP2,*PECOL1:EVERT,"1=DRG, 2=MBS";
          KEYIN     *PECOL:EVERT,*V2LON,DIM1;
.
          RESET     DIM1
          GOTO      KCSMX910 IF EOS              * nothing entered
.
          CMATCH    EXITCHAR,DIM1
          GOTO      KCSMX920 IF EQUAL            * exit entered
.
          CMATCH    "0",DIM1
          GOTO      KCSMX930 IF EQUAL            * Zero entered for prev.field
.
          MOVE      DIM1,FORM1
          MOVE      FORM1,CMXTYPE
          LOAD      KEY10,FORM1,DRG,MBS,ADMT
.....          BRANCH    FORM1,KCSMX100,KCSMX100,KCSMX100
          BRANCH    FORM1,KCSMX100,KCSMX100
.
          BEEP
          GOTO      KCSMX000
.
KCSMX100  MOVE      ECOL,F2
          IF        FORM1=1
            ADD       FOUR,F2
          ELSE
            ADD       NINE,F2
          ENDIF
          DISPLAY   *PECOL1:EVERT,SP10,SP2,*PF2:EVERT,*V2LON,KEY10;
          MOVE      SP3,ACODE
          BRANCH    FORM1,KCSMX200,KCSMX300
.
.         Keyin admission type
.
          MOVE      "A ",CODE
          DISPLAY   *PECOL:EVERT,SP8;
          CALL      PATCODKY
          BRANCH    EXIT,KCSMX910,KCSMX920  * nothing,exit entered
          PACK      CMXCODE,ACODE,SP10
          GOTO      KCSMX900
.
.         Keyin DRG Code
.
KCSMX200  KEYIN     *PECOL:EVERT,*V2LON,DRGCODE;
          DISPLAY   *PF2:EVERT,SP10;
          ENDSET    DRGCODE
          APPEND    SP10,DRGCODE
          RESET     DRGCODE
          MATCH     SP3,DRGCODE
          GOTO      KCSMX910 IF EQUAL        * nothing entered
.
          CMATCH    EXITCHAR,DRGCODE
          GOTO      KCSMX920 IF EQUAL
.
          CMATCH    QUEST,DRGCODE           * question mark option entered ?
          GOTO      KCSMX210 IF NOT EQUAL
.
          CALL      GETSCR00
          MOVE      SP10,DRGCODE
          MOVE      "4",SVERT               * Starting line
          CALL      PATDSDGW
          CALL      PUTSCR00
          BRANCH    EXIT,KCSMX910
.
KCSMX210  PACK      CMXCODE,DRGCODE,SP10
...       PACK      KEY4,DRGCODE
          MOVE      "999",KEY3
          PACK      KEY7,DRGCODE,KEY3,SP10  * last DRG Version        *C-137125
          CALL      RSPTDGW1                * valid DRG Code ?        *C-137125
          CALL      RPPTDGW1
          BRANCH    OVRCD,KCSMX220
.
          MATCH     PTDWDRGC,DRGCODE                                  *I-137125
          GOTO      KCSMX220 IF NOT EQUAL                             *I-137125
.
          PACK      KEY60,PTDWDESC,SP20,SP20   * DRG Code description
          PACK      KEY40,PTDWDESC,SP20,SP20
          CALL      FDESC000                   * set up the description
          PACK      CMXCODE,DRGCODE,SP10
          PACK      CMXDESC,KEY40,SP30,SP30
          DISPLAY   *PECOL:EVERT,SP6,SP3,*PECOL:EVERT,*V2LON,CMXCODE;
          GOTO      KCSMX900
.
KCSMX220  DISPLAY   *P1:24,*EL,*B,*V2LON,"Invalid DRG Code. ";
          CALL      HITENTER
          GOTO      KCSMX200
.
.         Keyin MBS Code
.
KCSMX300  MOVE      ZERO,CKYIMAND
          MOVE      SP10,AMBS
          CALL      PATITMKY 
          BRANCH    EXIT,KCSMX910,KCSMX920   * nothing input,exit
          DISPLAY   *PF2:EVERT,SP10;
.
          PACK      CMXCODE,ITEMNO,SP20
          MATCH     SP9,CMXCODE
          GOTO      KCSMX910 IF EQUAL        * nothing entered
.
          PACK      KEY60,IDESC,SP20,SP20
          CALL      FDESC000                * set up the description
          PACK      CMXDESC,IDESC,SP30,SP30
          GOTO      KCSMX900
.
KCSMX900  MOVE      ZERO,EXIT
          GOTO      KCSMX999
.
KCSMX910  MOVE      ONE,EXIT                 * go back to keyin previous field
          DISPLAY   *PECOL:EVERT,SP10,*PF2:EVERT,SP10;
          PACK      CMXDESC,SP30,SP30
          GOTO      KCSMX999
.
KCSMX920  MOVE      TWO,EXIT                 * Exit char entered
          GOTO      KCSMX999
.
KCSMX930  MOVE      THREE,EXIT               * Zero entered
KCSMX999  RETURN
+
.******************************************************************************
.*                                 FDESC000              Called By : ML0000   *
.*                       Fixed up the Code description                        *
.******************************************************************************
FDESC000  PACK      CDESC1,SP20,SP20        * Initialise the description
          PACK      CDESC2,SP20,SP20
          RESET     KEY60,30                * set pointer to the middle
FDESC100  CMATCH    SP1,KEY60               * find the end of word
          GOTO      FDESC200 IF EQUAL
.
          BUMP      KEY60,-1
          GOTO      FDESC100 IF NOT EOS
          RESET     KEY60,1
          PACK      CDESC1,KEY60,SP20,SP20
          GOTO      FDESC999
.
FDESC200  BUMP      KEY60
          MOVE      KEY60,CDESC2            * set the second description
.
          BUMP      KEY60,-1
          LENSET    KEY60                   * set the end of first description
          RESET     KEY60                   * reset to the start
          MOVE      KEY60,CDESC1            * set the first description
FDESC999  RETURN
.
          INC       PATCODKY
          INC       PATITMKY
          INC       PATDSDGW
+
.

