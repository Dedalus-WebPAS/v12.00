. *****************************************************************************
.* Module  :        PRINTAAE
.*
.* Modifications:
.******************************************************************************
.*        V12.00.01 28/05/2025  Bella Turco    TSK 0955096
.*                  Alphanumeric changes                        
.******************************************************************************
.
          DEFPROC   PRINTAAE

SADACLS   DIM       3
SADACOMP  DIM       3
SADAINS   DIM       3
SADAMODE  DIM       3
SADAPREV  DIM       3
SADASIT   DIM       3
SADASRC   DIM       3
SADAUSR1  DIM       3
SADAUSR2  DIM       3
SADAUSR3  DIM       3
SADAUSR4  DIM       3
SADAUSR5  DIM       3

.------------------------------------------------------------
.  Print AAE Forms
.------------------------------------------------------------
PRTAAE00  CALL      CLPATMAS                * clear patmasfd
          CALL      INTPRA00                * clear patresfd
          CALL      CLWR0000                * clear warnings
...          CALL      CLDR0000                * clear donor dets.
          CALL      CVKC0000                * clear card details
          CALL      CLAAEDET                * clear a&e details
.
          MOVE      SP3,ADSDISC             * initialise discharge status
          MOVE      "01",SCRID              * set for PMI screen
.
          MOVE      ONE,RTAOVRCD            * set as no RTA details
          MOVE      ZERO,PRIDSYET           * no need to print Id sheet warn.
          MOVE      ADMISSNO,ADANUMB
.
          CALL      AAEDET00                * read in required aae variables
          BRANCH    OVRCD,PRTAAE90,PRTAAE91
.
          CALL      DSPAAE00
          BRANCH    EXIT,PRTAAE99
.
          MOVE      STATNCOD,HEMR5COD
          CALL      PRTMR500
          BRANCH    EXIT,PRTAAE99
.
          MOVE      ADANUMB,KEY8
          CALL      UUAEDEA1
.
          MOVE      PURNO,KEY8
          CALL      UUPTMAS1
.
          GOTO      PRTAAE99
.
PRTAAE90  MOVE      "Not an AAE patient",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PRTAAE99
.
PRTAAE91  MOVE      "Patient Locked",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PRTAAE99
.
PRTAAE99  GOTO      PRTAAEND
.------------------------------------------------------------
. MR5
.------------------------------------------------------------
PRTMR500  BRANCH    HEPMR5,PRTMR510
          GOTO      PRTMR590
.
PRTMR510  MOVE      CPPAPER2,CPPAPER        * Set up Second Printer
          MOVE      CDEFPRT2,CDEFPRT
          MOVE      ZERO,PRIDSYET           *flag saying Id sheet printed
.
          BRANCH    HEMR5L,PRTMR520         * branch on MR% layout
          CALL      SETLMR5
          CALL      PRTMR5                  * Print the MR-5 Form
          GOTO      PRTMR599
.
PRTMR520  MOVE      HEMR5COD,KEY6
          CALL      RDIBTH1                * read header details file
          BRANCH    OVRCD,PRTMR591
.
          CALL      GETVCI0                 * Get visit card information
          CALL      PRTMR5N2                * print the MR5
.
          COMPARE   ZERO,EXIT
          GOTO      PRTMR599 IF EQUAL
.
          MOVE      "No Template details on file.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PRTMR599
.
PRTMR590  MOVE      "Not using MR-5 Forms.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PRTMR599
.
PRTMR591  MOVE      "User defined Stationary Code not on file",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
PRTMR599  RETURN
.------------------------------------------------------------
.------ Get visit card information ----- From IBAAAE01
.------------------------------------------------------------
GETVCI0   MOVE       SP20,PTVKCARD
          MOVE       SP10,PTVKCEXP
          MOVE       SP10,PTVKCXMP
          MOVE       SP10,PTVKCOMP
.
          COMPARE   ONE,PTCNUCRD
          GOTO      GETVCI9 IF NOT EQUAL    * not using card information
          MOVE      ADANUMB,PTVKBILL        * visit number
          MOVE      PTVKBILL,KEY8
          CALL      RDPTVKC1
.
GETVCI9   RETURN
.------------------------------------------------------------
.  Read the required AAE variables
.------------------------------------------------------------
AAEDET00  MOVE      ADANUMB,KEY8
          CALL      RLAEDEA1
          MOVE      OVRCD,EXIT
.
          MOVE      SP70,PTHSAPPR
          PACK      KEY8,ADANUMB,SP70
          CALL      RDPMVX11
          IF        OVRCD<>1
            PACK      KEY3,PMVXMHOS,SP70
            CALL      RDPTHSP1
          ENDIF
.  
.  Get the discharge status.  This is required for determining if the
.  patient is chargeable
.
          MOVE      ADANUMB,SAVEVIS         * SAVE the A&E number
          CALL      CLRD0000                * clear discharge vars
          MOVE      ADANUMB,KEY8     
          CALL      RDDISA1
.
          MOVE      AEDANPNA,CKYIDEF3      * set default amount for change
          PROC      CNPNATT0               * calculate prev new att.
.
          IF        PTCNUCRD = ONE
            CALL      CVKC0000             * clear VKC file variables
            MOVE      ADANUMB,KEY8    
            CALL      RDPTVKC1             * read visit card file
            MOVE      TWO,FORM1            * before change audit
            CALL      AVKC0000             * write audit
          ENDIF
.
          MOVE      AEDAPVIS,ORIGVISI       * original visits
.
          CALL      ACTBSV00
          CALL      UUAEDEA1
.
AAEDET99  RETURN
.------------------------------------------------------------
.         clear the aae discharge variables
.         Routine from IBAAAE01
.------------------------------------------------------------
CLRD0000  MOVE      SP8,ADSDATE
          MOVE      SP5,ADSTIME
          PACK      ADSSSES,SP30,SP30,SP20
          MOVE      SP3,ADSDISC
          MOVE      SP7,ADSDIAG
          UNPACK    SP30,AEDSDCFT,AEDSTCFT,AEDSDDTA,AEDSDCON
          UNPACK    SP30,AEDSTCON,AEDSDADM,AEDSTADM
          MOVE      SP3,AEDSUSR6
          MOVE      SP3,AEDSUSR7
          MOVE      SP3,AEDSUSR8
          MOVE      SP10,AEDSOPER
.
          RETURN
.------------------------------------------------------------
.  DISPAAE1 routine modified from IBAAAE01
.------------------------------------------------------------
DSPAAE00  MOVE      ADAURNO,PURNO
          MOVE      PURNO,KEY8
          CALL      RLPTMAS1
          BRANCH    OVRCD,DSPAAE90,DSPAAE91
.
          IF        PTCNNMPI=ONE & PTCNNHII=1
            PACK      KEY8,PURNO
            CALL      RDNHMAS2
            COMPARE   ONE,OVRCD
            IF        !@EQUAL
              PACK      NMPNUMB,SP10,SP1,SP1,NHMANMPI
            ENDIF
          ENDIF
.
          MOVE      SP3,SAVTYP
          CALL      DSPFIELD
.
          MOVE      ZERO,EXIT
          GOTO      DSPAAE99
.
DSPAAE90  MOVE      "Invalid U/R Number.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      DSPAAE99
.
DSPAAE91  MOVE      "Patient Locked",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      DSPAAE99
DSPAAE99  RETURN
.------------------------------------------------------------
.  Routine from IBAAAE01 to clear patresfd
.------------------------------------------------------------
INTPRA00  MOVE      ZEROVISN,PKADMN
          PACK      PKNAME,SP70,SP10
          PACK      PKADD1,SP30,SP10
          PACK      PKADD2,SP30,SP10
          PACK      PKSUBR,SP30,SP10
          PACK      PKADD4,SP30,SP10
          MOVE      SP8,PKPOST
          MOVE      SP20,PKTELEP
          MOVE      SP20,PKTELEB
          MOVE      SP10,PKRELP
INTPRA99  RETURN
.------------------------------------------------------------
.    Clear Warnings
.------------------------------------------------------------
CLWR0000  MOVE      SP10,PTMWURNO
          MOVE      SP10,PTMWCLSS
          MOVE      SP10,PTMWDATE
          PACK      PTMWTEXT,SP30,SP30,SP30
          MOVE      SP10,PTMWMARC
          MOVE      SP20,PTMWHOSP
          MOVE      SP10,PTMWDOCT
          MOVE      ZERO,PTMWSYS
          MOVE      SP10,PTMWCODE
          MOVE      SP20,PTMWLUPD
          MOVE      SP30,PTMWSPAR
          MOVE      ZERO,PTMWCNT
CLWR9999  RETURN
...------------------------------------------------------------
...    Clear donor dets.
...------------------------------------------------------------
..CLDR0000  MOVE      SP10,PTDNURNO
..          MOVE      SP10,PTDNCODE
..          MOVE      SP10,PTDNLUPD
..          MOVE      SP30,PTDNSPAR
..          MOVE      ZERO,PTDNCNT
..CLDR9999  RETURN
.------------------------------------------------------------
.                   CVKC0000
.         Clear the PATVKCFD file variables
.------------------------------------------------------------
CVKC0000  MOVE      ZEROVISN,PTVKBILL
          MOVE      SP20,PTVKCARD
          UNPACK    SP30,PTVKCEXP,PTVKCXMP,PTVKFMLY
          MOVE      SP30,PTVKSPAR
CVKC9999  RETURN
.*********************************************************************
.         display the variables for screen painted screens
.*********************************************************************
DISPVARS  MOVE      SCRID,FORM2
          BRANCH    FORM2,DVARS100,DVARS100,DVARS200,DVARS200
          GOTO      DVARS999
DVARS100  CALL      DXPATMAS
          GOTO      DVARS999
DVARS200  CALL      DXAAEDET
DVARS999  RETURN
.*********************************************************************
.*                  AVKC0000                                         *
.*        Write audit record for PATVKCFD                            *
.*********************************************************************
AVKC0000  BRANCH    PTCNVKCA,AVKC9999       * audits not on
.
          COMPARE   FIVE,FORM1
          GOTO      AVKC5000 IF EQUAL       * delete before change audit
.
          COMPARE   THREE,FORM1
          GOTO      AVKC1000 IF EQUAL       * after change audit
          PACK      PTVKAUDD,CCC,CYY,CMM,CDD
          REP       " 0",PTVKAUDD
          MOVE      CTIMEIS,PTVKAUDT
.
AVKC1000  MOVE      PORT,PTVKAUDP
          MOVE      FORM1,PTVKAUDR
          REP       "1A2B3C4D",PTVKAUDR
          MOVE      ONE,PTVKAUDS
          MOVE      PASSCODE,PTVKAUDO
.
          PACK      KEY19,PTVKAUDD,PTVKAUDT,PTVKAUDP,PTVKAUDR
          CALL      AWPTVKC
          GOTO      AVKC9999
.
.         delete before change audit
.
AVKC5000  MOVE      ANSB,PTVKAUDR
          PACK      KEY19,PTVKAUDD,PTVKAUDT,PTVKAUDP,PTVKAUDR
          CALL      ADPTVKC
.
AVKC9999  RETURN
.------------------------------------------------------------
.  SUBROUTINE to save all the BEFORE Changes before writing to AAEAUA
.------------------------------------------------------------
ACTBSV00  MOVE      ADANUMB,SADANUMB
          MOVE      ADADATE,SADADATE
          MOVE      ADATIME,SADATIME
          MOVE      ADAURNO,SADAURNO
          MOVE      ADAPRTY,SADAPRTY      * srf 641605 - added this line
.
          MOVE      ADACOMP,SADACOMP
          MOVE      ADACLASS,SADACLS
          MOVE      ADAINSUR,SADAINS
          MOVE      ADASRC,SADASRC
          MOVE      ADASIT,SADASIT
          MOVE      ADAMODE,SADAMODE
          MOVE      ADAPREV,SADAPREV
          MOVE      ADAUSR1,SADAUSR1
          MOVE      ADAUSR2,SADAUSR2
          MOVE      ADAUSR3,SADAUSR3
          MOVE      ADAUSR4,SADAUSR4
          MOVE      ADAUSR5,SADAUSR5
.
          MOVE      ADADOCT,SADADOCT
ACTBSV99  RETURN
.------------------------------------------------------------
.         Calculate dates difference
.         Routine from IBAAAE01
.------------------------------------------------------------
CALC      REP       " 0",WDATE1
          REP       " 0",WDATE2
          UNPACK    WDATE1,CCENT,XYEAR,XMON,XDAY
          MOVE      XYEAR,YY
          MOVE      XMON,MM
          MOVE      XDAY,DD
          CALL      DMYCON
          MOVE      JULDAY,CALDAYS
          MOVE      JULYR,JYEAR
.
          UNPACK    WDATE2,CCENT,XYEAR,XMON,XDAY
          MOVE      XYEAR,YY
          MOVE      XMON,MM
          MOVE      XDAY,DD
          CALL      DMYCON
.
          COMPARE   JULYR,JYEAR
          GOTO      NEXT1 IF EQUAL
          ADD       "365" TO CALDAYS
          ADD       LEAPYR TO CALDAYS
NEXT1     SUB       JULDAY,CALDAYS
          RETURN
.*********************************************************************
.         clear the AAEDE1FD/AAEDE1FD variables
.*********************************************************************
CLAAEDET  MOVE      ZEROVISN,SADANUMB           * must clear Save variables
          MOVE      SP30,SADATIME
          MOVE      SP30,SADADATE
          MOVE      ZERO,SADAURNO
          MOVE      SP30,SADADOCT
          MOVE      SP3,SADAPRTY
.
          MOVE      SP8,ADADATE
          MOVE      SP8,ADATIME
          MOVE      SP3,ADACOMP
          MOVE      SP1,CLIND
          MOVE      SP3,ADACLASS
          MOVE      SP3,ADASRC
          MOVE      SP3,ADAINSUR
          MOVE      SP3,ADASIT
          MOVE      SP3,ADAMODE
          MOVE      SP1,ADAFILL
          MOVE      SP3,ADAPREV
          MOVE      SP6,ADADOCT
          MOVE      SP5,ADASEEN
          MOVE      ZERO,ADAADMIT
          MOVE      SP2,ADALOCK
          MOVE      SP30,ADAEMPL
          PACK      ADAADD1,SP30,SP10
          PACK      ADAADD2,SP30,SP10
          PACK      ADASUBR,SP30,SP10
          PACK      ADAADD4,SP30,SP10
          MOVE      SP30,ADAPOST
          MOVE      SP30,ADATELE
          MOVE      SP30,ADACONT
.
          MOVE      SP3,ADAUSR1
          MOVE      SP3,ADAUSR2
          MOVE      SP3,ADAUSR3
          MOVE      SP3,ADAUSR4
          MOVE      SP3,ADAUSR5
          PACK      ADADIAG WITH SP30,SP30
          MOVE      SP3,ADAPRTY
          MOVE      SP5,ADAWAIT
          MOVE      SP8,ADASDTE
.
          MOVE      SP30,AEDARFGP
          UNPACK    SP30,AEDAGPPC,AEDAGPPT
          MOVE      ZERO,AEDANPNA
          MOVE      SP30,AEDATIAS
          MOVE      SP30,AEDADTAT
          MOVE      SP30,AEDATESI
          MOVE      SP30,AEDAEMPL
          MOVE      SP30,AEDARTAS
          MOVE      SP30,AEDAINIT
          MOVE      SP30,AEDAPLIN
          MOVE      SP30,AEDADIAS
          MOVE      ZEROUR,AEDARNEN
          MOVE      SP6,AEDACONS
          PACK      AEDASPAR,SP30,SP30,SP30
          MOVE      SP5,AEDAPRTM
          MOVE      SP5,AEDARDOC
          RETURN
+
.------------------------------------------------------------
. DUMMY ROUTINE
KFREEF00
KFREEF99  RETURN
.------------------------------------------------------------
          INC       AAEMR5N2
          INC       AAEMR5
          INC       AAEAUXIO/INC
          INC       CALCYRS
          INC       DSPFIELD
          INC       DXPATMAS
          INC       DXPATCOD
          INC       DXPATDOC
          INC       DXAAEDET
          INC       IBAOVRIO/INC
....      INC       KYAAEDET
          INC       PATCODKY
          INC       SCRCKFLD


PRTAAEND  ENDPROC
