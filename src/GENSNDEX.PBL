. *----------------------------------------------------------------------
. * Include Name  :   GENSNDEX.PBL
. *
. * Function      :   Common routine to generate a Soundex key
. *                   Must include GENSNDVR.INC for variables used by this
. *                   routine
. *
. * Modifications :
. *
. * V10.11.01   08/08/2017  Tracey Nguyen  TSK 0299206
. *             Created Include
. *----------------------------------------------------------------------
.
GSNDX000  CLEAR     SXKEYOUT
          RESET     SXKEYIN
          GOTO      GSNDX999 IF EOS
.
.         Make sure the saved keyword contains only upper case characters
.
          MOVE      SXKEYIN,SXKEYSTR
          REP       UPPLOW,SXKEYSTR
.
.         Ignore all characters from "(" onward
.
          MOVE      "(",SXCHAR1
          SCAN      SXCHAR1,SXKEYSTR
          GOTO      GSNDX100 IF NOT EQUAL
.
          BUMP      SXKEYSTR,-1
          APPEND    SP20,SXKEYSTR
          RESET     SXKEYSTR
.
.         Ignore trailing S's
.
GSNDX100  SCAN      SXCHARSC,SXKEYSTR
          GOTO      GSNDX10A IF NOT EQUAL
.
          CMOVE     SP1,SXKEYSTR
          RESET     SXKEYSTR
.
.         Validate the first character - must be alpha
.
GSNDX10A  MOVE      SXKEYSTR,SXCHAR2
          REP       "KC",SXCHAR2
          SCAN      SXCHAR2,UPPLOW
          GOTO      GSNDX10B IF EQUAL
.
          BUMP      SXKEYSTR
          GOTO      GSNDX10A IF NOT EOS
.
.         Move this first character to the SOUND-EX key
.
GSNDX10B  RESET     UPPLOW
          PACK      SXKEYOUT WITH SXCHAR2,SP20
          REP       " 0",SXKEYOUT
.
.         Loop to build up key
.
GSNDX10C  BUMP      SXKEYOUT
          GOTO      GSNDX10E IF EOS
.
GSNDX10D  BUMP      SXKEYSTR
          GOTO      GSNDX10E IF EOS
.
.         Check if this is a valid character
.
          MOVE      SXKEYSTR,SXCHAR1
          SCAN      SXCHAR1,UPPLOW
          GOTO      GSNDX10D IF NOT EQUAL
.
          RESET     UPPLOW
.
.         Check for special pairs of characters
.
          MATCH     SXCHARCK,SXKEYSTR        * Ignore the C in CK
          GOTO      GSNDX10D IF EQUAL
.
          MATCH     SXCHARGH,SXKEYSTR        * Ignore the G in GH
          GOTO      GSNDX10D IF EQUAL
.
          MATCH     SXCHARLM,SXKEYSTR        * Ignore the L in LM
          GOTO      GSNDX10D IF EQUAL
.
          MATCH     SXCHARMN,SXKEYSTR        * Ignore the M in MN
          GOTO      GSNDX10D IF EQUAL
.
          MATCH     SXCHARSC,SXKEYSTR        * Ignore the S in SC
          GOTO      GSNDX10D IF EQUAL
.
          MATCH     SXCHARTZ,SXKEYSTR        * Ignore the T in TZ
          GOTO      GSNDX10D IF EQUAL
.
.         Check for special pairs of characters (Check with previous char)
.
          PACK      SXCHPAIR WITH SXCHAR2,SXCHAR1
.
          MATCH     SXCHARMP,SXCHPAIR        * Ignore the P in MP
          GOTO      GSNDX10D IF EQUAL
.
          MATCH     SXCHARST,SXCHPAIR        * Ignore the T in ST
          GOTO      GSNDX10D IF EQUAL
.
.         Ignore if this is the same as the previous character
.
          CMATCH    SXCHAR1,SXCHAR2
          GOTO      GSNDX10D IF EQUAL
.
          MOVE      SXCHAR1,SXCHAR2
.
.         Convert the character to a SOUND-EX value and if non-zero, add to key
.
          REP       SXNUMREP,SXCHAR1
          MATCH     "0",SXCHAR1
          GOTO      GSNDX10D IF EQUAL
.
          CMOVE     SXCHAR1,SXKEYOUT
          GOTO      GSNDX10C
.
.         Finished loop
.
GSNDX10E  RESET     SXKEYOUT                * Soundex Key out

GSNDX999  RETURN
.
