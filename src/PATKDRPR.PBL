.------------------------------------------------------------
.  File          :  PATKDRPR.PBL
.
.  Function      :  Update Keyword Index File for DRG Codes
.
.  Parameters    :  PTDWDRGC - Key to DRG Code Table
.
.  Modifications :
.         V9.08.01  19/04/2007  Mike Laming   CAR 137125
.                   Add DRG Version to key of PATDGWaf  - added PTDWDRGV
.         V5.07.01  01/12/1999  Davin
.                   Changed for 4 character drgs
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Procedure to Update
.  a Key Word Index Table that can be used for searching 
.  Masterfiles. The procedure should be called from the 
.  Add, Update and Delete functions in the Masterfile 
.  Maintenance Function.
.  
.  A Key Word Index Table is Defined in the following Manner
.                              
.  PTKDITM    DIM       xx    * Code Field Segment 1
.  PTKDITM    DIM       xx    * Code Field Segment n
.  PTKDKWD     DIM       15    * Key Word
.  PTKDSPA     DIM       10    * Spare
.
.  Index 1   PTKDITM, PTKDITM, PTKDKWD
.  Index 2   PTKDKWD, PTKDITM, PTKDITM
.
.  To convert this Procedure the following should be performed
.    Global Change PAT to the System ID eg :%s/PAT/PAT/g
.    Global Change KDR to the File   ID eg :%s/KDR/DKI/g
.    Global Change PTKD to the IO Call ID eg :%s/PTKD/PTDK/g
.    Modify the KEYINDEX defintion to combined Code Field Lengths eg 6
.    Global Change KEY18 to the Key for the Key Word Table eg KEY21
.    Change xxxxxxx to the Code Fields eg DCODEhh
.    Change the GENR0000 routine to call the BWORD000 for each
.    string of key words to be indexed for the Coded Field
.    eg MOVE    DSNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    DGNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    TDESC,KEYWORDS
.       CALL    BWORD000
.------------------------------------------------------------
          DEFPROC   PATKDRUP
.
          INC       PATKDRFD/INC
.
KEYINDEX  DIM       4         * Code Index in Table
KEYWORDS  DIM       60        * String Containing the Key Words to be Indexed
SIXTY     FORM      "60"
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
.------------------------------------------------------------
. Generate Word Index
.------------------------------------------------------------
GENR0000  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     PATKDRA1,"patkdraf"
          TRAPCLR  IO
          BRANCH   OVRCD,GENR9999
.
          MOVE     PTDWDRGC,KEYINDEX
          CALL     CLEAR000           * Remove Current Key Word Index
          BRANCH   EXIT,GENR9999
.
...       MOVE     PTDWDRGC,KEY4      * Validate Record on File
          PACK     KEY7,PTDWDRGC,PTDWDRGV,SP10                        *I-137125
          CALL     RDPTDGW1
          BRANCH   OVRCD,GENR1900     * If Not then it Must be a Delete/Clear
.
          MOVE     PTDWDRGC,KEYINDEX
          MOVE     PTDWDRGC,KEYWORDS
          CALL     BWORD000
.
          MOVE     PTDWDESC,KEYWORDS
          CALL     BWORD000
.
GENR1900  CLOSE    PATKDRA1
.
GENR9999  GOTO     PATDKIEP
.------------------------------------------------------------
. Clear Current Key Words
.------------------------------------------------------------
CLEAR000  PACK     KEY19,KEYINDEX,SP70
          CALL     RSPTKD1
          CALL     RKPTKD1
          BRANCH   OVRCD,CLEAR999
          MATCH    KEYINDEX,PTKDITM
          GOTO     CLEAR999 IF NOT EQUAL
.
          PACK     KEY19,PTKDITM,PTKDKWD,SP70
          CALL     DEPTKD1
          GOTO     CLEAR000
.
CLEAR999  RETURN
.-------------------------------------------------
. Determine Words to Index
.-------------------------------------------------
BWORD000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      BWORD000 IF NOT EOS
            GOTO      BWORD999               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWORDS,SP30      * reset form pointer
          MOVE      KEY60,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      BWORD100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Save this Word
.
BWORD100  MOVE      KEYWORDS,KEY15
          PACK      KEY15,KEY15,SP70
          REP       UPPLOW,KEY15             * Always Upper Case
          PACK      KEY19,KEYINDEX,KEY15
          UNPACK    KEY19,PTKDITM,PTKDKWD
          CALL      RDPTKD1
          IF        OVRCD=1
            CALL      WRPTKD1
          ENDIF
          GOTO      BWORD190
.
.         Check for any more words
.
BWORD190  SETLPTR   KEYWORDS,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY60,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY60,KEYWORDS
          GOTO      BWORD000
.
BWORD999  RETURN
.
          INC       PATKDRIO/INC
          INC       IBAOVRIO/INC
.
PATDKIEP  ENDPROC                          * End of Procedure
.
