. *----------------------------------------------------------------------
. * Include Name  :   OUTCPBTM.PBL
. *
. * Function      :   CGI variables for OUTCPBFD - outcpbaf.dat
. *
. * Includes Needed :
. *
. * Modifications   :
. *
. * V10.11.01 14/11/2017  Don Nguyen        TSK 0816814
. *           Added OTPB1800 - VMTTAB00
. * V10.06.00 14/05/2015 Ebon Clements      CAR 303890
. *           Created include
. *----------------------------------------------------------------------
.
.------------------------------------------------------------------------
.   Clear Parameters for Outpatients  CPB Table
.------------------------------------------------------------------------
CPOTCB00  MOVE      Z70,OTCPB001
          MOVE      Z70,OTCPB002
          MOVE      Z70,OTCPB003
          MOVE      Z70,OTCPB004
          MOVE      Z70,OTCPB005
          MOVE      Z70,OTCPB006
          MOVE      Z70,OTCPB007
          MOVE      Z70,OTCPB008
          MOVE      Z70,OTCPB009
          MOVE      Z70,OTCPB010
          MOVE      Z70,OTCPB011
          MOVE      Z70,OTCPB012
.
CPOTCB99  RETURN
.
.------------------------------------------------------------
. Add fields for Outpatients CPB Table
.------------------------------------------------------------
OTPB0000  BRANCH    FLDITMNO,OTPB0100,OTPB0200,OTPB0300,OTPB0400,OTPB0500:
                             OTPB0600,OTPB0700,OTPB0800,OTPB0900,OTPB1000:
                             OTPB1100,OTPB1200,OTPB1300,OTPB1400,OTPB1500:
                             OTPB1600,OTPB1700,OTPB1800
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      OTPB9999
.
OTPB0100  WRITE     HTMLFILE;OTPBSITE;
          GOTO      OTPB9999
.
OTPB0200  WRITE     HTMLFILE;CLINTYPE;
          GOTO      OTPB9999
.
OTPB0300  WRITE     HTMLFILE;OTPBPROB;
          GOTO      OTPB9999
.
OTPB0400  WRITE     HTMLFILE;OTPBDESC;
          GOTO      OTPB9999
.
OTPB0500  WRITE     HTMLFILE;OTPBICDC;
          GOTO      OTPB9999
.
OTPB0600  WRITE     HTMLFILE;OTPBACTV;
          GOTO      OTPB9999
.
OTPB0700  MATCH     OTPBCDAT,SP70
          GOTO      OTPB9999 IF EQUAL
          UNPACK    OTPBCDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      OTPB9999
.
OTPB0800  WRITE     HTMLFILE;OTPBCTIM;
          GOTO      OTPB9999
.
OTPB0900  MATCH     SP70,OTPBCUID
          GOTO      OTPB9999 IF EQUAL
          PACK      KEY10,OTPBCUID
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      OTPBCUID,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM;
          GOTO      OTPB9999
.
OTPB1000  MATCH     OTPBUDAT,SP70
          GOTO      OTPB9999 IF EQUAL
          UNPACK    OTPBUDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      OTPB9999
.
OTPB1100  WRITE     HTMLFILE;OTPBUTIM;
          GOTO      OTPB9999
.
OTPB1200  MATCH     SP70,OTPBUUID
          GOTO      OTPB9999 IF EQUAL
          PACK      KEY10,OTPBUUID
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      OTPBUUID,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM;
          GOTO      OTPB9999
.
OTPB1300  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          IF        F1=1
            MOVE      ONE,CLSTFLAG          * display list for 'lookup'
          ELSE
            MOVE      ZERO,CLSTFLAG         * display table for maintenance
          ENDIF
.
          CALL      CBTAB000
          GOTO      OTPB9999
.
OTPB1400  CALL      NEXTCB00
          GOTO      OTPB9999
.
OTPB1500  CALL      PREVCB00
          GOTO      OTPB9999
.
OTPB1600  MATCH     SP70,OTPBICDC
          IF        !@EQUAL
            PACK      KEY9,OTPBICDC,SP70
            CALL      RDPT10D1
            IF        OVRCD=1
              MOVE    SP70,DDESC
            ENDIF
            WRITE     HTMLFILE;DDESC;
          ELSE
            MOVE      SP70,DDESC
            WRITE     HTMLFILE;DDESC;
          ENDIF
          GOTO      OTPB9999
.
OTPB1700  UNPACK    DIM127,D1,KEY1,DIM127    * Clinic Type
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,OTPB1710,OTPB1720
          PACK      KEY12,WBSESIT,CLINTYPE,SP70
          CALL      RDCTYA1                  * Read Clinic Type File
          BRANCH    OVRCD,OTPB9999
          WRITE     HTMLFILE;OCTDESC;        * Description
          GOTO      OTPB9999
.
OTPB1710  WRITE     HTMLFILE;CLINTYPE;       * Code
          GOTO      OTPB9999
.
OTPB1720  CALL      OTBCTY00                 * clinic type selection list
          GOTO      OTPB9999
.
OTPB1800  PACK      MAPTBIND,OTPBSITE,OTPBCTYP,OTPBPROB,SP70
          CALL      VMTTAB00           * Clinical Coding System Mappings Table
          GOTO      OTPB9999
.
OTPB9999  RETURN
.
.------------------------------------------------------------
. Clinic Type List
.------------------------------------------------------------
OTBCTY00  PACK      KEY12,WBSESIT,SP70
          CALL      RDSCTYA1
OTBCTY10  CALL      RDKCTYA1
          BRANCH    OVRCD,OTBCTY99
.
          MATCH     OCTSITE,WBSESIT
          GOTO      OTBCTY10 IF NOT EQUAL
.
          MATCH     CLINTYPE,OCTCTYP
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=#"",OCTCTYP,"#" selected>":
                               OCTDESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=#"",OCTCTYP,"#">":
                               OCTDESC,"</option>"
          ENDIF
          GOTO      OTBCTY10
.
OTBCTY99  RETURN
.------------------------------------------------------------
. Table of Outpatients Clinic Type Problems
.------------------------------------------------------------
CBTAB000  IF        CLSTFLAG=1
            WRITE     HTMLFILE;"<tr align=center>":
                               "<td class=HeadingCell colspan=2><b>":
                               CLINTYPE,"</td>":
                               "</tr>":
                               "<tr>":
                               "<td class=HeadingCell><b>Problems</b></td>":
                               "<td class=HeadingCell><b>Description</b></td>":
                               "</tr>"
          ENDIF
.
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      "10",NORECORD       * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY21,WBSESIT,CLINTYPE,STARTKEY,SP70
          CALL      RSOTCPB1
          CALL      RPOTCPB1
.
CBTAB100  CALL      RKOTCPB1
          BRANCH    OVRCD,CBTAB999
.
          MATCH     WBSESIT,OTPBSITE
          GOTO      CBTAB999 IF NOT EQUAL
.
          MATCH     CLINTYPE,OTPBCTYP
          GOTO      CBTAB999 IF NOT EQUAL
.
          IF        NORECORD<>99
            ADD       ONE,RECNUMB
            IF        STARTNUM>=RECNUMB
              GOTO     CBTAB100
            ENDIF
          ENDIF
.
          CALL      CBROW000                * Display CPC Row
.
          COMPARE   NINTY9,NORECORD         * Display all records
          GOTO      CBTAB100 IF EQUAL
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      CBTAB100 IF NOT EQUAL
.
CBTAB999  RETURN
.
.------------------------------------------------------------
.  Outpatients Clinic Type Problem (TABLE ROW)
.------------------------------------------------------------
CBROW000  BRANCH  CLSTFLAG,CBROW100
.
          MATCH   "1",OTPBACTV
          IF      @EQUAL
             MOVE    "ACTIVE  ",ACTIDESC
          ELSE
             MOVE    "INACTIVE",ACTIDESC
          ENDIF
.
          MATCH   SP70,OTPBICDC
          IF      @EQUAL
             MOVE    "&nbsp",OTPBICDC
          ENDIF
.
          MATCH   SP70,ACTIDESC
          IF      @EQUAL
             MOVE    "&nbsp",ACTIDESC
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail12":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&clintype=",CLINTYPE:
                             "&otcpb003=",OTPBPROB,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             OTPBPROB,"</td>":
                             "<td>",OTPBDESC,"</td>":
                             "<td align=center>",OTPBICDC,"</td>":
                             "<td align=center>",ACTIDESC,"</td>":
                             "</tr>"
          GOTO      CBROW999
.
CBROW100  MATCH   "1",OTPBACTV
          GOTO    CBROW999 IF NOT EQUAL
.
          WRITE     HTMLFILE;"<tr valign=top>":
                             "<td><a href=#"":
                    "javascript:updateParent('",OTPBPROB,"','",OTPBDESC,"')#">":
                             OTPBPROB,"</a>":
                             "</td><td>",OTPBDESC,"</td>";
.
CBROW999  RETURN
.
.-----------------------------------------------------------
. Link to Next Group
.-----------------------------------------------------------
NEXTCB00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,D3,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",CLINTYPE
          REP       " +",OTPBPROB
          REP       " +",STARTKEY
.
          WRITE     HTMLFILE;"outweb05.pbl":
                                 "?reportno=",F2:
                                 "&template=",D3:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&clintype=",CLINTYPE:
                                 "&otcpb003=",OTPBPROB:
                                 "&startkey=",STARTKEY;
.
          REP       "+ ",CLINTYPE
          REP       "+ ",OTPBPROB
          REP       "+ ",STARTKEY
.
NEXTCB99  RETURN
.
.-------------------------------------------------------------
. Link to Prev Group
.-------------------------------------------------------------
PREVCB00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",OTPBPROB
          REP       " +",CLINTYPE
          REP       " +",STARTKEY
.
          WRITE     HTMLFILE;"outweb05.pbl":
                                "?reportno=",F2:
                                "&template=",FLDPARA:
                                "&startnum=",KEY3:
                                "&norecord=",KEY2:
                                "&clintype=",CLINTYPE:
                                "&otcpb003=",OTPBPROB:
                                "&startkey=",STARTKEY;
.
          REP       "+ ",OTPBPROB
          REP       "+ ",CLINTYPE
          REP       "+ ",STARTKEY
.
PREVCB99  RETURN
.
.------------------------------------------------------------
.   Set Parameters for OUtpatients Clinic Type Problems
.------------------------------------------------------------
SPOTCB00  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPOTCB01,SPOTCB02,SPOTCB03,SPOTCB04,SPOTCB05:
                       SPOTCB06,SPOTCB07,SPOTCB08,SPOTCB09,SPOTCB10:
                       SPOTCB11,SPOTCB12
.
          MOVE      ONE,EXIT
          GOTO      SPOTCB99
.
SPOTCB01  PACK      OTCPB001,QRYDATA,SP70
          GOTO      SPOTCB99
.
SPOTCB02  PACK      OTCPB002,QRYDATA,SP70
          GOTO      SPOTCB99
.
SPOTCB03  PACK      OTCPB003,QRYDATA,SP70
          GOTO      SPOTCB99
.
SPOTCB04  PACK      OTCPB004,QRYDATA,SP70
          GOTO      SPOTCB99
.
SPOTCB05  PACK      OTCPB005,QRYDATA,SP70
          GOTO      SPOTCB99
.
SPOTCB06  PACK      OTCPB006,QRYDATA,SP70
          GOTO      SPOTCB99
.
SPOTCB07  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      OTCPB007,CCENT,CYEAR,CMON,CDAY
          GOTO      SPOTCB99
.
SPOTCB08  PACK      OTCPB008,QRYDATA,SP70
          GOTO      SPOTCB99
.
SPOTCB09  PACK      OTCPB009,QRYDATA,SP70
          GOTO      SPOTCB99
.
SPOTCB10  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      OTCPB010,CCENT,CYEAR,CMON,CDAY
          GOTO      SPOTCB99
.
SPOTCB11  PACK      OTCPB011,QRYDATA,SP70
          GOTO      SPOTCB99
.
SPOTCB12  PACK      OTCPB012,QRYDATA,SP70
          GOTO      SPOTCB99
.
SPOTCB99  RETURN
.------------------------------------------------------------
.  Update outpatients clinic type problem table
.------------------------------------------------------------
MVOTCB00  MOVE      WBSESIT,OTPBSITE
.>>>>     MATCH     OTPBB001,Z70
.>>>>     IF        !@EQUAL
.>>>>       MOVE      OTPBB001,OTPBSITE
.>>>>     ENDIF
.
          MATCH     OTCPB002,Z70
          IF        !@EQUAL
            MOVE      OTCPB002,OTPBCTYP
          ENDIF
.
          MATCH     OTCPB003,Z70
          IF        !@EQUAL
            MOVE      OTCPB003,OTPBPROB
          ENDIF
.
          MATCH     OTCPB004,Z70
          IF        !@EQUAL
            MOVE      OTCPB004,OTPBDESC
          ENDIF
.
          MATCH     OTCPB005,Z70
          IF        !@EQUAL
            MOVE      OTCPB005,OTPBICDC
          ENDIF
.
          MATCH     OTCPB006,Z70
          IF        !@EQUAL
            MOVE      OTCPB006,OTPBACTV
          ELSE
            MOVE      ZERO,OTPBACTV
          ENDIF
.
          MATCH     OTCPB007,Z70
          IF        !@EQUAL
            MOVE      OTCPB007,OTPBCDAT
          ENDIF
.
          MATCH     OTCPB008,Z70
          IF        !@EQUAL
            MOVE      OTCPB008,OTPBCTIM
          ENDIF
.
          MATCH     OTCPB009,Z70
          IF        !@EQUAL
            MOVE      OTCPB009,OTPBCUID
          ENDIF
.
          MATCH     OTCPB010,Z70
          IF        !@EQUAL
            MOVE      OTCPB010,OTPBUDAT
          ENDIF
.
          MATCH     OTCPB011,Z70
          IF        !@EQUAL
            MOVE      OTCPB011,OTPBUTIM
          ENDIF
.
          MATCH     OTCPB012,Z70
          IF        !@EQUAL
            MOVE      OTCPB012,OTPBUUID
          ENDIF
.
MVOTCB99  RETURN
