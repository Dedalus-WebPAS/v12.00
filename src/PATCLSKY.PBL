.******************************************************************************
.*  Include Name  : PATCLSKY.PBL                                              *
.*  Description   : Keyin a clinical speciality code                          *
.*  Author        : Greg Horvat                                               *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF2 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                                                                            *
.*  Returns       : PTCICODE - Keyed in code                                  *
.*                  PTCIDESC - Description                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or spaces input                        *
.*                       = 2   Exitchar input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself                 *
.*                                                                            *
.*  Mods          :                                                           *
.*                                                                            *
.*                                                                            *
.******************************************************************************
PATCLSKY  
CLSKY000  MATCH     SP2,CKYIDEF2
          GOTO      CLSKY050 IF NOT EQUAL   * Got a default code
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN2:
                    *PECOL:EVERT,*V2LON,PTCICODE:
                    *PECOL:EVERT,*DV,PTCICODE;
          GOTO      CLSKY100
.
CLSKY050  MOVE      CKYIDEF2,PTCICODE
          KEYIN     *PECOL:EVERT,*DV,PTCICODE:
                    *PECOL:EVERT,*V2LON,*RV,PTCICODE:
                    *PECOL:EVERT,*DV,PTCICODE;
.
CLSKY100  CALL      CLSVL000                * Validate the clinical speciality
          BRANCH    EXIT,CLSKY150,CLSKY900,CLSKY910,CLSKY920
.                        "?"      valid    nothing  exitchar
          GOTO      CLSKY000
.
CLSKY150  MOVE      "0",HLEF
          CALL      GETSCR00                * Save the current screen
.
CLSKY200  CALL      PATCLSDS                * Display the clinical spec codes
          BRANCH    CNEWDS,CLSKY250
.
          CALL      PUTSCR00                * Redisplay the previous screen
          GOTO      CLSKY000
.
CLSKY250  DISPLAY   *P1:24,*EL,"Clinical Speciality : ";
          MATCH     SP2,CKYIDEF2
          GOTO      CLSKY300 IF NOT EQUAL   * Got a default code
.
          KEYIN     *P23:24,*DV,UNDLN2:
                    *P23:24,*V2LON,PTCICODE:
                    *P23:24,*DV,PTCICODE;
          GOTO      CLSKY350
.
CLSKY300  MOVE      CKYIDEF2,PTCICODE
          KEYIN     *P23:24,*DV,PTCICODE:
                    *P23:24,*V2LON,*RV,PTCICODE:
                    *P23:24,*DV,PTCICODE;
.
CLSKY350  CALL      CLSVL000                * Validate the clinical speciality
          BRANCH    EXIT,CLSKY200,CLSKY400,CLSKY450,CLSKY500
.                        "?"      valid    nothing  exitchar
          GOTO      CLSKY250
. 
CLSKY400  CALL      PUTSCR00                * Restore the previous screen
          DISPLAY   *PECOL:EVERT,*V2LON,PTCICODE;
          GOTO      CLSKY900
.
CLSKY450  CALL      PUTSCR00                * Restore the previous screen
          DISPLAY   *PECOL:EVERT,*V2LON,PTCICODE;
          GOTO      CLSKY910
.
CLSKY500  CALL      FRESCR00                * Free the screen saved
          GOTO      CLSKY920
.
CLSKY900  MOVE      FALSE,EXIT
          GOTO      CLSKY999 
.
CLSKY910  MOVE      ONE,EXIT
          GOTO      CLSKY999 
.
CLSKY920  MOVE      TWO,EXIT
CLSKY999  RETURN
+
.******************************************************************************
.*                                 CLSVL000              Called by : PATCLSKY *
.*                     Valiadte The Clinical Speciality                       *
.* Returns : Exit = 0 Invalid                                                 *
.*                = 1 "?"                                                     *
.*                = 2 Valid                                                   *
.*                = 3 Nothing                                                 *
.*                = 4 Exitchar                                                *
.******************************************************************************
CLSVL000  ENDSET    PTCICODE
          APPEND    SP2,PTCICODE
          RESET     PTCICODE
.
          MATCH     EXITCHAR,PTCICODE
          GOTO      CLSVL600 IF EQUAL       * exit char
.
          MATCH     SP2,PTCICODE
          GOTO      CLSVL700 IF EQUAL       * nothing
.
          MATCH     QUEST,PTCICODE
          GOTO      CLSVL800 IF EQUAL       * ? mark
.
          PACK      KEY2,PTCICODE
          CALL      RDPTCLS1                * Read a clinical spec file record
          IF        CKYIMODE=0
            BRANCH    OVRCD,CLSVL900        * Invalid code
          ELSE
            COMPARE   ZERO,OVRCD
            GOTO      CLSVL900 IF EQUAL     * Code exists
          ENDIF
.
          MOVE      TWO,EXIT                * Valid code
          GOTO      CLSVL999
.
CLSVL600  MOVE      FOUR,EXIT
          UNPACK    SP70,PTCICODE,PTCIDESC
          GOTO      CLSVL999
.
CLSVL700  BRANCH    CKYIMAND,CLSVL950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          UNPACK    SP70,PTCICODE,PTCIDESC
          GOTO      CLSVL999
.
CLSVL800  MOVE      ONE,EXIT
          GOTO      CLSVL999
.
CLSVL900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
.
          IF        CKYIMODE=0
            DISPLAY   *P1:24,*EL,*B,"Invalid code.  ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      CLSVL999
.
CLSVL950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is mandatory.  ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
CLSVL999  RETURN
.
          INC       PATCLSDS                * Display clinical spec codes
+
.

