.------------------------------------------------------------
. File       : PATDOCCD.PBL
.
. Functions  : This file contains standard routines for
.              Displaying Patient Names in different formats
.
. Routines   : DOCNM000  - Formats doctors name depending on System Parameter
.
. Variables  : Input - Valid read of Health Care Professional File (PMSHCPFD)  
.                    - Or Valid read of doctors file (PATDO1FD)
.                    - Move Title into FMTTITLE
.                    - Move Given Name into FMTGNAME
.                    - Move Surname into FMTSNAME
.
.              Returns - DOCFNAME - Formated Name   
.
.------------------------------------------------------------
.       V10.08.01 04/10/2016  Ania P         CAR 822868
.                 Uncommented "reset form pointer" in DOCGIX00
.       V10.07.01 21/10/2015  Ania P         CAR 309421
.                 Strip FMTTITLE to stop additional blank spaces
.       V10.02.01 14/10/2011  Mike Laming    CAR 251645
.                 Renamed old DOCGIV00 as DOCGIX00 and reinstated this (was
.                 commented out as it clashed with PATDOCTM.PBL)
.       V9.10.01  23/09/2008  Davin          CAR 180172
.                 Commented out calls to namstr00 as per 9.08
.                 (in DOCNM040, DOCNM050 & DOCNM060)
.       V9.09.01  18/12/2007  Peter McMullen CAR 155123
.                 Added support for text case as entered
.       V9.03.01  16/03/2004  Peter Vela     CAR 13038
.                 Added more doctor name formats
.	V5.10.00  16/04/2001  Bronko Sosic
.                 Created Include
.------------------------------------------------------------
DOCNM000  CLEAR    DISPNAME
          MOVE      SP70,DOCFNAME
          READ     CONTROLF,HUND01;*47,CWEBDNAM,*53,CWEBDNAI 
          MOVE     CWEBDNAM,F2
          STRIP    FMTTITLE
          BRANCH   F2,DOCNM010,DOCNM020,DOCNM030,DOCNM040,DOCNM050,DOCNM060
.                                                                  * C CAR 13038
          RETURN             
.------------------------------------------------------------
.         DOCNAM10  Short Doctor Name (Title Initial. Surname)
.------------------------------------------------------------
DOCNM010  PACK      NAME35,FMTTITLE,SP70
          MATCH     "1",CWEBDNAI       * name left as is? 
          IF        @EQUAL
           CALL       NAMASI00
          ELSE
           CALL       NAMSTR00
          ENDIF
          PACK      NAME35,FMTGNAME,SP70
          SQUEEZE   NAME35
          MOVE      NAME35,KEY1
          APPEND    KEY1,DISPNAME
          APPEND    ". ",DISPNAME
          PACK      NAME35,FMTSNAME,SP70
          MATCH     "1",CWEBDNAI       * name left as is?
          IF        @EQUAL
           CALL       NAMASI00
          ELSE
           CALL       NAMSTR00
          ENDIF
          RESET     DISPNAME
          MOVE      DISPNAME,DOCFNAME
          STRIP     DOCFNAME
          RETURN
.------------------------------------------------------------
.         DOCNAM20  Doctor Name (Title Fisrt name initial second name Surname)
.------------------------------------------------------------
DOCNM020  PACK      NAME35,FMTTITLE,SP70
          MATCH     "1",CWEBDNAI       * name left as is?
          IF        @EQUAL
           CALL       NAMASI00
          ELSE
           CALL       NAMSTR00
          ENDIF
          PACK      NAME35,FMTGNAME,SP70
          CALL      DOCGIX00
          PACK      NAME35,FMTSNAME,SP70
          MATCH     "1",CWEBDNAI       * name left as is?
          IF        @EQUAL
           CALL       NAMASI00
          ELSE
           CALL       NAMSTR00
          ENDIF
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,DOCFNAME
          STRIP     DOCFNAME
          RETURN
.------------------------------------------------------------
.         DOCNAM30  Doctor Name (Title First name Second name Surname)
.------------------------------------------------------------
DOCNM030  PACK      NAME35,FMTTITLE,SP70
          MATCH     "1",CWEBDNAI       * name left as is?
          IF        @EQUAL
           CALL       NAMASI00
          ELSE
           CALL       NAMSTR00
          ENDIF
          PACK      NAME35,FMTGNAME,SP70
          MATCH     "1",CWEBDNAI       * name left as is?
          IF        @EQUAL
           CALL       NAMASI00
          ELSE
           CALL       NAMSTR00
          ENDIF
          PACK      NAME35,FMTSNAME,SP70
          MATCH     "1",CWEBDNAI       * name left as is?
          IF        @EQUAL
           CALL       NAMASI00
          ELSE
           CALL       NAMSTR00
          ENDIF
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,DOCFNAME
          STRIP     DOCFNAME
          RETURN
.------------------------------------------------------------ * I CAR 13038
.         DOCNAM40  Doctor Name (Surname, Firstname Second Name (Title))
.------------------------------------------------------------
DOCNM040  PACK      NAME35,FMTSNAME,COMMA,SP70
          MATCH     "1",CWEBDNAI       * name left as is?
          IF        @EQUAL
           CALL       NAMASI00
          ELSE
           CALL       NAMSTR00
          ENDIF
          PACK      NAME35,FMTGNAME,SP70
          MATCH     "1",CWEBDNAI       * name left as is?
          IF        @EQUAL
           CALL       NAMASI00
          ELSE
           CALL       NAMSTR00
          ENDIF
.         PACK      NAME35,LBRACK,FMTTITLE,RBRACK,SP70
.>>>>     MATCH     "1",CWEBDNAI       * name left as is?
.>>>>     IF        @EQUAL
.>>>>      CALL       NAMASI00
.>>>>     ELSE
.>>>>      CALL       NAMSTR00
.>>>>     ENDIF
          APPEND    LBRACK,DISPNAME
          APPEND    FMTTITLE,DISPNAME
          APPEND    RBRACK,DISPNAME
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,DOCFNAME
          STRIP     DOCFNAME
          RETURN
.------------------------------------------------------------ * I CAR 13038
.         DOCNAM50  Short Doctor Name (Surname, Initial. (Title))
.------------------------------------------------------------
DOCNM050  PACK      NAME35,FMTSNAME,COMMA,SP70
          MATCH     "1",CWEBDNAI       * name left as is?
          IF        @EQUAL
           CALL       NAMASI00
          ELSE
           CALL       NAMSTR00
          ENDIF
          PACK      NAME35,FMTGNAME,SP70
          SQUEEZE   NAME35
          MOVE      NAME35,KEY1
          APPEND    KEY1,DISPNAME
          APPEND    ". ",DISPNAME
.         PACK      NAME35,FMTTITLE,SP70
.>>>>     MATCH     "1",CWEBDNAI       * name left as is?
.>>>>     IF        @EQUAL
.>>>>      CALL       NAMASI00
.>>>>     ELSE
.>>>>      CALL       NAMSTR00
.>>>>     ENDIF
          APPEND    LBRACK,DISPNAME
          APPEND    FMTTITLE,DISPNAME
          APPEND    RBRACK,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,DOCFNAME
          STRIP     DOCFNAME
          RETURN
.------------------------------------------------------------ * I CAR 13038
.         DOCNAM60  Doctor Name (Surname, Firstname initial Second Name (Title))
.------------------------------------------------------------
DOCNM060  PACK      NAME35,FMTSNAME,COMMA,SP70
          MATCH     "1",CWEBDNAI       * name left as is?
          IF        @EQUAL
           CALL       NAMASI00
          ELSE
           CALL       NAMSTR00
          ENDIF
          PACK      NAME35,FMTGNAME,SP70
          CALL      DOCGIX00
.         PACK      NAME35,LBRACK,FMTTITLE,RBRACK,SP70
.>>>>     MATCH     "1",CWEBDNAI       * name left as is?
.>>>>     IF        @EQUAL
.>>>>      CALL       NAMASI00
.>>>>     ELSE
.>>>>      CALL       NAMSTR00
.>>>>     ENDIF
          APPEND    LBRACK,DISPNAME
          APPEND    FMTTITLE,DISPNAME
          APPEND    RBRACK,DISPNAME
          APPEND    SP70,DISPNAME
          RESET     DISPNAME
          MOVE      DISPNAME,DOCFNAME
          STRIP     DOCFNAME
          RETURN
.-------------------------------------------------
. Format Doctors Given Name - was DOCGIV00 but clashes with PATDOCTM.PBL which
.                           also has DOCGIV00. Changed to DOCGIX00 & reinstated
.-------------------------------------------------
DOCGIX00  CMATCH    SP1,NAME35               * Remove Leading Blanks

          IF        @EQUAL
            BUMP      NAME35
            GOTO      DOCGIX00 IF NOT EOS
            GOTO      DOCGIX99               * entire name if blank
          ENDIF
          PACK      KEY35,NAME35,SP30        * reset form pointer
          MOVE      KEY35,NAME35
.
          SCAN      SP1,NAME35             * Locate the blank
          GOTO      DOCGIX20 IF NOT EQUAL
          BUMP      NAME35,-1              * go back 1 to end of name
          MOVEFPTR  NAME35,CCITEM          * another handly form field
          RESET     NAME35                 * reset the form pointer
          SETLPTR   NAME35,CCITEM          * set logical length to end of name
.
.         Save this name
.
          UNPACK    NAME35,KEY1,KEY50
          REP       DOWNCASE,KEY50
          APPEND    KEY1,DISPNAME
          STRIP     KEY50
          APPEND    KEY50,DISPNAME
          APPEND    SP1,DISPNAME
.
.         Check for any more names
.
          SETLPTR   NAME35,35              * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     NAME35,CCITEM          * reset to this point
          PACK      KEY35,NAME35,SP70      * reset form pointer
          MOVE      KEY35,NAME35

DOCGIX10  CMATCH    SP1,NAME35               * Remove Leading Blanks
          IF        @EQUAL
            BUMP      NAME35
            GOTO      DOCGIX10 IF NOT EOS
            GOTO      DOCGIX99               * entire name if blank
         ENDIF
         PACK      KEY35,NAME35,SP30        * reset form pointer
         MOVE      KEY35,NAME35

          SCAN      SP1,NAME35             * Locate the blank
          GOTO      DOCGIX20 IF NOT EQUAL
          BUMP      NAME35,-1              * go back 1 to end of name
          MOVEFPTR  NAME35,CCITEM          * another handly form field
          RESET     NAME35                 * reset the form pointer
          SETLPTR   NAME35,CCITEM          * set logical length to end of name
.
.         Save this Initial
.
          UNPACK    NAME35,KEY1,KEY50
          APPEND    KEY1,DISPNAME
          APPEND    SP1,DISPNAME
.
          GOTO      DOCGIX99
.
.         Rest of the string is one name
.
DOCGIX20  UNPACK    NAME35,KEY1,KEY50
          REP       DOWNCASE,KEY50
          APPEND    KEY1,DISPNAME
          STRIP     KEY50
          APPEND    KEY50,DISPNAME
          APPEND    SP1,DISPNAME
.
DOCGIX99  RETURN
