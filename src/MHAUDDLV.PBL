.******************************************************************************
.* System         : Mental Health                                             *
.*                                                                            *
.* Include Name   : MHAUDDLV.PBL                                              *
.*                                                                            *
.* Function       : Maintain the MEH Discharge-to-leave file audits.          *
.*                                                                            *
.* Author         : Justin Coates                                             *
.*                                                                            *
.* Date Written   : 17/09/1991                                                *
.*                                                                            *
.* Index used     : MEHAUDDL,"mehauddl"                                       *
.*                                                                            *
.* Parameters     : MHCNAUDC      Audits being used; 1 = No; 0 = Yes          *
.*                  AUDIFLAG      which type of audit to do                   *
.*                     1 = 'ADD' audit                                        *
.*                     2 = 'BEFORE CHANGE' audit                              *
.*                     3 = 'AFTER CHANGE' audit                               *
.*                     4 = 'DELETE' audit                                     *
.*                     5 = delete 'before change' audit                       *
.*                                                                            *
.* Modifications  :                                                           *
.******************************************************************************
.
MHAUDDLV  BRANCH    MHCNAUDC,AUDDL999       * audits not on
.
          COMPARE   FIVE,AUDIFLAG
          GOTO      AUDDL700 IF EQUAL       * delete before change audit
.
          COMPARE   THREE,AUDIFLAG
          GOTO      AUDDL200 IF EQUAL       * after change - leave date/time
.
.         get the current date and time
.
          CALL      IBACLOCK                * get date and time
          PACK      MHDLAUDD,CCC,CYY,CMM,CDD
          REP       " 0",MHDLAUDD           * date
          MOVE      CTIMEIS,MHDLAUDT        * time
.
AUDDL200  MOVE      PORT,MHDLAUDP           * port
          MOVE      AUDIFLAG,MHDLAUDR       * copy audit type
          REP       "1A2B3C4D",MHDLAUDR     * get the correct letter
          MOVE      ONE,MHDLAUDS            * set as not printed
          MOVE      PASSCODE,MHDLAUDO       * operator id
.
          PACK      KEY19,MHDLAUDD,MHDLAUDT,MHDLAUDP,MHDLAUDR
          CALL      AWMHDLV                 * write the audit
          GOTO      AUDDL999
.
.         delete the before change audit
.
AUDDL700  PACK      KEY19,MHDLAUDD,MHDLAUDT,MHDLAUDP,ANSB
          CALL      ADMHDLV                 * delete the audit
.
AUDDL999  RETURN
+
