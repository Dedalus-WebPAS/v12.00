.******************************************************************************
.* System         : Patient Management                                        *
.* Include        : PATWIS11.PBL                                              *
.* Description    : Calculate WIES11 value for an individual admission        *
.******************************************************************************
.* Date           : 08/08/2003                                                *
.* Author         : Henry Son                                                 *
.*                                                                            *
.* Vars Required  : VARWIS11 - Variables required for WIES11                  *
.*                                                                            *
.* Files Required Open :                                                      *
.*        PATASFFD  PATASFA1 - Admission special funding arrangement file     *
.*        PATCODFD  PATCODE1 - Codes file                                     *
.*        PATECDFD  PATECDA2 - Episode disease coding file                    *
.*        PATECOFD  PATECOA2 - Episode operation coding file                  *
.*        PAT10OFD  PATI10O1 - ICD10 operations file                          *
.*        PATICUFD  PATICUA1 - ICU file                                       *
.*        PATTRNFD  PATTRAN2 - Transfer file                                  *
.*        PATW11FD  PATW11A1 - Victorian cost weights (for WIES11) file       *
.*                                                                            *
.* Parameters Req : Successful read on admission file                         *
.*                  Successful read on discharge file                         *
.*                  NBRDAYS  - for LOS calculation (from NHOSPDAY)            *
.*                  PNSWRACE - User defined field for race                    *
.*                  WIS11DRG - DRG to calculate WIES11 for                    *
.*                                                                            *
.* Returns        : TOTWIS11 - Total WIES11 value                             *
.*                  HIHLOS   - for Hospital in Home LOS (from RHIHDAYS)       *
.*                  WIS11DRG - DRG used to calculate WIES11                   *
.*                  WIS11LOS - LOS used to calculate WIES11                   *
.*                  WIS11WGT - Weight used to calculate WIES11                *
.*                  WIS11DSC - Description of DRG weight used                 *
.*                                                                            *
.* Modifications  :                                                           *
.******************************************************************************
.* V12.00.01 20/05/2025 Thanh T         TSK 0955096                           *
.*           Changed for alphanumeric visit number                            *
.******************************************************************************
.*        V10.02.01 28/03/2011  Mike Laming   CAR 240107                      *
.*                  Mods to PATECDaf & PATECOaf variables & Keys - file change*
.*        V9.03.03  21/01/2004  Henry Son  CAR 46856                          *
.*                  Further mapping changs to Nasopharyngeal Intubation.      *
.*        V9.03.02  23/11/2003  Henry Son  CAR 41650                          *
.*                  Fix Nasopharyngeal Intubation.                            *
.*        V9.03.01  08/08/2003  CAR 42039   Henry Son                         *
.*                  Modified from PATWIS10 for 2003/2004 changes              *
.******************************************************************************
+
PATWIS11
WIS11000  CALL      IN11W000                * Initialise variables
          BRANCH    EXIT,WIS11950
.
          CALL      C11TY000                * Determine and Check Care Type
          BRANCH    EXIT,WIS11950
.
          CALL      C11TR000                * Check for Contracted Patient
          BRANCH    EXIT,WIS11950
.
          CALL      M11RG000                * Modify DRG if necessary
.
          CALL      C11RG000                * Check for problem DRG
          BRANCH    EXIT,WIS11950
.
          CALL      M11CH000                * Check for Mechanical Ventilation
.
          CALL      T11AL000                * Check for Thalessaemia
.
          CALL      A11AA000                * Check for AAA
.
          CALL      A11SD000                * Check for ASD
.
          CALL      C11OL000                * Check for Colonoscopy
.
          CALL      B11SW000                * Calculate Base WIES11
.
          CALL      C11AB000                * Check for aboriginality
.
. ---- Calculate WIES11 value ----
.
          MOVE      TOTBWIES,TOTWIS11       * base WIES
          ADD       MVCOPAY,TOTWIS11        * mechv copayment
          ADD       THCOPAY,TOTWIS11        * thal  copayment
          ADD       AAACOPAY,TOTWIS11       * AAA   copayment
          ADD       ASDCOPAY,TOTWIS11       * ASD   copayment
          ADD       COLCOPAY,TOTWIS11       * colon copayment
          ADD       ATSIWIES,TOTWIS11       * ATSI  copayment
.
          MOVE      ZERO,EXIT
          GOTO      WIS11999
.
WIS11950  MOVE      ONE,EXIT
WIS11999  RETURN
+
.******************************************************************************
.*                                  IN11W000              Called by: WIS11000 *
.*                         Initialise WIES11 variables                        *
.******************************************************************************
IN11W000  MOVE      ZERO,TOTWIS11
          MOVE      ZERO,WIS11LOS
          MOVE      ZERO,WIS11WGT
          MOVE      SP20,WIS11DSC
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATW11A1,"patw11af"
          TRAPCLR   IO
          BRANCH    OVRCD,IN11W950
.
IN11W900  MOVE      ZERO,EXIT
          GOTO      IN11W999
.
IN11W950  MOVE      ONE,EXIT
IN11W999  RETURN
+
.******************************************************************************
.*                                  C11TY000              Called by: WIS11000 *
.*                 Check for valid Care Type & Incomplete Coding              *
.******************************************************************************
C11TY000  PACK     KEY5,ANSC,ANSC,ACARE     * use admission care type
          CALL     RDCODE1
          BRANCH   OVRCD,C11TY900
.
          MATCH    "6",TCINDC4              * Check Program Funding Source
          GOTO     C11TY900 IF NOT EQUAL
.
          MATCH    "0   ",THCSCOD           * check care types
          GOTO     C11TY900 IF EQUAL
          MATCH    "1   ",THCSCOD
          GOTO     C11TY900 IF EQUAL
          MATCH    "2   ",THCSCOD
          GOTO     C11TY900 IF EQUAL
          MATCH    "6   ",THCSCOD
          GOTO     C11TY900 IF EQUAL
          MATCH    "7   ",THCSCOD
          GOTO     C11TY900 IF EQUAL
          MATCH    "8   ",THCSCOD
          GOTO     C11TY900 IF EQUAL
          MATCH    "9   ",THCSCOD
          GOTO     C11TY900 IF EQUAL
          MATCH    "5   ",THCSCOD
          GOTO     C11TY900 IF EQUAL
          MATCH    "3   ",THCSCOD
          GOTO     C11TY900 IF EQUAL
          MATCH    "E   ",THCSCOD
          GOTO     C11TY900 IF EQUAL
          MATCH    "F   ",THCSCOD
          GOTO     C11TY900 IF EQUAL
          MATCH    "J   ",THCSCOD
          GOTO     C11TY900 IF EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     C11TY999
.
C11TY900  MOVE     ONE,EXIT                 * Not valid
C11TY999  RETURN
+
.******************************************************************************
.*                                  C11TR000              Called by: WIS11000 *
.*                        Check for Contracted Patient                        *
.******************************************************************************
C11TR000  PACK     KEY25,AADMNO,ANSA,SP30
          CALL     RSPTASF1
C11TR100  CALL     RKPTASF1                 * Read adm. special funding file
          BRANCH   OVRCD,C11TR800           * still process
.
          MATCH    AADMNO,PTAFADMN
          GOTO     C11TR800 IF NOT EQUAL    * still process
.
          MATCH    ANSA,PTAFTYPE            * special fund. arrang. type = adm
          GOTO     C11TR100 IF NOT EQUAL
.
          MATCH    ANSB,PTAFCROL            * Ignore if contract role = 'B'
          GOTO     C11TR900 IF EQUAL
.
C11TR800  MOVE     ZERO,EXIT                * valid
          GOTO     C11TR999
.
C11TR900  MOVE     ONE,EXIT                 * invalid
C11TR999  RETURN
+
.******************************************************************************
.*                                  M11RG000              Called by: WIS11000 *
.*                      Modify DRG if necessary for WIES11                    *
.******************************************************************************
M11RG000  CALL      P11IT000                * Peritoneal Dialysis
          CALL      R11IO000                * Radiotherapy
          CALL      B11EM000                * Bone Marrow Transplants
          CALL      H11TE000                * Hysterscopy Sterilisation
.
...       CALL      N11SO000                * Nasopharyngeal Intubation*I-41650
...                                         *   Mapping done before    *D-46856
...                                         *   details sent to grouper
.
M11RG999  RETURN
+
.******************************************************************************
.*                                  P11IT000              Called by: M11RG000 *
.*                      Modify DRG if Peritoneal Dialysis                     *
.******************************************************************************
P11IT000  PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
P11IT100  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,P11IT999
.
          MATCH     AADMNO,PTEDADMN
          GOTO      P11IT999 IF NOT EQUAL
.
          MATCH     CMDISP,PTEDTYPE         * want principal diagnosis
          GOTO      P11IT100 IF NOT EQUAL
.
          MATCH     "Z49.2",PTEDCODE
          GOTO      P11IT100 IF NOT EQUAL
.
          MOVE      "L61Y",WIS11DRG         * modified for WIES11
P11IT999  RETURN
+
.******************************************************************************
.*                                  R11IO000              Called by: M11RG000 *
.*                         Modify DRG if Radiotherapy                         *
.******************************************************************************
R11IO000  PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
R11IO100  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,R11IO999
.
          MATCH     AADMNO,PTEDADMN         * same admission?
          GOTO      R11IO999 IF NOT EQUAL
.
          MATCH     "Z51.0",PTEDCODE        * radiotherapy?
          GOTO      R11IO100 IF NOT EQUAL
.
          MOVE      "R64Z",WIS11DRG         * modified for WIES11
R11IO999  RETURN
+
.******************************************************************************
.*                                  B11EM000              Called by: M11RG000 *
.*                    Modify DRG if Bone Marrow Transplant                    *
.******************************************************************************
B11EM000  MATCH     "A04Z",WIS11DRG
          GOTO      B11EM999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
B11EM100  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,B11EM900
.
          MATCH     AADMNO,PTEOADMN
          GOTO      B11EM900 IF NOT EQUAL
.
          MATCH     "13706-00",PTEOCODE
          IF        !@EQUAL
            MATCH     "13706-06",PTEOCODE
            IF        !@EQUAL
              MATCH     "13706-09",PTEOCODE
              IF        !@EQUAL
                MATCH     "13706-10",PTEOCODE
                GOTO      B11EM100 IF NOT EQUAL
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      "A04A",WIS11DRG         * new DRG = A04A
          GOTO      B11EM999
.
B11EM900  MOVE      "A04B",WIS11DRG         * new DRG = A04B
.
B11EM999  RETURN
+
.******************************************************************************
.*                                  H11TE000              Called by: M11RG000 *
.*                    Modify DRG if Hysterscopy Sterilisation                 *
.******************************************************************************
H11TE000  MATCH     "N09Z",WIS11DRG
          IF        !@EQUAL
            MATCH     "N10Z",WIS11DRG
            IF        !@EQUAL
              MATCH     "N11B",WIS11DRG
              IF        !@EQUAL
                MATCH     "O40Z",WIS11DRG
                GOTO      H11TE999 IF NOT EQUAL
              ENDIF
            ENDIF
          ENDIF
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
H11TE100  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,H11TE999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      H11TE999 IF NOT EQUAL
.
          MATCH     "35688-01",PTEOCODE
          IF        !@EQUAL        
            GOTO      H11TE100 IF NOT EQUAL
          ENDIF
.
          MOVE      "N11C",WIS11DRG         * new DRG = N11C
          GOTO      H11TE999
.
H11TE999  RETURN
+
.******************************************************************************
.*                                  N11SO000              Called by: M11RG000 *
.*                    Modify DRG if Nasopharyngeal Instubation                *
.******************************************************************************
.         For Nasopharyngeal Instubation check the operations code and
.         Hours of Mechanical ventilation
.******************************************************************************
N11SO000  PACK      KEY16,AADMNO,SP20                                  *I-41650
          CALL      RSPTECO2                * Position on & read a patient
N11SO100  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,N11SO999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      N11SO999 IF NOT EQUAL
.
          MATCH     "90179-02",PTEOCODE
          IF        !@EQUAL
            MATCH     "90179-05",PTEOCODE
            IF        !@EQUAL
              GOTO      N11SO100 IF NOT EQUAL
            ENDIF
          ENDIF
.
          MOVE      "A41Z",WIS11DRG         * Default  DRG = A41Z
          PACK      KEY8,AADMNO
          CALL      RDPTICU1                * get mech. vent. hours
          BRANCH    OVRCD,N11SO999
.
          IF        PTICUMEC > 95
            MOVE      "A06Z",WIS11DRG       * If mech vent >= 96 hrs
          ENDIF                             * then new DRG = A06Z
.                                           * else new DRG = A41Z - default
          GOTO      N11SO999
.
N11SO999  RETURN
+
.******************************************************************************
.*                                  C11RG000              Called by: WIS11000 *
.*                            Check for Problem DRG                           *
.******************************************************************************
C11RG000  PACK     KEY4,WIS11DRG
          CALL     RDPT111                  * check WIES11 weights file
          BRANCH   OVRCD,C11RG900
.
          MATCH    "960Z",PT11DRGC
          GOTO     C11RG900 IF EQUAL
.
          MATCH    "961Z",PT11DRGC
          GOTO     C11RG900 IF EQUAL
.
          MATCH    "962Z",PT11DRGC
          GOTO     C11RG900 IF EQUAL
.
          MATCH    "963Z",PT11DRGC
          GOTO     C11RG900 IF EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     C11RG999
.
C11RG900  MOVE     ONE,EXIT
C11RG999  RETURN
+
.******************************************************************************
.*                                  M11CH000              Called by: WIS11000 *
.*                Calculate Mechanical Ventialtion Co-payment                 *
.******************************************************************************
M11CH000  MOVE      ZERO,ADJMVDAY
          MOVE      ZERO,MVCOPAY
.
          PACK      KEY8,AADMNO
          CALL      RDPTICU1                * get mech. vent. hours
          BRANCH    OVRCD,M11CH999
.
          MATCH     "D",PT11MVEL            * check mech. vent. eligibility
          IF        @EQUAL
            IF        PTICUMEC > 6
              ASSIGN    ((PTICUMEC+12)/24),FORM12P4    * days receiving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7729)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
            GOTO      M11CH999
          ENDIF
.
          MATCH     "N",PT11MVEL            * check mech. vent. eligibility
          IF        @EQUAL
            IF        PTICUMEC > 6
              ASSIGN    ((PTICUMEC+12)/24),FORM12P4    * days receiving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7729)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
            GOTO      M11CH999
          ENDIF
.
          MATCH     "4",PT11MVEL
          IF        @EQUAL
            IF        PTICUMEC > 96
              ASSIGN    (((PTICUMEC+12)/24)-4),FORM12P4   * days recving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7729)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
          ENDIF
.
M11CH999  RETURN
+
.******************************************************************************
.*                                  T11AL000              Called by: WIS11000 *
.*                      Calculate Thalessaemia Co-payment                     *
.******************************************************************************
T11AL000  MOVE      ZERO,THCOPAY
          MATCH     "T",PT11CPAY            * check WIES11 weights file
          IF        @EQUAL
            PACK      KEY16,AADMNO,SP20
            CALL      RSPTECD2              * Position on & read a patient
T11AL100    CALL      RKPTECD2                episode disease file record
            BRANCH    OVRCD,T11AL999
.
            MATCH     AADMNO,PTEDADMN
            GOTO      T11AL999 IF NOT EQUAL
.
            MATCH     "D56",PTEDCODE        * check ICD-10 code
            IF        !@EQUAL
              MATCH     "D57.2",PTEDCODE    * check ICD-10 code
              GOTO      T11AL100 IF NOT EQUAL
            ENDIF
            MOVE      "0.2648",THCOPAY      * eligible for thal. copayment
          ENDIF
T11AL999  RETURN
+
.******************************************************************************
.*                                  A11AA000              Called by: WIS11000 *
.*                          Calculate AAA Co-payment                          *
.******************************************************************************
A11AA000  MOVE      ZERO,AAACOPAY
          MATCH     "AAA",PT11CPAY          * check WIES11 weights file
          GOTO      A11AA999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
A11AA100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,A11AA999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      A11AA999 IF NOT EQUAL
.
          MATCH     "33116-00",PTEOCODE     * check ICD-10 code
          GOTO      A11AA100 IF NOT EQUAL
.
          MOVE      "3.1421",AAACOPAY       * eligible for AAA copayment
A11AA999  RETURN
+
.******************************************************************************
.*                                  A11SD000              Called by: WIS11000 *
.*                          Calculate ASD Co-payment                          *
.******************************************************************************
A11SD000  MOVE      ZERO,ASDCOPAY
          MATCH     "ASD",PT11CPAY          * check WIES11 weights file
          GOTO      A11SD999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
A11SD100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,A11SD999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      A11SD999 IF NOT EQUAL
.
          MATCH     "38742-00",PTEOCODE     * check ICD-10 code
          GOTO      A11SD100 IF NOT EQUAL
.
          MOVE      "2.4713",ASDCOPAY       * eligible for ASD copayment
A11SD999  RETURN
+
.******************************************************************************
.*                                  C11OL000              Called by: WIS11000 *
.*                       Calculate Colonoscopy Co-payment                     *
.******************************************************************************
C11OL000  MOVE      ZERO,COLCOPAY
          MATCH     "Col",PT11CPAY          * check WIES11 weights file
          GOTO      C11OL999 IF NOT EQUAL
.
          CALL      LDCOA000                * load col. copayment codes
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
C11OL100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,C11OL999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      C11OL999 IF NOT EQUAL
.
          MOVE      ZERO,COUNT
C11OL200  ADD       ONE,COUNT
          COMPARE   TEN2,COUNT
          GOTO      C11OL100 IF EQUAL       * check next code on patecoaf
.
          MATCH     COLCODES[COUNT],PTEOCODE     * check ICD-10 code
          GOTO      C11OL200 IF NOT EQUAL
.
          MOVE      "0.1765",COLCOPAY       * eligible for Colono. copayment
C11OL999  RETURN
+
.******************************************************************************
.*                                  LDCOA000              Called by: C11OL000 *
.*                      Load Colonoscopy Copayment Codes                      *
.******************************************************************************
LDCOA000  MOVE      "30479-01",COLCODES[1]
          MOVE      "30479-02",COLCODES[2]
          MOVE      "90308-00",COLCODES[3]
          MOVE      "90312-01",COLCODES[4]
          MOVE      "32084-00",COLCODES[5]
          MOVE      "32084-01",COLCODES[6]
          MOVE      "32087-00",COLCODES[7]
          MOVE      "32090-00",COLCODES[8]
          MOVE      "32090-01",COLCODES[9]
          MOVE      "32093-00",COLCODES[10]
          MOVE      "32094-00",COLCODES[11]
LDCOA999  RETURN
+
.******************************************************************************
.*                                  B11SW000              Called by: WIS11000 *
.*                            Calculate Base WIES11                           *
.******************************************************************************
B11SW000  CALL      D11SC000                * length of stay category
          CALL      C11ST000                * calculate inlier status
.
          MATCH     "L",W11NLIER            * low outlier
          CALL      BW11L000 IF EQUAL       * calculate base WIES11
.
          MATCH     "I",W11NLIER            * inlier
          CALL      BW11N000 IF EQUAL       * calculate base WIES11
.
          MATCH     "H",W11NLIER            * high outlier
          CALL      BW11H000 IF EQUAL       * calculate base WIES11
.
B11SW999  RETURN
+
.******************************************************************************
.*                                  D11SC000              Called by: B11SW000 *
.*                      Determine Length Of Stay Category                     *
.******************************************************************************
D11SC000  MOVE      NBRDAYS,WIS11LOS        * length of stay from NHOSPDAY
          MOVE      "M",LOSCAT              * set LOS category to multiday
          MATCH     ADATE,DDATE             * check for sameday
          IF        @EQUAL
            MOVE      ONE,SAMEDAY           * sameday
            MOVE      ONE,WIS11LOS          * LOS = 1
            MOVE      "S",LOSCAT            * set LOS category to sameday
            GOTO      D11SC999
          ELSE
            MOVE      ZERO,SAMEDAY          * not sameday
          ENDIF
.
          IF        SAMEDAY = 0 & WIS11LOS = 1
            MOVE      "O",LOSCAT            * set LOS category to oneday
            GOTO      D11SC999
          ENDIF
.
          IF        WIS11LOS > 1825
            MOVE      "1825",WIS11LOS       * maximum LOS is 5 years
          ENDIF
.
D11SC999  RETURN
+
.******************************************************************************
.*                                  C11ST000              Called by: B11SW000 *
.*                           Calculate Inlier Status                          *
.******************************************************************************
C11ST000  MOVE      "I",W11NLIER            * set status to inlier
          IF        WIS11LOS < PT11LIBP
            MOVE      "L",W11NLIER          * set status to low outlier
            GOTO      C11ST999
          ENDIF
.
          ASSIGN    (PT11HIBP+ADJMVDAY),FORM12P4   * high bound. + mech.v days
          IF        WIS11LOS > FORM12P4
            MOVE      "H",W11NLIER          * set status to high outlier
          ENDIF
C11ST999  RETURN
+
.******************************************************************************
.*                                  BW11L000              Called by: B11SW000 *
.*                     Calculate Base WIES11 for Low Outlier                  *
.******************************************************************************
BW11L000  MATCH     "S",LOSCAT            * sameday
          IF        @EQUAL
            MOVE      PT11TSDW,TOTBWIES             * base WIES11
            ASSIGN    (TOTBWIES/PT11TIMW),TOTIES11  * IES11
.
            MOVE      PT11TSDW,WIS11WGT             * for display
            MOVE      "Same Day Wgt ",WIS11DSC
            GOTO      BW11L999
          ENDIF
.
          MATCH     "O",LOSCAT            * oneday
          IF        @EQUAL
            MOVE      PT11TODW,TOTBWIES             * base WIES11
            ASSIGN    (TOTBWIES/PT11TIMW),TOTIES11  * IES11
.
            MOVE      PT11TODW,WIS11WGT             * for display
            MOVE      "One Day Wgt  ",WIS11DSC
            GOTO      BW11L999
          ENDIF
.
          MATCH     "M",LOSCAT            * multiday
          IF        @EQUAL
            ASSIGN    (PT11TODW+((WIS11LOS-1)*PT11TLOM)),TOTBWIES
.                                                    * base WIES11
            ASSIGN    (TOTBWIES/PT11TIMW),TOTIES11   * IES11
.
            MOVE      PT11TLOM,WIS11WGT             * for display
            MOVE      "Low Outlr Wgt",WIS11DSC
            GOTO      BW11L999
          ENDIF
BW11L999  RETURN
+
.******************************************************************************
.*                                  BW11N000              Called by: B11SW000 *
.*                      Calculate Base WIES11 for Inlier                      *
.******************************************************************************
BW11N000  MOVE      ONE,TOTIES11          * IES11 = 1
          MATCH     "S",LOSCAT            * sameday
          IF        @EQUAL
            MOVE      PT11TSDW,TOTBWIES   * base WIES11
.
            MOVE      PT11TSDW,WIS11WGT             * for display
            MOVE      "Same Day Wgt ",WIS11DSC
            GOTO      BW11N999
          ENDIF
.
          MATCH     "O",LOSCAT            * oneday
          IF        @EQUAL
            MOVE      PT11TODW,TOTBWIES   * base WIES11
.
            MOVE      PT11TODW,WIS11WGT             * for display
            MOVE      "One Day Wgt  ",WIS11DSC
            GOTO      BW11N999
          ENDIF
.
          MATCH     "M",LOSCAT            * multiday
          IF        @EQUAL
            MOVE      PT11TIMW,TOTBWIES   * base WIES11
.
            MOVE      PT11TIMW,WIS11WGT             * for display
            MOVE      "Inlier Wgt   ",WIS11DSC
            GOTO      BW11N999
          ENDIF
BW11N999  RETURN
+
.******************************************************************************
.*                                  BW11H000              Called by: B11SW000 *
.*                    Calculate Base WIES11 for High Outlier                  *
.******************************************************************************
BW11H000  ASSIGN    ((WIS11LOS-PT11HIBP)-ADJMVDAY),HIGHDAYS  * high outlr days
          COMPARE   ZERO,HIGHDAYS
          IF        @LESS
            MOVE      ZERO,HIGHDAYS         * highdays must be >= zero
          ENDIF
.
          COMPARE   HIGHDAYS,HIHLOS
          IF        @LESS
            MOVE      HIHLOS,HITHDAYS       * get lesser of highdays and
          ELSE                              *  HIHLOS
            MOVE      HIGHDAYS,HITHDAYS
          ENDIF
.
          ASSIGN    (HIGHDAYS-HITHDAYS)*PT11THOM,TOTBWIES    *high WIES
          ASSIGN    (HITHDAYS*PT11THIH)+TOTBWIES,TOTBWIES    *HITH WEIS
          ASSIGN    (PT11TIMW+TOTBWIES),TOTBWIES             *base WEIS11
          ASSIGN    (TOTBWIES/PT11TIMW),TOTIES11             * IES11
.
          MOVE      PT11THOM,WIS11WGT                        * for display
          MOVE      "High Outl Wgt",WIS11DSC
BW11H999  RETURN
+
.******************************************************************************
.*                                  C11AB000              Called by: WIS11000 *
.*                          Check for Aboriginality                           *
.******************************************************************************
C11AB000  MOVE      ZERO,ATSIWIES                * set ATSI WIES to zero
          IF        PNSWRACE<1 | PNSWRACE>14
            GOTO      C11AB999              * Invalid parameter
          ENDIF
.
          LOAD      TCODE,PNSWRACE,CATP1,CATP2,CATP3,CATP4,CATP5:
                                   CATP6,CATA1,CATA2,CATA3,CATA4:
                                   CATA5,CATA6,CATA7,CATVA
          LOAD      ACODE,PNSWRACE,PUSR1,PUSR2,PUSR3,PUSR4,PUSR5:
                                   PUSR6,AUSR1,AUSR2,AUSR3,AUSR4:
                                   AUSR5,AUSR6,AUSR7,PMPXABRG
.
          MATCH     ACODE,SP3
          GOTO      C11AB999 IF EQUAL
.
          PACK      KEY5,TCODE,ACODE
          CALL      RDCODE1                 * Read a code file record
          BRANCH    OVRCD,C11AB999
.
          MOVE      THCSCOD,KEY1
          TYPE      KEY1
          GOTO      C11AB999 IF NOT EQUAL   * Non numeric, error
.
          MOVE      KEY1,FORM1
.
. ---- Calculate ATSI Loading if aboriginality = 5, 6, or 7 ----
.
          IF        FORM1=5 | FORM1=6 | FORM1=7
            MOVE      TOTBWIES,FORM12P4       * base WIES
            ADD       MVCOPAY,FORM12P4        * mechv copayment
            ADD       THCOPAY,FORM12P4        * thal  copayment
            ADD       AAACOPAY,FORM12P4       * AAA   copayment
            ADD       ASDCOPAY,FORM12P4       * ASD   copayment
            ADD       COLCOPAY,FORM12P4       * colon copayment
            ASSIGN    (0.1*FORM12P4),ATSIWIES
          ENDIF
.
C11AB999  RETURN
+
