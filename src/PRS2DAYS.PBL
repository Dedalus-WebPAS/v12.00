+
.******************************************************************************
.*                                                                            *
.* Include Name     : PRS2DAYS.PBL                                            *
.*                                                                            *
.* Description      : Calculate the days in hospital for a patient for a      *
.*                    given date range                                        *
.*                                                                            *
.* Author           : Junior  12/07/1996                                      *
.*                                                                            *
.* Files Required   : PATTRNFD                                                *
.*                                                                            *
.* Parameters Req   : AADMNO   - Admission number                             *
.*                    FROMDATE - From date (ccyymmdd)                         *
.*                    TODATE   - To date   (ccyymmdd)                         *
.*                                                                            *
.* Subroutines Used : CALCDAYS - Calculate difference between 2 dates         *
.*                                                                            *
.* Modifications    :                                                         *
.*        V12.00.01 20/05/2025  Ebon Clements  TSK 0955096                    *
.*                  Alphanueric visit number changes                          *
.*        V11.03.02 27.09.2023  DA Sarkies     Task 0931017                   *
.*                  Added mod to subtract 1 if contract leave in adm date     *
.*        V11.03.01 11/11/2022  Ebon Clements  Task 0926750                   *
.*                  Removed PRS2LWIP                                          *
.*        V11.02.01 27/09/2022  Ebon Clements TSK 1913482                     *
.*                  Added PRS2LWIP to calculate leave with permission days.   *
.*        V9.10.01  15/08/2008  Jill Habasque CAR 175371                      *
.*                  Mods to include contract leave days as patient days       *
.*                  Mods to not subtract 1 if contract leave is on adm date   *
.*        V9.04.01  16/09/2004  Guomin Fu CAR 53489                           *
.*                  Fixed in PRS2LWOP to set up "to date" to get correct leave*
.*                  days.                                                     *
.*        V9.02.01  04/09/2002  Jill Habasque  SRF 17573                      *
.*                  Added RTIO days                                           *
.*                  08/10/1998  Glenn Berry      5.07 changes                 *
.******************************************************************************
.*        V5.01     20/12/1993  Graeme Williams                               *
.*                  Corrected key being used on PATTRAN2                      *
.*        V6.01.01  04/03/1994  Graeme Williams  SRF 128240                   *
.*                  Altered CALCU000 to allow for invalid date ranges         *
.*        V6.02.01  30/11/1994  Graeme Williams  SRF 133701                   *
.*                  Ignore returns from leave on admn date                    *
.*        V5.03.01  15/06/1996  Greg Horvat      PRS2 96 Changes              *
.*                  Changed to use CALCDAYS to calculate difference between 2 *
.*                  dates                                                     *
.*        V5.03.02  09/10/1996  Greg Horvat      SRF 617366                   *
.*                  Problem with getting leave days when going over different *
.*                  periods                                                   *
.*                                                                            *
.******************************************************************************
PRS2DAYS  
PRS2D000  MOVE      ZERO,NBRDAYS
          MOVE      SP8,STDATE
          MOVE      SP1,STMOVE
.
. ----- Check if the from & to dates are within range -----
.
          MATCH     TODATE,FROMDATE
          GOTO      PRS2D900 IF NOT LESS    * From date >= to date, exit
.
          MATCH     EPIADATE,CHDATE
          GOTO      PRS2D900 IF LESS        * Admission date > to date, exit
.
. ----- Loop thru transfer file to get days in hospital -----
.
          PACK      KEY30,AADMNO,SP30
          CALL      RDSTRAN2                * Position on & read the transfer
PRS2D050  CALL      RDKTRAN2                  file
          BRANCH    OVRCD,PRS2D550
.
          MATCH     AADMNO,TADMN
          GOTO      PRS2D550 IF NOT EQUAL   * Different admission number
.
          MOVE      TMOVE,STMOVE
          REP       " 0",TDATE
.
          MATCH     "A",TMOVE
          GOTO      PRS2D100 IF EQUAL       * Admission record
.
          MATCH     "C",TMOVE
          GOTO      PRS2D200 IF EQUAL       * Change record
.
          MATCH     "L",TMOVE
          GOTO      PRS2D250 IF EQUAL       * Leave record
.
          MATCH     "R",TMOVE
          GOTO      PRS2D350 IF EQUAL       * Return record
.
          MATCH     "D",TMOVE
          GOTO      PRS2D500 IF EQUAL       * Discharge record
          GOTO      PRS2D050            
.
. ----- Admission record -----
.
PRS2D100  MATCH     TDATE,ADATE
          IF        !@EQUAL
            DISPLAY   *P1:22,*EL,*B,"Admission : ",*V2LON,AADMNO,*HOFF:
                      " Transfer and admission dates are different.",*W2;
          ENDIF
.
          MATCH     TDATE,TODATE
          GOTO      PRS2D900 IF LESS        * To date < transfer date
.
          MATCH     FROMDATE,TDATE
          GOTO      PRS2D150 IF NOT LESS    * Transfer date >= from date
.
          MOVE      FROMDATE,STDATE         * Save the from date
          GOTO      PRS2D050 
.
PRS2D150  MATCH     TDATE,TODATE
          GOTO      PRS2D900 IF LESS        * To date < transfer date
.
          MOVE      TDATE,STDATE            * Save the to date
          GOTO      PRS2D050
.
. ----- Change record -----
.
PRS2D200  MATCH     TDATE,TODATE
          GOTO      PRS2D550 IF LESS        * To date < transfer date
          GOTO      PRS2D050
.
. ----- Leave record -----
.
PRS2D250  MATCH     SP3,PTTRLTYP 
          GOTO      PRS2D260 IF EQUAL       * Not contract leave
.
          PACK      KEY5,ANST,ANSL,PTTRLTYP
          CALL      RDCODE1                 * Read the codes file
          BRANCH    OVRCD,PRS2D260
.
          MATCH     ANSC,THCSCOD
          GOTO      PRS2D260 IF NOT EQUAL   * Not contract leave
.
          CALL      RDKTRAN2                * skip return record
.
          GOTO      PRS2D050
.
PRS2D260  MATCH     FROMDATE,TDATE
          GOTO      PRS2D050 IF LESS        * Transfer date < from date
.
          PACK      CDYSFDTE,STDATE     * Set up the from date
          MATCH     TODATE,TDATE
          GOTO      PRS2D300 IF LESS        * Transfer date < to date
.
          PACK      CDYSTDTE,TODATE     * Set up the to date
          CALL      CALCU000                * Calculate diff between 2 dates
          GOTO      PRS2D900
.
PRS2D300  PACK      CDYSTDTE,TDATE      * Set up the to date
          CALL      CALCU000                * Calculate diff between 2 dates
          MOVE      TDATE,STDATE            * Save the transfer date
          GOTO      PRS2D050
.
. ----- Return record -----
.
PRS2D350  MATCH     FROMDATE,TDATE
          GOTO      PRS2D400 IF NOT LESS    * Transfer date >= from date
.
          MATCH     TDATE,TODATE
          GOTO      PRS2D900 IF LESS        * To date < transfer date
.
          MOVE      FROMDATE,STDATE         * Save the from date
          GOTO      PRS2D050 
.
PRS2D400  MATCH     STDATE,EPIADATE
          GOTO      PRS2D450 IF LESS        * Admission date < saved to date
.
          MATCH     TDATE,EPIADATE
          GOTO      PRS2D450 IF EQUAL       * Admission date = transfer date
.
          ADD       "1",NBRDAYS             * On leave over admission date
.
PRS2D450  MATCH     TDATE,TODATE
          GOTO      PRS2D900 IF LESS        * To date < transfer date
.
          MOVE      TDATE,STDATE            * Update Calc. from date
          GOTO      PRS2D050
.
. ----- Discharge record -----
.
PRS2D500  MATCH     FROMDATE,TDATE
          GOTO      PRS2D900 IF LESS        * Transfer date < from date
.
          MATCH     TDATE,TODATE
          GOTO      PRS2D550 IF LESS        * To date < transfer date
.
          MATCH     EPIADATE,TDATE
          GOTO      PRS2D550 IF NOT EQUAL   * Admission date <> transfer date
.
          MOVE      "1",NBRDAYS             * Same day patient
          GOTO      PRS2D900
.
. ----- Record out of date range, work out days in hospital -----
.
PRS2D550  MOVE      TODATE,TDATE
          MATCH     "L",STMOVE
          GOTO      PRS2D900 IF EQUAL       * Leave movement, exit
.
          MATCH     SP1,STMOVE
          GOTO      PRS2D900 IF EQUAL       * Blank movement, exit
.
          PACK      CDYSFDTE,STDATE     * Set up the from date
          PACK      CDYSTDTE,TDATE      * Set up the to date
          CALL      CALCU000                * Calculate diff between 2 dates
.
PRS2D900  MOVE      ZERO,EXIT
PRS2D999  CALL      RTDY0000
          ADD       RTDAYS,NBRDAYS
          RETURN
+
.******************************************************************************
.*                                                                            *
.* Include Name     : PRS2CLDY.PBL                                            *
.*                                                                            *
.* Description      : Calculate the contract leaves days for a patient for a  *
.*                    given date range                                        *
.*                                                                            *
.* Author           : Junior  12/07/1996                                      *
.*                                                                            *
.* Files Required   : PATASFFD  PATCODFD  PATTRNFD                            *
.*                                                                            *
.* Parameters Req   : AADMNO   - Admission number                             *
.*                    FROMDATE - From date (yymmdd)                           *
.*                    TODATE   - To date   (yymmdd)                           *
.*                                                                            *
.* Subroutines Used : CALCDAYS - Calculate difference between 2 dates         *
.*                                                                            *
.* Modifications    :                                                         *
.*        V5.03.01  25/09/1996  Greg Horvat                                   *
.*                  - Changed to not count a patient going on contract leave  *
.*                    on the admission date as a contract leave day           *
.*                  - Changed to check if the contract leave is within the    *
.*                    required date range                                     *
.*                                                                            *
.******************************************************************************
PRS2CLDY
PRS2CL00  MOVE      ZERO,NBRDAYS
          UNPACK    SP30,CDYSFDTE,CDYSTDTE  * Clear the from date & to date
.
.
. ----- Check if the from & to dates are within range -----
.
          MATCH     FROMDATE,TODATE
          GOTO      PRS2CL90 IF LESS        * To date < from date, exit
.
          MATCH     EPIADATE,CHDATE
          GOTO      PRS2CL90 IF LESS        * Admission date > to date, exit
.
. ----- Loop thru admission SFA file to get contract leave days -----
.
          PACK      KEY25,AADMNO,ANSL,SP30
          CALL      RSPTASF1                * Position on & read the admission
PRS2CL10  CALL      RKPTASF1                  SFA file
          BRANCH    OVRCD,PRS2CL20
.
          MATCH     AADMNO,PTAFADMN
          GOTO      PRS2CL20 IF NOT EQUAL   * Different admission number
.
          MATCH     PTAFDATE,TODATE
          GOTO      PRS2CL20 IF LESS        * To date < SFA date
.
. ----- Check for the leave record on the transfer file -----
.
          PACK      KEY30,PTAFADMN,PTAFDATE,PTAFTIME,SP30
          CALL      RDSTRAN2                * Position on & read the transfer
          CALL      RDKTRAN2                  file
          BRANCH    OVRCD,PRS2CL10
.
          MATCH     PTAFADMN,TADMN
          GOTO      PRS2CL10 IF NOT EQUAL   * Different admission number
.
          MATCH     PTAFDATE,TDATE
          GOTO      PRS2CL10 IF NOT EQUAL   * Different admission date
.
          MATCH     PTAFTIME,TTIME
          GOTO      PRS2CL10 IF NOT EQUAL   * Different admission time
.
          MATCH     "L",TMOVE
          GOTO      PRS2CL10 IF NOT EQUAL   * Leave record
.
. ----- Leave record found -----
.
          MATCH     SP3,PTTRLTYP
          GOTO      PRS2CL10 IF EQUAL       * Not contract leave
.
          PACK      KEY5,ANST,ANSL,PTTRLTYP
          CALL      RDCODE1                 * Read the codes file
          BRANCH    OVRCD,PRS2CL10
.
          MATCH     ANSC,THCSCOD
          GOTO      PRS2CL10 IF NOT EQUAL   * Not contract leave
.
          MATCH     FROMDATE,TDATE
          IF        @LESS
            PACK      CDYSFDTE,FROMDATE * Set up from date
          ELSE
            PACK      CDYSFDTE,TDATE    * Set up from date
          ENDIF
.
          MATCH     EPIADATE,TDATE
          IF        @EQUAL
......      SUB       ONE,NBRDAYS           * Leave on admin day, not leave day
          ENDIF
.
. ----- Get the return record from the transfer file -----
.
          CALL      RDKTRAN2                * Read the next transfer record
          BRANCH    OVRCD,PRS2CL20
.
          MATCH     PTAFADMN,TADMN
          GOTO      PRS2CL20 IF NOT EQUAL   * Different admission number
.
          MATCH     "R",TMOVE
          GOTO      PRS2CL20 IF NOT EQUAL   * Return record
.
. ----- Return record found -----
.
          MATCH     TDATE,FROMDATE
          IF        @LESS
            PACK      CDYSTDTE,TDATE    * Set up to date
            CALL      CALCU000              * Calculate diff between 2 dates
          ENDIF
.
          UNPACK    SP30,CDYSFDTE,CDYSTDTE  * Clear the from date & to date
          GOTO      PRS2CL10
.
. ----- Record out of date range, work out contract leave days -----
.
PRS2CL20  MATCH     SP8,CDYSFDTE
          GOTO      PRS2CL90 IF EQUAL       * Not a leave record, exit
.
          PACK      CDYSTDTE,TODATE     * Set up to date
          CALL      CALCU000                * Calculate diff between 2 dates
.
PRS2CL90  MOVE      ZERO,EXIT
PRS2CL99  RETURN
+
.******************************************** Start of addition        *C-49016
.******************************************************************************
.*                                                                            *
.* Include Name     : PRS2LWOP.PBL  (cloned from PRS2CLDY)                    *
.*                                                                            *
.* Description      : Calculate the leave days "without permission" for a     *
.*                    given date range (Leave with HDP equiv. of "W")         *
.*                                                                            *
.* Author           : MikeL   31/05/2004                                      *
.*                                                                            *
.* Files Required   : PATCODFD  PATTRNFD                                      *
.*                                                                            *
.* Parameters Req   : AADMNO   - Admission number                             *
.*                    FROMDATE - From date (yymmdd)                           *
.*                    TODATE   - To date   (yymmdd)                           *
.*                                                                            *
.* Subroutines Used : CALCDAYS - Calculate difference between 2 dates         *
.*                                                                            *
.* Modifications    :                                                         *
.*                                                                            *
.*        V9.03.01  07/06/2004  Peter Vela  CAR 49016                         *
.*                  - 2004 Statutory Changes Ported subroutine from 5.12      *
.*                                                                            *
.******************************************************************************
PRS2LWOP
PRS2LW00  MOVE      ZERO,NBRDAYS
          UNPACK    SP30,CDYSFDTE,CDYSTDTE  * Clear the from date & to date
.
. ----- Check if the from & to dates are within range -----
.
          MATCH     FROMDATE,TODATE
          GOTO      PRS2LW90 IF LESS        * To date < from date, exit
.
          MATCH     EPIADATE,CHDATE
          GOTO      PRS2LW90 IF LESS        * Admission date > to date, exit
.
. ----- Loop thru the leave records looking for reqd Leave records -----
.
          PACK      KEY30,AADMNO,SP30
          CALL      RDSTRAN2                * Position on & read the transfer
PRS2LW10  CALL      RDKTRAN2                  file
          BRANCH    OVRCD,PRS2LW20          * C-53393(was PRS2LW10)
.
          MATCH     AADMNO,TADMN
          GOTO      PRS2LW20 IF NOT EQUAL   * Different admission number
.
          MATCH     TDATE,TODATE
          GOTO      PRS2LW20 IF LESS        * end of Tran records
.
. ----- Only looking for "L" records -----
.
          MATCH     "L",TMOVE
          GOTO      PRS2LW10 IF NOT EQUAL   * some other record
.
. ----- Leave record found -----
.
          PACK      KEY5,ANST,ANSL,PTTRLTYP
          CALL      RDCODE1                 * Read the codes file
          BRANCH    OVRCD,PRS2LW10
.
          MATCH     ANSW,THCSCOD
          GOTO      PRS2LW10 IF NOT EQUAL   * Not "leave wihout permission"
.
          MATCH     FROMDATE,TDATE
          IF        @LESS
            PACK      CDYSFDTE,FROMDATE     * Set up from date
          ELSE
            PACK      CDYSFDTE,TDATE        * Set up from date
          ENDIF
.
          MATCH     EPIADATE,TDATE
          IF        @EQUAL
            SUB       ONE,NBRDAYS           * Leave on admin day, not leave day
          ENDIF
.
. ----- Get the return record from the transfer file -----
.
          CALL      RDKTRAN2                * Read the next transfer record
          BRANCH    OVRCD,PRS2LW20
.
          MATCH     AADMNO,TADMN
          GOTO      PRS2LW20 IF NOT EQUAL   * Different admission number
.
          MATCH     "R",TMOVE
          GOTO      PRS2LW20 IF NOT EQUAL   * Return record
.
. ----- Return record found -----
.
          MATCH     TDATE,TODATE            * C-53489 setup todate
          IF        @LESS
            PACK      CDYSTDTE,TODATE        * Set up to date
          ELSE
            PACK      CDYSTDTE,TDATE        * I-53489 
          ENDIF
          CALL      CALCU000              * Calculate diff between 2 dates
.
          UNPACK    SP30,CDYSFDTE,CDYSTDTE  * Clear the from date & to date
          GOTO      PRS2LW10
.
. ----- Record out of date range, work out contract leave days -----
.
PRS2LW20  MATCH     SP8,CDYSFDTE
          GOTO      PRS2LW90 IF EQUAL       * Not a leave record, exit
.
          PACK      CDYSTDTE,TODATE         * Set up to date
          CALL      CALCU000                * Calculate diff between 2 dates
.
PRS2LW90  MOVE      ZERO,EXIT
PRS2LW99  RETURN
.********************************************   End of addition        *C-49016
+
.******************************************************************************
.*                                  CALCU000                                  *
.*                     Calculate Difference Between 2 Dates                   *
.******************************************************************************
CALCU000  REP       " 0",CDYSFDTE
          REP       " 0",CDYSTDTE
          MATCH     CDYSFDTE,CDYSTDTE
          GOTO      CALCU100 IF LESS        * To date < from date, exit
.
          CALL      CALCDAYS                * Calculate diff between 2 dates
          SUB       ONE,CDYSDAYS            * We dont want inclusive dates
          ADD       CDYSDAYS,NBRDAYS        * Accumulate contract leave days
          GOTO      CALCU900
.
. ----- Error with the dates, set to zero -----
.
CALCU100  MOVE      ZERO,NBRDAYS
.
CALCU900  MOVE      ZERO,EXIT
CALCU999  RETURN
+
.
.******************************************************************************
.*                                                                            *
.* Include Name     : PRS2LWOP.PBL  (cloned from PRS2CLDY)                    *
.*                                                                            *
.* Description      : Calculate the leave days "without permission" for a     *
.*                    given date range (Leave with HDP equiv. of "W")         *
.*                                                                            *
.* Author           : MikeL   31/05/2004                                      *
.*                                                                            *
.* Files Required   : PATCODFD  PATTRNFD                                      *
.*                                                                            *
.* Parameters Req   : AADMNO   - Admission number                             *
.*                    FROMDATE - From date (yymmdd)                           *
.*                    TODATE   - To date   (yymmdd)                           *
.*                                                                            *
.* Subroutines Used : CALCDAYS - Calculate difference between 2 dates         *
.*                                                                            *
.* Modifications    :                                                         *
.*                                                                            *
.*        V9.03.01  07/06/2004  Peter Vela  CAR 49016                         *
.*                  - 2004 Statutory Changes Ported subroutine from 5.12      *
.*                                                                            *
.******************************************************************************
PRS2LDAY
PRS2LD00  MOVE      ZERO,NBRDAYS
          UNPACK    SP30,CDYSFDTE,CDYSTDTE  * Clear the from date & to date
.
. ----- Check if the from & to dates are within range -----
.
          MATCH     FROMDATE,TODATE
          GOTO      PRS2LD90 IF LESS        * To date < from date, exit
.
          MATCH     EPIADATE,CHDATE
          GOTO      PRS2LD90 IF LESS        * Admission date > to date, exit
.
. ----- Loop thru the leave records looking for reqd Leave records -----
.
          PACK      KEY30,AADMNO,SP30
          CALL      RDSTRAN2                * Position on & read the transfer
PRS2LD10  CALL      RDKTRAN2                  file
          BRANCH    OVRCD,PRS2LD20          * C-53393(was PRS2LD10)
.
          MATCH     AADMNO,TADMN
          GOTO      PRS2LD20 IF NOT EQUAL   * Different admission number
.
          MATCH     TDATE,TODATE
          GOTO      PRS2LD20 IF LESS        * end of Tran records
.
. ----- Only looking for "L" records -----
.
          MATCH     "L",TMOVE
          GOTO      PRS2LD10 IF NOT EQUAL   * some other record
.
. ----- Leave record found -----
.
          MATCH     FROMDATE,TDATE
          IF        @LESS
            PACK      CDYSFDTE,FROMDATE     * Set up from date
          ELSE
            PACK      CDYSFDTE,TDATE        * Set up from date
          ENDIF
.
.....     MATCH     EPIADATE,TDATE
.....     IF        @EQUAL
.....       SUB       ONE,NBRDAYS           * Leave on admin day, not leave day
.....     ENDIF
.
. ----- Get the return record from the transfer file -----
.
          CALL      RDKTRAN2                * Read the next transfer record
          BRANCH    OVRCD,PRS2LD20
.
          MATCH     AADMNO,TADMN
          GOTO      PRS2LD20 IF NOT EQUAL   * Different admission number
.
          MATCH     "R",TMOVE
          GOTO      PRS2LD20 IF NOT EQUAL   * Return record
.
. ----- Return record found -----
.
          MATCH     TDATE,TODATE            * C-53489 setup todate
          IF        @LESS
            PACK      CDYSTDTE,TODATE        * Set up to date
          ELSE
            PACK      CDYSTDTE,TDATE        * I-53489
          ENDIF
          CALL      CALCU000              * Calculate diff between 2 dates
.
          UNPACK    SP30,CDYSFDTE,CDYSTDTE  * Clear the from date & to date
          GOTO      PRS2LD10
.
. ----- Record out of date range, work out contract leave days -----
.
PRS2LD20  MATCH     SP8,CDYSFDTE
          GOTO      PRS2LD90 IF EQUAL       * Not a leave record, exit
.
          PACK      CDYSTDTE,TODATE         * Set up to date
          CALL      CALCU000                * Calculate diff between 2 dates
.
PRS2LD90  MOVE      ZERO,EXIT
PRS2LD99  RETURN
+
