.        ISBCATBI : Independent Service Billing
.
.        IF PROGFLAG = 1  THEN WORK OUT THE AMOUNT TO BE INVOICED FOR ALL PAYERS
.
.        IF PROGFLAG = 3  THEN PRINT NEXT INVOICE
.
.        IF PROGFLAG = 7  THEN WRITE TO INVOCIE BREAKDOWN
.
.--------------------------------------------------------------------------
.
.        VERSION  :
.
.        This include produces the body of the next invoice for the current
.        patient. PROGFLAG is used to determine if the invoice is to be
.        displayed, printed, or just have the total calculated.
.
.        It refers to a number of subroutines that are not included in this
.        include. The main includes where these may be found (other than
.        the I/O includes) are :
.
.               PATCALFN : Include to update the Fees Invoiced Statistics
.
.     Modifications:
.
.         V12.00.01 30/05/2025  Don Nguyen       TSK 0955096
.                   Alphanumeric visit number changes
.*******************************************************************************
.         calculating a payer Accommodation charges
.*******************************************************************************
ISAC0000  
          CALL     INVR0000                  * Initialise variables
.
          MOVE     SP70,PMVXMHOS
          OPEN     PMSVX1A1,"pmsvx1af"
          PACK     KEY8,AADMNO
          CALL     RDPMVX11
.
          PACK     KEY9,ACLAIM,AFUNDH,SP70
          CALL     GETCNEFF               * Get Contract Effective From
.
.         Get the starting accommodation date
.
          UNPACK    ALACDTE INTO XCENT,XYEAR,XMON,XDAY
          MOVE      XDAY,FDAY
          MOVE      XMON,FMON
          MOVE      XYEAR,FYEAR
          MOVE      XCENT,FCENT
.
          PACK     IDATEF WITH FCENT,FYEAR,FMON,FDAY
          REP      " 0",IDATEF
          PACK     SIDATET,SP20
          PACK     IDATET,SP20
          MOVE     SP70,CMENDDTE
.
          MOVE     THREE,PINVSYS
          MOVE     ONE,ISBLFLAG
          MOVE     ZERO,TOTACCM             * billing amount
          MOVE     ZERO,ACCMTOT             * total invoice amnt for all payers
          MOVE     ZERO,FFEEAMNT
          MOVE     ZERO,FINFLAG
.
. ----- Get GST stuff -----
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,NINTY8;*130,PTCNAGST
          CALL      AGST0000                * Get GST Accommodation if applicabl
          MOVE      IBGEAMNT,GSTPCEN        * Save GST accommodation rate
.
. ----- Check if chargeable theatre fees
.
          MOVE      ZERO,CMBSFEE
          READ      CONTROLF,HUND05;*177,PTCNTFEE       * Using theatre fees
          PACK      KEY5,CODECL,ACLAIM
          CALL      RDCODE1
          IF        OVRCD=0
            CMATCH    "1",TCINDC1
            IF        @EQUAL
              CMATCH    "1",TCINDC2
              IF        @EQUAL
                MOVE      ONE,CMBSFEE
              ENDIF
            ENDIF
          ENDIF
.
          MOVE     ONE,WEBFLAG
          MOVE     ZERO,CMXFLAG 
          MOVE     ZERO,FRSTACCM
          MOVE     ZERO,TOTXTRA
          MOVE     ZERO,SAMTG
.
          MOVE     ZERO,OVERPFL
          MOVE     SP70,PREVTMOV
          MOVE     SP70,PREVTDAT
.
          UNPACK   IDATEF INTO FCENT1,FYEAR1,FMON1,FDAY1
          PACK     KEY30 WITH AADMNO,SP70
          CALL     RDSTRAN2
ISAC5000  CALL     RDKTRAN2
          BRANCH   OVRCD OF ISAC8400
.
          MATCH    TADMN,AADMNO
          GOTO     ISAC8400 IF NOT EQUAL
.
          MOVE     TRBEND,SAVTBEND
          MOVE     TMOVE,PREVTMOV
          MOVE     TDATE,PREVTDAT
          MATCH    SP70,PMSGCLSS
          IF       !@EQUAL
            MOVE      PMSGCLSS,TATYPE         * Use ICD10 Suggested Class.
          ENDIF
.
          PACK     SKEY30 WITH TADMN,TDATE,TTIME,TWARD,TBED      * moved up
          MATCH    ANSA,TMOVE
          GOTO     ISAC5200 IF EQUAL
.
          MATCH     "L",TMOVE
          IF        @EQUAL
            MOVE      TDATE,SAVTDTE
            MOVE      TWARD,SAVTWARD
            MOVE      TBED,SAVTBED
            MOVE      TRBTYP,SAVBDTYP
.
            PACK      SVKEY30,TADMN,TDATE,TTIME,TWARD,TBED
ISAC5100    CALL      RDKTRAN2
            BRANCH    OVRCD,ISAC5110
.
            MATCH     DTADMN,DAADMNO
            GOTO      ISAC5110 IF NOT EQUAL
.
            MATCH     "R",TMOVE
            GOTO      ISAC5100 IF NOT EQUAL
.
            MATCH     TDATE,SAVTDTE
            GOTO      ISAC5110 IF NOT EQUAL
.
            MATCH     TWARD,SAVTWARD
            GOTO      ISAC5110 IF NOT EQUAL
.
            MATCH     TBED,SAVTBED
            GOTO      ISAC5110 IF NOT EQUAL
.
            MATCH     TRBTYP,SAVBDTYP
            GOTO      ISAC5110 IF NOT EQUAL
.
            PACK      KEY30,SVKEY30,SP70
            CALL      RDTRAN2
.
            GOTO      ISAC5000
.
ISAC5110    PACK      KEY30,SVKEY30,SP70
            CALL      RDTRAN2
.
          ENDIF
.
          MATCH     "R",TMOVE
          IF        @EQUAL
            MOVE      TDATE,SAVTDTE
            MOVE      TWARD,SAVTWARD
            MOVE      TBED,SAVTBED
            MOVE      TRBTYP,SAVBDTYP
.
            PACK      SVKEY30,TADMN,TDATE,TTIME,TWARD,TBED
ISAC5150    CALL      RDPTRAN2
            BRANCH    OVRCD,ISAC5160
.
            MATCH     DTADMN,DAADMNO
            GOTO      ISAC5160 IF NOT EQUAL
.
            MATCH     "L",TMOVE
            GOTO      ISAC5150 IF NOT EQUAL
.
            MATCH     TDATE,SAVTDTE
            GOTO      ISAC5160 IF NOT EQUAL
.
            MATCH     TWARD,SAVTWARD
            GOTO      ISAC5160 IF NOT EQUAL
.
            MATCH     TBED,SAVTBED
            GOTO      ISAC5160 IF NOT EQUAL
.
            MATCH     TRBTYP,SAVBDTYP
            GOTO      ISAC5160 IF NOT EQUAL
.
            PACK      KEY30,SVKEY30,SP70
            CALL      RDTRAN2
.
            GOTO      ISAC5000
.
ISAC5160    PACK      KEY30,SVKEY30,SP70
            CALL      RDTRAN2
.
          ENDIF
.
.         Ignore any Doctor Change record in Transfer file
.         as Changing doctor should not change rates
.
          MATCH     ANSC,TMOVE
          GOTO      ISAC5200 IF NOT EQUAL
.
          MATCH     TADOCT,STADOCT
          GOTO      ISAC5200 IF EQUAL
.
          MATCH     TATYPE,STATYPE
          GOTO      ISAC5200 IF NOT EQUAL * Changed of adm.type
          MATCH     TRBTYP,STRBTYP
          GOTO      ISAC5200 IF NOT EQUAL * Changed of bed type
.
          MOVE      TADOCT,STADOCT
          GOTO      ISAC5000              * Ignore doctor change record
.
.         Check Contract ID end date
.
...............................................................................
ISAC5200  
.
          MOVE      TDATE,IDATET
          CMATCH    "A",TMOVE
          IF        @EQUAL
            CALL      INITVAR                 * Initialise
            MOVE      ACLAIM,PTCNCLEV         * include leave days 
            MOVE      CONTRCID,SAVCONTR
            MOVE      TDATE,IDATET
            GOTO      ISAC8000
          ENDIF
.
          MATCH     SP70,CMENDDTE
          GOTO      ISAC5920 IF EQUAL
.
          CMATCH    "R",TMOVE
          GOTO      ISAC5920 IF EQUAL          * Return from leave
.
          MATCH     IDATET,CMENDDTE
          GOTO      ISAC5920 IF EQUAL
          MATCH     IDATEF,CMENDDTE
          GOTO      ISAC5910 IF LESS       * Contract Ends b/f Invoice startdate
.
.         Check if the transfer date before the end of contract
.
          MATCH     TDATE,CMENDDTE
          GOTO      ISAC5920 IF NOT LESS
.
          CALL      RDPTRAN2      * read previous record
          BRANCH    OVRCD,ISAC5910
          MATCH     TADMN,AADMNO
          GOTO      ISAC5910 IF NOT EQUAL
.
          MOVE      ANSC,TMOVE
          MATCH     BACKDATE,IDATEF
          GOTO      ISAC5920 IF NOT LESS       * Inv.from date>Invoice enddate
          MATCH     CMENDDTE,BACKDATE
          IF        @LESS
            MATCH     IDATEF,IDATET
            CALL      CAPR0000 IF NOT LESS    * write an Accom. record
            GOTO      ISAC9000
          ENDIF
.
          MOVE      CMENDDTE,TDATE
          MOVE      CMENDDTE,IDATET
          MATCH     IDATEF,IDATET
          CALL      CAPR0000 IF NOT LESS    * write an Accom. record
.
.         Get bed fees from the start of New Contract
.
          MOVE      CMENDDTE,DATFIELD
          CALL      SCTR0000
          IF        NOFEE=1
            CALL      RDKTRAN2          * read next record
            MOVE      PREVTDAT,TDATE
            MOVE      PREVTDAT,SAVDATE
            CALL      SAVARB
          ENDIF
          GOTO      ISAC5920
.
.         If this is an interim invoice, check if a new Contract applied
.
ISAC5910  COMPARE   ZERO,FRSTACCM
          GOTO      ISAC5920 IF NOT EQUAL      * Not the first Acc.line
          COMPARE   ZERO,AINVPRT
          GOTO      ISAC5920 IF EQUAL          * Not an interim invoice
.
          MOVE      ADYHOSP,TEMPDAY
          MOVE      ZERO,NBRDAYS
          DAYSEP    FRDATE,IDATEF,NBRDAYS
          ADD       NBRDAYS,TEMPDAY
          ADD       ONE,TEMPDAY
          MOVE      IDATEF,CEFFDATE
.
          CALL      GETBFE
          IF        NOFEE=1
            MOVE      ZERO,TRATEH
            MOVE      ZERO,TRATEF
            MOVE      ZERO,PTTRADPT
            MOVE      ZERO,PTTRADRB
            MOVE      SP70,CONTRCID
            MOVE      SP70,CMENDDTE
          ENDIF
          MOVE      CONTRCID,SAVCONTR
          MOVE      TRATEH,STRATEH
          MOVE      TRATEF,LSTRATE
          ADD       TRATEH,LSTRATE
.
          MOVE      LSTRATE,ACCRATE        * save actual accommodation rate
          IF        PMPBMAXD<LSTRATE
            MOVE      PMPBMAXD,LSTRATE   * Use the maximum daily
          ENDIF
.
ISAC5920  
          MOVE      TDATE,DATFIELD
          MATCH     TDATE,PREVTDAT
          IF        !@EQUAL
            ADD       ONE,DATFIELD
          ENDIF
          MOVE      DATFIELD,CEFFDATE
.
          CMATCH    "R",TMOVE
          IF        @EQUAL
            MOVE      ZERO,NBRDAYS
            DAYSEP    FRDATE,TDATE,NBRDAYS
            MOVE      NBRDAYS,TEMPDAY
            CALL      RETTYP                  * Return from leave
            MOVE      CONTRCID,SAVCONTR
            MOVE      TDATE,IDATET
            GOTO     ISAC6000
          ENDIF
.
          CMATCH    "D",TMOVE
          IF        @EQUAL
            MOVE      TDATE,SAVDATE
            MOVE      TTIME,SAVTIME
          ENDIF
.
          MATCH     TATYPE,SAVATYP
          GOTO      ISAC5930 IF EQUAL
.
          BRANCH    PTCNCNDY,ISAC5930        * don't reset day count
.
.        Change of admission type, reset count
.
          MOVE      TDATE,FRDATE
.
ISAC5930  CALL       SAVARB
          CALL       GETBFE
          IF         NOFEE=1
            MOVE      ZERO,TRATEH
            MOVE      ZERO,TRATEF
            MOVE      ZERO,PTTRADPT
            MOVE      ZERO,PTTRADRB
            MOVE      SP70,CONTRCID
            MOVE      SP70,CMENDDTE
          ENDIF
          MOVE      CURBEND,TRBEND
.
          CMATCH    "L",TMOVE
          GOTO      ISAC6000 IF NOT EQUAL
.
          MOVE      TDATE,SAVDATE
.
ISAC6000  MOVE      TDATE,IDATET
.
.         If this is not a Change record, then write a record
.
          CMATCH    "C",TMOVE
          GOTO      ISAC7400 IF NOT EQUAL
.
.         If the ward or bed has changed, then write a record
.
          MATCH     TWARD,STWARD
          GOTO      ISAC7400 IF NOT EQUAL
.
          MATCH     TBED,STBED
          GOTO      ISAC7400 IF NOT EQUAL
.
          MOVE      TRATEF,NEWRATE
          ADD       TRATEH,NEWRATE
          SUB       TALLW,NEWRATE
          SUB       TDISC,NEWRATE
          IF        PMPBMAXD<NEWRATE
            MOVE      PMPBMAXD,NEWRATE   * Use the maximum daily
          ENDIF
.
.         Compare the old and new rates
.
          COMPARE   LSTRATE,NEWRATE
          GOTO      ISAC7400 IF NOT EQUAL
.
.         If the rebate rate has changed, then write a record
.
          COMPARE   TRATEH,STRATEH
          GOTO      ISAC7400 IF NOT EQUAL
.
.         If the end day of rate is changed
.
          COMPARE   TRBEND,STRBEND
          GOTO      ISAC7400 IF NOT EQUAL
.
.         If the bed type is changed
.
          MATCH     TRBTYP,STRBTYP
          GOTO      ISAC7400 IF NOT EQUAL
.
.         If the Admission type has altered then write a record
.
          MATCH     TATYPE,STATYPE
          GOTO      ISAC5000 IF EQUAL
.
.         If this records date is greater than or equal to the starting date,
.         then generate an accommodation record.
.
ISAC7400  MOVE      IDATET,SIDATET
          MOVE      BACKDATE,DIM8
          MATCH     SP10,DDATE
          IF        !@EQUAL
            MATCH     DDATE,BACKDATE
            IF        !@LESS
              MOVE      DDATE,DIM8
            ENDIF
          ENDIF
.
          MATCH     IDATET,DIM8
          IF        @LESS
            MOVE      DIM8,IDATET
          ENDIF
.
          MATCH     IDATEF,IDATET
          CALL      CAPR0000 IF NOT LESS    * write an Accom. record
.
.         If this is the discharge record, then we have finished
.
          CMATCH    "D",TMOVE
          GOTO      ISAC7700 IF NOT EQUAL
.
          GOTO      ISAC9000
.
ISAC7700  MOVE      ZERO,CALDAYS
          MATCH     STDATE,TDATE
          IF        !@EQUAL
            DAYSEP    STDATE,TDATE,CALDAYS
          ENDIF
          ADD       CALDAYS,NBRDAYS
.
          CMATCH    "L",TMOVE
          IF        @EQUAL
            MOVE      SP3,STWARD
            MOVE      SP3,STBED
           GOTO       ISAC8300
          ENDIF
.
.         Save the rate and ward/bed information
.
ISAC8000  MOVE      TWARD,STWARD
          MOVE      TBED,STBED
.
ISAC8300  MOVE      TRATEF,STRATEF
          MOVE      TRATEG,STRATEG
          MOVE      TDISC,STDISC
          MOVE      TALLW,STALLW
          MOVE      TATYPE,STATYPE
          MOVE      TADOCT,STADOCT
          MOVE      TRBTYP,STRBTYP
          MOVE      TDATE,STDATE
          MOVE      TEXTRA,STEXTRA
          MOVE      TCHSTAT,STCHSTAT
          MOVE      PTTREPSD,SPTTREPS
          MOVE      TRBTYP,SAVBTYP
          MOVE      TATYPE,SAVATYP
.
          MOVE      TRBEND,STRBEND
          MOVE      PTTRAEND,SPTTRAEN
          MOVE      PTTRADPT,SPTTRADP
          MOVE      PTTRADRB,SPTTRADR
          MOVE      PTTRITFS,SPTTRITF
          MOVE      TMOVE,STMOVE
          MOVE      CONTRCID,SAVCONTR
.
          MOVE      TRATEH,STRATEH
          MOVE      TRATEF,LSTRATE
          ADD       TRATEH,LSTRATE
          SUB       TALLW,LSTRATE
          SUB       TDISC,LSTRATE
.
          MOVE      LSTRATE,ACCRATE
          IF        PMPBMAXD<LSTRATE
            MOVE      PMPBMAXD,LSTRATE     * Use the maximum daily
          ENDIF
          GOTO      ISAC5000
.
.******************************************************************************
.         End of this admissions records in transfer file. No Discharge record
.
ISAC8400  PACK      IDATET WITH TCENT,TYEAR,TMON,TDAY
          REP       " 0",IDATET
          MOVE      ONE,OVERPFL
.
          MOVE      SP1,TMOVE
          LOAD      TMOVE USING ASTAT OF ANSC,ANSC,ANSC,ANSR
          MOVE      STATYPE,TATYPE
          MOVE      STRBTYP,TRBTYP
          MOVE      STRBTYP,SAVBTYP
          MOVE      STRBEND,TRBEND
          MOVE      STWARD,TWARD
          MOVE      STBED,TBED
.
          MATCH     SAVDATE,IDATEF
          IF        !@LESS
            MOVE      IDATEF,DATFIELD
          ELSE
            MOVE      SAVDATE,DATFIELD
            MATCH     SAVDATE,PREVTDAT
            IF        !@EQUAL
              ADD       ONE,DATFIELD
            ENDIF
          ENDIF
.
          MATCH     ANSA,PREVTMOV
          IF        @EQUAL
            IF        AINVPRT=0
              MOVE      FRDATE,DATFIELD
            ENDIF
          ENDIF
          MOVE      DATFIELD,CEFFDATE
          MOVE      TMOVE,PREVTMOV
.
.         Check if Contract has Expired
.
          MATCH     SP70,CMENDDTE
          IF        !@EQUAL
            MATCH     CMENDDTE,CEFFDATE
            IF        !@LESS
              MOVE      IDATEF,TDATE
              MOVE      IDATEF,DATFIELD
              MOVE      TWO,STEPNO             * for GETACCM not to use STRBEND
              CALL      SCTR0000               * Get a new Contract
            ENDIF
          ENDIF
.
          MATCH     CEFFDATE,CMENDDTE
          GOTO      ISAC8600 IF EQUAL
.
          MOVE      TDATE,CEFFDATE
          CALL      GETBFE
          IF        NOFEE=1
            MOVE      ZERO,TRATEH
            MOVE      ZERO,TRATEF
            MOVE      ZERO,PTTRADPT
            MOVE      ZERO,PTTRADRB
            MOVE      SP70,CONTRCID
            MOVE      SP70,CMENDDTE
          ENDIF
          CALL      DCNT0000            * Check Contract Eff.for Disc.
.
ISAC8600  IF        NOFEE=1
            MOVE      ZERO,LSTRATE
            MOVE      SP70,SAVCONTR
            GOTO      ISAC8800
          ENDIF
.
.         Check if Contract expired prior the end invoice date
.
ISAC8700  MATCH     SP70,CMENDDTE
          GOTO      ISAC8800 IF EQUAL
.
          MATCH     IDATEF,CMENDDTE
          GOTO      ISAC8800 IF LESS
.
          MATCH     IDATET,CMENDDTE
          GOTO      ISAC8710 IF LESS          * Contract Ended b/w IDATEF IDATET
.
          GOTO      ISAC8800
.
.         Contract ended in between IDATEF and IDATET
.
ISAC8710  MATCH     IDATET,IDATEF
          GOTO      ISAC9000 IF EQUAL
.
.         Contract ended prior End of Invoice
.
ISAC8720  MOVE      IDATET,PREVTDAT
          MATCH     SP70,CMENDDTE
          GOTO      ISAC7400 IF EQUAL
.
          MOVE      CMENDDTE,TDATE
          MOVE      CMENDDTE,IDATET
.
          MATCH     IDATEF,IDATET
          CALL      CAPR0000 IF NOT LESS   * Accom.record till end of Contract
.
          MOVE      PREVTDAT,IDATET
.
.         Check if End of Contract is before Fromdate
.
          MATCH     IDATEF,CMENDDTE
          IF        @LESS
            MOVE      IDATEF,TDATE
          ENDIF
.
          MOVE      CMENDDTE,DATFIELD
          CALL      SCTR0000      * Get bed fees for next start of New Contract
          BRANCH    NOFEE,ISAC8800
.
          MATCH     SP70,CMENDDTE
          GOTO      ISAC8800 IF EQUAL
.
          MATCH     IDATET,CMENDDTE
          IF        @LESS
            MOVE      IDATEF,TDATE
            MOVE      IDATEF,SAVDATE
            MOVE      CMENDDTE,IDATET
            GOTO      ISAC7400
          ELSE
            MOVE      PREVTDAT,TDATE
            MOVE      IDATEF,SAVDATE
            GOTO      ISAC8800
          ENDIF
.
.  
.         process up to the end date
.
ISAC8800  MOVE     IDATET,SIDATET
          MOVE     IDATEF,TDATE
.
          MATCH     IDATEF,IDATET
          CALL      CAPR0000 IF NOT LESS    * write an Accom. record
.
ISAC9000  MOVE      ZERO,ISBLFLAG
.
          BRANCH    PROGFLAG,ISAC9999
          BRANCH    DEFPAYCL,ISAC9999
.
          BRANCH    FINFLAG,ISAC9999
.
.         Check with the Maximum per Billing record
.
          COMPARE   PMPBMAXB,TOTACCM
          GOTO      ISAC9999 IF LESS
          MOVE      PMPBMAXB,TOTACCM
.
ISAC9999  RETURN
+
.*******************************************************************************
.         This subroutine writes one accommodation record
.*******************************************************************************
CAPR0000  
.
          COMPARE   ONE,DCFLAG
          GOTO      CAPR0500 IF NOT EQUAL
.
          UNPACK    IDATET INTO TCENT1,TYEAR1,TMON1,TDAY1
          MOVE      ZERO,NODAYS
          IF        DEFPAYCL=1
            CALL      DPAC0000             * calculate default payer accom.chrg
            GOTO      CAPR8000
          ELSE
            CALL      MXBP0000             * calculate max billing
          ENDIF
          COMPARE   NODAYS,ZERO
          GOTO      CAPR6000 IF NOT LESS
.
          COMPARE   ONE,DISFLG
          GOTO      CAPR0100 IF NOT EQUAL
.
          CMATCH    ANSL,TMOVE
          GOTO      CAPR0100 IF EQUAL
          CMATCH    ANSR,TMOVE
          GOTO      CAPR0100 IF EQUAL
          CMATCH    ANSA,TMOVE       * not discharged, so charged on adm.
          GOTO      CAPR9990 IF NOT EQUAL
.
CAPR0100  CALL      CHKACCM0           * Check if accom.has been charged
          BRANCH    EXIT,CAPR9990      * Found accommodation record
          MOVE      ONE,NODAYS
          GOTO      CAPR5000
.
CAPR0500 IF        DEFPAYCL=1
            CALL      DPAC0000             * calculate default payer accom.chrg
            GOTO      CAPR8000
          ELSE
            CALL      MXBP0000             * calculate max billing
          ENDIF
.
.         Check zero bed days (ie. From date == To date)
.
          COMPARE   ZERO,NODAYS
          GOTO      CAPR5000 IF NOT EQUAL
.
.         Check if this is the "D" record
.
          CMATCH    "D",TMOVE
          GOTO      CAPR0700 IF EQUAL
.
          GOTO      CAPR9999
.
.         Check if this is really a day case
.
CAPR0700  MATCH     ADATE,DDATE
          GOTO      CAPR5000 IF NOT EQUAL
.
          COMPARE   ZERO,AINVPRT                 * Invoice printed yet ?
          GOTO      CAPR1000 IF EQUAL            * No.
.
          CALL      CHKACCM0           * Check if accommodation has been charged
          BRANCH    EXIT,CAPR9990       * Found accommodation record
.
CAPR1000  MOVE      ONE,NODAYS
          GOTO      CAPR5000
.
.         Get the accommodation description and calculate the rates
.
CAPR5000  CALL      GACM0000
          MOVE      ZERO,NOFEE
.
CAPR6000  COMPARE   SEVEN,PROGFLAG
          GOTO      CAPR8000 IF NOT EQUAL
.
          MOVE      "1",SRECTYPE            * Accommodation record
          CALL      WTID0000                * Write to temp.Inv.payer details
.
CAPR8000  CALL      MTDNFD00                * Move to date to next from date
.
CAPR9990  MOVE      SP70,PTDTDES2
          UNPACK    SP70,LSDESC1,LSDESC2
.
CAPR9999  RETURN
+
.*******************************************************************************
.         Write to temporary Invoice Payer details
.*******************************************************************************
WTID0000  MATCH     "1",SRECTYPE
          IF        @EQUAL
            COMPARE   ZERO,NODAYS
            GOTO      WTID9999 IF EQUAL
          ENDIF
.
          MOVE      ONE,F6
          MOVE      SP70,PMIDINVN
          PACK      KEY25,DAADMNO,PMPBCLTY,PMIDINVN,Z70
          CALL      RSPMIDE1
          CALL      RPPMIDE1
          BRANCH    OVRCD,WTID7000
.
          PACK      KEY19,PMIDADMN,PMIDCLTY,PMIDINVN,SP70
          MATCH     KEY19,KEY25
          GOTO      WTID7000 IF NOT EQUAL
          MOVE      PMIDTRNO,F6
.
WTID6000  ADD       ONE,F6
WTID7000  MOVE      SP70,PMIDINVN
          MOVE      F6,PMIDTRNO
          PACK      KEY25,DAADMNO,PMPBCLTY,PMIDINVN,PMIDTRNO,SP70
          CALL      RAPMIDE1
          COMPARE   ZERO,OVRCD
          GOTO      WTID6000 IF EQUAL
.
          CALL      CLPMSIDE                   * Clear variable
.
          MOVE      DAADMNO,PMIDADMN
          MOVE      PMPBCLTY,PMIDCLTY
          MOVE      F6,PMIDTRNO
          MOVE      SRECTYPE,PMIDRTYP
          MATCH     "1",SRECTYPE
          IF        @EQUAL
            PACK      PMIDFDAT,FCENT1,FYEAR1,FMON1,FDAY1
            REP       " 0",PMIDFDAT
            PACK      PMIDTDAT,TCENT1,TYEAR1,TMON1,TDAY1
            REP       " 0",PMIDTDAT
            PACK      PMIDDESC,TDDESC,SP70
            MOVE      SP70,PMIDITEM
            MOVE      NODAYS,PMIDNDAY
            MOVE      SAVCONTR,PMIDCNID
          ELSE
            MOVE      PMMISERV,PMIDNDAY     * Quantity
            MOVE      PMMITDAT,PMIDFDAT
            REP       " 0",PMIDFDAT
            MOVE      PMIDFDAT,ISBLDATE
            ADD       PMIDNDAY,ISBLDATE
            MOVE      ISBLDATE,PMIDTDAT
            PACK      PMIDDESC,PMMIDESC,SP70
            MOVE      PMMIITEM,PMIDITEM
            IF        PRTIFLAG=1
              MOVE      "1",PMIDSPLT        * split invoice for item record
            ENDIF
          ENDIF
.
          MOVE      LINETOT,PMIDAMNT
          MOVE      LINEGST,PMIDGSTM
          MOVE      PMPBDEFT,PMIDDEFT
          MOVE      SP70,PMIDREGM
.
          PACK      PMIDITDT,CHNGDATE,SP70
          PACK      PMIDCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMIDCDAT           * date created
          CLOCK     TIME,PMIDCTIM           * time created
          MOVE      WBSEUID,PMIDCUID         * web user id
          UNPACK    SP70,PMIDUUID,PMIDUDAT,PMIDUTIM
          PACK      PMIDSPAR,SP70,SP70
.
          CALL      WRPMIDE1
          GOTO      WTID9999
.
WTID9999  RETURN
+
.*******************************************************************************
.         Initialize the description
.*******************************************************************************
GACM0000  MOVE      SP30,TDDESC
          MOVE      ZERO,CALDAYS
          DAYSEP    ALACDTE,IDATEF,CALDAYS
          ADD       ADYHOSP,CALDAYS
          MOVE      CALDAYS,LSTDAYS
.
.         Check if this is an on-leave period
.
          MATCH     SP3,STWARD
          GOTO      GACM1000 IF NOT EQUAL
.
.         Description for on-leave
.
          MOVE      "PATIENT ON LEAVE         ",TDDESC
          MOVE      ZERO,LINETOT
          MOVE      ZERO,LINEGST
          GOTO      GACM7200
.
.         Normal Accommodation record
.
GACM1000  MOVE      ZERO,USEDEF
          MATCH     SP3,STRBTYP
          GOTO      GACM4400 IF NOT EQUAL
.
          PACK      KEY6 WITH STWARD,STBED
          CALL      RDWARD1
          BRANCH    OVRCD OF GACM6000
.
          MOVE      WEFRBT,STRBTYP
.
GACM4400  CALL      GTAB0000       * get table type
          PACK      DIM24 WITH SAVCONTR,ACLAIM,AFUNDH,PTHFBCAT,STATYPE,STRBTYP
          GOTO      GACM4800
.
GACM4500  PACK      DIM24,SAVCONTR,ACLAIM,SP6,SP3,STATYPE,STRBTYP
          MOVE      ONE,USEDEF
          GOTO      GACM4800
.
GACM4600  CALL      DCLM0000                * Check Hospital claim code charging
          PACK      DIM24,SAVCONTR,PTCNDCLM,SP6,SP3,STATYPE,STRBTYP
          MOVE      TWO,USEDEF
.
.         Read in the description
.
GACM4800  PACK      KEY27 WITH DIM24,SP3
          OPEN      PATBFEE3,"patbfeef"
          CALL      RDSBFEE3
GACM5600  CALL      RDKBFEE3
          BRANCH    OVRCD OF GACM6000
.
          PACK      DIM24A,PTBFCNID,BFCLM,BFHFUND,PTBFHTYP,BFATYPE,BFBTYPE
          MATCH     DIM24,DIM24A
          GOTO      GACM6000 IF NOT EQUAL
.
          COMPARE   ZERO,BFENDDY
          GOTO      GACM5700 IF NOT EQUAL
.
          BRANCH    ACDAYCS OF GACM6400
          GOTO      GACM5600
.
.         Check the number of days
.
GACM5700  COMPARE   BFENDDY,LSTDAYS           * Days to Start of New Contract
          GOTO      GACM6400 IF LESS
.
          GOTO      GACM5600
.
GACM6000  BRANCH    USEDEF OF GACM4600,GACM7000
          GOTO      GACM4500
.
.         We have found the description
.
GACM6400  MOVE      BFDESC,TDDESC
          RESET     TDDESC,20
          PACK      PTDTDES2,SP20,SP20
.
.         Add the ward/bed to the description
.
GACM7000  IF        PTCNPRWD=1
            APPEND    "(",TDDESC
            APPEND    STWARD,TDDESC
            APPEND    SLASH,TDDESC
            APPEND    STBED,TDDESC
            APPEND    ")",TDDESC
          ENDIF
          RESET     TDDESC
.
.         Calculate the amount for the accommodation line
.
GACM7200  MOVE      ACCRATE,FORM12P2
          MULT      NODAYS,FORM12P2       * total invoice amount
          ADD       FORM12P2,ACCMTOT
.
          MOVE      LSTRATE,LINETOT
          MULT      NODAYS,LINETOT
.
          MOVE      ZERO,LINEGST
          IF        IBCNUGST=2
            MOVE      LINETOT,GSTWORK
            MULT      GSTPCEN,GSTWORK
            DIV       "100.00",GSTWORK
            MOVE      GSTWORK,LINEGST
          ENDIF
.
.         Add up Gross total and Accommodation total
.
          ADD       LINETOT,GROSSTOT
          IF        IBCNUGST=2
            ADD       LINEGST,TOTGST
          ENDIF
          ADD       LINETOT,TOTACCM
          GOTO      GACM9999
.
GACM9999  RETURN
+
.*****************************************************************************
.         Check maximum billing
.*****************************************************************************
MXBP0000  MOVE      ZERO,NODAYS
          IF        PROGFLAG=1
            UNPACK    IDATEF INTO FCENT1,FYEAR1,FMON1,FDAY1
            UNPACK    IDATET INTO TCENT1,TYEAR1,TMON1,TDAY1
            DAYSEP    IDATEF,IDATET,NODAYS
            GOTO      MXBP9999
          ENDIF
.
          UNPACK    IDATET INTO TCENT1,TYEAR1,TMON1,TDAY1
          MOVE      IDATET,SAVDATE1      * save original end date
          MOVE      LSTRATE,TOTOTHR           * Save original rate
.
          BRANCH    FINFLAG,MXBP2000
.
          MATCH     IDATEF,IDATET
          GOTO      MXBP9999 IF EQUAL
.
.         Check start of Accom. of payer against Invoice fromdate
.
          MATCH     SFRDATE,IDATEF
          GOTO      MXBP1000 IF NOT LESS
.
          MATCH     SFRDATE,IDATET
          GOTO      MXBP2000 IF LESS          * zero rate
.
.         Accom.charge will have zero amt up to start of Accom. of the payer
.
          MOVE      SFRDATE,IDATET
          MOVE      ZERO,LSTRATE
          DAYSEP    IDATEF,IDATET,NODAYS
          CALL      GACM0000
          UNPACK    IDATET INTO TCENT1,TYEAR1,TMON1,TDAY1
          IF        PROGFLAG=7
            MOVE      "1",SRECTYPE          * Accommodation record
            CALL      WTID0000              * Write to temp.Inv.payer details
          ENDIF
.
          MOVE      IDATET,IDATEF
          UNPACK    IDATEF INTO FCENT1,FYEAR1,FMON1,FDAY1
          MOVE      SAVDATE1,IDATET         * restore original end date
          MOVE      TOTOTHR,LSTRATE         * restore
.
          UNPACK    IDATET INTO TCENT1,TYEAR1,TMON1,TDAY1
          DAYSEP    IDATEF,IDATET,NODAYS
.
.         Check maximum billing
.
MXBP1000  DAYSEP    IDATEF,IDATET,NODAYS
          MOVE      LSTRATE,LINETOT
          MULT      NODAYS,LINETOT
          ADD       LINETOT,FFEEAMNT
.
          COMPARE   FFEEAMNT,PMPBMAXB
          GOTO      MXBP3000 IF NOT LESS
.
.         Maximum billing is lees than total charge
.         Charge accomm.up to Maximum biling and zero rate for rest of period
.
          MOVE      PMPBMAXB,FORM12P2
          DIV       PMPBMAXD,FORM12P2
          MOVE      FORM12P2,FORM4      * No of days for maximum billing
.
          SUB       TOTDAYS,FORM4       * days for this rate before Max bill.
.
          MOVE      IDATEF,ISBLDATE
          ADD       FORM4,ISBLDATE
          MOVE      ISBLDATE,IDATET      * maximum billing date
          UNPACK    IDATET INTO TCENT1,TYEAR1,TMON1,TDAY1
.
          MOVE      FORM4,NODAYS
          MOVE      LSTRATE,LINETOT
.
.         Bill up to the maximum day and
.         Zero rate from the maximum billing date to end of Accom. period
.
          CALL      GACM0000
          IF        PROGFLAG=7
            MOVE      "1",SRECTYPE          * Accommodation record
            CALL      WTID0000              * Write to temp.Inv.payer details
          ENDIF
          MOVE      ONE,FINFLAG             * flag zero rate for rest of Accomm.
          MOVE      IDATET,IDATEF
          MOVE      SAVDATE1,IDATET
.
.         zero amount for the rest of accommodation date
.
MXBP2000  
          UNPACK    IDATET INTO TCENT1,TYEAR1,TMON1,TDAY1
          MOVE      ZERO,LSTRATE
.
MXBP3000  
          UNPACK    IDATEF INTO FCENT1,FYEAR1,FMON1,FDAY1
          DAYSEP    IDATEF,IDATET,NODAYS
          ADD       NODAYS,TOTDAYS      *  Add up the total number of days
          GOTO      MXBP9999
.
MXBP9999  RETURN
+
.*******************************************************************************
.         Get the Accommodaton amount for the default payer
.         the Original rate minus the amount from other accommodation payers
.*******************************************************************************
DPAC0000  
          DAYSEP    IDATEF,IDATET,NODAYS
          ADD       NODAYS,TOTDAYS      *  Add up the total number of days
          UNPACK    IDATET INTO TCENT1,TYEAR1,TMON1,TDAY1
          MOVE      IDATET,SAVDATE1           * save original end date
          MOVE      SP70,SAVDATE2
          MOVE      ACCRATE,TOTOTHR           * Save original rate
          MOVE      "1",KEY1
.
          MOVE      SP70,PMIDINVN
          PACK      KEY34,DAADMNO,PMIDINVN,KEY1,IDATEF,SP70
	  CALL      RSPMIDE3
DPAC1000  CALL      RKPMIDE3
          BRANCH    OVRCD,DPAC7000
.
          PACK      KEY17,PMIDADMN,PMIDINVN,PMIDRTYP,SP70
          MATCH     KEY17,KEY34
          GOTO      DPAC7000 IF NOT EQUAL
          MATCH     DEFPAYER,PMIDCLTY
          GOTO      DPAC1000 IF EQUAL         * Skip default payer record
.
          COMPARE   ZERO,PMIDAMNT
          GOTO      DPAC1000 IF EQUAL         * try next payer
.
          MATCH     PMIDTDAT,IDATET
          GOTO      DPAC7000 IF LESS
.
.         Found a payer pays accommodation
.
          MOVE      PMIDAMNT,FORM12P2
          DIV       PMIDNDAY,FORM12P2         * amount of Accom.payer to pay
          SUB       FORM12P2,ACCRATE
          COMPARE   ZERO,ACCRATE
          GOTO      DPAC1000 IF LESS
          MOVE      ACCRATE,LSTRATE
.
.         Write the reminder of Accom.charge to default payer
.
          UNPACK    PMIDFDAT,FCENT1,FYEAR1,FMON1,FDAY1
          UNPACK    PMIDTDAT,TCENT1,TYEAR1,TMON1,TDAY1
          DAYSEP    PMIDFDAT,PMIDTDAT,NODAYS
.
          MOVE      "1",PMPBDEFT            * Default payer
          CALL      GACM0000
          IF        PROGFLAG=7
            MOVE      "1",SRECTYPE          * Accommodation record
            CALL      WTID0000              * Write to temp.Inv.payer details
          ENDIF
.
.         Check with the end of accommodation date
.
DPAC2000  MATCH     IDATET,PMIDTDAT
          GOTO      DPAC9000 IF NOT LESS
.
          MOVE      PMIDTDAT,SAVDATE2
          MOVE      PMIDTDAT,IDATEF
.
.         Reposition to the end date of Accommodation
.
          MOVE      "1",KEY1
          MOVE      SP70,PMIDINVN
          PACK      KEY34,DAADMNO,PMIDINVN,KEY1,PMIDTDAT,SP70
          CALL      RSPMIDE3
DPAC3000  CALL      RKPMIDE3
          BRANCH    OVRCD,DPAC7000
.
          PACK      KEY17,PMIDADMN,PMIDINVN,PMIDRTYP,SP70
          MATCH     KEY17,KEY34
          GOTO      DPAC7000 IF NOT EQUAL
          MATCH     DEFPAYER,PMIDCLTY
          GOTO      DPAC3000 IF EQUAL         * Skip default payer record
.
          COMPARE   ZERO,PMIDAMNT
          GOTO      DPAC3000 IF EQUAL         * try next payer
.
          MATCH     PMIDTDAT,IDATET
          GOTO      DPAC7000 IF LESS
.
.         Found a payer pays accommodation
.
          MOVE      TOTOTHR,ACCRATE           * restore original rate
          MOVE      PMIDAMNT,FORM12P2
          DIV       PMIDNDAY,FORM12P2         * amount of Accom.payer to pay
          SUB       FORM12P2,ACCRATE
          COMPARE   ZERO,ACCRATE
          GOTO      DPAC3000 IF LESS
          MOVE      ACCRATE,LSTRATE
.
          UNPACK    PMIDFDAT,FCENT1,FYEAR1,FMON1,FDAY1
          UNPACK    PMIDTDAT,TCENT1,TYEAR1,TMON1,TDAY1
          DAYSEP    PMIDFDAT,PMIDTDAT,NODAYS
          MOVE      "1",PMPBDEFT            * Default payer
          CALL      GACM0000
          IF        PROGFLAG=7
            MOVE      "1",SRECTYPE          * Accommodation record
            CALL      WTID0000              * Write to temp.Inv.payer details
          ENDIF
          MOVE      PMIDTDAT,SAVDATE2       * save from date for next accomm.
          GOTO      DPAC2000
.
.         No Accommodation payer for this period, set all to Default payer
.
DPAC7000  MOVE      TOTOTHR,LSTRATE
          MOVE      TOTOTHR,ACCRATE           * restore original rate
          UNPACK    IDATEF,FCENT1,FYEAR1,FMON1,FDAY1
          UNPACK    IDATET,TCENT1,TYEAR1,TMON1,TDAY1
          DAYSEP    IDATEF,IDATET,NODAYS
.
DPAC8000  MOVE      "1",PMPBDEFT            * Default payer
          CALL      GACM0000
          IF        PROGFLAG=7
            MOVE      "1",SRECTYPE          * Accommodation record
            CALL      WTID0000              * Write to temp.Inv.payer details
          ENDIF
.
DPAC9000  MOVE      TOTOTHR,ACCRATE           * restore original rate
DPAC9999  RETURN
+
.*****************************************************************************
.         Check if any other payer paying accommodation
.*****************************************************************************
OTHP0000  MOVE      ZERO,FORM12D2
          MOVE      "1",KEY1
          MOVE      SP70,PMIDINVN
          PACK      KEY34,AADMNO,PMIDINVN,KEY1,IDATEF,SP70
          CALL      RSPMIDE3
OTHP1000  CALL      RKPMIDE3
          BRANCH    OVRCD,OTHP9999
.
          PACK      KEY25,PMIDADMN,PMIDINVN,PMIDRTYP,PMIDFDAT,SP70
          MATCH     KEY25,KEY34
          GOTO      OTHP9999 IF NOT EQUAL
.
          MATCH     PMIDCLTY,PMPBCLTY
          GOTO      OTHP1000 IF EQUAL        * Same payer
.
          COMPARE   ZERO,PMIDAMNT
          GOTO      OTHP1000 IF EQUAL
.
          ADD       PMIDAMNT,FORM12D2
.
          MATCH     IDATET,PMIDTDAT
          IF        @LESS
            MOVE      PMIDTDAT,IDATET
          ENDIF
.
OTHP9999  RETURN
+
.*******************************************************************************
.         Processing Items for Independence services billing
.*******************************************************************************
ISIT0000  MATCH     "2",PMMIRTYP
          GOTO      ISIT2000 IF EQUAL    * MBS item goes to Default payer
.
          MOVE      "3",KEY1             * Item record type
          CALL      GPYR0000       * Check if there is a payer for this item
.
          COMPARE   ZERO,PMIDAMNT
          GOTO      ISIT2000 IF EQUAL    * No payer for this item
.
.         Check if item amount is greater than the Payer Invoice
.
          COMPARE   PMMIAMTT,PMIDAMNT
          GOTO      ISIT9999 IF NOT LESS * payer is paying whole item amount
.
          IF        EXIT=0
            MOVE      "1",PMIDSPLT
            CALL      UPPMIDE3           * Split invoice
          ENDIF
.
.         Check if payer pays full amount of item rate
.
          MOVE      PMIDAMNT,FORM12P2
          DIV       PMIDNDAY,FORM12P2
.
          ADD       PMMIRBAT,PMMIAMTP
          COMPARE   FORM12P2,PMMIAMTP
          GOTO      ISIT1200 IF EQUAL    * Payers pays full rate
.
          MOVE      PMMIAMTT,FORM12D2    * save total item amount
          SUB       PMIDAMNT,FORM12D2    * minus payer amount
          MOVE      PMMISERV,FORM6
.
          MOVE      PMIDNDAY,PMMISERV
.
          SUB       FORM12P2,PMMIAMTP
          MOVE      PMMIAMTP,PMIDAMNT
          MULT      PMIDNDAY,PMIDAMNT
.
.         Part of the Item rate amount goes to Default payer
.
          MOVE      DEFPAYER,PMPBCLTY    * default payer
          MOVE      "3",SRECTYPE         * Item record
          MOVE      PMIDAMNT,LINETOT
          IF        IBCNUGST=2 | IBCNUGST=3
            MOVE      PMMIGSTA,PTDTGSTA
            MOVE      PMMIGSTC,PTDTGSTC
            CALL      IGST0000
            MOVE      LINETOT,LINEGST
            MULT      IBGEAMNT,LINEGST
            DIV       "100.00",LINEGST
          ENDIF
.
          MOVE      "1",PMPBDEFT
          MOVE      ONE,PRTIFLAG        * flag for split invoice
.
.      If item is fully paid, set split flag to zero (pmsmtiaf can be removed)
.
          IF        FORM6=PMIDNDAY
            MOVE      ZERO,PRTIFLAG
          ENDIF
          CALL      WTID0000
.
          MOVE      FORM12D2,PMMIAMTT   * restore
          MOVE      FORM6,PMMISERV
          COMPARE   ZERO,PRTIFLAG
          GOTO      ISIT9999 IF EQUAL
.
.         set pmidsplt to zero for next part of item amount, so that record in
.         pmsmtiaf file can be deleted for this record when printing invoice
.
ISIT1200  MOVE      ZERO,PRTIFLAG
.
.         Part of the total items amount go to default payer
.         
          SUB       PMIDAMNT,PMMIAMTT
          SUB       PMIDNDAY,PMMISERV
          COMPARE   PMMISERV,ZERO
          GOTO      ISIT9999 IF NOT LESS
.
          MOVE      PMMITDAT,ISBLDATE
          ADD       PMIDNDAY,ISBLDATE
          MOVE      ISBLDATE,PMMITDAT     * New item date
.
.         Item goes to Default payer
.
ISIT2000  MOVE      DEFPAYER,PMPBCLTY    * default payer 
          MOVE      PMMIRTYP,SRECTYPE    * Item record
          MOVE      PMMIAMTT,LINETOT
          IF        IBCNUGST=2 | IBCNUGST=3
            MOVE      PMMIGSTA,PTDTGSTA
            MOVE      PMMIGSTC,PTDTGSTC
            CALL      IGST0000
            MOVE      LINETOT,LINEGST
            MULT      IBGEAMNT,LINEGST
            DIV       "100.00",LINEGST
          ENDIF
          MOVE      "1",PMPBDEFT
          CALL      WTID0000
          MOVE      ZERO,PRTIFLAG
.
ISIT9999  RETURN
+
.*******************************************************************************
.         check if there is a payer for this item
.*******************************************************************************
GPYR0000  MOVE      SP70,PMIDINVN
          PACK      KEY34,AADMNO,PMIDINVN,KEY1,SP70
          CALL      RSPMIDE3
GPYR1000  CALL      RKPMIDE3
          BRANCH    OVRCD,GPYR9000
.
          PACK      KEY17,PMIDADMN,PMIDINVN,PMIDRTYP,SP70
          MATCH     KEY17,KEY34
          GOTO      GPYR9000 IF NOT EQUAL
.
          MATCH     PMMIITEM,PMIDITEM       * Same item?
          GOTO      GPYR1000 IF NOT EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      GPYR9999
.
GPYR9000  MOVE      ZERO,PMIDAMNT
          MOVE      SP70,PMIDDEFT
          MOVE      ONE,EXIT
GPYR9999  RETURN
+
