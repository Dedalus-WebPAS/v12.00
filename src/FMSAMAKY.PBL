.---------------------------------------------------------------------------
.   System    :   Financial Management System        
.   Include   :   FMSAMAKY
.   Routine   :   QFMAMA00 Account Code        
.   Return    :           
.-------------------------------------------------------------------------------
QFMAMA00  TOPIC     "FMAMACCT"
          MOVE      ZERO,CSCRSIZ
          IF        HTOP<3
            MOVE      "10",HTOP
            MOVE      "24",HBOT
            MOVE      "20",HLEF
            MOVE      "80",HRIG
          ENDIF
QFMAMA04
          CALL      GSCRN000
          BRANCH    EXIT,QFMAMA99
          MOVE      STDCOL00,STDCOL01
          ADD       " 5",STDCOL01
          MOVE      STDCOL01,STDCOL02
          ADD       "13",STDCOL02
          MOVE      STDCOL02,STDCOL03
          ADD       "12",STDCOL03
          MOVE      STDCOL03,STDCOL04
          ADD       "36",STDCOL04
          MOVE      STDCOL04,STDCOL05
          ADD       " 6",STDCOL05
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON,"Account"            
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"Type"               
          DISPLAY   *PSTDCOL03:STDROW,*V2LON,*ULON,"Description"        
          DISPLAY   *PSTDCOL04:STDROW,*V2LON,*ULON,"Level"              
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
          PACK      SAVKEY80,FMAMLEDG,FMAMACCT,SP70                             
          SETLPTR   SAVKEY80,2                                                  
          GOTO      QFMAMA55
QFMAMA06
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          MOVE      SP70,FMAMACCT
          PACK      KEY14,FMAMLEDG,FMAMACCT,SP70                                
          IF        CSEARCH=1
            IF        SCHTST01=1
              PACK      KEY15,FMAMLEDG,SCHFLD02,SP70 * Scan
            ELSE
              PACK      KEY15,FMAMLEDG,SCHFLD02,SCHFLD01,SP70 * Match
            ENDIF
            CALL      RSFMAM2
            CALL      RPFMAM2
          ELSE
            IF        SCHTST03=1
              PACK      KEY50,FMAMLEDG,SCHFLD02,SP70 * Scan
            ELSE
              PACK      KEY50,FMAMLEDG,SCHFLD02,SCHFLD03,SP70 * Match
            ENDIF
            CALL      RSFMAM3
            CALL      RPFMAM3
          ENDIF
.
QFMAMA10  
         IF        CSEARCH=1
            CALL      RKFMAM2                      * search by Code
          ELSE
            CALL      RKFMAM3                      * search by Name
          ENDIF
          BRANCH    OVRCD,QFMAMA15
          PACK      KEY14,FMAMLEDG,FMAMACCT,SP70                                
          MATCH     SAVKEY80,KEY14
          GOTO      QFMAMA15 IF NOT EQUAL
          CALL      CFMAMA00                      * Check Search
          BRANCH    EXIT,QFMAMA10,QFMAMA15        * Exit 1=Search More
.                                                       2=No More
          ADD       ONE,XITEMNO
          ADD       ONE,STDROW
          STORE     KEY14,XITEMNO,ST1,ST2,ST3,ST4,ST5:
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          MOVE      FMAMTYPE,F1
          MOVE      "Invalid Type",ACCTYPE
          LOAD      ACCTYPE,F1,ACCTYPE1,ACCTYPE2,ACCTYPE3,ACCTYPE4,ACCTYPE5:
                               ACCTYPE6,ACCTYPE7,ACCTYPE8,ACCTYPE9
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,XITEMNO,DOT
          DISPLAY   *PSTDCOL01:STDROW,FMAMACCT
          DISPLAY   *PSTDCOL02:STDROW,ACCTYPE
          DISPLAY   *PSTDCOL03:STDROW,FMAMDESC
          DISPLAY   *PSTDCOL04:STDROW,FMAMCLEV
          COMPARE   MAXITEM,XITEMNO
          GOTO      QFMAMA10 IF NOT EQUAL
QFMAMA15
          UNPACK    SAVKEY80,FMAMLEDG,FMAMACCT                                  
          MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
QFMAMA20
          CALL      HSCRI000
          BRANCH    EXIT,QFMAMA30,QFMAMA35,QFMAMA40,QFMAMA45,QFMAMA04:
                              QFMAMA91,QFMAMA50
          GOTO      QFMAMA92
QFMAMA30
          IF        CSEARCH=1
            CALL      RKFMAM2                      * search by Code
          ELSE
            CALL      RKFMAM3                      * search by Name
          ENDIF
          BRANCH    OVRCD,QFMAMA85
          PACK      KEY14,FMAMLEDG,FMAMACCT,SP70                                
          MATCH     SAVKEY80,KEY14                                              
          GOTO      QFMAMA85 IF NOT EQUAL                                       
          CALL      CFMAMA00                      * Check Search
          BRANCH    EXIT,QFMAMA30,QFMAMA85        * Exit 1=Search More
    
          IF        CSEARCH=1
            CALL      RPFMAM2                      * search by Code
          ELSE
            CALL      RPFMAM3                      * search by Name
          ENDIF
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      QFMAMA10
QFMAMA35
          MOVE      ST1,KEY14
          CALL      RDFMAM1 
          IF        CSEARCH=1
            PACK      KEY15,FMAMLEDG,FMAMTYPE,FMAMACCT,SP70
            CALL      RDFMAM2                      * search by Code
          ELSE
            PACK      KEY50,FMAMLEDG,FMAMTYPE,FMAMDESC,SP70
            CALL      RDFMAM3                      * search by Name
          ENDIF
QFMAMA36
          IF        CSEARCH=1
            CALL      RPFMAM2                      * search by Code
          ELSE
            CALL      RPFMAM3                      * search by Name
          ENDIF
          BRANCH    OVRCD,QFMAMA85
          PACK      KEY14,FMAMLEDG,FMAMACCT,SP70                                
          MATCH     SAVKEY80,KEY14                                              
          GOTO      QFMAMA85 IF NOT EQUAL                                       
          CALL      CFMAMA00                      * Check Search
          BRANCH    EXIT,QFMAMA36,QFMAMA85        * Exit 1=Search More
          MATCH     SP70,FMAMACCT                                               
          GOTO      QFMAMA85 IF EQUAL                                           
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
QFMAMA37
          IF        CSEARCH=1
            CALL      RPFMAM2                      * search by Code
          ELSE
            CALL      RPFMAM3                      * search by Name
          ENDIF
          BRANCH    OVRCD,QFMAMA38
          PACK      KEY14,FMAMLEDG,FMAMACCT,SP70                                
          MATCH     SAVKEY80,KEY14                                              
          GOTO      QFMAMA38 IF NOT EQUAL                                       
          CALL      CFMAMA00                      * Check Search
          BRANCH    EXIT,QFMAMA37,QFMAMA38        * Exit 1=Search More
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      QFMAMA37 IF NOT EQUAL
QFMAMA38
          MOVE      ZERO,XITEMNO
          GOTO      QFMAMA10
QFMAMA40
          MOVE      ST1,KEY14
          CALL      RDFMAM1 
          IF        CSEARCH=1
            PACK      KEY15,FMAMLEDG,FMAMTYPE,FMAMACCT,SP70
            CALL      RDFMAM2                      * search by Code
          ELSE
            PACK      KEY50,FMAMLEDG,FMAMTYPE,FMAMDESC,SP70
            CALL      RDFMAM3                      * search by Name
          ENDIF
QFMAMA41
          IF        CSEARCH=1
            CALL      RPFMAM2                      * search by Code
          ELSE
            CALL      RPFMAM3                      * search by Name
          ENDIF
          BRANCH    OVRCD,QFMAMA85
          PACK      KEY14,FMAMLEDG,FMAMACCT,SP70                                
          MATCH     SAVKEY80,KEY14                                              
          GOTO      QFMAMA85 IF NOT EQUAL                                       
          CALL      CFMAMA00                      * Check Search
          BRANCH    EXIT,QFMAMA41,QFMAMA85        * Exit 1=Search More
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      QFMAMA06
QFMAMA45
          IF        CSEARCH=1
            CALL      RKFMAM2                      * search by Code
          ELSE
            CALL      RKFMAM3                      * search by Name
          ENDIF
          BRANCH    OVRCD,QFMAMA85
          PACK      KEY14,FMAMLEDG,FMAMACCT,SP70                                
          MATCH     SAVKEY80,KEY14                                              
          GOTO      QFMAMA85 IF NOT EQUAL                                       
          CALL      CFMAMA00                      * Check Search
          BRANCH    EXIT,QFMAMA45,QFMAMA85        * Exit 1=Search More
          SUSPEND
          CLIP      1,1,CRIG,24
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
          IF        CSEARCH=1
            PACK      KEY15,FMAMLEDG,SCHFLD02,Z70
            CALL      RSFMAM2                      * search by Code
          ELSE
            PACK      KEY50,FMAMLEDG,SCHFLD02,Z70 
            CALL      RSFMAM3                      * search by Name
          ENDIF
QFMAMA46
          IF        CSEARCH=1
            CALL      RPFMAM2                      * search by Code
          ELSE
            CALL      RPFMAM3                      * search by Name
          ENDIF
          BRANCH    OVRCD,QFMAMA47
          PACK      KEY14,FMAMLEDG,FMAMACCT,SP70                                
          MATCH     SAVKEY80,KEY14                                              
          GOTO      QFMAMA47 IF NOT EQUAL                                       
          CALL      CFMAMA00                      * Check Search
          BRANCH    EXIT,QFMAMA46,QFMAMA47        * Exit 1=Search More
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      QFMAMA46 IF NOT EQUAL
QFMAMA47
          MOVE      ZERO,XITEMNO
          GOTO      QFMAMA10
.
QFMAMA50  
          BOXCLR    CLEF,CTOP,CRIG,CBOT
QFMAMA55
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          UNPACK    SP70,SCHFLD01,SCHFLD02,SCHFLD03,SCHFLD04,SCHFLD05
          UNPACK    SP70,SCHFLD06,SCHFLD07,SCHFLD08,SCHFLD09,SCHFLD10
          MOVE      ZERO,SCHTST01
          MOVE      ZERO,SCHTST02
          MOVE      ZERO,SCHTST03
          MOVE      ZERO,SCHTST04
          MOVE      ZERO,SCHTST05
          MOVE      ZERO,SCHTST06
          MOVE      ZERO,SCHTST07
          MOVE      ZERO,SCHTST08
          MOVE      ZERO,SCHTST09
          MOVE      ZERO,SCHTST10
          MOVE      ZERO,CSEARCH
.
          DISPLAY   *PSTDCOL00:STDROW,*V2LON,*HON,"Sch"
          KEYIN     *PSTDCOL01:STDROW,*V2LON,SCHFLD01
          ENDSET    SCHFLD01
          APPEND    SP70,SCHFLD01
          RESET     SCHFLD01
          MATCH     SP70,SCHFLD01
          IF        !@EQUAL
            MOVE      ONE,CSEARCH
            MATCH     "*",SCHFLD01
            IF        @EQUAL
              RESET     SCHFLD01,2
              MOVE      SCHFLD01,SCHFLD01
              STRIP     SCHFLD01
              ADD       ONE,SCHTST01
              REP       UPPLOW,SCHFLD01
            ELSE 
              STRIP     SCHFLD01
            ENDIF
          ELSE 
            STRIP     SCHFLD01
          ENDIF
.
          CALL      KFMTY000              * Enter Account Type
.
          KEYIN     *PSTDCOL03:STDROW,*V2LON,SCHFLD03
          ENDSET    SCHFLD03
          APPEND    SP70,SCHFLD03
          RESET     SCHFLD03
          MATCH     SP70,SCHFLD03
          IF        !@EQUAL
            IF        CSEARCH=0
              MOVE      TWO,CSEARCH
            ENDIF
            MATCH     "*",SCHFLD03
            IF        @EQUAL
              RESET     SCHFLD03,2
              MOVE      SCHFLD03,SCHFLD03
              STRIP     SCHFLD03
              ADD       ONE,SCHTST03
              REP       UPPLOW,SCHFLD03
            ELSE 
              STRIP     SCHFLD03
            ENDIF
          ELSE 
            STRIP     SCHFLD03
          ENDIF
.
          IF        CSEARCH=0
            MOVE      ONE,CSEARCH
          ENDIF
.
          PACK      KEY8,SP70
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          UNPACK    SAVKEY80,FMAMLEDG,FMAMACCT                                  
          GOTO      QFMAMA06
.
QFMAMA85
          LOAD      KEY14,XITEMNO,ST1,ST2,ST3,ST4,ST5:
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY14,FMAMLEDG,FMAMACCT                                     
          CALL      RDFMAM1 
          IF        CSEARCH=1
            PACK      KEY15,FMAMLEDG,FMAMTYPE,FMAMACCT,SP70
            CALL      RDFMAM2                      * search by Code
          ELSE
            PACK      KEY50,FMAMLEDG,FMAMTYPE,FMAMDESC,SP70
            CALL      RDFMAM3                      * search by Name
          ENDIF
QFMAMA90
          UNPACK    SAVKEY80,FMAMLEDG,FMAMACCT                                  
          BEEP
          GOTO      QFMAMA20
QFMAMA91
          MOVE      SP70,FMAMDESC
          MOVE      SP70,FMAMSEC 
          MOVE      SP70,FMAMTYPE
          MOVE      SP70,FMAMREF1
          MOVE      SP70,FMAMREF2
          MOVE      SP70,FMAMREF3
          MOVE      SP70,FMAMREF4
          MOVE      SP70,FMAMDAT1
          MOVE      SP70,FMAMDAT2
          MOVE      SP70,FMAMDAT3
          MOVE      SP70,FMAMDAT4
          MOVE      SP70,FMAMCOD1
          MOVE      SP70,FMAMCOD2
          MOVE      SP70,FMAMCOD3
          MOVE      SP70,FMAMCOD4
          MOVE      ZERO,FMAMAMT1
          MOVE      ZERO,FMAMAMT2
          MOVE      ZERO,FMAMAMT3
          MOVE      ZERO,FMAMAMT4
          MOVE      ZERO,FMAMSTAT
          MOVE      ZERO,FMAMCTRL
          MOVE      ZERO,FMAMASST
          MOVE      ZERO,FMAMPPT 
          MOVE      ZERO,FMAMONC 
          MOVE      SP70,FMAMONCC
          MOVE      SP70,FMAMCHEQ
          MOVE      ZERO,FMAMOBAL
          MOVE      SP70,FMAMBDAT
          MOVE      SP70,FMAMDPLA
          MOVE      SP70,FMAMUNIT
          MOVE      ZERO,FMAMAYTD
          MOVE      ZERO,FMAMMULT
          MOVE      ZERO,FMAMDIV 
          MOVE      SP70,FMAMCLEV
          MOVE      SP70,FMAMDPRO
          MOVE      SP70,FMAMSPAR
          MOVE      SP70,FMAMLOCK
          MOVE      SP70,KEY14
          MOVE      SP70,FMAMACCT
          GOTO      QFMAMA96
QFMAMA92
          MOVE      SP70,KEY14
          MOVE      SP70,FMAMACCT
          LOAD      KEY14,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY14,FMAMLEDG,FMAMACCT                                     
QFMAMA96
          PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
QFMAMA99
          TOPIC    PRGID
          RETURN
.--------------------------------------------------------------------
. Keyin Type
.                                                         Valid Types
.        Requires - CACCTIND  0=all                         1-9
.                             1=posting                     1-5
.                             2=total/head                  6,7
.                             3=stat                        8,9
.                             4=total                       6
.                             5=post/total                  1-6
.                             6=stat post                   8
.                             7=asset,liab,equity           3,4,5
.                             8=all but heading             1-6 + 8-9
.                             9=stat total                  9
.                             10=stat&norm post             1-5,8
.                             11=Equity Only                5
.--------------------------------------------------------------------
KFMTY000  
          BRANCH    CACCTIND,KFMTY110,KFMTY120,KFMTY130,KFMTY140,KFMTY150:
                             KFMTY160,KFMTY170,KFMTY180,KFMTY190,KFMTY200:
                             KFMTY210
          PACK      KEY20,HLEF,HTOP,HRIG,HBOT
          MOVE      "60",HLEF
          MOVE      "80",HRIG
          MOVE      "13",HTOP
          MOVE      "24",HBOT
          CALL      GETSCR00
          BRANCH    EXIT,KFMTY500
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          DISPLAY   *P63:14,*V2LON,*ULON,"Valid Acc. Types",*HOFF:
                    *P63:15,*V2LON,"1",*HOFF," Income":
                    *P63:16,*V2LON,"2",*HOFF," Expenditure":
                    *P63:17,*V2LON,"3",*HOFF," Asset":
                    *P63:18,*V2LON,"4",*HOFF," Liability":
                    *P63:19,*V2LON,"5",*HOFF," Equity":
                    *P63:20,*V2LON,"6",*HOFF," Total":
                    *P63:21,*V2LON,"7",*HOFF," Heading":
                    *P63:22,*V2LON,"8",*HOFF," Stats Posting":
                    *P63:23,*V2LON,"9",*HOFF," Stats Total"
          GOTO      KFMTY500
KFMTY110
          PACK      KEY20,HLEF,HTOP,HRIG,HBOT
          MOVE      "60",HLEF
          MOVE      "80",HRIG
          MOVE      "17",HTOP
          MOVE      "24",HBOT
          CALL      GETSCR00
          BRANCH    EXIT,KFMTY500
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          DISPLAY   *P63:18,*V2LON,*ULON,"Valid Acc. Types",*HOFF:
                    *P63:19,*V2LON,"1",*HOFF," Income":
                    *P63:20,*V2LON,"2",*HOFF," Expenditure":
                    *P63:21,*V2LON,"3",*HOFF," Asset":
                    *P63:22,*V2LON,"4",*HOFF," Liability":
                    *P63:23,*V2LON,"5",*HOFF," Equity"
          GOTO      KFMTY500
KFMTY120
          PACK      KEY20,HLEF,HTOP,HRIG,HBOT
          MOVE      "60",HLEF
          MOVE      "80",HRIG
          MOVE      "20",HTOP
          MOVE      "24",HBOT
          CALL      GETSCR00
          BRANCH    EXIT,KFMTY500
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          DISPLAY   *P63:21,*V2LON,*ULON,"Valid Acc. Types",*HOFF:
                    *P63:22,*V2LON,"6",*HOFF," Total":
                    *P63:23,*V2LON,"7",*HOFF," Heading"
          GOTO      KFMTY500
KFMTY130
          PACK      KEY20,HLEF,HTOP,HRIG,HBOT
          MOVE      "60",HLEF
          MOVE      "80",HRIG
          MOVE      "20",HTOP
          MOVE      "24",HBOT
          CALL      GETSCR00
          BRANCH    EXIT,KFMTY500
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          DISPLAY   *P63:21,*V2LON,*ULON,"Valid Acc. Types",*HOFF:
                    *P63:22,*V2LON,"8",*HOFF," Stats Posting":
                    *P63:23,*V2LON,"9",*HOFF," Stats Total"
          GOTO      KFMTY500
KFMTY140  
          MOVE      "6",SCHFLD02
          GOTO      KFMTY900
KFMTY150  
          PACK      KEY20,HLEF,HTOP,HRIG,HBOT
          MOVE      "60",HLEF
          MOVE      "80",HRIG
          MOVE      "16",HTOP
          MOVE      "24",HBOT
          CALL      GETSCR00
          BRANCH    EXIT,KFMTY500
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          DISPLAY   *P63:17,*V2LON,*ULON,"Valid Acc. Types",*HOFF:
                    *P63:18,*V2LON,"1",*HOFF," Income":
                    *P63:19,*V2LON,"2",*HOFF," Expenditure":
                    *P63:20,*V2LON,"3",*HOFF," Asset":
                    *P63:21,*V2LON,"4",*HOFF," Liability":
                    *P63:22,*V2LON,"5",*HOFF," Equity":
                    *P63:23,*V2LON,"6",*HOFF," Total"
          GOTO      KFMTY500
KFMTY160  
          MOVE      "8",SCHFLD02
          GOTO      KFMTY900
KFMTY170  
          PACK      KEY20,HLEF,HTOP,HRIG,HBOT
          MOVE      "60",HLEF
          MOVE      "80",HRIG
          MOVE      "19",HTOP
          MOVE      "24",HBOT
          CALL      GETSCR00
          BRANCH    EXIT,KFMTY500
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          DISPLAY   *P63:20,*V2LON,*ULON,"Valid Acc. Types",*HOFF:
                    *P63:21,*V2LON,"3",*HOFF," Asset":
                    *P63:22,*V2LON,"4",*HOFF," Liability":
                    *P63:23,*V2LON,"5",*HOFF," Equity"
          GOTO      KFMTY500
KFMTY180  
          PACK      KEY20,HLEF,HTOP,HRIG,HBOT
          MOVE      "60",HLEF
          MOVE      "80",HRIG
          MOVE      "14",HTOP
          MOVE      "24",HBOT
          CALL      GETSCR00
          BRANCH    EXIT,KFMTY500
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          DISPLAY   *P63:15,*V2LON,*ULON,"Valid Acc. Types",*HOFF:
                    *P63:16,*V2LON,"1",*HOFF," Income":
                    *P63:17,*V2LON,"2",*HOFF," Expenditure":
                    *P63:18,*V2LON,"3",*HOFF," Asset":
                    *P63:19,*V2LON,"4",*HOFF," Liability":
                    *P63:20,*V2LON,"5",*HOFF," Equity":
                    *P63:21,*V2LON,"6",*HOFF," Total":
                    *P63:22,*V2LON,"8",*HOFF," Stats Posting":
                    *P63:23,*V2LON,"9",*HOFF," Stats Total"
          GOTO      KFMTY500
.
KFMTY190  MOVE      "9",SCHFLD02
          GOTO      KFMTY900
.
KFMTY200  PACK      KEY20,HLEF,HTOP,HRIG,HBOT
          MOVE      "60",HLEF
          MOVE      "80",HRIG
          MOVE      "16",HTOP
          MOVE      "24",HBOT
          CALL      GETSCR00
          BRANCH    EXIT,KFMTY700
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          DISPLAY   *P63:17,*V2LON,*ULON,"Valid Acc. Types",*HOFF:
                    *P63:18,*V2LON,"1",*HOFF," Income":
                    *P63:19,*V2LON,"2",*HOFF," Expenditure":
                    *P63:20,*V2LON,"3",*HOFF," Asset":
                    *P63:21,*V2LON,"4",*HOFF," Liability":
                    *P63:22,*V2LON,"5",*HOFF," Equity":
                    *P63:23,*V2LON,"8",*HOFF," Stats Posting"
          GOTO      KFMTY500
.
KFMTY210  
          MOVE      "5",SCHFLD02
          GOTO      KFMTY900
.
KFMTY500  KEYIN     *PSTDCOL02:STDROW,*V2LON,F1
          MOVE      SP70,SCHFLD02
          COMPARE   ZERO,F1
          GOTO      KFMTY500 IF EQUAL
          BRANCH    CACCTIND,KFMTY510,KFMTY520,KFMTY530,KFMTY600,KFMTY540:
                             KFMTY600,KFMTY550,KFMTY560,KFMTY600,KFMTY570
          BRANCH    F1,KFMTY600,KFMTY600,KFMTY600,KFMTY600,KFMTY600:
                       KFMTY600,KFMTY600,KFMTY600,KFMTY600
          GOTO      KFMTY500
KFMTY510  BRANCH    F1,KFMTY600,KFMTY600,KFMTY600,KFMTY600,KFMTY600
          GOTO      KFMTY500
KFMTY520  BRANCH    F1,KFMTY500,KFMTY500,KFMTY500,KFMTY500,KFMTY500:
                       KFMTY600,KFMTY600,KFMTY500,KFMTY500
          GOTO      KFMTY500
KFMTY530  BRANCH    F1,KFMTY500,KFMTY500,KFMTY500,KFMTY500,KFMTY500:
                       KFMTY500,KFMTY500,KFMTY600,KFMTY600
          GOTO      KFMTY500
KFMTY540  BRANCH    F1,KFMTY600,KFMTY600,KFMTY600,KFMTY600,KFMTY600:
                       KFMTY600,KFMTY500,KFMTY500,KFMTY500
          GOTO      KFMTY500
KFMTY550  BRANCH    F1,KFMTY500,KFMTY500,KFMTY600,KFMTY600,KFMTY600:
                       KFMTY500,KFMTY500,KFMTY500,KFMTY500
          GOTO      KFMTY500
KFMTY560  BRANCH    F1,KFMTY600,KFMTY600,KFMTY600,KFMTY600,KFMTY600:
                       KFMTY600,KFMTY500,KFMTY600,KFMTY600
          GOTO      KFMTY500
KFMTY570  BRANCH    F1,KFMTY600,KFMTY600,KFMTY600,KFMTY600,KFMTY600:
                       KFMTY500,KFMTY500,KFMTY600,KFMTY500
          GOTO      KFMTY500
KFMTY600  MOVE      F1,SCHFLD02
          STRIP     SCHFLD02
          CALL      PUTSCR00
          UNPACK    KEY20,KEY3,KEY2
          MOVE      KEY3,HLEF
          MOVE      KEY2,HTOP
          UNPACK    KEY20,KEY5,KEY3,KEY2
          MOVE      KEY3,HRIG
          MOVE      KEY2,HBOT
          GOTO      KFMTY999
.
KFMTY700
          KEYIN     *PSTDCOL02:STDROW,*V2LON,F1
          MOVE      F1,SCHFLD02
          STRIP     SCHFLD02
          GOTO      KFMTY999
.
KFMTY900  STRIP     SCHFLD02

KFMTY999  
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,SCHFLD02
          RETURN
.---------------------------------------------------------------
. Check Search Options
.---------------------------------------------------------------
CFMAMA00  MATCH     SCHFLD02,FMAMTYPE          * Type Correct
          GOTO      CFMAMA90 IF NOT EQUAL      * No
          MOVE      FMAMACCT,KEY12
          MATCH     SCHFLD01,SP70
          GOTO      CFMAMA10 IF EQUAL
          GOTO      CFMAMA10 IF EOS  
          IF        SCHTST01=0
            MATCH     SCHFLD01,KEY12
            GOTO      CFMAMA90 IF NOT EQUAL
          ELSE
            REP       UPPLOW,KEY12
            SCAN      SCHFLD01,KEY12
            GOTO      CFMAMA91 IF NOT EQUAL
          ENDIF
.
CFMAMA10  MATCH     SCHFLD03,SP70
          GOTO      CFMAMA20 IF EQUAL
          GOTO      CFMAMA20 IF EOS
          MOVE      FMAMDESC,KEY35
          IF        SCHTST03=0
            MATCH     SCHFLD03,KEY35
            GOTO      CFMAMA90 IF NOT EQUAL
          ELSE
            REP       UPPLOW,KEY35
            SCAN      SCHFLD03,KEY35
            GOTO      CFMAMA91 IF NOT EQUAL
          ENDIF
CFMAMA20
          MOVE      ZERO,EXIT                  * Valid
          GOTO      CFMAMA99
.
CFMAMA90  MOVE      TWO,EXIT                   * End of Search
          GOTO      CFMAMA99
CFMAMA91  MOVE      ONE,EXIT                   * Get Next
          GOTO      CFMAMA99
.
CFMAMA99  RETURN
.-------------------------------------------------------------------------------
.   System    :   FMS Accounting System  
.                           
.   Program   :   FMSAMAFM 
.                            
.   Routines  :   KAMAFM00 Keyin Account   - Returns a valid record
.                 AAMAFM00 Accept Account  - Returns a key NOT on file
.   
.   Requires  :   FMLALEDG Ledger Number
.                 FMLADESC Ledger Number Description
.                 CACCTIND 0=all acc, 1=posting, 2=total/head, 3=stat, 4=total,
.                          5=post/total, 6=stats post, 7=asset,liab,equity
.                          8=all but heading, 9=stat total, 10=stat&norm post
.                          11=Equity Only
.
.   Return    :   EXIT = 1 null entry EXIT = 2 EXITCHAR entered
.-------------------------------------------------------------------------------
.   Mods      :   24/07/91  Neeriem Dye
.                           Fixed infinite loop on previous page
.                           Only occurred if first item on file is invalid
.                 10.01.92  B.G.Ackland
.                           Modified to use new standard Generated Routines
.                 30.09.92  Neeriem "I Hate Support" Dye
.                           Fixed so V6 Keyin Checked if type was a valid type
.-------------------------------------------------------------------------------
. Normal Code Keyin - Code Must Exist Exit = 1 if Null Entry
.-------------------------------------------------------------------------------
KAMAFM00  MOVE      ZERO,CKEYTYP
          MOVE      FMLALEDG,FMAMLEDG
          DISPLAY   *PCCOL:CVERT,*EL;
          CALL      KFMAMA00
.
KAMAFM99  RETURN
.-------------------------------------------------------------------------------
. Accept Code Keyin - Code Must Not Exist 
.-------------------------------------------------------------------------------
AAMAFM00  MOVE      ONE,CKEYTYP
          MOVE      FMLALEDG,FMAMLEDG
          DISPLAY   *PCCOL:CVERT,*EL;
          CALL      KFMAMA00
          RETURN
.**********************************************************************
. CKAMA - Check if correct account indicator           Called By ML
.        Requires - CACCTIND (0=all, 1=posting, 2=total/head, 3=stat, 4=total,
.                          5=post/total, 6=stat post, 7=asset,liab,equity)
.                          8=all but heading, 9=stat total, 10=stat&norm post
.                          11=Equity Only
.                   FMAMTYPE (account type of record to be tested)
.        Returns  - OVRCD    (1=invalid record)
.**********************************************************************
CKAMA000  BRANCH    CACCTIND,CKAMA100,CKAMA200,CKAMA300,CKAMA400,CKAMA500:
                             CKAMA600,CKAMA700,CKAMA800,CKAMA850,CKAMA870:
                             CKAMA880
          GOTO      CKAMA9000                    * all
.
CKAMA100  MATCH     "6",FMAMTYPE
          GOTO      CKAMA900 IF LESS             * posting type
          GOTO      CKAMA950
.
CKAMA200  MATCH     "6",FMAMTYPE
          GOTO      CKAMA900 IF EQUAL            * head/total type
          MATCH     "7",FMAMTYPE
          GOTO      CKAMA900 IF EQUAL
          GOTO      CKAMA950
.
CKAMA300  MATCH     "8",FMAMTYPE
          GOTO      CKAMA900 IF EQUAL            * stat type
          MATCH     "9",FMAMTYPE
          GOTO      CKAMA900 IF EQUAL
          GOTO      CKAMA950
.
CKAMA400  MATCH     "6",FMAMTYPE
          GOTO      CKAMA900 IF EQUAL            * total type
          GOTO      CKAMA950
.
CKAMA500  MATCH     "7",FMAMTYPE
          GOTO      CKAMA900 IF LESS             * post/total type
          GOTO      CKAMA950
.
CKAMA600  MATCH     "8",FMAMTYPE
          GOTO      CKAMA900 IF EQUAL            * stat posting type
          GOTO      CKAMA950
.
CKAMA700  MATCH     "3",FMAMTYPE
          GOTO      CKAMA900 IF EQUAL            * asset, liab, equity type
          MATCH     "4",FMAMTYPE
          GOTO      CKAMA900 IF EQUAL
          MATCH     "5",FMAMTYPE
          GOTO      CKAMA900 IF EQUAL
          GOTO      CKAMA950
.
CKAMA800  MATCH     "7",FMAMTYPE
          GOTO      CKAMA900 IF NOT EQUAL        * all but heading
          GOTO      CKAMA950
.
CKAMA850  MATCH     "9",FMAMTYPE
          GOTO      CKAMA900 IF EQUAL            * stat total
          GOTO      CKAMA950
.
CKAMA870  MATCH     "6",FMAMTYPE
          GOTO      CKAMA900 IF LESS             * norm posting type
          MATCH     "8",FMAMTYPE
          GOTO      CKAMA900 IF EQUAL            * stat posting type
          GOTO      CKAMA950
.
CKAMA880  MATCH     "5",FMAMTYPE
          GOTO      CKAMA900 IF EQUAL            * equity type
          GOTO      CKAMA950
.
.
CKAMA900  MOVE      ZERO,OVRCD                   * valid record
          GOTO      CKAMA999
.
CKAMA950  MOVE      ONE,OVRCD                    * invalid record
.
CKAMA999  RETURN
.---------------------------------------------------------------------------
.   System    :   Financial Management System        
.   Include   :   FMSAMAKY
.   Routine   :   KFMAMA00 Keyin Account             
.   Parameter :   CKEYTYP 0 = Keyin , 1 = Accept, 2 = Either
.   Return    :   EXIT = 1 null entry, 2 EXITCHAR entered
.-------------------------------------------------------------------------------
KFMAMA00 TOPIC     "FMAMACCT"
          MOVE      UNDLN70,FMAMACCT
          KEYIN     *PCCOL:CVERT,*DV,FMAMACCT:                                  
                    *PCCOL:CVERT,*V2LON,FMAMACCT:                               
                    *PCCOL:CVERT,*DV,FMAMACCT
.
          ENDSET    FMAMACCT
          APPEND    SP70,FMAMACCT
          RESET     FMAMACCT
.
          MATCH     SP70,FMAMACCT
          GOTO      KFMAMA90 IF EQUAL
.
          CMATCH    EXITCHAR,FMAMACCT
          GOTO      KFMAMA95 IF EQUAL
.
          CMATCH    QUEST,FMAMACCT
          GOTO      KFMAMA10 IF NOT EQUAL
.
          MOVE      CCITEM,SCCITEM
          CALL      QFMAMA00
          MOVE      SCCITEM,CCITEM
.
          ENDSET    FMAMACCT
          APPEND    SP70,FMAMACCT
          RESET     FMAMACCT
.
          MATCH     SP70,FMAMACCT
          GOTO      KFMAMA00 IF EQUAL
.
          DISPLAY   *PCCOL:CVERT,*V2LON,FMAMACCT                                
KFMAMA10
          PACK      KEY14,FMAMLEDG,FMAMACCT                                     
          CALL      RAFMAM1 
          BRANCH    OVRCD,KFMAMA80
          BRANCH    CKEYTYP,KFMAMA75
          CALL      RDFMAM1 
          CALL      CKAMA000                  * check if record has correct ind.
          BRANCH    OVRCD,KFMAMA85
KFMAMA70
          MOVE      ZERO,EXIT
          GOTO      KFMAMA99
KFMAMA75
          MOVE      "Account Already Exist - ",DISPMSG
          CALL      MESSAGE1
          GOTO      KFMAMA00
KFMAMA80
          MOVE      ZERO,EXIT
          BRANCH    CKEYTYP,KFMAMA99,KFMAMA99
          MOVE      "Account Does Not Exist - ",DISPMSG
          CALL      MESSAGE1
          GOTO      KFMAMA00
.
KFMAMA85  MOVE      ZERO,F1
          MOVE      FMAMTYPE,F1
          MOVE      "Invalid Type",ACCTYPE
          LOAD      ACCTYPE,F1,ACCTYPE1,ACCTYPE2,ACCTYPE3,ACCTYPE4,ACCTYPE5:
                               ACCTYPE6,ACCTYPE7,ACCTYPE8,ACCTYPE9
          MOVE      " Account Type Invalid - ",KEY30
          PACK      DISPMSG,ACCTYPE,KEY30
          CALL      MESSAGE1
          GOTO      KFMAMA00
.
KFMAMA90
          MOVE      ONE,EXIT
          GOTO      KFMAMA99
KFMAMA95
          MOVE      TWO,EXIT
KFMAMA99 TOPIC    PRGID
          RETURN
