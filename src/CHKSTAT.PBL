.*****************************************************************************
.*                                                                           *
.*    Include    :  CHKSTAT.PBL                                              *
.*                                                                           *
.*    Function   :  Check the Status of Patient as at a Date                 *
.*                                                                           *
.*    Author     :  Paul Howells                                             *
.*                                                                           *
.*    Date       :  11/12/92                                                 *
.*                                                                           *
.*    Parameters :  WMSTAT,ASATDTE                                           *
.*                                                                           *
.*    Returns    :  FSTAT = status during period                            *
.*                  or FSTAT = 1 if not that status during period           *
.*                                                                           *
.*****************************************************************************
.                   07/10/1998  Glenn Berry      5.07 changes
.*****************************************************************************
CHKSTAT
CHKS0000  MOVE      WMSTAT,FSTAT
          BRANCH    WMSTAT,CHKS9999,CHKS1000,CHKS1000,CHKS3000:
                           CHKS4000,CHKS5000,CHKS6000
          GOTO      CHKS9999 
.
.         Status 1 = unscheduled
.
.         Status 2 = scheduled, 3 = pre-admitted
.         If current status is 2 or 3 read the bookings file and if 
.         the booking date is <=  date of the period return with 
.         that status, else return with a status of 1
.
CHKS1000  MOVE      WMBOOK,KEY8
          CALL      RDBKREC1
          MATCH     BKREADAT,ASATDTE
          GOTO      CHKS9999 IF NOT LESS
.
          MOVE      ONE,FSTAT
          GOTO      CHKS9999
.
.         Status 4 = admitted
.         If current status is 4 read the admission file and if 
.         the admission date is <= date of the period return with 
.         that status. Otherwise read the booking file and if the booking 
.         date is <= date return with a status of 3, else return 
.         with a status of 1
.
CHKS3000  MOVE      WMBOOK,KEY8
          CALL      RDMISS1
          PACK      KEY8,ADATE
          MATCH     KEY8,ASATDTE
          GOTO      CHKS9999 IF NOT LESS
.
          MOVE      WMBOOK,KEY8
          CALL      RDBKREC1
          MATCH     BKREADAT,ASATDTE
          GOTO      CHKS3500 IF NOT LESS
.
          MOVE      ONE,FSTAT
          GOTO      CHKS9999
.
CHKS3500  MOVE      THREE,FSTAT
          GOTO      CHKS9999
.
.         Status 5 = discharged
.         If current status is 5 read the discharge file and if 
.         the discharge date is <= date of the period return with 
.         that status. Otherwise read the admission file and if the admission 
.         date is < starting date return with a status of 4. Otherwise read 
.         the booking file and if the booking date is <= starting date of the 
.         period return with a status of 3, else return with a status of 1
.
CHKS4000  MOVE      WMBOOK,KEY8
          CALL      RDDSCH1
          PACK      KEY8,DDATE
          MATCH     KEY8,ASATDTE
          GOTO      CHKS9999 IF NOT LESS
.
          MOVE      WMBOOK,KEY8
          CALL      RDMISS1
          PACK      KEY8,ADATE
          MATCH     KEY8,ASATDTE
          GOTO      CHKS4500 IF NOT LESS
.
          MOVE      WMBOOK,KEY8
          CALL      RDBKREC1
          MATCH     BKREADAT,ASATDTE
          GOTO      CHKS4700 IF NOT LESS
.
          MOVE      ONE,FSTAT
          GOTO      CHKS9999
.
CHKS4500  MOVE      FOUR,FSTAT
          GOTO      CHKS9999
.
CHKS4700  MOVE      THREE,FSTAT
          GOTO      CHKS9999
.
.         Status 6 = removed
.         If current status is 6 check the removal date on the waiting 
.         list record and if it is <= date of the period return 
.         with that status, else return with a status of 1
.
CHKS5000  MATCH     WMDATE4,ASATDTE
          GOTO      CHKS9999 IF NOT LESS
.
          MOVE      ONE,FSTAT
          GOTO      CHKS9999
.
.         Status 7 = performed 
.         If current status is 7 check the performed date on the waiting 
.         list record and if it is <= date of the period return 
.         with that status else
.         Read the admission file and if 
.         the admission date is <= date of the period return with 
.         that status. Otherwise read the booking file and if the booking 
.         date is <= date return with a status of 3, else return 
.         with a status of 1
.
CHKS6000  MATCH     WMDATE2,ASATDTE
          GOTO      CHKS9999 IF NOT LESS
.
          MOVE      WMBOOK,KEY8
          CALL      RDMISS1
          PACK      KEY8,ADATE
          MATCH     KEY8,ASATDTE
          GOTO      CHKS6700 IF NOT LESS
.
CHKS6500  MOVE      WMBOOK,KEY8
          CALL      RDBKREC1
          MATCH     BKREADAT,ASATDTE
          GOTO      CHKS6800 IF NOT LESS
.
          MOVE      ONE,FSTAT
          GOTO      CHKS9999
.
CHKS6700  MOVE      FOUR,FSTAT
          GOTO      CHKS9999
.
CHKS6800  MOVE      THREE,FSTAT
          GOTO      CHKS9999
.
CHKS9999  RETURN
+
