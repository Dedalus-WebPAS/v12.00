. *----------------------------------------------------------------------
. * Include Name  :   OUTCPCTM.PBL
. *
. * Function      :   CGI variables for OUTCPCFD - outcpcaf.dat
. *
. * Includes Needed :
. *
. * Modifications   :
. *
. * V10.11.01 14/11/2017  Don Nguyen        TSK 0816814
. *           Added OTCP1800 - VMTTAB00
. * V10.06.00 14/05/2015 Ebon Clements      CAR 303890
. *           Created include
. *----------------------------------------------------------------------
.
.------------------------------------------------------------------------
.   Clear Parameters for Outpatients  CPC Table
.------------------------------------------------------------------------
CPOTCP00  MOVE      Z70,OTCPC001
          MOVE      Z70,OTCPC002
          MOVE      Z70,OTCPC003
          MOVE      Z70,OTCPC004
          MOVE      Z70,OTCPC005
          MOVE      Z70,OTCPC006
          MOVE      Z70,OTCPC007
          MOVE      Z70,OTCPC008
          MOVE      Z70,OTCPC009
          MOVE      Z70,OTCPC010
          MOVE      Z70,OTCPC011
          MOVE      Z70,OTCPC012
.
CPOTCP99  RETURN
.
.------------------------------------------------------------
. Add fields for Outpatients CPC Table
.------------------------------------------------------------
OTCP0000  BRANCH    FLDITMNO,OTCP0100,OTCP0200,OTCP0300,OTCP0400,OTCP0500:
                             OTCP0600,OTCP0700,OTCP0800,OTCP0900,OTCP1000:
                             OTCP1100,OTCP1200,OTCP1300,OTCP1400,OTCP1500:
                             OTCP1600,OTCP1700,OTCP1800
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      OTCP9999
.
OTCP0100  WRITE     HTMLFILE;OTCPSITE;
          GOTO      OTCP9999
.
OTCP0200  WRITE     HTMLFILE;CLINTYPE;
          GOTO      OTCP9999
.
OTCP0300  WRITE     HTMLFILE;OTCPPROC;
          GOTO      OTCP9999
.
OTCP0400  WRITE     HTMLFILE;OTCPDESC;
          GOTO      OTCP9999
.
OTCP0500  WRITE     HTMLFILE;OTCPICDC;
          GOTO      OTCP9999
.
OTCP0600  WRITE     HTMLFILE;OTCPACTV;
          GOTO      OTCP9999
.
OTCP0700  MATCH     OTCPCDAT,SP70
          GOTO      OTCP9999 IF EQUAL
          UNPACK    OTCPCDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      OTCP9999
.
OTCP0800  WRITE     HTMLFILE;OTCPCTIM;
          GOTO      OTCP9999
.
OTCP0900  MATCH     SP70,OTCPCUID
          GOTO      OTCP9999 IF EQUAL
          PACK      KEY10,OTCPCUID
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      OTCPCUID,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM;
          GOTO      OTCP9999
.
OTCP1000  MATCH     OTCPUDAT,SP70
          GOTO      OTCP9999 IF EQUAL
          UNPACK    OTCPUDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      OTCP9999
.
OTCP1100  WRITE     HTMLFILE;OTCPUTIM;
          GOTO      OTCP9999
.
OTCP1200  MATCH     SP70,OTCPUUID
          GOTO      OTCP9999 IF EQUAL
          PACK      KEY10,OTCPUUID
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      OTCPUUID,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM;
          GOTO      OTCP9999
.
OTCP1300  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          IF        F1=1
            MOVE      ONE,CLSTFLAG          * display list for 'lookup'
          ELSE
            MOVE      ZERO,CLSTFLAG         * display table for maintenance
          ENDIF
.
          CALL      CPTAB000
          GOTO      OTCP9999
.
OTCP1400  CALL      NEXTCP00
          GOTO      OTCP9999
.
OTCP1500  CALL      PREVCP00
          GOTO      OTCP9999
.
OTCP1600  MATCH     SP70,OTCPICDC
          IF        !@EQUAL
            PACK      KEY9,OTCPICDC,SP70
            CALL      RDPT10O1
            IF        OVRCD=1
              MOVE    SP70,ODESC
            ENDIF
            WRITE     HTMLFILE;ODESC;
          ELSE
            MOVE      SP70,ODESC
            WRITE     HTMLFILE;ODESC;
          ENDIF
          GOTO      OTCP9999
.
OTCP1700  UNPACK    DIM127,D1,KEY1,DIM127    * Clinic Type
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,OTCP1710,OTCP1720
          PACK      KEY12,WBSESIT,CLINTYPE,SP70
          CALL      RDCTYA1                  * Read Clinic Type File
          BRANCH    OVRCD,OTCP9999
          WRITE     HTMLFILE;OCTDESC;        * Description
          GOTO      OTCP9999
.
OTCP1710  WRITE     HTMLFILE;CLINTYPE;       * Code
          GOTO      OTCP9999
.
OTCP1720  CALL      OTPCTY00                 * clinic type selection list
          GOTO      OTCP9999
.
OTCP1800  PACK      MAPTBIND,OTCPSITE,OTCPCTYP,OTCPPROC,SP70
          CALL      VMTTAB00           * Clinical Coding System Mappings Table
          GOTO      OTCP9999
.
OTCP9999  RETURN
.
.------------------------------------------------------------
. Clinic Type List
.------------------------------------------------------------
OTPCTY00  PACK      KEY12,WBSESIT,SP70
          CALL      RDSCTYA1
OTPCTY10  CALL      RDKCTYA1
          BRANCH    OVRCD,OTPCTY99
.
          MATCH     OCTSITE,WBSESIT
          GOTO      OTPCTY10 IF NOT EQUAL
.
          MATCH     CLINTYPE,OCTCTYP
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=#"",OCTCTYP,"#" selected>":
                               OCTDESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=#"",OCTCTYP,"#">":
                               OCTDESC,"</option>"
          ENDIF
          GOTO      OTPCTY10
.
OTPCTY99  RETURN
.------------------------------------------------------------
. Table of Outpatients Clinic Type Procedure
.------------------------------------------------------------
CPTAB000  IF        CLSTFLAG=1
            WRITE     HTMLFILE;"<tr align=center>":
                               "<td class=HeadingCell colspan=2><b>":
                               CLINTYPE,"</td>":
                               "</tr>":
                               "<tr>":
                               "<td class=HeadingCell><b>Procedure</b></td>":
                               "<td class=HeadingCell><b>Description</b></td>":
                               "</tr>"
          ENDIF
.
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      "10",NORECORD       * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY21,WBSESIT,CLINTYPE,STARTKEY,SP70
          CALL      RSOTCPC1
          CALL      RPOTCPC1
.
CPTAB100  CALL      RKOTCPC1
          BRANCH    OVRCD,CPTAB999
.
          MATCH     WBSESIT,OTCPSITE
          GOTO      CPTAB999 IF NOT EQUAL
.
          MATCH     CLINTYPE,OTCPCTYP
          GOTO      CPTAB999 IF NOT EQUAL
.
          IF        NORECORD<>99
            ADD       ONE,RECNUMB
            IF        STARTNUM>=RECNUMB
              GOTO     CPTAB100
            ENDIF
          ENDIF
.
          CALL      CPROW000                * Display CPC Row
.
          COMPARE   NINTY9,NORECORD         * Display all records
          GOTO      CPTAB100 IF EQUAL
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      CPTAB100 IF NOT EQUAL
.
CPTAB999  RETURN
.
.------------------------------------------------------------
.  Outpatients Clinic Type Procedure (TABLE ROW)
.------------------------------------------------------------
CPROW000  BRANCH  CLSTFLAG,CPROW100
.
          MATCH   "1",OTCPACTV
          IF      @EQUAL
             MOVE    "ACTIVE  ",ACTIDESC
          ELSE
             MOVE    "INACTIVE",ACTIDESC
          ENDIF
.
          MATCH   SP70,OTCPICDC
          IF      @EQUAL
             MOVE    "&nbsp",OTCPICDC
          ENDIF
.
          MATCH   SP70,ACTIDESC
          IF      @EQUAL
             MOVE    "&nbsp",ACTIDESC
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail11":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&clintype=",CLINTYPE:
                             "&otcpc003=",OTCPPROC,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             OTCPPROC,"</td>":
                             "<td>",OTCPDESC,"</td>":
                             "<td align=center>",OTCPICDC,"</td>":
                             "<td align=center>",ACTIDESC,"</td>":
                             "</tr>"
          GOTO      CPROW999
.
CPROW100  MATCH   "1",OTCPACTV
          GOTO    CPROW999 IF NOT EQUAL
.
          WRITE     HTMLFILE;"<tr valign=top>":
                             "<td><a href=#"":
                    "javascript:updateParent('",OTCPPROC,"','",OTCPDESC,"')#">":
                             OTCPPROC,"</a>":
                             "</td><td>",OTCPDESC,"</td>";
.
CPROW999  RETURN
.
.-----------------------------------------------------------
. Link to Next Group
.-----------------------------------------------------------
NEXTCP00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,D3,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",CLINTYPE
          REP       " +",OTCPPROC
          REP       " +",STARTKEY
.
          WRITE     HTMLFILE;"outweb05.pbl":
                                 "?reportno=",F2:
                                 "&template=",D3:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&clintype=",CLINTYPE:
                                 "&otcpc003=",OTCPPROC:
                                 "&startkey=",STARTKEY;
.
          REP       "+ ",CLINTYPE
          REP       "+ ",OTCPPROC
          REP       "+ ",STARTKEY
.
NEXTCP99  RETURN
.
.-------------------------------------------------------------
. Link to Prev Group
.-------------------------------------------------------------
PREVCP00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",OTCPPROC
          REP       " +",CLINTYPE
          REP       " +",STARTKEY
.
          WRITE     HTMLFILE;"outweb05.pbl":
                                "?reportno=",F2:
                                "&template=",FLDPARA:
                                "&startnum=",KEY3:
                                "&norecord=",KEY2:
                                "&clintype=",CLINTYPE:
                                "&otcpc003=",OTCPPROC:
                                "&startkey=",STARTKEY;
.
          REP       "+ ",OTCPPROC
          REP       "+ ",CLINTYPE
          REP       "+ ",STARTKEY
.
PREVCP99  RETURN
.
.------------------------------------------------------------
.   Set Parameters for OUtpatients Clinic Type Procedure
.------------------------------------------------------------
SPOTCP00  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPOTCP01,SPOTCP02,SPOTCP03,SPOTCP04,SPOTCP05:
                       SPOTCP06,SPOTCP07,SPOTCP08,SPOTCP09,SPOTCP10:
                       SPOTCP11,SPOTCP12
.
          MOVE      ONE,EXIT
          GOTO      SPOTCP99
.
SPOTCP01  PACK      OTCPC001,QRYDATA,SP70
          GOTO      SPOTCP99
.
SPOTCP02  PACK      OTCPC002,QRYDATA,SP70
          GOTO      SPOTCP99
.
SPOTCP03  PACK      OTCPC003,QRYDATA,SP70
          GOTO      SPOTCP99
.
SPOTCP04  PACK      OTCPC004,QRYDATA,SP70
          GOTO      SPOTCP99
.
SPOTCP05  PACK      OTCPC005,QRYDATA,SP70
          GOTO      SPOTCP99
.
SPOTCP06  PACK      OTCPC006,QRYDATA,SP70
          GOTO      SPOTCP99
.
SPOTCP07  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      OTCPC007,CCENT,CYEAR,CMON,CDAY
          GOTO      SPOTCP99
.
SPOTCP08  PACK      OTCPC008,QRYDATA,SP70
          GOTO      SPOTCP99
.
SPOTCP09  PACK      OTCPC009,QRYDATA,SP70
          GOTO      SPOTCP99
.
SPOTCP10  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      OTCPC010,CCENT,CYEAR,CMON,CDAY
          GOTO      SPOTCP99
.
SPOTCP11  PACK      OTCPC011,QRYDATA,SP70
          GOTO      SPOTCP99
.
SPOTCP12  PACK      OTCPC012,QRYDATA,SP70
          GOTO      SPOTCP99
.
SPOTCP99  RETURN
.------------------------------------------------------------
.  Update outpatients clinic type procedure table
.------------------------------------------------------------
MVOTCP00  MOVE      WBSESIT,OTCPSITE
.>>>>     MATCH     OTCPC001,Z70
.>>>>     IF        !@EQUAL
.>>>>       MOVE      OTCPC001,OTCPSITE
.>>>>     ENDIF
.
          MATCH     OTCPC002,Z70
          IF        !@EQUAL
            MOVE      OTCPC002,OTCPCTYP
          ENDIF
.
          MATCH     OTCPC003,Z70
          IF        !@EQUAL
            MOVE      OTCPC003,OTCPPROC
          ENDIF
.
          MATCH     OTCPC004,Z70
          IF        !@EQUAL
            MOVE      OTCPC004,OTCPDESC
          ENDIF
.
          MATCH     OTCPC005,Z70
          IF        !@EQUAL
            MOVE      OTCPC005,OTCPICDC
          ENDIF
.
          MATCH     OTCPC006,Z70
          IF        !@EQUAL
            MOVE      OTCPC006,OTCPACTV
          ELSE
            MOVE      ZERO,OTCPACTV
          ENDIF
.
          MATCH     OTCPC007,Z70
          IF        !@EQUAL
            MOVE      OTCPC007,OTCPCDAT
          ENDIF
.
          MATCH     OTCPC008,Z70
          IF        !@EQUAL
            MOVE      OTCPC008,OTCPCTIM
          ENDIF
.
          MATCH     OTCPC009,Z70
          IF        !@EQUAL
            MOVE      OTCPC009,OTCPCUID
          ENDIF
.
          MATCH     OTCPC010,Z70
          IF        !@EQUAL
            MOVE      OTCPC010,OTCPUDAT
          ENDIF
.
          MATCH     OTCPC011,Z70
          IF        !@EQUAL
            MOVE      OTCPC011,OTCPUTIM
          ENDIF
.
          MATCH     OTCPC012,Z70
          IF        !@EQUAL
            MOVE      OTCPC012,OTCPUUID
          ENDIF
.
MVOTCP99  RETURN
