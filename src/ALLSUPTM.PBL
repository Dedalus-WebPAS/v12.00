. *----------------------------------------------------------------------
. * Include Name  :   ALLSUPTM.PBL
. *
. * Function      :   CGI variables for ALLSUPTM - allsupaf.dat
. *
. * Includes Needed : 
. *                   
. * Modifications   :
. *       V10.04.01 25/03/2014  Patrick Adair CAR 226788
. *                 Added ALSUP007
. *       V10.00.01 25/03/2010  Ebon Clements CAR 208232
. *                 Added ALSUP005 and ALSUP006
. *       V9.11.01  16/10/2008  Peter Vela    CAR 179155
. *                 Fixed redirect in NEXTSP00 and PREVSP00
. *       V9.10.01  24/07/2008  Mike Laming   CAR 174378
. *                 Add tag for INCOCATE (Supply Catalog Code format)
. *                   V9.08.01 10/04/2007 Jill Habasque  
. *                            Created include
. *----------------------------------------------------------------------
.
.------------------------------------------------------------------------
.   Clear Parameters for Allied Health SUP Table
.------------------------------------------------------------------------
.
CPALSP00  MOVE      Z70,ALSUP001
          MOVE      Z70,ALSUP002
          MOVE      Z70,ALSUP003
          MOVE      Z70,ALSUP004
          MOVE      Z70,ALSUP005
          MOVE      Z70,ALSUP006
          MOVE      Z70,ALSUP007
.
CPALSP99  RETURN
.
.------------------------------------------------------------
. Add fields for allied health SUP Table
.------------------------------------------------------------
ALSP0000  BRANCH    FLDITMNO,ALSP0100,ALSP0200,ALSP0300,ALSP0400,ALSP0500:
                             ALSP0600,ALSP0700,ALSP0800,ALSP0900,ALSP1000:
                             ALSP1100,ALSP1200,ALSP1300,ALSP1400,ALSP1500:
                             ALSP1600,ALSP1700
.
          WRITE     HTMLFILE;"Invalid IBA Tag - ALSP0000";
          GOTO      ALSP9999
.
.
ALSP0100  WRITE     HTMLFILE;ALSPVISN;
          GOTO      ALSP9999
.
ALSP0200  WRITE     HTMLFILE;ALSPENCT;
          GOTO      ALSP9999
.
ALSP0300  WRITE     HTMLFILE;ALSPSCNT;
          GOTO      ALSP9999
.
ALSP0400  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      KEY1,F1
          BRANCH    F1,ALSP0410
.
          WRITE     HTMLFILE;ALSPSCOD;
          GOTO      ALSP9999
.
ALSP0410  PACK      KEY7,ALSPSCOD,SP70
          CALL      RDSIIA1
          BRANCH    OVRCD,ALSP9999
.
          WRITE     HTMLFILE;SIIADES;
          GOTO      ALSP9999
.
ALSP0500  MATCH     ALSPCDAT,SP70
          GOTO      ALSP9999 IF EQUAL
          UNPACK    ALSPCDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      ALSP9999
.
ALSP0600  WRITE     HTMLFILE;ALSPCTIM; 
          GOTO      ALSP9999
.
ALSP0700  MATCH     SP70,ALSPCUID
          GOTO      ALSP9999 IF EQUAL
          PACK      KEY10,ALSPCUID
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      ALSPCUID,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM;
          GOTO      ALSP9999
.
ALSP0800  MATCH     ALSPUDAT,SP70
          GOTO      ALSP9999 IF EQUAL
          UNPACK    ALSPUDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      ALSP9999
.
ALSP0900  WRITE     HTMLFILE;ALSPUTIM;
          GOTO      ALSP9999
.
ALSP1000  MATCH     SP70,ALSPUUID
          GOTO      ALSP9999 IF EQUAL
          PACK      KEY10,ALSPUUID
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      ALSPUUID,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM;
          GOTO      ALSP9999
.
ALSP1100  CALL      SPTAB000
          GOTO      ALSP9999
.
ALSP1200  CALL      NEXTSP00
          GOTO      ALSP9999
.
ALSP1300  CALL      PREVSP00
          GOTO      ALSP9999
.
ALSP1400  WRITE     HTMLFILE;INCOCATE;      * Catalog Code format
          GOTO      ALSP9999
.
ALSP1500  WRITE     HTMLFILE;ALSPQNTY;
          GOTO      ALSP9999
.
ALSP1600  WRITE     HTMLFILE;ALSPPRCE;
          GOTO      ALSP9999
.
ALSP1700  WRITE     HTMLFILE;ALSPAPPN;
          GOTO      ALSP9999
.
ALSP9999  RETURN
.
.------------------------------------------------------------
. Table of Allied Health Supplies Details
.------------------------------------------------------------
SPTAB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      "10",NORECORD       * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
          CALL      SPHED000                * Display SUP heading
.
          UNPACK    ENCOUTKY,ADMISSNO,ALSPENCT
.
          PACK      KEY24,ENCOUTKY,STARTKEY,SP70
          CALL      RSALSUP1
          CALL      RPALSUP1
SPTAB100  CALL      RKALSUP1
          BRANCH    OVRCD,SPTAB999
.
          MATCH     ADMISSNO,ALSPVISN
          GOTO      SPTAB999 IF NOT EQUAL
.
          MATCH     ALENENCT,ALSPENCT
          GOTO      SPTAB999 IF NOT EQUAL
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO     SPTAB100
          ENDIF
.
          CALL      SPROW000                * Display SUP Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      SPTAB100 IF NOT EQUAL
.
SPTAB999  RETURN
.
.******************************************************************************
.*                      Display Of Header(Intervention List)                  *
.******************************************************************************
SPHED000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=12%>":
                               "<b>Supply Code</b></td>":
                               "<td class=HeadingCell width=18%>":
                               "<b>Description</b></td>":
                             "</tr>"
.
SPHED999  RETURN
.
.------------------------------------------------------------
.  Allied Health Supplies Details (TABLE ROW)
.------------------------------------------------------------
SPROW000   PACK      KEY7,ALSPSCOD,SP70
           CALL      RDSIIA1
           IF        OVRCD=1
             PACK      SIIADES,SP70
           ENDIF
.
           REP       " +",ENCOUTKY
           REP       " +",ALSPVISN
           REP       " +",ALSPENCT
           REP       " +",ALSPSCNT 
           WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&encoutky=",ENCOUTKY:
                             "&alsup001=",ALSPVISN:
                             "&alsup002=",ALSPENCT:
                             "&alsup003=",ALSPSCNT,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             ALSPSCOD,"</td>":
                             "<td>",SIIADES,"&nbsp;</td>":
                             "</tr>"
           REP       "+ ",ENCOUTKY
           REP       "+ ",ALSPVISN
           REP       "+ ",ALSPENCT
           REP       "+ ",ALSPSCNT 
.
SPROW999  RETURN
.
.-----------------------------------------------------------
. Link to Next Group
.-----------------------------------------------------------
NEXTSP00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,D3,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",ENCOUTKY
          REP       " +",ALSPSCNT
.
          WRITE     HTMLFILE;"allweb03.pbl":
                                 "?reportno=",F2:
                                 "&template=",D3:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&encoutky=",ENCOUTKY:
                                 "&alsup003=",ALSPSCNT;
.
          REP       "+ ",ENCOUTKY
          REP       "+ ",ALSPSCNT
.
NEXTSP99  RETURN
.
.-------------------------------------------------------------
. Link to Prev Group
.-------------------------------------------------------------
PREVSP00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",ENCOUTKY
          REP       " +",ALSPSCNT
.
          WRITE     HTMLFILE;"allweb03.pbl":
                                "?reportno=",F2:
                                "&template=",FLDPARA:
                                "&startnum=",KEY3:
                                "&norecord=",KEY2:
                                "&encoutky=",ENCOUTKY:
                                "&alsup003=",ALSPSCNT;
.
          REP       "+ ",ENCOUTKY
          REP       "+ ",ALSPSCNT
.
PREVSP99  RETURN
.
.------------------------------------------------------------
.   Set Parameters for allied health SUP table
.------------------------------------------------------------
SPALSP00  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPALSP01,SPALSP02,SPALSP03,SPALSP04,SPALSP05:
                       SPALSP06,SPALSP07
.
          MOVE      ONE,EXIT
          GOTO      SPALSP99
.
SPALSP01  MOVE      QRYDATA,ALSUP001
          GOTO      SPALSP99
.
SPALSP02  MOVE      QRYDATA,ALSUP002
          GOTO      SPALSP99
.
SPALSP03  MOVE      QRYDATA,ALSUP003
          GOTO      SPALSP99
.
SPALSP04  MOVE      QRYDATA,ALSUP004
          GOTO      SPALSP99
.
SPALSP05  MOVE      QRYDATA,ALSUP005
          GOTO      SPALSP99
.
SPALSP06  MOVE      QRYDATA,ALSUP006
          GOTO      SPALSP99
.
SPALSP07  MOVE      QRYDATA,ALSUP007
          GOTO      SPALSP99
.
SPALSP99  RETURN
.------------------------------------------------------------
.  Update allied health SUP table
.------------------------------------------------------------
MVALSP00  MATCH     ALSUP001,Z70
          IF        !@EQUAL
            MOVE      ALSUP001,ALSPVISN
          ENDIF
.
          MATCH     ALSUP002,Z70
          IF        !@EQUAL
            MOVE      ALSUP002,ALSPENCT
          ENDIF
.
          MATCH     ALSUP003,Z70
          IF        !@EQUAL
            MOVE      ALSUP003,ALSPSCNT
          ENDIF
.
          MATCH     ALSUP004,Z70
          IF        !@EQUAL
            MOVE      ALSUP004,ALSPSCOD
          ENDIF
.
          MATCH     ALSUP005,Z70
          IF        !@EQUAL
            MOVE      ALSUP005,ALSPQNTY
          ENDIF
.
          MATCH     ALSUP006,Z70
          IF        !@EQUAL
            MOVE      ALSUP006,ALSPPRCE
          ENDIF
.
          MATCH     ALSUP007,Z70
          IF        !@EQUAL
            MOVE      ALSUP007,ALSPAPPN
          ENDIF
.
MVALSP99  RETURN
