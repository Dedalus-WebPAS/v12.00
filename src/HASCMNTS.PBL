.************************************************************************
.*  HASCMNTS  :  Determine if there are admission or U/R comments       *
.*               Returns : OVRCD=1 No admission or U/R comments         *
.************************************************************************
. MODIFICATIONS: 
.
.----------------------------------------------------------------------
. V10.11.01 25/08/2017  Thanh T.       TSK 0821710
.           Audit admission/outpatient/UR comments.
.----------------------------------------------------------------------
.
          DEFPROC   HASCMNTS
.
          INC       PMSUCHFD/INC
.
HASCMNTS  MOVE      ONE,OVRCD               * set flag for no comments
.
.         See if there are any admission based comments on file
.         Retrieve the latest active admission comment header
.
HASCM100  PACK      KEY17,AADMNO,VSMDTYPE,Z70   * Key for start of admission comment
          CALL      RSVSMDT1
HASCM110  CALL      RPVSMDT1
          BRANCH    OVRCD,HASCM500
.
          MATCH     KEY8,VSMDVISN
          GOTO      HASCM500 IF NOT EQUAL
.
          MATCH     VSCMTTYP,VSMDTYPE
          GOTO      HASCM500 IF NOT EQUAL
.
          MATCH     "1",VSMDDELT             * is comment deleted
          GOTO      HASCM110 IF EQUAL
.
          GOTO      HASCM950                * yes - comments found so finish
.
.         There were no admission based comments so
.         see if there are any U/R based comments on file
.         Retrieve the latest active admission comment header
.
HASCM500  MOVE      ONE,OVRCD
          COMPARE   ONE,PTCNURCO            * using U/R based comments ?
          GOTO      HASCM950 IF NOT EQUAL   * no - finish
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*248,PTCNSOUC
          OPEN      PMSUCHA1,"pmsuchaf"
.
          PACK      KEY17,PURNO,URCMTTYP,Z70   * Key for start of UR comment
          CALL      RSPMUCH1
HASCM510  CALL      RPPMUCH1
          BRANCH    OVRCD,HASCM950
.
          MATCH     PURNO,PMUHURNO
          GOTO      HASCM950 IF NOT EQUAL
.
          MATCH     URCMTTYP,PMUHCTYP
          GOTO      HASCM950 IF NOT EQUAL
.
          MATCH     "1",PMUHDELT             * is comment deleted
          GOTO      HASCM510 IF EQUAL
.
.         Active comment header found
.         If there is security on the U/R comments, check that the user
.         has access to comments
.
          IF        PTCNSOUC = ONE
            MATCH     SP70,PMUHCUID            * is there creation user id
            IF        !@EQUAL
              MATCH     USERID,PMUHCUID        * same user ?
              GOTO      HASCM510 IF NOT EQUAL  * no
            ENDIF
          ENDIF
.
HASCM900  CLOSE     PMSUCHA1 
HASCM950  GOTO      HASCM999
.
          INC       PMSUCHIO/INC
          INC       IBAOVRIO/INC
.
HASCM999  ENDPROC
