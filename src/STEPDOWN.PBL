.
.STEPDOWN.PBL
.------------
.
.      Re-written on 01/11/88 by Graeme Williams to take advantage of TRBEND
.
.         V12.00.01 19/05/2025  J.Tan          TSK 0955096
.                   Added alpanumeric visit number generation
.         V10.04.01 23/05/2013  J.Tan          CAR 300784
.                   Fixed force stepdown prior Invoice Fromdate for Interim Inv.
.         V10.03.08 11/10/2013  J.Tan             CAR 251004
.                   Fixed checking Disc.date/time before writting transfer rec.
.         V10.03.07 01/09/2013  J.Tan          CAR 291588
.                   Fixed TEMPDAY for patient returns from leave
.         V10.03.05 06/05/2013  J.Tan          CAR 284902
.                   Mods to ignore force stepdown done prior Invoice FromDate
.         V10.03.04 12/09/2012  J.Tan          CAR 267784
.                   Mods to check TCINDC19 Claim for Contract Eff.'Disc.From'
.         V10.03.03 26/06/2012 J.Tan       CAR 267037
.                   Mods checking for blank Contract ID end date
.         V10.03.02 14/06/2012 J.Tan       CAR 267037
.                   Fixed Bed fees accommodation for 'As at Date' Contract
.         V10.03.01 22/11/2011 J.Tan       CAR 234262
.                   Added one day to TEMPDAY on return from leave (RETTYP)
.         V10.02.01 25/05/2011 J.Tan       CAR 243446
.                   Fixed generating bed fee with no effective date
.         V10.01.02 31/03/2011 J.Tan       CAR 233195
.                   Checking Contract Expired date against new stepdown date
.         V10.01.01 30/11/2010  Peter Vela CAR 233148
.                   Initialised pattranf variables before write to pattranf
.                   17/01/2011 J.Tan       CAR 233034
.                   Mods Bed fees to use Health fund table type
.         V10.00.01 14/07/2010  J.Tan      CAR 226158
.                   Fixed stepdown for bed fee update (old bed fee)
.         V9.12.05  11/03/2010  J.Tan      CAR 216044
.                   Mods to include Leave days in stepdown count for TAC
.         V9.12.04  25/10/2009  J.Tan      CAR 214548
.                   Fixed automatic STEPDOWN after bed fees update
.         V9.12.03  22/10/2009  J.Tan      CAR 207359
.                   Fixed for number of days (TEMPDAY) greater than 999
.         V9.12.02  21/09/2008  J.Tan      CAR 204390
.                   Mods to old bed fees file
.         V9.12.01  06/08/2008  J.Tan      CAR 202869
.                   Mods to Bed Fees update transfer record (PTTREPSD=2) to
.                   use rates from the transfer instead generate Stepdown
.         V9.11.01  16/10/2008  J.Tan      CAR 181090
.                   Fixed overnight perdiem to include days hospitalisation
.         V9.10.03  22/09/2008  J.Tan      CAR 179009
.                   Fixed 'Exclude days Hosp.' parameter for Daycase only
.         V9.10.02  15/07/2008  J.Tan      CAR 163654
.                   Mods to use disc.date for SX bed fee effective date
.         V9.10.01  18/04/2008  J.Tan      CAR 164355
.                   Mods not to re-open bed fees file for IBARSH10
.         V9.09.02  03/01/2008  J.Tan            CAR 136785
.                   Added parameter to exclude days hospitalisation for Daycase
.         V9.09.01  21/06/2007  J.Tan            CAR 138887
.                   Mods for global Update bed fee
.         V9.08.02  20/03/2007  J.Tan     CAR 120014
.                   Fixed checking for eff.date of NZ Bed fees
.         V9.08.01  08/02/07 J.Tan    CAR 120001
.                   Mods for NZ Billing
.         V9.07.02  26/09/2006  J.Tan            CAR 113167
.                   Mods for IBABIL83 - not to reset frdate on force stepdown
.         V9.07.01  19/07/2005  J.Tan            CAR 113222
.                   Mods to save TDEPT
.         V9.06.01  05/07/2005  J.Tan            CAR 93862
.                   Fixed problem with force stepdown done on same date
.         V9.05.B01 03/02/2005  J.Tan            CAR 93862
.                   Fixed problem with force stepdown done on same date as
.                   the existing transfer record
.         V9.04.03  07/11/2005  J.Tan            CAR 62083
.                   Fixed checking Days hospitalisations for Daycase rate
.         V9.04.02  26/10/2004  Mike Laming  CAR 48340
.                   Add save Start date (FRDATE) for manual stepdowns at CHKB30
.         V9.04.01  08/09/2004 J.Tan             
.                   Fixed problem with looping when bed fees not setup
.         V9.03.01  24/05/2004 J.Tan  CAR 49644
.                   Mods not to display errors on screen 
.                   07/06/2004 J.Tan  CAR 50510
.                   I*C on patbfeef file
.         V9.02.06  04/09/2002 J.Tan  
.                   Mods to save skey30 for patcatbi
.                   Mods specialty bed type - set up last date bedtype changed
.         V9.02.05  16/08/2002  Mike Laming  SRF 17570
.                   Copy changes for V6.10 Bug fixes to V9.02
.         V9.02.04  08/08/2002 J.Tan  
.                   Mods for specialty bed type
.         V9.02.03  02/08/2002 J.Tan  
.                   Remove checking ptmicmxp field when checking pttrepsd
.         V9.02.02  01/07/2002  J.Tan
.                   Mods to use default claim code for charging from sys.param.
.         V9.02.01  26/06/2002  J.Tan
.                   Mods to set trbend after getbfe
.         V6.09.07  18/05/2002  Dean Cramer 
.                   Initialise SAVHF and SAVPAT at start
.                   Fix screen display erasing other display                  
.         V6.09.06  22/02/2002  J.Tan      
.                   Mods rates from trans record to overwrite manual stepdown 
.         V6.09.05  21/02/2002  J.Tan      
.                   Put back the change and fixed problems for Epworth
.         V6.09.04  08/02/2002  Greg Horvat
.                   Commented out Jeni's change below as it caused calculation
.                   errors
.         V6.09.03  02/02/2002  J.Tan      
.                   Mods to recalculate rate for admission record of trans 
.         V6.09.02  24/01/2002  J.Tan      
.                   Mods to save last ending day of stepdown
.         V6.09.B01 12/02/2001  Greg Horvat
.                   Replaced the call to CALC with DAYSEP
.         V6.06.01  30/09/1999  Steve Downing
.                   Mods to beginning of period calculations
.         V6.05.01  14/10/1998  Jill Habasque
.                   Changed Julian date calculation to use century
.         V6.04.01  14/03/1996  Greg Horvat
.                   Recompiled for PATTRNFD & PATIOTRN
.         V6.03.06  29/10/1996  Greg Horvat      Holy Shit Mods
.                   Recompiled for PATTRNFD & PATIOTRN
.         V6.03.05  02/07/1996  Greg Horvat      PRS2 96 Changes
.                   Recompiled for PATTRNFD & PATIOTRN
.         V6.03.04  22/05/96 J.Tan   SRF 615301
.                   If the new stepdown date is before the previous transfer
.                   date, then just use the existing transfer rate.
.         V6.03.03  13/05/96 J.Tan   SRF 615219
.                   Mods to check if new date is before the previous accom. date
.                   regardless the reset day count parameter.
.         V6.03.02  27/10/1995  Greg Horvat      SRF 612064 612205 612206
.                   Changed to use from 4's when doing calculations & changed
.                   to handle patients who have been in hospital for move than
.                   1000 days
.         V6.03.01  20/10/95 J.Tan     SRF 611787
.                   Mods to set up the last invoiced date
.
.      V5.01  02/05/89 J.Tan                 SRF 58
.             Fix to Allow discount and allowance to follow through.
.      V5.02  17/05/89 J.Tan                 SRF 100582
.             Fix day case problem.
.      V5.03  31/05/89 J.Tan                 SRF 100850
.             Fix overnight stay patient being charged daycase rate.
.      V5.04  26/06/89 J.Tan                 SRF 101242
.             Fixed to use the existing Fees (from TRANS file) instead of
.             generating a new bed fee.
.      V5.05  11/07/89 J.Tan                 SRF 101242
.             Fixed to use the no. of days of the existing bed fee.
.      V5.06  05/09/89 J.Tan                 SRF 102172
.             Fixed fees if no of hosp.is changed after admitted.
.      V5.07  18/10/89 J.Tan
.             Fixed bed fee valid for one day only
.      V5.08  23/02/90 J.Tan                 SRF 104060
.             Mods to display admission number of no bed fee available
.      V5.09  12/06/90 J.Tan                 SRF 105193
.             Check for the stepdown discount and allowance can't be greater
.             than the patient portion
.      V5.10  30/07/90 i. chung              SRF 105195
.             Added system parameter check for Rate Change Audit (PATRCAUD)
.      V5.11  17/09/90 J.Tan              SRF 106164
.             Mods to have operator name in rate audit
.      V5.12  09/10/90 J.Tan                 SRF 106608
.             Added a new flag not to open bed fee file, if it's called from
.             bed fee maintenance program
.             OPNFLAG = 0 - to open bed fee file
.                     = 1 - not to open bed fee file
.      V5.13  03/06/91 J.Tan       SRF 108733
.             Added an indicator to reset day count
.      V5.14  08/08/91 J.Tan       SRF 109947
.             Fixed problems with the stepdown date same as transfer date
.      V5.15  20/08/91 J.Tan       SRF 110192, 110180
.             Fixed problems to recognise transfer records with same dates
.      V5.16  22/08/91 J.Tan
.             Fixed stepdown discharged pats caused by the above changes
.      V5.17  30/08/91 J.Tan       SRF 110356
.             Fixed using the rate from trans file to overwrite stepdown
.      V5.18  02/09/91 J.Tan
.             Check for change of admission type for same date of trans rec.
.      V5.20  02/06/92 J.Tan   SRF 113652
.             Added CURBEND and COLNUM
.      V6.00  22/05/91 Steve Armstrong
.             Health Fund Table increased to 8 chars.
.      V6.01  06/05/92 Graeme Williams                  SRF 113588
.             Added system parameter PTCNCNDY to determine if the day count
.             should be reset when the admission type changes
.   V6.00.02  09/07/93 Gabrielle    SRF 117233
.             Recompiled for PATTRNFD / PATIOTRN
.         V6.02.01  07/10/94 J.Tan    SRF 132596
.                   Mods not to generate stepdown bed fee after invoiced
.         V6.03.B00 08/02/1995  Greg Horvat      SRF 134455
.                   Changed New Bed Fee Not Available error message to display
.                   for 1 second instead of 3 seconds
.                   
.      This routine is to run a stepdown of a new bed fee 
.
.
STEPDOWN CMATCH         "A",TMOVE
         GOTO           INITVAR IF EQUAL
.
         CMATCH         "R",TMOVE
         GOTO           RETTYP IF EQUAL
.
         CMATCH         "D",TMOVE
         GOTO           STEP10 IF NOT EQUAL
.
         IF             SPTTREPS=2
           MOVE           SAVDATE,FRDATE
           MOVE           SPTTREPS,PTTREPSD
         ENDIF
.
         MOVE           TDATE,SAVDATE
         MOVE           TTIME,SAVTIME
         GOTO           STEP20
.
.        Manual stepdown is identified from the field pttrepsd = 1
.
STEP10    MATCH         "IBAHMS70",PRGID                               *I-61004
          GOTO          STEP20 IF EQUAL                                *I-61004
.
          BRANCH         PTTREPSD,STEP11,STEP11     * manual stepdown
          GOTO           STEP20           * recalculate rate/ignore tran record
.
.         The following routine is used if the change records on trans file
.         to over write the stepdown
.         Find the no of days up to this point
.
STEP11    
.         BRANCH         PTRESDAY,STEP20          * Reset the day count ?
.
.         If this is the Stepdown from bed fees Update, then just use the rates 
.         from Trans record
.
.         BRANCH         PTTREPSD,STEP20,STEP20
          BRANCH         PTTREPSD,STEP20,STEP12
.
          MATCH          SAVDATE,TDATE
          GOTO           STEP20 IF NOT EQUAL
.
          MATCH          TATYPE,SAVATYP
          GOTO           STEP12 IF EQUAL
.
          BRANCH         PTCNCNDY,STEP12          * don't reset day count
.
.        Change of admission type, reset count
.
          MOVE           ZERO,TEMPDAY
          MOVE           ZERO,DAYNUM
          MOVE           TDATE,FRDATE
.
STEP12    CALL           SAVARB
          MOVE           TRBEND,CURBEND
          IF             PTTREPSD=2
            MOVE           TRBEND,CURMDAYS
            CMATCH         "L",TMOVE
            IF             @EQUAL
              MOVE           ZERO,NBRDAYS
              DAYSEP         FRDATE,TDATE,NBRDAYS
              ADD            NBRDAYS,DAYNUM
            ENDIF
          ENDIF
          GOTO           STEP99
.
STEP20    MOVE           ZERO,NBRDAYS
          DAYSEP         FRDATE,TDATE,NBRDAYS
.
          CALL           CBTY0000            * Check the bed type
          IF             EXIT=1
            MOVE           NPENDDTE,KEY8               * save date for CALDAT
          ELSE
            MOVE           DAYNUM,TEMPDAY
            ADD            NBRDAYS,TEMPDAY
            MOVE           FRDATE,KEY8                * save date for CALDAT
            MOVE           DAYNUM,FORM4B
          ENDIF
.
          IF             TEMPDAY>999
            MOVE           "999",TEMPDAY      * Only 999 days avail. in bed fee
.
          ENDIF
.
.        Check with the end of bed fee period
. 
          IF             CURMDAYS=TEMPDAY | CURMDAYS>TEMPDAY
           IF             EXIT=1
             MOVE          ZERO,PRDAYS
             DAYSEP        SIDATET,TDATE,PRDAYS
             ADD           PRDAYS,BDYARRAY[FORM2]
           ENDIF
           GOTO           CHKWRD
          ENDIF
.
.        COMPARE        TEMPDAY,CURMDAYS
.        GOTO           CHKWRD IF EQUAL
.        GOTO           CHKWRD IF NOT LESS
.
.        Calculate the date of the new bed fee period
.
         CALL           CALDAT
.
         PACK           DIM8,CC,YY,MM,DD
         REP            " 0",DIM8
.
.        BRANCH         PTRESDAY,STEP30          * Reset the day count ?
.
.        Check if the new date is before the previous 'todate' of accom.
.
         MATCH          DIM8,SIDATET
         IF             !@LESS
           CALL           SAVARB
           MOVE           TDATE,NPENDDTE
           GOTO           STEP99
         ENDIF
.
.        If this is a specialty bed, save the bed days for the bed type.
.
STEP30   IF             EXIT=1
            MOVE          ZERO,PRDAYS
            DAYSEP        SIDATET,DIM8,PRDAYS
            ADD           PRDAYS,BDYARRAY[FORM2]
         ENDIF
.
.        Get the begining of a new period
.
         MOVE       CURMDAYS,PRDAYS
         ADD        ONE,PRDAYS
         MOVE       PRDAYS,TEMPDAY
.
         IF         TEMPDAY>999
           MOVE       "999",TEMPDAY         * Only 999 days avail. in bed fee
         ENDIF
.
.        Check if this a  discharged record
.
         CMATCH     "D",TMOVE
         GOTO       STEP50 IF EQUAL
.
         MATCH      SAVDATE,DIM8
         GOTO       CHKWRD IF LESS
.
.        Check if the new stepdown date is after the Contract id Expired
.
         MATCH      SP70,CMENDDTE
         GOTO       STEP52 IF EQUAL
         MATCH      DIM8,CMENDDTE
         IF         @LESS
           CALL       SAVARB
           MOVE       DIM8,DATFIELD
           ADD        ONE,DATFIELD
           MOVE       DATFIELD,TDATE 
           MOVE       SAVHF,TRATEH
           MOVE       SAVPAT,TRATEF
           GOTO       STEP99
         ENDIF
         GOTO       STEP52
.
.
.        Change date can not be greater than discharged date
.
STEP50   MATCH          DIM8,SAVDATE
         GOTO           STEP99 IF LESS
.
STEP52   PACK           TDATE WITH CC,YY,MM,DD
         REP            " 0",TDATE
.
         MOVE           TDATE,CEFFDATE
         CALL           GETBFE
         COMPARE        THREE,FORM2
         GOTO           STEP53 IF NOT EQUAL
.
         MATCH          SP70,SAVCONTR
         GOTO           STEP52A IF EQUAL
         MATCH          SP70,CMENDDTE
         GOTO           STEP52A IF EQUAL
.
         MATCH          TDATE,CMENDDTE
         IF             @LESS
           MOVE           CMENDDTE,TDATE
           MOVE           ZERO,NOFEE
           MOVE           SAVCONTR,CONTRCID
         ENDIF
         GOTO           STEP99
.
STEP52A  MOVE           SAVDATE,TDATE     * no fee found
         GOTO           STEP99
.
STEP53   MOVE           "01:00:00",TTIME
.
         MOVE           CURBEND,TRBEND
         MOVE           SAVPAT,TRATEF
         MOVE           SAVHF,TRATEH
         MOVE           ZERO,TRATEG
         MOVE           SAVWARD,TWARD
         MOVE           SAVBED,TBED
         MOVE           AADMNO,TADMN
         MOVE           AURNO,TURNO
         MOVE           ZERO,TDISC
         MOVE           ZERO,TALLW
.
.        Check with Discount/Allowance Stepdowns indicator
.
         COMPARE        ONE,CDLSTEP
         GOTO           STEP56 IF NOT EQUAL
.
.        Check if the discount + allowance are greater than the patient portion
.
         MOVE           SAVDISC,FORM62
         ADD            SAVALLW,FORM62
.
         COMPARE        FORM62,TRATEF
         GOTO           STEP54 IF NOT LESS
.
.        Use the patient portion as their maximum discount
.
         MOVE           TRATEF,TDISC
         MOVE           ZERO,TALLW
         GOTO           STEP56
.
.        Carry through the previous discount and allowance
.
STEP54   MOVE           SAVDISC,TDISC
         MOVE           SAVALLW,TALLW
.
STEP56   MOVE           SAVATYP,TATYPE
         MOVE           SAVBTYP,TRBTYP
         MOVE           SAVADOC,TADOCT
         MOVE           CURMDAYS,TRBEND
         MOVE           SAVCHST,TCHSTAT
         MOVE           STDEPT,TDEPT
         MOVE           ZERO,TEXTRA
         MOVE           ZERO,PTTREPSD       * Episode type
         MOVE           ZERO,PTTRAEND       * Ending day for additional rate
         MOVE           ZERO,PTTRADPT       * Additional rate
         MOVE           ZERO,PTTRADRB
         MOVE           ZERO,PTTRLSPT       * No lumpsum
         MOVE           ZERO,PTTRLSRB
.
         MOVE           "C",TMOVE
.
.        Check if this date has already been invoiced
.
         MATCH          IDATEF,TDATE
         GOTO           STEP72 IF LESS
.
         MOVE           TDATE,NPENDDTE
.
.     Write a new change record for the new bed fee only when printing invoice
.
         COMPARE        THREE,PROGFLAG
         GOTO           STEP70 IF NOT EQUAL
.
          IF        ASTAT=3
            MATCH     TDATE,DDATE
            IF        @EQUAL
              MATCH     DTIME,TTIME
              GOTO      STEP70 IF NOT LESS
            ELSE
              MATCH     TDATE,DDATE
              GOTO      STEP70 IF LESS
            ENDIF
          ENDIF
.
          PACK      KEY30 WITH AADMNO,TDATE,TTIME,TWARD,TBED
          CALL      RDATRAN2
          COMPARE   ZERO,OVRCD
          GOTO      STEP70 IF EQUAL
.
          MOVE      KEY30,SKEY30
          MOVE      SP3,PTTRLTYP
          MOVE      ZERO,PTTREPSD        * episode type
          LOAD      PTTREPSD,WRMNSTDN,ONE   * Set to maunal stepdown   *I-90205
.
          MOVE      ZERO,PTTRLSPT        * lumpsum
          MOVE      ZERO,PTTRLSRB
          MOVE      ZERO,PTTRAEND        * ending day
          MOVE      ZERO,PTTRADRB        * additional rate
          MOVE      ZERO,PTTRADPT
          MOVE      SP1,PTTRAUAT
.         MOVE      SP5,PTTRLTSC
          RESET     PRGID
          MOVE      ACLAIM,PTTRCLTY
          PACK      PTTRSPAR,SP30,SP30
          PACK      PTTRCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTTRCDAT
          CLOCK     TIME,PTTRCTIM
          MOVE      WBSEUID,PTTRUCID
          UNPACK    SP70,TRCDATE,TRCTIME,PTTRUUID
.
          CALL      WRTRAN2
.
.         Only write to Rate Change Audit file if system parameter is on (0)
.
        BRANCH         CAUDQ,STEP70             * added for V5.10 - SRF 105195
.
STEP60   OPEN           PATRCAUD,"patrcaud"
        CALL           RDSRCA
.
        CALL           WRIRCA
        MOVE           AADMNO,RADMNO
        MOVE           PGNAME,ANS
        PACK           RNAME WITH ANS,DOT,PSNAME
        MOVE           ACLAIM,RCLAIM
        MOVE           AFUNDH,RFUND
        MOVE           AFNDTB,RHFTAB
        MOVE           TRBTYP,RBTYPE
.
        MOVE           SAVPAT,RNPAT
        MOVE           SAVHF,RNFUND
        MOVE           SAVPAT,RCGPAT
        MOVE           SAVHF,RCGHF
.
        MOVE           TDATE,RDATE
        MOVE           TATYPE,RATYPE
        MOVE           PASSCODE,ROPER       * Operator code
.
        MOVE           CURMDAYS,REDAYS
        SUB            PRDAYS,REDAYS
        ADD            ONE,REDAYS
.
        CALL           WRRCA
        CLOSE          PATRCAUD
.
.        Reposition to the new key
.
STEP70   PACK           KEY30 WITH TADMN,TDATE,TTIME,TWARD,TBED
         CALL           RDSTRAN2
         CALL           SAVARB
         MOVE           TRBEND,CURMDAYS
         MOVE           TRBEND,SAVBEND
         GOTO           STEP99
.
STEP72   CALL           SAVARB
         MOVE           TRBEND,CURMDAYS
         MOVE           TRBEND,SAVBEND
         PACK           KEY30 WITH TADMN,TDATE,TTIME,TWARD,TBED
         CALL           RDSTRAN2
         CALL           RDKTRAN2
         BRANCH         OVRCD,STEP74
.
         MATCH          TADMN,AADMNO
         GOTO           STEP74 IF NOT EQUAL
.
         GOTO           STEP10
.
.        Restore the original transfer record
.
STEP74   MOVE           SAVWARD,TWARD
         MOVE           SAVBED,TBED
         MOVE           SAVATYP,TATYPE
         MOVE           SAVBTYP,TRBTYP
         MOVE           SAVADOC,TADOCT
         MOVE           SAVDATE,TDATE
         MOVE           SAVTIME,TTIME
         MOVE           SAVCHST,TCHSTAT
         MOVE           STDEPT,TDEPT
         MOVE           CURMDAYS,TRBEND
         MOVE           SAVBEND,TRBEND
         MOVE           SAVDISC,TDISC
         MOVE           SAVALLW,TALLW
         CALL           RDSTRAN2
STEP99   RETURN
.
+
.------------------------------------------------------------------------------
.        Initialize the variables
.------------------------------------------------------------------------------
.
INITVAR  READ           CONTROLF,HUND16;*219,PTCNCLEV
         READ           CONTROLF,HUND29;*89,PTCNPPIN
.
         MOVE           ADYHOSP,TEMPDAY
         MOVE           ADYHOSP,DAYNUM
.
         MATCH          DDATE,ADATE
         GOTO           INIT100 IF NOT EQUAL
.
         MATCH          "1",PTCNXCHS     * exclude days hosp.for daycase
         IF             @EQUAL
           MOVE           ZERO,TEMPDAY
           MOVE           ZERO,DAYNUM
         ENDIF
.
INIT100  MOVE           TDATE,SIDATET
         MOVE           ZERO,COLNUM      * The column number of stepdown
         MOVE           ZERO,CURBEND
.
         PACK           KEY9,ACLAIM,AFUNDH,SP70
         CALL           GETCNEFF               * Get Contract Effective From
.
         MOVE           TDATE,FRDATE     * Start of the period
         CALL           SAVARB
         MOVE           TRBEND,CURMDAYS
         MOVE           TRBEND,SAVBEND
         MOVE           TDATE,DATFIELD
.        ADD            ONE,DATFIELD
         MOVE           DATFIELD,CEFFDATE          * set date for 'As at date'
         CALL           GETBFE           * Get the bed fee record
         MOVE           CONTRCID,SAVCONTR
         IF             PTTREPSD<>2
           MOVE           SAVPAT,TRATEF
           MOVE           SAVHF,TRATEH
         ENDIF
.
         MOVE           ZERO,COUNTA
         MOVE           ZERO,FORM2
         WHILE          COUNTA <= 9
           ADD            ONE,COUNTA
           ADD            ONE,FORM2
           MOVE           ZERO,BDYARRAY[FORM2]
         DO
         RETURN
.
.------------------------------------------------------------------------------
.        Save the variables for writting a change new bed fee record
.------------------------------------------------------------------------------
.
SAVARB   MOVE           TWARD,SAVWARD
         MOVE           TBED,SAVBED
         MOVE           TATYPE,SAVATYP
         MOVE           TRBTYP,SAVBTYP
         MOVE           TADOCT,SAVADOC
         MOVE           TDATE,SAVDATE
         MOVE           TDATE,NPENDDTE     * save the actual previous trandate
         MOVE           TTIME,SAVTIME
         MOVE           TCHSTAT,SAVCHST
         MOVE           TDEPT,STDEPT
         MOVE           PTTREPSD,SPTTREPS
.
.        MOVE           TRBEND,CURMDAYS
.        MOVE           TRBEND,SAVBEND
         MOVE           TDISC,SAVDISC
         MOVE           TALLW,SAVALLW
.
         MATCH          SP3,SAVBTYP
         RETURN         IF NOT EQUAL
.
         PACK           KEY6 WITH TWARD,TBED
         CALL           RDWARD1
         MOVE           WEFRBT,TRBTYP
         MOVE           WEFRBT,SAVBTYP
         RETURN
.
.        Check if adm.type was changed, which affect the bed fee
.
CHKWRD   MATCH          TATYPE,SAVATYP
         GOTO           CHKW10 IF EQUAL
.
         BRANCH         PTCNCNDY,CHKW10          * don't reset day count
.
.        Change of admission type, reset count
.
         MOVE           ZERO,TEMPDAY
         MOVE           ZERO,DAYNUM
         MOVE           TDATE,FRDATE
.
.        Get the new bed fee
.
CHKW10   CALL           SAVARB     * don't save the ending day from trans rec.
.
.        Recalculate the current number of days for calculating new rate
.
         CALL           CBTY0000
         IF             EXIT=0
           MOVE           ZERO,NBRDAYS
           DAYSEP         FRDATE,TDATE,NBRDAYS
           MOVE           DAYNUM,TEMPDAY
           ADD            NBRDAYS,TEMPDAY
         ENDIF
.
         CALL           GETBFE     * as we want stepdown to overwrite trans
         MOVE           CURBEND,TRBEND
.
         CMATCH         "L",TMOVE
         GOTO           CHKW99 IF NOT EQUAL
.
          MOVE      ZERO,NBRDAYS
          DAYSEP    FRDATE,TDATE,NBRDAYS
.
.       Accumulate the number of days in hospital
.
.        CALL           CBTY0000            * Check the bed type
.        IF             EXIT=1
.          ADD            NBRDAYS,DAYNUM
.        ELSE
           ADD            NBRDAYS,DAYNUM
.        ENDIF
.
         MOVE           TDATE,SAVDATE
         MOVE           TDATE,NPENDDTE     * save the actual previous trandate
CHKW99   RETURN
.
.------------------------------------------------------------------------------
.        Return from leave
.------------------------------------------------------------------------------
.
RETTYP   MATCH          TATYPE,SAVATYP
         GOTO           RETTYP1 IF EQUAL
.
         BRANCH         PTCNCNDY,RETTYP1         * don't reset day count
.
.        Change of admission type - reset the number of days
.
         MOVE           ZERO,DAYNUM
         MOVE           ZERO,TEMPDAY
         MOVE           TDATE,FRDATE       * From date for the admission type
         GOTO           RETTYP2
.
.        Check if Leave period to be included in the stepdown count
.
RETTYP1  MATCH          ACLAIM,PTCNCLEV
         IF             @EQUAL
           DAYSEP         SAVDATE,TDATE,NBRDAYS
           ADD            NBRDAYS,DAYNUM
           ADD            NBRDAYS,TEMPDAY
         ENDIF
.
.        Get the new bed fee as ward/bed might change when they return
.
RETTYP2  CALL           SAVARB
.
.        Check if return to specialty bed ?
.
         PACK           KEY5,ANSB,ANST,SAVBTYP
         CALL           RDCODE1
         BRANCH         OVRCD,RETTYP3
.
         REP            " 0",TCINDC6
         MOVE           ZERO,FORM1
         MOVE           TCINDC6,FORM1              * check for specialty bedtype
         COMPARE        ZERO,FORM1
         GOTO           RETTYP3 IF EQUAL          * normal bed type
.
         MOVE           FORM1,FORM2
         MOVE           BDYARRAY[FORM2],TEMPDAY    * save the previous bed days
         GOTO           RETTYP4
.
RETTYP3  
.        MATCH          ACLAIM,PTCNCLEV
.        IF             !@EQUAL
.          ADD            ONE,TEMPDAY           * add one day
.        ENDIF
.
RETTYP4  MOVE           TRBEND,CURMDAYS
         MOVE           TRBEND,SAVBEND
         MOVE           TDATE,CEFFDATE          * set date for 'As at date'
         CALL           GETBFE
         MOVE           CURBEND,TRBEND
         MOVE           TDATE,FRDATE
RETTYP9  RETURN
.
.
.******************************************************************************
.   Routine to workout the date of the begining of period 
.******************************************************************************
CALDAT  REP         " 0",KEY8
        UNPACK      KEY8,XCENT,XYEAR,XMON,XDAY
        MOVE        XDAY,DD
        MOVE        XMON,MM
        MOVE        XYEAR,YY
        MOVE        XCENT,CC
        CALL        DMYCON
.
        MOVE        JULDAY,FORM4
        ADD         CURMDAYS,FORM4
        SUB         FORM4B,FORM4       * Only if start from admission date
.
        MOVE        FORM4,JULDAY
        MOVE        JULDAY,JWKDAY
        MOVE        JULYR,JWKYER
        MOVE        JULCC,JWKCC
.
        CALL        JULCON
.
CALD99  RETURN
.
.******************************************************************************
.        Routine to get the new bed fee
.******************************************************************************
GETBFE   COMPARE        FIVE,CFEETYP
         GOTO           GNZBF000 IF EQUAL      * Use NZ Private billing
.
         MOVE           ZERO,NOFEE
         MOVE           ZERO,FORM2
         MOVE           ZERO,FORM3
         MOVE           ZERO,SAVPAT 
         MOVE           ZERO,SAVHF  
         MOVE           ZERO,COLNUM      * The column number of stepdown
         MOVE           ZERO,CURBEND
         MOVE           ZERO,LASTDAYS
         MOVE           CONTRCID,SAVCONTR
         MOVE           SP70,CONTRCID
         PACK           DIM23 WITH ACLAIM,AFUNDH,AFNDTB,SAVATYP,SAVBTYP
.
.        Johns Hopkins bed fees (has no Contract ID)
.
         COMPARE        NINE,CFEETYP
         GOTO           CHKB01 IF EQUAL      * Johns Hopkins Bed fees
.
         MOVE           SP70,PTHFBCAT
         PACK           KEY14,AFUNDH,AFNDTB
         CALL           RDFUND1
.
         COMPARE        THREE,CNTRCEFR
         GOTO           CHKB60 IF EQUAL     * Contract Eff.details not found
.
         LOAD           CEFFDATE,CNTRCEFR,ADATE,DDATE
         IF             CNTRCEFR=2
           PACK           DDATE,DDATE,SP70
           MATCH          SP70,DDATE
           IF             @EQUAL
             PACK           KEY5,ANSC,ANSL,ACLAIM
             CALL           RDCODE1
             IF             OVRCD=0
               MATCH          "D",TCINDC19
               IF             @EQUAL
                 PACK           CEFFDATE,CCC,CYY,CMM,CDD,SP70
                 REP            " 0",CEFFDATE
               ENDIF
             ENDIF
           ENDIF
         ENDIF
.
         PACK           DIM18 WITH ACLAIM,AFUNDH,PTHFBCAT,SAVATYP,SAVBTYP
.
CHKB01   IF             CFEETYP=9  
           OPEN           JHSBFEA1,"jhsbfeaf"
           PACK           KEY26 WITH DIM23,SP3
           CALL           RSJHBFE1
         ELSE
           OPEN           PATBFEE1,"patbfeef"
           PACK           KEY27 WITH DIM18,SP70
           CALL           RDSBFEE1
         ENDIF
CHKB10   IF             CFEETYP=9
           CALL           RKJHBFE1
           BRANCH         OVRCD OF CHKB40
           PACK           DIM23A WITH BFCLM,BFHFUND,BFHFTAB,BFATYPE,BFBTYPE
           MATCH          DIM23,DIM23A
           GOTO           CHKB40 IF NOT EQUAL
         ELSE
           CALL           RDKBFEE1
           BRANCH         OVRCD OF CHKB40
           PACK           DIM18A WITH BFCLM,BFHFUND,PTBFHTYP,BFATYPE,BFBTYPE
           MATCH          DIM18,DIM18A
           GOTO           CHKB40 IF NOT EQUAL
.
           MOVE           PTBFCNID,KEY6        * Contract ID
           CALL           VALCONTR             * Validate Contract ID
           BRANCH         EXIT,CHKB10          * search for different bed fees
           MOVE           PTBFCNID,CONTRCID    * Contract ID
.
           CALL           CKPAT0000    * Check for 'Using New Pat Portion Inv.'
.
.          If Contract Effective date is set for 'As at the effective date'
.          then save the Contract End date
.
           IF             CNTRCEFR=0
             MATCH          SP70,PMCITDAT
             IF             !@EQUAL
               MOVE           PMCITDAT,DATFIELD
               ADD            ONE,DATFIELD
               MOVE           DATFIELD,PMCITDAT
               REP            " 0",PMCITDAT
             ENDIF
             MOVE           PMCITDAT,CMENDDTE    * End of Contract
           ENDIF
         ENDIF
.
.        Check for days of hospitalisation for daycase patient
.
         IF             BFENDDY=0 
           MATCH          DDATE,ADATE
           IF             @EQUAL
             MATCH          "1",PTCNXCHS
             GOTO           CHKB16 IF EQUAL     * ignore Days hospitalisation
             COMPARE        ZERO,ADYHOSP
             GOTO           CHKB16 IF EQUAL
           ENDIF
           GOTO           CHKB10
         ENDIF
.
         ADD            ONE,COLNUM
.
         IF             TEMPDAY>999
           MOVE          "999",TEMPDAY         * Only 999 days avail. in bed fee
         ENDIF
         COMPARE        TEMPDAY,BFENDDY
         GOTO           CHKB25 IF LESS
         GOTO           CHKB16 IF NOT EQUAL
.
         IF             BFENDDY=999 & TEMPDAY=999
           GOTO           CHKB16
         ENDIF
.
         MATCH          ANSA,TMOVE
         IF             @EQUAL
           IF             ADYHOSP<>0
             COMPARE        TEMPDAY,ADYHOSP
             GOTO           CHKB25 IF EQUAL
           ENDIF
         ENDIF
.
         COMPARE        TEMPDAY,CURMDAYS
         GOTO           CHKB25 IF NOT LESS
.
         MATCH          ACLAIM,PTCNCLEV
         IF             @EQUAL
           MATCH          "R",TMOVE
           GOTO           CHKB25 IF EQUAL
         ENDIF
.
CHKB16   MOVE           BFPAT,SAVPAT
         MOVE           BFHF,SAVHF
         MOVE           BFENDDY,CURMDAYS
         MOVE           BFENDDY,CURBEND
.
.         If it's not a manual stepdown, recalculate rate overwrite trans record
.
          MATCH         "IBAHMS70",PRGID                               *I-61004
          GOTO          CHKB20 IF EQUAL                                *I-61004
.
.         Ignore force stepdown if it's done prior 'Invoice from date'
.
          MATCH         IDATEF,TDATE
.         GOTO          CHKB20 IF LESS    * already been invoiced
          IF            @LESS
            MOVE          ZERO,PTTREPSD
            GOTO          CHKB20
          ENDIF
.
.         MATCH         ANSY,PTMICMXP
.         IF            !@EQUAL
            BRANCH         PTTREPSD,CHKB18,CHKB18     * manual stepdown
.         ENDIF
          GOTO          CHKB20                 * recalculate rates
.
CHKB18    MATCH          ADATE,DDATE               * skip a day case patient
          RETURN         IF EQUAL
.
.        Check if this patient get charged as a daycase rate
.
          COMPARE        ZERO,SAVBEND
          RETURN         IF EQUAL
.
          CMATCH         "D",TMOVE
          GOTO           CHKB20 IF EQUAL
.
          CMATCH         "L",TMOVE
          GOTO           CHKB20 IF EQUAL
.
.        Check if there is an existing bed fee from TRANS file
.
          MATCH          TDATE,SAVDATE
          GOTO           CHKB30 IF EQUAL
.
.        Save the new bed fee for the next stepdown
.
CHKB20   MOVE           SAVPAT,TRATEF
         MOVE           SAVHF,TRATEH
         MOVE           ZERO,TRATEG
         RETURN
.
.        Highest day in the period for the stepdown
.
CHKB25   MOVE           BFENDDY,FORM3
         MOVE           BFENDDY,LASTDAYS
         GOTO           CHKB10
.
.        If this is a manual stepdown, use rates from transfer file
.
CHKB30   
.        MATCH         ANSY,PTMICMXP
.        IF            !@EQUAL
           IF            PTTREPSD=1 | PTTREPSD=2
             MOVE          TRATEF,SAVPAT
             MOVE          TRATEH,SAVHF
             MOVE          TRBEND,CURBEND
             MOVE          TRBEND,CURMDAYS                             *I-48340
.
.
.            Do not Reset start date if the Change record is from bedfees update
.
             COMPARE        TWO,PTTREPSD
             RETURN         IF EQUAL
.
             MATCH         "IBABIL83",PRGID
             IF            !@EQUAL
               MOVE          TDATE,FRDATE     * reset Start date       *I-48340
             ENDIF
           ENDIF
.        ENDIF
         GOTO       CHKB99
.
.        Use default bed fee
.
CHKB40   ADD            ONE,FORM2
         BRANCH         FORM2 OF CHKB70,CHKB80
.
.        Check if this date has already been invoiced
.
         MATCH          IDATEF,SAVDATE
         GOTO           CHKB50 IF LESS
         GOTO           CHKB45 IF NOT EQUAL
.
.        Check if the admission date has been invoiced, by checking number of
.        invoice printed
.
         COMPARE        ZERO,AINVPRT
         GOTO           CHKB50 IF NOT EQUAL
.
CHKB45
.        DISPLAY        *P1:23,*EL,*P10:23:
.                       " Adm. No.:",*V2LON,AADMNO,*HOFF:
.                       " Adm.Type:",*V2LON,SAVATYP,*HOFF:
.                       " Bed type:",*V2LON,SAVBTYP,*HOFF;
.
.        DISPLAY        *P48:24,*EL,*V2LON:
.                       "** New Bed Fee Not Available ** ",*HOFF:
.                       *W,*P1:23,*EL,*P48:24,*EL;
         GOTO           CHKB60
.
CHKB50   MOVE           "999",CURMDAYS
         MOVE           "999",CURBEND
.
CHKB60   MOVE           ONE,NOFEE
         MOVE           THREE,FORM2
         GOTO           CHKB99
.
CHKB70   PACK           DIM18 WITH ACLAIM,SP6,SP3,SAVATYP,SAVBTYP
         PACK           DIM23 WITH ACLAIM,SP10,SP4,SAVATYP,SAVBTYP
         GOTO           CHKB01
.
CHKB80   READ           CONTROLF,TWENTY;*193,PTCNDCLM
         CALL           DCLM0000        * Check Hospital claim code charging
         PACK           DIM18 WITH PTCNDCLM,SP6,SP3,SAVATYP,SAVBTYP 
         PACK           DIM23 WITH PTCNDCLM,SP10,SP4,SAVATYP,SAVBTYP
         GOTO           CHKB01
CHKB99   RETURN
+
.        -----------------------------------------
.        Routine to get the new NZ Private bed fee
.        -----------------------------------------
.
GNZBF000  OPEN      NZPBFEA1,"nzpbfeaf"
          MOVE      ZERO,NOFEE
          MOVE      ZERO,FORM2
          MOVE      ZERO,FORM3
          MOVE      ZERO,SAVPAT
          MOVE      ZERO,SAVHF
          MOVE      ZERO,COLNUM      * The column number of stepdown
          MOVE      ZERO,CURBEND
          MOVE      ZERO,LASTDAYS
          PACK      KEY26 WITH PMVXMHOS,ACLAIM,AFUNDH,SP8,SAVATYP,SAVBTYP
.
GNZBF100  CALL      EFTD0000          Get the current effective date
          MATCH     SP70,EFTDATE
          GOTO      GNZBF900 IF EQUAL
.
          PACK      KEY34,KEY26,EFTDATE,SP70
          PACK      KEY37,KEY34,SP70
          CALL      RSNZBFE1
GNZBF120  CALL      RKNZBFE1
          BRANCH    OVRCD OF GNZBF900
.
          PACK      DIM34A WITH NZBFHOSP,NZBFCLMC,NZBFCNTR,NZBFFTAB,NZBFATYP:
                                NZBFBTYP,NZBFEFDT,SP70
.
          MATCH     KEY34,DIM34A
          GOTO      GNZBF900 IF NOT EQUAL
.
.         Check for days of hospitalisation for daycase patient
.
          MOVE      ZERO,SAVFEDAY
          MOVE      NZBFEDAY,SAVFEDAY
          IF        SAVFEDAY=0
            MATCH     DDATE,ADATE
            IF        @EQUAL
              MATCH     "1",PTCNXCHS
              GOTO      GNZBF200 IF EQUAL     * ignore Days hospitalisation
              COMPARE   ZERO,ADYHOSP
              GOTO      GNZBF200 IF EQUAL
            ENDIF
            GOTO        GNZBF120
          ENDIF
.
          ADD       ONE,COLNUM
          COMPARE   TEMPDAY,SAVFEDAY
          GOTO      GNZBF440 IF LESS
          GOTO      GNZBF200 IF NOT EQUAL
.
          IF        SAVFEDAY=999 & TEMPDAY=999
            GOTO      GNZBF200
          ENDIF
.
          COMPARE   TEMPDAY,CURMDAYS
          GOTO      GNZBF440 IF NOT LESS
.
GNZBF200  MOVE      NZBFRATE,SAVPAT
          MOVE      NZBFREBA,SAVHF
          MOVE      SAVFEDAY,CURMDAYS
          MOVE      SAVFEDAY,CURBEND
.
.         If it's not a manual stepdown, recalculate rate overwrite trans record
.
          BRANCH    PTTREPSD,GNZBF400   * manual stepdown
          GOTO      GNZBF420                 * recalculate rates
.
GNZBF400  MATCH     ADATE,DDATE               * skip a day case patient
          GOTO      GNZBF999 IF EQUAL
.
.        Check if this patient get charged as a daycase rate
.
          COMPARE   ZERO,SAVBEND
          GOTO      GNZBF999 IF EQUAL
.
          CMATCH    "D",TMOVE
          GOTO      GNZBF420 IF EQUAL
.
          CMATCH    "L",TMOVE
          GOTO      GNZBF420 IF EQUAL
.
.        Check if there is an existing bed fee from TRANS file
.
          MATCH     TDATE,SAVDATE
          GOTO      GNZBF500 IF EQUAL
.
.        Save the new bed fee for the next stepdown
.
GNZBF420  MOVE      SAVPAT,TRATEF
          MOVE      SAVHF,TRATEH
          MOVE      ZERO,TRATEG
          GOTO      GNZBF999
.
.        Highest day in the period for the stepdown
.
GNZBF440  MOVE      SAVFEDAY,FORM3
          MOVE      SAVFEDAY,LASTDAYS
          GOTO      GNZBF120
.
.        If this is a manual stepdown, use rates from transfer file
.
GNZBF500  IF        PTTREPSD=1
            MOVE      TRATEF,SAVPAT
            MOVE      TRATEH,SAVHF
            MOVE      TRBEND,CURBEND
            MOVE      TRBEND,CURMDAYS
            MOVE      TDATE,FRDATE     * reset Start date 
          ENDIF
          RETURN
.
.        Use the existing bed fee from transfer file, check if the fee is
.        is different from the actual stepdown fee
.
GNZBF520  COMPARE   SAVPAT,TRATEF
          GOTO      GNZBF600 IF NOT EQUAL
.
          COMPARE   SAVHF,TRATEH
          GOTO      GNZBF999 IF EQUAL
.
.        If the bed fee was manually changed, then we do not reset the number
.        of days in hospital
.        Get the number of days available for the rate
.
GNZBF600  MOVE      ZERO,FORM4
          PACK      KEY26 WITH PMVXMHOS,ACLAIM,AFUNDH,SP8,SAVATYP,SAVBTYP
.
          PACK      KEY37 WITH KEY26,EFTDATE,SP3
          CALL      RSNZBFE1
GNZBF700  CALL      RKNZBFE1
          BRANCH    OVRCD OF GNZBF999
.
          PACK      DIM26A WITH NZBFHOSP,NZBFCLMC,NZBFCNTR,NZBFFTAB,NZBFATYP:
                                NZBFBTYP,SP70
.
          MATCH     KEY26,DIM26A
          GOTO      GNZBF999 IF NOT EQUAL
.
.         Check if this rate is equal to the existing bed fee
.
          COMPARE   NZBFRATE,TRATEF
          GOTO      GNZBF800 IF NOT EQUAL
.
          COMPARE   NZBFREBA,TRATEH
          GOTO      GNZBF820 IF EQUAL
.
.        Highest day in the period for the existing bed fee
.
GNZBF800  MOVE      SAVFEDAY,FORM4
          GOTO      GNZBF700
.
.        Find out the number of days diffrence between the
.        existing bed fee and the bed fee from the stepdown
.
GNZBF820  MOVE      SAVFEDAY,CURMDAYS
          MOVE      SAVFEDAY,CURBEND
.
          CMATCH    "A",TMOVE
          GOTO      GNZBF840 IF NOT EQUAL
.
.        This is the admission record of TRANS file,
.        check if the Number of Hosp. has been changed
.
          COMPARE   FORM3,SAVFEDAY
          GOTO      GNZBF840 IF NOT LESS
.
.        The highest day of existing bed fee is less than the stepdown bed fee
.
          MOVE      FORM4,DAYNUM
          GOTO      GNZBF999
.
GNZBF840  SUB       FORM3,FORM4
          ADD       FORM4,DAYNUM
          GOTO      GNZBF999
.
.         Use default bed fee
.
GNZBF900  ADD       ONE,FORM2
          BRANCH    FORM2 OF GNZBF950,GNZBF960
.
.        Check if this date has already been invoiced
.
          MATCH     IDATEF,SAVDATE
          GOTO      GNZBF920 IF LESS
          GOTO      GNZBF930 IF NOT EQUAL
.
.         Check if the admission date has been invoiced, by checking number of
.         invoice printed
.
          COMPARE   ZERO,AINVPRT
          GOTO      GNZBF930 IF EQUAL
.
GNZBF920  MOVE      "999",CURMDAYS
          MOVE      "999",CURBEND
.
GNZBF930  MOVE      ONE,NOFEE
          GOTO      GNZBF999
.
GNZBF950  PACK      KEY26 WITH PMVXMHOS,ACLAIM,SP10,SP4,SAVATYP,SAVBTYP
          GOTO      GNZBF100
.
GNZBF960  READ      CONTROLF,TWENTY;*193,PTCNDCLM
          CALL      DCLM0000        * Check Hospital claim code charging
          PACK      KEY26 WITH PMVXMHOS,PTCNDCLM,SP10,SP4,SAVATYP,SAVBTYP
          GOTO      GNZBF100
.
GNZBF999  RETURN
+
.************************************************************************
.        Save bed days for each speciality bed type
.************************************************************************
CBTY0000 MOVE       ZERO,EXIT
         PACK       KEY5,ANSB,ANST,SAVBTYP
         CALL       RDCODE1
         BRANCH     OVRCD,CBTY9999
.
         REP        " 0",TCINDC6
         MOVE       ZERO,FORM1
         MOVE       TCINDC6,FORM1                * check for specialty bedtype
         COMPARE    ZERO,FORM1
         GOTO       CBTY9999 IF EQUAL            * normal bed type
.
         MOVE       FORM1,FORM2
         MOVE       BDYARRAY[FORM2],FORM4B       * save the previous bed days
.
         DAYSEP     NPENDDTE,TDATE,NBRDAYS
         MOVE       ONE,EXIT
.
         MOVE       BDYARRAY[FORM2],TEMPDAY
         ADD        NBRDAYS,TEMPDAY
         GOTO       CBTY9999
.
CBTY9999 RETURN
.
.------------------------------------------------------------
.         Get the last effective date
.------------------------------------------------------------
EFTD0000  MOVE      SP70,EFTDATE
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MATCH     SP70,DDATE
          IF        !@EQUAL
            PACK      KEY8,DDATE,SP70
          ENDIF
          PACK      KEY34,KEY26,KEY8,SP70
          PACK      KEY37,KEY34,SP70
          CALL      RSNZBFE1
EFTD1000  CALL      RKNZBFE1
          BRANCH    OVRCD,EFTD2000
          PACK      DIM34A WITH NZBFHOSP,NZBFCLMC,NZBFCNTR,NZBFFTAB,NZBFATYP:
                                NZBFBTYP,NZBFEFDT,SP70
          MATCH     DIM34A,KEY34
          GOTO      EFTD4000 IF EQUAL
.
EFTD2000  CALL      RPNZBFE1                  * check for previous record
          BRANCH    OVRCD,EFTD9999
.
          PACK      DIM26A WITH NZBFHOSP,NZBFCLMC,NZBFCNTR,NZBFFTAB,NZBFATYP:
                                NZBFBTYP,SP70
          MATCH     DIM26A,KEY26
          GOTO      EFTD9999 IF NOT EQUAL
.
EFTD4000  MOVE      NZBFEFDT,EFTDATE         * Save the effective date
EFTD9999  RETURN
.
.*******************************************************************************
.         Check if we are using New Patient Portion functionality
.         for Accommodation
.*******************************************************************************
CKPAT000  MOVE      ONE,NUMBINVR                   * default to one no of Inv.
          MATCH     "1",PTCNPPIN
          GOTO      CKPAT999 IF NOT EQUAL
.
          PACK      ACCCONTR,ACCCONTR,SP70
          MATCH     SP70,ACCCONTR
          IF        !@EQUAL
            MOVE      ACCCONTR,CONTRCID
          ENDIF
          MOVE      CONTRCID,ACCCONTR              * save Accommodation Contract
.
          PACK      KEY6,CONTRCID
          CALL      RDPMCID1
          BRANCH    OVRCD,CKPAT900
.
          MOVE      ZERO,IPASS
.
          BRANCH    IPATFLAG,CKPAT900,CKPAT200
          MATCH     "1",PMCIPINV
          IF        !@EQUAL
            MOVE      ONE,PACMCHRG        * do not include to Patient payable
          ENDIF
          GOTO      CKPAT900
.
.     Check 'Use Patient Portion Invoicing' field from Contract
.
CKPAT200  MATCH     "1",PMCIPINV
          GOTO      CKPAT900 IF NOT EQUAL
.
          MOVE      TWO,NUMBINVR          * New Patient Portion Inv.

          MOVE      ONE,IPASS
          MOVE      ZERO,NCHACCOM         * Charge Accommodation
          IF        ADSCHI=1
            MOVE      ONE,NCHACCOM        * No Accom - Printed Disc.Invoice
          ENDIF
.
CKPAT900  MOVE      PTBFCNID,CONTRCID    * Contract ID
          PACK      KEY6,CONTRCID
          CALL      RDPMCID1
CKPAT999  RETURN
+
