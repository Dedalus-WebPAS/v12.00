.******************************************************************************
.*                                                                            *
.* Include        : PMSHCGDS.PBL                                              *
.*                                                                            *
.* Function       : Display the contents of the GP practice file              *
.*                  For a given GP validated on the GP/practice Link file     *
.*                  (PMSHCGFD.INC   and  PMSHCLFD.INC)                        *
.*                                                                            *
.* Author         : Whirlpool       27/11/92                                  *
.*                                                                            *
.* Parameters     : KEY8  -  GP     (KEY8 =SPACES --> display ALL Practices)  *
.*                  PMHGFLAG - parameter to display active GP                 *
.*                                                                            *
.* Returns        : EXIT = ONE    nothing selected                            *
.*                  PMHGPRAC      the GP practice code                        *
.*                  PMHGCNTR      the GP practice code count                  *
.*                                                                            *
.* Modifications  :                                                           *
.*        V9.02.00  12/12/2002  J.Tan                                         *
.*                  Ported from r5.10                                         *
.*        V5.01.02  31/03/1993  Graeme Williams                               *
.*                  If KEY8 = SP8, then display practices in practice code    *
.*                  sequence.                                                 *
.*        V5.01.03  13/07/1993  Whirlpool      SRF 440129                     *
.*                  Added use of PMHGFLAG to display all codes                *
.*        V5.01.04  28/10/1993    Justin Coates  QUOTE 440017 SRF 440204      *
.*                  allow for new GP practice file key                        *
.*        V5.01.05  14/01/1994    Justin Coates  SRF 440457                   *
.*                  check on the active flag for second loop on patgppfd      *
.*        V5.01.06  07/03/1994    Justin Coates  SRF 440717                   *
.*                  fix check on PMHLflag in second loop                      *
.******************************************************************************
.
PMSHCGDS  MOVE      ZERO,EXIT
.
. ******* see if anything on file *******
.
          MATCH     SP10,KEY8              * is there a default Ref. GP
          IF        @EQUAL
.
            PACK      KEY12,KEY8,SP20
            CALL      RSPMHCG1                * position in Practice file
.
.           loop over GP practice file only
.
PTGPP020    CALL      RKPMHCG1
            BRANCH    OVRCD,PTGPP900
            IF        PMHGFLAG = 0
              MATCH     "1",PMHGSTTS
              GOTO      PTGPP020 IF EQUAL       * non-active 
            ENDIF
          ELSE
.
            PACK      KEY20,KEY8,SP20
            CALL      RSPMHCL1                * position in link file
.
.           loop over GP link file then the GP practice file
.
PTGPP030    CALL      RKPMHCL1
            BRANCH    OVRCD,PTGPP900
            MATCH     KEY8,PMHLHCPC
            GOTO      PTGPP900 IF NOT EQUAL 
            IF        PMHLFLAG = 0
              MATCH     "1",PMHLSTAT
              GOTO      PTGPP030 IF EQUAL       * non-active
            ENDIF
.
            PACK      KEY12,PMHLHCPR,SP2
            CALL      RSPMHCG1
PTGPP040    CALL      RKPMHCG1
            BRANCH    OVRCD,PTGPP030          * no more practices
            MATCH     PMHLHCPR,PMHGPRAC
            GOTO      PTGPP030 IF NOT EQUAL   * no more practices
            IF        PMHGFLAG = 0
              MATCH     "1",PMHGSTTS
              GOTO      PTGPP040 IF EQUAL       * non-active 
            ENDIF
          ENDIF
.
. ******* have details on file so display them *******
.
PTGPP050  DISPLAY   *P1:3,*EF,*P20:4,*V2LON,*ULON," GP Practice Codes "
.
          MOVE      ZERO,PMHGQCNT           * clear counter
          MOVE      FIVE,CVERT              * set line
.
.         position in the required file
.
          MATCH     SP10,KEY8             * is there a default ref. GP
          IF        @EQUAL
            MOVE      SP70,KEY12
            CALL      RSPMHCG1
          ELSE
            PACK      KEY20,KEY8,SP20
            CALL      RSPMHCL1
          ENDIF
.
.         Get the next record
.
PTGPP100  MATCH     SP10,KEY8
          IF        @EQUAL
            CALL      RKPMHCG1
            BRANCH    OVRCD,PTGPP500
            IF        PMHGFLAG = 0
              MATCH     "1",PMHGSTTS          * Non-active ?
              GOTO      PTGPP100 IF EQUAL
            ENDIF
          ELSE
            CALL      RKPMHCL1
            BRANCH    OVRCD,PTGPP500
            MATCH     CFNAME,PMHLHCPC
            GOTO      PTGPP500 IF NOT EQUAL 
            IF        PMHLFLAG = 0
              MATCH     "1",PMHLSTAT
              GOTO      PTGPP100 IF EQUAL
            ENDIF
.
.           now have to loop over practice file
.
            PACK      KEY12,PMHLHCPR,SP2
            CALL      RSPMHCG1
PTGPP200    CALL      RKPMHCG1
            BRANCH    OVRCD,PTGPP100
            MATCH     PMHLHCPR,PMHGPRAC
            GOTO      PTGPP100 IF NOT EQUAL   * no more practices
            IF        PMHGFLAG = 0
              MATCH     "1",PMHGSTTS          * Non-active ?
              GOTO      PTGPP200 IF EQUAL
            ENDIF
          ENDIF
.
.         check if we need a new screen
.
PTGPP300  ADD       ONE,CVERT               * add to line count
          IF        CVERT >= 23
            MOVE      ONE,FORM1
            DISPLAY   *P1:24,*EL,"Select item, (",*V2LON,"F",*HOFF:
                        ")irst Screen, (":
                        *V2LON,"N",*HOFF,")ext Screen, (":
                        *V2LON,"E",*HOFF,")xit ? ";
PTGPP305    KEYIN     *P54:24,*V2LON,*JR,DIM2:
                      *P54:24,*DV,DIM2
            PACK      DIM2,DIM2,SP2
            REP       UPPLOW,DIM2
            TYPE      DIM2                          * numeric input?
            GOTO      PTGPP570 IF EQUAL
            MATCH     " F",DIM2
            GOTO      PTGPP050 IF EQUAL
            MATCH     " N",DIM2
            GOTO      PTGPP600 IF EQUAL
            MATCH     " E",DIM2
            GOTO      PTGPP950 IF EQUAL
            BEEP
            GOTO      PTGPP305
          ENDIF
.
. display record
.
PTGPP350  CALL      PTGAD000                * get the full address
          ADD       ONE,PMHGQCNT            * add to counter
          DISPLAY   *P1:CVERT,*V2LON,PMHGQCNT,*HOFF,". ",PMHGPRAC,*HOFF:
                    *P14:CVERT,KEY60
          PACK      PMHGQARR[PMHGQCNT],PMHGPRAC,PMHGCNTR
          MATCH     SP10,KEY8 
          GOTO      PTGPP200 IF NOT EQUAL   * go back to link second loop
          GOTO      PTGPP100
.
.         no more data to display
.
PTGPP500  MOVE      TWO,FORM1
          DISPLAY   *P1:24,*EL,"Select item, (",*V2LON,"F",*HOFF:
                    ")irst Screen, (",*V2LON,"E",*HOFF,")xit ? ";
.
PTGPP550  KEYIN     *P39:24,*V2LON,*JR,DIM2:
                    *P39:24,*DV,DIM2
          PACK      DIM2,DIM2,SP2
          REP       UPPLOW,DIM2
          TYPE      DIM2                          * numeric input?
          GOTO      PTGPP570 IF EQUAL
          MATCH     " F",DIM2
          GOTO      PTGPP050 IF EQUAL
          MATCH     " E",DIM2
          GOTO      PTGPP950 IF EQUAL
          BEEP
          GOTO      PTGPP550
.
.         a numeric was input
.
PTGPP570  MOVE      DIM2,FORM2
          IF        (FORM2<1) | (FORM2>PMHGQCNT)
            MOVE      SP2,DIM2
            BEEP                                  * not in valid range
            BRANCH    FORM1,PTGPP305,PTGPP550
          ENDIF
.
          UNPACK    PMHGQARR[FORM2],PMHGPRAC,DIM2
          MOVE      DIM2,PMHGCNTR
          MOVE      ZERO,EXIT
          GOTO      PTGPP999
.
.         Clear the screen
.
PTGPP600  MOVE      SIX,CVERT
          MOVE      ZERO,PMHGQCNT           * init item counter
          DISPLAY   *P1:CVERT,*EF
          GOTO      PTGPP350
.
PTGPP900  DISPLAY   *P1:3,*EF,*P30:4,*V2LON,*BLINKON:
                    "No Active Practices on file.  ";
          CALL      HITENTER
PTGPP950  MOVE      ONE,EXIT
          MOVE      SP6,PMHGPRAC
          MOVE      SP2,PMHGCNTR 
.
PTGPP999  RETURN
.
.*********************************************************************
.         format the surgery name address
.*********************************************************************
PTGAD000  MOVE      ZERO,FORM2
          PACK      KEY60,SP30,SP30
          CLEAR     KEY60
PTGAD100  ADD       ONE,FORM2
          COMPARE   SEVEN,FORM2
          GOTO      PTGAD900 IF NOT LESS    * finished
.
          PACK      KEY35,SP20,SP20
          LOAD      KEY35,FORM2,PMHGPNAM,PMHGADD1,PMHGADD2:
                                PMHGADD3,PMHGADD4,PMHGPOST
          MATCH     SP30,KEY35
          GOTO      PTGAD100 IF EQUAL       * nothing to add
.
          IF        FORM2 <> ONE
            APPEND    ", ",KEY60
          ENDIF
          APPEND    KEY35,KEY60
          RESET     KEY60
          STRIP     KEY60
          ENDSET    KEY60
          GOTO      PTGAD100
.
PTGAD900  RESET     KEY60
          PACK      KEY60,KEY60,SP30,SP30
PTGAD999  RETURN
+
