.******************************************************************************
.*  Include Name  : PATCATKY.PBL                                              *
.*  Description   : Keyin of a Catagories in codes file entry in V5.01        *
.*  Author        : Matt Surridge.                                            * 
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF3 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMODE - Insert Mode        (0=No  1=Yes)               *
.*                                                                            *
.*  Returns       : TCODE     - Keyed in code                                 *
.*                  TDESC    - Description                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Modifications : 07/09/94  ROD                                             *
.*                            Category is only 2 chars not 3 !!!              *
.*                                                                            *
.******************************************************************************
PATCATKY
PCTKY010  MATCH     SP3,CKYIDEF3                  * using a default ?
          GOTO      PCTKY050 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN2:      * no default used
                    *PECOL:EVERT,*V2LON,CODE:
                    *PECOL:EVERT,*DV,CODE
          GOTO      PCTKY080
.
PCTKY050  MOVE      CKYIDEF3,CODE                 * default
           KEYIN     *PECOL:EVERT,*DV,CODE:     * default used
                     *PECOL:EVERT,*V2LON,*RV,CODE:
                     *PECOL:EVERT,*DV,CODE
.
.         check what was entered
.
PCTKY080  CALL      CATCK000
          BRANCH    EXIT,PCTKY100,PCTKY705,PCTKY805,PCTKY905
.                        "?"      valid    nothing  exitchar
          GOTO      PCTKY010
.
.         a "?" was input
.
PCTKY100  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
PCTKY105  CALL      PATCATDS
          BRANCH    CNEWDS,PCTKY150
.
          CALL      PUTSCR00                * redisplay screen
          GOTO      PCTKY010                * rekey code
.
.         now ask for code while leaving displayed codes on screen
.
PCTKY150  MATCH     SP3,CKYIDEF3                  * using a default
          GOTO      PCTKY200 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Code    : ":
                    *P11:24,*DV,UNDLN3:      * no default used
                    *P11:24,*V2LON,CODE:
                    *P11:24,*DV,CODE
          GOTO      PCTKY300
.
PCTKY200  MOVE      CKYIDEF3,CODE
          KEYIN     *P1:24,*EL,*DV,"Code    : ":
                    *P11:24,*DV,CODE:     * default used
                    *P11:24,*V2LON,*RV,CODE:
                    *P11:24,*DV,CODE
.
.         repeat the validation of the code
.         check what was entered
.
PCTKY300  CALL      CATCK000
          BRANCH    EXIT,PCTKY105,PCTKY700,PCTKY800,PCTKY900
.                        "?"      valid    nothing  exitchar
.
          GOTO      PCTKY150
.
.         a valid code was entered 
. 
PCTKY700  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,*EL,CODE
.
PCTKY705  MOVE      FALSE,EXIT                    * valid input
          GOTO      PCTKY999 
.
.         nothing was input
.
PCTKY800  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,CODE
.
PCTKY805  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      PCTKY999 
.
.         EXITCHAR input
.
PCTKY900  CALL      FRESCR00                * free the screen saved
.
PCTKY905  MOVE      TWO,EXIT                * EXITCHAR
.
PCTKY999  RETURN
+
.*********************************************************************
.*                  CATCK000                    Called by : PATCODKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
CATCK000  ENDSET    CODE
          APPEND    SP3,CODE
          RESET     CODE
.
          MATCH     EXITCHAR,CODE
          GOTO      CATCK600 IF EQUAL       * exit char
.
          MATCH     SP3,CODE
          GOTO      CATCK700 IF EQUAL       * nothing
.
          MATCH     QUEST,CODE
          GOTO      CATCK800 IF EQUAL       * ? mark
.
.         a code was entered so validate
. 
CATCK500  PACK      KEY5,CODE,SP10
          CALL      RDCODE1
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,CATCK900          * invalid code
          ELSE
            COMPARE   ZERO,OVRCD
            GOTO      CATCK900 IF EQUAL       * Code Exists
          ENDIF
.
          MOVE      CODE,TCODE
          MOVE      TWO,EXIT                * valid code
          GOTO      CATCK999
.
.         exitchar entered
.
CATCK600  MOVE      FOUR,EXIT
          MOVE      SP3,CODE
          MOVE      SP20,TDESC              * clear desc field
          GOTO      CATCK999
.
.         nothing entered
.
CATCK700  BRANCH    CKYIMAND,CATCK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP2,CODE
          MOVE      SP20,TDESC              * clear desc field
          GOTO      CATCK999
.
.         "?" entered
.
CATCK800  MOVE      ONE,EXIT
          GOTO      CATCK999
.
.         invalid code entered
.
CATCK900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
.
          IF        CKYIMODE=0
            DISPLAY   *P1:24,*EL,*B,"Invalid code.  ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      CATCK999
.
.         field is mandatory
.
CATCK950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is mandatory.  ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
CATCK999  RETURN
+
.
          INC       PATCATDS
.
+
