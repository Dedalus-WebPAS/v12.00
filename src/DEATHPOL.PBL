.------------------------------------------------------------
. Include Name: DEATHPOL
.
. Function: This routine updates the Patient Death Polling Table.
.
. V10.03.01 01/12/2011  Mike Laming   CAR 250985
.           Add logic for MultiDatabase processing see PATDPATH & VFILE000
. V10.02.1  08/09/2011  Jill Parkinson CAR 250438
.           Added open and close of patdthaf
. V9.04.00  26/09/2005  Ebon Clements CAR 56868
.           Added web user id
.------------------------------------------------------------
DEAPOL00  COMPARE   ZERO,PCEASE
          GOTO      DEAPOL99 IF EQUAL
.
          UNPACK    SP70,PTDHURNO,PTDHWEBU,PTDHSPAR
          MOVE      PURNO,PTDHURNO
          MOVE      USERID,PTDHWEBU
          MOVE      SP70,PTDHSPAR
.                                           * check if multi database
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,SIXTY;*129,MRCNNOHS
          IF        MRCNNOHS = 0
            OPEN      PATDTHA1,"patdthaf"
            MOVE      PURNO,KEY8
            CALL      RDPTDH1
            IF        OVRCD=1
              CALL      WRPTDH1
            ENDIF
          ELSE                              * multi database
            CALL      PATDPATH              * get multi database dpaths
            MOVE      ZERO,HOSPCNT
DEAPOL21    CALL      VFILE000              * setup Dpath and open file
            BRANCH    EXIT,DEAPOL99
            MOVE      PURNO,KEY8
            CALL      RDPTDH1
            IF        OVRCD=1
              CALL      WRPTDH1
            ENDIF
            GOTO      DEAPOL21              * loop thru data directories
          ENDIF
.
DEAPOL99  CLOSE     PATDTHA1
          RETURN
.
.------------------------------------------------------------
. Open Multi database tables for vistab00
.------------------------------------------------------------
VFILE000  MOVE      ZERO,EXIT
.
          ADD       ONE,HOSPCNT
.
          COMPARE   ZERO,MRCNNOHS
          GOTO      VFILE950 IF EQUAL       * not using data directories
.
          COMPARE   HOSPCNT,MRCNNOHS
          GOTO      VFILE950 IF LESS
.
          PACK      XXDPATH,SP20,SP20,SP20
          LOAD  XXDPATH,HOSPCNT,HDPATH1,HDPATH2,HDPATH3,HDPATH4,HDPATH5,HDPATH6
          LOAD  HOSPITAL,HOSPCNT,HDDESC1,HDDESC2,HDDESC3,HDDESC4,HDDESC5,HDDESC6
          LOAD  HOSPCODE,HOSPCNT,HDNAME1,HDNAME2,HDNAME3,HDNAME4,HDNAME5,HDNAME6
.
          MOVE      "patdthaf",OPFILE
          MOVE      ONE,FILENUM
          CALL      OMDFN000                * this routine is in PATDPATH
.
          GOTO      VFILE999
.
VFILE950  MOVE      ONE,EXIT
VFILE999  RETURN
.
.                                       * OMDFN000 calls OTRF0000 in PATDPATH
OTRF0000  IF        FILENUM = 1
            CLOSE     PATDTHA1
            OPEN      PATDTHA1,PATH
          ENDIF
OTRF9999  RETURN
