.******************************************************************************
.*  Include Name  : PMSHCGKY.PBL                                              *
.*  Description   : Keyin of a GP Practice code from PMSHCGFD                 *
.*  Author        : Steve Armstrong (Extracted from PATGPPKY)                 *
.*                                                                            *
.*  Parameters    : CCOL     - column of keyin                                *
.*                  CVERT    - row of keyin                                   *
.*                  CKYIDEF8 - referring GP code                              *
.*                  CKYIDEF6 - default GP practice code                       *
.*                  CKYIDEF2 - default GP practice code count                 *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMODE - 1 for insert, otherwise zero                   *
.*                  CKACTIVE - Whether to check for active or not             *
.*                               (0=No 1=Yes)                                 * 
.*                  CKLINKED - Whether to check if GP is linked to a Practice *
.*                             (1) = No, (0) = Yes                            * 
.*                                                                            *
.*  Returns       : CKYIDEF6 - Keyed in GP practice code                      *
.*                  CKYIDEF2 - Keyed in GP practice code count                *
.*                  PMHGPNAM - Description     (PMHGxxxx record)              *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   Exitchar Entered                               *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Modifications :                                                           *
.*        V9.02.00  12/12/2002  J.Tan                                         *
.*                  Ported from r5.10                                         *
.*                  14/11/2002 J.Tan                                          *
.*                  Mods for v9.00                                            *
.*                  13/05/93 J.Tan    SRF440035                               *
.*                  Mods to display message field is mandatory                *
.*                  28/10/1993    Justin Coates  QUOTE 440017 SRF 440204      *
.*                  allow for new GP practice file                            *
.*                  07/03/94 I.Rutt                                           *
.*                  Renamed to PATGPPKY.PBL                                   * 
.*                  10/10/1994  bez              SRF 441120                   *
.*                  Validate that GP belongs to practice.                     *
.*                  23/03/1995  Paul Howells     SRF 135691 & 135690          *
.*                  We Don't want to check whether the GP Practice is linked  *
.*                  to a GP, if we are in the GP Practice Maintenance!!       *
.******************************************************************************
PMSHCGKY
PTGPKY00  IF        CKYIMODE = 1
            MOVE      ONE,CKYIDEF2
          ENDIF
          COMPARE   ONE,CKYIMAND
          GOTO      PTGPKY03 IF EQUAL
.
          PACK      PMHGPRAC,CKYIDEF6,SP70
          MOVE      CKYIDEF2,PMHGCNTR
          KEYIN     *PCCOL:CVERT,*V2LON,*RV,PMHGPRAC:
                    *PCCOL:CVERT,*DV,PMHGPRAC;
          GOTO      PTGPKY04   
.
PTGPKY03  PACK      PMHGPRAC,CKYIDEF6,SP70
          MOVE      CKYIDEF2,PMHGCNTR
          DISPLAY   *PCCOL:CVERT,SP9
.
          KEYIN     *PCCOL:CVERT,*V2LON,*RV,PMHGPRAC:
                    *PCCOL:CVERT,*DV,PMHGPRAC;
PTGPKY04  PACK      PMHGPRAC,PMHGPRAC,SP70
          MOVE      CKYIDEF2,PMHGCNTR
          MATCH     SP10,PMHGPRAC
          GOTO      PTGPKY10 IF NOT EQUAL   * code entered
.
          BRANCH    CKYIMAND,PTGPKY08        * field is mandatory
.
PTGPKY06  MOVE      ONE,EXIT                * nothing input
          UNPACK    SP20,CKYIDEF6,CKYIDEF2
          PACK      PMHGPNAM,SP30,SP30,SP30
          PACK      PMHGADD1,SP30,SP30,SP30
          PACK      PMHGPTEL,SP30,SP30,SP30
          GOTO      PTGPKY98
.
PTGPKY08  DISPLAY   *P1:24,*B,*EL,"Field is mandatory.  ";
          CALL      HITENTER
          GOTO      PTGPKY00
.
. Exitchar input  ?
.
PTGPKY10  MATCH     EXITCHAR,PMHGPRAC
          IF        @EQUAL
            MOVE      TWO,EXIT
            GOTO      PTGPKY98
          ENDIF
.
. "?" input ?
.
          MATCH     QUEST,PMHGPRAC
          GOTO      PTGPKY50 IF EQUAL       * ? entered
.
. ------- keyin the count -------
.
PTGPKY30  ASSIGN    (CCOL+6),CCOL1
          DISPLAY   *PCCOL1:CVERT,"-"
          ADD       ONE,CCOL1
          KEYIN     *PCCOL1:CVERT,*V2LON,*RV,PMHGCNTR:
                    *PCCOL1:CVERT,*DV,PMHGCNTR
          MOVE      PMHGCNTR,FORM2
          COMPARE   ZERO,FORM2
          IF        @EQUAL
            DISPLAY   *PCCOL1:CVERT,SP2
            GOTO      PTGPKY00
          ENDIF
.
.         check if input is on file
.
          PACK      KEY12,PMHGPRAC,PMHGCNTR
          UNPACK    PMHGPRAC,CKYIDEF6
          MOVE      PMHGCNTR,CKYIDEF2
          CALL      VLPCT000
          BRANCH    EXIT,PTGPKY00
.
          PACK      CKYIDEF6,PMHGPRAC,SP70
          MOVE      PMHGCNTR,CKYIDEF2
          MOVE      ZERO,EXIT
          GOTO      PTGPKY98                    * valid input
.
. a "?" was input
.
PTGPKY50  MOVE      ZERO,HLEF
          MOVE      CVERT,SVERT
          MOVE      CCOL,SCCOL
          CALL      GETSCR00
PTGPKY60  PACK      KEY8,CKYIDEF8,SP70                * referring GP
          CALL      PMSHCGDS
          MOVE      SVERT,CVERT
          MOVE      SCCOL,CCOL
          MOVE      PMHGPRAC,CKYIDEF6
          MOVE      PMHGCNTR,CKYIDEF2
          IF        EXIT=1
            BRANCH    CKYIMAND,PTGPKY80        * field is mandatory
            PACK      PMHGPNAM,SP30,SP30,SP30  * Exit was selected
            PACK      PMHGADD1,SP30,SP30,SP30
            PACK      PMHGPTEL,SP30,SP30,SP30
            MOVE      SP10,CKYIDEF6
            MOVE      SP2,CKYIDEF2
            MOVE      ONE,EXIT                * nothing input
            GOTO      PTGPKY76
          ENDIF
.
          CALL      VLPCT000                * validate practice
          BRANCH    EXIT,PTGPKY80           * invalid codes
.
          MOVE      ZERO,EXIT
.
PTGPKY76  CALL      PUTSCR00
          MOVE      SVERT,CVERT
          MOVE      SCCOL,CCOL
          DISPLAY   *PCCOL:CVERT,*V2LON,CKYIDEF6,SLASH,CKYIDEF2;
          GOTO      PTGPKY98
.
PTGPKY80  CALL      PUTSCR00
          MOVE      SVERT,CVERT
          MOVE      SCCOL,CCOL
          GOTO      PTGPKY00
.
PTGPKY98  RETURN
+
.*********************************************************************
.         validate the practice
.*********************************************************************
VLPCT000  
.-------- check if practice on file
.
          PACK      PMHGPRAC,CKYIDEF6,SP70
          PACK      KEY12,PMHGPRAC,CKYIDEF2
          CALL      RDPMHCG1
          IF        OVRCD = 1
            IF        CKYIMODE <> 1
              DISPLAY   *P1:24,*B,*EL,"GP Practice is not on File. ";
              CALL      HITENTER
              GOTO      VLPCT900
            ELSE
              MOVE      ONE,CKYIDEF2         * insert mode
              GOTO      VLPCT800
            ENDIF
          ELSE
            IF        CKYIMODE = 1
              DISPLAY   *P1:24,*EL,*B,"GP Practice Already Exists. ";
              CALL      HITENTER
              GOTO      VLPCT900
            ENDIF 
          ENDIF
.
. --------- check if gp is linked to a practice if flag is on
.
          IF        CKLINKED = 0
            PACK      PMHLHCPC,CKYIDEF8,SP70
            PACK      PMHLHCPR,CKYIDEF6,SP70
            PACK      KEY20,PMHLHCPC,PMHLHCPR
            CALL      RDPMHCL1
            IF        OVRCD = 1
              DISPLAY *P1:24,*B,*EL,"The GP is not with this Practice. ";
              CALL    HITENTER
              GOTO    VLPCT900
            ENDIF
          ENDIF
.
          COMPARE     ZERO,CKACTIVE          * check for inactive?
          GOTO        VLPCT800 IF EQUAL      * no
.
          MATCH     "1",PMHLSTAT
          IF        @EQUAL
            DISPLAY   *P1:24,*B,*EL,"GP/practice is not Active. ";
            CALL      HITENTER
            GOTO      VLPCT900
          ENDIF
.
VLPCT800  MOVE      ZERO,EXIT                * valid
          GOTO      VLPCT999
.
VLPCT900  MOVE      ONE,EXIT                 * invalid
.
VLPCT999  RETURN
.
+
          INCLUDE   PMSHCGDS 
+
