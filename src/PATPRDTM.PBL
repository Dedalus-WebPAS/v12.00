. *----------------------------------------------------------------------
. * Include Name  : PATPRDTM.PBL
. *
. * Function      : Routines for Provisional DRG via CMBS Item Code (patprdaf)
. *
. * Modifications :
. *
. *      V11.03.01  19/12/2022  Bella Turco    TSK 0921957
. *                 Added NOMORE00 & PTCNNRTD to maintenance lists
. *      V10.13.01  31/07/2018  Don Nguyen     TSK 0851002
. *                 Modified DRGMBS00 to ignore blank values when matching
. *      V10.12.00  20/07/2018  Don Nguyen     TSK 0851002
. *                 Created include
. *----------------------------------------------------------------------
.-----------------------------------------------
.         Clear file variables
.-----------------------------------------------
CLPATPRD  MOVE      SP70,PTPDCMBS
          MOVE      SP70,PTPDPDRG
          MOVE      SP70,PTPDWGHT
          MOVE      SP70,PTPDPSEX
          MOVE      SP70,PTPDSDAY
          MOVE      SP70,PTPDAGEF
          MOVE      SP70,PTPDAGET
          MOVE      SP70,PTPDACTV
          MOVE      SP70,PTPDSPAR
.
          RETURN
+
.-----------------------------------------------
.         Move cgi values into file variables
.-----------------------------------------------
MVPTPD00  MATCH     PTPRD001,Z70
          IF        !@EQUAL
            MOVE      PTPRD001,PTPDCMBS
          ENDIF
.
          MATCH     PTPRD002,Z70
          IF        !@EQUAL
            MOVE      PTPRD002,PTPDPDRG
          ENDIF
.
          MATCH     PTPRD003,Z70
          IF        !@EQUAL
            MOVE      PTPRD003,PTPDWGHT
            RJUSTIFY  PTPDWGHT
          ENDIF
.
          MATCH     PTPRD004,Z70
          IF        !@EQUAL
            MOVE      PTPRD004,PTPDPSEX
          ENDIF
.
          MATCH     PTPRD005,Z70
          IF        !@EQUAL
            MOVE      PTPRD005,PTPDSDAY
          ENDIF
.
          MATCH     PTPRD006,Z70
          IF        !@EQUAL
            MOVE      PTPRD006,PTPDAGEF
            RJUSTIFY  PTPDWGHT
          ENDIF
.
          MATCH     PTPRD007,Z70
          IF        !@EQUAL
            MOVE      PTPRD007,PTPDAGET
            RJUSTIFY  PTPDWGHT
          ENDIF
.
          MATCH     PTPRD008,Z70
          IF        !@EQUAL
            MOVE      PTPRD008,PTPDACTV
          ENDIF
.
MVPTPD99  RETURN
+
.------------------------------------------------------------------------------
.   Set Parameters for pmsihuaf
.------------------------------------------------------------------------------
SPTPD000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPTPD001,SPTPD002,SPTPD003,SPTPD004,SPTPD005:
                       SPTPD006,SPTPD007,SPTPD008
.
          MOVE      ONE,EXIT
          GOTO      SPTPD999
.
SPTPD001  PACK      PTPRD001,QRYDATA,SP70
          GOTO      SPTPD999
.
SPTPD002  PACK      PTPRD002,QRYDATA,SP70
          GOTO      SPTPD999
.
SPTPD003  PACK      PTPRD003,QRYDATA,SP70
          GOTO      SPTPD999
.
SPTPD004  PACK      PTPRD004,QRYDATA,SP70
          GOTO      SPTPD999
.
SPTPD005  PACK      PTPRD005,QRYDATA,SP70
          GOTO      SPTPD999
.
SPTPD006  PACK      PTPRD006,QRYDATA,SP70
          GOTO      SPTPD999
.
SPTPD007  PACK      PTPRD007,QRYDATA,SP70
          GOTO      SPTPD999
.
SPTPD008  PACK      PTPRD008,QRYDATA,SP70
          GOTO      SPTPD999
.
SPTPD999  RETURN
+
.------------------------------------------------------------------------------
.         Display (tag output) for patprdaf
.------------------------------------------------------------------------------
PTPD0000  BRANCH    FLDITMNO,PTPD0010,PTPD0020,PTPD0030,PTPD0040,PTPD0050:
                             PTPD0060,PTPD0070,PTPD0080,PTPD0090,PTPD0100:
                             PTPD0110,PTPD0120
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      PTPD9999
.
PTPD0010  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,PTPD0011
.
          WRITE     HTMLFILE;PTPDCMBS;      * CMBS Item Code
          GOTO      PTPD9999
.
PTPD0011  PACK      KEY17,PTPDCMBS,SP10
          CALL	    PATITMRD
          BRANCH    OVRCD,PTPD9999
.
          WRITE     HTMLFILE;IDESC;         * CMBS Item Description
          GOTO      PTPD9999
.
PTPD0020  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,PTPD0021
.
          WRITE     HTMLFILE;PTPDPDRG;      * Provisional DRG
          GOTO      PTPD9999
.
PTPD0021  MOVE      "999",KEY3
          PACK      KEY7,PTPDPDRG,KEY3,SP10      * Get last DRG version
          CALL      RSPTDGW1
          CALL      RPPTDGW1
          BRANCH    OVRCD,PTPD9999
.
          MATCH     PTDWDRGC,PTPDPDRG
          GOTO      PTPD9999 IF NOT EQUAL
.
          WRITE     HTMLFILE;PTDWDESC;      * Provisional DRG Description
          GOTO      PTPD9999
.
PTPD0030  WRITE     HTMLFILE;PTPDWGHT;      * Weighted Value
          GOTO      PTPD9999
.
PTPD0040  WRITE     HTMLFILE;PTPDPSEX;      * Patient Sex
          GOTO      PTPD9999
.
PTPD0050  WRITE     HTMLFILE;PTPDSDAY;      * Sameday Only Visit
          GOTO      PTPD9999
.
PTPD0060  WRITE     HTMLFILE;PTPDAGEF;      * Valid Age Range From
          GOTO      PTPD9999
.
PTPD0070  WRITE     HTMLFILE;PTPDAGET;      * Valid Age Range To
          GOTO      PTPD9999
.
PTPD0080  WRITE     HTMLFILE;PTPDACTV;      * Active
          GOTO      PTPD9999
.
PTPD0090  CALL      PDROW000                * Table Rows (List)
          GOTO      PTPD9999
.
PTPD0100  CALL      PREVPD00                * Previous Button Link
          GOTO      PTPD9999
.
PTPD0110  CALL      NEXTPD00                * Next Button Link
          GOTO      PTPD9999
.
PTPD0120  CALL      DRGMBS00           * Provisional DRG by CMBS Search Results
          GOTO      PTPD9999
.
PTPD9999  RETURN
+
.------------------------------------------------------------------------------
.         Output table (list) rows
.------------------------------------------------------------------------------
PDROW000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY13,STARTKEY,SP70
          CALL      RSPTPRD1
PDROW100  CALL      RKPTPRD1
          BRANCH    OVRCD,PDROW090
.
          ADD       ONE,RECNUMB
          IF        STARTNUM >= RECNUMB
            GOTO      PDROW100
          ENDIF
.
          MOVE      SP10,TDESC
          MATCH     "M",PTPDPSEX
          IF        @EQUAL
            MOVE      "Male",TDESC
          ENDIF
          MATCH     "F",PTPDPSEX
          IF        @EQUAL
            MOVE      "Female",TDESC
          ENDIF
          MATCH     "R",PTPDPSEX
          IF        @EQUAL
            MOVE      "Other",TDESC
          ENDIF
          MATCH     "U",PTPDPSEX
          IF        @EQUAL
            MOVE      "Unknown",TDESC
          ENDIF
          MATCH     "I",PTPDPSEX
          IF        @EQUAL
            MOVE      "Intermediate",TDESC
          ENDIF
.
          MOVE      SP10,DIM3
          MATCH     "0",PTPDSDAY
          IF        @EQUAL
            MOVE      "No",DIM3
          ENDIF
          MATCH     "1",PTPDSDAY
          IF        @EQUAL
            MOVE      "Yes",DIM3
          ENDIF
.
          MOVE      SP10,D9
          MATCH     "0",PTPDACTV
          IF        @EQUAL
            MOVE      "Active",D9
          ENDIF
          MATCH     "1",PTPDACTV
          IF        @EQUAL
            MOVE      "Inactive",D9
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail(":
                             "#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&ptprd001=",PTPDCMBS:
                             "&ptprd002=",PTPDPDRG,"#")'>":
                             "<img src='../images/MaintenanceIcon.gif'":
                             " hspace=4 border=0 align=absmiddle></a>":
                             PTPDCMBS,"</td>":
                             "<td>&nbsp;",PTPDPDRG,"</td>":
                             "<td>&nbsp;",PTPDWGHT,"</td>":
                             "<td>&nbsp;",TDESC,"</td>":
                             "<td>&nbsp;",DIM3,"</td>":
                             "<td>&nbsp;",PTPDAGEF,"</td>":
                             "<td>&nbsp;",PTPDAGET,"</td>":
                             "<td>&nbsp;",D9,"</td>";
          WRITE     HTMLFILE;"</tr>"
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      PDROW100 IF NOT EQUAL
          GOTO      PDROW099
.
PDROW090  CALL      NOMORE00      * Display No More Records Message
.
PDROW099  RETURN
+
.------------------------------------------------------------------------------
.         Previous Button
.------------------------------------------------------------------------------
PREVPD00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          WRITE     HTMLFILE;"patweb88.pbl":
                                 "?reportno=",F2:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&startkey=",STARTKEY;
          GOTO      PREVPD99
.
PREVPD99  RETURN
+
.------------------------------------------------------------------------------
.         Next Button
.------------------------------------------------------------------------------
NEXTPD00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          WRITE     HTMLFILE;"patweb88.pbl":
                                 "?reportno=",F2:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&startkey=",STARTKEY;
          GOTO      NEXTPD99
.
NEXTPD99  RETURN
+
.------------------------------------------------------------------------------
.         Output table (list) rows for Provisional DRG via CMBS Item.
.         Sorted by descending Weighted Value.
.------------------------------------------------------------------------------
DRGMBS00  PACK      KEY16,PTPRD001,Z70
          CALL      RSPTPRD2
DRGMBS10  CALL      RPPTPRD2
          BRANCH    OVRCD,DRGMBS99
.
          MATCH     PTPDCMBS,PTPRD001       * same CMBS Item Code?
          GOTO      DRGMBS99 IF NOT EQUAL
.
.         Validate Active code?
          MATCH     "0",PTPDACTV            * active?
          GOTO      DRGMBS10 IF NOT EQUAL   * skip if not
.
.         Get the last matching DRG version for this Provisional DRG
          MOVE      "999",KEY3
          PACK      KEY7,PTPDPDRG,KEY3,SP10
          CALL      RSPTDGW1
          CALL      RPPTDGW1
          BRANCH    OVRCD,DRGMBS10
.
          MATCH     PTDWDRGC,PTPDPDRG       * matching DRG code?
          GOTO      DRGMBS10 IF NOT EQUAL   * skip if not
.
.
.         Validate Patient Sex? (if not blank)
DRGMBS11  MATCH     SP70,PTPDPSEX
          GOTO      DRGMBS12 IF EQUAL  
.
          MATCH     PTPDPSEX,PTPRD004       * matching patient sex?
          GOTO      DRGMBS10 IF NOT EQUAL   * skip if not
.
.
.         Validate Sameday Only Visit? (if not blank)
DRGMBS12  MATCH     SP70,PTPDSDAY
          GOTO      DRGMBS13 IF EQUAL  
.
          MATCH     SP70,PTPRD005           * Blank Sameday Only flag?
          IF        !@EQUAL
            MOVE      ZERO,DIM1
            MATCH     "2",PTPRD005          * Same Day visit?
            IF        @EQUAL
              MOVE      ONE,DIM1
            ENDIF
.
            MATCH     PTPDSDAY,DIM1         * Sameday Only Code?
            GOTO      DRGMBS10 IF NOT EQUAL * skip if not matching
          ENDIF
.
.
.         Validate Patient Age is within Valid Age Range
DRGMBS13  MOVE      ZERO,FORM3
          RJUSTIFY  PTPRD007
          MOVE      PTPRD007,FORM3
.
          MOVE      ZERO,AGERNGFR           * default age range From value
          MOVE      PTPDAGEF,AGERNGFR       * Age Range From
.
          MOVE      "999",AGERNGTO          * default age range To value
          MOVE      PTPDAGET,AGERNGTO       * Age Range To
.
          COMPARE   AGERNGFR,FORM3          * Patient Age < Range From value?
          GOTO      DRGMBS10 IF LESS        * skip
.
          COMPARE   FORM3,AGERNGTO          * Range To value < Patient Age?
          GOTO      DRGMBS10 IF LESS        * skip
.
.
DRGMBS20  WRITE     HTMLFILE;"<tr valign=#"top#">":
                             "<td>&nbsp;&nbsp;<a href=#"":
                             "javascript:updateParent('",PTPDPDRG,"')#">":
                             PTPDCMBS,"</a></td>":
                             "<td>&nbsp;&nbsp;",PTPDWGHT,"</td>":
                             "<td>&nbsp;&nbsp;",PTPDPDRG:
                             "&nbsp;&nbsp;&nbsp;&nbsp;",PTDWDESC,"</td>":
                             "</tr>"
.
          GOTO      DRGMBS10
.
DRGMBS99  RETURN
+
