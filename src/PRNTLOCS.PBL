.**************************************************************************
.*                             PRNTLOCS              Called by:           *
.*              Display's which departments use any specific printer      *
.*                                                                        *
.**************************************************************************
.
PRPR0000  MOVE      ONE,CPAGENO
          MOVE      SEVEN,CVERT
          MOVE      FIVE,CCOL
          MOVE      ZERO,FIELD
.
          PACK      KEY3,ANS3,SP3
          CALL      RDPRTA1                  * obtain desc for display
          BRANCH    OVRCD,PRPR9000
.
          DISPLAY   *P41:4,*EF,PRTDESC,*P4:6,"Code",SP2,"Description"
          PACK      KEY5,SP5
          CALL      RDSICOD1                 * position in file
PRPR1000  CALL      RDKICOD1                 * read next record
          BRANCH    OVRCD,PRPR5000
.
          MATCH     ANS3,PRXCODE             * same printer code ?
          GOTO      PRPR1000 IF NOT EQUAL
.
          ADD       ONE,CVERT
          COMPARE   TWENTY2,CVERT
          IF        @EQUAL
            COMPARE   FIFTY,CCOL
            IF        @EQUAL
              GOTO      PRPR4000
            ELSE
              MOVE      FIFTY,CCOL           * move column across for displays
              MOVE      SEVEN,CVERT
              DISPLAY   *P49:6,"Code",SP2,"Description"
              GOTO      PRPR1000
            ENDIF
          ELSE
            GOTO      PRPR3000
          ENDIF
.
PRPR3000  ADD       ONE,FIELD
          MOVE      PRXCATA,PRDIM2           * save current data
          MOVE      PRXCODE,PRDIM3           * save current data
          PACK      KEY5,PRXCATA,SP2
          CALL      RDICOD1
          DISPLAY   *PCCOL:CVERT,*V2LON,PRXCATA,SP3,*HOFF,PRXDESC
          PACK      TMPKEY[FIELD],PRXCATA,PRXCODE
          PACK      KEY5,PRDIM2,PRDIM3       * go back to previous location 
          CALL      RDICOD1
          GOTO      PRPR1000
.
.  new page needed
.
PRPR4000  BRANCH    CPAGENO,PRPR4500
.
.  middle pages  :  Next & First options
.
PRPR4100  MOVE      "1",CALLPOSN
          CALL      KEYD0000
          BRANCH    EXIT,PRPR9999:            * exit
                         PRPR9999:            * search again
                         PRPR7000:            * next
                         PRPR8000             * previous
          GOTO      KEYD0000
.
.  first page  :  Next option
.
PRPR4500  MOVE      "2",CALLPOSN
          CALL      KEYD0000
          BRANCH    EXIT,PRPR9999:            * exit
                         PRPR9999:            * search again
                         PRPR7000:            * next
                         PRPR5900             * previous
          GOTO      KEYD0000
.
.  end of data 
.
PRPR5000  BRANCH    CPAGENO,PRPR5500
.
.  middle pages  :  First option
.
PRPR5100  MOVE      "3",CALLPOSN
          CALL      KEYD0000
          BRANCH    EXIT,PRPR9999:            * exit
                         PRPR9999:            * search again
                         PRPR5900:            * next
                         PRPR8000             * previous
          GOTO      KEYD0000
.
.  first page  :  No options
.
PRPR5500  MOVE      "4",CALLPOSN
          CALL      KEYD0000
          BRANCH    EXIT,PRPR9999:            * exit
                         PRPR9999:            * search again
                         PRPR5900:            * next
                         PRPR5900             * previous
          GOTO      KEYD0000
.
PRPR5900  BEEP
PRPR5910  BRANCH    CALLPOSN,PRPR4100,PRPR4500,PRPR5100,PRPR5500
.
.   number entered
.
PRPR7000  MOVE      EIGHT,CVERT
          MOVE      FIVE,CCOL
          ADD       ONE,CPAGENO
          DISPLAY   *P1:CVERT,*EF
          MOVE      ZERO,FIELD
          GOTO      PRPR3000
.
.   previous screen selected
.
PRPR8000  MOVE      TMPKEY[1],KEY5
          CALL      RDICOD1                   * reread first record on screen
          MOVE      ZERO,FORM2                * initialise count
          WHILE     FORM2 <= TWENTY8
            CALL      RDPICOD1                * read previous record
            ADD       ONE,FORM2               * increment count
          DO
.
          MOVE      EIGHT,CVERT
          MOVE      FIVE,CCOL
          SUB       ONE,CPAGENO
          MOVE      ZERO,FIELD
          DISPLAY   *P1:CVERT,*EF
          GOTO      PRPR3000
.
PRPR9000  DISPLAY   *P1:8,*EF,*P1:24,*EL,"Invalid printer code. ";
          CALL      HITENTER
          MOVE      TWO,EXIT
.
PRPR9999  RETURN



KEYD0000  DISPLAY   *P1:24,*EF,"e(",*V2LON,ANSX,*HOFF,")it, ":
                    "next (",*V2LON,ANSP,*HOFF,")rinter";
          MOVE      TWENTY3,CCOL
          IF        CALLPOSN = 1|CALLPOSN = 2
            DISPLAY   ", (",*V2LON,ANSN,*HOFF,")ext";
            ADD       EIGHT,CCOL
          ENDIF
          IF        CALLPOSN = 1|CALLPOSN = 3
            DISPLAY   ", p(",*V2LON,ANSR,*HOFF,")evious";
            ADD       TEN2,CCOL
          ENDIF
.
          DISPLAY   " :  ";
          ADD       FIVE,CCOL
.
KEYD1000  KEYIN     *PCCOL:24,*DV,UNDLN1:
                    *PCCOL:24,*V2LON,*JR,DIM1:
                    *PCCOL:24,*DV,DIM1
          UNPACK    DIM1,ANS
          REP       UPPLOW,ANS
.
          MATCH     ANSX,ANS                * exit selected ?
          GOTO      KEYD9100 IF EQUAL
.
          MATCH     ANSP,ANS                * search again selected ?
          GOTO      KEYD9200 IF EQUAL
.
          MATCH     ANSN,ANS                * next selected ?
          GOTO      KEYD9300 IF EQUAL
.
          MATCH     ANSR,ANS                * previous selected ?
          GOTO      KEYD9400 IF EQUAL
.
KEYD1500  BEEP
          GOTO      KEYD1000
.
KEYD9100  MOVE      ONE,EXIT                * exit                
          GOTO      KEYD9999
.
KEYD9200  MOVE      TWO,EXIT                * search again
          GOTO      KEYD9999
.
KEYD9300  MOVE      THREE,EXIT              * next
          GOTO      KEYD9999
.
KEYD9400  MOVE      FOUR,EXIT               * previous
          GOTO      KEYD9999
.
KEYD9999  RETURN
+













