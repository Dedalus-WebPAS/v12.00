. *****************************************************************************
. * System     : Common Source Code                                           *
. * Filename   : DMYCON                                                       *
. * Function   : Convert a gregorian date to julian format (DDDYYCC)          *
. *****************************************************************************
. * Author     : Graeme Williams                                              *
. * Date       : 27/09/1991    (Rewrite to include century)                   *
. * Includes   : JULCALEN                                                     *
. * Mods       :                                                              *
. * 10.03.01 06/02/2012 Peter McMullen  CAR 259587                            *
. *                     Alter routine DOFYR000 to calculate first day         *
. *****************************************************************************
.
. *****************************************************************************
. * Subroutine : DMYCON                                                       *
. * Function   : Convert a gregorian date to julian format (DDDYYCC)          *
. * Parameters : DD       - Day of month      (eg. 23 = 23rd of month)        *
. *              MM       - Month of year     (eg. 11 = November)             *
. *              YY       - Year of century   (eg. 89 )                       *
. *              CC       - Century           (eg. 19 = 1900 to 1999)         *
. * Return     : JULDAY   - Julian day of year                                *
. *              JULYR    - Julian year (YY)                                  *
. *              JULCC    - Julian century (CC)                               *
. *****************************************************************************
. 
DMYCON    CALL      DMLEAPYR                  * determine if a leap year
.
.        Get the number of days prior to the start of the month
.
CJD2      LOAD      JULDAY FROM MM OF JJAN,JFEB,JMAR:
                    JAPR,JMAY,JJUN,JJUL,JAUG,JSEP:
                    JOCT,JNOV,JDEC
.
.         Special case, allow for the current year being a leap year
.
          COMPARE   THREE,MM                  * march or later ?
          GOTO      CJD3 IF LESS              * no, don't worry about leapyears
.
.         The leap year flag is "1" for a leap year, and "0" otherwise. Thus
.         we can use this flag as the number of days extra in the current year.
.
          ADD       LEAPYR,JULDAY
.
.         We have the number of days before the start of the month. Just
.         add the number of days in the month
.
CJD3      ADD       DD,JULDAY
          MOVE      YY,JULYR                 * set the year
          MOVE      CC,JULCC                 * set the century
          RETURN
.
. *****************************************************************************
. * Subroutine : DMLEAPYR                                                     *
. * Function   : Set the leap year flag if current year is a leap year        *
. * Parameters : YY       - Julian year (YY)                                  *
. *              CC       - Julian century (CC)                               *
. * Returns    : LEAPYR   - Leap year flag  0 = No, 1 = Yes                   *
. *****************************************************************************
.
DMLEAPYR  MOVE      YY,JULWK                 * work variable
.
.         Check if the year is divisible by four. If it isn't, then this is not
.         a leap year.
.
          DIV       FOUR,JULWK               * divide by four, truncating
          MULT      FOUR,JULWK               * multiple to get modulus
.
          COMPARE   YY,JULWK                 * are they still the same ?
          GOTO      DMLEAP90 IF NOT EQUAL    * no, this is not a leap year
.
.         The year is divisible by four. We probably have a leap year. However
.         there is one special case to handle. If the year is divisible by
.         100, and not 400, then this is NOT a leap year (the last time this
.         happenned was in 1900, then next time is 2100).
.
          COMPARE   ZERO,YY                  * year 0 (divisible by 100) ?
          GOTO      DMLEAP80 IF NOT EQUAL    * no, this is a leap year
.
          MOVE      CC,JULWK                 * work variable
.
          DIV       FOUR,JULWK               * divide by four, truncating
          MULT      FOUR,JULWK               * multiple to get modulus
.
          COMPARE   CC,JULWK                 * are they still the same ?
          GOTO      DMLEAP90 IF NOT EQUAL    * no, this is not a leap year
.
.         This is a leap year
.
DMLEAP80  MOVE      ONE,LEAPYR
          GOTO      DMLEAP99
.
.         This is not a leap year
.
DMLEAP90  MOVE      ZERO,LEAPYR
.
DMLEAP99  RETURN
.**********************************************************************
.*                               DOFYR000                             *
.*        Routine to get the starting day of a given Year 1900-9999   *
.**********************************************************************
DOFYR000  MOVE      KEY4,FORM4
          MOVE      ONE,EXIT
.
          MOVE      "1900",F4    * Start from  1900
          MOVE      ONE,FSTDAY   * first day is Monday
          COMPARE   F4,FORM4
          GOTO      DOFYR999 IF  LESS
.
          IF        FORM4 > 1999
            MOVE      "2000",F4   * start from 2000
            MOVE       SIX,FSTDAY * first day was Saturday
          ENDIF
.
          MOVE      ZERO,EXIT
DOFYR010  COMPARE   F4,FORM4
          GOTO      DOFYR999 IF EQUAL
.
. loop through years advancing first day by one or two days
.
          MOVE      F4,KEY4      * check if working year is a leap year
          UNPACK    KEY4,DIM2,KEY2
          MOVE      DIM2,CC
          MOVE      KEY2,YY
          CALL      DMLEAPYR
          ADD       ONE,FSTDAY     * first day advances one day most years
          ADD       LEAPYR,FSTDAY  * and another day in leap years
          ADD       ONE,F4         * increment the working year
.
          IF        FSTDAY > 7
            SUB       SEVEN,FSTDAY
          ENDIF
          GOTO      DOFYR010
.
DOFYR999  RETURN
.
