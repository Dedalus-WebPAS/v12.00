.******************************************************************************
.*  Include Name  : PATCODKY.PBL                                              *
.*  Description   : Keyin of a codes file entry in V5.01                      *
.*  Author        : ROD                                                       *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CODE     - category of code                               *
.*                  CKYIDEF3 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                                                                            *
.*  Returns       : ACODE    - Keyed in code                                  *
.*                  TDESC    - Description                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Mods          : 22/03/1994  Steve Armstrong                               *
.*                              Changed routine from PATCODKY to PATCODKY     *
.*                  28/09/1994  Rob Leonard  SRF 118380 (Bul 29)              *
.*                              Check if Code Inactive                        *
.******************************************************************************
PATCODKY  
PCODE010  MATCH     SP3,CKYIDEF3                  * using a default ?
          GOTO      PCODE050 IF NOT EQUAL   
.
          MATCH     "J ",CODE
          IF        @EQUAL
            KEYIN     *PECOL:EVERT,*DV,UNDLN2:      * no default used
                      *PECOL:EVERT,*V2LON,DIM2:
                      *PECOL:EVERT,*DV,DIM2
            PACK      ACODE,DIM2,SP5
          ELSE  
            KEYIN     *PECOL:EVERT,*DV,UNDLN3:      * no default used
                      *PECOL:EVERT,*V2LON,ACODE:
                      *PECOL:EVERT,*DV,ACODE
          ENDIF
.
          GOTO      PCODE080
.
PCODE050  MOVE      CKYIDEF3,ACODE                 * default
          MATCH     "J ",CODE
          IF        @EQUAL
            MOVE      CKYIDEF3,DIM2
            KEYIN     *PECOL:EVERT,*DV,DIM2:     * default used
                      *PECOL:EVERT,*V2LON,*RV,DIM2:
                      *PECOL:EVERT,*DV,DIM2
            PACK      ACODE,DIM2,SP5
          ELSE 
            KEYIN     *PECOL:EVERT,*DV,ACODE:     * default used
                      *PECOL:EVERT,*V2LON,*RV,ACODE:
                      *PECOL:EVERT,*DV,ACODE
          ENDIF
.
.         check what was entered
.
PCODE080  CALL      DCCOD000
          BRANCH    EXIT,PCODE100,PCODE705,PCODE805,PCODE905
.                        "?"      valid    nothing  exitchar
          GOTO      PCODE010
.
.         a "?" was input
.
PCODE100  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
PCODE105  CALL      PATCODDS
          BRANCH    CNEWDS,PCODE150
.
          CALL      PUTSCR00                * redisplay screen
          GOTO      PCODE010                * rekey code
.
.         now ask for code while leaving displayed codes on screen
.
PCODE150  MATCH     SP3,CKYIDEF3                  * using a default
          GOTO      PCODE200 IF NOT EQUAL
.
          MATCH     "J ",CODE
          IF        @EQUAL
            KEYIN     *P1:24,*EL,*DV,"Code    : ":
                      *P11:24,*DV,UNDLN2:      * no default used
                      *P11:24,*V2LON,DIM2:
                      *P11:24,*DV,DIM2
            PACK      ACODE,DIM2,SP5
          ELSE
            KEYIN     *P1:24,*EL,*DV,"Code    : ":
                      *P11:24,*DV,UNDLN3:      * no default used
                      *P11:24,*V2LON,ACODE:
                      *P11:24,*DV,ACODE
          ENDIF
.
          GOTO      PCODE300
.
PCODE200  MOVE      CKYIDEF3,ACODE
.
          MATCH     "J ",CODE
          IF        @EQUAL
            MOVE      CKYIDEF3,DIM2
            KEYIN     *P1:24,*EL,*DV,"Code    : ":
                      *P11:24,*DV,DIM2:     * default used
                      *P11:24,*V2LON,*RV,DIM2:
                      *P11:24,*DV,DIM2
            PACK      ACODE,DIM2,SP5
          ELSE
            KEYIN     *P1:24,*EL,*DV,"Code    : ":
                      *P11:24,*DV,ACODE:     * default used
                      *P11:24,*V2LON,*RV,ACODE:
                      *P11:24,*DV,ACODE
          ENDIF
.
.         repeat the validation of the code
.         check what was entered
.
PCODE300  CALL      DCCOD000
          BRANCH    EXIT,PCODE105,PCODE700,PCODE800,PCODE900
.                        "?"      valid    nothing  exitchar
.
          GOTO      PCODE150
.
.         a valid code was entered 
. 
PCODE700  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE
.
PCODE705  MOVE      FALSE,EXIT                    * valid input
          GOTO      PCODE999 
.
.         nothing was input
.
PCODE800  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE
.
PCODE805  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      PCODE999 
.
.         EXITCHAR input
.
PCODE900  CALL      FRESCR00                * free the screen saved
.
PCODE905  MOVE      TWO,EXIT                * EXITCHAR
.
PCODE999  RETURN
+
.*********************************************************************
.*                  DCCOD000                    Called by : PATCODKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
DCCOD000  ENDSET    ACODE
          APPEND    SP3,ACODE
          RESET     ACODE
.
          MATCH     EXITCHAR,ACODE
          GOTO      DCCOD600 IF EQUAL       * exit char
.
          MATCH     SP3,ACODE
          GOTO      DCCOD700 IF EQUAL       * nothing
.
          MATCH     QUEST,ACODE
          GOTO      DCCOD800 IF EQUAL       * ? mark
.
.         a code was entered so validate
. 
DCCOD500  PACK      KEY5,CODE,ACODE
          CALL      RDCODE1
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,DCCOD900          * invalid code
          ELSE
            COMPARE   ZERO,OVRCD
            GOTO      DCCOD900 IF EQUAL       * Code Exists
          ENDIF
.
          MATCH     PTCOACTV,ANSI             * inactive ?
          GOTO      DCCOD550 IF NOT EQUAL
.
          MOVE      ONE,HLEF                  * Inactive code
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*EL,*B,"Inactive code.  ";
          CALL      HITENTER
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      DCCOD999
.
DCCOD550  MOVE      TWO,EXIT                * valid code
          GOTO      DCCOD999
.
.         exitchar entered
.
DCCOD600  MOVE      FOUR,EXIT
          MOVE      SP3,ACODE
          MOVE      SP20,TDESC              * clear desc field
          GOTO      DCCOD999
.
.         nothing entered
.
DCCOD700  BRANCH    CKYIMAND,DCCOD950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP3,ACODE
          MOVE      SP20,TDESC              * clear desc field
          GOTO      DCCOD999
.
.         "?" entered
.
DCCOD800  MOVE      ONE,EXIT
          GOTO      DCCOD999
.
.         invalid code entered
.
DCCOD900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
.
          IF        CKYIMODE=0
            DISPLAY   *P1:24,*EL,*B,"Invalid code.  ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      DCCOD999
.
.         field is mandatory
.
DCCOD950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is mandatory.  ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
DCCOD999  RETURN
+
          INCLUDE   PATCODDS
.
+
