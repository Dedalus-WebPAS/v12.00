.************************************************************************
.*  Procedure: DGCLICUD  -  Send Cancel Emergency Visit Discharge       *
.*                                                                      *
.*  Author   : Steve Armstrong  (29/12/2005)  CAR 88829                 *
.*                                                                      *
.*  Mods     :                                                          *
.*                                                                      *
.************************************************************************
.*  V11.04.01 09/01/2024 Thanh T           TSK 0936263                  *
.*            Added OPEN/CLOSE PMSQPXA1 index                           *
.************************************************************************
.*  V10.13.01 24/10/2018  Steve Armstrong   TSK 0863512                 *
.*            Mods to write to pmsqviaf.                                *
.************************************************************************
.*  V10.03.02 06/06/2013  Steve Armstrong   CAR 268961                  *
.*            Mods to populate H7CIRTAG & H7CISTAG                      *
.*  V10.03.01 17/04/2013  Davin S          CAR 280425                   *
.*            Changed IBCNUCIS - value 1 or greater will write trigger  *
.************************************************************************
.*  V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                  *
.*            Added setting of H7CITRID & H7CIINCL                      *
.************************************************************************
          DEFPROC   DGCLICUD
.
          INC       EMRQUEFD/INC
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
          INC       PMSQPTFD/INC
          INC       PMSQVIFD/INC
.
CISMFLAG  FORM      1             * CIS message flag
.                                    0 = don't send HL7 CIS message
.                                    1 = send HL7 CIS message
IBAMFLAG  FORM      1             * IBA proprietary message flag
.                                    0 = don't send IBA proprietary message
.                                    1 = send IBA proprietary message
MESSAGID  DIM       20
.
DGCLI000  MATCH     ZEROUR,EMVIURNO              * "0" U/R ?
          GOTO      DGCLI999 IF EQUAL            * yes - don't send
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,EIGHTY9;*178,EMCNDGEC:   * sending CEC
                                     *203,EMCNA13M    * sending A13
.
          MOVE      ZERO,CISMFLAG                * set default interface flag
          MATCH     "1",IBCNUCIS                 * using CIS interface ?
          IF        !@LESS
            COMPARE   ONE,EMCNA13M               * yes - sending A13 ?
            IF        @EQUAL
              MOVE      ONE,CISMFLAG             * yes- set flag for CIS message
            ENDIF
          ENDIF
.
.         Check if we are sending any messages.
.
          IF        EMCNDGEC <> 1 & CISMFLAG <> 1
            GOTO      DGCLI999
          ENDIF
.
.         We are sending IBA Proprietary and/or CIS HL7 messages, so
.         first open the relevant holding tables
.
          MOVE      ZERO,IBAMFLAG
          IF        EMCNDGEC = 1
            OPEN      HL7INB01,"hl7inbaf"
            MOVE      ONE,IBAMFLAG
          ENDIF
          IF        CISMFLAG = 1
            OPEN      HL7CISA1,"hl7cisaf"
          ENDIF
          OPEN      PMSQPTA1,"pmsqptaf"
          OPEN      PMSQPXA1,"pmsqpxaf"
          OPEN      PMSQVIA1,"pmsqviaf"
          OPEN      EMRQUEA1,"emrqueaf"
.
          CALL      DGMESSID                    * Get Message ID, Date & Time
.
.         Write a record to pmsqptaf (holding patient details)
.
          MOVE      MESSAGID,PMQPMESI
          MOVE      SP8,PMQPOURN
          PACK      PMQPSPAR,SP30,SP30,SP20
          CALL      WRPMQPT1
.
.         Write a record to pmsqviaf (holding visit details)
.
          MOVE      MESSAGID,PMQVMESI
          MOVE      ONE,PMQVTRAN
          PACK      PMQVSPAR,SP70,SP30
          CALL      WRPMQVI1
.
.         Write a record to emrqueaf (holding visit details)
.
          MOVE      MESSAGID,EMQUMESI
          PACK      EMQUSPAR,SP30,SP30,SP30
          CALL      WREMQUE1
.
          COMPARE   ONE,CISMFLAG                 * sending CIS HL7 A13 message?
          IF        @EQUAL
            MOVE      "A13",H7CIMETP
            MOVE      HL7TRGID,H7CITRID
            MOVE      HL7INCLD,H7CIINCL
            MATCH     "HL7REGEN",PRGID
            IF        @EQUAL
              MOVE      HL7RCTAG,H7CIRTAG
              MOVE      HL7SFTAG,H7CISTAG
            ELSE
              MOVE      SP20,H7CIRTAG
              MOVE      SP3,H7CISTAG
            ENDIF
            CALL      WRCIS000                   * yes - write hl7cisaf record
          ENDIF
.
          COMPARE   ONE,EMCNDGEC                 * sending proprietary message?
          IF        @EQUAL
            MOVE      "CEC",H7INMETP
            CALL      WRINB000                   * yes - write hl7inbaf record
          ENDIF
.
          CLOSE     HL7INB01
          CLOSE     HL7CISA1
          CLOSE     PMSQPTA1
          CLOSE     PMSQPXA1
          CLOSE     PMSQVIA1
          CLOSE     EMRQUEA1
.
DGCLI999  GOTO      DGCLIEND                    * end procedure
.
.         IO's go in here
.
          INC       BRHLCOMN
          INC       CLPMSQPX
.
          INC       EMRQUEIO/INC
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       IBAOVRIO/INC
          INC       PMSQPTIO/INC
          INC       PMSQVIIO/INC
.
DGCLIEND  ENDPROC
