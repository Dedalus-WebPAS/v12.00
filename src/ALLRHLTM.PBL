. *----------------------------------------------------------------------
. * Include Name  :   ALLRHLTM.PBL
. *
. * Function      :   CGI variables for ALLRHLFD - allrhlaf.dat
. *----------------------------------------------------------------------
.  Modifications Done :
.
.  V11.01.01  05/07/2021  Ebon Clements TSK             
.             Output HDP values for all referrals episode health conditions
.  V10.14.01  01/05/2018  Ebon Clements TSK 0868837
.             Allow blank ALRHDATE
.  V10.10.01  06/04/2017  Ebon Clements TSK 0833052
.             Added more that one main health condition tag - ALRH0220
.  V10.02.02  05/05/2011  Ebon Clements CAR 239576
.             Removed referral in place from TBIRF000
.  V10.02.01  28/04/2011  Ebon Clements CAR 239576
.             Only show delete icon on last transtition record
.  V10.01.01  22/03/2011  Ebon Clements CAR 239576
.             Added transition care dates tag
.  V10.00.01  05/05/2010  Ebon Clements CAR 220289
.             Added referral in/out place
.  V9.12.02   13/11/2009  Saroeun Soeur CAR 207550
.             extended with new tags ALRH0060 to ALRH0140
.  V9.12.01   14/10/2009  Ebon Clements CAR 207550
.             Created include
. *----------------------------------------------------------------------
.------------------------------------------------------------------------
.   Clear Parameters for health condition file
.------------------------------------------------------------------------
.           
CPALRH00  MOVE      Z70,ALRHL001
          MOVE      Z70,ALRHL002
          MOVE      Z70,ALRHL003
          MOVE      Z70,ALRHL004
          MOVE      Z70,ALRHL005
          MOVE      Z70,ALRHL006
.
CPALRH99  RETURN
.
.------------------------------------------------------------
. Add health condition table file
.------------------------------------------------------------
ALRH0000  BRANCH    FLDITMNO,ALRH0010,ALRH0020,ALRH0030,ALRH0040:
                             ALRH0050,ALRH0060,ALRH0070,ALRH0080:
                             ALRH0090,ALRH0100,ALRH0110,ALRH0120:
                             ALRH0130,ALRH0140,ALRH0150,ALRH0160:
                             ALRH0170,ALRH0180,ALRH0190,ALRH0200:
                             ALRH0210,ALRH0220,ALRH0230,ALRH0240
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      ALRH9999 
.
ALRH0010  WRITE     HTMLFILE;ALRHVISN;
          GOTO      ALRH9999 
.                            
ALRH0020  WRITE     HTMLFILE;ALRHTYPE;
          GOTO      ALRH9999
.
ALRH0030  WRITE     HTMLFILE;ALRHUNIQ;
          GOTO      ALRH9999
.
ALRH0040  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,ALRH0041
.
          MATCH     "01",ALRHTYPE
          IF        @EQUAL
            PACK      KEY12,ALREDEPT,ALRHCODE,SP70
            CALL      RDALDIA1
            IF        OVRCD<>1
              WRITE     HTMLFILE;ALDIDESC;
            ENDIF
          ENDIF
.
          MATCH     "02",ALRHTYPE
          IF        @EQUAL
            PACK      KEY12,ALREDEPT,ALRHCODE,SP70
            CALL      RDALPRR1
            IF        OVRCD<>1
              WRITE     HTMLFILE;ALPRDESC;
            ENDIF
          ENDIF
          GOTO      ALRH9999
.
ALRH0041  WRITE     HTMLFILE;ALRHCODE;
          GOTO      ALRH9999
.
ALRH0050  CALL      TABMH000   * main health table
          GOTO      ALRH9999
.
ALRH0060                       * CALL      TABMI000
          GOTO      ALRH9999
.
ALRH0070  MATCH     ALRHDATE,SP70
          GOTO      ALRH9999 IF EQUAL
.
          UNPACK    ALRHDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR; 
          GOTO      ALRH9999
.
ALRH0080  PACK      KEY13,ADMISSNO,ZERO1,SP70
          CALL      RSALRHL1
ALRH0081  CALL      RKALRHL1
          BRANCH    OVRCD,ALRH0083
.
          MATCH     ADMISSNO,ALRHVISN
          GOTO      ALRH0083 IF NOT EQUAL
.
          MATCH     ZERO1,ALRHTYPE
          GOTO      ALRH0081 IF NOT EQUAL
.
ALRH0082  WRITE     HTMLFILE;"1";       * true record found - main health
          GOTO      ALRH9999
.
ALRH0083  WRITE     HTMLFILE;"0";       * false record not found - main health
          GOTO      ALRH9999
.
ALRH0090  CALL      TABOF000   * other factors table
          GOTO      ALRH9999
.
ALRH0100  PACK      KEY13,ADMISSNO,ZERO2,SP70
          CALL      RSALRHL1
ALRH0101  CALL      RKALRHL1
          BRANCH    OVRCD,ALRH0103
.
          MATCH     ADMISSNO,ALRHVISN
          GOTO      ALRH0103 IF NOT EQUAL
.
          MATCH     ZERO2,ALRHTYPE
          GOTO      ALRH0101 IF NOT EQUAL          
.
ALRH0102  WRITE     HTMLFILE;"1";       * other factors - true record found
          GOTO      ALRH9999
.
ALRH0103  WRITE     HTMLFILE;"0";
          GOTO      ALRH9999
.
ALRH0110  MOVE      "zU",CATEGORY
          MOVE      SP70,CODE
          CALL      CODITM00
          GOTO      ALRH9999
.          
ALRH0120  CALL      TABRF000  * referral out destination table
          GOTO      ALRH9999
.
ALRH0130  PACK      KEY13,ADMISSNO,ZERO3,SP70
          CALL      RSALRHL1
ALRH0131  CALL      RKALRHL1
          BRANCH    OVRCD,ALRH0133
.
          MATCH     ADMISSNO,ALRHVISN
          GOTO      ALRH0133 IF NOT EQUAL
.
          MATCH     ZERO3,ALRHTYPE
          GOTO      ALRH0131 IF NOT EQUAL
.
ALRH0132  WRITE     HTMLFILE;"1";       * referral destination - true record found
          GOTO      ALRH9999
.
ALRH0133  WRITE     HTMLFILE;"0";
          GOTO      ALRH9999
.
ALRH0140  CALL      RFDDES00
          GOTO      ALRH9999
.
ALRH0150  MOVE      "zV",CATEGORY
          MOVE      SP70,CODE
          CALL      CODITM00
          GOTO      ALRH9999
.          
ALRH0160  PACK      KEY13,ADMISSNO,ZERO4,SP70
          CALL      RSALRHL1
ALRH0161  CALL      RKALRHL1
          BRANCH    OVRCD,ALRH0163
.
          MATCH     ADMISSNO,ALRHVISN
          GOTO      ALRH0163 IF NOT EQUAL
.
          MATCH     ZERO4,ALRHTYPE
          GOTO      ALRH0161 IF NOT EQUAL
.
ALRH0162  WRITE     HTMLFILE;"1";       * referral In destination - true
          GOTO      ALRH9999
.
ALRH0163  WRITE     HTMLFILE;"0";
          GOTO      ALRH9999
.
ALRH0170  CALL      TBIRF000  * referral in destination table
          GOTO      ALRH9999
.
ALRH0180  PACK      KEY13,ADMISSNO,ZERO,FIVE,SP70
          CALL      RSALRHL1
ALRH0181  CALL      RKALRHL1
          BRANCH    OVRCD,ALRH0183
.
          MATCH     ADMISSNO,ALRHVISN
          GOTO      ALRH0183 IF NOT EQUAL
.
          MATCH     "05",ALRHTYPE
          GOTO      ALRH0181 IF NOT EQUAL
.
ALRH0182  WRITE     HTMLFILE;"1";     * transition dates - true record found
          GOTO      ALRH9999

ALRH0183  WRITE     HTMLFILE;"0";
          GOTO      ALRH9999
.
ALRH0190  MOVE      "zW",CATEGORY
          MOVE      SP70,CODE
          CALL      CODITM00
          GOTO      ALRH9999
.          
ALRH0200  CALL      TABTC000           * transition care date table
          GOTO      ALRH9999
.
ALRH0210  PACK      KEY13,ADMISSNO,ZERO,FIVE,UNIQCODE,SP70
          CALL      RDALRHL1
          BRANCH    OVRCD,ALRH9999
.
          MOVE      SP70,TDESC
          MATCH     SP70,ALRHCODE
          IF        !@EQUAL
            MOVE      "zW",CATEGORY
            PACK      KEY5,CATEGORY,ALRHCODE,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE     ALRHCODE,TDESC
            ENDIF
          ENDIF
.
          UNPACK    ALRHDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          MATCH     SP70,ALRHDATE
          IF        @EQUAL
            UNPACK    SP70,CDAY,MTHNAM3,CCENT,CYEAR
          ENDIF
.
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1:
                             CCENT,CYEAR,"|",ALRHCODE,"|",TDESC,"|",ALRHUNIQ;
          GOTO      ALRH9999
.
ALRH0220  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          MOVE      ZERO,F2
.
          PACK      KEY13,ADMISSNO,ZERO1,SP70
          CALL      RSALRHL1
ALRH0221  CALL      RKALRHL1
          BRANCH    OVRCD,ALRH0223
.
          MATCH     ADMISSNO,ALRHVISN
          GOTO      ALRH0223 IF NOT EQUAL
.
          MATCH     ZERO1,ALRHTYPE
          GOTO      ALRH0221 IF NOT EQUAL
.
          ADD       ONE,F2              * Main health condition count
.
          IF        F2<F1
            GOTO      ALRH0221          * More than X alerts
          ENDIF
.
ALRH0222  WRITE     HTMLFILE;"1";       * true X records found -main health
          GOTO      ALRH9999
.
ALRH0223  WRITE     HTMLFILE;"0";       * false X records not found -main health
          GOTO      ALRH9999
.
ALRH0230  UNPACK    DIM127,D1,KEY1,DIM127   * Main health condition X
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          UNPACK    DIM127,D1,KEY1,DIM127   * Display type
          MOVE      ZERO,FORM1
          MOVE      KEY1,FORM1
.
          MOVE      ZERO,F2
          PACK      KEY13,ADMISSNO,ZERO1,SP70
          CALL      RSALRHL1
ALRH0231  CALL      RKALRHL1
          BRANCH    OVRCD,ALRH9999
.
          MATCH     ADMISSNO,ALRHVISN
          GOTO      ALRH9999 IF NOT EQUAL
.
          MATCH     ZERO1,ALRHTYPE
          GOTO      ALRH9999 IF NOT EQUAL
.
          ADD       ONE,F2              * Main health condition count
.
          IF        F2<F1
            GOTO      ALRH0231          * Main health condition X
          ENDIF
.
          BRANCH    FORM1,ALRH0232,ALRH0233
.
          WRITE     HTMLFILE;ALRHCODE;  * Main health condition X - Code   
          GOTO      ALRH9999
.
ALRH0232  PACK      KEY12,ALREDEPT,ALRHCODE,SP70
          CALL      RDALDIA1
          IF        OVRCD=1
            MOVE      ALRHCODE,ALDIDESC
          ENDIF
.
          WRITE     HTMLFILE;ALDIDESC;  * Main health condition description
          GOTO      ALRH9999
.
ALRH0233  WRITE     HTMLFILE;ALRHUNIQ;  * Unique Number
          GOTO      ALRH9999
.
ALRH0240  MOVE      ZERO,F1
.
          PACK      KEY13,ADMISSNO,ZERO1,SP70
          CALL      RSALRHL1
ALRH0241  CALL      RKALRHL1
          BRANCH    OVRCD,ALRH9999
.
          MATCH     ADMISSNO,ALRHVISN
          GOTO      ALRH9999 IF NOT EQUAL
.
          MATCH     ZERO1,ALRHTYPE
          GOTO      ALRH9999 IF NOT EQUAL
.
          PACK      KEY12,ALREDEPT,ALRHCODE,SP70
          CALL      RDALDIA1
          BRANCH    OVRCD,ALRH0241
.
          IF        F1=1
            WRITE     HTMLFILE;"|";
          ENDIF
.
          SQUEEZE   ALDIMHDP
          WRITE     HTMLFILE;ALDIMHDP;           * Main health condition HDP
          MOVE      ONE,F1
          GOTO      ALRH0241
.
ALRH9999  RETURN
+
.------------------------------------------------------------------------------
.         RFDDESC00 - get referral destination details 
.------------------------------------------------------------------------------
.
RFDDES00  IF        REFERLIN=1
            PACK      KEY22,DIAGCODE,ADMISSNO,ZERO4,UNIQCODE,SP70
          ELSE
            PACK      KEY22,DIAGCODE,ADMISSNO,ZERO3,UNIQCODE,SP70
          ENDIF
.
          CALL      RDALRHL2
          BRANCH    OVRCD,ALRH9999
.
          MOVE      SP70,DIM20
          MATCH     SP70,ALRHCOD2
          IF        !@EQUAL
            MOVE      "zV",CATEGORY
            PACK      KEY5,CATEGORY,ALRHCOD2,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE    ALRHCOD2,TDESC
            ENDIF
            MOVE      TDESC,DIM20
          ENDIF
.
          MOVE      "zU",CATEGORY
          PACK      KEY5,CATEGORY,ALRHCODE,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE    "N/A",TDESC
          ENDIF
.
          UNPACK    ALRHDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          MATCH     SP70,ALRHDATE
          IF        @EQUAL
            UNPACK    SP70,CDAY,MTHNAM3,CCENT,CYEAR
          ENDIF
.
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1:
                             CCENT,CYEAR,"|",ALRHCODE,"|",TDESC,"|",ALRHUNIQ:
                             "|",ALRHCOD2,"|",DIM20;
          GOTO      RFDDESC99
.
RFDDESC99 RETURN
+
.------------------------------------------------------------
.         TABRF000 - referral destination table
.------------------------------------------------------------
.
TABRF000  PACK      KEY13,ADMISSNO,ZERO3,SP70
          CALL      RSALRHL1
TABRF100  CALL      RKALRHL1
          BRANCH    OVRCD,TABRF999
.
          MATCH     ADMISSNO,ALRHVISN
          GOTO      TABRF999 IF NOT EQUAL
.
          MATCH     ZERO3,ALRHTYPE
          GOTO      TABRF100 IF NOT EQUAL
.
          REP       " +",ADMISSNO
.
          MOVE      SP70,DIM20
          MATCH     SP70,ALRHCOD2
          IF        !@EQUAL
            MOVE      "zV",CATEGORY
            PACK      KEY5,CATEGORY,ALRHCOD2,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE    ALRHCOD2,TDESC
            ENDIF
            MOVE      TDESC,DIM20
          ENDIF
.
          MOVE      "zU",CATEGORY
          PACK      KEY5,CATEGORY,ALRHCODE,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE    "N/A",TDESC
          ENDIF
.
          UNPACK    ALRHDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          MATCH     SP70,ALRHDATE
          IF        @EQUAL
            UNPACK    SP70,CDAY,MTHNAM3,CCENT,CYEAR
          ENDIF
.          
          WRITE     HTMLFILE;"<tr><td>":
                             "<img src=#"../images/DeleteIcon.gif#" ":
                             "onclick=#"removeReferralDestination('":
                             ALRHCODE,"');#" class=#"Icon#"/>":
                             "</td>":
                             "<td><img src=#"../images/MaintenanceIcon.gif#" ":
                             " onclick=#"openEditDateFrame('":
                             ALRHCODE,"','",ALRHUNIQ,"');#"":
                             " class=#"Icon#" /></td>":
                             "<td>",ALRHCODE,"</td>":
                             "<td>",TDESC,"</td>":
                             "<td>",ALRHCOD2,"&nbsp;</td>":
                             "<td>",DIM20,"&nbsp;</td>":
                             "<td>",CDAY,SP1,MTHNAM3,SP1:
                             CCENT,CYEAR,"</td>":
                             "</tr>";
.
          REP       "+ ",ADMISSNO
.
          GOTO      TABRF100
.
TABRF999  RETURN
+
.------------------------------------------------------------
.         TABRF000 - referral in destination table
.------------------------------------------------------------
TBIRF000  PACK      KEY13,ADMISSNO,ZERO4,SP70
          CALL      RSALRHL1
TBIRF100  CALL      RKALRHL1
          BRANCH    OVRCD,TBIRF999
.
          MATCH     ADMISSNO,ALRHVISN
          GOTO      TBIRF999 IF NOT EQUAL
.
          MATCH     ZERO4,ALRHTYPE
          GOTO      TBIRF100 IF NOT EQUAL
.
          REP       " +",ADMISSNO
.
          MOVE      "zU",CATEGORY
          PACK      KEY5,CATEGORY,ALRHCODE,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE    "N/A",TDESC
          ENDIF
.
          UNPACK    ALRHDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          MATCH     SP70,ALRHDATE
          IF        @EQUAL
            UNPACK    SP70,CDAY,MTHNAM3,CCENT,CYEAR
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td>":
                             "<img src=#"../images/DeleteIcon.gif#" ":
                             "onclick=#"removeReferralDestination('":
                             ALRHCODE,"');#" class=#"Icon#"/>":
                             "</td>":
                             "<td><img src=#"../images/MaintenanceIcon.gif#" ":
                             " onclick=#"openEditDateFrame('":
                             ALRHCODE,"','",ALRHUNIQ,"');#"":
                             " class=#"Icon#" /></td>":
                             "<td>",ALRHCODE,"</td>":
                             "<td>",TDESC,"</td>":
                             "<td>",CDAY,SP1,MTHNAM3,SP1:
                             CCENT,CYEAR,"</td>":
                             "</tr>";
.
          REP       "+ ",ADMISSNO
.
          GOTO      TBIRF100
.
TBIRF999  RETURN
+
.------------------------------------------------------------
.         TABMH000 - main health table
.------------------------------------------------------------
.
TABMH000  PACK      KEY13,ADMISSNO,ZERO1,SP70
          CALL      RSALRHL1
TABMH100  CALL      RKALRHL1
          BRANCH    OVRCD,TABMH999
.
          MATCH     ADMISSNO,ALRHVISN
          GOTO      TABMH999 IF NOT EQUAL
.
          MATCH     ZERO1,ALRHTYPE
          GOTO      TABMH100 IF NOT EQUAL
.         
          REP       " +",ADMISSNO
.
          PACK      KEY12,DEPTCODE,ALRHCODE,SP70
          CALL      RDALDIA1
          IF        OVRCD=1
            MOVE    "N/A",ALDIDESC
          ENDIF

          WRITE     HTMLFILE;"<tr><td>":
                             "<img src=#"../images/DeleteIcon.gif#" ":
                             "onclick=#"removeHealthCondition('":
                             ALRHCODE,"');#" />":
                             "</td>":
                             "<td>",ALRHCODE,"</td>":
                             "<td>",ALDIDESC,"</td></tr>";
.
          REP       "+ ",ADMISSNO
.
          GOTO      TABMH100
.
TABMH999  RETURN
+
.------------------------------------------------------------
.         TABOF000 - Other Factors affecting health table
.------------------------------------------------------------
.
TABOF000  PACK      KEY13,ADMISSNO,ZERO2,SP70
          CALL      RSALRHL1
TABOF100  CALL      RKALRHL1
          BRANCH    OVRCD,TABOF999
.
          MATCH     ADMISSNO,ALRHVISN
          GOTO      TABOF999 IF NOT EQUAL
.
          MATCH     ZERO2,ALRHTYPE
          GOTO      TABOF100 IF NOT EQUAL
.
          REP       " +",ADMISSNO
.
          PACK      KEY12,DEPTCODE,ALRHCODE,SP70
          CALL      RDALPRR1
          IF        OVRCD=1
            MOVE    "N/A",ALPRDESC
          ENDIF

          WRITE     HTMLFILE;"<tr><td>":
                             "<img src=#"../images/DeleteIcon.gif#" ":
                             "onclick=#"removeOtherFactors('":
                             ALRHCODE,"');#" />":
                             "</td>":
                             "<td>",ALRHCODE,"</td>":
                             "<td>",ALPRDESC,"</td></tr>";
.
          REP       "+ ",ADMISSNO
.
          GOTO      TABOF100
.
TABOF999  RETURN
+
.------------------------------------------------------------
.         TABTC000 - transition care date table
.------------------------------------------------------------
TABTC000  MOVE      SP70,KEY3
          PACK      KEY13,ADMISSNO,ZERO,FIVE,Z70
          CALL      RSALRHL1
          CALL      RPALRHL1
          BRANCH    OVRCD,TABTC999
.
          MATCH     ADMISSNO,ALRHVISN
          GOTO      TABTC999 IF NOT EQUAL
.
          MATCH     "05",ALRHTYPE
          GOTO      TABTC999 IF NOT EQUAL
.
          MOVE      ALRHUNIQ,KEY3           * Save unique number of last record
.
TABTC050  PACK      KEY13,ADMISSNO,ZERO,FIVE,SP70
          CALL      RSALRHL1
TABTC100  CALL      RKALRHL1
          BRANCH    OVRCD,TABTC999
.
          MATCH     ADMISSNO,ALRHVISN
          GOTO      TABTC999 IF NOT EQUAL
.
          MATCH     "05",ALRHTYPE
          GOTO      TABTC100 IF NOT EQUAL
.
          REP       " +",ADMISSNO
.
          MOVE      SP70,TDESC
          MATCH     SP70,ALRHCODE
          IF        !@EQUAL
            MOVE      "zW",CATEGORY
            PACK      KEY5,CATEGORY,ALRHCODE,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE    ALRHCODE,TDESC
            ENDIF
          ENDIF
.
          UNPACK    ALRHDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          MATCH     SP70,ALRHDATE
          IF        @EQUAL
            UNPACK    SP70,CDAY,MTHNAM3,CCENT,CYEAR
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td>";
.
          MATCH     KEY3,ALRHUNIQ
          IF        @EQUAL
            WRITE     HTMLFILE;"<img src=#"../images/DeleteIcon.gif#" ":
                               "onclick=#"removeTransitionDate('":
                               ALRHUNIQ,"');#" class=#"Icon#"/>";
          ELSE
            WRITE     HTMLFILE;"&nbsp;";
          ENDIF
.
          WRITE     HTMLFILE;"</td>":
                             "<td><img src=#"../images/MaintenanceIcon.gif#" ":
                             " onclick=#"openEditDateFrame('":
                             ALRHCODE,"','",ALRHUNIQ,"');#"":
                             " class=#"Icon#" /></td>":
                             "<td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                             "<td>",TDESC,"</td>":
                             "</tr>";
.
          REP       "+ ",ADMISSNO
.
          GOTO      TABTC100
.
TABTC999  RETURN
.------------------------------------------------------------
.   Set Parameters for main health condition file
.------------------------------------------------------------
SPALH000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPALH001,SPALH002,SPALH003,SPALH004,SPALH005:
                       SPALH005
.
          MOVE      ONE,EXIT
          GOTO      SPALH0999
.
SPALH001  PACK      ALRHL001,QRYDATA,SP70
          GOTO      SPALH999
.
SPALH002  PACK      ALRHL002,QRYDATA,SP70
          GOTO      SPALH999
.
SPALH003  PACK      ALRHL003,QRYDATA,SP70
          GOTO      SPALH999
.
SPALH004  MOVE      QRYDATA,ALRHL004
          GOTO      SPALH999
.
SPALH005  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      ALRHL005,CCENT,CYEAR,CMON,CDAY
          GOTO      SPALH999
.
SPALH006  MOVE      QRYDATA,ALRHL006
          GOTO      SPALH999
.
SPALH999  RETURN
.
.------------------------------------------------------------
.  Update main health condition file
.------------------------------------------------------------
MVALRH00  MATCH     ALRHL001,Z70
          IF        !@EQUAL
            MOVE      ALRHL001,ALRHVISN
          ENDIF     
.
          MATCH     ALRHL002,Z70
          IF        !@EQUAL
            MOVE      ALRHL002,ALRHTYPE
          ENDIF     
.
          MATCH     ALRHL003,Z70
          IF        !@EQUAL
            MOVE      ALRHL003,ALRHUNIQ
          ENDIF     
.
          MATCH     ALRHL004,Z70
          IF        !@EQUAL
            MOVE      ALRHL004,ALRHCODE
          ENDIF     
.
          MATCH     ALRHL005,Z70
          IF        !@EQUAL
            MOVE      ALRHL005,ALRHDATE
          ENDIF     
.
          MATCH     ALRHL006,Z70
          IF        !@EQUAL
            MOVE      ALRHL006,ALRHCOD2
          ENDIF     
.
MVALRH99  RETURN
.
