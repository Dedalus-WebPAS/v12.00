.***************************************************************************
.*   System          : Theatre System                                      *
.*   Include Function: OPADDTIM.PBL                                        *
.*   Description     : Add and subtract durations to start time            *
.***************************************************************************
.*   Date            : 12/02/1991                                          *
.*   Author          : Graeme Williams                                     *
.*   Modifications   :                                                     *
.***************************************************************************
.*   Parameters      : EXPSTART - Expected start time                      .
.*                     EXPDURA  - Duration to be start time                .
.***************************************************************************
.*   Returns         : EXPSTART - New expected start time                  .
.***************************************************************************
.
.                                 OPADDTIM
.
.    It works somthing like this.
.
.    /* chour and cmin are expected hours and mins */
.
.    totmins = chour*60 + cmin + expdura;
.    if ( totmins < 0 ) totmins += 1440;
.    else totmins -= 1440;
.    newhour = (int)(totmins/60);
.    newmin  = totmins - newhour*60;
.
.*******************************************************************************
OPADDTIM
          UNPACK    EXPSTART,CHOUR,ANS,CMIN
          MOVE      CHOUR,FORM4        * FORM4 will be totmins
          MOVE      CMIN,FORM2
.
          MULT      "60",FORM4         * convert hours to minutes
          ADD       FORM2,FORM4        * add the minutes
          ADD       EXPDURA,FORM4      * add the duration for total mins
.
OPAT0022
          COMPARE   ZERO,FORM4         * test if added or subtracted
          GOTO      OPAT0033 IF NOT LESS
          ADD       "1440",FORM4       * add a day from totmins
          GOTO      OPAT0022
.
OPAT0033
          COMPARE   "1440",FORM4       * test if added or subtracted
          GOTO      OPAT0044 IF LESS
          SUB       "1440",FORM4       * add a day from totmins
          GOTO      OPAT0033
.
OPAT0044
          MOVE      FORM4,SAVEF10      * save the new total mins
          DIV       "60",FORM4
          MOVE      FORM4,FORM2
          MOVE      FORM2,CHOUR        * save the new hours
.
          MULT      "60",FORM4         * calculate remaining minutes
          SUB       FORM4,SAVEF10
          MOVE      SAVEF10,FORM2
          MOVE      FORM2,CMIN         * save the new minutes
.
          PACK      EXPSTART,CHOUR,COLON,CMIN
          REP       " 0",EXPSTART
OPAT0099  RETURN
