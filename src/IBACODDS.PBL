.*******************************************************************************
.*                                 IBACODDS                                    *
.*                                                                             *
.*     INClude to display all entries in Codes Table file (ibacodef)           *
.*                                                                             *
.*     Author      : i chung                                                   *
.*                                                                             *
.*     Date        : 03/07/90                                                  *
.*                                                                             *
.*     File Needed : IBACODE1                                                  *
.*                                                                             *
.*******************************************************************************
IBACODDS
          MOVE      SP5,KEY5
          CALL      RDSICOD1
          CALL      RDKICOD1
          BRANCH    OVRCD,IBACO800
.
. ------  display INClude heading and then loop entries  ------
.
          DISPLAY   *P1:4,*EF,*P28:4,*V2LON,*ULON,"Codes Table File entries"
.
          MOVE      SIX,CVERT
          MOVE      ONE,CCOL
          CALL      RDSICOD1
.
IBACO100  CALL      RDKICOD1
          BRANCH    OVRCD,IBACO900
          MATCH     SP3,PRXCODE
          GOTO      IBACO100 IF NOT EQUAL
.
          DISPLAY   *PCCOL:CVERT,*V2LON,PRXCATA,*HOFF,SP3,PRXDESC
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      IBACO100 IF LESS
.
          MOVE      SIX,CVERT
          ADD       "40",CCOL
          COMPARE   "70",CCOL
          GOTO      IBACO100 IF LESS
.
          DISPLAY   *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue, (",*V2LON:
                               "F",*HOFF,")irst screen or (",*V2LON,"N",*HOFF:
                               ")ext screen :"
.
IBACO200  KEYIN     *P47:24,*DV,UNDLN1,*P47:24,*V2LON,ANS
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
.
          MATCH     SP1,ANS
          GOTO      IBACO300 IF EQUAL
          REP       UPPLOW,ANS
          DISPLAY   *P47:24,*V2LON,ANS
.
          MATCH     ANSC,ANS
          GOTO      IBACO900 IF EQUAL
          MATCH     ANSF,ANS
          GOTO      IBACODDS IF EQUAL
          MATCH     ANSN,ANS
          GOTO      IBACO300 IF EQUAL
          BEEP
          GOTO      IBACO200
.
IBACO300  MOVE      SIX,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
          GOTO      IBACO100
.
. ------  if missing entries show entries missing  ------
.
IBACO800  DISPLAY   *P1:24,*EL,*B,*P30:4,*V2LON,*BLINKON:
                    "No Codes Table File entries on file",*W2;
          MOVE      ONE,OVRCD
          GOTO      IBACO999
.
. ------  end of Codes Table file  ------
.
IBACO900  MOVE      ZERO,OVRCD
.
IBACO999  RETURN
+
