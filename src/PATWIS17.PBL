.******************************************************************************
.* System         : Patient Management                                        *
.* Include        : PATWIS17.PBL                                              *
.* Description    : Calculate WIES17 value for an individual admission        *
.******************************************************************************
.* Date           : 19/03/2010                                                *
.* Author         : Davin Sloan                                               *
.*                                                                            *
.* Vars Required  : VARWIS17 - Variables required for WIES17                  *
.*                                                                            *
.* Files Required Open :                                                      *
.*        PATASFFD  PATASFA1 - Admission special funding arrangement file     *
.*        PATCODFD  PATCODE1 - Codes file                                     *
.*        PATECDFD  PATECDA2 - Episode disease coding file                    *
.*        PATECOFD  PATECOA2 - Episode operation coding file                  *
.*        PAT10OFD  PATI10O1 - ICD10 operations file                          *
.*        PATICUFD  PATICUA1 - ICU file                                       *
.*        PATTRNFD  PATTRAN2 - Transfer file                                  *
.*        PATW17FD  PATW17A1 - Victorian cost weights (for WIES17) file       *
.*                                                                            *
.* Parameters Req : Successful read on admission file                         *
.*                  Successful read on discharge file                         *
.*                  HIHLOS   - for Hopsipta in Home LOS (from RHIHDAYS)       *
.*                  NBRDAYS  - for LOS calculation (from NHOSPDAY)            *
.*                  PNSWRACE - User defined field for race                    *
.*                  WIS17DRG - DRG to calculate WIES17 for                    *
.*                                                                            *
.* Returns        : TOTWIS17 - Total WIES17 value                             *
.*                  WIS17DRG - DRG used to calculate WIES17                   *
.*                  WIS17LOS - LOS used to calculate WIES17                   *
.*                  WIS17WGT - Weight used to calculate WIES17                *
.*                  WIS17DSC - Description of DRG weight used                 *
.*                                                                            *
.* Modifications  :                                                           *
.*        V12.00.01 26/05/2025  Bella Turco   TSK 0955096                     *
.*                  Alphanumeric changes                                      *
.*        V10.02.01 28/03/2011  Mike Laming   CAR 240107                      *
.*                  Mods to PATECDaf & PATECOaf variables & Keys - file change*
.*       V10.00.01  10/08/2010  Davin            CAR 216505                   *
.*                  Changed mechanical ventilation daily copayment from       *
.*                  .7729 to .7659                                            *
.*       V10.00.00  19/03/2010  Davin            CAR 216505                   *
.*                  Created for 2010/2011 WIES17 changes:                     *
.******************************************************************************
+
PATWIS17  CALL      IN17W000                * Initialise variables
          BRANCH    EXIT,WIS17950
.
          CALL      C17TY000                * Determine and Check Care Type
          BRANCH    EXIT,WIS17950
.
          CALL      C17TR000                * Check for Contracted Patient
          BRANCH    EXIT,WIS17950
.
          CALL      M17RG000                * Modify DRG if necessary
.
          CALL      C17RG000                * Check for problem DRG
          BRANCH    EXIT,WIS17950
.
          CALL      M17CH000                * Check for Mechanical Ventilation
.
          CALL      T17AL000                * Check for Thalessaemia
.
          CALL      A17AA000                * Check for AAA
.
          CALL      A17SD000                * Check for ASD
.
          CALL      B17SW000                * Calculate Base WIES17
.
          CALL      C17AB000                * Check for aboriginality
.
. ---- Calculate WIES17 value ----
.
          MOVE      TOTBWIES,TOTWIS17       * base WIES
          ADD       MVCOPAY,TOTWIS17        * mechv copayment
          ADD       THCOPAY,TOTWIS17        * thal  copayment
          ADD       AAACOPAY,TOTWIS17       * AAA   copayment
          ADD       ASDCOPAY,TOTWIS17       * ASD   copayment
          ADD       ATSIWIES,TOTWIS17       * ATSI  copayment
.
          MOVE      ZERO,EXIT
          GOTO      WIS17999
.
WIS17950  MOVE      ONE,EXIT
WIS17999  RETURN
+
.******************************************************************************
.*                                  IN17W000              Called by: PATWIS17 *
.*                         Initialise WIES17 variables                        *
.******************************************************************************
IN17W000  MOVE      ZERO,TOTWIS17
          MOVE      ZERO,WIS17LOS
          MOVE      ZERO,WIS17WGT
          MOVE      SP20,WIS17DSC
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATW17A1,"patw17af"
          TRAPCLR   IO
          BRANCH    OVRCD,IN17W950
.
IN17W900  MOVE      ZERO,EXIT
          GOTO      IN17W999
.
IN17W950  MOVE      ONE,EXIT
IN17W999  RETURN
+
.******************************************************************************
.*                                  C17TY000              Called by: PATWIS17 *
.*                 Check for valid Care Type & Incomplete Coding              *
.******************************************************************************
C17TY000  PACK     KEY5,ANSC,ANSC,ACARE     * use admission care type
          CALL     RDCODE1
          BRANCH   OVRCD,C17TY900
.
          MATCH    "6",TCINDC4              * Check Program Funding Source
          GOTO     C17TY900 IF NOT EQUAL
.
          MATCH    "0   ",THCSCOD           * check care types
          GOTO     C17TY900 IF EQUAL
          MATCH    "1   ",THCSCOD
          GOTO     C17TY900 IF EQUAL
          MATCH    "2   ",THCSCOD
          GOTO     C17TY900 IF EQUAL
          MATCH    "6   ",THCSCOD
          GOTO     C17TY900 IF EQUAL
          MATCH    "7   ",THCSCOD
          GOTO     C17TY900 IF EQUAL
          MATCH    "8   ",THCSCOD
          GOTO     C17TY900 IF EQUAL
          MATCH    "9   ",THCSCOD
          GOTO     C17TY900 IF EQUAL
          MATCH    "5   ",THCSCOD
          GOTO     C17TY900 IF EQUAL
          MATCH    "3   ",THCSCOD
          GOTO     C17TY900 IF EQUAL
          MATCH    "E   ",THCSCOD
          GOTO     C17TY900 IF EQUAL
          MATCH    "F   ",THCSCOD
          GOTO     C17TY900 IF EQUAL
          MATCH    "J   ",THCSCOD
          GOTO     C17TY900 IF EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     C17TY999
.
C17TY900  MOVE     ONE,EXIT                 * Not valid
C17TY999  RETURN
+
.******************************************************************************
.*                                  C17TR000              Called by: PATWIS17 *
.*                        Check for Contracted Patient                        *
.******************************************************************************
C17TR000  PACK     KEY25,AADMNO,ANSA,SP30
          CALL     RSPTASF1
C17TR100  CALL     RKPTASF1                 * Read adm. special funding file
          BRANCH   OVRCD,C17TR800           * still process
.
          MATCH    AADMNO,PTAFADMN
          GOTO     C17TR800 IF NOT EQUAL    * still process
.
          MATCH    ANSA,PTAFTYPE            * special fund. arrang. type = adm
          GOTO     C17TR100 IF NOT EQUAL
.
          MATCH    ANSB,PTAFCROL            * Ignore if contract role = 'B'
          GOTO     C17TR900 IF EQUAL
.
C17TR800  MOVE     ZERO,EXIT                * valid
          GOTO     C17TR999
.
C17TR900  MOVE     ONE,EXIT                 * invalid
C17TR999  RETURN
+
.******************************************************************************
.*                                  M17RG000              Called by: PATWIS17 *
.*                      Modify DRG if necessary for WIES17                    *
.******************************************************************************
M17RG000  CALL      R17IO000           * Radiotherapy
M17RG999  RETURN
+
.******************************************************************************
.*                                  R17IO000              Called by: M17RG000 *
.*                         Modify DRG if Radiotherapy                         *
.******************************************************************************
.>>>>     Don't modify these DRGs:
.
R17IO000  MATCH     "B61A",WIS17DRG
          GOTO      R17IO999 IF EQUAL
.
          MATCH     "B61B",WIS17DRG
          GOTO      R17IO999 IF EQUAL
.
          MATCH     "A40Z",WIS17DRG
          GOTO      R17IO999 IF EQUAL
.
          MATCH     "W60Z",WIS17DRG
          GOTO      R17IO999 IF EQUAL
.
          MATCH     "W61A",WIS17DRG
          GOTO      R17IO999 IF EQUAL
.
          MATCH     "W61B",WIS17DRG
          GOTO      R17IO999 IF EQUAL
.
          MATCH     "S65A",WIS17DRG
          GOTO      R17IO999 IF EQUAL
.
          MATCH     "S65B",WIS17DRG
          GOTO      R17IO999 IF EQUAL
.
          MATCH     "S65C",WIS17DRG
          GOTO      R17IO999 IF EQUAL
.
          MATCH     "B60A",WIS17DRG
          GOTO      R17IO999 IF EQUAL
.
          MATCH     "B60B",WIS17DRG
          GOTO      R17IO999 IF EQUAL
.
          MATCH     "B82A",WIS17DRG
          GOTO      R17IO999 IF EQUAL
.
          MATCH     "B82B",WIS17DRG
          GOTO      R17IO999 IF EQUAL
.
          MATCH     "B82C",WIS17DRG
          GOTO      R17IO999 IF EQUAL
.
.>>>>     Check if operation code exists:
.
          PACK      ICDRDDTE,DDATE,SP10
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
R17IO050  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,R17IO999
.
          MATCH     AADMNO,PTEOADMN         * same admission?
          GOTO      R17IO999 IF NOT EQUAL
.
.>>>>     Check if block number is in range 1786-1792, 1794 or 1795:
.
          PACK      KEY9,PTEOCODE,SP9
          CALL      RDPTICO1
          BRANCH    OVRCD,R17IO050
.
          MATCH     "1786",PT0OBLKN         * block code >= 1786?
          GOTO      R17IO050 IF LESS
.
          MATCH     "1796",PT0OBLKN         * block code <=1795?
          GOTO      R17IO050 IF NOT LESS
.
          MATCH     "1793",PT0OBLKN         * block code = 1793?
          GOTO      R17IO050 IF EQUAL
.
.>>>>     Check if disease code exists in range C00-D48:
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
R17IO100  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,R17IO999
.
          MATCH     AADMNO,PTEDADMN         * same admission?
          GOTO      R17IO999 IF NOT EQUAL
.
          MATCH     "C00",PTEDCODE          * neoplasm code >= C00?
          GOTO      R17IO100 IF LESS
.
          MATCH     "D49",PTEDCODE          * neoplasm code <= D48.9?
          GOTO      R17IO100 IF NOT LESS
.
          MOVE      "R64Z",WIS17DRG         * modified for WIES17
R17IO999  RETURN
+
.******************************************************************************
.*                                  C17RG000              Called by: PATWIS17 *
.*                            Check for Problem DRG                           *
.******************************************************************************
C17RG000  PACK     KEY4,WIS17DRG
          CALL     RDPT171                  * check WIES17 weights file
          BRANCH   OVRCD,C17RG900
.
          MATCH    "960Z",PT17DRGC
          GOTO     C17RG900 IF EQUAL
.
          MATCH    "961Z",PT17DRGC
          GOTO     C17RG900 IF EQUAL
.
          MATCH    "962Z",PT17DRGC
          GOTO     C17RG900 IF EQUAL
.
          MATCH    "963Z",PT17DRGC
          GOTO     C17RG900 IF EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     C17RG999
.
C17RG900  MOVE     ONE,EXIT
C17RG999  RETURN
+
.******************************************************************************
.*                                  M17CH000              Called by: PATWIS17 *
.*                Calculate Mechanical Ventialtion Co-payment                 *
.******************************************************************************
M17CH000  MOVE      ZERO,ADJMVDAY
          MOVE      ZERO,MVCOPAY
.
          PACK      KEY8,AADMNO
          CALL      RDPTICU1                * get mech. vent. hours
          BRANCH    OVRCD,M17CH999
.
          MATCH     "D",PT17MVEL            * check mech. vent. eligibility
          IF        @EQUAL
            IF        PTICUMEC > 6
              ASSIGN    ((PTICUMEC+12)/24),FORM12P4    * days receiving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7659)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
            GOTO      M17CH999
          ENDIF
.
          MATCH     "4",PT17MVEL
          IF        @EQUAL
            IF        PTICUMEC > 96
              ASSIGN    (((PTICUMEC+12)/24)-4),FORM12P4   * days recving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7659)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
          ENDIF
.
M17CH999  RETURN
+
.******************************************************************************
.*                                  T17AL000              Called by: PATWIS17 *
.*                      Calculate Thalessaemia Co-payment                     *
.******************************************************************************
T17AL000  MOVE      ZERO,THCOPAY
          MATCH     "T",PT17CPAY            * check WIES17 weights file
          IF        @EQUAL
            PACK      KEY16,AADMNO,SP20
            CALL      RSPTECD2              * Position on & read a patient
T17AL100    CALL      RKPTECD2                episode disease file record
            BRANCH    OVRCD,T17AL999
.
            MATCH     AADMNO,PTEDADMN
            GOTO      T17AL999 IF NOT EQUAL
.
            MATCH     "D56",PTEDCODE        * check ICD-10 code
            IF        !@EQUAL
              MATCH     "D57.2",PTEDCODE    * check ICD-10 code
              GOTO      T17AL100 IF NOT EQUAL
            ENDIF
            MOVE      "0.2648",THCOPAY      * eligible for thal. copayment
          ENDIF
T17AL999  RETURN
+
.******************************************************************************
.*                                  A17AA000              Called by: PATWIS17 *
.*                          Calculate AAA Co-payment                          *
.******************************************************************************
A17AA000  MOVE      ZERO,AAACOPAY
          MATCH     "AAA",PT17CPAY          * check WIES17 weights file
          GOTO      A17AA999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
A17AA100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,A17AA999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      A17AA999 IF NOT EQUAL
.
          MATCH     "33116-00",PTEOCODE     * check ICD-10 code
          GOTO      A17AA100 IF NOT EQUAL
.
          MOVE      "3.1421",AAACOPAY       * eligible for AAA copayment
A17AA999  RETURN
+
.******************************************************************************
.*                                  A17SD000              Called by: PATWIS17 *
.*                          Calculate ASD Co-payment                          *
.******************************************************************************
A17SD000  MOVE      ZERO,ASDCOPAY
          MATCH     "ASD",PT17CPAY          * check WIES17 weights file
          GOTO      A17SD999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
A17SD100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,A17SD999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      A17SD999 IF NOT EQUAL
.
          MATCH     "38742-00",PTEOCODE     * check ICD-10 code
          GOTO      A17SD100 IF NOT EQUAL
.
          MOVE      "2.4713",ASDCOPAY       * eligible for ASD copayment
A17SD999  RETURN
+
.******************************************************************************
.*                                  B17SW000              Called by: PATWIS17 *
.*                            Calculate Base WIES17                           *
.******************************************************************************
B17SW000  CALL      D17SC000                * length of stay category
          CALL      C17ST000                * calculate inlier status
.
          MATCH     "L",W17NLIER            * low outlier
          CALL      BW17L000 IF EQUAL       * calculate base WIES17
.
          MATCH     "I",W17NLIER            * inlier
          CALL      BW17N000 IF EQUAL       * calculate base WIES17
.
          MATCH     "H",W17NLIER            * high outlier
          CALL      BW17H000 IF EQUAL       * calculate base WIES17
.
B17SW999  RETURN
+
.******************************************************************************
.*                                  D17SC000              Called by: B17SW000 *
.*                      Determine Length Of Stay Category                     *
.******************************************************************************
D17SC000  MOVE      NBRDAYS,WIS17LOS        * length of stay from NHOSPDAY
          MOVE      "M",LOSCAT              * set LOS category to multiday
          MATCH     ADATE,DDATE             * check for sameday
          IF        @EQUAL
            MOVE      ONE,SAMEDAY           * sameday
            MOVE      ONE,WIS17LOS          * LOS = 1
            MOVE      "S",LOSCAT            * set LOS category to sameday
            GOTO      D17SC999
          ELSE
            MOVE      ZERO,SAMEDAY          * not sameday
          ENDIF
.
          IF        SAMEDAY = 0 & WIS17LOS = 1
            MOVE      "O",LOSCAT            * set LOS category to oneday
            GOTO      D17SC999
          ENDIF
.
          IF        WIS17LOS > 1825
            MOVE      "1825",WIS17LOS       * maximum LOS is 5 years
          ENDIF
.
D17SC999  RETURN
+
.******************************************************************************
.*                                  C17ST000              Called by: B17SW000 *
.*                           Calculate Inlier Status                          *
.******************************************************************************
C17ST000  MOVE      "I",W17NLIER            * set status to inlier
          IF        WIS17LOS < PT17LIBP
            MOVE      "L",W17NLIER          * set status to low outlier
            GOTO      C17ST999
          ENDIF
.
          ASSIGN    (PT17HIBP+ADJMVDAY),FORM12P4   * high bound. + mech.v days
          IF        WIS17LOS > FORM12P4
            MOVE      "H",W17NLIER          * set status to high outlier
          ENDIF
C17ST999  RETURN
+
.******************************************************************************
.*                                  BW17L000              Called by: B17SW000 *
.*                     Calculate Base WIES17 for Low Outlier                  *
.******************************************************************************
BW17L000  MATCH     "S",LOSCAT            * sameday
          IF        @EQUAL
            MOVE      PT17TSDW,TOTBWIES             * base WIES17
            ASSIGN    (TOTBWIES/PT17TIMW),TOTIES17  * IES17
.
            MOVE      PT17TSDW,WIS17WGT             * for display
            MOVE      "Same Day Wgt ",WIS17DSC
            GOTO      BW17L999
          ENDIF
.
          MATCH     "O",LOSCAT            * oneday
          IF        @EQUAL
            MOVE      PT17TODW,TOTBWIES             * base WIES17
            ASSIGN    (TOTBWIES/PT17TIMW),TOTIES17  * IES17
.
            MOVE      PT17TODW,WIS17WGT             * for display
            MOVE      "One Day Wgt  ",WIS17DSC
            GOTO      BW17L999
          ENDIF
.
          MATCH     "M",LOSCAT            * multiday
          IF        @EQUAL
            ASSIGN    (PT17TODW+((WIS17LOS-1)*PT17TLOM)),TOTBWIES
.                                                    * base WIES17
            ASSIGN    (TOTBWIES/PT17TIMW),TOTIES17   * IES17
.
            MOVE      PT17TLOM,WIS17WGT             * for display
            MOVE      "Low Outlr Wgt",WIS17DSC
            GOTO      BW17L999
          ENDIF
BW17L999  RETURN
+
.******************************************************************************
.*                                  BW17N000              Called by: B17SW000 *
.*                      Calculate Base WIES17 for Inlier                      *
.******************************************************************************
BW17N000  MOVE      ONE,TOTIES17          * IES17 = 1
          MATCH     "S",LOSCAT            * sameday
          IF        @EQUAL
            MOVE      PT17TSDW,TOTBWIES   * base WIES17
.
            MOVE      PT17TSDW,WIS17WGT             * for display
            MOVE      "Same Day Wgt ",WIS17DSC
            GOTO      BW17N999
          ENDIF
.
          MATCH     "O",LOSCAT            * oneday
          IF        @EQUAL
            MOVE      PT17TODW,TOTBWIES   * base WIES17
.
            MOVE      PT17TODW,WIS17WGT             * for display
            MOVE      "One Day Wgt  ",WIS17DSC
            GOTO      BW17N999
          ENDIF
.
          MATCH     "M",LOSCAT            * multiday
          IF        @EQUAL
            MOVE      PT17TIMW,TOTBWIES   * base WIES17
.
            MOVE      PT17TIMW,WIS17WGT             * for display
            MOVE      "Inlier Wgt   ",WIS17DSC
            GOTO      BW17N999
          ENDIF
BW17N999  RETURN
+
.******************************************************************************
.*                                  BW17H000              Called by: B17SW000 *
.*                    Calculate Base WIES17 for High Outlier                  *
.******************************************************************************
BW17H000  ASSIGN    ((WIS17LOS-PT17HIBP)-ADJMVDAY),HIGHDAYS  * high outlr days
          COMPARE   ZERO,HIGHDAYS
          IF        @LESS
            MOVE      ZERO,HIGHDAYS         * highdays must be >= zero
          ENDIF
.
          COMPARE   HIGHDAYS,HIHLOS
          IF        @LESS
            MOVE      HIHLOS,HITHDAYS       * get lesser of highdays and
          ELSE                              *  HIHLOS
            MOVE      HIGHDAYS,HITHDAYS
          ENDIF
.
          ASSIGN    (HIGHDAYS-HITHDAYS)*PT17THOM,TOTBWIES    *high WIES
          ASSIGN    (HITHDAYS*PT17THIH)+TOTBWIES,TOTBWIES    *HITH WIES
          ASSIGN    (PT17TIMW+TOTBWIES),TOTBWIES             *base WIES17
          ASSIGN    (TOTBWIES/PT17TIMW),TOTIES17             * IES17
.
          MOVE      PT17THOM,WIS17WGT                        * for display
          MOVE      "High Outl Wgt",WIS17DSC
BW17H999  RETURN
+
.******************************************************************************
.*                                  C17AB000              Called by: PATWIS17 *
.*                          Check for Aboriginality                           *
.******************************************************************************
C17AB000  MOVE      ZERO,ATSIWIES                * set ATSI WIES to zero
          IF        PNSWRACE<1 | PNSWRACE>14
            GOTO      C17AB999              * Invalid parameter
          ENDIF
.
          LOAD      TCODE,PNSWRACE,CATP1,CATP2,CATP3,CATP4,CATP5:
                                   CATP6,CATA1,CATA2,CATA3,CATA4:
                                   CATA5,CATA6,CATA7,CATVA
          LOAD      ACODE,PNSWRACE,PUSR1,PUSR2,PUSR3,PUSR4,PUSR5:
                                   PUSR6,AUSR1,AUSR2,AUSR3,AUSR4:
                                   AUSR5,AUSR6,AUSR7,PMPXABRG
.
          MATCH     ACODE,SP3
          GOTO      C17AB999 IF EQUAL
.
          PACK      KEY5,TCODE,ACODE
          CALL      RDCODE1                 * Read a code file record
          BRANCH    OVRCD,C17AB999
.
          UNPACK    THCSCOD,DIM1,KEY1       * use 2nd char of HDP Equiv
.>>>>     MOVE      THCSCOD,KEY1
          TYPE      KEY1
          GOTO      C17AB999 IF NOT EQUAL   * Non numeric, error
.
          MOVE      KEY1,FORM1
.
. ---- Calculate ATSI Loading if aboriginality = 1, 2, or 3 ----
.
          IF        FORM1=1 | FORM1=2 | FORM1=3
            MOVE      TOTBWIES,FORM12P4       * base WIES
            ADD       MVCOPAY,FORM12P4        * mechv copayment
            ADD       THCOPAY,FORM12P4        * thal  copayment
            ADD       AAACOPAY,FORM12P4       * AAA   copayment
            ADD       ASDCOPAY,FORM12P4       * ASD   copayment
            ASSIGN    (0.3*FORM12P4),ATSIWIES
          ENDIF
.
C17AB999  RETURN
+
