. *----------------------------------------------------------------------
. * Include Name  :  EMRDCMTX.PBL
. *
. * Function      :  Write diagnosis free text details to emrdcmaf            
. *
. *----------------------------------------------------------------------
.  Modifications Done :
.
.  V11.03.01  09/10/2023  Ebon Clements     TSK 0938188
.             Fixed I * D on emrdcmaf writes - Added traps
.  V10.02.01  28/03/2011  Ebon Clements     CAR 239311
.             Changed to save free format diagnosis from emrvcdaf (EMVCFTXT)
.  V10.00.00  28/01/2011  Ebon Clements     CAR 235686
.             Created include
. -----------------------------------------------------------------------
. Update diagnosis free text details with last diagnosis
. -----------------------------------------------------------------------
EMDT0000  MATCH     "1",EMCNLDIA                 * Using last diagnosis only
          GOTO      EMDT9999 IF NOT EQUAL
.
          PACK      KEY8,ADMISSNO,SP70           * Delete diagnosis free text
          CALL      DEEMDCM1                     * record
.
          MOVE      SP70,EMDCTXT1
          MOVE      SP70,EMDCTXT2
          MOVE      SP70,EMDCTXT3
          MOVE      SP70,EMDCTXT4
          MOVE      SP70,EMDCTXT5
          MOVE      SP70,EMDCTXT6
          MOVE      SP70,EMDCTXT7
          MOVE      SP70,EMDCTXT8
          MOVE      SP70,EMDCTXT9
          MOVE      ZERO,WRITEFLG                * No valid records
.
          PACK      KEY14,ADMISSNO,ZERO,ZERO,FIVE,SP70
          CALL      RSEMVCD1
EMDT1000  CALL      RKEMVCD1
          BRANCH    OVRCD,EMDT8000
.
          MATCH     ADMISSNO,EMVCVIST
          GOTO      EMDT8000 IF NOT EQUAL
.
          MATCH      "005",EMVCTYPE              * Diagnosis
          GOTO      EMDT8000 IF NOT EQUAL    
.
          MATCH     SP70,EMVCFTXT                                              
          IF        @EQUAL
            PACK      KEY9,EMVCMNCD,SP70         * If free format diagnosis is
            CALL      RDEMICD1                   * blank populate it with the
            BRANCH    OVRCD,EMDT1000             * coded diagnosis description
.
            PACK      EMVCFTXT,EMICDESC,SP70   
          ENDIF
.
          CALL      SPDT0000                     * Split diagnosis
.
          MOVE      ONE,WRITEFLG                 * Yes valid records
          GOTO      EMDT1000
.
EMDT8000  MATCH     "1",WRITEFLG                 * Any valid diagnosis records
          GOTO      EMDT9999 IF NOT EQUAL
.
          MOVE      ADMISSNO,EMDCADMI
          PACK      KEY8,ADMISSNO,SP70
.
          CALL      RAEMDCM1
          IF        OVRCD=1
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            CALL      WREMDCM1                   * Write diagnosis fee text
            TRAPCLR   IO                         * record
          ENDIF 
.
EMDT9999  RETURN
.
.-----------------------------------------------------------------------
. Split Diagnosis description into 50 character portions and save
.-----------------------------------------------------------------------
SPDT0000  MOVE      ZERO,SAVSPACE           * Clear save space position
          MOVE      ZERO,CURSPACE           * Clear current space position
.
          MATCH     SP70,EMVCFTXT           * Free format diagnosis blank
          GOTO      SPDT9999 IF EQUAL       * so exit
.
SPDT1000  SCAN      SP1,EMVCFTXT            * Exit if no space found
          GOTO      SPDT5000 IF NOT EQUAL
.
          BUMP      EMVCFTXT,1
          MOVEFPTR  EMVCFTXT,CURSPACE
          IF        CURSPACE<50
            MOVE      CURSPACE,SAVSPACE     * Save space position that is < 50
            GOTO      SPDT1000
          ENDIF
.
SPDT5000  IF        SAVSPACE=0
            RESET     EMVCFTXT
            UNPACK    EMVCFTXT,KEY50,EMVCFTXT    * Take first 50 chars and leave
            PACK      EMVCFTXT,EMVCFTXT,SP70     * remaining chars
          ELSE
            RESET     EMVCFTXT,SAVSPACE
            MOVE      EMVCFTXT,SAVEDIAG          * Save left over diagnsosis
            RESET     EMVCFTXT
            SUB       ONE,SAVSPACE
            SETLPTR   EMVCFTXT,SAVSPACE         
            ADD       ONE,SAVSPACE
            MOVE      EMVCFTXT,KEY50             * Take first part if diagnosis
            PACK      KEY50,KEY50,SP70           & that is less than 50 chars
.
            RESET     EMVCFTXT                   * Restore left over diagnosis
            PACK      EMVCFTXT,SAVEDIAG,SP70,SP70
          ENDIF
.
          CALL      STDT0000
          GOTO      SPDT0000
.
SPDT9999  RETURN
.
.-----------------------------------------------------------------------
. Routen to save 50 characters of free format diagnosis
. to the next empty file var
.-----------------------------------------------------------------------
STDT0000  MATCH     SP70,EMDCTXT1
          IF        @EQUAL
            MOVE      KEY50,EMDCTXT1
            GOTO      STDT9999
          ENDIF
.
          MATCH     SP70,EMDCTXT2
          IF        @EQUAL
            MOVE      KEY50,EMDCTXT2
            GOTO      STDT9999
          ENDIF
.
          MATCH     SP70,EMDCTXT3
          IF        @EQUAL
            MOVE      KEY50,EMDCTXT3
            GOTO      STDT9999
          ENDIF
.
          MATCH     SP70,EMDCTXT4
          IF        @EQUAL
            MOVE      KEY50,EMDCTXT4
            GOTO      STDT9999
          ENDIF
.
          MATCH     SP70,EMDCTXT5
          IF        @EQUAL
            MOVE      KEY50,EMDCTXT5
            GOTO      STDT9999
          ENDIF
.
          MATCH     SP70,EMDCTXT6
          IF        @EQUAL
            MOVE      KEY50,EMDCTXT6
            GOTO      STDT9999
          ENDIF
.
          MATCH     SP70,EMDCTXT7
          IF        @EQUAL
            MOVE      KEY50,EMDCTXT7
            GOTO      STDT9999
          ENDIF
.
          MATCH     SP70,EMDCTXT8
          IF        @EQUAL
            MOVE      KEY50,EMDCTXT8
            GOTO      STDT9999
          ENDIF
.
          MATCH     SP70,EMDCTXT9
          IF        @EQUAL
            MOVE      KEY50,EMDCTXT9
            GOTO      STDT9999
          ENDIF
.
STDT9999  RETURN

