.-----------------------------------------------------------------------------
.   Program     :   loaddcsg
.   Function    :   Used by /users/bin/loaddcs Loads a dcs program details
.                   into screen file prg,mas,fld,pst,slt,sbg
.   Last Change :
.
.   V9.09.01 17.04.2008 Sandra Barcham     166599
.            Changed from compress to gaip    
.   V6.00.04 12.05.93 B.G.Ackland
.            Fix File Layout for Screen Master
.            Transfer Linked Program ID Details
.   V6.00.03 Fix I*D on Selections File.
.   V6.00.02 Fix Attribute Transafer of *V2LON,*BLINKON
.   V6.00.01 Added new fields to SCRMAS
.-----------------------------------------------------------------------------
          INC       IBACOMM/INC
          INC       IBAKEYS/INC
          INC       SCRSBGFD/INC
          INC       SCRSLTFD/INC
          INC       SCRPSTFD/INC
          INC       SCRFLDFD/INC
          INC       SCRMASFD/INC
          INC       SCRPRGFD/INC
.=========================
.Local Variable Definition
.-------------------------
OLDPID    DIM      8
.
PROGNAME  DIM       8
RECTYPE   FORM      1
DRECTYPE   DIM      1
CEXTENS   INIT      ".dcs.gz"
DCS       INIT      ".dcs"
DCOMPRES  INIT      "gzip -d "
COMPRESS  INIT      "gzip "
EXTENS    INIT      "/dcs"
FILE      DIM       12
TRANFILE  FILE      ASCII, FIXED=250
.
PRGID     INIT      "loaddcsg"
PRGNAM    INIT      "Load a dcs program <filename>.dcs.gz"
VERSION   INIT      "V12.00.00"
.
.--------------------------------
. MAINLINE - Controlling Logic
.--------------------------------
ML0000
          CALL      INIT0000
          KEYIN     *EOFF,PROGNAME
          PACK      KEY15,PROGNAME,CEXTENS
          SQUEEZE   KEY15
          DISPLAY   *ES,*P1:1,*HON,SP20,SP20,SP20,SP20:
                        *P1:1,*HON,PRGID,"/",VERSION,SP2,"Loading ",KEY15
          PACK      KEY50,DCOMPRES,KEY15
          EXECUTE   KEY50,KEY8
.
          PACK      FILE,PROGNAME,EXTENS
          SQUEEZE   FILE 
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TRANFILE,FILE
          TRAPCLR   IO
          IF        OVRCD=1
            DISPLAY   *P1:2,"Program ",FILE," File Not Found. "
            GOTO      ML9900
          ENDIF
          MOVE      ZERO,SECTOR
.
ML0100
          CALL      RDTRTR1
          BRANCH    OVRCD,ML9000
          ADD       TWO,SECTOR
          BRANCH    RECTYPE,ML1000,ML2000,ML3000,ML4000,ML5000,ML6000
          KEYIN     ANS
          GOTO      ML0100
.
ML1000
          CALL      RDTRPR1
          PACK      KEY8,SCPRPID
          CALL      RASCPR1
          IF        OVRCD=0
            CALL      UPSCPR1
          ELSE
            CALL      WRSCPR1
          ENDIF
.
          CALL      PRDEL000    *  Clear Current Values
          GOTO      ML0100
.
. Record Type 2 - Screen Masterfile 
. 
ML2000   
          CALL      RDTRMA1
          PACK      KEY10,SCMAPID,SCMASID
          CALL      RASCMA1
          IF        OVRCD=0
            CALL      UPSCMA1
          ELSE
            CALL      WRSCMA1
          ENDIF
          GOTO      ML0100
.
. Record Type 3 - Field Masterfile 
. 
ML3000   
          CALL      RDTRFL1
          PACK      KEY15,SCFLPID,SCFLSID,SCFLFNO
          CALL      RASCFL1
          IF        OVRCD=0
            CALL      UPSCFL1
          ELSE
            CALL      WRSCFL1
          ENDIF
          GOTO      ML0100
.
. Record Type 4 - Screen Background
. 
ML4000    
          CALL      RDTRSB1
          PACK      KEY12,SCSBPID,SCSBSID,SCSBLIN
          CALL      RASCSB1
          IF        OVRCD=0
            CALL      UPSCSB1
          ELSE
            CALL      WRSCSB1
          ENDIF
          GOTO      ML0100
.
. Record Type 5 - Screen Positions
. 
ML5000    
          CALL      RDTRPS1
          PACK      KEY15,SCPSPID,SCPSSID,SCPSROW,SCPSCOL
          CALL      RASCPS1
          IF        OVRCD=0
            CALL      UPSCPS1
          ELSE
            CALL      WRSCPS1
          ENDIF
          GOTO      ML0100
.
. Record Type 6 - Screen Selections
. 
ML6000    
          CALL      RDTRSL1
          PACK      KEY13,SCSLPID,SCSLSID,SCSLSEL
          CALL      RASCSL1
          IF        OVRCD=0
            CALL      UPSCSL1
          ELSE
            CALL      WRSCSL1
          ENDIF
          GOTO      ML0100
.
ML9000
          CLOSE     TRANFILE
          PACK      KEY13,PROGNAME,DCS
          SQUEEZE   KEY13
          PACK      KEY50,COMPRESS,KEY13
          EXECUTE   KEY50,KEY8
ML9900
          SHUTDOWN  "" 
.---------------------------------------
. INIT - Open Files & Display Heading
.---------------------------------------
INIT0000  
          OPEN      SCRSBGA1,"scrsbgaf"
          OPEN      SCRPSTA1,"scrpstaf"
          OPEN      SCRPSTA2,"scrpstaf"
          OPEN      SCRSLTA1,"scrsltaf"
          OPEN      SCRFLDA1,"scrfldaf"
          OPEN      SCRMASA1,"scrmasaf"
          OPEN      SCRPRGA1,"scrprgaf"
INIT9999  
          RETURN
.---------------------------------------------------
. Delete Program from all Files 
.----------------------------------------------------
PRDEL000  MOVE      SCPRPID,OLDPID
.
PRDEL010  
          PACK      KEY15,OLDPID,SP20
          CALL      RSSCFL1
          CALL      RKSCFL1
          BRANCH    OVRCD,PRDEL020
          MATCH     OLDPID,SCFLPID
          GOTO      PRDEL020 IF NOT EQUAL
          PACK      KEY15,SCFLPID,SCFLSID,SCFLFNO
          CALL      DESCFL1
          GOTO      PRDEL010
.
PRDEL020  
PRDEL030  
          PACK      KEY10,OLDPID,SP20
          CALL      RSSCMA1
          CALL      RKSCMA1
          BRANCH    OVRCD,PRDEL040
          MATCH     OLDPID,SCMAPID
          GOTO      PRDEL040 IF NOT EQUAL
          PACK      KEY10,SCMAPID,SCMASID
          CALL      DESCMA1
          GOTO      PRDEL030
.
PRDEL040  
PRDEL050  
          PACK      KEY15,OLDPID,SP20
          CALL      RSSCPS1
          CALL      RKSCPS1
          BRANCH    OVRCD,PRDEL060
          MATCH     OLDPID,SCPSPID
          GOTO      PRDEL060 IF NOT EQUAL
          PACK      KEY15,SCPSPID,SCPSSID,SCPSROW,SCPSCOL
          CALL      DESCPS1
          GOTO      PRDEL050
.
PRDEL060  
          PACK      KEY12,OLDPID,SP20
          CALL      RSSCSB1
          CALL      RKSCSB1
          BRANCH    OVRCD,PRDEL070
          MATCH     OLDPID,SCSBPID
          GOTO      PRDEL070 IF NOT EQUAL
          PACK      KEY12,SCSBPID,SCSBSID,SCSBLIN
          CALL      DESCSB1
          GOTO      PRDEL060
.
PRDEL070  
          PACK      KEY13,OLDPID,SP20
          CALL      RSSCSL1
          CALL      RKSCSL1
          BRANCH    OVRCD,PRDEL080
          MATCH     OLDPID,SCSLPID
          GOTO      PRDEL080 IF NOT EQUAL
          PACK      KEY13,SCSLPID,SCSLSID,SCSLSEL
          CALL      DESCSL1
          GOTO      PRDEL070
.
PRDEL080  
PRDEL999  MOVE      OLDPID,SCPRPID
          RETURN
.
RDTRFL1   MOVE      ZERO,OVRCD
          READ      TRANFILE,SEQ;SCFLPID,SCFLSID,SCFLFNO,SCFLDES:
                                  SCFLTYP,SCFLFLD,SCFLCAL,SCFLWPF,SCFLFSC:
                                  SCFLDEC,SCFLMIN,SCFLMAX,SCFLFLN,SCFLFTY:
                                  SCFLKTY,SCFLMAN,SCFLFEX,SCFLDAF,SCFLMUL:
                                  SCFLKEY,SCFLMTY,SCFLSPA
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTRFL1   WRITE     TRANFILE,SEQ;SCFLPID,SCFLSID,SCFLFNO,SCFLDES:
                                  SCFLTYP,SCFLFLD,SCFLCAL,SCFLWPF,SCFLFSC:
                                  SCFLDEC,SCFLMIN,SCFLMAX,SCFLFLN,SCFLFTY:
                                  SCFLKTY,SCFLMAN,SCFLFEX,SCFLDAF,SCFLMUL:
                                  SCFLKEY,SCFLMTY,SCFLSPA
          RETURN
.
RDTRMA1   MOVE      ZERO,OVRCD
          READ      TRANFILE,SEQ;SCMAPID,SCMASID,SCMADES,SCMATOP:
                                 SCMALEF,SCMABOT,SCMARIG,SCMACBF,SCMACAF:
                                 SCMATYP,SCMASEX,SCMAFSC,SCMACLR,SCMAFPR,SCMASPA
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTRMA1   WRITE     TRANFILE,SEQ;SCMAPID,SCMASID,SCMADES,SCMATOP:
                                 SCMALEF,SCMABOT,SCMARIG,SCMACBF,SCMACAF:
                                 SCMATYP,SCMASEX,SCMAFSC,SCMACLR,SCMAFPR,SCMASPA
          RETURN
.
RDTRPR1   MOVE      ZERO,OVRCD
          READ      TRANFILE,SEQ;SCPRPID,SCPRNAM,SCPRNAT,SCPRCBF:
                                 SCPRCAF,SCPRISC,SCPRSPA
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTRPR1   WRITE     TRANFILE,SEQ;SCPRPID,SCPRNAM,SCPRNAT,SCPRCBF:
                                 SCPRCAF,SCPRISC,SCPRSPA
          RETURN
.
RDTRPS1   MOVE      ZERO,OVRCD
          READ      TRANFILE,SEQ;SCPSPID,SCPSSID,SCPSROW,SCPSCOL:
                                 SCPSITM,SCPSLEN,SCPSMAN,SCPSATT,SCPSNOR:
                                 SCPSMTY,SCPSFMT,SCPSSPA
          GOTO      OVERCOND IF OVER
          MATCH     "A",SCPSATT
          IF        @EQUAL
            CMOVE     0x0A,SCPSATT
          ENDIF
          RETURN
.
WRTRPS1   
          CMATCH    0x0A,SCPSATT
          IF        @EQUAL
            CMOVE     "A",SCPSATT
          ENDIF
          WRITE     TRANFILE,SEQ;SCPSPID,SCPSSID,SCPSROW,SCPSCOL:
                                 SCPSITM,SCPSLEN,SCPSMAN,SCPSATT,SCPSNOR:
                                 SCPSMTY,SCPSFMT,SCPSSPA
          RETURN
.
RDTRSB1   MOVE      ZERO,OVRCD
          READ      TRANFILE,SEQ;SCSBPID,SCSBSID,SCSBLIN,SCSBDAT:
                                 SCSBATT,SCSBSPA
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTRSB1   WRITE     TRANFILE,SEQ;SCSBPID,SCSBSID,SCSBLIN,SCSBDAT:
                                 SCSBATT,SCSBSPA
          RETURN
.
RDTRSL1   MOVE      ZERO,OVRCD
          READ      TRANFILE,SEQ;SCSLPID,SCSLSID,SCSLSEL,SCSLITM:
                                 SCSLROW,SCSLCOL,SCSLDES,SCSLSEC,SCSLSPA
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTRSL1   WRITE     TRANFILE,SEQ;SCSLPID,SCSLSID,SCSLSEL,SCSLITM:
                                 SCSLROW,SCSLCOL,SCSLDES,SCSLSEC,SCSLSPA
          RETURN
.
RDTRTR1   MOVE      ZERO,OVRCD
          READ      TRANFILE,SEQ;DRECTYPE
          GOTO      OVERCOND IF OVER
          MOVE      ZERO,RECTYPE
          MOVE      DRECTYPE,RECTYPE
          RETURN
.
WRTRTR1   WRITE     TRANFILE,SEQ;RECTYPE
          RETURN
.
.
          INC       SCRSBGIO/INC
          INC       SCRSLTIO/INC
          INC       SCRPSTIO/INC
          INC       SCRFLDIO/INC
          INC       SCRMASIO/INC
          INC       SCRPRGIO/INC
          INC       IBAOVRIO/INC
.
HITENTER  KEYIN     ANS
          RETURN
