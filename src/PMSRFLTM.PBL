. *----------------------------------------------------------------------
. * Include Name  :   PMSRFLTM.INC
. *
. * Function      :   CGI variables for PMSRFLFD - pmsrflaf.dat
. *----------------------------------------------------------------------
.  Modifications Done :
.
.  V9.09.00   05/10/2007  Jill Habasque CAR 150324
.             Created include
. *----------------------------------------------------------------------
.
.------------------------------------------------------------
.clear cgi vars for patient referral details file
.------------------------------------------------------------
CPPMRF00  MOVE      Z70,PMRFL001
          MOVE      Z70,PMRFL002
          MOVE      Z70,PMRFL003
          MOVE      Z70,PMRFL004
.
CPPMRF99  RETURN
.------------------------------------------------------------
. Add patient referral details file
.------------------------------------------------------------
PMRF0000  BRANCH    FLDITMNO,PMRF0100,PMRF0200,PMRF0300,PMRF0400,PMRF0500:
                             PMRF0600,PMRF0700,PMRF0800,PMRF0900,PMRF1000:
                             PMRF1100
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      PMRF9999
.
PMRF0100  WRITE     HTMLFILE;PMRFURNO;
          GOTO      PMRF9999
.
PMRF0200  MOVE      PMRFCONS,GENPCODE
          CALL      HCPDET00
          GOTO      PMRF9999
.
PMRF0300  MATCH     PMRFEXPD,SP70
          GOTO      PMRF9999 IF EQUAL
          UNPACK    PMRFEXPD,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      PMRF9999
.
PMRF0400  MOVE      PMRFRFGP,GENPCODE
          CALL      HCPDET00
          GOTO      PMRF9999
.
PMRF0500  MATCH     PMRFCDAT,SP70
          GOTO      PMRF9999 IF EQUAL
          UNPACK    PMRFCDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      PMRF9999
.
PMRF0600  WRITE     HTMLFILE;PMRFCTIM;
          GOTO      PMRF9999
.
PMRF0700  PACK      KEY10,PMRFCUID,SP70
          CALL      RDWBSE1
          WRITE     HTMLFILE;WBSENAM;
          GOTO      PMRF9999
.
PMRF0800  MATCH     PMRFUDAT,SP70
          GOTO      PMRF9999 IF EQUAL
          UNPACK    PMRFUDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      PMRF9999
.
PMRF0900  WRITE     HTMLFILE;PMRFUTIM;
          GOTO      PMRF9999
.
PMRF1000  PACK      KEY10,PMRFUUID,SP70
          CALL      RDWBSE1 
          WRITE     HTMLFILE;WBSENAM;
          GOTO      PMRF9999
.
PMRF1100  CALL      LISRFL00
          GOTO      PMRF9999
.
PMRF9999  RETURN
.
.------------------------------------------------------------
.   Set Parameters for patient referral table
.------------------------------------------------------------
SPPMR000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPPMR001,SPPMR002,SPPMR003,SPPMR004
.
          MOVE      ONE,EXIT
          GOTO      SPPMR999
.
SPPMR001  PACK      PMRFL001,QRYDATA,SP70
          GOTO      SPPMC999
.
SPPMR002  PACK      PMRFL002,QRYDATA,SP70
          GOTO      SPPMC999
.
SPPMR003  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00            * Set CMON from MTHNAM3
          PACK      PMRFL003,CCENT,CYEAR,CMON,CDAY
          GOTO      SPPMC999
.
SPPMR004  PACK      PMRFL004,QRYDATA,SP70
          GOTO      SPPMC999
.
SPPMR999  RETURN  
.
.------------------------------------------------------------
.  Update patient referral details table file
.------------------------------------------------------------
MVPMRF00  MATCH     PMRFL001,Z70
          IF        !@EQUAL
            MOVE      PMRFL001,PMRFURNO
          ENDIF
. 
          MATCH     PMRFL002,Z70
          IF        !@EQUAL
            MOVE      PMRFL002,PMRFCONS
          ENDIF
. 
          MATCH     PMRFL003,Z70
          IF        !@EQUAL
            MOVE      PMRFL003,PMRFEXPD
          ENDIF
. 
          MATCH     PMRFL004,Z70
          IF        !@EQUAL
            MOVE      PMRFL004,PMRFRFGP
          ENDIF
. 
MVPMRF99  RETURN
.
.------------------------------------------------------------
.  Clear patient referral details variables 
.------------------------------------------------------------
CLPMSRFL  PACK      PMRFURNO,SP70
          PACK      PMRFCONS,SP70
          PACK      PMRFEXPD,SP70
          PACK      PMRFRFGP,SP70
          PACK      PMRFCDAT,SP70
          PACK      PMRFCTIM,SP70 
          PACK      PMRFCUID,SP70
          PACK      PMRFUDAT,SP70
          PACK      PMRFUTIM,SP70 
          PACK      PMRFUUID,SP70
          PACK      PMRFSPAR,SP70
.
          RETURN
+

.------------------------------------------------------------------------------
.               Referral Details Table
.------------------------------------------------------------------------------
LISRFL00  PACK      KEY26,URNUMBER,SP70
          CALL      RSPMRFL1
LISRFL10  CALL      RKPMRFL1
          BRANCH    OVRCD,LISRFL99
.
          MATCH     URNUMBER,PMRFURNO
          GOTO      LISRFL99 IF NOT EQUAL
.
          CLEAR     HTMLLINK
          APPEND    "javascript:ReferralDetails('",HTMLLINK
          APPEND    PMRFURNO,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMRFCONS,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMRFEXPD,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK
.
          PACK      KEY10,PMRFCONS,SP70
          CALL      RDPMHCP1
          IF        OVRCD<>1
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
          ELSE
            CLEAR     DOCFNAME
            APPEND    "Invalid Consultant - ",DOCFNAME
            APPEND    PMRFCONS,DOCFNAME
            RESET     DOCFNAME
          ENDIF
          MOVE      DOCFNAME,CONSNAME
.
          PACK      DOCFNAME,SP70
          MATCH     SP70,PMRFRFGP
          GOTO      LISRFL80 IF EQUAL
.
          PACK      KEY10,PMRFRFGP,SP70
          CALL      RDPMHCP1
          IF        OVRCD<>1
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
          ELSE
            CLEAR     DOCFNAME
            APPEND    "Invalid Referring Consultant - ",DOCFNAME
            APPEND    PMRFCONS,DOCFNAME
            RESET     DOCFNAME
          ENDIF
.
LISRFL80  WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":
                             "#"",CONSNAME,"#",":
                             "#"",PMRFEXPD,"#",":
                             "#"",DOCFNAME,"#")"
          GOTO      LISRFL10
.
LISRFL99  RETURN
.
