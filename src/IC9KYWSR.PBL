.------------------------------------------------------------
. Function          : Procedure for Seach of 
.                      Fill in description of Search
.
. Parameters        : CKEYSUSP    1=Show Suspended 
.                   : CKEYORDR    0=Key Word Search, 1=Alphabetic Search
.                      Fill in Other Parameters Required
.
. Includes Required :
. Open Tables Req   :
. Returns Fields    : OVRCD    0=Valid Record Selected, 1=No Record Selected
.                     Record Read from xxxxxxx Table
.
. Modifications :
.         V5.07.01  05/07/1999  Jill Habasque SRF 645594
.                   Copied ICOKYWSR and changed to only open the ICD9 files
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Routine to Seach for
.  a coded field via either a Key Word Index or Alphabetical
.  Index and output a ? Selection List.
.  
.  A Key Word Index Table is Defined in the following Manner
.
.  patoicaf
.  PATOICA1
.  PATOICA2    
.  
.  PTOIICD     DIM       xx    * Code Field 
.  PTOIKWD     DIM       15    * Key Word
.
.  Index 1   PTOIICD, PTOIKWD
.  Index 2   PTOIKWD, PTOIICD
.
.To convert this Procedure the following should be performed in this sequence
.    Global Change paticdof to name of Alphabetic Search Table   eg patdo1af
.    Global Change PATICDO3 to index of Alphabetic Search Table  eg PATDO1A2
.    Global Change RDICO3  to the Read on Alpha Search Index    eg RDDOCT2
.    Global Change RDKICO3 to the Read Next Alpha Search Index  eg RDKDOCT2
.    Global Change RDPICO3 to the Read Prev Alpha Search Index  eg RDPDOCT2
.    Global Change RDSICO3 to the Read Post Alpha Search Index  eg RDSDOCT2
.    Global Change OPCODE to the Code Field from the Table     eg DCODE
.    Global Change ODESC to the Name Field from the Table     eg DSNAME
.    Global Change KEY79    to the Key for the Aplha Index       eg KEY26
.    Global Change patoicaf to name of Key Word Table            eg patdkiaf
.    Global Change PATOICA1 to index 1 of Key Word Table         eg PATDKIA1
.    Global Change PATOICA2 to index 2 of Key Word Table         eg PATDKIA2
.    Global Change PTOIICD to Key Word Index Code Field         eg PTDKDOC
.    Global Change PTOIKWD to Key Word Index Word Field         eg PTDKKWD
.    Global Change HPT9IA   to Routine Label                     eg HPTDKA00
.    Global Change KEY24    to the Key needed for Key Word Table eg KEY21
.    Global Change KEY9    to the Key for the Code Field        eg KEY6
.    Global Change RDICO1 to the Read on Master Table          eg RDDOCT1
.    Global Change PTOI     to the IO Call ID for the Key Words  eg PTDK
.    Global Change INACTV to the field to Indicate Inactive Code eg DRSTAT
.
.    Modify List Display to Suit
.
.    Note : vi Global change command   :%s/FromString/ToString/g
.  
.  A Alphabetic Index on a Master Table is Defined in the following Manner
.                              
.  OPCODE    DIM       xx    * Code Field 
.  ODESC    DIM       xx    * Name or Description Field
.
.------------------------------------------------------------
. Search 
.------------------------------------------------------------
HPT9IA00  MOVE      CKEYSUSP,SHOWSUSP
          MOVE      CKEYORDR,SEARCHTY
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATOICA1,"patoicaf"
          TRAPCLR   IO
          IF        OVRCD=1
            BEEP
            MOVE      "ERROR: Search Table Missing (patoicaf)- ",DISPMSG
            CALL      MESSAGE1
            GOTO      HPT9IA99
          ENDIF
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATICDO2,"paticdof"
          TRAPCLR   IO
          IF        OVRCD=1
            BEEP
            MOVE      "ERROR: Search Table Missing (paticdof)- ",DISPMSG
            CALL      MESSAGE1
            GOTO      HPT9IA99
          ENDIF
.
          OPEN      PATOICA2,"patoicaf"
          MOVE      ZERO,CSCRSIZ
.
          MOVE      "08",HTOP
          MOVE      "24",HBOT
          MOVE      "01",HLEF
          MOVE      "80",HRIG
.
HPT9IA10  CALL      KWINT000
          BRANCH    EXIT,HPT9IA90
          MOVE      STDCOL00,STDCOL01
          ADD       " 5",STDCOL01
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON,"Code"            
          MOVE      STDCOL01,STDCOL02
          ADD       "10",STDCOL02
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"Description",*HOFF
          MOVE      STDCOL02,STDCOL03
          ADD       "25",STDCOL03
.
          CALL      SHWFL900                * Display Flags
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
HPT9IA20  IF        SEARCHTY=1
            CALL      ALPSR900              * Alphabetic Search
          ELSE
            CALL      KEYSR900              * Key Word Search
          ENDIF
          BRANCH    DOAGAIN,HPT9IA10,HPT9IA20
.
          PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
.
HPT9IA90  CLOSE     PATOICA1
          CLOSE     PATOICA2
HPT9IA99  RETURN
.------------------------------------------------------------
. Perform Key Word Search
.------------------------------------------------------------
KEYSR900  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    STDROW+1,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-13,FLAGCOL      
          DISPLAY   *PFLAGCOL:FLAGROW,*V2LON,*ULON,"Enter Key Words for Search"
          ASSIGN    STDROW+3,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-30,FLAGCOL
          KEYIN     *PFLAGCOL:FLAGROW,*V2LON,KEYWRD00
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          PACK      KEYWRD00,KEYWRD00,SP30,SP30,SP10
          RESET     KEYWRD00
          CALL      GETWRD00     * Break Words into Seprate Fields
.
KEYSR909  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          MOVE      SP9,PTOIICD 
.
          STRIP     KEYWRD01
          PACK      KEY24,KEYWRD01,SP30
          CALL      RSPTOI2 
KEYSR910  CALL      RKPTOI2 
          BRANCH    OVRCD,KEYSR915
.
          CALL      CHRE9000
          BRANCH    OVRCD,KEYSR910,KEYSR915
.
          ADD       ONE,SELITMNO
          ADD       ONE,STDROW
          PACK      KEY24,PTOIKWD,PTOIICD
          STORE     KEY24,SELITMNO,ST1,ST2,ST3,ST4,ST5:                      
                                 ST6,ST7,ST8,ST9,ST10:
                                 ST11,ST12,ST13,ST14,ST15:
                                 ST16,ST17
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,SELITMNO,DOT;
          IF        CHECKSUS<>0
            DISPLAY   *V2LON,*BLINKON,"*"
          ENDIF
          DISPLAY   *PSTDCOL01:STDROW,PTOIICD
          DISPLAY   *PSTDCOL02:STDROW,ODESC
. 
          COMPARE   MAXITEM,SELITMNO
          GOTO      KEYSR910 IF NOT EQUAL
.
KEYSR915  MOVE      ONE,NEWDITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,NEWDITEM,DOT
.
KEYSR920  CALL      KWSEL000
          BRANCH    EXIT,KEYSR930,KEYSR935,KEYSR940,KEYSR945,KEYSR998:
                                  KEYSR991,KEYSR900,KEYSR970
.                        Next     Last     Home     End      Expand/Reduce
.                                 Exit     Search   Flags
          GOTO      KEYSR992
.
KEYSR930  CALL      RKPTOI2 
          BRANCH    OVRCD,KEYSR985
          CALL      CHRE9000
          BRANCH    OVRCD,KEYSR930,KEYSR985
.
          PACK      KEY24,PTOIKWD,PTOIICD,SP30                                  
          CALL      RPPTOI2 
          MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      KEYSR910
.
KEYSR935  MOVE      ST1,KEY24
          CALL      RDPTOI2 
.
KEYSR936  CALL      RPPTOI2 
          BRANCH    OVRCD,KEYSR985
          CALL      CHRE9000
          BRANCH    OVRCD,KEYSR936,KEYSR985
          PACK      KEY24,PTOIKWD,PTOIICD,SP30                                  
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
.
KEYSR937  CALL      RPPTOI2 
          BRANCH    OVRCD,KEYSR938
          CALL      CHRE9000
          BRANCH    OVRCD,KEYSR937,KEYSR938
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      KEYSR937 IF NOT EQUAL
.
KEYSR938  MOVE      ZERO,SELITMNO
          GOTO      KEYSR910
.
KEYSR940  MOVE      ST1,KEY24
          CALL      RDPTOI2 
KEYSR941  CALL      RPPTOI2 
          BRANCH    OVRCD,KEYSR985
          CALL      CHRE9000
          BRANCH    OVRCD,KEYSR941,KEYSR985
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      KEYSR909
.
KEYSR945  CALL      RKPTOI2 
          BRANCH    OVRCD,KEYSR985
          CALL      CHRE9000
          BRANCH    OVRCD,KEYSR945,KEYSR985
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,SELITMNO
          PACK      KEY24,KEYWRD01,Z70                                          
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
          CALL      RSPTOI2 
.
KEYSR946  CALL      RPPTOI2 
          BRANCH    OVRCD,KEYSR947
          CALL      CHRE9000
          BRANCH    OVRCD,KEYSR946,KEYSR947
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      KEYSR946 IF NOT EQUAL
.
KEYSR947  MOVE      ZERO,SELITMNO
          GOTO      KEYSR910
.
KEYSR970  CALL      FLAG9000
          MOVE      TWO,DOAGAIN
          GOTO      KEYSR999
.
KEYSR985  LOAD      KEY24,SELITMNO,ST1,ST2,ST3,ST4,ST5:                         
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY24,PTOIKWD,PTOIICD
          CALL      RDPTOI2 
.
KEYSR990  BEEP
          GOTO      KEYSR920
.
KEYSR991  MOVE      SP30,KEY24
          MOVE      SP9,PTOIICD 
          PACK      PTOIKWD,SP30,SP30,SP10
          MOVE      ONE,OVRCD
          GOTO      KEYSR996
.
KEYSR992  MOVE      SP30,KEY24
          MOVE      SP9,PTOIICD 
          LOAD      KEY24,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY24,PTOIKWD,PTOIICD
          MOVE      PTOIICD,KEY9
          CALL      RDICO1
.
KEYSR996  MOVE      ZERO,DOAGAIN
          GOTO      KEYSR999
.
KEYSR998  MOVE      ONE,DOAGAIN
.
KEYSR999  RETURN
.------------------------------------------------------------
. Perform Alphabetic Search
.------------------------------------------------------------
ALPSR900  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    STDROW+1,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-10,FLAGCOL
          DISPLAY   *PFLAGCOL:FLAGROW,*V2LON,*ULON,"Enter Name for Search"
          ASSIGN    STDROW+3,FLAGROW
          ASSIGN    ((HRIG-HLEF)/2)-30,FLAGCOL
          KEYIN     *PFLAGCOL:FLAGROW,*V2LON,KEYWRD00
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          PACK      KEYWRD00,SP30,SP30,SP10
          RESET     KEYWRD00
.
ALPSR909  MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          MOVE      SP9,OPCODE 
.
          STRIP     KEYWRD00
          PACK      KEY79,KEYWRD00,SP30,SP30,SP20
          CALL      RDSICO2
ALPSR910  CALL      RDKICO2
          BRANCH    OVRCD,ALPSR915
.
          CALL      CHRE9000
          BRANCH    OVRCD,ALPSR910,ALPSR915
.
          ADD       ONE,SELITMNO
          ADD       ONE,STDROW
          PACK      KEY79,ODESC,OPCODE
          STORE     KEY79,SELITMNO,ST1,ST2,ST3,ST4,ST5:                        
                                 ST6,ST7,ST8,ST9,ST10:
                                 ST11,ST12,ST13,ST14,ST15:
                                 ST16,ST17
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,SELITMNO,DOT;
          IF        CHECKSUS<>0
            DISPLAY   *V2LON,*BLINKON,"*"
          ENDIF
          DISPLAY   *PSTDCOL01:STDROW,OPCODE
          DISPLAY   *PSTDCOL02:STDROW,ODESC 
.
          COMPARE   MAXITEM,SELITMNO
          GOTO      ALPSR910 IF NOT EQUAL
.
ALPSR915  MOVE      ONE,NEWDITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,NEWDITEM,DOT
.
ALPSR920  CALL      KWSEL000
          BRANCH    EXIT,ALPSR930,ALPSR935,ALPSR940,ALPSR945,ALPSR998:
                                  ALPSR991,ALPSR900,ALPSR970
.                        Next     Last     Home     End      Expand/Reduce
.                                 Exit     Search   Flags
          GOTO      ALPSR992
.
ALPSR930  CALL      RDKICO2
          BRANCH    OVRCD,ALPSR985
          CALL      CHRE9000
          BRANCH    OVRCD,ALPSR930,ALPSR985
.
          PACK      KEY79,ODESC,OPCODE,SP30,SP30,SP20
          CALL      RPKICO2
          MOVE      ZERO,SELITMNO
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      ALPSR910
.
ALPSR935  MOVE      ST1,KEY79
          CALL      RDICO2
.
ALPSR936  CALL      RPKICO2
          BRANCH    OVRCD,ALPSR985
          CALL      CHRE9000
          BRANCH    OVRCD,ALPSR936,ALPSR985
          PACK      KEY79,ODESC,OPCODE,SP30,SP30,SP20
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
.
ALPSR937  CALL      RPKICO2
          BRANCH    OVRCD,ALPSR938
          CALL      CHRE9000
          BRANCH    OVRCD,ALPSR937,ALPSR938
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      ALPSR937 IF NOT EQUAL
.
ALPSR938  MOVE      ZERO,SELITMNO
          GOTO      ALPSR910
.
ALPSR940  MOVE      ST1,KEY79
          CALL      RDICO2
ALPSR941  CALL      RPKICO2
          BRANCH    OVRCD,ALPSR985
          CALL      CHRE9000
          BRANCH    OVRCD,ALPSR941,ALPSR985
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      ALPSR909
.
ALPSR945  CALL      RDKICO2
          BRANCH    OVRCD,ALPSR985
          CALL      CHRE9000
          BRANCH    OVRCD,ALPSR945,ALPSR985
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,SELITMNO
          PACK      KEY79,KEYWRD01,Z70,Z70                                          
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,SELITMNO
          CALL      RDSICO2
.
ALPSR946  CALL      RPKICO2
          BRANCH    OVRCD,ALPSR947
          CALL      CHRE9000
          BRANCH    OVRCD,ALPSR946,ALPSR947
.
          SUB       ONE,SELITMNO
          COMPARE   ZERO,SELITMNO
          GOTO      ALPSR946 IF NOT EQUAL
.
ALPSR947  MOVE      ZERO,SELITMNO
          GOTO      ALPSR910
.
ALPSR970  CALL      FLAG9000
          MOVE      TWO,DOAGAIN
          GOTO      ALPSR999
.
ALPSR985  LOAD      KEY79,SELITMNO,ST1,ST2,ST3,ST4,ST5:
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY79,ODESC,OPCODE                                       
          CALL      RDICO2
.
ALPSR990  BEEP
          GOTO      ALPSR920
.
ALPSR991  MOVE      SP9,OPCODE 
          PACK      KEY79,SP30,SP30,SP20
          PACK      ODESC,SP30,SP30,SP20
          MOVE      ONE,OVRCD
          GOTO      ALPSR996
.
ALPSR992  PACK      KEY79,SP30,SP30,SP20
          MOVE      SP9,OPCODE 
          LOAD      KEY79,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY79,ODESC,OPCODE                                       
          MOVE      OPCODE,KEY9
          CALL      RDICO1
.
ALPSR996  MOVE      ZERO,DOAGAIN
          GOTO      ALPSR999
.
ALPSR998  MOVE      ONE,DOAGAIN
.
ALPSR999  RETURN
.------------------------------------------------------------
. Alter Flags
.------------------------------------------------------------
FLAG9000  BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    MINITEM+2,F2
FLAG9110  MOVE      "N",ANS
          KEYIN     *PSTDCOL02:F2,"Show Inactive (",*V2LON,"Y",*HOFF,"/":
                                  *V2LON,"N",*HOFF,")    : ",*V2LON,*RV,ANS
          REP       UPPLOW,ANS
          MOVE      ONE,SHOWSUSP
          MATCH     "Y",ANS
          GOTO      FLAG9190 IF EQUAL
          MATCH     "N",ANS
          GOTO      FLAG9110 IF NOT EQUAL
          MOVE      ZERO,SHOWSUSP
FLAG9190  ASSIGN    MINITEM+3,F2
FLAG9210  MOVE      "K",ANS
          KEYIN     *PSTDCOL02:F2,*V2LON,"K",*HOFF,"ey Word or ":
                                  *V2LON,"A",*HOFF,"lphabetic Search : ":
                                  *V2LON,*RV,ANS
          REP       UPPLOW,ANS
          MOVE      ZERO,SEARCHTY
          MATCH     "K",ANS
          GOTO      FLAG9290 IF EQUAL
          MATCH     "A",ANS
          GOTO      FLAG9210 IF NOT EQUAL
          MOVE      ONE,SEARCHTY
FLAG9290
          CALL      SHWFL900
          RETURN 
.------------------------------------------------------------
. Display Flag Settings
.------------------------------------------------------------
SHWFL900  CLEAR     FLAGDESC
          IF        SHOWSUSP=1
            APPEND    "Show Inactive, ",FLAGDESC
          ELSE
            APPEND    "Hide Inactive, ",FLAGDESC
          ENDIF
          IF        SEARCHTY=1
            APPEND    "Alphabetic Search",FLAGDESC
          ELSE
            APPEND    "Key Word Search",FLAGDESC
          ENDIF
          RESET     FLAGDESC
.
          ASSIGN    HBOT,FLAGROW
          MOVELPTR  FLAGDESC,F2
          ADD       "9",F2
          ASSIGN    ((HRIG-HLEF)/2)-(F2/2),FLAGCOL
          MOVELPTR  FLAGDESC,F2
          SFORMAT   DISPVAR,F2
          MOVE      FLAGDESC,DISPVAR
          DISPLAY   *PFLAGCOL:FLAGROW,*V2LON,*ULON," Flags: ",DISPVAR,SP1
          SFORMAT   DISPVAR,127
.
          RETURN
.------------------------------------------------------------
. Check Record
.------------------------------------------------------------
CHRE9000  BRANCH    SEARCHTY,CHRE9100
          MATCH     KEYWRD01,PTOIKWD
          GOTO      CHRE9920 IF NOT EQUAL                                       
          IF        KEYWRDNO>1
            PACK      SAVKEY24,PTOIKWD,PTOIICD
            CALL      MCHWR900
            BRANCH    EXIT,CHRE9910
            MOVE      SAVKEY24,KEY24
            CALL      RDPTOI2
          ENDIF
.
          MOVE      PTOIICD,KEY9
          CALL      RDICO1
          BRANCH    OVRCD,CHRE9910
          GOTO      CHRE9800
.
CHRE9100  MATCH     KEYWRD00,ODESC
          GOTO      CHRE9920 IF NOT EQUAL                                       
.
CHRE9800  MOVE      ZERO,INACTV
          MOVE      INACTV,CHECKSUS
          BRANCH    SHOWSUSP,CHRE9900
          BRANCH    INACTV,CHRE9910
.
CHRE9900  MOVE      ZERO,OVRCD
          GOTO      CHRE9999
CHRE9910  MOVE      ONE,OVRCD
          GOTO      CHRE9999
CHRE9920  MOVE      TWO,OVRCD
CHRE9999  RETURN
.------------------------------------------------------------
. Check for Matching Words
.------------------------------------------------------------
MCHWR900  PACK      SAVKEY9,PTOIICD
          MOVE      ONE,F2
          MOVE      ZERO,EXIT
.
MCHWR910  COMPARE   KEYWRDNO,F2
          GOTO      MCHWR999 IF EQUAL
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY24,SAVKEY9,KEYWRDXX
          CALL      RDPTOI1
          COMPARE   ZERO,OVRCD
          GOTO      MCHWR910 IF EQUAL
.
          CALL      RKPTOI1
          BRANCH    OVRCD,MCHWR990
          PACK      KEY9,PTOIICD,SP9
          MATCH     SAVKEY9,KEY9 
          GOTO      MCHWR990 IF NOT EQUAL
.
          STRIP     KEYWRDXX
          MATCH     PTOIKWD,KEYWRDXX
          GOTO      MCHWR990 IF NOT EQUAL
          GOTO      MCHWR910
.
MCHWR990  MOVE      ONE,EXIT
MCHWR999  RETURN
