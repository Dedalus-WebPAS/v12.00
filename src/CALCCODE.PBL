.
.CALCCODE.PBL
.
.******************************************************************************
.         CALCCODE : Calculate Hours duration for ICU - TCINDC3 = 5
.                                                 CCU - TCINDC3 = 4
.                                                 NCU - TCINDC3 = 7
.         Parameter: DIM1 - Bed type Indicator TCINDC3
.         Return   : FORM6
.
.         Modifications :
.         
.         V12.00.01 30/05/2025  Don Nguyen    TSK 0955096
.                   Alphanumeric visit number changes around AADMNO
.         V11.03.02 14/06/2023  Don Nguyen    TSK 0928367
.                   Re-worked code to round up the ICU/NCU hrs; move to RTIM0000
.         V11.03.01 17/05/2023  Don Nguyen    TSK 0928367
.                   Added RUPICU00 & RUPNCU00 to round up the hrs in ICU & NCU
.                   for Victorian hospitals.
.******************************************************************************
.         V10.15.01 09/10/2019 Thanh T        TSK 0860841
.         Changed RTIM0000 to cater for rounding parameter MRCNROUN
.******************************************************************************
.         V10.14.01 27/03/2019 Peter Vela     Task 0857392
.         Added PTICCHSC to WRPTICU1
.
.         V10.07.01 26/10/2015 J.Tan          CAR 317554
.         Mods Daycase calculation (CDCS000) to use the Overnight calculation
.
.         V10.06.01 19/03/2015 Don Nguyen     CAR 300414
.         Added check for PTCNICUT before performing calculations
.
.         V10.05.03 22/12/2014 Jill Parkinson CAR 309655
.         Moved checks for zero in UICU000 so records can be updated with zero,
.         they just won't be written if the value is zero
.
.         V10.05.02 17/12/2014 J.Tan  CAR 309655
.         Mods to initialise F6 - Queensland ICU time
.
.         V10.05.01 17/09/2014 J.Tan  CAR 299241
.         Added Minutes of ICU for Queensland
.
.         V10.03.01 07/11/2011 J.Tan  CAR 254871
.         Fixed checking for ICU daycase
.
.         V10.02.01 23/08/2011 J.Tan  CAR 249025
.         Mods to use TTRNDATE TTRNTIME instead of TRANDATE/TTRNTIME
.
.         V10.00.00 J.Tan  CAR 217548
.         New routine to recalculate ICU/CCU/NCU from trans.file
.
.******************************************************************************
.
          DEFPROC   CALCCODE
.
TOTBEDHR  FORM      8       * Total bed hours
TOTBEDMN  FORM      8       * Total bed minutes
TTRNDATE  DIM       8
TTRNTIME  DIM       8
PTCNHDPS  FORM      1
.
          INC       PATICUFD/INC
          INC       PATTRNFD/INC
.
.---------------------------------------------------------------------
. CCODE00 - Recalculate hrs of ICU and CCU from admission date
.---------------------------------------------------------------------
CCODE000  OPEN      PATICUA1,"paticuaf"
          OPEN      PATTRAN2,"pattranf"
          READ      CONTROLF,EIGHTY;*250,PTCNHDPS
          READ      CONTROLF,HUND05;*173,PTCNICUT
.
          COMPARE   ZERO,PTCNICUT  * Checking for an ICU/CCU/NCU Bed Type?
          GOTO      CCODE999 IF EQUAL
.
          CALL      CLRICU00       * Clear ICU vars for Re-calculation
.
          MOVE      "5",DIM1       * for ICU
          CALL      CCOD0000       * Durations for ICU time
          CALL      UICU0000       * Update ICU time
.
          MOVE      "4",DIM1       * for CCU
          CALL      CCOD0000       * Durations for CCU time
          CALL      UCCU0000       * Update CCU time
.
          MOVE      "7",DIM1       * for NCU
          CALL      CCOD0000       * Durations for NCU time
          CALL      UNCU0000       * Update NCU time
.
          CLOSE     PATICUA1
          CLOSE     PATTRAN2
.
CCODE999  GOTO      ECOD9999
+
.---------------------------------------------------------------------
.         Calculate duration of ICU/CCU/NCU         
.---------------------------------------------------------------------
CCOD0000  UNPACK    SP70,SAVDATE,SAVTIME,TTRNDATE,TTRNTIME
          MOVE      ZERO,FORM6A
.
.         COMPARE   THREE,ASTAT            * Discharge?
.         GOTO      CCOD1000 IF NOT EQUAL
.
.         MATCH     ADATE,DDATE             * day case patient ?
.         GOTO      CCOD1000 IF NOT EQUAL   * no.
.
.         CALL      CDCS0000                * Calculate duration for Daycase
.         GOTO      CCOD9000
.
CCOD1000  PACK      KEY30,AADMNO,SP70
          CALL      RDSTRAN2
CCOD1200  CALL      RDKTRAN2
          BRANCH    OVRCD,CCOD4000
.
          MATCH     TADMN,AADMNO            * Admission No?
          GOTO      CCOD4000 IF NOT EQUAL
.
          MATCH     ANSA,TMOVE              * Admission record ?
          GOTO      CCOD2000 IF EQUAL
.
          MATCH     ANSR,TMOVE              * Return record ?
          GOTO      CCOD2000 IF EQUAL
.
          CMATCH    ANSC,TMOVE              * Change record ?
          GOTO      CCOD2200 IF EQUAL
          GOTO      CCOD3000
.
.         Admission/Return record
.
CCOD2000  PACK      KEY5,ANSB,ANST,TRBTYP  * Check for ICU
          CALL      RDCODE1
          BRANCH    OVRCD,CCOD1200
          MATCH     DIM1,TCINDC3
          GOTO      CCOD1200 IF NOT EQUAL
.
          MOVE      TDATE,SAVDATE        * Save the transfer date
          MOVE      TTIME,SAVTIME        * Save Transfer Time
          GOTO      CCOD1200
.
.         Change
.
CCOD2200  PACK      KEY5,ANSB,ANST,TRBTYP  * Check for ICU
          CALL      RDCODE1
          BRANCH    OVRCD,CCOD2400
          MATCH     DIM1,TCINDC3
          GOTO      CCOD2400 IF NOT EQUAL
.
          MATCH     SP70,SAVDATE
          GOTO      CCOD1200 IF NOT EQUAL
.
          MOVE      TDATE,SAVDATE        * Save the transfer date
          MOVE      TTIME,SAVTIME        * Save Transfer Time
          GOTO      CCOD1200
.
.        Changed to a different bed type, check if it was from selected bedtype
.
CCOD2400  MATCH     SP70,SAVDATE
          GOTO      CCOD1200 IF EQUAL
.
          MOVE      TDATE,TTRNDATE
          MOVE      TTIME,TTRNTIME
          CALL      CDUR0000
          GOTO      CCOD1200
.
.         Discharge/Leave record always on the same bed type as previous record
.
CCOD3000  MATCH     SP70,SAVDATE
          GOTO      CCOD3200 IF EQUAL
.
          MOVE      TDATE,TTRNDATE
          MOVE      TTIME,TTRNTIME
          CALL      CDUR0000
.
CCOD3200  CMATCH    "D",TMOVE
          GOTO      CCOD1200 IF NOT EQUAL
          GOTO      CCOD9000
.
.         Check the last record
.
CCOD4000  MATCH     SP70,SAVDATE
          GOTO      CCOD9000 IF EQUAL
.
          CALL      IBACLOCK
          PACK      TTRNDATE,CCC,CYY,CMM,CDD
          REP       " 0",TTRNDATE
          MOVE      CTIMEIS,TTRNTIME
          CALL      CDUR0000
.
CCOD9000  MOVE      ZERO,FORM6
          MOVE      ZERO,F6
          MOVE      ZERO,F2
          COMPARE   ZERO,FORM6A
          GOTO      CCOD9999 IF EQUAL
.
          CALL      RTIM0000                * Rounding the time
CCOD9999  RETURN
+
.******************************************************************************
.         Calculate duration for Daycase
.******************************************************************************
CDCS0000  PACK      KEY30,AADMNO,Z70
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,CDCS9999
.
          MATCH     TADMN,AADMNO           * same admission ?
          GOTO      CDCS9999 IF NOT EQUAL  * No.
.
          MATCH     "D",TMOVE
          GOTO      CDCS9999 IF NOT EQUAL  * No.
.
.         Check if the last movement was from the required bed type
.
          PACK      KEY5,ANSB,ANST,TRBTYP
          CALL      RDCODE1
          BRANCH    OVRCD,CDCS9999
          MATCH     DIM1,TCINDC3
          GOTO      CDCS9999 IF NOT EQUAL
.
          MOVE      DDATE,TTRNDATE
          MOVE      TTIME,TTRNTIME
.
          PACK      KEY30,AADMNO,SP70
          CALL      RDSTRAN2
          CALL      RDKTRAN2
          BRANCH    OVRCD,CDCS9999
.
          MATCH     TADMN,AADMNO
          GOTO      CDCS9999 IF NOT EQUAL
.
          MATCH     "A",TMOVE
          GOTO      CDCS9999 IF NOT EQUAL
.
          MOVE      TDATE,SAVDATE
          MOVE      TTIME,SAVTIME
          CALL      CDUR0000
.
CDCS9999  RETURN
+
.******************************************************************************
.         Calculate the Dates and Times difference
.******************************************************************************
CDUR0000  MOVE      SAVDATE,FIRSTDAT            * Previous transfer date
          MOVE      TTRNDATE,LASTDATE           * New Transfer date
.
          UNPACK    SAVTIME,CHOUR,KEY1,CMIN
          PACK      FIRSTIME,CHOUR,CMIN         * Previous Transfer Time
.
          UNPACK    TTRNTIME,CHOUR,KEY1,CMIN
          PACK      LASTTIME,CHOUR,CMIN         * New Transfer Time
          CALL      TIMEDIFF                    * Calculate Time Diff in Mins
          ADD       CALCTIME,FORM6A             * Save the total time
.
          UNPACK    SP70,SAVDATE,SAVTIME,TTRNDATE,TTRNTIME
CDUR9999  RETURN
+
.******************************************************************************
.         Rounding the time difference
.******************************************************************************
RTIM0000  ASSIGN    FORM6A/SIXTY,CALCTIME
          MOVE      CALCTIME,FORM6              * Set to Hrs
          MOVE      FORM6,F6                    * save the actual hrs
          ASSIGN    CALCTIME*SIXTY,CALCTIME     * Set hrs back to mins
          ASSIGN    (FORM6A-CALCTIME),FORM6A    * now subtract from sav var
          MOVE      FORM6A,F2                   * Now have remainding mins
.
          COMPARE   THREE,PTCNHDPS
          GOTO      RTIM2000 IF EQUAL           * Victoria
.
          MATCH     "1",MRCNROUN
          GOTO      RTIM1000 IF EQUAL
.
          IF        F2 > ZERO
            ADD       ONE,FORM6                 * Round up mins to 1 hr
          ENDIF
          GOTO      RTIM9999
.
RTIM1000  IF       F2 > 29
            ADD       ONE,FORM6                 * Round up mins to 1 hr
          ENDIF
          GOTO      RTIM9999
.
.
.         For Victoria only, round up the hours
.
RTIM2000  IF        F2 > 29 & F2 < 60
            ADD       ONE,FORM6                 * Round up if mins between 30-59
          ENDIF
          GOTO      RTIM9999
.
RTIM9999  RETURN
+
.---------------------------------------------------------------------
.    Update ICU time
.---------------------------------------------------------------------
UICU0000  COMPARE   FOUR,PTCNHDPS
          GOTO      UICU4000 IF EQUAL       * Queensland stores ICU hrs/minutes
.
...       COMPARE   ZERO,FORM6
...       GOTO      UICU9999 IF EQUAL
.
          MOVE      AADMNO,KEY8
          CALL      RDPTICU1
          IF        OVRCD=0
            ADD       FORM6,PTICUINT        * Add to hrs in ICU (FORM 4)
            CALL      CTBH0000
            IF        PTICUINT>TOTBEDHR
              MOVE      TOTBEDHR,PTICUINT
            ENDIF
            CALL      UPPTICU1
          ELSE
            COMPARE   ZERO,FORM6
            GOTO      UICU9999 IF EQUAL
.
            MOVE      AADMNO,PTICUADM       * Admission number
            MOVE      FORM6,PTICUINT        * Hrs in ICU (FORM 4)
.
            CALL      CTBH0000
            IF        PTICUINT>TOTBEDHR
              MOVE      TOTBEDHR,PTICUINT
            ENDIF
.
            MOVE      ZERO,PTICUMEC         * Hrs on Mechanical Ventilator
            MOVE      ZERO,PTICUCCU         * Hrs in Critical care
            MOVE      ZERO,PTICCPAP         * Hrs on CPAP
            MOVE      ZERO,PTICHNCU         * Hrs in NCU
            MOVE      SP70,PTICMECM         * Minutes of Mechanical Ventilation
            MOVE      SP70,PTICAMBN         * Ambulance client number
            MOVE      PTICUADM,KEY8
            MOVE      SP70,PTICCHSC
            CALL      WRPTICU1
          ENDIF
          GOTO      UICU9999
.
.         Queensland
.
UICU4000  
...       COMPARE   ZERO,F6                 * Hrs in ICU
...       GOTO      UICU5000 IF NOT EQUAL
...       COMPARE   ZERO,F2                 * Minutes in ICU
...       GOTO      UICU9999 IF EQUAL
.
UICU5000  MOVE      AADMNO,KEY8
          CALL      RDPTICU1
          IF        OVRCD=0
            MOVE      F6,PTICUINT         * Hrs in ICU
            MOVE      F2,PTICINTM         * Minutes in ICU
            CALL      CTBD0000            * Check total bed hrs
            CALL      UPPTICU1
          ELSE
            COMPARE   ZERO,F6                 * Hrs in ICU
            GOTO      UICU5500 IF NOT EQUAL
            COMPARE   ZERO,F2                 * Minutes in ICU
            GOTO      UICU9999 IF EQUAL
.
UICU5500    MOVE      AADMNO,PTICUADM     * Admission number
            MOVE      F6,PTICUINT         * Hrs in ICU
            MOVE      F2,PTICINTM         * Minutes of ICU
.
            CALL      CTBD0000            * Check total bed hrs
            MOVE      ZERO,PTICUMEC         * Hrs on Mechanical Ventilator
            MOVE      ZERO,PTICUCCU         * Hrs in Critical care
            MOVE      ZERO,PTICCPAP         * Hrs on CPAP
            MOVE      ZERO,PTICHNCU         * Hrs in NCU
            MOVE      SP70,PTICMECM         * Minutes of Mechanical Ventilation
            MOVE      SP70,PTICAMBN         * Ambulance client number
            MOVE      PTICUADM,KEY8
            MOVE      SP70,PTICCHSC
            CALL      WRPTICU1
          ENDIF
.
UICU9999  RETURN
+
.---------------------------------------------------------------------
.         Check total bed in minutes
.---------------------------------------------------------------------
CTBD0000  MOVE      F6,FORM6A           * total bed in hours
          MULT      SIXTY,FORM6A        * total bed in minutes
          ADD       F2,FORM6A           * add minutes
          CALL      CTBH0000
          IF        FORM6A>TOTBEDMN
            MOVE      TOTBEDMN,FORM6A   * Total bed in minutes
            CALL      RTIM0000
            MOVE      F6,PTICUINT       * Total bed hrs
            MOVE      F2,PTICINTM       * Total bed minutes
          ENDIF
CTBD9999  RETURN
+
.---------------------------------------------------------------------
.    Update CCU time
.---------------------------------------------------------------------
UCCU0000  COMPARE   ZERO,FORM6
          GOTO      UCCU9999 IF EQUAL
.
          MOVE      AADMNO,KEY8
          CALL      RDPTICU1
          IF        OVRCD=0
            ADD       FORM6,PTICUCCU        * Add to hrs in CCU (FORM 4)
            CALL      UPPTICU1
          ELSE
            MOVE      AADMNO,PTICUADM       * Admission number
            MOVE      ZERO,PTICUINT         * Hrs in ICU
            MOVE      ZERO,PTICUMEC         * Hrs on Mechanical Ventilator
            MOVE      FORM6,PTICUCCU        * Hrs in Critical care (FORM 4)
            MOVE      ZERO,PTICCPAP         * Hrs on CPAP
            MOVE      ZERO,PTICHNCU         * Hrs in NCU
            MOVE      SP70,PTICINTM         * Minutes of ICU
            MOVE      SP70,PTICMECM         * Minutes of Mechanical Ventilation
            MOVE      SP70,PTICAMBN         * Ambulance client number
            MOVE      PTICUADM,KEY8
            MOVE      SP70,PTICCHSC
            CALL      WRPTICU1
          ENDIF
.
UCCU9999  RETURN
+
.---------------------------------------------------------------------
.    Update NCU time
.---------------------------------------------------------------------
UNCU0000  COMPARE   ZERO,FORM6
          GOTO      UNCU9999 IF EQUAL
.
          MOVE      AADMNO,KEY8
          CALL      RDPTICU1
          IF        OVRCD=0
            ADD       FORM6,PTICHNCU        * Add to hrs in NCU (FORM 4)
            CALL      UPPTICU1
          ELSE
            MOVE      AADMNO,PTICUADM       * Admission number
            MOVE      ZERO,PTICUINT         * Hrs in ICU
            MOVE      ZERO,PTICUMEC         * Hrs on Mechanical Ventilator
            MOVE      ZERO,PTICUCCU         * Hrs in Critical care
            MOVE      ZERO,PTICCPAP         * Hrs on CPAP
            MOVE      FORM6,PTICHNCU        * Hrs in NCU (FORM 4)
            MOVE      SP70,PTICINTM         * Minutes of ICU
            MOVE      SP70,PTICMECM         * Minutes of Mechanical Ventilation
            MOVE      SP70,PTICAMBN         * Ambulance client number
            MOVE      PTICUADM,KEY8
            MOVE      SP70,PTICCHSC
            CALL      WRPTICU1
          ENDIF
.
UNCU9999  RETURN
+
.------------------------------------------------------------
.         Clear ICU variables for recalculation
.------------------------------------------------------------
CLRICU00  MOVE      AADMNO,KEY8
          CALL      RDPTICU1
          IF        OVRCD=0
            MOVE      ZERO,PTICUINT        * Hrs in ICU
            MOVE      SP70,PTICINTM        * Minutes in ICU
            MOVE      ZERO,PTICUCCU        * Hrs in Critical care
            MOVE      ZERO,PTICHNCU        * Add to hrs in NCU
            CALL      UPPTICU1
          ENDIF
.
CLRICU99  RETURN
+
.******************************************************************************
.*                                  CTBH0000              Called by: DIDV0000 *
.*                          Calculate Total Bed Hours                         *
.******************************************************************************
CTBH0000  MOVE      ZERO,TOTBEDHR           * Clear total bed hours
          MOVE      ZERO,TOTBEDMN           * Clear total bed minutes
          MOVE      ZERO,FORM6
.
          CALL      IBACLOCK
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MATCH     SP8,DDATE
          IF        !@EQUAL
            MOVE      DDATE,KEY8
          ENDIF
          MATCH     ADATE,KEY8
          GOTO      CTBH1000 IF EQUAL       * Sameday patient, only calc hours
.
          MOVE      ZERO,NBRDAYS
          MOVE      ADATE,FROMDATE          * From date
          MOVE      KEY8,TODATE             * To date
          CALL      RTIODAYS                * Calculate total bed days
          COMPARE   NBRDAYS,ZERO
          GOTO      CTBH9000 IF NOT LESS    * 0 >= number of days
.
          ASSIGN    NBRDAYS*1440,FORM6      * Total minutes for days
.
CTBH1000  UNPACK    ATIME,CHOUR,ANS,CMIN
          MOVE      CHOUR,IHOUR
          MOVE      CMIN,IMIN
          ASSIGN    (IHOUR*60)+IMIN,FORM5
.
          MATCH     SP8,DDATE
          IF        @EQUAL
            CLOCK     TIME,CTIMEIS
            UNPACK    CTIMEIS,CHOUR,DIM1,CMIN,DIM2
          ELSE
            UNPACK    DTIME,CHOUR,DIM1,CMIN,DIM2
          ENDIF
          MOVE      CHOUR,IHOUR
          MOVE      CMIN,IMIN
          ASSIGN    ((IHOUR*60)+IMIN)-FORM5,FORM5
          MOVE      FORM5,TOTBEDMN           * Total bed minutes
          ADD       FORM6,TOTBEDMN
.
          ASSIGN    (FORM5+FORM6)/60,FORM6P2 * Total bed hours
          ASSIGN    (FORM6P2*SIXTY),FORM6
          IF        FORM6%60<>0
            ADD       ONE,FORM6P2            * Round up mins to 1 hr
          ENDIF
          MOVE      FORM6P2,TOTBEDHR        * Total bed hours
.
CTBH9000  MOVE      ZERO,EXIT
CTBH9999  RETURN
.******************************************************************************
          INC       PATICUIO/INC
          INC       IBAOVRIO/INC
          INC       PATTRNIO/INC
.
ECOD9999  ENDPROC
