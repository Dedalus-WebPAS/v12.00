.------------------------------------------------------------
.  File          :  WEBSKIPR.PBL
.
.  Function      :  Update Keyword Index File for Web User Id Table
.
.  Parameters    :  WBSEUID - Key to Web User Id Table
.
.------------------------------------------------------------
. Modifications : 
. V10.14.01 04/02/2019 Thanh T.      TSK 0862937
.           Added login name(WBSELOGN) to key word search table webskiaf
. V10.03.01 03/02/2012 Ebon Clements CAR 236654     
.           Add trap to write 
. V10.01.00 29/10/2010 Ebon Clements CAR 233151     
.           Created program
.------------------------------------------------------------
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Procedure to Update
.  a Key Word Index Table that can be used for searching 
.  Masterfiles. The procedure should be called from the 
.  Add, Update and Delete functions in the Masterfile 
.  Maintenance Function.
.  
.  A Key Word Index Table is Defined in the following Manner
.                              
.  PTDKxxxx    DIM       xx    * Code Field Segment 1
.  PTDKyyyy    DIM       xx    * Code Field Segment n
.  PTDKKWD     DIM       15    * Key Word
.  PTDKSPA     DIM       10    * Spare
.
.  Index 1   PTDKxxxx, PTDKyyyy, PTDKKWD
.  Index 2   PTDKKWD, PTDKxxxx, PTDKyyyy
.
.  To convert this Procedure the following should be performed
.    Global Change PAT to the System ID eg :%s/PAT/PAT/g
.    Global Change CKI to the File   ID eg :%s/CKI/CKI/g
.    Global Change PTDK to the IO Call ID eg :%s/PTDK/PTDK/g
.    Modify the KEYINDEX defintion to combined Code Field Lengths eg 6
.    Global Change KEY40 to the Key for the Key Word Table eg KEY40
.    Change xxxxxxx to the Code Fields eg DCODEhh
.    Change the GENR0000 routine to call the BWORD000 for each
.    string of key words to be indexed for the Coded Field
.    eg MOVE    DSNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    DGNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    TDESC,KEYWORDS
.       CALL    BWORD000
.------------------------------------------------------------
          DEFPROC   WEBSKIUP
.
          INC       WEBSKIFD/INC
.
KEYINDEX  DIM       10        * Code Index in Table
KEYWORDS  DIM       80        * String Containing the Key Words to be Indexed
EIGHTY    FORM      "80"
KEYWRDNO  FORM      2
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
.------------------------------------------------------------
. Generate Word Index
.------------------------------------------------------------
GENR0000  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     WEBSKIA1,"webskiaf"
          TRAPCLR  IO
          BRANCH   OVRCD,GENR9999
.
          MOVE     WBSEUID,KEYINDEX
          CALL     CLEAR000           * Remove Current Key Word Index
          BRANCH   EXIT,GENR9999
.
          MOVE     WBSEUID,KEY10      * Validate Record on File
          CALL     RDWBSE1
          BRANCH   OVRCD,GENR1900     * If Not then it Must be a Delete/Clear
.
          MOVE     WBSEUID,KEYINDEX
.
          MOVE     WBSEUID,KEYWORDS
          CALL     BWORD000
.
          MOVE     WBSENAM,KEYWORDS
          CALL     BWORD000
.
          MOVE     WBSELOGN,KEYWORDS
          CALL     BWORD000
.
GENR1900  CLOSE    WEBSKIA1
.
GENR9999  GOTO     WEBSKIEP
.------------------------------------------------------------
. Clear Current Key Words
.------------------------------------------------------------
CLEAR000  PACK     KEY90,KEYINDEX,SP70,SP70
          CALL     RSWBSKI1
          CALL     RKWBSKI1
          BRANCH   OVRCD,CLEAR999
.
          MATCH    KEYINDEX,WBSKUSID
          GOTO     CLEAR999 IF NOT EQUAL
.
          PACK     KEY90,WBSKUSID,WBSKKWRD,SP70
          CALL     DEWBSKI1
          GOTO     CLEAR000
.
CLEAR999  RETURN
.-------------------------------------------------
. Determine Words to Index
.-------------------------------------------------
BWORD000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      BWORD000 IF NOT EOS
            GOTO      BWORD999               * entire name if blank
          ENDIF
          PACK      KEY80,KEYWORDS,SP70      * reset form pointer
          MOVE      KEY80,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      BWORD100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Save this Word
.
BWORD100  MOVE      KEYWORDS,KEY80
          PACK      KEY80,KEY80,SP70
          REP       UPPLOW,KEY80             * Always Upper Case
          PACK      KEY90,KEYINDEX,KEY80
          UNPACK    KEY90,WBSKUSID,WBSKKWRD
          CALL      RDWBSKI1
          IF        OVRCD=1
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            CALL      WRWBSKI1
            TRAPCLR   IO
          ENDIF
          GOTO      BWORD190
.
.         Check for any more words
.
BWORD190  SETLPTR   KEYWORDS,EIGHTY        * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY80,KEYWORDS,SP70    * reset form pointer
          MOVE      KEY80,KEYWORDS
          GOTO      BWORD000
.
BWORD999  RETURN
.
          INC       WEBSKIIO/INC
          INC       IBAOVRIO/INC
.
WEBSKIEP  ENDPROC                          * End of Procedure
.
