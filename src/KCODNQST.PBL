.******************************************************************************
.*  Include Name  : KCODNQST.PBL                                              *
.*  Description   : Keyin of a codes file entry in V5.01                      *
.*  ------>> NOTE : This differs from PATCODKY in that it uses a              *
.*                  parameter to supress "?"                                  *
.*  Author        : ROD                                                       *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CODE     - category of code                               *
.*                  CKYIDEF3 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*       ------->   SUPRSQST - supress "?"  0=No  1=Yes    <--------          *
.*                                                                            *
.*  Returns       : ACODE    - Keyed in code                                  *
.*                  TDESC    - Description                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Mods            28/09/1994  Rob Leonard  SRF 118380 (Bul 29)              *
.*                              Check if Code Inactive                        *
.******************************************************************************
KCODNQST  
KCDNQ010  MATCH     SP3,CKYIDEF3                  * using a default ?
          GOTO      KCDNQ050 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN3:      * no default used
                    *PECOL:EVERT,*V2LON,ACODE:
                    *PECOL:EVERT,*DV,ACODE
          GOTO      KCDNQ080
.
KCDNQ050  MOVE      CKYIDEF3,ACODE                 * default
          KEYIN     *PECOL:EVERT,*DV,ACODE:     * default used
                    *PECOL:EVERT,*V2LON,*RV,ACODE:
                    *PECOL:EVERT,*DV,ACODE
.
.         check what was entered
.
KCDNQ080  CALL      CDCNQ000
          BRANCH    EXIT,KCDNQ100,KCDNQ705,KCDNQ805,KCDNQ905
.                        "?"      valid    nothing  exitchar
          GOTO      KCDNQ010
.
.         a "?" was input
.
KCDNQ100  IF        SUPRSQST=1
            BEEP                                  * supress "?"
            GOTO      KCDNQ010
          ENDIF
.
          MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
KCDNQ105  CALL      PATCODDS
          BRANCH    CNEWDS,KCDNQ150
.
          CALL      PUTSCR00                * redisplay screen
          GOTO      KCDNQ010                * rekey code
.
.         now ask for code while leaving displayed codes on screen
.
KCDNQ150  MATCH     SP3,CKYIDEF3                  * using a default
          GOTO      KCDNQ200 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Code    : ":
                    *P11:24,*DV,UNDLN3:      * no default used
                    *P11:24,*V2LON,ACODE:
                    *P11:24,*DV,ACODE
          GOTO      KCDNQ300
.
KCDNQ200  MOVE      CKYIDEF3,ACODE
          KEYIN     *P1:24,*EL,*DV,"Code    : ":
                    *P11:24,*DV,ACODE:     * default used
                    *P11:24,*V2LON,*RV,ACODE:
                    *P11:24,*DV,ACODE
.
.         repeat the validation of the code
.         check what was entered
.
KCDNQ300  CALL      CDCNQ000
          BRANCH    EXIT,KCDNQ105,KCDNQ700,KCDNQ800,KCDNQ900
.                        "?"      valid    nothing  exitchar
.
          GOTO      KCDNQ150
.
.         a valid code was entered 
. 
KCDNQ700  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE
.
KCDNQ705  MOVE      FALSE,EXIT                    * valid input
          GOTO      KCDNQ999 
.
.         nothing was input
.
KCDNQ800  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,ACODE
.
KCDNQ805  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      KCDNQ999 
.
.         EXITCHAR input
.
KCDNQ900  CALL      FRESCR00                * free the screen saved
.
KCDNQ905  MOVE      TWO,EXIT                * EXITCHAR
.
KCDNQ999  RETURN
+
.*********************************************************************
.*                  CDCNQ000                    Called by : PATCODKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
CDCNQ000  ENDSET    ACODE
          APPEND    SP3,ACODE
          RESET     ACODE
.
          MATCH     EXITCHAR,ACODE
          GOTO      CDCNQ600 IF EQUAL       * exit char
.
          MATCH     SP3,ACODE
          GOTO      CDCNQ700 IF EQUAL       * nothing
.
          MATCH     QUEST,ACODE
          GOTO      CDCNQ800 IF EQUAL       * ? mark
.
.         a code was entered so validate
. 
CDCNQ500  PACK      KEY5,CODE,ACODE
          CALL      RDCODE1
          BRANCH    OVRCD,CDCNQ900            * invalid code
.
          MATCH     PTCOACTV,ANSI              * inactive ?
          GOTO      CDCNQ550 IF NOT EQUAL
.
          MOVE      ONE,HLEF                  * Inactive code
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*EL,*B,"Inactive code.  ";
          CALL      HITENTER
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      CDCNQ999
.
CDCNQ550  MOVE      TWO,EXIT                * valid code
          GOTO      CDCNQ999
.
.         exitchar entered
.
CDCNQ600  MOVE      FOUR,EXIT
          MOVE      SP3,ACODE
          MOVE      SP20,TDESC              * clear desc field
          GOTO      CDCNQ999
.
.         nothing entered
.
CDCNQ700  BRANCH    CKYIMAND,CDCNQ950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP3,ACODE
          MOVE      SP20,TDESC              * clear desc field
          GOTO      CDCNQ999
.
.         "?" entered
.
CDCNQ800  MOVE      ONE,EXIT
          GOTO      CDCNQ999
.
.         invalid code entered
.
CDCNQ900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*EL,*B,"Invalid code.  ";
          CALL      HITENTER
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      CDCNQ999
.
.         field is mandatory
.
CDCNQ950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is mandatory.  ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
CDCNQ999  RETURN
+
