.
. *****************************************************************************
. *                                                                           *
. * Include Name :  IBAMNCOM.PBL                                              *
. *                                                                           *
. * Function     :  Subroutine Include for                                    *
. *                 Keyin Option on Menu's. Uses Time-Outs                    *
. *                                                                           *
. * Subroutines  :  MENUKEY  : Get Main Menu Option                           *
. *                                                                           *
. * Files needed :  IBACOMM.INC    IBA Special Variables                      *
. *                 PATCOMM.INC    Special Variables                          *
. *                 STDIO001       Standard IBA library                       *
. *                                                                           *
. * Modifications:  19/01/90 Steve O'Gorman                                   *
. *                 Include Written                                           *
. *                 27/04/90 Graeme Williams                                  *
. *                 Fixed bug with security lvl of less than 9, after a retn  *
. *                 from a sub-menu.                                          *
. *                 16/03/1992    Justin Coates                               *
. *                 Added check for alaises if "*" entered                    *
. *                 08/09/1995    Howellsy     SRF 611674                     *
. *                 Kick user out if timeout and direct login                 *
. *                 22.11.1996    B.G.Ackland  Quote 8792                     *
. *                 No *RV on CMENUOPT so " 0" Does not Display in TBL7       *
. *****************************************************************************
.
. *****************************************************************************
. * MENUKEY    : Get Main Menu Option ( Keyed into a DIM 2 )                  *
. *                                                                           *
. * Files Open : controlf                                                     *
. *                                                                           *
. * Parameters : CTIMEOUT   nnn = Length of Timeout in menu option            *
. *                           0 = No Timeout                                  *
. *              CCOL       Column to start keyin of the menu option          *
. *              CVERT      Line to keyin the menu option on                  *
. *                         current century (CCC from common)                 *
. *                                                                           *
. *  Returns   : EXIT       0 = A Value Entered                               *
. *                         1 = No Value Entered                              *
. *                                                                           *
. *              CMENUOPT   Menu Option Number if EXIT = 0 in a FORM 2        *
. *              MENUOPT    Menu Option Keyed in a DIM 2                      *
. *                                                                           *
. *                                                                           *
. *****************************************************************************
.
MENUKEY   READ      CONTROLF,ZERO;*11,CTIMEOUT
.
          CLEAR     MENUOPT                      * Clear the Keyin Variables
          MOVE      ZERO,CMENUOPT
.
          DISPLAY   *PCCOL:CVERT,UNDLN2;
.
          COMPARE   NINE,SECLEV                  * Check if Security Level = 9
          GOTO      TMOUT500 IF NOT EQUAL
.
          COMPARE   ZERO,CTIMEOUT                * Check if Time-out Being Used
          GOTO      TMOUT250 IF EQUAL
TMOUT0000
.
. -----   Keyin Menu Option with Security Level = 9 and Time-Out Set  -----
. -----   i.e Must allow for Special Chars e.g. 's' , 'v'  etc.       -----
.
          KEYIN     *PCCOL:CVERT,*V2LON,*RV,*T60,MENUOPT;
.
          GOTO      TMOUT100 IF NOT LESS         * Check for a Time-Out
.
          SUB       ONE,CTIMEOUT
.
          COMPARE   CTIMEOUT,ZERO
          GOTO      TMOUT000 IF LESS
.
          CALL      CLOG0000                     * check for direct login
          IF        EXIT=1
            DISPLAY   *ES
            SHUTDOWN  " "
          ELSE
            CHAIN     "ANSWER"
          ENDIF
TMOUT100
          RESET     MENUOPT                      * Check for a <CR>
          GOTO      TMOUT950 IF EOS
.
          CMATCH    ASK,MENUOPT
          CALL      CHAIN000 IF EQUAL
.
          GOTO      TMOUT990
TMOUT250
.
. -----   Keyin Menu Option with Security Level = 9 with no Time-Out Set  -----
. -----   i.e Must allow for Special Chars e.g. 's' , 'v'  etc.           -----
.
          KEYIN     *PCCOL:CVERT,*V2LON,*RV,MENUOPT;
.
          RESET     MENUOPT                      * Check for a <CR>
          GOTO      TMOUT950 IF EOS
.
          CMATCH    ASK,MENUOPT
          CALL      CHAIN000 IF EQUAL
.
          GOTO      TMOUT990
TMOUT500
          COMPARE   ZERO,CTIMEOUT                * Check if Time-out Being Used
          GOTO      TMOUT750 IF EQUAL
.
. -----   Keyin the Menu Option With Security Level < 9 and Using the  -----
. -----   Time-out Feature. Allow for a '*' but no Special Characters  -----
.
TMOUT550
          KEYIN     *PCCOL:CVERT,*V2LON,*T60,CMENUOPT;
.
          GOTO      TMOUT600 IF NOT LESS         * Check for a Time-Out
.
          SUB       ONE,CTIMEOUT
.
          COMPARE   CTIMEOUT,ZERO
          GOTO      TMOUT550 IF LESS
.
          CALL      CLOG0000                     * check for direct login
          IF        EXIT=1
            DISPLAY   *ES
            SHUTDOWN  " "
          ELSE
            CHAIN     "ANSWER"
          ENDIF
TMOUT600
          MOVE      CMENUOPT,MENUOPT
          GOTO      TMOUT990
TMOUT750
.
. -----   Keyin the Menu Option With Security Level < 9 without Using the  -----
. -----   Time-out Feature. Allow for a '*' but no Special Characters      -----
.
          KEYIN     *PCCOL:CVERT,*V2LON,CMENUOPT;
.
          MOVE      CMENUOPT,MENUOPT
          GOTO      TMOUT990
TMOUT950
          MOVE      ZERO,MENUOPT            * A <CR> Hit so Set MENUOPT to 
          MOVE      MENUOPT,CMENUOPT        * Zero
          MOVE      FALSE,EXIT
          GOTO      TMOUT999
TMOUT990
          MOVE      ZERO,CMENUOPT           * Zero FORM field and then move
          MOVE      MENUOPT,CMENUOPT        * the DIM field to the FORM
.
          MOVE      FALSE,EXIT
          TYPE      MENUOPT
          GOTO      TMOUT999 IF EQUAL
          MOVE      TRUE,EXIT
TMOUT999
          RETURN
.
.********************************************************************** 
.*                            CHAIN000                                *
.*                  Chain to another program using "*"                *
.*        check for alaises                                           *
.********************************************************************** 
.
CHAIN000  KEYIN     *P1:3,*EF,*P1:12,"* Enter Program Name...":
                    *P23:12,*DV,UNDLN8,*V2LON,*P23:12,PROGRAM;
          PACK      KEY16,PROGRAM,SP8
          CALL      RDSSECU2
          CALL      RDKSECU2
          BRANCH    OVRCD,CHAIN100
.
          MATCH     PROGRAM,PSEALIAS
          GOTO      CHAIN100 IF NOT EQUAL
.
          MOVE      PSECNME,PROGRAM
.
CHAIN100  MOVE      PROGRAM,PROGNAME
.
          NORETURN                               * Return from Call to CHAIN000
          NORETURN                               * Return from Call to MENUKEY
.
          PACK      PROGNAME,PROGNAME,SP8
          MATCH     SP8,PROGNAME
          IF        @EQUAL
            CHAIN     PGM                        * Chain back to menu if empty
          ENDIF
.
          GOTO      CHECKPW
.
.#####    RETURN                                 * Return Not Used Because
.                                                * CHECKPW is an Old Spastic 
.                                                * Routine which is full of
.                                                * Non-Standard GOTO's
.
CHAIN999 RETURN
.********************************************************************** 
.*                            CLOG0000                                *
.*                      Check for a direct login id                   *
.*                                                                    *
.*   Returns Exit     : 0 = More than 1 user with same unix login id  *
.*                      1 = Only 1 user with this unix login id       *
.*                      2 = No one on file with this unix login id    *
.*                                                                    *
.********************************************************************** 
.
CLOG0000  OPEN      IHAPASS1,"ihapassf"
          OPEN      IHAPASS3,"ihapassf"
.
...          MATCH     SP4,PASSCODE
...          GOTO      CLOG9999 IF NOT EQUAL   
.
          MOVE      ZERO,EXIT
.
          CLOCK     USER,KEY11              * get users unix login id
          PACK      KEY11,KEY11,SP20        * pad out with blanks
.
          MOVE      ZERO,FORM1
.
          PACK      KEY24,KEY11,SP30
          CALL      RDSPASS3
CLOG1000  CALL      RDKPASS3
          BRANCH    OVRCD,CLOG5000
.
          MATCH     KEY11,SECNUMB         * Test Unix login id
          GOTO      CLOG5000 IF NOT EQUAL
.
          ADD       ONE,FORM1               * Increment number
.
          COMPARE   FORM1,ONE               * check if > 1 CLOG for id
          GOTO      CLOG6000 IF LESS
.
          MOVE      SECODE,KEY4          * Save the code for first CLOG
          GOTO      CLOG1000
.
CLOG5000  COMPARE   ZERO,FORM1              * Unix id not on file
          GOTO      CLOG9500 IF EQUAL
.
CLOG6000  BRANCH    FORM1,CLOG9000          * Only one CLOG with id
          GOTO      CLOG9999

CLOG9000  MOVE      ONE,EXIT            * Only one CLOG with id
          MOVE      KEY4,PASSCODE        * set up passcode
          PACK      KEY4,PASSCODE
          CALL      RDPASS1                 * Read in the correct record
          MOVE      SECDEPT,CDEPCDE         * Pass CLOG Dept Thru Dept
          GOTO      CLOG9999
.
CLOG9500  MOVE      TWO,EXIT            * no user on file with id
          GOTO      CLOG9999
.
CLOG9999  RETURN

