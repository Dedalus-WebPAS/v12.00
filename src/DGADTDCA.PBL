.**************************************************************************
.*  DGADTDCA  :  Cancel an Discharge from a Datagate request              *
.*               ~~~~~~~~~~~~~~~~~~~                                      *
.*                                                                        *
.*  Author    :  Mike Laming  (19/04/2002)                                *
.*                                                                        *
.*  Mods      :                                                           *
.*  V12.00.01 16/05/2025  Bella Turco       TSK 0955096                   *
.*            Alphanumeric changes                                        *
.*  V11.00.01 01/04/2020  J.Tan             TSK 0868813                   *
.*            Changed PKNAME to DIM80                                     *
.*  V10.06.01 : 12/05/2015  Steve Armstrong  CAR 314108                   *
.*              Replaced PTCGDATE with KEY8 as PTCGDATE is no longer used *
.**************************************************************************
.*   V9.02.01 : 22/04/2002  Mike Laming                                   *
.*              Replace PMEDI with Composite variable PMEDIXX (which      *
.*              contains PMEDI+PTMXMCCD) Add call (SPLTPMED)to split these*
.*                                                                        *
.*                                                                        *
.**************************************************************************
          DEFPROC   DGADTDCA
 IFGE     $$VER,7
.
+
.*************************************************************************
.*  DGDCA000  :  Process datagate Admission message                      *
.*************************************************************************
DGDCA000  MOVE      "DCA",RPLYHEAD
          CALL      ADOPE000                      * open some files
          CALL      CLEAR000                      * clear some fields
.
          MOVE      ZERO,OVRCD
          READ      DGATEOUT;*SB,PURNO;
.
          CALL      VALID000                      * read pat. details & validate
          BRANCH    EXIT,DGDCA900                 * Error!  Patient not valid
.
.
. *SB will suppress blank fill, *SP will turn off suppress blank fill
.
          MOVE      ZERO,OVRCD
          READ      DGATEOUT;*SB,CSALIAS,KEY8,PTITL,PHOSP,PLDAYS:
                            PBDEBT,PSNAME,PGNAME,PPSNAME,PADD1:
                            PADD2,PSUBRB,PADD4,PPOST,PTELEP:
                            PTELEB,PSEX,PMSTAT,PBDATE,PCONT:
                            PREG,PTYPE,POCCUP,PLDDATE,PMEDIXX:
                            PPENNO,PPNDTE,PREPAT,PSMOK,PMICRO:
                            PCEASE,PCASE,PAUTOPY,PHIGH1,PHIGH2:
                            PHIGH3,PLOCDOC,PFUNDH,PFNDTB,PFNDMM:
                            PUSR1,PUSR2,PUSR3,PUSR4,PUSR5:
                            PUSR6,PUYN1,PUYN2,PUYN3,PYRRES:
                            PNKNAME,PNKADD1,PNKADD2,PNKSUBR,PNKADD4:
                            PNKPOST,PNKTELP,PNKTELB,PNKRELP,PFNDYY:
                            PFNDCG,PDECDTE,PDIET,PINITHOS,PHKADUNI:
                            PTMAEMPC,PTMANOKO,PTMANOKE,PTMADCNO,PTMXRDAT:
                            PTMXMEAL,PTMXLGA,PTMXLS,PTMXLSDT,PTMXFHMD:
                            PTMXMHU1,PTMXMHU2,PTMXMHU3,PTMXECNM,PTMXECA1:
                            PTMXECA2,PTMXECA3,PTMXECA4,PTMXECPC,PTMXECPP:
                            PTMXECBP,PTMXECRE,PTMXNRNM,PTMXNRA1,PTMXNRA2:
                            PTMXNRA3,PTMXNRA4,PTMXNRPC,PTMXNRPP,PTMXNRBP:
                            PTMXNRRE,PTMX3BEX,PTMXCARD,PTMXCEXP,PTMXCXMP:
                            PTMXFMLY,PTMXCHNM,PTMXDOFR,PMPXRHC1,PMPXRH1G:
                            PTMXSPID,PMPXR1GC,PTMXOSDT,PTNINMPI:
                            NHMASURN,NHMAGIV1,NHMAGIV2,NHMAGIV3,NHMAPREI:
                            NHMAADD1,NHMAADD2,NHMAADD3,NHMAADD4,NHMAADD5:
                            NHMADOB,NHMADDTH,NHMADOMC,NHMARES,NHMAETH:
                            NHMASEX,NHMADAT,NHMATIM,NHMAETH2,NHMAETH3:
                            PTMXOPER,PTMXUKDO,PTMXCELL,PTMXADD1,PTMXADD2:
                            PTMXADD3,PTMXADD4,PTMXPOST:
                            AADMNO,ADATE,ATIME,AINVPRT,ALACDTE:
                            ACANCEL,ADOCTR,ADOCTL,ASRCE,ACLSS:
                            ACARE,AFUNDH,AFNDTB,AFNDMM,AELIG:
                            AVISIT,AALERG,AILLN,ADIAG1,ADIAG2:
                            ADISC,ADOCTT,AALLOW,AMBS,ACLAIM:
                            ADIET,APLOCCR,ASTAY,ADYHOSP,AMEMB:
                            AMEMBNO,ACOMM1,ACOMM2,APLUR,APURGE:
                            AUSR1,AUSR2,AUSR3,AUSR4,AUSR5:
                            AUSR6,AUSR7,ADSCHI,ARDRNAM,AFNDYY:
                            AFNDCG,AOCCDTE,ACODDTE,ARETDTE,AMUMADM:
                            PTMIPVIS,ABWGHT,PMVXRHC1,PMVXRH1G,PTMSFHAN:
                            PTMSECRA,PTMSMGIN,PTMIDDIG,PMVXRH1C,PTMIDOFR:
                            PTMIREFD,PTMIDFCN,PTMIPHPU,PTMIAGNC,PTMIESSF:
                            PTMIUSR8,PTMIUSR9,PTMIUSR0,PTMIPDRG,PTMIOPER:
                            PTMIXWRD,PTMIADOC,PTMIREGS,PTMIUYN1,PTMIUYN2:
                            DDATE,DTIME,DSTAT,DDEST,DDIAG:
                            DDIAG2,DUSD1,DUSD2,DUSD3,DUSD4:
                            DUSD5,DFMBS,PTDSDMDC,PTDSDDRG,DWLREASN:
                            PTDSSIDX,PTDSVOGU,PTDSOPER,PTDSUYN1,PTDSUYN2:
                            PTDSUYN3,PTDSUYN4:
                            TWARD,TBED,TRATEF,TRATEG,TDISC:
                            TALLW,TATYPE,TADOCT,TRATEH,TEXTRA:
                            TRBTYP,TRBEND,TDEPT,PTTRLTYP,PTTREPNO:
                            DSNAME,DGNAME,DATETIME
.
          CALL      SPLTPMED                * Split PMEDIXX into PMEDI, PTMXMCCD
.
          MOVE      AADMNO,ADMISSNO         * get ready for call to CLDSC000
          CALL      CLDSC000
          BRANCH    EXIT,DGDCA900           * errors detected
.
.
. send reply message as successful
.
          MOVE      "00000",RPLYCODE              * successful message
          MOVE      SP50,RPLYDESC                 * clear error description
          PACK      CCENCODE,CCDATE,CCDST,CCSRC,CCNAME,REPLYRPL
          WRITE     DGATEIN;CCENCODE:
                            RPLYHEAD,RPLYCODE,RPLYDESC:
                            DGMISC1,DGMISC2,DGMISC3
.
          ADD       ONE,SUCCPROC                  * increm # success transactns
          GOTO      DGDCA999
.
. --- send back an Error response ---
.
DGDCA900  MOVE      WEBTITLE,RPLYDESC
.                                                 * RPLYCODE set up in PATCHADT
          PACK      CCENCODE,CCDATE,CCDST,CCSRC,CCNAME,REPLYRPL
          WRITE     DGATEIN;CCENCODE:
                            RPLYHEAD,RPLYCODE,RPLYDESC:
                            DGMISC1,DGMISC2,DGMISC3
          ADD       ONE,ERRRPROC                  * increm # error transactions
.
DGDCA999  CALL      CLOS0000
          GOTO      ENDPROC1
+
.*************************************************************************
.*                      Internal routines                                *
.*************************************************************************
.
.***************************************************************************
.*  VALID000  :  read patients details & make sure we have a valid patient *
.*               Returns:  EXIT=0 --> Valid patient,   EXIT=1 --> ERROR    *
.***************************************************************************
VALID000  MOVE      ZERO,OVRCD
.
. get master file details
.
          MOVE      PURNO,KEY8
          CALL      RDMAST1                       * U/R on file ?
          IF        OVRCD = 1
            GOTO    VALID800
          ENDIF
          CALL      RDPMPX21
.
.**       CALL      AMSV1000                      * save old address vars
.
VALID700  MOVE      ZERO,EXIT                     * we have a valid patient
          GOTO      VALID999
.
.
VALID800  MOVE      "Patient Master details not found",WEBTITLE
          MOVE      "02106",RPLYCODE
          GOTO      VALID990
.
.
VALID990  MOVE      ONE,EXIT
.
VALID999  RETURN
.
.***************************************************************************
.*  ADOPE000  :  open a few files                                          *
.***************************************************************************
ADOPE000  MOVE      ZERO,OVRCD
.
          OPEN      MEHCJAA1,"mehcjaaf"
          OPEN      MEHDS1A1,"mehds1af"
          OPEN      MEHLEGA1,"mehlegaf"
          OPEN      MEHVI1A1,"mehvi1af"
.
.
          OPEN      PATAXEA1,"pataxeaf"       * 
          OPEN      PATDADA1,"patdadaf"       * Patient Transfer Dest.
          OPEN      PATIPEN1,"patipenf"       * Invoices Pending
          OPEN      PATLOCA1,"patlocaf"       * Location Details 
          OPEN      PATNOBE1,"patnobef"       * Ward with no Beds
          OPEN      PATONLV1,"patonlvf"       * 
          OPEN      PATRATE1,"patratef"       * Rates Table
          OPEN      PATUNAA1,"patunaaf"       * Adm & Dsch Date Change Audit
          OPEN      PATWR1A4,"patwr1af"       * Ward/Bed Details
          OPEN      WATTR1A1,"wattr1af"       * W/L Patient Procedures
.
          IF        CAUDA<>1                                           
            OPEN      PATAUAA1,"pataa1af"     * Yes - Audit required
          ENDIF
.
.
ADOPE9999 RETURN
.
.***************************************************************************
.*  CLEAR000  :  clear fields                                              *
.***************************************************************************
CLEAR000  CALL      CLPATMAS                      * clear master file vars
          CALL      CLPMSPX2                      * Clear Master Extn.2 
          CALL      CLPATMIS                      * clear admission vars
          MOVE      ZEROVISN,AADMNO
.
. clear PRA details
.
          PACK      PKNAME,SP70,SP10
          MOVE      SP50,PKADD1
          MOVE      SP50,PKADD2
          MOVE      SP50,PKSUBR
          MOVE      SP50,PKADD4
          MOVE      SP50,PKPOST
          MOVE      SP50,PKTELEP
          MOVE      SP50,PKTELEB
          MOVE      SP50,PKRELP
          MOVE      ZEROVISN,PKADMN
.
CLEAR999  RETURN
.
.***************************************************************************
.*  CLOS0000  :  close files                                               *
.***************************************************************************
CLOS0000  MOVE      ZERO,OVRCD
.
          CLOSE     MEHCJAA1
          CLOSE     MEHDS1A1
          CLOSE     MEHLEGA1
          CLOSE     MEHVI1A1
          CLOSE     PATAXEA1
          CLOSE     PATDADA1
          CLOSE     PATIPEN1
          CLOSE     PATLOCA1
          CLOSE     PATNOBE1
          CLOSE     PATONLV1
          CLOSE     PATRATE1
          CLOSE     PATUNAA1
          CLOSE     PATWR1A4
          CLOSE     WATTR1A1
          CLOSE     PATAUAA1

.
CLOS99999 RETURN
.
.***************************************************************************
.*                            End of DGADTACA ModulE                       *
.***************************************************************************
.
ENDPROC1  ENDPROC
+
