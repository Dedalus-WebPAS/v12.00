.  V11.03.01 02/06/2023  Peter Vela     TSK 0927262
.            ClinicalAide FHIR api
.            Added Subject include FHRSUBIN functionality
.            Added Participant include FHRPARIN functionality
.            Added Location include FHRLOCIN functionality
.  V11.00.01 03/03/2020  J.Tan          TSK 0838262
.            Added Effective from and to date to MBS Item file
.------------------------------------------------------------
. Add Resource to Bundle
.------------------------------------------------------------
FHRAPP00  IF        DISNUMB>0
            WRITE     HTMLFILE;*+,",";
          ENDIF
          MOVE      CASEKEYS,FHRAPPID
          REP       " -",FHRAPPID
          ADD       ONE,DISNUMB
          WRITE     HTMLFILE;*+," {":
                                " #"fullUrl#": #"%BASEURL/Appointment/THE-",FHRAPPID,"#",":
                                  " #"resource#": ";
          CALL      RESAPP00
          WRITE     HTMLFILE;*+,"}";
.
          IF        FHRSUBIN=1
.
            PACK      FHRPATID,URNUMBER
            SQUEEZE   FHRPATID
.
            WRITE     HTMLFILE;*+,",{":
                                  " #"fullUrl#": #"%BASEURL/Patient/",FHRPATID,"#",":
                                    " #"resource#": ";
.
            CALL      RESPAT00
.
            WRITE     HTMLFILE;*+,"}"
          ENDIF
.
          IF        FHRPARIN=1
.
            PACK      SURGNAME,SURGNAME,SP70
            MATCH     SP70,SURGNAME
            IF        !@EQUAL
              STRIP     SURGNAME
.
              MATCH     SP70,OPDACLIN
              GOTO      FHRAPP50 IF EQUAL
              GOTO      FHRAPP50 IF EOS
.
              PACK      KEY6,OPDACLIN,SP70
              CALL      RDDOCT1
              IF        OVRCD=0
.
                WRITE     HTMLFILE;*+,",{":
                                      " #"fullUrl#": #"%BASEURL/Practitioner/HCP-",OPDACLIN,"#",":
                                        " #"resource#": ";
.
                CALL      RESPRA00
.
                WRITE     HTMLFILE;*+,"}"
.
              ELSE
.
                PACK      KEY6,OPDACLIN,SP70
                CALL      RDOPCLI1
                BRANCH    OVRCD,FHRAPP50
.
                PACK      KEY6,OPCLDOCT,SP70
                CALL      RDDOCT1
                IF        OVRCD=0
.
                  WRITE     HTMLFILE;*+,",{":
                                        " #"fullUrl#": #"%BASEURL/Practitioner/HCP-",OPDACLIN,"#",":
                                          " #"resource#": ";
.
                  CALL      RESPRA00
.
                  WRITE     HTMLFILE;*+,"}"
.
                ENDIF
.
              ENDIF
.
            ENDIF
.
FHRAPP50    PACK      ANAENAME,ANAENAME,SP70
            MATCH     SP70,ANAENAME
            IF        !@EQUAL
              STRIP     ANAENAME
.
              MATCH     SP70,OPSEANAE
              GOTO      FHRAPP60 IF EQUAL
              GOTO      FHRAPP60 IF EOS
.
              PACK      KEY6,OPSEANAE,SP7
              CALL      RDDOCT1
              IF        OVRCD=0
.
                WRITE     HTMLFILE;*+,",{":
                                      " #"fullUrl#": #"%BASEURL/Practitioner/HCP-",OPSEANAE,"#",":
                                        " #"resource#": ";
.
                CALL      RESPRA00
.
                WRITE     HTMLFILE;*+,"}"
.
              ENDIF
.
            ENDIF
.
          ENDIF
.
FHRAPP60  IF        FHRLOCIN=1
.
            MATCH     SP70,OPDATHET
            IF        !@EQUAL
              PACK      KEY6,OPDATHET,SP70
              CALL      RDOPOPR1
              IF        OVRCD=0
.
                PACK      FHRLOCC,OPDATHET
                SQUEEZE   FHRLOCC
.
                WRITE     HTMLFILE;*+,",{":
                                      " #"fullUrl#": #"%BASEURL/Location/Theatre-",FHRLOCC,"#",":
                                        " #"resource#": ";
.
                CALL      RESLOC00
.
                WRITE     HTMLFILE;*+,"}"
.
              ENDIF
.
            ENDIF
.
          ENDIF
.

FHRAPP99  RETURN
.------------------------------------------------------------
. Appointment FHIR Resource Output for Theatre Bookings
.------------------------------------------------------------
RESAPP00  MOVE      URNUMBER,FHRPATID
          SQUEEZE   FHRPATID
          UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          STRIP     DIM30
          MOVE      "OA",CATEGORY           * Anae Type
          MOVE      OPDAANAE,CODE
          CALL      GETCOD00
          MOVE      TDESC,OADESC
          MATCH     SP70,CODE
          IF        @EQUAL
            MOVE      "n/a",OADESC
          ENDIF
.
          MOVE      "ST",CATEGORY           * Type
          MOVE      OPDAUNIT,CODE
          CALL      GETCOD00
          MOVE      TDESC,STDESC
          MATCH     SP70,CODE
          IF        @EQUAL
            MOVE      "n/a",STDESC
          ENDIF
.
          CALL      GETAN000
.
          PACK      KEY6,OPDATHET,SP70
          CALL      RDOPOPR1
          IF        OVRCD=1
            MOVE      "Not Allocated",OPRMDESC
          ENDIF
          REP       "\ ",OPDADES1
          REP       "\ ",OPDADES2
          REP       "\ ",OPDADES3
.
          WRITE     HTMLFILE;*+,"{ #"resourceType#": #"Appointment#",":
                    " #"id#" : #"THE-",FHRAPPID,"#",":
                    " #"extension#": [":
                    " { #"url#":#"%BASEURL/extension/webPASService#"":
                        " ,#"valueString#":#"",PRGID,SP1,PRGNAM,SP1,VERSION,"#"},":
                    " { #"url#":#"%BASEURL/extension/webPASTemplate#"":
                        " ,#"valueString#":#"%TEMPLATEHOME/",WBTPFIL," %TEMPLATEVERSION#"},":
                    " { #"url#":#"%BASEURL/extension/dxc-hc-anaesthetictype#"":
                        " ,#"valueString#":#"",OADESC,"#"},":
                    " { #"url#":#"%BASEURL/extension/dxc-hc-procedurestatus#"":
                        " ,#"valueString#":#"",DIM30,"#"}":
                    " ],":
                    " #"status#" : #"",FHRSTAT,"#",":
                    " #"minutesDuration#" : ",OPDAEXPD,",":
                    " #"reason#":  [ { ":
                    "  #"text#" : #"",OPDADES1,OPDADES2,OPDADES3,"#",  ":
                    "  #"coding#" : [";
          CALL      FHRCOD00
          WRITE     HTMLFILE;*+," ] } ],":
                    " #"appointmentType#":  {  ":
                    "  #"coding#" : [":
                    "   { #"code#"      : #"THEATRE#",":
                    "     #"display#"   : #"Theatre#" } ":
                    "  ] },":
                    " #"serviceType#": [ { ":
                    "  #"coding#" : [":
                    "   { #"system#"    : #"%BASEURL/ValueSet/Category-ST#",":
                    "     #"code#"      : #"",OPDAUNIT,"#",":
                    "     #"display#"   : #"",STDESC,"#" } ":
                    "  ] } ],":
                    " #"start#" : #"",CCENT,CYEAR,FHRDASH,CMON,FHRDASH,CDAY:
                    "T",OPDAEXPS,":00",TIMEZONE,"#",":
                    " #"participant#" : [ {":
                    "  #"type#":  [ {":
                    "  #"text#" : #"Patient#",":
                    "  #"coding#" : [":
                    "   { #"code#"      : #"PART#",":
                    "     #"display#"   : #"Participation#" } ":
                    "  ] } ],":
                    "  #"status#" : #"accepted#",":
                    "  #"actor#" : {":
                    "    #"display#" : #"",PATFNAME,"#", ":
                    "    #"reference#" : #"Patient/",FHRPATID,"#"}":
                    "   }   ";
.
          WRITE     HTMLFILE;*+,", {":
                    "  #"status#" : #"accepted#",":
                    "  #"actor#" : {":
                    "    #"reference#"   : #"Location/Theatre-",OPDATHET,"#", ":
                    "    #"display#"   : #"",OPRMDESC,"#"  ":
                    "   } } ";
.
          PACK      SURGNAME,SURGNAME,SP70
          MATCH     SP70,SURGNAME
          IF        !@EQUAL
            STRIP     SURGNAME
            WRITE     HTMLFILE;*+,", {":
                      "  #"type#":  [ {":
                      "  #"text#" : #"Surgeon#",":
                      "  #"coding#" : [":
                      "   { #"code#"      : #"PRPF#",":
                      "     #"display#"   : #"primary performer#" } ":
                      "  ] } ],":
                      "  #"status#" : #"accepted#",":
                      "  #"actor#" : {":
                      "     #"reference#" : #"Practitioner/HCP-",OPDACLIN,"#",":
                      "     #"display#" : #"",SURGNAME,"#" ":
                      "   } } ";
          ENDIF
.
          PACK      ANAENAME,ANAENAME,SP70
          MATCH     SP70,ANAENAME
          IF        !@EQUAL
            STRIP     ANAENAME
            WRITE     HTMLFILE;*+,", {":
                      "  #"type#":  [ {":
                      "  #"text#" : #"Anaesthetist#",":
                      "  #"coding#" : [":
                      "   { #"code#"      : #"SPRF#",":
                      "     #"display#"   : #"secondary performer#" } ":
                      "  ] } ],":
                      "  #"status#" : #"accepted#",":
                      "  #"actor#" : {":
                      "     #"reference#" : #"Practitioner/HCP-",OPSEANAE,"#",":
                      "     #"display#" : #"",ANAENAME,"#" ":
                      "   } } ";
          ENDIF
.
. Service Type Not Implemented yet? Category OY?
. Need to clarifiy requirements
. 
.          MOVE      "OY",CATEGORY           
.          MOVE      OPDATYPE,CODE
.          CALL      GETCOD00
.          MOVE      TDESC,OYDESC
.          MATCH     SP70,CODE
.          IF        @EQUAL
.            MOVE      "n/a",OYDESC
.          ENDIF
.
.                    " #"serviceCategory#":  {  ":
.                    "  #"coding#" : [":
.                    "   { #"system#"    : #"%BASEURL/ValueSet/Category-OY#",":
.                    "     #"code#"      : #"",OPDATYPE,"#",":
.                    "     #"display#"   : #"",OYDESC,"#" } ":
.                    "  ] },":
.
          WRITE     HTMLFILE;*+," ] }"
.
RESAPP99  RETURN
.
FHRCOD00  PACK      KEY9,OPDAPROV,SP70
          IF        OPCNCODE=1
            PACK      KEY17,KEY9,OPDADATE,SP70
            CALL      PATITMRD       * Read MBS code file
            IF        OVRCD=0
              WRITE     HTMLFILE;*+,"{ #"code#"      : #"",KEY9,"#",":
                        "     #"display#"   : #"",PTITDESC,"#" } ";
            ELSE
              WRITE     HTMLFILE;*+,"{ #"display#"   : #"",OPDADES1,"#" } ";   * Fall back to just the desc
            ENDIF
          ELSE
            CALL      RDPTICO1       * Read ICD operation file
            IF        OVRCD=0
              WRITE     HTMLFILE;*+,"{ #"code#"      : #"",KEY9,"#",":
                        "     #"display#"   : #"",ODESC,"#" } ";
            ELSE
              WRITE     HTMLFILE;*+,"{ #"display#"   : #"",OPDADES1,"#" } ";   * Fall back to just the desc
            ENDIF
          ENDIF
.
          PACK      KEY9,OPDAPRV2,SP70
          CALL      ADDFCD00
.
          PACK      KEY9,OPDAPRV3,SP70
          CALL      ADDFCD00

          PACK      KEY13,OPDAUNIQ,SP70
          CALL      RSOPMBS1
FHRCOD10  CALL      RKOPMBS1
          BRANCH    OVRCD,FHRCOD99
          MATCH     OPMBUNIQ,OPDAUNIQ
          GOTO      FHRCOD99 IF NOT EQUAL
.
          PACK      KEY9,OPMBMBSI,SP70
          CALL      ADDFCD00
          GOTO      FHRCOD10
.      
FHRCOD99  RETURN
.
ADDFCD00  MATCH     SP70,KEY9
          GOTO      ADDFCD99 IF EQUAL
          MOVE      SP70,IDESC
          IF        OPCNCODE=1
            PACK      KEY17,KEY9,OPDADATE,SP70
            CALL      PATITMRD       * Read MBS code file
            BRANCH    OVRCD,ADDFCD99
            WRITE     HTMLFILE;*+,",{ #"code#"      : #"",KEY9,"#",":
                      "     #"display#"   : #"",PTITDESC,"#" } ";
          ELSE
            CALL      RDPTICO1       * Read ICD operation file
            BRANCH    OVRCD,ADDFCD99
            WRITE     HTMLFILE;*+,",{ #"code#"      : #"",KEY9,"#",":
                      "     #"display#"   : #"",ODESC,"#" } ";
          ENDIF
ADDFCD99  RETURN
.
