.------------------------------------------------------------
. Visit Based Comment Processing
.  Requires Includes VISCMTDF.PBL
.                    VISCMTFD & IO 
.
. V11.02.01 05/09/2022 Ebon Clements  TSK 0922654
.           Added trap IO for write - UPVCM000
. V10.03.01 23/02/2012 Jill Parkinson CAR 260753
.           Fixed packing of VSCTUKEY
. V9.04.03 31/01/2005  Ebon Clements CAR 53466
.          Allow one blank line in visit comments
. V9.04.02 12/10/2004  Ebon Clements CAR 53466
.          Removed changes from V9.04.01
. V9.04.01 17/09/2004  Peter Vela   CAR 53466
.          Allow one blank line in between text filled lines SETVCM00
. V9.03.01 13/10/2003  Davin        CAR 39038
.          Use new date/time fields instead of spare variable
. V9.02.01 01/04/2003 Ebon Clements SRF 37754
.          Change SETVCM00 to not save blank comment lines
.------------------------------------------------------------
.  CLRVCM00 - Call from CLRPAR00 Routine to Initialise Comments Input File
.  SETVCM00 - Call from SETPAR00 Routine when input parameter matches "vicmt"
.  UPVCM000 - Call to Update Comments for Add or Update
.  VCMHTM00 - Call to Output Comments to HTML
.------------------------------------------------------------
. Clear Inputs
.------------------------------------------------------------
CLRVCM00  CLEAR     VSCTTFNM
          APPEND    "VISCMT",VSCTTFNM
          APPEND    PORT,VSCTTFNM
          RESET     VSCTTFNM
          REP       " 0",VSCTTFNM
          PREP      VSCTTFIL,VSCTTFNM
          RETURN
.------------------------------------------------------------
. Set Inputs
.------------------------------------------------------------
SETVCM00  MOVE      ZERO,BLNKFLAG
          UNPACK    QRYNAME,KEY5,KEY3
          WRITE     VSCTTFIL,SEQ;"%START:",KEY3
          PACK      QRYDATA,QRYDATA,SP70,SP70
          WRITE     VSCTTFIL,SEQ;QRYDATA
.
SETVCM10  READ      CONFFILE,SEQ;QRYNAME,QRYDATA * Read CONFFILE sequentially
          GOTO      SETVCM99 IF OVER             * If end of file
          MATCH     SP70,QRYNAME                 * If next CGI parameter
          GOTO      SETVCM80 IF NOT EQUAL
.
          PACK      QRYDATA,QRYDATA,SP70,SP70
.
          MATCH     SP70,QRYDATA
          IF        @EQUAL
            BRANCH    BLNKFLAG,SETVCM10
            MOVE      ONE,BLNKFLAG               * Blank line yes
            SQUEEZE   QRYDATA
            GOTO      SETVCM70                   * don't write second blank line
          ENDIF
          IF        @EOS
            BRANCH    BLNKFLAG,SETVCM10          * don't write second blank line
            MOVE      ONE,BLNKFLAG               * Blank line yes
            SQUEEZE   QRYDATA
            GOTO      SETVCM70
          ENDIF
.
          MOVE      ZERO,BLNKFLAG                * Blank line no
.
SETVCM70  WRITE     VSCTTFIL,SEQ;QRYDATA
          GOTO      SETVCM10
.
SETVCM80  MOVE      ONE,TEXTAREA                 * Flag for Next CGI Parameter
SETVCM99  RETURN
.------------------------------------------------------------
. Add/Update Comments
.------------------------------------------------------------
UPVCM000  OPEN      VSCTTFIL,VSCTTFNM
UPVCM100  READ      VSCTTFIL,SEQ;VSCTTLIN
          GOTO      UPVCM999 IF OVER
          MATCH     "%START:",VSCTTLIN
          IF        @EQUAL
            UNPACK    VSCTTLIN,KEY7,VSCTTTYP
            CALL      CLVCM000
            MOVE      ZERO,VSCTTCNT
            GOTO      UPVCM100
          ENDIF
UPVCM110  ADD       ONE,VSCTTCNT
          PACK      KEY14,ADMISSNO,VSCTTTYP,VSCTTCNT,SP70
          UNPACK    KEY14,VSCTVIST,VSCTTYPE,VSCTLINE
          CALL      RDVSCMT1
          COMPARE   ZERO,OVRCD
          GOTO      UPVCM110 IF EQUAL
.
          MOVE      VSCTTLIN,VSCTDATA
          UNPACK    UPDATEKY,KEY8,VSCTUKEY
          PACK      VSCTUKEY,VSCTUKEY,SP70
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRVSCMT1
          TRAPCLR   IO
          GOTO      UPVCM100
.
UPVCM999  RETURN
.------------------------------------------------------------
. Clear/Delete Comments of a Particular Type
.------------------------------------------------------------
CLVCM000  PACK      KEY14,ADMISSNO,VSCTTTYP,SP70
          CALL      RSVSCMT1
CLVCM100  CALL      RKVSCMT1
          BRANCH    OVRCD,CLVCM999
          MATCH     VSCTVIST,ADMISSNO
          GOTO      CLVCM999 IF NOT EQUAL
          MATCH     VSCTTTYP,VSCTTYPE
          GOTO      CLVCM999 IF NOT EQUAL
          PACK      KEY14,VSCTVIST,VSCTTYPE,VSCTLINE,SP70
          CALL      DEVSCMT1
          CALL      RSVSCMT1
          GOTO      CLVCM100
CLVCM999  RETURN
.------------------------------------------------------------
. Output Comments to HTMLFILE
.------------------------------------------------------------
VCMHTM00  UNPACK    DIM127,ANS,VSCTTTYP,DIM127
          PACK      KEY14,ADMISSNO,VSCTTTYP,SP70
          CALL      RSVSCMT1
VCMHTM10  CALL      RKVSCMT1
          BRANCH    OVRCD,VCMHTM99
          MATCH     ADMISSNO,VSCTVIST
          GOTO      VCMHTM99 IF NOT EQUAL
          MATCH     VSCTTTYP,VSCTTYPE
          GOTO      VCMHTM99 IF NOT EQUAL
          STRIP     VSCTDATA
          MOVELPTR  VSCTDATA,LENGTH
.
          COMPARE   LENGTH,ZERO 
          IF        @EQUAL
            MOVE      ONE,LENGTH
          ENDIF
.
          SFORMAT   VAR,LENGTH
          MOVE      VSCTDATA,VAR
          WRITE     HTMLFILE;VAR
          SFORMAT   VAR,127
          GOTO      VCMHTM10
.
VCMHTM99  RETURN
.
