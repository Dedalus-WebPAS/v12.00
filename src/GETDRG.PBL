.******************************************************************************
.* Include Name   : GETDRG.PBL                                                *
.* Description    : Gets a DRG & MDC code for a patient                       *
.* Author         : Greg Horvat  03/06/2000                                   *
.* Parameters Req : A valid admission record                                  *
.* Returns        : DDRGCODE - DRG code                                       *
.*                  DMDCCODE - MDC code                                       *
.*                  DRGVER   - DRG version                                    *
.* Modifications  :                                                           *
.*       V11.05.02  26/03/2025  Davin         TSK 0956210                     *
.*                  Added PTCNE120 for DRG Version 12.0                       *
.*       V11.05.01  13/03/2025  Alina Bhari   TSK 0955915                     *
.*                  Output '120' for AR DRG Version for Episodes with a       *
.*                  Discharge Date on or after the new 'Using GroupeR - DRG   *
.*                  Version 12.0' (PTCNE120) parameter -GTDRG000,GDRGD000     *
.*       V11.02.01  20/04/2022  Davin         TSK 0917793                     *
.*                  Added PTCNE110 for DRG Version 11.0                       *
.*       V10.14.01  27/02/2019  Richa Phogat  TSK 0870439                     *
.*                  Added PTCNE100 for DRG Version 10.0                       *
.*       V10.12.01  23/03/2018  Tracey Nguyen TSK 0852793                     *
.*                  Added PTCNED90 for DRG Version 9.0                        *
.*       V10.08.02  20/09/2016  Don Nguyen    TSK 0312570                     *
.*                  Modified GTDRG200 to call GSTDRG00 if applicable          *
.*       V10.08.01  26/08/2016  Don Nguyen    TSK 0312570                     *
.*                  Modified GETDRG (GTDRG000) to call GTEDRG00 (GETEFDRG.PBL)*
.*       V10.06.01  29/05/2015  Ebon Clements CAR 317094                      *
.*                  Added DRG Version 8.0                                     *
.*       V10.05.01  17/09/2014  Davin         CAR 304204                      *
.*                  Mods to get drg based on discharge date (gdrgd000)        *
.*        V9.08.01  09/11/2006  Davin         CAR 123200                      *
.*                  Allow 999 as grouper version if ptcndgpv=9.9              *
.*        V9.02.01  08/11/2001  Davin                                         *
.*                  Replaced PTCNUGRP with PTCNDGPV                           *
.*        V5.09.B01 09/02/2001  Greg Horvat   SRF 8390                        *
.*                  Changed to return the DRG version                         *
.*        V5.08.03  06/11/2000  Dean Cramer   SRF 6886                        *
.*                  Clear DRG & MDC code variables at start (stop previous    *
.*                  returned if none). Read of patdschaf (discharge file)     *
.*                  for DRG & MDC code to use as default (if any)             *
.*        V5.08.02  14/09/2000  Jill Habasque                                 *
.*                  Added PTCDGRPV & PTHFGRPV                                 *
.*        V5.08.01  04/09/2000  Davin                                         *
.*                  Changed to not use discharge vars for display             *
.******************************************************************************
GETDRG
GTDRG000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND05;*158,PTCNDGPV
          READ      CONTROLF,HUND19;*183,PTCNED41,*191,PTCNED42,*199,PTCNED50:
                                    *207,PTCNED51,*215,PTCNED52,*223,PTCNED60:
                                    *231,PTCNED62,*239,PTCNED70
          READ      CONTROLF,HUND20;*232,PTCNED80
          READ      CONTROLF,HUND23;*232,PTCNED90
          READ      CONTROLF,HUND24;*121,PTCNDGED,*224,PTCNE100
          READ      CONTROLF,HUND28;*239,PTCNE110
          READ      CONTROLF,HUND30;*94,PTCNE120
.
          MATCH     "1",PTCNDGED
          IF        @EQUAL
            OPEN      PMSEDGA1,"pmsedgaf"     * Effective DRG
            OPEN      PMSEDGA2,"pmsedgaf"     * Effective DRG
          ENDIF
.
          MATCH     "999",PTCNDGPV          * use drg entered by user
          GOTO      GTDRG200 IF EQUAL
.
          UNPACK    SP20,DDRGCODE,DMDCCODE,DRGVER
          OPEN      PATDSCH1,"patdschf"
          PACK      KEY8,AADMNO,SP8
          CALL      RDDSCH1                 * Read a discharge file record
          BRANCH    OVRCD,GTDRG900
.
          MOVE      PTDSDDRG,DDRGCODE
          MOVE      PTDSDMDC,DMDCCODE
          MOVE      PTDSVOGU,DRGVER
.
          MATCH     "1",PTCNDGED
          GOTO      GTDRG160 IF EQUAL
.
          MATCH     SP6,AFUNDH
          GOTO      GTDRG100 IF EQUAL       * Blank health fund
.
          OPEN      PATFN1A1,"patfn1af"
          PACK      KEY14,AFUNDH,ZERO,ZERO,ZERO,ZERO,SP20
          CALL      RDFUND1                 * Read a health fund file record
          BRANCH    OVRCD,GTDRG100
.
          MATCH     SP3,PTHFGRPV  
          GOTO      GTDRG100 IF EQUAL       * Blank DRG version no
.
          MOVE      PTHFGRPV,KEY3
          GOTO      GTDRG300
.
GTDRG100  OPEN      PATCODE1,"patcodes"
          PACK      KEY5,ANSC,ANSL,ACLAIM
          CALL      RDCODE1                 * Read a codes file record
          BRANCH    OVRCD,GTDRG150
.
          MATCH     SP3,PTCDGRPV  
          GOTO      GTDRG150 IF EQUAL       * Blank DRG version no
.
          MOVE      PTCDGRPV,KEY3
          GOTO      GTDRG300
.
GTDRG150  MATCH     SP8,DDATE
          GOTO      GTDRG200 IF EQUAL       * Patient not discharged
.
          CALL      GDRGD000                * get vers based on discharge date
          GOTO      GTDRG300
.
GTDRG160  CALL      GTEDRG00       * Get the Effective DRG Version (using DDATE)
          BRANCH    EXIT,GTDRG200      * Error; use System Default Grouper vers
          GOTO      GTDRG300
.
GTDRG200  MATCH     "1",PTCNDGED
          IF        @EQUAL
            CALL      GSTDRG00              * Get State Effective DRG version
          ELSE
            MOVE      PTCNDGPV,KEY3
          ENDIF
.
GTDRG300  OPEN      PATPGRA1,"patpgraf"
          PACK      KEY13,AADMNO,SP1,ONE,KEY3
          CALL      RDPTPGR1                * Read a patient grouper file record
          BRANCH    OVRCD,GTDRG900
.
          MOVE      PTPGNDRG,DDRGCODE
          MOVE      PTPGNMDC,DMDCCODE
          MOVE      PTPGGPVR,DRGVER
.
GTDRG900  MOVE      ZERO,EXIT
GTDRG999  RETURN
+
.******************************************************************************
.*                                  GDRGD000                                  *
.*                      Get drg based on discharge date                       *
.******************************************************************************
GDRGD000  MATCH     PTCNE120,DDATE          * disc.date => DRG 12.0 effect.date?
          IF        !@LESS
            MOVE      "120",KEY3            * Use 12.0 DRG version
            GOTO      GDRGD999
          ENDIF
.
          MATCH     PTCNE110,DDATE          * disc.date > DRG 11.0 effect.date?
          IF        !@LESS
            MOVE      "110",KEY3            * Use 11.0 DRG version
            GOTO      GDRGD999
          ENDIF
.
          MATCH     PTCNE100,DDATE          * disc.date > DRG 10.0 effect.date?
          IF        !@LESS
            MOVE      "100",KEY3            * Use 10.0 DRG version
            GOTO      GDRGD999
          ENDIF
.
          MATCH     PTCNED90,DDATE          * disc.date > DRG 9.0 effect.date?
          IF        !@LESS
            MOVE      "090",KEY3            * Use 9.0 DRG version
            GOTO      GDRGD999
          ENDIF
.
          MATCH     PTCNED80,DDATE          * disc.date > DRG 8.0 effect.date?
          IF        !@LESS
            MOVE      "080",KEY3            * Use 8.0 DRG version
            GOTO      GDRGD999
          ENDIF
.
          MATCH     PTCNED70,DDATE          * disc.date > DRG 7.0 effect.date?
          IF        !@LESS
            MOVE      "070",KEY3            * Use 7.0 DRG version
            GOTO      GDRGD999
          ENDIF
.
          MATCH     PTCNED62,DDATE          * disc.date > DRG 6.2 effect.date?
          IF        !@LESS
            MOVE      "062",KEY3            * Using 6.2 DRG version
            GOTO      GDRGD999
          ENDIF
.
          MATCH     PTCNED60,DDATE          * disc.date > DRG 6.0 effect.date?
          IF        !@LESS
            MOVE      "060",KEY3            * Using 6.0 DRG version
            GOTO      GDRGD999
          ENDIF
.
          MATCH     PTCNED52,DDATE          * disc.date > DRG 5.2 effect.date?
          IF        !@LESS
            MOVE      "052",KEY3            * Using 5.2 DRG version
            GOTO      GDRGD999
          ENDIF
.
          MATCH     PTCNED51,DDATE          * disc.date > DRG 5.1 effect.date?
          IF        !@LESS
            MOVE      "051",KEY3            * Using 5.1 DRG version
            GOTO      GDRGD999
          ENDIF
.
          MATCH     PTCNED50,DDATE          * disc.date > DRG 5.0 effect.date?
          IF        !@LESS
            MOVE      "050",KEY3            * Using 5.0 DRG version
            GOTO      GDRGD999
          ENDIF
.
          MATCH     PTCNED42,DDATE          * disc.date > DRG 4.2 effect.date?
          IF        !@LESS
            MOVE      "042",KEY3            * Using 4.2 DRG version
            GOTO      GDRGD999
          ENDIF
.
          MATCH     PTCNED41,DDATE          * disc.date > DRG 4.1 effect.date?
          IF        !@LESS
            MOVE      "041",KEY3            * Using 4.1 DRG version
            GOTO      GDRGD999
          ENDIF
.
          MOVE      PTCNDGPV,KEY3           * revert to default version
GDRGD999  RETURN
+
