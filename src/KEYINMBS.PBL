.******************************************************************************
.* Include Name   : KEYINMBS.PBL                                              *
.* Description    : Keyin of a MBS code                                       *
.* Author         : Dino                                                      *
.* Modifications  :                                                           *
.*        V11.00.01 11/03/2020  J.Tan          TSK 0838262                    *
.*                  Added Effective from and to date to MBS Item file         *
.*        V6.03.B00 30/12/1994  Greg Horvat      New Release 6.03             *
.*                  Changed to ask whether to continue when keying in the MBS *
.*                  item (Exclusion List Item)                                *
.*        V6.03.B01 31/01/1995  Greg Horvat      New Release 6.03             *
.*                  Changed to use PATITMVD to vaild the MBS code instead of  *
.*                  just reading the MBS file                                 *
.*        V6.03.B02 23/02/1995  Greg Horvat      New Release 6.03             *
.*                  Changed to use PATCKEXC to check if an exclusion list item*
.*        V5.03.000 21/07/1995  Steve O'Gorman                                *
.*                  Ported from Release 6.03                                  *
.*                  Removed Checks for active, link and exclusion as these    *
.*                  these features are not available in 5.03                  *
.*                                                                            *
.* Required       :                                                           *
.* ----------------                                                           *
.* Other Includes : PATITMVD - Valid an MBS code to make sure it is active    *
.*                  PATCKEXC - Check if an exclusion list item                *
.*                                                                            *
.* Parameters     : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF9 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                                                                            *
.* Returns        : AMBS     - Keyed in code                                  *
.*                  IDESC    - Description                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.* On return      : You must display the description yourself.                *
.*                                                                            *
.******************************************************************************
KEYINMBS  
KMBSC010  MATCH     SP9,CKYIDEF9                  * using a default ?
          GOTO      KMBSC050 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN9:      * no default used
                    *PECOL:EVERT,*V2LON,AMBS:
                    *PECOL:EVERT,*DV,AMBS
          GOTO      KMBSC080
.
KMBSC050  MOVE      CKYIDEF9,AMBS
          KEYIN     *PECOL:EVERT,*DV,CKYIDEF9:     * default used
                    *PECOL:EVERT,*V2LON,*RV,AMBS:
                    *PECOL:EVERT,*DV,AMBS
.
.         check what was entered
.
KMBSC080  CALL      MBSCK000
          BRANCH    EXIT,KMBSC100,KMBSC705,KMBSC805,KMBSC905,KMBSC010
.                         "?"      valid    nothing exitchar keyin again
          GOTO      KMBSC010
.
.         a "?" was input
.
KMBSC100  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
KMBSC105  MOVE      ONE,CNEWDS
          UNPACK    AMBS,ANS,DIM1A
          CALL      PATDSITM
.
.         now ask for code while leaving displayed codes on screen
.
KMBSC150  MATCH     SP9,CKYIDEF9                  * using a default
          GOTO      KMBSC200 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Code    : ":
                    *P11:24,*DV,UNDLN9:      * no default used
                    *P11:24,*V2LON,AMBS:
                    *P11:24,*DV,AMBS
          GOTO      KMBSC300
.
KMBSC200  MOVE      CKYIDEF9,AMBS
          KEYIN     *P1:24,*EL,*DV,"Code    : ":
                    *P11:24,*DV,CKYIDEF9:     * default used
                    *P11:24,*V2LON,*RV,AMBS:
                    *P11:24,*DV,AMBS
.
.         repeat the validation of the code
.         check what was entered
.
KMBSC300  CALL      MBSCK000
          BRANCH    EXIT,KMBSC105,KMBSC700,KMBSC800,KMBSC900,KMBSC010
.                         "?"      valid    nothing exitchar keyin again
.
          GOTO      KMBSC150
.
.         a valid code was entered 
. 
KMBSC700  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,AMBS
.
KMBSC705  MOVE      FALSE,EXIT                    * valid input
          GOTO      KMBSC999 
.
.         nothing was input
.
KMBSC800  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,AMBS
.
KMBSC805  MOVE      SP20,IDESC                    * blank out description
          MOVE      TRUE,EXIT                     * Nothing or Spaces entered
          GOTO      KMBSC999 
.
.         EXITCHAR input
.
KMBSC900  CALL      FRESCR00                      * free the screen saved
.
KMBSC905  MOVE      TWO,EXIT                      * EXITCHAR
          GOTO      KMBSC999
.
KMBSC999  RETURN
+
.***************************************************************************
.*                              MBSCK000             Called by: KEYINMBS   *
.*                        check what was entered                           *
.*          Returns:  EXIT  = 0   invalid                                  *
.*                          = 1   "?"                                      *
.*                          = 2   valid                                    *
.*                          = 3   nothing                                  *
.*                          = 4   exitchar                                 *
.*                          = 5   keyin again                              *
.***************************************************************************
MBSCK000  ENDSET    AMBS
          APPEND    SP9,AMBS
          RESET     AMBS
.
          MATCH     EXITCHAR,AMBS
          GOTO      MBSCK500 IF EQUAL
.
          MATCH     SP9,AMBS
          GOTO      MBSCK600 IF EQUAL
.
. check for a "?"
.
          MATCH     QUEST,AMBS
          GOTO      MBSCK700 IF EQUAL
.
. a code was entered so validate
. 
MBSCK100  PACK      KEY17,AMBS,SP70
          CALL      PATITMRD
          BRANCH    OVRCD,MBSCK800
.
          DISPLAY   *PECOL:EVERT,*V2LON,AMBS
.
          MOVE      TWO,EXIT                * Item Ok
          GOTO      MBSCK999
.
. exitchar entered
.
MBSCK500  MOVE      FOUR,EXIT
          GOTO      MBSCK999
.
. nothing entered
.
MBSCK600  BRANCH    CKYIMAND,MBSCK950             * mandatory
          MOVE      THREE,EXIT
          MOVE      SP20,IDESC                    * clear desc field
          GOTO      MBSCK999
.
. "?" entered
.
MBSCK700  MOVE      ONE,EXIT
          GOTO      MBSCK999
.
. invalid code entered
.
MBSCK800  DISPLAY   *P1:24,*EL,*B,"Invalid code.  ";
          CALL      HITENTER
          MOVE      ZERO,EXIT
          GOTO      MBSCK999
.
. keyin again
.
MBSCK900  MOVE      FIVE,EXIT
          GOTO      MBSCK999
.
MBSCK950  BEEP
          MOVE      ZERO,EXIT
.
MBSCK999  RETURN
.
