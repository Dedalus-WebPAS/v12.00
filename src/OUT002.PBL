.
.******************************************************************************
.
.         OUT002.PBL - External subroutines used by OUTPRG66
.
.         Mods.     : 
.
.******************************************************************************
. V12.00.01 19/05/2025 Thanh T         TSK 0955096
.           Changed for alphanumeric visit number
.******************************************************************************
.         V10.14.02 09/10/2019  J.Tan           TSK 0857392 
.                   Mod Checking for Tier2 Cat NC Ind1=A for ABF funding
.                   Mod to initialise ABFFLAG
.         V10.14.01 28/06/2019  J.Tan           TSK 0857392 
.                   Mod for ABF invoice
.         V9.03.05  07/04/2003  J.Tan    
.                   Mods to remove FD and IO for pmsmtiaf
.         V9.03.04  23/10/2003  J.Tan    CAR 44172         
.                   Mods for Misc.theatre item file(pmsmitaf)
.         V9.02.03  18/10/2001  J.Tan                              
.                   Mods to use clinic indicator from outbb1 file
.         V9.02.02  11/10/2001  J.Tan                              
.                   Fixed key for patnipaf
.         V9.02.01  06/10/2001  J.Tan SRF 12214 (copied from 5.09)
.                   Changed to move 0 to exit when exiting the routines
.
.                     V5.07.000  15/10/1998  Jim Stathopoulos
.                                507 Changes
.******************************************************************************
.                     V5.01.002  31/01/1992  Graeme Williams
.                                New Zealand billing changes
.                     V5.01.003  05/11/1992  Graeme Williams
.                                Initialise OTSERVS when setting up misc items
.
.******************************************************************************
.
.         XATT0000 - Additional processing required
.
.------------------------------------------------------------------------------
.
XATT0000  MOVE      ZERO,EXIT                * Unset exit flag
XATT9999  RETURN
.******************************************************************************
.
.         XBOK0000 - Additional processing required
.
.------------------------------------------------------------------------------
.
XBOK0000  MOVE      ZERO,EXIT                * Unset exit flag
XBOK9999  RETURN
.
+
.******************************************************************************
.
.         XINI0000 - Open all relevant data files
.
.------------------------------------------------------------------------------
.
XINI0000  MOVE      ZERO,EXIT                * Unset exit flag
XINI9999  RETURN
.
+
.******************************************************************************
.
.         XDEL0000 - Delete additional attendance details posted via XATT0000
.         Returns : EXIT condition if anything fails
.
XDEL0000  MOVE      ZERO,EXIT                * Unset exit flag
XDEL9999  RETURN
.
+
.******************************************************************************
.
.         XCAN0000 - Delete additional booking details posted via XBOK0000
.         Returns : EXIT condition if anything fails
.
XCAN0000  MOVE      ZERO,EXIT                * Unset exit flag
XCAN9999  RETURN
.
+
.******************************************************************************
.
.         XCHG0000 - Get charge for this patient if applicable
.
.------------------------------------------------------------------------------
.
XCHG0000  CALL      ABFO0000                 * Check for ABF invoice
          BRANCH    EXIT,XCHG2000            * Charge ABF - exit
.
          BRANCH    OTCNFTYP,XCHG1000
.
.         Charging using the PATNIPFD file
.
          PROC      NIPCH000
          BRANCH    EXIT,XCHG2000
.
. ------- Charge applicable -------
.
          MOVE      ZERO,XCHGFLAG            * Charge is applicable
          GOTO      XCHG9000
.
.         Charging using the OUTBCHFD file
.
XCHG1000  PROC      BRDCH000
          BRANCH    EXIT,XCHG2000
.
. ------- Charge applicable -------
.
          MOVE      ZERO,XCHGFLAG            * Charge is applicable
          GOTO      XCHG9999
.
. ------- No charge -------
.
XCHG2000  MOVE      ONE,XCHGFLAG             * Charge is not applicable
          MOVE      ZERO,PMMIAMTT            * This is important for PCHG0000
          MOVE      ZERO,PMMIAMTP
          MOVE      ZERO,PMMIRBAT
.
XCHG9000  MOVE      ZERO,EXIT
XCHG9999  RETURN
.
. *****************************************************************************
. * Check for ABF Invoice                                                     *
. *****************************************************************************
ABFO0000  MOVE      ZERO,ABFFLAG
          READ      CONTROLF,HUND28;*105,PTCNUABF
          MATCH     "1",PTCNUABF
          GOTO      ABFO9000 IF NOT EQUAL
.
          PACK      KEY5,ANSC,ANSL,OBACOMP
          CALL      RDCODE1
          BRANCH    OVRCD,ABFO9000
          MATCH     "A",TCINDC2          * ABF Funding ?
          GOTO      ABFO9000 IF NOT EQUAL
.
          PACK      KEY5,ANSN,ANSC,OTBBTCOD
          CALL      RDCODE1
          BRANCH    OVRCD,ABFO9000
          MATCH     "A",TCINDC1
          GOTO      ABFO9000 IF NOT EQUAL
.
.         Check if ABF Invoice has been raised
.
          OPEN      PATINVR3,"patinvrf"
          PACK      KEY16,OBAOUTNO,SP70
          CALL      RSPTINV3
ABFO1000  CALL      RKPTINV3
          BRANCH    OVRCD,ABFO2000
.
          MATCH     PINVADM,OBAOUTNO
          GOTO      ABFO2000 IF NOT EQUAL
.
          REP       " 0",PTINCRST
          MATCH     "0",PTINCRST
          GOTO      ABFO1000 IF NOT EQUAL  * Credit note
.
          COMPARE   THREE,PTINCMIX
          GOTO      ABFO3000 IF EQUAL      * found an ABF Invoice
.
ABFO2000  PROC      ABFOTINV               * Outpatient ABF
.
ABFO3000  MOVE      ONE,EXIT
          GOTO      ABFO9999
.
ABFO9000  MOVE      ZERO,EXIT
ABFO9999  RETURN
+
. *****************************************************************************
. * NIPCH000 : Find charge when charging using the PATNIPFD file              *
. *****************************************************************************
.
          DEFPROC   NIPCH000
.
          INC       PATNIPFD/INC             * outpatient charging
.
.         Calculate the rate for this outpatient
.
NIPCH000  MATCH     SP3,OTBBCIND
          IF        !@EQUAL
            PACK      KEY13,TWO,OTBBCIND,PTYPE,OBACOMP,OBAVISIT
          ELSE
            PACK      KEY13,TWO,OMACIND,PTYPE,OBACOMP,OBAVISIT
          ENDIF
          OPEN      PATNIPB1,"patnipbf"
          CALL      RDNIPB1
          CLOSE     PATNIPB1
          BRANCH    OVRCD,NIPCH900
.
. ------- Charge applicable - setup pmsmtiaf variables -------
.
          MOVE      OBAOUTNO,PMMIVISN        * Outpatient Number
          MOVE      OBAURNO,PMMIURNO
          MOVE      " 2",PMMISYST
          MOVE      SP30,PMMIITEM
          MOVE      NBDESC,PMMIDESC
          MOVE      SP30,PMMIDES2
          MOVE      OBADATE,PMMITDAT
          MOVE      "3",PMMIRTYP
          MOVE      SP70,PMMIREFN
.
          MOVE      NBAMNT,PMMIAMTT
          MOVE      NBAMNT,PMMIAMTP
          MOVE      ZERO,PMMIRBAT
          MOVE      ZERO,PMMIAMTG
          MOVE      ZERO,PMMIAMTH
          UNPACK    SP70,PMMITDOC,PMMIODOC,PMMISESS
          MOVE      NBITEM,PMMIMGRP
.
          UNPACK    SP30,PMMIBTCH
          MOVE      ONE,PMMIINVN
          MOVE      ZERO,PMMISERV
          MOVE      SP70,PMMIREFN
          MOVE      ZERO,PMMIGSTA
          MOVE      SP10,PMMIGSTC
          MOVE      ZERO,PMMIGSTM
          MATCH     "OUTWEB",PRGID
          IF        @EQUAL
            MOVE      WBSEUID,PMMIWUSR
          ELSE
            MOVE      PASSCODE,PMMIWUSR
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      NIPCH999
.
.         No charge found
.
NIPCH900  MOVE      ONE,EXIT
.
NIPCH999  GOTO      ENDNIPCH
.
          INC       PATNIPIO/INC
          INC       IBAOVRIO/INC
.
ENDNIPCH  ENDPROC
.
. *****************************************************************************
. * BRDCH000 : Find charge when charging using the OUTBCHFD file              *
. *****************************************************************************
.
          DEFPROC   BRDCH000
.
          INC       OUTBCHFD/INC             * outpatient charging
          INC       PATCALAG/INC             * data variables for CALCAGE
.
BJDAY     FORM      3
CJDAY     FORM      3
ENDAGE    FORM      3
.
.         Calculate the rate for this outpatient
.
BRDCH000  PACK      AGEDATE,OBADATE          * date for age
          CALL      CALCAGE                  * calculate age
.
          OPEN      OUTBCHA1,"outbchaf"
          PACK      KEY18,OBACOMP,OTBBFUND,OBACLASS,PSAGEYRS,SP20
          CALL      RSOTBCH1
          CALL      RKOTBCH1
          BRANCH    OVRCD,BRDCH900           * no record -> no rate
.
          MATCH     OTBCCLM,OBACOMP          * correct code ?
          GOTO      BRDCH900 IF NOT EQUAL    * no -> no rate
.
          MATCH     OTBCBRD,OTBBFUND         * correct code ?
          GOTO      BRDCH900 IF NOT EQUAL    * no -> no rate
.
          MATCH     OTBCDEPT,OBACLASS        * correct code ?
          GOTO      BRDCH900 IF NOT EQUAL    * no -> no rate
.
          MOVE      OTBCAGEG,ENDAGE          * save the ending age in age group
.
.         Get the rate for the number of visits
.
          PACK      KEY18,OBACOMP,OTBBFUND,OBACLASS,ENDAGE,OTBBPVIS
          CALL      RSOTBCH1
          CALL      RKOTBCH1
          BRANCH    OVRCD,BRDCH900           * no record -> no rate
.
          MATCH     OTBCCLM,OBACOMP          * correct code ?
          GOTO      BRDCH900 IF NOT EQUAL    * no -> no rate
.
          MATCH     OTBCBRD,OTBBFUND         * correct code ?
          GOTO      BRDCH900 IF NOT EQUAL    * no -> no rate
.
          MATCH     OTBCDEPT,OBACLASS        * correct code ?
          GOTO      BRDCH900 IF NOT EQUAL    * no -> no rate
.
          COMPARE   OTBCAGEG,ENDAGE          * correct age group ?
          GOTO      BRDCH900 IF NOT EQUAL    * no -> no rate
.
          IF        (OTBCBPOR+OTBCPPOR) = 0.00
            GOTO      BRDCH900               * no charge
          ENDIF
.
.         We have the rate. Now setup the pmsmtiaf record
.
          MOVE      OBAOUTNO,PMMIVISN        * Outpatient Number
          MOVE      OBAURNO,PMMIURNO
          MOVE      " 2",PMMISYST
          MOVE      SP30,PMMIITEM
          MOVE      OTBCDESC,PMMIDESC
          MOVE      SP70,PMMIDES2
          MOVE      OBADATE,PMMITDAT
          MOVE      "3",PMMIRTYP
          MOVE      SP70,PMMIREFN
.
          ASSIGN    (OTBCPPOR+OTBCBPOR),PMMIAMTT
          MOVE      OTBCPPOR,PMMIAMTP
          MOVE      OTBCBPOR,PMMIRBAT
          MOVE      ZERO,PMMIAMTG
          MOVE      ZERO,PMMIAMTH
.
          UNPACK    SP70,PMMITDOC,PMMISESS,PMMIODOC,PMMIBTCH
          MOVE      OTBCITEM,PMMIMGRP
          UNPACK    SP30,OTDEPTYP,OTBATCHN
          MOVE      OTBCNINV,PMMIINVN
          MOVE      ZERO,PMMISERV
          MOVE      ZERO,PMMIGSTA
          MOVE      SP10,PMMIGSTC
          MOVE      ZERO,PMMIGSTM
          MATCH     "OUTWEB",PRGID
          IF        @EQUAL
            MOVE      WBSEUID,PMMIWUSR
          ELSE
            MOVE      PASSCODE,PMMIWUSR
          ENDIF
.
          MOVE      ZERO,EXIT                * we have a charge
          GOTO      BRDCH990
.
.         No rate on file
.
BRDCH900  MOVE      ONE,EXIT                 * charge
.
BRDCH990  CLOSE     OUTBCHA1
.
BRDCH999  GOTO      ENDBRDCH
.
          INC       CALCAGE
          INC       OUTBCHIO/INC
          INC       IBAOVRIO/INC
.
ENDBRDCH  ENDPROC
+
          INC       ABFOTINV
