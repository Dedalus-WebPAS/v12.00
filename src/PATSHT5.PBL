.
.         VERSION  
.*        V5.08.01  06/06/2000  Jill Habasque                                 
.*                  Changed from key10 to key14 for the health fund file      
.         V5.07.01  05/01/2000  J.Tan 
.                   Mods to have zero filled dates
.                   08/10/1998  Glenn Berry      5.07 changes
.******************************************************************************
.                  V4.01 23/12/88 J.Tan
.                        Mods to print Adm. users defined field 3
.                  V4.02 11/01/89 J.Tan
.                        Removed two lines at the end of each form
.                  V4.03 13/01/89 J.Tan
.                        Added one line after each form
.                  V4.04 06/02/89 J.Tan
.                        Alter left hand columns
.         PATSHT5
.         -------
.
.         THIS INCLUDE PRINTS THE PATIENT SUMMARY (IDENTIFICATION) DETAILS
.
.         Layout 5 - Melbourne Clinic
.
.        IDENTIFICATION VARIABLES
.
. DIM22    DIM      22
. ADOCDESC DIM      22
. ACDESC   DIM      20
. CLDESC   DIM      20
. CARECLS  DIM      20
.
. A8DATE   DIM      8
. D8DATE   DIM      8
. XDAY     DIM      2
. XMON     DIM      2
. XYEAR    DIM      2
. MARITAL  DIM      10
. RELIGN   DIM      20
. WDIM3    DIM      3
. MDIM3    DIM      3
. HDIM4    DIM      4
. XAGE     DIM      3
.
.        LINEUP PRINT
.
LINUPID5
         CALL      OPNPRINT
         PRINT     *N,*66,X5,X1,*N,*N:                            (LINE 1)
                   *13,X4,*26,X5,X3:
                   *40,X5,X3,*66,X5,X1:                      (LINE 2)
                   *N,*N,*N:
                   *13,X20,X2:                      (LINE 3)
                   *N,*N,*N,*N:
                   *13,X20:
                   *33,X20,X5:                  (LINE 4)
                   *N,*N,*N,*N,*N:
                   *13,X20,X5:
                   *56,X10,X2,*N:                            (LINE 5)
                   *13,X20,X5:                   (LINE 6)
                   *N,*13,X15,SP1,X4,*56,X10,X2:  (LINE 7)
                   *N,*N,*N:
                   *56,X10:                                 (LINE 8)
                   *N,*13,X20:                     (LINE 9)
                   *N,*13,X20,X5:
                   *56,X10,X2:                               (LINE10)
                   *N,*13,X20,X5:                (LINE11)
                   *N,*13,X15,SP1,X4,*56,X10,X2:  (LINE12)
                   *N,*N,*N,*56,X10:                        (LINE13)
                   *N,*13,X20:                     (LINE14)
                   *N,*13,X20,X5:
                   *56,X10,X2:                               (LINE15)
                   *N,*13,X20,X5:                (LINE16)
                   *N,*13,X15,SP1,X4,*56,X10,X2:  (LINE17)
                   *N,*N,*N,*N:
                   *16,X5,X3,*26,X3,*34,X1,*47,X10:  (LINE18)
                   *N,*N,*N,*N:
                   *16,X15,X2,*34,X3:                (LINE19)
                   *N,*N,*N,*N:
                   *16,X20:
                   *47,X20:                       (LINE20)
                   *N,*N,*N,*N:
                   *2,X20:                        (LINE21)
                   *N,*2,X30:
                   *42,X15,X1:
                   *59,X10,*N,*N:                           (LINE22)
                   *69,X3:                                        (LINE23)
                   *N,*N,*N,*N,*N,*N,*N:
                   *N,*N,*N,*N,*N,*N,*N,*N
.
        CALL      CLSPRINT
        RETURN
.
.       Setup All variables for print
.
SETLID5  CALL      LABELAGE                 * format age
. 
         UNPACK    ADATE,XCENT,XYEAR,XMON,XDAY
         PACK      A8DATE WITH XDAY,SLASH,XMON,SLASH,XYEAR
         REP       " 0",A8DATE
.
          UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY
         PACK      D8DATE WITH XDAY,SLASH,XMON,SLASH,XYEAR
         REP       " 0",D8DATE
.
.        Attending Doctor Info
.
         MOVE      ADOCTA,KEY6
         CALL      FNDDOC1
         MOVE      DIM22,ADOCDESC
.
.        Marital Description
.
         MOVE      SP20,MARITAL
         MATCH     SP3,PMSTAT
         GOTO      FNDREL IF EQUAL
.
         PACK      KEY5 WITH CODEM,PMSTAT
         CALL      RDCODE1
         BRANCH    OVRCD OF FNDREL
         MOVE      TDESC,MARITAL
.
.        Religion Description
.
FNDREL   MOVE      SP20,RELIGN
         MATCH     SP3,PREG
         GOTO      FNDCAR IF EQUAL
.
         PACK      KEY5 WITH CODER,PREG
         CALL      RDCODE1
         BRANCH    OVRCD OF FNDCAR
         MOVE      TDESC,RELIGN
.
.        Care Class
.
FNDCAR   MOVE      SP20,CARECLS
         MATCH     SP3,ACARE
         GOTO      FNDCLM IF EQUAL
.
         PACK      KEY5 WITH CODECC,ACARE
         CALL      RDCODE1
         BRANCH    OVRCD OF FNDCLM
         MOVE      TDESC,CARECLS
.
.        Get claim details if any
.
FNDCLM   MOVE      SP20,CLDESC
         MOVE      SP30,FUNDESC
         MOVE      SP20,TBLDESC
.
         PACK      KEY5 WITH CODECL,ACLAIM
         CALL      RDCODE1
         BRANCH    OVRCD OF FNDHF
         MOVE      TDESC,CLDESC
.
FNDHF    PACK      KEY14 WITH AFUNDH,ZERO4,SP10
         CALL      RDFUND1
         BRANCH    OVRCD OF FNDTBLE
         MOVE      HFNAME,FUNDESC
.
FNDTBLE  PACK      KEY14 WITH AFUNDH,AFNDTB,SP10
         CALL      RDFUND1
         BRANCH    OVRCD OF FNDBTYP
         MOVE      HFNAME,TBLDESC
.
.        Get the bed type
.
FNDBTYP  MOVE      SP20,ACDESC
         PACK      KEY6 WITH AWARD,ABED
         CALL      RDWARD1
         BRANCH    OVRCD OF NBTYPE
.
         PACK      KEY5 WITH CODEBT,WEFRBT
         CALL      RDCODE1
         BRANCH    OVRCD OF NBTYPE
.
         MOVE      TDESC,ACDESC
NBTYPE   RETURN
.
.
.       Get Doctor Record 
.
FNDDOC1  MOVE      SP20,DGNAME
         MOVE      SP20,DSNAME
         MOVE      SP30,DIM22
.
         CALL      RDDOCT1
.
         MATCH     SP20,DSNAME
         RETURN    IF EQUAL
.
         MOVE      DGNAME,ANS
         CLEAR     DIM22
         MATCH     SP1,ANS
         GOTO      FNDDOC2 IF EQUAL
         APPEND    ANS,DIM22
         APPEND    DOT,DIM22
FNDDOC2  APPEND    DSNAME,DIM22
         APPEND    SP2,DIM22
         RESET     DIM22
         RETURN
.
.        Print Patient ID
.
PRTSHT5  CALL      OPNPRINT
.
         PRINT     *N,*66,AURNO,*N,*N:                               (LINE 1)
                   *13,AUSR3,*26,A8DATE,*40,ATIME,*66,AADMNO:      (LINE 2)
                   *N,*N,*N:
                   *13,ADOCDESC:                                      (LINE 3)
                   *N,*N,*N,*N:
                   *13,PSNAME,*33,PGNAME:                             (LINE 4)
                   *N,*N,*N,*N,*N:
                   *13,PADD1,*56,PTELEP,*N:                           (LINE 5)
                   *13,PADD2:                                         (LINE 6)
                   *N,*13,PSUBRB,SP1,PPOST,*56,PTELEB:                (LINE 7)
                   *N,*N,*N:
                   *56,PNKRELP:                                      (LINE 8)
                   *N,*13,PNKNAME:                                    (LINE 9)
                   *N,*13,PNKADD1,*56,PNKTELP:                        (LINE10)
                   *N,*13,PNKADD2:                                    (LINE11)
                   *N,*13,PNKSUBR,SP2,PNKPOST,*56,PNKTELB:            (LINE12)
                   *N,*N,*N,*56,PKRELP:                              (LINE13)
                   *N,*13,PKNAME:                                     (LINE14)
                   *N,*13,PKADD1,*56,PKTELEP:                         (LINE15)
                   *N,*13,PKADD2:                                     (LINE16)
                   *N,*13,PKSUBR,SP2,PKPOST,*56,PKTELEB:              (LINE17)
                   *N,*N,*N,*N:
                   *16,D8DATE,*26,XAGE,*34,PSEX,*47,MARITAL:        (LINE18)
                   *N,*N,*N,*N:
                   *16,RELIGN,*34,AUSR4:                             (LINE19)
                   *N,*N,*N,*N:
                   *16,ACDESC,*47,CARECLS:                           (LINE20)
                   *N,*N,*N,*N:
                   *2,CLDESC:                                        (LINE21)
                   *N,*2,FUNDESC,*42,TBLDESC,*59,AFNDMM,*N,*N:       (LINE22)
                   *69,AUSR5:                                        (LINE23)
                   *N,*N,*N,*N,*N,*N,*N:
                   *N,*N,*N,*N,*N,*N,*N,*N
.
         CALL      CLSPRINT
         RETURN
.
