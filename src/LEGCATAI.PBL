.******************************************************************************
.
.        LEGCATAI.PBL  -  THIS IS FOR ALREADY INVOICED
.        ------------     -----------------------------
.
.          V12.00.01 19/05/2025  J.Tan          TSK 0955096
.                    Added Alphanumeric visit number changes
.          V11.00.01 10/03/2020  J.Tan          TSK 0838262
.                    Added Effective from and to date to MBS Item file
.          V9.12.01  10/09/2009  Davin         CAR 205742
.                    Removed open and close of PATITEM1 (in RDKDT000)
.          V9.03.01  30/06/2004  Ebon Clements CAR 51239
.                    Removed open and close of PATFN1A1
.          V9.03.00  11/05/2004  Davin         CAR 49209
.                    Based on 6.11 PATCATAI
.
.******************************************************************************
LCALCTAI  CALL      CINIT000
          MOVE      LPTINCMC,CMCODE           * Casemix code
.
. ------  Print Flag off - re-display Invoice  ------
.
          CALL      SCREN000                * Display the Initial Screen
.>>>>     CALL      CHKCMXPT                * Check if casemix & get epis type
.
          CALL      D1TYP000                * Display Accomodation Charges
          BRANCH    EXIT,LCLCTAIE
.
          CALL      DMVBL000                * Display any mechv billing item
          BRANCH    EXIT,LCLCTAIE
.
          CALL      D2TYP000                * Display Theatre Charges
          BRANCH    EXIT,LCLCTAIE
.
          CALL      D3TYP000                * Display Miscellaneous Items
          BRANCH    EXIT,LCLCTAIE
.
          IF        IBCNUGST=2
            CALL      D6TYP000                * Display Journals
            BRANCH    EXIT,LCLCTAIE
          ENDIF
.
          CALL      D5TYP000                * Display Cash Receipts
          BRANCH    EXIT,LCLCTAIE
.
          IF        IBCNUGST=2
            GOTO      LCLCTAIT
          ENDIF
.
          CALL      D6TYP000                * Display Journals
          BRANCH    EXIT,LCLCTAIE
.
LCLCTAIT  CALL      DENDI000                * Display End of Invoice
.
LCLCTAIE  RETURN
+
.********************************************************************** 
.*                            CINIT000                                *
.*                    Initialize Variables Needed                     *
.********************************************************************** 
CINIT000  MOVE      ZERO TO REBTOT
          MOVE      ZERO,LOOPTRN
          MOVE      ZERO,NOHIPAYM
          MOVE      ZERO,JOURTOT
.....     MOVE      ZERO,PAGENO                                         D-61105
.
          MOVE      ZERO,DCFLAG
          MOVE      ZERO,GROSSTOT
          MOVE      ZERO,TOTDAYS
          MOVE      ZERO,TOTGST
          MOVE      ZERO,TOTHEALTH
          MOVE      ZERO,PAYMTTOT
          MOVE      ZERO,FORM72         * Total payments only
          UNPACK    LPINVDTE INTO XCENT,XYEAR,XMON,XDAY
.
          MOVE      SP10,PTHFBCAT           * Initialise table type
          MATCH     SP6,LAFUNDH
          IF        !@EQUAL
            PACK      KEY14,LAFUNDH,LAFNDTB
            CALL      RDFUND1                 * read health fund file 
          ENDIF
.
          PACK      CMCODE,LPTMIPDR,SP10 * Default to provisional DRG
          PACK      CMDRG,LPTMIPDR,SP10
          PACK      KEY8,LPINVADM,SP10
          CALL      RDLGMIS1
          BRANCH    OVRCD,CINIT1000
.
          UNPACK    SP10,KEY4,KEY5
          UNPACK    LPTMIPDR,KEY4,KEY5     * check if it was a MBS Code
.
          IF        LASTAT = 3
            CALL      RDLGDSC1
            BRANCH    OVRCD,CINIT9999
.
.....            MATCH     SP4,PTDSDDRG         * check discharge DRG code
.....            IF        !@EQUAL
.....              PACK      CMCODE,PTDSDDRG,SP10
.....              PACK      CMDRG,PTDSDDRG,SP10
.....            ENDIF
.
            MATCH     LADATE,LDDATE
            IF        @EQUAL
              MOVE      ONE,DCFLAG
            ENDIF
          ENDIF
.
          MATCH     SP9,LPTINCMC
          IF        !@EQUAL
            PACK      CMCODE,LPTINCMC,SP10
            PACK      CMDRG,LPTINCMC,SP10
          ENDIF
.
          MOVE      CDD,CRDAY
          MOVE      CMM,CRMON
          MOVE      CYY,CRYEAR
          MOVE      CCC,CRCENT
          REP       " 0",CRDAY
          REP       " 0",CRMON
          REP       " 0",CRCENT
          REP       " 0",CRYEAR
.
.         Get last letter details (if any)
.
CINIT1000 MOVE      SP10,DIM10
          MOVE      LPINVADM,KEY8
CINIT999  RETURN
+
.********************************************************************** 
.*                            SCREN000                                *
.*                     Display the Initial Screen                     *
.********************************************************************** 
SCREN000  MOVE      XDAY,CDAY
          MOVE      XMON,CMON
          MOVE      XCENT,CCENT
          MOVE      XYEAR,CYEAR
          CALL      PACDATE
          REP       " 0",CPCDATE
          MATCH     "LEGWEB01",PRGID
          IF        !@EQUAL
            DISPLAY   *P1:7,*EF,*P1:8,"Invoice : ",*V2LON,LPINVNO,*HOFF:
                      *P20:8,"Invoice Date : ":
                      *V2LON,CPCDATE;
          ENDIF
.
SCREN100  MATCH     "LEGWEB01",PRGID
          IF        @EQUAL
            WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell align=center>Date</td>":
                               "<td class=HeadingCell>Description</td>":
                               "<td class=HeadingCell>Item</td>":
                               "<td align=right class=HeadingCell>Rate</td>":
                            "<td align=right class=HeadingCell>GST Exc.</td>":
                            "<td align=right class=HeadingCell>GST Amount</td>":
                            "<td align=right class=HeadingCell>Total</td>":
                            "</tr>"
          ELSE
            IF        IBCNUGST=2
              DISPLAY   *ULON,*V2LON,*P1:9,"  From  ":
                        *P10:9,"   To   ":                   * was P12 *C-61103
                        *P19:9,"Days":                       * was P23 *C-61103
                        *P25:9,"Description               ": * was P28 *C-61103
                        *P56:9,"Item                      ": * was P55 *C-61103
                        *P25:10,"    Rate":                  * was P28 *C-61103
                        *P40:10,"  GST Exc.":                * was P43 *C-61103
                        *P56:10,"GST Amount":                * was P55 *C-61103
                        *P71:10,"    Total ";
            ELSE
              DISPLAY   *ULON,*V2LON,*P1:9,"  From  ":
                        *P10:9,"   To   ":                   * was P12 *C-61103
                        *P19:9,"Days":                       * was P23 *C-61103
                        *P25:9,"Description             ":   * was P28 *C-61103
                        *P56:9,"Item":                       * was P53 *C-61103
                        *P60:9,"   Rate":                    * was P58 *C-61103
                        *P70:9,"    Total  ";
            ENDIF
          ENDIF
.
          IF        IBCNUGST=2
            MOVE      TEN1,VERT
          ELSE
            MOVE      TEN,VERT
          ENDIF
.
          COMPARE   ONE,LPINVTYP
          GOTO      SCREN999 IF NOT EQUAL
.
          MATCH     "LEGWEB01",PRGID
          IF        @EQUAL
            WRITE     HTMLFILE;"<tr><td colspan=5 align=center><b>":
                               "Insurance Invoice</b></td></tr>"
          ELSE
            DISPLAY   *P20:7,"I n s u r a n c e   I n v o i c e";
          ENDIF
.
SCREN999  RETURN
+
.********************************************************************** 
.*                            D1TYP000                                *
.*                       Routine Description                          *
.********************************************************************** 
D1TYP000  PACK      KEY31 WITH LPINVADM,LPINVNO,ONE,SP30
          MOVE      ZERO,INVBFLAG
.
          CALL      RDSDTRAN
D1TYP100  CALL      RDKDTRAN
          BRANCH    OVRCD OF D1TYP950
.
          MATCH     LPINVNO,LTREF
          GOTO      D1TYP950 IF NOT EQUAL
.
          COMPARE   ONE,LTRCTYPE
          GOTO      D1TYP950 IF NOT EQUAL
.
          IF        (LPDTLSPT <> 0) | (LPDTLSRB <> 0)
            IF        DCFLAG=1
              CALL      DLUM0000        * Get desc. for daycase lumpsum amount
            ELSE
              CALL      OLUM0000        * Get desc. for overnight lumpsum amount
            ENDIF
.
D1TYP200
.
D1TYP300    BRANCH    INVBFLAG,D1TYP500
            MOVE      LPDTLSPT,FORM72
            ADD       LPDTLSRB,FORM72
            ADD       LPDTLSRB,REBTOT
.
D1TYP400    MOVE      LTFDAY,CDAY
            MOVE      LTFMONTH,CMON
            MOVE      LTFCENT,CCENT
            MOVE      LTFYEAR,CYEAR
            CALL      PACDATE
            REP       " 0",CPCDATE
            PACK      KEY8,CCENT,CYEAR,CMON,CDAY
            REP       " 0",KEY8
            UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
            MATCH     "LEGWEB01",PRGID
            IF        @EQUAL
              MOVE      CMON,F2
              LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                                   NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
              WRITE     HTMLFILE;"<tr>":
                                "<td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>";
              IF        PTCNIDES=1 | PTCNIDES=2
                WRITE     HTMLFILE;"<td>&nbsp;",SAVDES1,"<br>",SAVDES2,"</td>";
              ELSE
                WRITE     HTMLFILE;"<td>&nbsp;",SAVDES1,"</td>";
              ENDIF
              MOVE      LPDTGSTL,GSTAMNT
              MOVE      FORM72,TOTAMNT
              ADD       GSTAMNT,TOTAMNT
              MOVE      GSTAMNT,GSTAMNT6
              WRITE     HTMLFILE;"<td>&nbsp;</td>":
                                 "<td>&nbsp;</td>":
                                 "<td align=right>",FORM72,"</td>":
                                 "<td align=right>",GSTAMNT6,"</td>":
                                 "<td align=right>",TOTAMNT,"</td>":
                                 "</tr>"
              ADD       GSTAMNT,TOTGST
            ELSE
              DISPLAY   *P1:VERT,CPDATE:                               *I-61103
                        *P25:VERT,SAVDES1;                   * was P28 *C-61103
              IF        PTCNIDES=1 | PTCNIDES=2
                MATCH     "LEGWEB01",PRGID
                IF        !@EQUAL
                  ADD       ONE,VERT
                  DISPLAY   *P25:VERT,SAVDES2                * was P28 *C-61103
                ENDIF
              ENDIF
              IF        IBCNUGST=2
                MOVE      LPDTGSTL,GSTAMNT
                MOVE      FORM72,TOTAMNT
                ADD       GSTAMNT,TOTAMNT
                ADD       ONE,VERT
                MOVE      GSTAMNT,GSTAMNT6
                DISPLAY   *P40:VERT,FORM72:                  * was P44 *C-61103
                          *P57:VERT,GSTAMNT6:
                          *P71:VERT,TOTAMNT
                ADD       GSTAMNT,TOTGST
              ELSE
                DISPLAY   *P71:VERT,FORM72
              ENDIF
            ENDIF
            ADD       FORM72,GROSSTOT
            ADD       ONE,VERT
.
          ENDIF
          BRANCH    INVBFLAG,D1TYP200
.
.         Daycase casemix patient does not have any additional charge
.
D1TYP500  COMPARE   "22",VERT
          IF        !@LESS
            CALL      SEMOR000                * See More Y/N/C
            BRANCH    EXIT,D1TYP900
          ENDIF
.
          IF        DCFLAG = 1
            COMPARE   ZERO,LTNODAYS
            GOTO      D1TYP800 IF EQUAL
          ENDIF
.
          ADD       LTREBATE TO REBTOT
          MOVE      LTFDAY,CDAY
          MOVE      LTFMONTH,CMON
          MOVE      LTFCENT,CCENT
          MOVE      LTFYEAR,CYEAR
          CALL      PACDATE
          REP       " 0",CPCDATE
          MOVE      LPDTAMTT,FORM7P2
          MOVE      LTDDESC,DIM24
          MOVE      LTDDESC,DIM30
.
          CALL      D1WEB000
.
          ADD       LPDTAMTT,GROSSTOT
          ADD       LTNODAYS,TOTDAYS
          ADD       ONE,VERT
.
          IF        PTCNIDES=1 | PTCNIDES=2
            COMPARE   "22",VERT
            IF        !@LESS
              CALL      SEMOR000                * See More Y/N/C
              BRANCH    EXIT,D1TYP900
            ENDIF
            DISPLAY   *P28:VERT,LPDTDES2;
            ADD       ONE,VERT
          ENDIF
.
D1TYP800  COMPARE   "22",VERT
          GOTO      D1TYP100 IF LESS
.
          CALL      SEMOR000                * See More Y/N/C
          BRANCH    EXIT,D1TYP900
          GOTO      D1TYP100
.
D1TYP900  MOVE      TRUE,EXIT
          GOTO      D1TYP999
.
D1TYP950  MOVE      FALSE,EXIT
.
D1TYP999  RETURN
.
D1WEB000  PACK      KEY8,LTFCENT,LTFYEAR,LTFMONTH,LTFDAY
          REP       " 0",KEY8
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          MOVE      LTFMONTH,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
          WRITE     HTMLFILE;"<tr>":
                             "<td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          PACK      KEY8,LTTCENT,LTTYEAR,LTTMONTH,LTTDAY
          REP       " 0",KEY8
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          MOVE      LTTMONTH,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
          WRITE     HTMLFILE;" to ",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                             "<td>",LTNODAYS," Days ";
          IF        PTCNIDES=1 | PTCNIDES=2
            WRITE     HTMLFILE;LTDDESC,"<br>",LPDTDES2,"</td>";
          ELSE
            WRITE     HTMLFILE;LTDDESC,"</td>";
          ENDIF
          MOVE      LPDTGSTM,GSTAMNT
          MOVE      LTPTAMTT,TOTAMNT
          ADD       GSTAMNT,TOTAMNT
          MOVE      GSTAMNT,GSTAMNT6
          ADD       GSTAMNT,TOTGST
.
          WRITE     HTMLFILE;"<td>&nbsp;</td>":
                             "<td align=right>",LTPTAMTI,"</td>":
                             "<td align=right>",LTPTAMTT,"</td>":
                             "<td align=right>",GSTAMNT6,"</td>":
                             "<td align=right>",TOTAMNT,"</td></tr>"
          RETURN
.********************************************************************** 
.*                            D2TYP000                                *
.*                       Routine Description                          *
.********************************************************************** 
D2TYP000  IF        LPINVSYS = 3
            PACK      KEY31 WITH LPINVADM,LPINVNO,TWO,SP30
          ELSE
            PACK      KEY31 WITH LPINVADM,LPINVNO,ONE,SP30
          ENDIF
.
          CALL      RDSDTRAN
D2TYP100  CALL      RDKDTRAN
          BRANCH    OVRCD OF D2TYP950
          MATCH     LPINVNO,LTREF
          GOTO      D2TYP950 IF NOT EQUAL
.
          IF        LTRCTYPE<>2
            IF        LTRCTYPE=3 & (LPINVSYS=1 | LPINVSYS=2)
.
. ----- Go thru all misc items to get theatre items for A&E & outpatients -----
.
              GOTO      D2TYP100
            ELSE
              GOTO      D2TYP950
            ENDIF
          ENDIF
.
          MOVE      LTDDESC,DIM18
.
          SCAN      " X",LTDDESC
          GOTO      D2TYP200 IF NOT EQUAL
          MOVE      ZERO,FORM2
          MOVE      LTDDESC,DIM5
          MOVEFPTR  LTDDESC,FORM2
          IF        FORM2 > 17
            RESET     DIM18,17
            APPEND    DIM5,DIM18
            RESET     DIM18
          ENDIF
.
D2TYP200  ADD       LTREBATE TO REBTOT
          MOVE      LTFDAY,CDAY
          MOVE      LTFMONTH,CMON
          MOVE      LTFCENT,CCENT
          MOVE      LTFYEAR,CYEAR
          CALL      PACDATE
          REP       " 0",CPCDATE
          MOVE      LPDTAMTT,FORM7P2
.
          MATCH     "LEGWEB01",PRGID
          IF        @EQUAL
            CALL      D2WEB000
          ELSE
.....       DISPLAY   *P1:VERT,CPCDATE:                                 D-61103
            DISPLAY   *P1:VERT,CPDATE:                                 *I-61103
                      *P25:VERT,DIM18:                       * was P28 *C-61103
                      *P56:VERT,LTITEMNO;                     * was P47 *C-61103
.
            IF        IBCNUGST=2
              ADD       ONE,VERT
              MOVE      LPDTGSTM,FORM72
              DISPLAY   *P40:VERT,FORM7P2:                   * was P44 *C-61103
                        *P56:VERT,FORM72;
              ADD       LPDTGSTM,FORM7P2
              ADD       LPDTGSTM,TOTGST
            ENDIF
            DISPLAY   *P71:VERT,FORM7P2;
          ENDIF
.
          ADD       LPDTAMTT,GROSSTOT
.
          ADD       ONE,VERT
          COMPARE   "23",VERT
          GOTO      D2TYP100 IF LESS
.
          CALL      SEMOR000                * See More Y/N/C
          BRANCH    EXIT,D2TYP900
          GOTO      D2TYP100
.
D2TYP900  MOVE      TRUE,EXIT
          GOTO      D2TYP999
.
D2TYP950  MOVE      FALSE,EXIT
.
D2TYP999  RETURN
.
D2WEB000  PACK      KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KEY8
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
          MOVE      LPDTGSTM,FORM72
          WRITE     HTMLFILE;"<tr>":
                             "<td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                             "<td>&nbsp;",DIM18,"</td>":
                             "<td>&nbsp;",LTITEMNO,"</td>":
                             "<td>&nbsp;</td>":
                             "<td align=right>",FORM7P2,"</td>":
                             "<td align=right>",FORM72,"</td>";
          ADD       LPDTGSTM,FORM7P2
          ADD       LPDTGSTM,TOTGST
          WRITE     HTMLFILE;"<td align=right>",FORM7P2,"</td></tr>"
          RETURN
+
.********************************************************************** 
.*                            DMVBL000                                *
.*                       Routine Description                          *
.********************************************************************** 
DMVBL000  PACK      KEY31,LPINVADM,LPINVNO,THREE,SP30
.
          CALL      RDSDTRAN
DMVBL100  CALL      RDKDTRAN
          BRANCH    OVRCD,DMVBL950
.
          MATCH     LPINVNO,LTREF
          GOTO      DMVBL950 IF NOT EQUAL
.
          COMPARE   THREE,LTRCTYPE
          GOTO      DMVBL950 IF NOT EQUAL
.
          MATCH     "1",LPDTMVBR                 * only want mv billing record
          GOTO      DMVBL100 IF NOT EQUAL
.
          MOVE      LTDDESC,DIM20
.
          SCAN      " X",LTDDESC
          GOTO      DMVBL200 IF NOT EQUAL
          MOVE      ZERO,FORM2
          MOVE      LTDDESC,DIM5
          MOVEFPTR  LTDDESC,FORM2
          IF        FORM2 > 17
            RESET     DIM20,17
            APPEND    DIM5,DIM20
            RESET     DIM20
          ENDIF
.
DMVBL200  ADD       LTREBATE TO REBTOT
          MOVE      LTFDAY,CDAY
          MOVE      LTFMONTH,CMON
          MOVE      LTFCENT,CCENT
          MOVE      LTFYEAR,CYEAR
          CALL      PACDATE
          REP       " 0",CPCDATE
.
          MOVE      LPDTAMTT,FORM7P2
.
          MATCH     "LEGWEB01",PRGID
          IF        @EQUAL
            CALL      D3WEB000
          ELSE
            MOVE      LPTINMVD,MVDAYS
            DISPLAY   *P19:VERT,MVDAYS:                      * was P23 *C-61103
                      *P25:VERT,DIM20:                       * was P28 *C-61103
                      *P56:VERT,LTITEMNO;                     * was P55 *C-61103
            IF        IBCNUGST=2
              MOVE      LPDTGSTM,FORM72
              ADD       ONE,VERT
              DISPLAY   *P40:VERT,FORM7P2:                   * was P44 *C-61103
                        *P56:VERT,FORM72                     * was P56 *C-61103
              ADD       FORM72,FORM7P2
              ADD       LPDTGSTM,TOTGST
            ENDIF
            DISPLAY   *P71:VERT,FORM7P2;
          ENDIF
          ADD       LPDTAMTT,GROSSTOT
.
          ADD       ONE,VERT
          COMPARE   "23",VERT
          GOTO      DMVBL950 IF LESS
.
          CALL      SEMOR000                * See More Y/N/C
          BRANCH    EXIT,DMVBL900
          GOTO      DMVBL950
.
DMVBL900  MOVE      TRUE,EXIT
          GOTO      DMVBL999
.
DMVBL950  MOVE      FALSE,EXIT
.
DMVBL999  RETURN
+
.********************************************************************** 
.*                            D3TYP000                                *
.*                       Routine Description                          *
.********************************************************************** 
D3TYP000  IF        LPINVSYS = 3
            PACK      KEY31 WITH LPINVADM,LPINVNO,THREE,SP30
          ELSE
            PACK      KEY31 WITH LPINVADM,LPINVNO,ONE,SP30
          ENDIF
.
          CALL      RDSDTRAN
D3TYP100  CALL      RDKDTRAN
          BRANCH    OVRCD OF D3TYP950
.
          MATCH     LPINVNO,LTREF
          GOTO      D3TYP950 IF NOT EQUAL
.
          COMPARE   THREE,LTRCTYPE
          GOTO      D3TYP950 IF NOT EQUAL
.
          MATCH     "1",LPDTMVBR                 * don't show mv billing record
          GOTO      D3TYP100 IF EQUAL
.
          MOVE      LTDDESC,DIM20
.
          SCAN      " X",LTDDESC
          GOTO      D3TYP200 IF NOT EQUAL
          MOVE      ZERO,FORM2
          MOVE      LTDDESC,DIM5
          MOVEFPTR  LTDDESC,FORM2
          IF        FORM2 > 17
            RESET     DIM20,17
            APPEND    DIM5,DIM20
            RESET     DIM20
          ENDIF
.
D3TYP200  ADD       LTREBATE TO REBTOT
          MOVE      LTFDAY,CDAY
          MOVE      LTFMONTH,CMON
          MOVE      LTFCENT,CCENT
          MOVE      LTFYEAR,CYEAR
          CALL      PACDATE
          REP       " 0",CPCDATE
.
          MOVE      LPDTAMTT,FORM7P2
.
          MATCH     "LEGWEB01",PRGID
          IF        @EQUAL
            CALL      D3WEB000
          ELSE
.....       DISPLAY   *P1:VERT,CPCDATE:                                 D-61103
            DISPLAY   *P1:VERT,CPDATE:                                 *I-61103
                      *P25:VERT,DIM20:                       * was P28 *C-61103
                      *P56:VERT,LTITEMNO;                     * was P55 *C-61103
            IF        IBCNUGST=2
              MOVE      LPDTGSTM,FORM72
              ADD       ONE,VERT
              DISPLAY   *P40:VERT,FORM7P2:                   * was P44 *C-61103
                        *P56:VERT,FORM72                     * was P56 *C-61103
              ADD       FORM72,FORM7P2
              ADD       LPDTGSTM,TOTGST
            ENDIF
            DISPLAY   *P71:VERT,FORM7P2;
          ENDIF
          ADD       LPDTAMTT,GROSSTOT
.
          ADD       ONE,VERT
          COMPARE   "23",VERT
          GOTO      D3TYP100 IF LESS
.
          CALL      SEMOR000                * See More Y/N/C
          BRANCH    EXIT,D3TYP900
          GOTO      D3TYP100
.
D3TYP900  MOVE      TRUE,EXIT
          GOTO      D3TYP999
.
D3TYP950  MOVE      FALSE,EXIT
.
D3TYP999  RETURN
.
D3WEB000  PACK      KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KEY8
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
          MOVE      LPDTGSTM,FORM72
          WRITE     HTMLFILE;"<tr>":
                             "<td>",CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                             "<td>&nbsp;",DIM20,"</td>":
                             "<td>&nbsp;",LTITEMNO,"</td>":
                             "<td>&nbsp;</td>":
                             "<td align=right>",FORM7P2,"</td>":
                             "<td align=right>",FORM72,"</td>";
          ADD       FORM72,FORM7P2
          ADD       LPDTGSTM,TOTGST
          WRITE     HTMLFILE;"<td align=right>",FORM7P2,"</td></tr>"
          RETURN
+
.********************************************************************** 
.*                            JPHED000                                *
.*                     Journal/Payment Heading                        *
.********************************************************************** 
JPHED000  MATCH     "LEGWEB01",PRGID
          IF        @EQUAL
.>>>>       WRITE     HTMLFILE;"<tr bgcolor=#CCCCCC>":
.>>>>                          "<td align=center><b>Date</b></td>":
.>>>>                          "<td><b>&nbsp From </b></td>":
.>>>>                          "<td><b>&nbsp Receipt </b></td>":
.>>>>                          "<td><b>&nbsp How Paid </b></td>":
.>>>>                          "<td align=right><b>Amount</b></td></tr>"
          ELSE
            DISPLAY   *P1:VERT,*EF,*ULON:
                      *P12:VERT,"   Date   ":
                      *P26:VERT,"From                     ":
                      *P54:VERT,"Receipt":
                      *P63:VERT,"How Paid":
                      *P72:VERT,"   Amount";
          ENDIF
          ADD       ONE,VERT
          RETURN
+
.********************************************************************** 
.*                            D5TYP000                                *
.*                       Routine Description                          *
.********************************************************************** 
D5TYP000  MOVE      ONE,CASHFLAG
.
          IF        LPINVSYS = 3
            PACK      KEY31 WITH LPINVADM,LPINVNO,FIVE,SP30
          ELSE
            PACK      KEY31 WITH LPINVADM,LPINVNO,TWO,SP30
          ENDIF
.
          CALL      RDSDTRAN
D5TYP100  CALL      RDKDTRAN
          BRANCH    OVRCD OF D5TYP950
.
          MATCH     LPINVNO,LTREF
          GOTO      D5TYP950 IF NOT EQUAL
.
          COMPARE   FIVE,LTRCTYPE
          GOTO      D5TYP950 IF NOT EQUAL
.
          COMPARE   ZERO,LTREBATE
          GOTO      D5TYP200 IF EQUAL
          MOVE      ONE,NOHIPAYM
.
D5TYP200  ADD       LTREBATE TO REBTOT
.
          COMPARE   ZERO,CASHFLAG           * Skip Total/Heading if not 1st time
          GOTO      D5TYP300 IF EQUAL
.
          COMPARE   TWO,IBCNUGST
          GOTO      D5TYP300 IF EQUAL
.
          CALL      DITOT000                * Display the Invoice Total
.
          COMPARE   "23",VERT
          GOTO      D5TYP210 IF LESS
.
          CALL      SEMOR000                * See More Y/N/C
          BRANCH    EXIT,D5TYP900
.
          MATCH     "LEGWEB01",PRGID
          IF        !@EQUAL
            DISPLAY   *P1:VERT," ";
          ENDIF
.
          ADD       ONE,VERT
.
D5TYP210  IF        IBCNUGST=2
            GOTO      D5TYP300
          ENDIF
          CALL      JPHED000                * Journal Payment Heading
.
D5TYP300  COMPARE   ZERO,LPDTAMTT
          GOTO      D5TYP100 IF EQUAL
          MOVE      "Payment ",DIM8
          ADD       LPDTAMTT,FORM72
          MOVE      LTDDESC,DIM22
.
.         Check if this is a health fund payment
.
          MATCH     "H" TO LTTYPDEB
          GOTO      D5TYP400 IF NOT EQUAL
          ADD       LPDTAMTT TO TOTHEALTH
.
.         Display Payment
.
D5TYP400  MOVE      SP6,DIM6
          LOAD      DIM6 FROM LTPAYTYP OF CASH,CHEQUE,CREDIT,MERCH,MONEY
.
          MOVE      LTDDESC,DIM25
          MOVE      LTFDAY,CDAY
          MOVE      LTFMONTH,CMON
          MOVE      LTFCENT,CCENT
          MOVE      LTFYEAR,CYEAR
          CALL      PACDATE
          REP       " 0",CPCDATE
          MOVE      LPDTAMTT,FORM7P2
          MATCH     "LEGWEB01",PRGID
          IF        @EQUAL
            CALL      D5WEB000
          ELSE
            DISPLAY   *P1:VERT,DIM8:
.....                 *P12:VERT,CPCDATE:                                D-61103
                      *P10:VERT,CPDATE:                                *I-61103
                      *P25:VERT,DIM25:                       * was P23 *C-61116
                      *P42:VERT,LTRECEPT:
                      *P57:VERT,DIM6:
                      *P71:VERT,FORM7P2;
          ENDIF
.
          ADD       LPDTAMTT,PAYMTTOT
          MOVE      ZERO,CASHFLAG
.
          ADD       ONE,VERT
          COMPARE   "23",VERT
          GOTO      D5TYP100 IF LESS
.
          CALL      SEMOR000                * See More Y/N/C
          BRANCH    EXIT,D5TYP900
          GOTO      D5TYP100
.
D5TYP900  MOVE      TRUE,EXIT
          GOTO      D5TYP999
.
D5TYP950  MOVE      FALSE,EXIT
.
D5TYP990  BRANCH    CASHFLAG,D5TYP999
          CALL      DCSHT000                * Display Cash Total
.
D5TYP999  RETURN
+
D5WEB000  PACK      KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KEY8
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
.
          WRITE     HTMLFILE;"<tr><td>",DIM8," - ":
                              CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                              "<td>",DIM25,LTRECEPT,"</td>":
                              "<td>&nbsp",DIM6,"</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td>&nbsp;</td>":
                              "<td align=right>",FORM7P2,"</td></tr>"
          RETURN
.
.********************************************************************** 
.*                            DCSHT000                                *
.*                       Display Cash Total                           *
.********************************************************************** 
DCSHT000  MATCH     "LEGWEB01",PRGID
          IF        @EQUAL
            WRITE     HTMLFILE;"<tr bgcolor=#CCCCCC><td>&nbsp;</td>":
                               "<td><b>Payment Total</b></td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td align=right><b>",PAYMTTOT,"</td></tr>"
          ELSE
            DISPLAY   *P54:VERT,"Payment Total ":
                      *V2LON,*ULON,"  $",*P71:VERT,PAYMTTOT;
          ENDIF
          ADD       ONE,VERT
          RETURN
.********************************************************************** 
.*                            D6TYP000                                *
.*                       Routine Description                          *
.********************************************************************** 
D6TYP000  MOVE      ONE,JOURFLAG
.
          IF        LPINVSYS = 3
            PACK      KEY31 WITH LPINVADM,LPINVNO,SIX,SP30
          ELSE
            PACK      KEY31 WITH LPINVADM,LPINVNO,THREE,SP30
          ENDIF
.
          CALL      RDSDTRAN
D6TYP100  CALL      RDKDTRAN
          BRANCH    OVRCD,D6TYP950               * Check for end of File
.
          MATCH     LPINVNO,LTREF                  * Check if Same Invoice
          GOTO      D6TYP950 IF NOT EQUAL
.
          COMPARE   SIX,LTRCTYPE                 * Check for a Journal
          GOTO      D6TYP950 IF NOT EQUAL
          ADD       LTREBATE,REBTOT
.
          MOVE      LPDTAMTT,FORM72
          IF        IBCNUGST=2
            ADD       LPDTGSTM,FORM72
          ENDIF
.
          COMPARE   ZERO TO FORM72
          GOTO      D6TYP100 IF EQUAL
.
          COMPARE   ZERO,JOURFLAG
          GOTO      D6TYP200 IF EQUAL
.
.         COMPARE   ZERO,CASHFLAG
.         GOTO      D6TYP200 IF EQUAL
.
          COMPARE   "21",VERT
          GOTO      D6TYP110 IF LESS
.
          CALL      SEMOR000                * See More Y/N/C
          BRANCH    EXIT,D6TYP900
          MATCH     "LEGWEB01",PRGID
          IF        !@EQUAL
            DISPLAY   *P1:VERT," ";
          ENDIF
          ADD       ONE,VERT
.
D6TYP110  CALL      DITOT000                * Display the Invoice Total
          IF        IBCNUGST=2
            GOTO      D6TYP200
          ENDIF
.
          COMPARE   ZERO,JOURFLAG         * Skip Total/Heading if not 1st time
          GOTO      D6TYP200 IF EQUAL
          CALL      JPHED000                * Journal Payment Heading
.
D6TYP200  MOVE      "Journal ",DIM8
          MOVE      LTDDESC,DIM22
.
          MOVE      SP6,DIM6
          COMPARE   ZERO,PTCNPJCI
          GOTO      D6TYP300 IF EQUAL
          MOVE      "JNL ",DIM4
          PACK      DIM6,DIM4,LTTYPE
.
D6TYP300  MOVE      LTFDAY,CDAY
          MOVE      LTFMONTH,CMON
          MOVE      LTFCENT,CCENT
          MOVE      LTFYEAR,CYEAR
          CALL      PACDATE
          REP       " 0",CPCDATE
.
          MOVE      LTDDESC,DIM25
          MOVE      LPDTAMTT,FORM7P2
          MATCH     "LEGWEB01",PRGID
          IF        @EQUAL
            CALL      D6WEB000
          ELSE
            DISPLAY   *P1:VERT,DIM8:
.....                 *P12:VERT,CPCDATE:                                D-61103
                      *P10:VERT,CPDATE:                                *I-61103
                      *P25:VERT,DIM25;                       * was P23 *C-61116
            IF        IBCNUGST=2 & LTRCTYPE=6
              IF        LPDTGSTM<>0
                MOVE      LPDTGSTM,FORM72
                DISPLAY   *P56:VERT,FORM72;
                ADD       LPDTGSTM,LTPTAMTT
              ELSE
                DISPLAY   *P40:VERT,LTPTAMTT;                * was P43 *C-61103
              ENDIF
            ELSE
              DISPLAY   *P50:VERT,LTRECEPT:
                        *P63:VERT,DIM6;
            ENDIF
          ENDIF
.
          DISPLAY   *P70:VERT,LTPTAMTT;
          MOVE      ZERO,JOURFLAG
.
          ADD       LTPTAMTT,PAYMTTOT
          ADD       LTPTAMTT,JOURTOT
.
          ADD       ONE,VERT
          COMPARE   "23",VERT
          GOTO      D6TYP100 IF LESS
.
          CALL      SEMOR000                * See More Y/N/C
          BRANCH    EXIT,D6TYP900
          GOTO      D6TYP100
.
D6TYP900  MOVE      TRUE,EXIT
          GOTO      D6TYP999
.
D6TYP950  MOVE      FALSE,EXIT
          COMPARE   "20",VERT
          GOTO      D6TYP960 IF LESS
.
          CALL      SEMOR000                * See More Y/N/C
          BRANCH    EXIT,D6TYP900
.
D6TYP960  BRANCH    JOURFLAG,D6TYP999
          CALL      DJRLT000                * Display Journal Total
.
D6TYP999  RETURN
.
D6WEB000  PACK      KEY8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",KEY8
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN:
                               NJUL,NAUG,NSEP,NOCT,NNOV,NDEC
.
          WRITE     HTMLFILE;"<tr><td>",DIM8," - ":
                              CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</td>":
                              "<td>",DIM25,LTRECEPT,"</td>":
                              "<td>&nbsp",DIM6,"</td>":
                              "<td>&nbsp;</td>";
          IF        LTRCTYPE=6
            IF        LPDTGSTM<>0
              MOVE      LPDTGSTM,FORM72
              ADD       LPDTGSTM,LTPTAMTT
              WRITE     HTMLFILE;"<td>&nbsp;</td>":
                                 "<td align=right>",FORM72,"</td>";
            ELSE
              WRITE     HTMLFILE;"<td align=right>",LTPTAMTT,"</td>":
                                 "<td>&nbsp;</td>";
            ENDIF
          ELSE
            WRITE     HTMLFILE;"<td>&nbsp",LTRECEPT,"</td>":
                               "<td>&nbsp",DIM6,"</td>";
          ENDIF
          WRITE     HTMLFILE;"<td align=right>",LTPTAMTT,"</td></tr>"
          RETURN
.
.********************************************************************** 
.*                            DJRLT000                                *
.*                    Display the Journal Total                       *
.********************************************************************** 
DJRLT000  MOVE      JOURTOT,FORM72
          MATCH     "LEGWEB01",PRGID
          IF        @EQUAL
            WRITE     HTMLFILE;"<tr bgcolor=#CCCCCC><td>&nbsp;</td>":
                               "<td><b>Adjustments Total</b></td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td align=right><b>",FORM72,"</td></tr>"
          ELSE
            DISPLAY   *P52:VERT,"Adjustments Total ":
                      *V2LON,*ULON,"$",*P71:VERT,FORM72;
          ENDIF
          ADD       ONE,VERT
          RETURN
+
.********************************************************************** 
.*                            DITOT000                                *
.*                    Display the Invoice Total                       *
.********************************************************************** 
DITOT000  MATCH     "LEGWEB01",PRGID
          IF        @EQUAL
            MOVE      GROSSTOT,TOTAMNT
            ADD       TOTGST,TOTAMNT
            WRITE     HTMLFILE;"<tr bgcolor=#CCCCCC><td>&nbsp;</td>":
                               "<td><b>Invoice Total</b></td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td align=right><b>",GROSSTOT,"</td>":
                               "<td align=right><b>",TOTGST,"</td>":
                               "<td align=right><b>",TOTAMNT,"</td></tr>"
          ELSE
            IF        IBCNUGST=2
              MOVE      GROSSTOT,TOTAMNT
              ADD       TOTGST,TOTAMNT
              DISPLAY   *P25:VERT,"Invoice Total":                     *C-61103
                        *P39:VERT,*V2LON,"$",GROSSTOT:                 *C-61103
                        *P56:VERT,TOTGST,*P71:VERT,TOTAMNT
            ELSE
              DISPLAY   *P54:VERT,"Invoice Total",*V2LON,"  $":
                        *P71:VERT,GROSSTOT
            ENDIF
          ENDIF
          ADD        ONE,VERT
          RETURN
+
.********************************************************************** 
.*                            DREBT000                                *
.*                  See More Data on Screen (Y/N/C)                   *
.********************************************************************** 
DREBT000  BRANCH    PTCNPEFR,DREBT010,DREBT999
          COMPARE   ZERO,REBTOT
          GOTO      DREBT999 IF EQUAL
.
DREBT010  ADD       ONE,VERT
          MATCH     "LEGWEB01",PRGID
          IF        @EQUAL
            WRITE     HTMLFILE;"<tr><td>&nbsp;</td>":
                               "<td><b>Estimated Rebate</b></td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td align=right><b>",REBTOT,"</td></tr>"
          ELSE
            IF        IBCNUGST=2
              MOVE      LPTINPMA,FORM82
              SUB       LPTINPMA,REBTOT
              DISPLAY   *P26:VERT,"Estimate Rebate":
                        *P66:VERT,*V2LON,"$    ",REBTOT
              ADD       ONE,VERT
              DISPLAY   *P26:VERT,"Patient Moiety":
                        *P66:VERT,*V2LON,"$",*P70:VERT,FORM82
            ELSE
              MOVE      LPTINPMA,FORM82
              SUB       LPTINPMA,REBTOT
              DISPLAY   *P47:VERT,"Estimate Rebate",*V2LON,"  $":
                        *P71:VERT,REBTOT
              ADD       ONE,VERT
              DISPLAY   *P26:VERT,"Patient Moiety":
                        *P66:VERT,*V2LON,"$",*P70:VERT,FORM82  
            ENDIF
          ENDIF
          ADD       ONE,VERT
.
DREBT999  RETURN
+
.********************************************************************** 
.*                            DENDI000                                *
.*                    Display End of the Invoice                      *
.********************************************************************** 
DENDI000  COMPARE   ZERO,CASHFLAG
          GOTO      DENDI500 IF EQUAL
.
          COMPARE   ZERO,JOURFLAG
          GOTO      DENDI500 IF EQUAL
.
          CALL      DITOT000                * Display Invoice Total
          CALL      DREBT000                * Display Rebate Total
          GOTO      DENDI9999
.
DENDI500  MOVE      GROSSTOT,NETTOT
          ADD       PAYMTTOT,NETTOT
.
          MATCH     "LEGWEB01",PRGID
          IF        @EQUAL
            ADD       TOTGST,NETTOT
            WRITE     HTMLFILE;"<tr><td>&nbsp;</td>":
                               "<td><b>Balance Payable</b></td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>":
                               "<td>&nbsp;</td>";
.
            IF        NETTOT<0
              WRITE     HTMLFILE;"<td align=right><font color=#FF0000><b>":
                                 NETTOT,"</font></b></td></tr>"
            ELSE
              WRITE     HTMLFILE;"<td align=right><font color=#0000FF><b>":
                                 NETTOT,"</font></b></td></tr>"
            ENDIF
          ELSE
            IF         IBCNUGST=2
              ADD       TOTGST,NETTOT
            ENDIF
            DISPLAY   *P54:VERT,"Balance Payable ":
                      *V2LON,*ULON,"$",*P71:VERT,NETTOT;
          ENDIF
          CALL      DREBT000                * Display Rebate Total
.
DENDI999  RETURN
+
.********************************************************************** 
.*                            SEMOR000                                *
.*                  See More Data on Screen (Y/N/C)                   *
.********************************************************************** 
SEMOR000  MOVE      FALSE,EXIT
          MOVE      TEN,VERT
SEMOR999  RETURN
+
.********************************************************************** 
.*                            RDSDT000                                *
.*                  Position on Next Dtra Record                      *
.********************************************************************** 
RDSDTRAN  BRANCH     LPINVSYS OF RDSDT300,RDSDT200,RDSDT100,RDSDT200
.
RDSDT100  CALL       RSLGDTR3
          GOTO       RDSDT999
.
RDSDT200  CALL       RSLGDTO3
          GOTO       RDSDT999
.
RDSDT300  CALL       RSLGDTA3
.
RDSDT999  RETURN
+
.********************************************************************** 
.*                            RDKDT000                                *
.*                      Get Next Dtra Record                          *
.********************************************************************** 
RDKDTRAN  BRANCH     LPINVSYS OF RDKDT300,RDKDT200,RDKDT100,RDKDT200
.
RDKDT100  CALL       RKLGDTR3
          GOTO       RDKDT999
.
RDKDT200  CALL       RKLGDTO3
          BRANCH     OVRCD OF RDKDT999
.
          MOVE       LOTNUMB,LTADMNO
          MOVE       LOTINVNO,LTREF
          MOVE       LOTDDESC,LTDDESC
          MOVE       LOTPATAM,LPDTAMTT
          MOVE       LOTDTGSA,LPDTGSTM
          MOVE       LOTPATAM,LTPTAMTT
          MOVE       LOTITEMN,LTITEMNO
.
          UNPACK     LOTDDATE WITH XCENT,XYEAR,XMON,XDAY
          MOVE       XDAY,LTFDAY
          MOVE       XMON,LTFMONTH
          MOVE       XYEAR,LTFYEAR
          MOVE       XCENT,LTFCENT
.
          MOVE       LOTRECEP,LTRECEPT
          MOVE       LOTTYPE,LTTYPE
          MOVE       LOTPAYTY,LTPAYTYP
          MOVE       ZERO,LTRCTYPE
          LOAD       LTRCTYPE USING LOTRECTY OF THREE,FIVE,SIX
          MOVE       LOTREBPO,LPDTREBT
          MOVE       LOTREBPO,LTREBATE
          GOTO       RDKDT999
.
RDKDT300  CALL       RKLGDTA3
          BRANCH     OVRCD OF RDKDT999
.
          MOVE       LATNUMB,LTADMNO
          MOVE       LATINVNO,LTREF
          MOVE       LATDDESC,LTDDESC
          MOVE       LATPATAM,LPDTAMTT
          MOVE       LATPATAM,LTPTAMTT
.
          UNPACK     LATDDATE WITH XCENT,XYEAR,XMON,XDAY
          MOVE       XDAY,LTFDAY
          MOVE       XMON,LTFMONTH
          MOVE       XYEAR,LTFYEAR
          MOVE       XCENT,LTFCENT
.
          MOVE       LATITEMN,LTITEMNO
          MOVE       LATRECEP,LTRECEPT
          MOVE       LATTYPE,LTTYPE
          MOVE       LATPAYTY,LTPAYTYP
.
          MOVE       ZERO,LTRCTYPE
          LOAD       LTRCTYPE USING LATRECTY OF THREE,FIVE,SIX
          MOVE       LATREBPO,LTREBATE
          MOVE       LATREBPO,LPDTREBT
.
.         Check if this is theatre item
.
          COMPARE    THREE,LTRCTYPE
          GOTO       RDKDT999 IF NOT EQUAL
.>>>>
          PACK       KEY17,LTITEMNO,SP5
          CALL       PATITMRD
          BRANCH     OVRCD OF RDKDT900
.
          COMPARE    ONE,PTMBSINV
          GOTO       RDKDT900 IF NOT EQUAL
          MOVE       TWO,LTRCTYPE
.
RDKDT900  MOVE       ZERO,OVRCD
.
RDKDT999  RETURN
+
.*******************************************************************************
.         OLUM0000 - Overnight lumpsum
.*******************************************************************************
OLUM0000  MOVE      SP10,PTHFBCAT         * initialise table type
          PACK      KEY14,LAFUNDH,LAFNDTB
          CALL      RDFUND1
.
          OPEN      PATHLFA1,"pathlfaf"
          PACK      KEY21,LACLAIM,LAFUNDH,PTHFBCAT,CMDRG
          CALL      RDPTHLFA1
          IF        OVRCD = 1
            PACK      KEY21,LACLAIM,SP6,SP3,CMDRG
            CALL      RDPTHLFA1
            IF        OVRCD = 1
             PACK      KEY21,ZERO,SP2,SP6,SP3,CMDRG
             CALL      RDPTHLFA1
              CLOSE     PATHLFA1
              BRANCH    OVRCD,OLUM9999        * no lumpsum charges
            ENDIF
          ENDIF
          CLOSE     PATHLFA1
          MOVE      PTHLDES1,SAVDES1
          MOVE      PTHLDES2,SAVDES2
.
OLUM9999  RETURN
+
.*******************************************************************************
.         DLUM0000 - Generate lumpsum for daycase
.*******************************************************************************
DLUM0000  MOVE      SP10,PTHFBCAT         * initialise table type
          PACK      KEY14,LAFUNDH,LAFNDTB
          CALL      RDFUND1
.
          PACK      SAVDES1,SP20,SP20
          PACK      SAVDES2,SP20,SP20
          OPEN      PATLSDA1,"patlsdaf"   * open Day case file
          PACK      KEY21,LACLAIM,LAFUNDH,PTHFBCAT,CMDRG,LADTYPE
          CALL      RDPTLSD1
          IF        OVRCD = 1
            PACK      KEY21,LACLAIM,SP6,SP3,CMDRG,LADTYPE
            CALL      RDPTLSD1
            IF        OVRCD = 1
              PACK      KEY21,ZERO,SP2,SP6,SP3,CMDRG,LADTYPE
              CALL      RDPTLSD1
              CLOSE     PATLSDA1
              BRANCH    OVRCD,DLUM9999        * no lumpsum charge
            ENDIF
          ENDIF
          CLOSE     PATLSDA1
.
          MOVE      PTLSDES1,SAVDES1
          MOVE      PTLSDES2,SAVDES2
DLUM9999  RETURN
+
