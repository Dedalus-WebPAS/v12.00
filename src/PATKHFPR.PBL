.******************************************************************************
.*  File          :  PATKHFPR.PBL                                             *
.*                                                                            *
.*  Function      :  Update Keyword Index File for health Fund Table Search   *
.*                                                                            *
.*  Parameters    :  HCODE,HFNAME,HFTABL,PTHFBCAT                             *
.*                                                                            *
.*  Modifications :                                                           *
.*        V5.08.01  06/06/2000  Jill Habasque                                 *
.*                  Changed from key10 to key14 for the health fund file      *
.*                   V6.06.00  29/06/1999 Pat Dirito                          *
.*                             Created Key Word Procedure                     *
.******************************************************************************
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Procedure to Update
.  a Key Word Index Table that can be used for searching 
.  Masterfiles. The procedure should be called from the 
.  Add, Update and Delete functions in the Masterfile 
.  Maintenance Function.
.  
.  A Key Word Index Table is Defined in the following Manner
.                              
.  PTHFFUN     DIM        6    * Code Field Segment 1
.  PTHFTAB     DIM        8    * Code Field Segment n
.  PTHFKWD     DIM       15    * Key Word
.  PTHFSPA     DIM       10    * Spare
.
.  Index 1   PTHFFUN, PTHFTAB, PTHFKWD
.  Index 2   PTHFKWD, PTHFFUN, PTHFTAB
.
.  To convert this Procedure the following should be performed
.    Global Change sss to the System ID eg :%s/PAT/PAT/g
.    Global Change fff to the File   ID eg :%s/KHF/DKI/g
.    Global Change ssff to the IO Call ID eg :%s/PTHF/PTDK/g
.    Modify the KEYINDEX defintion to combined Code Field Lengths eg 6
.    Global Change KEYxx to the Key for the Key Word Table eg KEY21
.    Change FUNxxx to the Code Fields eg DCODEhh
.    Change the GENR0000 routine to call the BWORD000 for each
.    string of key words to be indexed for the Coded Field
.    eg MOVE    DSNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    DGNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    TDESC,KEYWORDS
.       CALL    BWORD000
.------------------------------------------------------------
          DEFPROC   PATKHFUP
.
          INC       PATKHFFD/INC
.
KEYINDEX  DIM       14        * Code Index in Table
KEYWORDS  DIM       60        * String Containing the Key Words to be Indexed
SIXTY     FORM      "60"
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
ZERO8     INIT      "0000    "
FUNDNAM   DIM       30
TABLENAM  DIM       30
TABLENUM  DIM       8
TABLTYPE  DIM       3 
.------------------------------------------------------------
. Generate Word Index
.------------------------------------------------------------
GENR0000  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     PATKHFA1,"patkhfaf"
          TRAPCLR  IO
          BRANCH   OVRCD,GENR9999
.
          RESET    HCODE,6
          RESET    HCODE
          PACK     KEYINDEX,HCODE,HFTABL        
          CALL     CLEAR000           * Remove Current Key Word Index
          BRANCH   EXIT,GENR9999
.
.   Validate Record on File & get the Health Fund Table name
.
          PACK     KEY14,HCODE,HFTABL,SP70  * Validate Record on File
          CALL     RDFUND1
          BRANCH   OVRCD,GENR1900   * If Not then it Must be a Delete/Clear
          MOVE     HFNAME,TABLENAM    * Saves Table name 
          MOVE     HFTABL,TABLENUM    * Save Fund Table
          MOVE     PTHFBCAT,TABLTYPE  * Save Table Type 
.     
.   Get the Health Fund name 
.
          PACK     KEY14,HCODE,ZERO8,SP70
          CALL     RDFUND1
          BRANCH   OVRCD,GENR1000
          MOVE     HFNAME,FUNDNAM
.
.   Insert Code to Call BWORD000 function for each string to be indexed 
.   The string should be moved into the variable KEYWORDS
.
GENR1000  PACK     KEYINDEX,HCODE,TABLENUM
          PACK     KEYWORDS,HCODE,TABLENUM  
          CALL     BWORD000
.
          MOVE     FUNDNAM,KEYWORDS   * Health Fund Name             
          CALL     BWORD000
.
          MOVE     HCODE,KEYWORDS  * Health Fund Table         
          CALL     BWORD000
.
          MOVE     TABLENAM,KEYWORDS  * Table Description 
          CALL     BWORD000
.
.  Get Health Fund Table Type Category Description.....     
.    
          MATCH    SP70,TDESC
          GOTO     GENR1900 IF EQUAL 
          MOVE     TDESC,KEYWORDS     * Health Fund Table Type
          CALL     BWORD000
.
GENR1900  CLOSE    PATKHFA1
.
GENR9999  GOTO     PATKHIEP
.------------------------------------------------------------
. Clear Current Key Words
.------------------------------------------------------------
CLEAR000  PACK     KEY29,KEYINDEX,SP70
          CALL     RSPTHF1
CLEAR100  CALL     RKPTHF1
          BRANCH   OVRCD,CLEAR999
          MATCH    HCODE,PTHFFUN
          GOTO     CLEAR999 IF NOT EQUAL
          MATCH    HFTABL,PTHFTAB
          GOTO     CLEAR999 IF NOT EQUAL
.
          PACK     KEY29,PTHFFUN,PTHFTAB,PTHFKWD,SP70
          CALL     DEPTHF1
          GOTO     CLEAR100
.
CLEAR999  RETURN
.-------------------------------------------------
. Determine Words to Index
.-------------------------------------------------
BWORD000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      BWORD000 IF NOT EOS
            GOTO      BWORD999               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWORDS,SP30      * reset form pointer
          MOVE      KEY60,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      BWORD100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Save this Word
.
BWORD100  MOVE      KEYWORDS,KEY15
          PACK      KEY15,KEY15,SP70
          REP       UPPLOW,KEY15             * Always Upper Case
          PACK      KEY29,KEYINDEX,KEY15
          UNPACK    KEY29,PTHFFUN,PTHFTAB,PTHFKWD
          CALL      RDPTHF1
          IF        OVRCD=1
            CALL      WRPTHF1
          ENDIF
          GOTO      BWORD190
.
.         Check for any more words
.
BWORD190  SETLPTR   KEYWORDS,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY60,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY60,KEYWORDS
          GOTO      BWORD000
.
BWORD999  RETURN
.
          INC       PATKHFIO/INC
          INC       IBAOVRIO/INC
.
PATKHIEP  ENDPROC                          * End of Procedure
.
