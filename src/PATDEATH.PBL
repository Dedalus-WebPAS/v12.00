
.* System         : ALL                                                       *
.*                                                                            *
.* Procedure Name : PATDEATH.PBL                                              *
.*                                                                            *
.* Function       : To cancel all waiting list entries, inpatient bookings,   *
.*                  future outpatient bookings and close any open Episodes    *
.*                  of Care when someone dies.                                *
.*                                                                            *
.* Author         : Justin Coates                12/03/1993                   *
.*                                                                            *
.* Parameters     : U/R Number PURNO                                          *
.*                  Deceased Indicator PCEASE                                 *
.*                                                                            *
.* Modifications  :                                                           *
.*                                                                            *
.******************************************************************************
.* V12.00.02 09/09/2025  Ebon Clements   TSK 0961969                          *
.*           Added Auto activate waiting VINAH episode referral if it has     *
.*           contacts - ALVA0000                                              *
.* V12.00.01 08/08/2025  Ebon Clements  TSK 0964891                           *
.*           Fixed wattx1af read for ASA score - WESE0000                     *
.* V11.04.03 01/10/2024  Nikitha B      TSK 0948566                           *
.*           Added program 'IT' Infusion therapy at ALLH1000                  *
.* V11.04.02 23/05/2024  Ebon Clements  TSK 0937739                           *
.*           Call IBACLOCK prior to closer referral before audit record       *
.*           write ALLH2000                                                   *
.* V11.04.01 09/01/2024 Thanh T           TSK 0936263                         *
.*           Added OPEN/CLOSE PMSQPXA1 index                                  *
.******************************************************************************
.* V11.03.02 23/10/2023  Nikitha B      TSK 0938566                           *
.*           Made HBD, HEN, TPN, and Medi-Hotel programs closed if these do   *
.*           not have contact instead of Rejected at ALLH1000                 *
.* V11.03.01 10/08/2023  Nikitha B      TSK 0935669                           *
.*           Added WTEEMANU,WTEEPTWT,WTEESGID,WTEERADT,WTEETCMP at CLRWTESE   *
.* V11.01.01 15/09/2021  Ebon Clements  TSK 0910709                           *
.*           Added VINAH reject referrals with no contacts functionality      *
.*           ALLH0000 - ALCNVARR                                              *
.* V11.00.01 11/03/2020  J.Tan          TSK 0838262                           *
.*           Added Effective from and to date to MBS Item file                *
.* V10.14.01 30/08/2019  Davin            TSK 0880684                         *
.*           Added dgclirwl - broadcast HL7 Remove from Waiting List (I14)    *
.******************************************************************************
.* V10.13.01 24/08/2018  Steve Armstrong  TSK 0845563                         *
.*           Mods to call CLALLAUD in WRTRAU00.                               *
.******************************************************************************
.* V10.12.01 05/03/2018  Ebon Clements    TSK 0846913                         *
.*           Populate original booking date/time in OUTCANFD                  *
.******************************************************************************
.* V10.08.03 09/09/2016  Steve Armstrong  TSK 0825587                         *
.*           Added CALL to WRPOL000 for Theatre Booking Cancellation          *
.*           in CNOPR000 (POLLTYPE = 4).                                      *
.* V10.08.02 18/07/2016  Jill Parkinson TSK 0822130                           *
.*           Added CURSTAT before call to CANDEA00                            *
.* V10.08.01 05/05/2016  Steve Armstrong  CAR 0818530                         *
.*           Added RESET  PRGID after SCAN                                    *
.******************************************************************************
.* V10.05.03 17/11/2014  Ebon Clements    CAR 292656                          *
.*           Added link cancelled OP visit to a referral functionality        *
.* V10.05.02 12/09/2014  Patrick Adair    CAR 241461                          *
.*           Do not cancel future bookings of selected deceased patients      *
.* V10.05.01 17/07/2014  Ebon Clements    CAR 251384                          *
.*           Clear sub system key for ibapolaf write                          *
.******************************************************************************
.* V10.03.05 15/10/2013  Davin           CAR 288638                           *
.*           Only trigger a W/L cancel booking in wrpol000 if wmstat > 1      *
.* V10.03.04 14/10/2013  Ania P          CAR 278232                           *
.*           Refactored code - now using COUNTSLT for session recalculation   *
.* V10.03.03 15/04/2013  Steve Armstrong CAR 267329                           *
.*           Mods to trigger a broadcast message (via ibapolaf) if a W/L      *
.*           Booking, an O/P booking or a pre-admission is cancelled.         *
.* V10.03.02 07/11/2012  Steve Armstrong CAR 275865                           *
.*           Mods to not trigger a broadcast message if called from HL7RECVR. *
.* V10.03.01 09/12/2011  Ebon Clements   CAR 246872                           *
.*           Populate new fields in OUTCANFD                                  *
.******************************************************************************
.* V10.02.04 15/09/2011  Ebon Clements CAR 250141                             *
.*           Cancel suspended OP appointments                                 *
.* V10.02.03 06/09/2011  Ebon Clements CAR 242014                             *
.*           Added clinic type to cancelled OP appointment                    *
.* V10.02.02 05/05/2011  Mike Laming   CAR 240720                             *
.*           Added O/P Appointment Requests "outartaf" - at OUTPT700          *
.******************************************************************************
.* V10.00.00 02/02/2010  Steve Armstrong   CAR 135505                         *
.*           Added DGCLII13 trigger (and swapped calls to ALLH0000 and        *
.*           PRMAS000 so that the PMI details will have been read prior to    *
.*           calling DGCLII13).                                               *
.*           Also added HL7TRGID & HL7INCLD setting for all broadcast messages*
.******************************************************************************
.* V9.12.03  09/02/2010  Ebon Clements  CAR 215875                            *
.*           Populate program completion reason on all referrals              *
.* V9.12.02  22/10/2009  Ebon Clements  CAR 176783                            *
.*           Removed AH referral link when cancelling outpatient booking      *
.* V9.12.01  26/05/2009  Ebon Clements  CAR 196248                            *
.*           Added allied health auto program completion reason (SACS)        *
.* V9.11.01  08/01/2009  Ebon Clements  CAR 174080                            *
.*           Added bed board functionlity                                     *
.* V9.09.01  04/01/2008  J.Tan          CAR 135498                            *
.*           Mods for NZ MHINC Extract                                        *
.* V9.05.B01 21/02/2006  Ebon Clements CAR 76341                              *
.*           Added referral and encounter file audits                         *
.* V9.04.08  29/11/2005  Ebon Clements CAR 86062                              *
.*           Set manually added record to no for esis episode file            *
.* V9.04.07  22/11/2005  Peter Vela    CAR 81907                              *
.*           Change patient age PSAGE to date at death                        *
.* V9.04.06  26/09/2005  Ebon Clements CAR 56868                              *
.*           Added passcode to outpatient cancel booking functionality        *
.* V9.04.05  21/09/2005  Jill Habasque CAR 76910                              *
.*           Added check for DOL before 20050701 for WTEEARDT                 *
.* V9.04.04  20/07/2005  Jill Habasque                                        *
.*           Added writes to wateseaf                                         *
.* V9.04.03  13/01/2005  Ebon Clements     CAR 53290                          *
.*           Cancel outpatient referral letters that are before the date of   *
.*           death. Cancel referral letters linked to outpatient bookings     *
.*           that are being cancelled.                                        *
.* V9.04.02  22/10/2004  Ebon Clements     CAR 54126                          *
.*           Fixed open of outpatient files should use OSTFILE not CFILEPRE   *
.* V9.04.01  08/10/2004  Ebon Clements     CAR 54126                          *
.*           Added outpatient session slot count update to cancel bookings    *
.* V9.03.08  24/06/2004  Peter Vela        CAR 50402                          *
.*           Fixed to add an "A" record to wathisaf                           *
.* V9.03.07  18/03/2004  Guomin Fu         CAR 48171                          *
.*           Fixed when making a UR deceased incorrectly removed WL           *
.*           suspension records.                                              *
.* V9.03.06  03/09/2003  Jill Habasque     43010                              *
.*           Changed outpreaf to not be site dependant                        *
.* V9.03.05  20/08/2003  Ebon Clements CAR 41801                              *
.*           Added SCAN0000 to update the series booking status to canc       *
.* V9.03.04  07/08/2003  Jill Habasque CAR 41815                              *
.*           Changed match for opdastat                                       *
.* V9.03.03  16/07/2003  Guomin   CAR 41291                                   *
.*           Fix a bug that "deceased" WL removal reason not written          *
.*           into wattr1af when discharging a patient as deceased             *
.* V9.03.02  20/05/2003  Ebon Clements CAR 39108                              *
.*           Changed CLEOC000 to ignore closed referrals                      *
.* V9.03.01  15/05/2003  Ebon Clements CAR 39108                              *
.*           Changed ECRFCDAT EOC referral close date to be the               *
.*           date of death not the current date                               *
.******************************************************************************
.* V9.02.08  04/02/2003  Lina Vo                                              *
.*           Open outpatient audit file with site prefix                      *
.* V9.02.07  03/12/2002  Lina Vo                                              *
.*           Changed program to open outpatient files with site prefix        *
.* V9.02.06  20/08/2002  Ebon Clements SRF 20123                              *
.*           Changed to call CNOPR000 only if bkrestat is < 2                 *
.* V9.02.05  02/02/2002  Ebon Clements                                        *
.*           Added WATHI002 cgi for waiting list history file                 *
.* V9.02.04  24/01/2002  Ebon Clements                                        *
.*           Added ADDWLFLG for waiting list history file                     *
.* V9.02.03  09/11/2001  Ebon Clements                                        *
.*           Added cancel for Allied Health Transaction                       *
.* V9.02.02  02/11/2001  Ashwin                                               *
.*           Move spaces to Schedule Admission Date from removed from         *
.*           waiting list                                                     *
.* V9.02.01  22/10/2001  Ashwin - HPS                                         *
.*           Write record into History file after removal from waiting        *
.*           list due to patient death                                        *
.******************************************************************************
.*        V5.09.B01 01/02/2001  Greg Horvat                                   *
.*                  Removed a shit lot of variables from this procedure cause *
.*                  IBAOUT66 couldnt compile                                  *
.*        V5.08.03  06/10/2000  Jill Habasque SRF 647323                      *
.*                  Changed to call CNOPR000 even if bkrestat is >2           *
.*        V5.08.02  26/07/2000  Jill Habasque                                 *
.*                  Added WATPROFD & WATNBRFD for WLHISSUB                    *
.*        V5.08.01  16/05/2000  Katie Nelson                                  *
.*                  Added Flag so a keyin is not called during WEB programs   *
.*        V5.08.B04 19/04/2000  Greg Horvat                                   *
.*                  Changed to define the ICD operation file variables in this*
.*                  procedure                                                 *
.*        V5.08.B03 13/04/2000  Jill Habasque SRF 646660                      *
.*                  Mods to replace spaces with zero for ECRFCDAT             *
.*        V5.08.B02 30/03/2000  Greg Horvat      SRF 646507                   *
.*                  Recompiled for OPRDEA01 - Changed to open the bokrc1af    *
.*                  file before reading it                                    *
.*        V5.08.B01 16/02/2000  Katie Nelson                                  *
.*                  Added flag so that display statements are not executed    *
.*                  through WEB programs                                      *
.******************************************************************************
.*        V5.05.01  08/01/1998  Steve Armstrong    SRF 643822                 *
.*                  When using EOC, remove any outrf1af records that are      *
.*                  not related to EOC's                                      *
.******************************************************************************
.*        V5.04.02  23/10/1996  Howellsy         EOC                          *
.*                  Close any Open EOC for the Patient.                       *
.*        V5.04.01  17/06/1996  Howellsy         SRF 641485                   *
.*                  Fix I*A on bokcanaf from IBAPAT34.                        *
.*        V5.03.01  02/10/1995  Max White        SRF 441225 / Quote 440056    *
.*                  Record transport allocation (cat OB) in outcanaf when     *
.*                  cancelling outpatient bookings.                           *
.*        V5.03.B1  18/05/1995  Greg Horvat      SRF 134879  (Rebound)        *
.*                  Recompiled for OPR001FD                                   *
.******************************************************************************
.*        V5.01.02  24.03.93 Neeriem "I Hate Support" Dye (I.B.A.)            *
.*                  Fix to write site code to OTBBSITE                        *
.*        V5.01.03  01.04.93 Neeriem "I Hate Support" Dye (I.B.A.)            *
.*                  Delete Outpatient Referral Letters                        *
.*                                                                            *
.*        V5.01.01  15/09/93  ROD                                             *
.*                  Made a separate program (used by FORKing to it)           *
.*        V5.01.02  20/01/1994    Justin Coates                               *
.*                  Changed back to a procedure                               *
.*        V5.01.03  07/03/1994  Steve Armstrong                               *
.*                  Removed PATDSCOD include (now in PATCODKY)                *
.*                                                                            *
.******************************************************************************
.
          DEFPROC   PATDEATH
.
          INC       ALLAEFFD/INC
          INC       ALLCONFD/INC            * allied health control file
          INC       ALLENCFD/INC
          INC       ALLERCFD/INC
          INC       ALLLNKFD/INC            * allied health outpatient link
          INC       ALLQUEFD/INC
          INC       BOKCANFD/INC            * bookings cancellation file
          INC       BOKSTAFD/INC            * bookings stats file
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
          INC       IBAPOLFD/INC
          INC       OPR001FD/INC            * theatre variable defns
          INC       OPRDEAFD/INC            * theatre file
          INC       OPRDEBFD/INC            * theatre team details file
          INC       OPRDECFD/INC            * theatre usage details file
          INC       OPRSESFD/INC            * session file
          INC       OPRSIUFD/INC            * special item usage
          INC       OUTSITFD/INC            * site file
          INC       OUTBOAFD/INC            * bookings file
          INC       OUTBB1FD/INC            * bookings file
          INC       OUTBOCFD/INC            * bookings file
          INC       OUTCANFD/INC            * cancellation file
          INC       OUTDIAFD/INC            * cancellation file
          INC       OUTRF1FD/INC            * outpatient bookings
          INC       OUTARTFD/INC            * outpatient Appointment Request
          INC       PATICOFD/INC
          INC       PATCALAG/INC
          INC       PATDHEAD/INC
          INC       PMSEPBFD/INC
          INC       PMSQPTFD/INC
          INC       WATCATFD/INC            * W/L cat. change file
          INC       WATCONFD/INC
          INC       WATTR1FD/INC            * W/L treatment file
          INC       WATQUEFD/INC
          INC       WATRTDFD/INC
          INC       WATTX1FD/INC            * W/L treatment file
          INC       WATESEFD/INC            * W/L treatment file
          INC       WATHISFD/INC            * W/L treatment file
          INC       WATNBRFD/INC            * W/L nbrs file
          INC       WATPROFD/INC            * W/L procedure file
          INC       OPRHISFD/INC            * W/L treatment file
          INC       OUTHISFD/INC            * W/L treatment file
          INC       WATSUSFD/INC            * W/L suspension file
          INC       EOCREFFD/INC            * EOC Referral File   
          INC       EOCLNKFD/INC            * EOC Link File   
          INC       PATCODFD/INC            * Category code file
          INC       ALLREFFD/INC            * Allied Health Referrals
          INC       ALLGRPFD/INC            * Group bookings
          INC       ALLRLNFD/INC            * Referral links
          INC       ALLSTSFD/INC            * Referral Status History
          INC       ALLAUDFD/INC            * Referral Audit Table
          INC       OUTLKBFD/INC            * Outpatient linked bookings
          INC       OUTSESFD/INC            * Outpatient Sessions          
          INC       OUTMA1FD/INC            * Outpatient Clinic Masters
     
.
ADDWLFLG  FORM      1
WATHI002  DIM       8         * Date of change
AUDITKEY  DIM       24        * Audit Key
CHECKREF  DIM       8         * Auto activate internal referral
CISMFLAG  FORM      1
CTHETR    FORM      1         * using theatre ??
FORM10    FORM      10
HBWAIT    FORM      1         * using W/L ??
HLOGCANL  FORM      1         * log OP cancellations ??
HOAUDA    FORM      1         * outboafd audits
HOAUDB    FORM      1         * outbobfd audits
HOUR      FORM      2         * Hour Variable
IBAMFLAG  FORM      1
IDESC     DIM       1         * so it will compile
LNKAHREF  DIM       8         * Linked AH referral number
LOCKCNT   FORM      2         * Lock counter
MASTREFN  DIM       8         * Master referral number
MESSAGID  DIM       20
MIN       DIM       2         * Minutes Variable
MINUTES   FORM      6         * Minutes Variable
OPCNAUDB  FORM      1         * oprdeafd audits
OPCNAUDS  FORM      1         * oprsesfd audits
OPCNCODE  FORM      1         * so it will compile
OPCNFULL  FORM      3         * session full percentage
OPCNIDFL  FORM      1         * so it will compile
OPCNNPRV  FORM      1         * so it will compile
OPCNODSC  DIM       15        * so it will compile
OPCNSPET  FORM      1         * special items
OPCNUDSC  FORM      1         * so it will compile
OPCNUNIQ  FORM      10        * so it will compile
OTCNRDTH  DIM       3         * code for referral cancellation
OTCNSQO2  FORM      1
OTCNOPCR  FORM      1
POLLTYPE  FORM      1         * polling type
.                                 1 = W/L cancel booking
.                                 2 = O/P cancel booking
.                                 3 = Cancel pre-admission
.                                 4 = Cancel theatre booking
REJECTRF  FORM      1         * Reject nstead of close referral
SACSFLAG  FORM      1         * Precessing SACS master flag
SAVIADAT  DIM       8         * Save internal ref date active
SAVTXWRD  DIM       3
SAVKEY16  DIM       16
SUPRSQST  FORM      1
PTCNBDTH  DIM       3         * code for bkg cancellation
PTCNIDTH  DIM       3         * code for i/p cancellation
PTCNODTH  DIM       3         * code for o/p cancellation
PTCNTDTH  DIM       3         * code for opr cancellation
PTCNWDTH  DIM       3         * code for w/l cancellation
CODE      DIM       2
CPTDATE   DIM       8
DIM3      DIM       3
SAVEREFN  DIM       8         * Saved referral number
SESSION   DIM       28
SLOTOPT   DIM       3
TILDA10   INIT      "~~~~~~~~~~"
UPDTTYPE  DIM       2         * VINAH audit update type
VSESS     DIM       25        * OP session
VSLOT     DIM       3         * OP slot
.ALCNCLCD  DIM       3         * Allied Health Reason for closure (CAT LL)
.
.*********************************************************************
.         mainline code
.*********************************************************************
PTDTH000  COMPARE   ONE,PCEASE
          GOTO      PTDTH999 IF NOT EQUAL   * not dead
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,TWENTY1;*101,FORM1   * ptcnudrg
          COMPARE   ONE,FORM1
          GOTO      PTDTH999 IF NOT EQUAL   * not using death registration
.
          READ      CONTROLF,TEN5;*189,CTHETR
          READ      CONTROLF,TWENTY1;*102,PTCNWDTH,PTCNIDTH,PTCNODTH:
                                          PTCNBDTH,PTCNTDTH
          READ      CONTROLF,TWENTY7;*46,HBWAIT
          READ      CONTROLF,THIRTY1;*51,HOAUDA,HOAUDB,*124,HLOGCANL:
                                     *185,OTCNSQO2
          READ      CONTROLF,THIRTY2;*38,OTCNOPCR,*112,OTCNRDTH
          READ      CONTROLF,THIRTY7;*44,HWAUDB
          READ      CONTROLF,FORTY;*44,OPCNAUDB,*61,OPCNAUDS,*81,OPCNODSC
          READ      CONTROLF,FORTY2;*48,OPCNFULL
          READ      CONTROLF,SEVENTY9;*120,PTCNUEOC
          READ      CONTROLF,EIGHTY;*250,PTCNHDPS
.
          READ      CONTROLF,HUND06;*1,ALCNCLCD,*27,ALCNRAUD,*28,ALCNEAUD:
                                    *214,ALCNPRGC
          READ      CONTROLF,HUND21;*129,ALCNVARR
.
          PACK      TODAY,CCC,CYY,CMM,CDD
          REP       " 0",TODAY
.
          CALL      PRMAS000                * update patient age at death
          CALL      WLIST000                * clean up for wattrefd
          CALL      BOOKG000                * clean up for bokrecfd 
          CALL      PRMIS000                * clean up for patmisfd 
          CALL      OUTPT000                * clean up for outpatients
          CALL      CLEOC000                * close Episodes of Care
          CALL      ALLH0000                * clean up Allied Health
          GOTO      PTDTH999
.
.*********************************************************************
.         cancell all unscheduled, scheduled, pre-admitted W/L records
.*********************************************************************
WLIST000  COMPARE   ONE,HBWAIT
          GOTO      WLIST999 IF NOT EQUAL   * not using W/L
.
          SCAN      "WEB",PRGID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,"Cancelling waiting list details...";
          ENDIF
          RESET     PRGID
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATRTDA1,"watrtdaf"
          OPEN      WATTX1A1,"wattx1af"
          OPEN      WATESEA1,"wateseaf"
          OPEN      PATCODE1,"patcodes"
          OPEN      WATCATA1,"watcataf"
          OPEN      WATSUSA1,"watsusaf"
          IF        HWAUDB = ZERO
            OPEN      WATAUTR1,"watautr1"
          ENDIF
          PACK      KEY19,PURNO,SP20
          CALL      RDSTREA1
.
WLIST100  CALL      RDKTREA1
          BRANCH    OVRCD,WLIST500          * end of file
          MATCH     PURNO,WMURNO
          GOTO      WLIST500 IF NOT EQUAL   * diff. patient
.
          PACK      KEY19,WMURNO,WMCODE,WMCNT
          CALL      RDWTTX11
.
          COMPARE   FOUR,WMSTAT
          GOTO      WLIST100 IF NOT LESS    * not correct status
.
          CALL      SAVHIS00                * HPSI
.
          SCAN      "WEB",PRGID
          IF        !@EQUAL
            DISPLAY   *P41:24,*V2LON,WMBOOK
          ENDIF
          RESET     PRGID
          IF        HWAUDB = ZERO
            CALL      IBACLOCK           * get current date
            PACK      WTWMAUDD,CCC,CYY,CMM,CDD
            REP       " 0",WTWMAUDD
            CLOCK     TIME,WTWMAUDT           * clock current time
            MOVE      PORT,WTWMAUDP           * get port number
            MOVE      ANSB,WTWMAUDR           * set record type
            MOVE      ONE,WTWMAUDS            * set print status
            MOVE      PASSCODE,WTWMAUDO       * get user ID
            PACK      KEY19,WTWMAUDD,WTWMAUDT,WTWMAUDP,WTWMAUDR  
            CALL      AWTREA1            * write to audit file
          ENDIF
.
          MOVE      "20",WTWMBSCD
          MOVE     SP70,WMDATE3
          PACK     WMDATE4,CCC,CYY,CMM,CDD
          REP      " 0",WMDATE4
          MOVE     PTCNWDTH,WMREMOVE
          MOVE     WMSTAT,F1                * save wmstat (CAR 288638)
          MOVE     SIX,WMSTAT               * set to cancelled
          CALL     UPTREA1
.
          MATCH     "HL7RECVR",PRGID
          IF        !@EQUAL
            CALL      PMIGTNID              * get national id for dgate write
            MOVE      NMPNUMB,PTNINMPI
            MOVE      TWO,HL7TRGID
            MOVE      "PATDEATH",HL7INCLD
            CALL      DGCLIRWL              * Remove Waiting List Entry (I14)
          ENDIF
.
          IF       F1 > 1
            MOVE     ONE,POLLTYPE             * set for W/L type (if scheduled)
            CALL     WRPOL000                 * trigger HL7 cancellation message
          ENDIF
.
          CALL     WESE0000
.
          IF        HWAUDB = ZERO
            MOVE      ANSC,WTWMAUDR           * set record type
            PACK      KEY19,WTWMAUDD,WTWMAUDT,WTWMAUDP,WTWMAUDR  
            CALL      AWTREA1            * write to audit file
          ENDIF
.
. Write record into History File
.
          MOVE     ZERO,ADDWLFLG
          MOVE     Z70,WATHI002
          MOVE     ONE,NBRFLAG                                          *I-50402
          CALL     WRTHIS00                 * HPSI
.
.         write a record to the category change file
.
          MOVE     WMDATE4,WTCAEDAT
          MOVE     "WR",WTCACATF
          MOVE     WMURNO,WTCAURNO
          MOVE     WMCODE,WTCAPROC
          MOVE     WMCNT,WTCAPCNT
          PACK     KEY29,WTCAEDAT,WTCACATF,WTCAURNO,WTCAPROC,WTCAPCNT 
          CALL     RDWTCAT1
.
          MOVE     PTCNWDTH,WTCACODT        * set to code
          MOVE     SP3,WTCACODF             * no from code
.
          IF       OVRCD = ONE
            CALL     WRWTCAT1
          ELSE
            CALL     UPWTCAT1
          ENDIF
          GOTO      WLIST100
.
.         delete all suspensions for the UR number
..... D CAR 48171 - When patient died, the suspension records should still be
....                kept.
.
WLIST500  
....      PACK      KEY21,PURNO,SP30
....      CALL      RSWTSUS1
....      CALL      RKWTSUS1
....      BRANCH    OVRCD,WLIST900
....      MATCH     PURNO,WTSUURNO
....      GOTO      WLIST900 IF NOT EQUAL   * diff. pat
.
....      PACK      KEY21,WTSUURNO,WTSUCODE,WTSUCNT,WTSUSCNT
....      CALL      DEWTSUS1
....      GOTO      WLIST500
.
WLIST900  CLOSE     WATTR1A1
          CLOSE     WATCATA1
          CLOSE     WATSUSA1
          IF        HWAUDB = ZERO
            CLOSE     WATAUTR1
          ENDIF
.
WLIST999  RETURN
+
.*********************************************************************
.         cancel for all bookings and pre-admissions and theatre
.*********************************************************************
BOOKG000  OPEN      BOKRC1A6,"bokrc1af"
          OPEN      PATDRGA2,"patdrgaf"
          OPEN      BOKBSTA1,"bokstaaf"
          OPEN      BOKCANA1,"bokcanaf"
          IF        CTHETR = ONE
            OPEN      OPRDETA1,"oprdetaf"
            OPEN      OPRDETA2,"oprdetaf"
            OPEN      OPRSESA1,"oprsesaf"
            IF        OPCNAUDB = ZERO
              OPEN      OPRAUDDA,"opraudda"
            ENDIF
            IF        OPCNAUDS = ZERO
              OPEN      OPRAUDSE,"opraudse"
            ENDIF
          ENDIF
.
          SCAN      "WEB",PRGID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,"Cancelling Booking details...";
          ENDIF
          RESET     PRGID
.
          PACK      KEY16,PURNO,SP8
          CALL      RSBKREC6
.
BOOKG100  CALL      RKBKREC6
          BRANCH    OVRCD,BOOKG900
          MATCH     PURNO,BKREURNO
          GOTO      BOOKG900 IF NOT EQUAL
.
          COMPARE   TWO,BKRESTAT
          GOTO      BOOKG100 IF NOT LESS    * not valid to cancel
.
          PACK      KEY5,ANSC,ANSC,BKREEATY,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MATCH     "D",TCINDC12
            IF        @EQUAL
              GOTO      BOOKG100            * do not cancel booking
            ENDIF
          ENDIF
.
          CALL      CNOPR000                * cancel for theatre for booking #
.
          SCAN      "WEB",PRGID
          IF        !@EQUAL
            DISPLAY   *P31:24,*V2LON,BKREBOOK;
          ENDIF
          RESET     PRGID
          IF        BKRESTAT = ZERO
            CALL      BSTAT000                * update booking stats
          ENDIF
. 
. cancel the booking record
.
          MOVE      TWO,BKRESTAT            * set to cancelled
          MOVE      PTCNBDTH,BKRECANC       * set cancellation reason
          CALL      UPBKREC6
.
          CALL      BKCL0000                * Clear bed board
          GOTO      BOOKG100
.
BOOKG900  CLOSE     BOKBSTA1
          CLOSE     BOKCANA1
          IF        CTHETR = ONE
            CLOSE     OPRDETA1
            CLOSE     OPRDETA2
            CLOSE     OPRSESA1
            IF        OPCNAUDB = ZERO
              CLOSE     OPRAUDDA
            ENDIF
            IF        OPCNAUDS = ZERO
              CLOSE     OPRAUDSE
            ENDIF
          ENDIF
.
BOOKG999  RETURN
+
.*********************************************************************
.         Close any Open EOC                     
.*********************************************************************
CLEOC000  COMPARE   ONE,PTCNUEOC
          GOTO      CLEOC999 IF NOT EQUAL
.
          SCAN      "WEB",PRGID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,"Closing Episodes of Care ..........";
          ENDIF
          RESET     PRGID
.
          OPEN      EOCREFA1,"eocrefaf"
.
          PACK      KEY13,PURNO,SP20
          CALL      RSECREF1
CLEOC100  CALL      RKECREF1
          BRANCH    OVRCD,CLEOC999
.
          MATCH     PURNO,ECRFURNO
          GOTO      CLEOC999 IF NOT EQUAL
.
          MOVE      ONE,ECRFSTAT
          MATCH     SP70,PDECDTE                 
          IF        @EQUAL
            CALL      IBACLOCK                   * Use todays date if unknown
            PACK      ECRFCDAT,CCC,CYY,CMM,CDD   * date of death
            REP       " 0",ECRFCDAT
          ELSE
            MOVE      PDECDTE,ECRFCDAT           * close with date of death
          ENDIF
.
          CALL      UPECREF1
.
          GOTO      CLEOC100
.
CLEOC999  RETURN
.*********************************************************************
.         cancel all pre-admissions for patient
.*********************************************************************
PRMIS000  SCAN      "WEB",PRGID
          IF        !@EQUAL 
            DISPLAY   *P1:24,*EL,"Cancelling Pre-Admission details...";
          ENDIF
          RESET     PRGID
          OPEN      PATVISA2,"patvisaf"
          OPEN      PATMI1A1,"patmi1af"
          PACK      KEY24,PURNO,SP20
          CALL      RDSVISA2
.
PRMIS100  CALL      RDKVISA2
          BRANCH    OVRCD,PRMIS999
          MATCH     PURNO,PVIURNO
          GOTO      PRMIS999 IF NOT EQUAL   * diff. pat
.
          MOVE      PVIBILL,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,PRMIS100
.
          PACK      KEY5,ANSC,ANSC,ACARE,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MATCH     "D",TCINDC12
            IF        @EQUAL
              GOTO      PRMIS100              * do not cancel preadmission
            ENDIF
          ENDIF
.
          IF        ASTAT = ONE
            MOVE      PTCNIDTH,ACANCEL        * set cancel code
            MOVE      FIVE,ASTAT              * set to cancelled
            CALL      UPMISS1
.
            MOVE      THREE,POLLTYPE          * set for Pre-admission type
            CALL      WRPOL000                * trigger HL7 cancellation message
.
            CALL      PRCL0000                * Clear bed board
          ENDIF
          GOTO      PRMIS100
.
PRMIS999  RETURN
+
.*********************************************************************
.         update the outpatient files
.*********************************************************************
OUTPT000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OUTSITA1,"outsitaf"
          TRAPCLR   IO
          BRANCH    OVRCD,OUTPT999          * no site file
.
          SCAN      "WEB",PRGID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,"Cancelling Outpatient bookings...";
          ENDIF
          RESET     PRGID
          MOVE      SP6,KEY6
          CALL      RDSSITA1
.
. loop over each site record
.
OUTPT100  CALL      RDKSITA1
          BRANCH    OVRCD,OUTPT600
.
          OPEN      OUTPREA1,"outpreaf"    
.
. open third index of outboafd
.
          CLOSE     OUTBOKA3
          PACK      CFNAME,OSTFILE,FILBOKA1
          OPEN      OUTBOKA3,CFNAME
          CLOSE     OUTBOKA1
          PACK      CFNAME,OSTFILE,FILBOKA1
          OPEN      OUTBOKA1,CFNAME
          CLOSE     OUTLKBA1
          PACK      CFNAME,OSTFILE,FILLKBA1
          OPEN      OUTLKBA1,CFNAME
          CLOSE     OUTLKBA2
          PACK      CFNAME,OSTFILE,FILLKBA2
          OPEN      OUTLKBA2,CFNAME
          PACK      CFNAME,OSTFILE,FILSESA1  
          CLOSE     OUTSESA1
          OPEN      OUTSESA1,CFNAME           
          PACK      CFNAME,OSTFILE,FILMA1A1  
          CLOSE     OUTMA1A1
          OPEN      OUTMA1A1,CFNAME           
.
          IF        HOAUDA = ZERO     
            PACK      CFNAME,OSTFILE,FILAUDBA
            OPEN      OUTAUDBA,CFNAME    
          ENDIF
.
          OPEN      OUTRF1A1,"outrf1af"           * outpatient bookings
          OPEN      OUTRF1A2,"outrf1af"           * outpatient bookings
.
          CLOSE     OUTBB1A1
          PACK      CFNAME,OSTFILE,FILBB1A1
          CALL      OPOTBB11
          IF        HOAUDB = ZERO
            PACK      CFNAME,OSTFILE,FILAUBB1
            OPEN      OUTAUBB1,CFNAME    
          ENDIF
.
          CLOSE     OUTBOKC1
          PACK      CFNAME,OSTFILE,FILBOKC1
          OPEN      OUTBOKC1,CFNAME
.
          CLOSE     OUTDIAG1
          PACK      CFNAME,OSTFILE,FILDIAG1
          OPEN      OUTDIAG1,CFNAME
.
          CLOSE     OUTCANA1
          PACK      CFNAME,OSTFILE,FILCANA1
          OPEN      OUTCANA1,CFNAME
.
.---- delete data ----
.
          IF        PTCNUEOC = 1
            OPEN      EOCREFA1,"eocrefaf"
            OPEN      EOCLNKA1,"eoclnkaf"
          ENDIF
          PACK      KEY24,PURNO,SP30     * start at URNO
.
OUTPT150  CALL      RSOTRFL2
          CALL      RKOTRFL2
          BRANCH    OVRCD,OUTPT190          * end of file
.
          PACK      KEY24,OTRLURNO,OTRLDATE,OTRLOUTN,SP30
          MATCH     PURNO,OTRLURNO
          GOTO      OUTPT190 IF NOT EQUAL   * diff. patient
.
          MATCH     SP70,OTRLBSIT           * Skip if booked
          GOTO      OUTPT150 IF NOT EQUAL
.
          MATCH     SP70,OTRLCDTE           * Skip cancelled
          GOTO      OUTPT150 IF NOT EQUAL
.
.         Check if we are using EOC and if so, we need to check if
.         this referral record is linked to an EOC.  If it is then we
.         can't delete it.
.
          IF        PTCNUEOC = 1
            PACK      KEY13,PURNO,SP20
            CALL      RSECREF1                   * position in file for U/R
OUTPT155    CALL      RKECREF1                   * read next record
            BRANCH    OVRCD,OUTPT160             * eof - ok to delete
.
            MATCH     PURNO,ECRFURNO             * same U/R still ?
            GOTO      OUTPT160 IF NOT EQUAL      * no - no eoc on file
.
            MATCH     ECRFREFN,SOTRLOTN          * same O/P number ?
            GOTO      OUTPT150 IF EQUAL          * yes - don't delete
.
            GOTO      OUTPT155
          ENDIF
.
OUTPT160  MATCH     PDECDTE,OTRLDATE                 * book. date < death date
          IF        @LESS
            PACK      OTRLCDTE,CCC,CYY,CMM,CDD       * Cancel referral
            REP       " 0",OTRLCDTE
            MOVE      OTCNRDTH,OTRLREAS
            MOVE      SP70,OTRLBSIT
            MOVE      SP70,OTRLBCLI
            MOVE      SP70,OTRLBDTE
            MOVE      SP70,OTRLBSTR
            MOVE      SP70,OTRLBSLT
            CALL     UPOTRFL2
          ELSE
            CALL      DEOTRFL2
          ENDIF 
.
.         This next code is only here because Big Jim (Featherstone) is still
.         running using the Outpatient Referral Y/N question in EOC, so there
.         may be a referral record in the eoc link file which can be deleted
.         without affecting the original EOC referral.
.
          IF        PTCNUEOC = 1
            PACK      KEY23,PURNO,SP30
OUTPT165    CALL      RSECLNK1                   * position in file for U/R
OUTPT170    CALL      RKECLNK1                   * read next record
            BRANCH    OVRCD,OUTPT150             * eof - finished
.
            MATCH     PURNO,ECLNURNO             * same U/R still ?
            GOTO      OUTPT150 IF NOT EQUAL      * no - finished
.
            MATCH     ECLNVISI,SOTRLOTN          * same visit number ?
            GOTO      OUTPT170 IF NOT EQUAL      * no - ignore
.
            PACK      KEY23,ECLNURNO,ECLNEPNO,ECLNHOSP,ECLNVISI
            CALL      DEECLNK1                   * delete record
            GOTO      OUTPT165                   * get next record
          ENDIF
          GOTO      OUTPT150
.
OUTPT190  PACK      KEY36,PURNO,PDECDTE,SP30
          CALL      RDSBOKA3
.
OUTPT200  CALL      RDKBOKA3
          BRANCH    OVRCD,OUTPT100          * end of file
          MATCH     PURNO,OBAURNO
          GOTO      OUTPT100 IF NOT EQUAL   * diff. patient
.
          MATCH     PDECDTE,OBADATE
          GOTO      OUTPT200 IF LESS        * book. date < death date
          MATCH     OSTSITE,OBASITE
          GOTO      OUTPT200 IF NOT EQUAL   * diff. site
.
          COMPARE   ONE,OBASTAT               * booked
          IF        !@EQUAL
            COMPARE   SIX,OBASTAT
            GOTO      OUTPT200 IF NOT EQUAL   * suspended
          ENDIF
.
          PACK      KEY5,ANSC,ANSC,OTBBCART,SP70
          CALL      RDCODE1
          IF        OVRCD=0
            MATCH     "D",TCINDC12
            IF        @EQUAL
              GOTO      OUTPT200              * do not cancel booking
            ENDIF
          ENDIF
.
          SCAN      "WEB",PRGID
          IF        !@EQUAL
            DISPLAY   *P40:24,*V2LON,OBAOUTNO
          ENDIF
          RESET     PRGID
          PACK      VSESS,OBASITE,OBACLIN,OBADATE,OBASTRT
          MOVE      OBASLOT,VSLOT
.
          MOVE      OBAOUTNO,KEY8
          CALL      RDBOKB1                 * read booking details
          MOVE      OBAOUTNO,KEY8
          CALL      DEBOKB1                 * delete booking details
          MOVE      FOUR,AUDIFLAG
          CALL      AUDOTBOB
.
          CALL      SUBBOK00                * decrease booked count
.
          MOVE      OBAOUTNO,KEY8
          CALL      DEOTBOC1                * delete booking details
.
          MOVE      OBAOUTNO,KEY8
          CALL      DEDIAG1                 * delete diagnosis details
.
          MOVE      OBAOUTNO,KEY8
          CALL      DEPREA1                 * delete pre-attendance details
.
          CALL      DELT0000                * delete from history
.
          PACK      KEY8,OBAOUTNO,SP70
          CALL      RDOTRFL1                * Cancel referral letter
          IF        OVRCD=0
            PACK      OTRLCDTE,CCC,CYY,CMM,CDD
            REP       " 0",OTRLCDTE
            MOVE      OTCNRDTH,OTRLREAS
            MOVE      SP70,OTRLBSIT
            MOVE      SP70,OTRLBCLI
            MOVE      SP70,OTRLBDTE
            MOVE      SP70,OTRLBSTR
            MOVE      SP70,OTRLBSLT
            CALL     UPOTRFL1
          ENDIF
.
          CALL      SCAN0000                * Cancel series booking record
.
          CALL      DLNK0000                * Delete referral link
.
. keep a log of the cancellation
.
          COMPARE   ONE,HLOGCANL
          GOTO      OUTPT400 IF NOT EQUAL   * not keeping log of cancellation
.
          MOVE      OBAOUTNO,KEY8            * Key for file
          CALL      RDOTCAN1
.
          MOVE      OBASITE,OPCNSITE         * Site
          MOVE      OBACLIN,OPCNCLIN         * Clinic id
          MOVE      OBADATE,OPCNDATE         * Booking date
          MOVE      OBASTRT,OPCNSTRT         * Clinic start time
          MOVE      OBASLOT,OPCNSLOT         * Booking slot number
          MOVE      OBATIME,OPCNTIME         * Booking slot time
          MOVE      OBAOUTNO,OPCNOUTN        * Outpatient number
          MOVE      OBAURNO,OPCNURNO         * Site
          PACK      OPCNRDTE,CCC,CYY,CMM,CDD * Cancellation date
          REP       " 0",OPCNRDTE            * Zero fill cancellation date
          CLOCK     TIME,OPCNRTIM            * Cancellation time
          MOVE      OBASRCR,OPCNSRCR         * Source of referral
          MOVE      OBACOMP,OPCNCOMP         * Compensation eligibilty
          MOVE      OBACLASS,OPCNCLAS        * Patient classification
          MOVE      OBAINS,OPCNINS           * U1 user defined field
          MOVE      OBACAT,OPCNCAT           * Patient category
          MOVE      OBAPRTY,OPCNPRTY         * Priority code
          MOVE      OTBBTRAN,OPCNTRAN        * Transport Allocation (cat OB)
          MOVE      PTCNODTH,OPCNREAS        * reason
          MOVE      PASSCODE,OPCNOPER        * Passcode for operator
          MOVE      SP30,OPCNSPAR            * Spare variable
.
          MOVE      OBACTYP,OPCNCTYP         * Clinic Type
          MOVE      OTBBNMDS,OPCNNMDS        * NMDS Clinic (Cat NM)
          MOVE      OTBBSRVD,OPCNSRVC        * Service Delivery (Cat sd)
          MOVE      OTBBCART,OPCNCART        * Care Type (Cat CC)
          MOVE      OBABKDTE,OPCNBKDT        * Book Date/Time
.
          IF        OVRCD = ONE
            MOVE      LNKAHREF,OPCNREFN        * linked AF referral number
            CALL      WROTCAN1                 * Write the cancellation record
          ELSE
            CALL      UPOTCAN1                 * Update the cancellation record
          ENDIF
.
OUTPT400  CALL      CANSL000                * cancel the slot
          GOTO      OUTPT200
.
.
OUTPT600  CLOSE     OUTSITA1
          CLOSE     OUTBOKA3
          CLOSE     OUTBOKA1
          CLOSE     OUTBB1A1
          CLOSE     OUTBOKC1
          CLOSE     OUTDIAG1
          CLOSE     OUTCANA1
          CLOSE     OUTLKBA1
          CLOSE     OUTLKBA2
          IF        PTCNUEOC = 1
            CLOSE     EOCREFA1
            CLOSE     EOCLNKA1
          ENDIF
          IF        HOAUDA = ZERO     
            CLOSE     OUTAUDBA
          ENDIF
          IF        HOAUDB = ZERO
            CLOSE     OUTAUBB1
          ENDIF
.
OUTPT700  OPEN      OUTARTA1,"outartaf"     * modify Appointment Request table
.
          PACK      KEY32,PURNO,SP70
          CALL      RSOTART1
OUTPT720  CALL      RKOTART1                * read Appointment Request record
          BRANCH    OVRCD,OUTPT900
.
          MATCH     PURNO,OTARURNO
          GOTO      OUTPT900 IF NOT EQUAL
.
          MATCH     "3",OTARSTAT
          GOTO      OUTPT720 IF EQUAL       * read next if not cancelled
.
          MOVE      "3",OTARSTAT            * cancel this App. Request
          MOVE      PTCNODTH,OTARRCAN
          PACK      OTARCADT,CCC,CYY,CMM,CDD * Cancellation date
          REP       " 0",OTARCADT
          MOVE      CTIMEIS,OTARCATM
          MOVE      WBSEUID,OTARCAUS
          CALL      UPOTART1
          GOTO      OUTPT720
.
OUTPT900  CLOSE     OUTARTA1
.
OUTPT999  RETURN
.**********************************************************************
.*  SCAN0000  - Update the status of a series booking to cancelled    *
.*              OTLKBSTA  1=Cancelled  0=Not Cancelled                *
.**********************************************************************
SCAN0000  PACK      KEY24,URNUMBER,OBAOUTNO,SP70 * Is this a master record
          CALL      RDOTLKB1
          BRANCH    OVRCD,SCAN1000
.
          MOVE      ONE,OTLKBSTA
          PACK      OTLKUDAT,CCC,CYY,CMM,CDD * current date
          REP       " 0",OTLKUDAT
          MOVE      CTIMEIS,OTLKUTIM
          MOVE      WBSEUID,OTLKUUID
          CALL      UPOTLKB1
          GOTO      SCAN9999
.
SCAN1000  PACK      KEY24,URNUMBER,OBAOUTNO,SP70 * Check non master records
          CALL      RSOTLKB2
          CALL      RKOTLKB2
          BRANCH    OVRCD,SCAN9999
.
          MATCH     URNUMBER,OTLKURNO
          GOTO      SCAN9999 IF NOT EQUAL
.
          MATCH     DOBAOUTN,OTLKLBOK
          GOTO      SCAN9999 IF NOT EQUAL
.
          MOVE      ONE,OTLKBSTA
          PACK      OTLKUDAT,CCC,CYY,CMM,CDD * current date
          REP       " 0",OTLKUDAT
          MOVE      CTIMEIS,OTLKUTIM
          MOVE      WBSEUID,OTLKUUID
          CALL      UPOTLKB2
.
SCAN9999  RETURN
+
.*******************************************************************************
. DLNK0000 - Delete allied health referral letter link
.*******************************************************************************
DLNK0000  OPEN      ALLLNKA1,"alllnkaf"
          OPEN      ALLLNKA2,"alllnkaf"
. 
DLNK0500  MOVE      SP70,LNKAHREF           * Linked AH referral number
          PACK      KEY16,OBAOUTNO,SP70
          CALL      RSALLNK2
DLNK0550  CALL      RKALLNK2
          BRANCH    OVRCD,DLNK9999          * not a referral
.
          MATCH     DOBAOUTN,ALLNLNKV
          GOTO      DLNK9999 IF NOT EQUAL
.
          MOVE      ALLNVISN,LNKAHREF       * Save linked referral
          MOVE      ONE,ALLNSTAT            * Set status to cancelled
.
          PACK      KEY16,ALLNVISN,ALLNLNKV,SP70
          CALL      UPALLNK2
.
          GOTO      DLNK0550
.
DLNK9999  RETURN
+
.*********************************************************************
.         update bokcanfd and bokstafd
.*********************************************************************
BSTAT000  PACK      CPTDATE,CCC,CYY,CMM,CDD
          REP       " 0",CPTDATE
          CALL      GPERD
          BRANCH    EXIT,BSTAT900           * invalid period
.
          MOVE      BKRETYPE,BCTYPE         * booking type
          MOVE      PTCNBDTH,BCCANC         * cancellation code
          PACK      BCDATE,DRGYR,DRGNUM
          REP       " 0",BCDATE             * set period
.
          MOVE      ZERO,BCCANCL            * clear value
          PACK      KEY12,BCTYPE,BCCANC,BCDATE
          CALL      RDCANA1
.
. ------- get the operator id ------------
.
          MOVE      SP4,OPCNOPER
          BRANCH    OTCNOPCR,BSTAT500,BSTAT700
          GOTO      BSTAT800
.
. ------- Use Login Id ------------
.
BSTAT500  MOVE      PASSCODE,OPCNOPER 
          GOTO      BSTAT800
.
. ------- Keyin Login Id  - Category O2 ------------
.
BSTAT700  SCAN      "WEB",PRGID
          IF        @EQUAL
            GOTO      BSTAT800
          ENDIF
          RESET     PRGID
.
          MOVE      "O2",CODE               * Category 
          MOVE      SP3,CKYIDEF3            * Default code
          MOVE      OTCNSQO2,SUPRSQST       * suppress "?" ?
.
          DISPLAY   *P1:24,*EL,"Operator Id :";
          MOVE      "15",ECOL
          MOVE      "24",EVERT
BSTAT750  CALL      KCODNQST                * Keyin the code suppress "?"
          BRANCH    EXIT,BSTAT750,BSTAT750
.
          PACK      OPCNOPER,ACODE,SP4
          MOVE      ZERO,BCCANCL            * clear value
          PACK      KEY12,BCTYPE,BCCANC,BCDATE
          CALL      RDACANA1
.
BSTAT800  ADD       ONE,BCCANCL
.
          IF        OVRCD = ONE
            MOVE      SP30,BCSPARE
            CALL      WRCANA1
          ELSE
            CALL      UPCANA1
          ENDIF
.
. update bokstafd
.
BSTAT900  MOVE      BKREEDAT,CPTDATE        * set up the input date 
          CALL      GPERD                   * get the period and year
          BRANCH    EXIT,BSTAT999           * period not on file
.
          PACK      BSDATE,DRGYR,DRGNUM     * set up the year and period, CCYYMM
          REP       " 0",BSDATE
          MOVE      BKRETYPE,BSTYPE         * booking type
.
          MOVE      ZERO,BSCANCB
          MOVE      ZERO,BSUNDLV
          MOVE      ZERO,BSBOOKS
          MOVE      ZERO,BSADMTS
          MOVE      ZERO,BSDELVY
          MOVE      ZERO,BSCANCP
          MOVE      ZERO,BSDSCHS
          MOVE      ZERO,BSDEPOS
          MOVE      SP30,BSSPARE
.
          PACK      KEY9,BSTYPE,BSDATE
          CALL      RDBSTA1
.
          ADD       ONE,BSCANCB             * add to cancelled bookings
          SUB       ONE,BSUNDLV             * sub from undelivered
          IF        OVRCD = ONE
            CALL      WRBSTA1
          ELSE
            CALL      UPBSTA1
          ENDIF
.
BSTAT999  RETURN
+
.********************************************************************
.         post the cancellation to the OPRDEAFD
.********************************************************************
CNOPR000  COMPARE   ONE,CTHETR
          GOTO      CNOPR999 IF NOT EQUAL   * not using theatre
.
          SCAN      "WEB",PRGID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,"Cancelling Theatre details...";
          ENDIF
          RESET     PRGID
          PACK      KEY31,BKREBOOK,ZERO,PDECDTE,SP30
          CALL      RSOPDEA2
CNOPR100  CALL      RKOPDEA2
          BRANCH    OVRCD,CNOPR999          * no theatre bookings
.
          MATCH     BKREBOOK,OPDAADMN
          GOTO      CNOPR999 IF NOT EQUAL   * diff. person
.
          COMPARE   THREE,OPDASTAT
          GOTO      CNOPR100 IF EQUAL       * already cancelled
.
          MATCH     PDECDTE,OPDADATE
          GOTO      CNOPR100 IF LESS        * only cancel current/future bkgs
.
.         We have a booking to be cancelled
.
          UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.
          SCAN      "WEB",PRGID
          IF        !@EQUAL
            DISPLAY   *P41:24,*EL,*V2LON,OPDACLIN,SP1,CPCDATE
          ENDIF
          RESET     PRGID
.
.         Set up the parameters for the routine to cancel the booking
.
          PACK      CURSESS,OPDADATE,OPDATIME,OPDACLIN
          MOVE      OPDACASE,CURCASE         * case number
          MOVE      OPDASTAT,CURSTAT         * current status number
          MOVE      PTCNTDTH,REPLY           * code
          CALL      CANDEA00                 * Cancel booking
.
          MOVE      FOUR,POLLTYPE            * set for cancel theatre booking
          CALL      WRPOL000                 * trigger HL7 cancellation message

          GOTO      CNOPR100
.
CNOPR999  RETURN
+
.*********************************************************************
.         audits for OPRDEAFD 
.*********************************************************************
AUDEA000  BRANCH    OPCNAUDB,AUDEA999
.
          PACK      TDESC,KEY19,SP1      * save key19 on entry
.
          COMPARE   THREE,AUDIFLAG       * is it an after change audit?
          GOTO      AUDEA100 IF EQUAL    * Yes. So don't change time
.
          CALL      IBACLOCK             * get current date
          PACK      OPDAAUDD,CCC,CYY,CMM,CDD
          REP       " 0",OPDAAUDD
          MOVE      CTIMEIS,OPDAAUDT     * clock current time
.
AUDEA100  MOVE      ONE,OPDAAUDS         * not printed
          MOVE      PASSCODE,OPDAAUDO    * get user id
          MOVE      PORT,OPDAAUDP        * get port number
.
          MOVE      AUDIFLAG,OPDAAUDR
          REPLACE   "1A2B3C4D",OPDAAUDR
          PACK      KEY19,OPDAAUDD,OPDAAUDT,OPDAAUDP,OPDAAUDR
          CALL      AWOPDEA              * write audit record
          GOTO      AUDEA995
.
.         now delete B audit
.
AUDEA600  PACK      KEY19,OPDAAUDD,OPDAAUDT,OPDAAUDP,ANSB
          CALL      ADOPDEA              * delete audit record
.
AUDEA995  UNPACK    TDESC,KEY19,ANS      * restore the key19
.
AUDEA999  RETURN
+
.*********************************************************************
.         audits for OPRSESFD 
.*********************************************************************
AUSES000  BRANCH    OPCNAUDS,AUSES999 
.
          PACK      TDESC,KEY19,SP1      * save key19 on entry
.
          COMPARE   THREE,AUDIFLAG       * is it an after change audit?
          GOTO      AUSES100 IF EQUAL    * Yes. So don't change time
.
          CALL      IBACLOCK             * get current date
          PACK      OPSEAUDD,CCC,CYY,CMM,CDD
          REP       " 0",OPSEAUDD
          MOVE      CTIMEIS,OPSEAUDT     * clock current time
.
AUSES100  MOVE      ONE,OPSEAUDS         * not printed
          MOVE      PASSCODE,OPSEAUDO    * get user id
          MOVE      PORT,OPSEAUDP        * get port number
.
          MOVE      AUDIFLAG,OPSEAUDR
          REPLACE   "1A2B3C4D",OPSEAUDR
          PACK      KEY19,OPSEAUDD,OPSEAUDT,OPSEAUDP,OPSEAUDR
          CALL      AWOPSES              * write audit record
          GOTO      AUSES995
.
.         now delete B audit
.
AUSES600  PACK      KEY19,OPSEAUDD,OPSEAUDT,OPSEAUDP,ANSB
          CALL      ADOPSES              * delete audit record
.
AUSES995  UNPACK    TDESC,KEY19,ANS      * restore the key19
.
AUSES999  RETURN
+
.*********************************************************************
.         cancel the slot
.*********************************************************************
CANSL000  MOVE      TWO,AUDIFLAG             * set to a before audit
          CALL      AUDOTBOA                 * write the audit
.
          PACK      KEY28,VSESS,VSLOT        * Key for slot being cancelled
          CALL      RDBOKA1                  * Position on the record
          BRANCH    OVRCD,CANSL600           * Check for extended slots
.
          MOVE      TWO,POLLTYPE             * set for O/P type
          CALL      WRPOL000                 * trigger HL7 cancellation message
.
          MOVE      ZERO,OBASTAT             * Change the status back to zero
          MOVE      ZEROUR,OBAURNO           * Set the U/R Number back to zero
          MOVE      ZEROVISN,OBAOUTNO        * Set the outpatient number to zero
          MOVE      SP20,OBABKDTE            * Reset the booking date
          MOVE      ZERO,OBAPULL             * Reset pulling list indicator
          CALL      UPBOKA1                  * Clear this slot
.
          MOVE      THREE,AUDIFLAG           * set to an after audit
          CALL      AUDOTBOA                 * write the audit
.
.         Check for any extended slots that also need to be cleared
.
CANSL600  CALL      RDKBOKA1                 * Get the next booking record
          BRANCH    OVRCD,CANSL999           * Booking cancelled.
.
          PACK      SESSION,OBASITE,OBACLIN,OBADATE,OBASTRT
          MATCH     SESSION,VSESS            * Correct session ?
          GOTO      CANSL999 IF NOT EQUAL    * No. Finished
.
          COMPARE   THREE,OBASTAT            * Is this an extended slot ?
          GOTO      CANSL999 IF NOT EQUAL    * No. Finished
.
          MOVE      OBAEXSL,SLOTOPT          * Move parent slot to a dim field
          MATCH     SLOTOPT,VSLOT            * Correct parent ?
          GOTO      CANSL999 IF NOT EQUAL    * No. Finished
.
          MOVE      TWO,AUDIFLAG             * set to a before audit
          CALL      AUDOTBOA                 * write the audit
          MOVE      ZEROUR,OBAURNO           * Set the U/R Number back to zero
          MOVE      ZEROVISN,OBAOUTNO        * Set the outpatient number to zero
          MOVE      SP20,OBABKDTE            * Reset the booking date
          MOVE      ZERO,OBAPULL             * Reset pulling list indicator
          CALL      UPBOKA1                  * Clear this slot
          MOVE      THREE,AUDIFLAG           * set to an after audit
          CALL      AUDOTBOA                 * write the audit
          GOTO      CANSL600                 * Check for another extended slot
.
CANSL999  RETURN
+
.****************************************************************************
.         Audits for patient outpatients bookings file (outaudba)
.****************************************************************************
AUDOTBOA  BRANCH    HOAUDA,OTBOA999
.
          COMPARE   THREE,AUDIFLAG     * is it a after ?
          GOTO      OTBOA200 IF EQUAL
.
          COMPARE   FIVE,AUDIFLAG      * is it a delete ?
          GOTO      OTBOA600 IF EQUAL
.
          CALL      IBACLOCK           * get current date
          PACK      OTBAAUDD,CCC,CYY,CMM,CDD
          REP       " 0",OTBAAUDD
          CLOCK     TIME,OTBAAUDT      * clock current time
          MOVE      ONE,OTBAAUDS       * set print status
          MOVE      PASSCODE,OTBAAUDO  * get user ID
          MOVE      PORT,OTBAAUDP      * get port number
.
          BRANCH    AUDIFLAG,OTBOA200,OTBOA200,OTBOA200,OTBOA200,OTBOA600
          SCAN      "WEB",PRGID
          IF        !@EQUAL
            DISPLAY   *P60:24,*V2LON,*BLINKON,"Audit flag not set";
          ENDIF
          RESET     PRGID
          GOTO      OTBOA999
.
OTBOA200  MOVE      AUDIFLAG,OTBAAUDR
          REPLACE   "1A2B3C4D",OTBAAUDR
          PACK      KEY19,OTBAAUDD,OTBAAUDT,OTBAAUDP,OTBAAUDR  
          CALL      AWOTBOA           * write to audit file
          GOTO      OTBOA999
.
OTBOA600  PACK      KEY19,OTBAAUDD,OTBAAUDT,OTBAAUDP,ANSB
          CALL      ADOTBOA           * delete audit file
.
OTBOA999  RETURN
. *****************************************************************************
. * DELT0000 : Delete a Record                                                *
. *****************************************************************************
.
DELT0000  
.
. ------- delete record from the history file ---------------
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,SEVENTY9;*138,PTCNURHA
.
          COMPARE   ONE,PTCNURHA
          GOTO      DELT9999 IF NOT EQUAL
.
          PACK      CFNAME,OSTFILE,FILHISA1
          OPEN      OUTHISA1,CFNAME     
          PACK      KEY16,OBAURNO,OBAOUTNO,SP20
.
DELT0500  PACK      KEY28,KEY16,SP20,SP20
          CALL      RSOTHIS1
          CALL      RKOTHIS1
          BRANCH    OVRCD,DELT9999
.
          PACK      KEY17,OTHIURNO,OTHIEVNT,SP10
          MATCH     KEY17,KEY16
          GOTO      DELT9999 IF NOT EQUAL
.
          PACK      KEY28,OTHIURNO,OTHIEVNT,OTHIEDAT,OTHIUCNT,SP10
          CALL      DEOTHIS1
.
          GOTO      DELT0500
.
DELT9999  RETURN
.
.---------------------------------------------------
. Reduce Booking Count on Session
.---------------------------------------------------
SUBBOK00  MOVE      ZERO,LOCKCNT
          MOVE      "CV",KEY2
          PACK      KEY5,KEY2,OBAVISIT
          CALL      RDCODE1
          BRANCH    OVRCD,SUBBOK99
          IF        TCFORM6=0
            MOVE      ONE,TCFORM6
          ENDIF
.
SUBBOK10  PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT,SP70
          CALL      RLOTSES1
          BRANCH    OVRCD,SUBBOK99,SUBBOK90
          SUB       TCFORM6,OSESLOTB
          MATCH     ANSN,TCINDC1
          IF        @EQUAL
            SUB       ONE,OSEBNEW
          ENDIF
          MATCH     ANSR,TCINDC1
          IF        @EQUAL
            SUB       ONE,OSEBRV
          ENDIF
          MATCH     ANSS,TCINDC1
          IF        @EQUAL
            SUB       ONE,OSEBSPC
          ENDIF
          CALL      RDSLT000
          CALL      SUBTIM00
          CALL      UPSESA1
          CALL      UUOTSES1
          MOVE      ZERO,EXIT
          GOTO      SUBBOK99
.
SUBBOK90  DISPLAY   *W
          ADD       ONE,LOCKCNT
          IF        LOCKCNT<5
            GOTO      SUBBOK10
          ENDIF
.
SUBBOK99  RETURN
+
.****************************************************************************
.                                 AUDOTBOB
.    Audits for patient outpatients bookings file (outaudba)
.****************************************************************************
AUDOTBOB  BRANCH    HOAUDB,OTBOB999
.
          COMPARE   THREE,AUDIFLAG     * is it a delete ?
          GOTO      OTBOB200 IF EQUAL
.
          COMPARE   FIVE,AUDIFLAG      * is it a delete ?
          GOTO      OTBOB600 IF EQUAL
.
          CALL      IBACLOCK           * get current date
          PACK      OTBBAUDD,CCC,CYY,CMM,CDD
          REP       " 0",OTBBAUDD
          CLOCK     TIME,OTBBAUDT      * clock current time
          MOVE      ONE,OTBBAUDS       * set print status
          MOVE      PASSCODE,OTBBAUDO  * get user ID
          MOVE      PORT,OTBBAUDP      * get port number
.
          BRANCH    AUDIFLAG,OTBOB200,OTBOB200,OTBOB200,OTBOB200,OTBOB600
          SCAN      "WEB",PRGID
          IF        !@EQUAL
            DISPLAY   *P60:24,*V2LON,*BLINKON,"Audit flag not set";
          ENDIF
          RESET     PRGID
          GOTO      OTBOB999
.
OTBOB200  MOVE      AUDIFLAG,OTBBAUDR
          REPLACE   "1A2B3C4D",OTBBAUDR
          PACK      KEY19,OTBBAUDD,OTBBAUDT,OTBBAUDP,OTBBAUDR  
          MOVE      OSTSITE,OTBBSITE
          CALL      AWOTBOB           * write to audit file
          GOTO      OTBOB999
.
OTBOB600  PACK      KEY19,OTBBAUDD,OTBBAUDT,OTBBAUDP,ANSB
          CALL      ADOTBOB           * delete audit file
.
OTBOB999  RETURN
+
.****************************************************************************
.                                 ALLH0000
.    Close Allied Health Referrals                  
.    Close internal referrals first then SACS masters
.    A VINAH episode referral will be rejected instead of closed if it
.    doesn't have any contacts - Victoria only
.    A VINAH program referral will be rejected instead of closed if all it's
.    episode referrals are rejected or cancelled - Victoria only
.****************************************************************************
ALLH0000  OPEN      ALLREFA1,"allrefaf"
          OPEN      ALLREFA2,"allrefaf"
          OPEN      ALLRLNA1,"allrlnaf"
          OPEN      ALLRLNA2,"allrlnaf"
          OPEN      ALLSTSA1,"allstsaf"
          OPEN      ALLAUDA1,"allaudaf"
          OPEN      ALLENCA1,"allencaf"
          OPEN      ALLGRPA3,"allgrpaf"
.
          MATCH     "1",ALCNRAUD            
          IF        @EQUAL
            OPEN      ALLAUDRE,"allaudre"
          ENDIF
.
. Auto activate waiting VINAH episode referral if it has contacts
.
          CALL      ALVA0000               * VINAH Auto activate
          DISPLAY   *W1                    * Pause before auto closure
.
          MOVE      ZERO,SACSFLAG
.
ALLH0500  PACK      KEY16,PURNO,SP70
          CALL      RSALREF2
ALLH1000  CALL      RKALREF2
          BRANCH    OVRCD,ALLH9000
.
          MATCH     PURNO,ALREURNO
          GOTO      ALLH9000 IF NOT EQUAL
.
          MATCH     "2",ALRESTAT
          GOTO      ALLH1000 IF EQUAL
.
          MATCH     "4",ALRESTAT
          GOTO      ALLH1000 IF EQUAL
.
          MATCH     "5",ALRESTAT
          GOTO      ALLH1000 IF EQUAL
.
          IF        SACSFLAG=0
            MATCH     "1",ALREUYN4          * SACS referral
            GOTO      ALLH1000 IF EQUAL
          ENDIF
.
          MOVE      ZERO,REJECTRF           * Don't reject referral

          COMPARE   THREE,PTCNHDPS          * Victoria 
          GOTO      ALLH2000 IF NOT EQUAL
.
          MATCH     SP70,ALCNVARR           * Blank auto rejection reason
          GOTO      ALLH2000 IF EQUAL
.
          MATCH     "000",ALCNVARR          * Auto rejection reason not set
          GOTO      ALLH2000 IF EQUAL
.
          PACK      KEY5,ANSC,ANSG,ALREDEPT,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,ALLH2000
.
          MATCH     SP70,TCINDC8            * VINAH Referral
          GOTO      ALLH2000 IF EQUAL
.
          MATCH     "D",TCINDC8             * HDB? then close as no contact
          GOTO      ALLH2000 IF EQUAL
.
          MATCH     "E",TCINDC8             * HEN? then close as no contact
          GOTO      ALLH2000 IF EQUAL
.
          MATCH     "L",TCINDC8             * TPN? then close as no contact
          GOTO      ALLH2000 IF EQUAL
.
          MATCH     "F",TCINDC8             * IT? then close as no contact
          GOTO      ALLH2000 IF EQUAL
.
          MATCH     "M",TCINDC8           * Medi-hotel? then close as no contact
          GOTO      ALLH2000 IF EQUAL
.
          MOVE      ALREVISN,SAVEREFN       * Save referral for re position
          PACK      SAVKEY16,ALREURNO,ALREVISN,SP70  * Save key for re porition
.
          IF        SACSFLAG=0
            CALL      EREJ0000          * Reject referral if it 
          ENDIF                         * doesn't have any contacts
.
          IF        SACSFLAG=1
            CALL      PREJ0000          * Reject Program referral if all
          ENDIF                         * episodes refs are cancelled/rejected
.
          PACK      KEY16,SAVKEY16,SP70
          CALL      RDALREF2                * Re position on original referral
          BRANCH    OVRCD,ALLH1000
.
ALLH2000  CALL      IBACLOCK 
          MOVE      TWO,AUDTTYPE            * before history record
          CALL      WAALRE00
.
          BRANCH    REJECTRF,ALLH4000       * Reject the referral  (Vic Only)
.
.  Close the referral
.
          MOVE      "2",ALRESTAT            * Populate all the fields and write
          MOVE      SP70,ALRERINA           * in database
          MOVE      SP70,ALREDINA
          MOVE      SP70,ALRETINA
          MOVE      SP70,ALREDINU
          MOVE      SP70,ALREUINA
.
          MOVE      ALCNCLCD,ALRERCLO        * Reason for Closure
          CALL      IBACLOCK
          PACK      ALREDCLO,CCC,CYY,CMM,CDD * current date
          REP       " 0",ALREDCLO
          CLOCK     TIME,ALRETCLO            * current time
.
          MOVE      ALCNPRGC,ALREUC33        * Program completion reason
.
          IF        SACSFLAG=1
            MATCH     "1",ALREUYN4           * SACS referral
            IF        @EQUAL
              MOVE      ALREDCLO,ALREUDT6    * Case end date
              MOVE      ALRETCLO,ALREUTM6    * Case end time
            ENDIF
          ENDIF
.
          GOTO      ALLH5000
.
.  Reject the referral - Victoria Only
.
ALLH4000  MOVE      "5",ALRESTAT          * Rejected
          MOVE      SP70,ALRERCLO
          MOVE      SP70,ALREDCLO
          MOVE      SP70,ALRETCLO
          MOVE      SP70,ALREUCLO
.
          MOVE      SP70,ALRERINA
          MOVE      SP70,ALREDINA
          MOVE      SP70,ALRETINA
          MOVE      SP70,ALREDINU
          MOVE      SP70,ALREUINA
.
          MOVE      SP70,ALRERCAN           * fields related to Cancellation
          MOVE      SP70,ALREDCAN
          MOVE      SP70,ALRETCAN
          MOVE      SP70,ALREUCAN
          MOVE      USERID,ALRESRUI
.
          MOVE      ALCNVARR,ALRESREJ
          CALL      IBACLOCK
          PACK      ALRESRDT,CCC,CYY,CMM,CDD * current date
          REP       " 0",ALRESRDT
          CLOCK     TIME,ALRESRTM            * current time
.
ALLH5000  PACK      ALREUDAT,CCC,CYY,CMM,CDD * current date
          REP       " 0",ALREUDAT
          CLOCK     TIME,ALREUTIM           * current time
          MOVE      WBSEUID,ALREUUID        * user id
          CALL      ARMHI000                * Set MHINC indicator
          CALL      UPALREF2                * update the referral table
.
          MATCH     "HL7RECVR",PRGID
          IF        !@EQUAL
            CALL      PMIGTNID              * get national id for dgate write
            MOVE      NMPNUMB,PTNINMPI
            MOVE      ONE,HL7TRGID
            MOVE      "PATDEATH",HL7INCLD
            CALL      DGCLII13              * broadcast Update Ref. (REF^I13)
          ENDIF
.                                   ***** write to Referral History Table
          PACK      ALSTSDAT,CCC,CYY,CMM,CDD * current date
          REP       " 0",ALSTSDAT
          CALL      WRTRSH00                * write a record to allstsaf
.
          IF        REJECTRF=1
            MOVE      "J ",UPDTTYPE         * Reject referral
          ELSE
            MOVE      "C ",UPDTTYPE         * Close referral
          ENDIF
          CALL      WRTRAU00                * write to the referral audit table
.
          MOVE      THREE,AUDTTYPE          * after history record
          CALL      WAALRE00
.
          GOTO      ALLH1000
.
ALLH9000  ADD       ONE,SACSFLAG
          BRANCH    SACSFLAG,ALLH0500
          MOVE      ZERO,SACSFLAG
.
ALLH9999  RETURN
.
.****************************************************************************
.                                 ALVA0000
.  If Victoria and a VINAH referral department. Activate a waiting episode
.  referral and it's program referral if it has active contacts
.  Activate waiting referrals for programs that don't collect contacts
.****************************************************************************
ALVA0000  COMPARE   THREE,PTCNHDPS               * Victoria
          GOTO      ALVA9999 IF NOT EQUAL
.
          PACK      KEY16,PURNO,SP70
          CALL      RSALREF2
ALVA1000  CALL      RKALREF2                     * Check referrals for this U/R
          BRANCH    OVRCD,ALVA9999
.
          MATCH     PURNO,ALREURNO               * Correct U/R
          GOTO      ALVA9999 IF NOT EQUAL
.
          MATCH     "0",ALRESTAT                 * Waiting only
          GOTO      ALVA1000 IF NOT EQUAL
.
          MATCH     "1",ALREUYN4                 * Program referral
          GOTO      ALVA1000 IF EQUAL
.
          PACK      KEY5,ANSC,ANSG,ALREDEPT,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,ALVA1000
.
          MATCH     SP70,TCINDC8                 * VINAH Referral
          GOTO      ALVA1000 IF EQUAL
.
          MATCH     "D",TCINDC8          * HDB? not using contacts
          GOTO      ALVA2000 IF EQUAL
.
          MATCH     "E",TCINDC8          * HEN? not using contacts
          GOTO      ALVA2000 IF EQUAL
.
          MATCH     "M",TCINDC8          * Medi-hotel? not using contacts
          GOTO      ALVA2000 IF EQUAL
.
          MATCH     "L",TCINDC8          * TPN? not using contacts
          GOTO      ALVA2000 IF EQUAL
.
          MOVE      ALREVISN,SAVEREFN            * Save referral
          CALL      ALCA0000                     * Contacts exists
          BRANCH    EXIT,ALVA1000
.
. Contacts exists or are not required for a waiting VINAH episode referral
. so activate it and it's master
.
ALVA2000  MOVE      ALREVISN,SAVEREFN            * Save referral
          PACK      SAVKEY16,ALREURNO,ALREVISN,SP70  * Save key for re porition
.
          CALL      ACTW0000                     * Activate waiting referral
.
          PACK      KEY16,SAVKEY16,SP70
          CALL      RDALREF2
.
          GOTO      ALVA1000
.
ALVA9999  RETURN
.
.****************************************************************************
. Do contacts exists for this referral that are not cancelled
.****************************************************************************
ALCA0000  PACK      KEY16,SAVEREFN,SP70      * check Referral contacts
          CALL      RSALENC1
ALCA1000  CALL      RKALENC1
          BRANCH    OVRCD,ALCA9000
.
          MATCH     SAVEREFN,ALENVISN
          GOTO      ALCA9000 IF NOT EQUAL
.
          MATCH     "1",ALENRSTA             * Cancelled encounter skip
          GOTO      ALCA1000 IF EQUAL
.
ALCA8000  MOVE      ZERO,EXIT
          GOTO      ALCA9999
.
ALCA9000  MOVE      ONE,EXIT
          GOTO      ALCA9999
.
ALCA9999  RETURN
.
.******************************************************************************
. Auto activate a single waiting episode referral                             *
.******************************************************************************
ACTW0000  PACK      KEY8,SAVEREFN,SP70
          CALL      RDALREF1                * Read the referral table
          BRANCH    OVRCD,ACTW9999
.
          MATCH     "0",ALRESTAT            * Exit if not waiting
          GOTO      ACTW9999 IF NOT EQUAL
.
          MATCH     "1",ALREUYN4            * Exit if not an internal referral
          GOTO      ACTW9999 IF EQUAL
.
          MOVE      TWO,AUDTTYPE            * before history record
          CALL      WAALRE00
.
          MOVE      "1",ALRESTAT
          PACK      ALREDACT,CCC,CYY,CMM,CDD * Date activated
          REP       " 0",ALREDACT
.
          PACK      ALREUDAT,CCC,CYY,CMM,CDD
          REP       " 0",ALREUDAT
          CLOCK     TIME,ALREUTIM
          MOVE      USERID,ALREUUID
.
          CALL      UPALREF1                * Update referral Table (allrefaf)
.
          MATCH     "HL7RECVR",PRGID
          IF        !@EQUAL
            CALL      PMIGTNID                * get national id for dgate write
            MOVE      NMPNUMB,PTNINMPI
            MOVE      THREE,HL7TRGID
            MOVE      "PATDEATH",HL7INCLD
            CALL      DGCLII13                * broadcast Update Ref. (REF^I13)
          ENDIF
.
          MOVE      THREE,AUDTTYPE          * after history record
          CALL      WAALRE00
.
          MOVE      ALREDACT,ALSTSDAT
          CALL      WRTRSH00                * write a record to allstsaf
.
          MOVE      "T ",UPDTTYPE           * Update type
          CALL      WRTRAU00                * write to the referral audit table
.
          MOVE      ALREVISN,CHECKREF
          CALL      AMAS0000                * Auto activate master referral
.
ACTW9999  RETURN
.
.*******************************************************************************
. Activate a waitingprogram referral
. Requires CHECKREF - Internal Referral Number
.*******************************************************************************
AMAS0000  PACK      KEY8,CHECKREF,SP70
          CALL      RDALREF1                * Read the referral to see if it's
          BRANCH    OVRCD,AMAS9999          * an internal referral
.
          MATCH     "1",ALREUYN4            * Exit if not an internal referral
          GOTO      AMAS9999 IF EQUAL
.
          MATCH     "1",ALRESTAT            * Exit if not active
          GOTO      AMAS9999 IF NOT EQUAL
.
          MOVE      ALREDACT,SAVIADAT       * Save internal referral date active
          MOVE      SP70,MASTREFN           * Clear master referral number
.
          PACK      KEY16,CHECKREF,SP70
          CALL      RSALRLN2                * Determine master referral number
          CALL      RKALRLN2
          BRANCH    OVRCD,AMAS9999
.
          MATCH     CHECKREF,ALRLLNKV       * Linked referrals
          GOTO      AMAS9999 IF NOT EQUAL
.
          MOVE      ALRLVISN,MASTREFN       * Save master referral number

          PACK      KEY8,MASTREFN,SP70
          CALL      RDALREF1                * Read the master referral
          BRANCH    OVRCD,AMAS9999
.
          MATCH     "1",ALREUYN4            * Exit if not an master referral
          GOTO      AMAS9999 IF NOT EQUAL
.
          MATCH     "0",ALRESTAT            * Exit if not waiting
          GOTO      AMAS9999 IF NOT EQUAL
.
          MOVE      TWO,AUDTTYPE            * before history record
          CALL      WAALRE00
.
          MOVE      "1",ALRESTAT            * Set status to active
          MOVE      SAVIADAT,ALREDACT
.
          PACK      ALREUDAT,CCC,CYY,CMM,CDD
          REP       " 0",ALREUDAT
          CLOCK     TIME,ALREUTIM
          MOVE      USERID,ALREUUID
.
          CALL      UPALREF1                * Update referral Table (allrefaf)
.
          MATCH     "HL7RECVR",PRGID
          IF        !@EQUAL
            CALL      PMIGTNID                * get national id for dgate write
            MOVE      NMPNUMB,PTNINMPI
            MOVE      FOUR,HL7TRGID
            MOVE      "PATDEATH",HL7INCLD
            CALL      DGCLII13                * broadcast Update Ref. (REF^I13)
          ENDIF
.
.         Check if we need to write an after audit record
.
          MOVE      THREE,AUDTTYPE        * after history record
          CALL      WAALRE00
.
          MOVE      ALREDACT,ALSTSDAT
          CALL      WRTRSH00                * write a record to allstsaf
.
          MOVE      "T ",UPDTTYPE           * Update type
          CALL      WRTRAU00                * write to the referral audit table
.
AMAS9999  RETURN
.
.******************************************************************************
. Reject epiosde referral if it doesn't have any contacts
.******************************************************************************
EREJ0000  MOVE      ONE,REJECTRF             * Reject referral
.
          PACK      KEY16,SAVEREFN,SP70      * check Referral contacts        
          CALL      RSALENC1                 * to see if they are all cancelled
EREJ1000  CALL      RKALENC1
          BRANCH    OVRCD,EREJ2000
.
          MATCH     SAVEREFN,ALENVISN
          GOTO      EREJ2000 IF NOT EQUAL
.
          MATCH     "1",ALENRSTA            * Cancelled encounter skip
          GOTO      EREJ1000 IF EQUAL
.
          MOVE      ZERO,REJECTRF           * Don't reject referral it has
          GOTO      EREJ9999
.                                           * contacts
EREJ2000  PACK      KEY24,PURNO,SP70
          CALL      RSALGRP3
EREJ3000  CALL      RKALGRP3                * Check group contacts
          BRANCH    OVRCD,EREJ9999
.
          MATCH     PURNO,ALGPURNO       * Group session patient matching UR?
          GOTO      EREJ9999 IF NOT EQUAL   * no, skip
.
          MATCH     SAVEREFN,ALGPREFN       * Same referral?
          GOTO      EREJ3000 IF NOT EQUAL   * no, get next referral
.
          MATCH     "0",ALGPACTV            * Group session Active?
          IF        @EQUAL
            MOVE      ZERO,REJECTRF         * Don't reject group booking exists
            GOTO      EREJ9999
          ENDIF
.
          GOTO      EREJ3000
.
EREJ9999  RETURN
.
.******************************************************************************
. Reject program referral if all linked episode referrals are
. cancelled or rejected
.******************************************************************************
PREJ0000  MOVE      ONE,REJECTRF             * Reject referral
.
          PACK      KEY16,SAVEREFN,SP70      * check Referral contacts        
          CALL      RSALENC1                 * to see if they are all cancelled
PREJ1000  CALL      RKALENC1
          BRANCH    OVRCD,PREJ2000
.
          MATCH     SAVEREFN,ALENVISN
          GOTO      PREJ2000 IF NOT EQUAL
.
          MATCH     "1",ALENRSTA            * Cancelled encounter skip
          GOTO      PREJ1000 IF EQUAL
.
          MOVE      ZERO,REJECTRF           * Don't reject referral it has
.                                           * contacts
          GOTO      PREJ9999
.
PREJ2000  PACK      KEY16,SAVEREFN,SP70
          CALL      RSALRLN1
PREJ3000  CALL      RKALRLN1                * Check linked referrals
          BRANCH    OVRCD,PREJ4000
.
          MATCH     SAVEREFN,ALRLVISN       * Linked referrals
          GOTO      PREJ4000 IF NOT EQUAL
.
          PACK      KEY8,ALRLLNKV,SP70
          CALL      RDALREF1
          BRANCH    OVRCD,PREJ3000
.
          MATCH     "4",ALRESTAT            * Cancelled
          GOTO      PREJ3000 IF EQUAL
.
          MATCH     "5",ALRESTAT            * Rejected
          GOTO      PREJ3000 IF EQUAL
.
          MOVE      ZERO,REJECTRF    * Don't reject referral linked referrals
.                                    * exist that are not canelled/rejected
          GOTO      PREJ9999
.
PREJ4000  PACK      KEY24,PURNO,SP70
          CALL      RSALGRP3
PREJ5000  CALL      RKALGRP3                * Check group contacts
          BRANCH    OVRCD,PREJ9999
.
          MATCH     PURNO,ALGPURNO       * Group session patient matching UR?
          GOTO      PREJ9999 IF NOT EQUAL   * no, skip
.
          MATCH     SAVEREFN,ALGPREFN       * Same referral?
          GOTO      PREJ5000 IF NOT EQUAL   * no, get next referral
.
          MATCH     "0",ALGPACTV            * Group session Active?
          IF        @EQUAL
            MOVE      ZERO,REJECTRF         * Don't reject group booking exists
            GOTO      PREJ9999
          ENDIF
.
          GOTO      PREJ5000
.
PREJ9999  RETURN
.
.******************************************************************************
.         MHINC - MH Department and Primary Ref.
.******************************************************************************
ARMHI000  PACK      KEY5,ANSC,ANSG,ALREDEPT,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,ARMHI999
.
          MATCH     "M",TCINDC4
          GOTO      ARMHI999 IF NOT EQUAL      * Not a Mental Health department
.
          MATCH     "1",ALREUYN4
          GOTO      ARMHI999 IF NOT EQUAL      * Not Primary Referral
.
          MOVE      "0",ALREMOHR               * Unsent
.
ARMHI999  RETURN
+
.******************************************************************************
.      write a record to the Referral Status History Table
.******************************************************************************
WRTRSH00  MOVE      ALREVISN,ALSTVISN       * Populate all the fields and write
          MOVE      ALRESTAT,ALSTSTAT
          PACK      ALSTCDAT,CCC,CYY,CMM,CDD
          REP       " 0",ALSTCDAT
          CLOCK     TIME,ALSTCTIM
          MOVE      WBSEUID,ALSTCUID
          PACK      KEY17,ALSTVISN,ALSTSTAT,ALSTSDAT,SP70
          CALL      RAALSTS1
          IF        OVRCD=0
            CALL      UPALSTS1                * update to the file
          ELSE
            CALL      WRALSTS1                * write to the file
          ENDIF
.
WRTRSH99  RETURN
.******************************************************************************
.      write a record to the Referral Audit Table
.******************************************************************************
WRTRAU00  CALL      CLALLAUD                     * clear allaudaf variables
.
          MOVE      ALREVISN,ALAUVISN
          PACK      ALAUADAT,CCC,CYY,CMM,CDD
          REP       " 0",ALAUADAT
          CLOCK     TIME,ALAUATIM
          MOVE      UPDTTYPE,ALAUUPDT            * Update type
          MOVE      WBSEUID,ALAUAUID
.
          PACK      AUDITKEY,ALAUVISN,ALAUADAT,ALAUATIM,SP70
          PACK      KEY24,ALAUVISN,ALAUADAT,ALAUATIM,SP70
          CALL      RDALAUD1
          IF        OVRCD=1
            CALL      WRALAUD1                * write to the file
          ENDIF
WRTRAU99  RETURN
+
CLRWTESE  PACK      WTEEUNIQ,SP70
          PACK      WTEEURNO,SP70
          PACK      WTEEADAT,SP70
          PACK      WTEEDEST,SP70
          PACK      WTEEINSD,SP70
          PACK      WTEEPLOS,SP70
          PACK      WTEEPROC,SP70
          PACK      WTEEPDES,SP70
          PACK      WTEERREM,SP70
          PACK      WTEERDAT,SP70
          PACK      WTEEREMD,SP70
          PACK      WTEESREF,SP70
          PACK      WTEESSPC,SP70
          PACK      WTEEWEBC,SP70
          PACK      WTEECDAT,SP70
          PACK      WTEECTIM,SP70
          PACK      WTEEWEBU,SP70
          PACK      WTEEUDAT,SP70
          PACK      WTEEUTIM,SP70
          PACK      WTEEARDT,SP70
          PACK      WTEESPAR,SP70
          PACK      WTEEMANU,SP70
          PACK      WTEEPTWT,SP70
          PACK      WTEESGID,SP70
          PACK      WTEERADT,SP70
          PACK      WTEETCMP,SP70
.
          RETURN
.******************************************************************************
.*                                 WESE0000                                   *
.*                Write/update wateseaf (episode details)                     *
.******************************************************************************
WESE0000  READ      CONTROLF,EIGHTY;*250,PTCNHDPS
          READ      CONTROLF,THIRTY7;*242,WTCNUSXB
.
          COMPARE   THREE,PTCNHDPS
          GOTO      WESE9999 IF NOT EQUAL   * Not in Victoria
.
          PACK      KEY17,WTWMECNT,Z70
          CALL      RSWTESE1
          CALL      RPWTESE1
          BRANCH    OVRCD,WESE1000
.
          MATCH     WTEEUNIQ,WTWMECNT
          GOTO      WESE1000 IF NOT EQUAL
.
          CALL      CHKESE00
          BRANCH    EXIT,WESE1000   * no changes
          GOTO      WESE2000
.
WESE1000  CALL      CLRWTESE
.
WESE2000  PACK      WTEEUNIQ,WTWMECNT,SP70
          PACK      WTEEEDAT,SP70
          PACK      WTEEURNO,WMURNO,SP70
.
          PACK      WTEEDEST,SP70
          PACK      WTEESREF,WTWMSRCR,SP70
          PACK      KEY19,WMURNO,WMCODE,WMCNT,SP70
          CALL      RDWTRTD1
          IF        OVRCD<>1
            PACK      WTEEDEST,WTRTTDES,SP70
          ENDIF
          IF        WTCNUSXB=0
            UNPACK    WMSTAY,DIM2,WTEEPLOS
          ELSE
            PACK      WTEEPLOS,WTTXIDYC,SP70
          ENDIF
.
          IF        WMSTAT=6
            PACK      KEY5,ANSW,ANSR,WMREMOVE
            CALL      RDCODE1               * Read a codes file record
            BRANCH    OVRCD,WESE3000
            MATCH     ANSX,TCINDC3
            IF        !@EQUAL
              MATCH     ANSS,TCINDC3
              IF        !@EQUAL
                GOTO      WESE3000
              ENDIF
            ENDIF
            PACK      WTEEINSD,WTTXCTYP
          ENDIF
.
WESE3000  PACK      WTEEPROC,WMCODE,SP70
          PACK      WTEEPDES,WMDESC,SP70
          PACK      WTEERREM,WMREMOVE,SP70
          PACK      WTEERDAT,WMDATE1,SP70
          REP       " 0",WTEERDAT
          PACK      WTEEREMD,WMDATE4,SP70
          MATCH     SP70,WTEEADAT
          IF        !@EQUAL
            MOVE      WTEEADAT,WTEEREMD
          ENDIF
          MATCH     "20050701",WMDATE1
          IF        @LESS
            PACK      WTEEARDT,WMDATE1,SP70
            REP       " 0",WTEEARDT
          ELSE
            PACK      WTEEARDT,WMKEYDT,SP70
            REP       " 0",WTEEARDT
          ENDIF
.
          PACK      WTEEPREV,SP70
          PACK      WTEEREFR,SP70
          PACK      WTEESSPC,WMUNIT,SP70
          PACK      WTEEWEBU,SP70
          PACK      WTEEUDAT,SP70
          PACK      WTEEUTIM,SP70
          MOVE      ZERO,WTEEMANU
          PACK      WTEEASAS,SP70
.
          CALL      CLWATTX1
          PACK      KEY19,WMURNO,WMCODE,WMCNT,SP70
WESE3100  CALL      RDWTTX11
          BRANCH    OVRCD,WESE3500
.
          MOVE      WTTXASAS,WTEEASAS
.
WESE3500  PACK      KEY17,WTEEUNIQ,WTEEEDAT,SP70
          CALL      RAWTESE1
          IF        OVRCD=0
            PACK      WTEEWEBU,WBSEUID,SP70
            CALL      IBACLOCK
            PACK      WTEEUDAT,CCC,CYY,CMM,CDD
            REP       " 0",WTEEUDAT
            CLOCK     TIME,WTEEUTIM
            REP       " 0",WTEEUTIM
            CALL      UPWTESE1
          ELSE
            PACK      WTEEWEBC,WBSEUID,SP70
            CALL      IBACLOCK
            PACK      WTEECDAT,CCC,CYY,CMM,CDD
            REP       " 0",WTEECDAT
            CLOCK     TIME,WTEECTIM
            REP       " 0",WTEECTIM
            CALL      WRWTESE1
          ENDIF
.
WESE9999  RETURN
+
.******************************************************************************
.*                                 CHKESE00                                   *
.*                Check if any wateseaf details have changed                  *
.******************************************************************************
CHKESE00  MOVE      ZERO,EXIT
.
          MATCH     WTEEURNO,WMURNO
          GOTO      CHKESE90 IF NOT EQUAL
.
          MATCH     WTEEDEST,WTRTTDES
          GOTO      CHKESE90 IF NOT EQUAL
.
          IF        WTCNUSXB=0
            MOVE      WTEEPLOS,F3
            COMPARE   F3,WMSTAY
            GOTO      CHKESE90 IF NOT EQUAL
          ELSE
            MATCH     WTTXIDYC,WTEEPLOS
            GOTO      CHKESE90 IF NOT EQUAL
          ENDIF
.
          MATCH     WTEEPROC,WMCODE
          GOTO      CHKESE90 IF NOT EQUAL
.
          MATCH     WTEEPDES,WMDESC
          GOTO      CHKESE90 IF NOT EQUAL
.
          MATCH     WTEERREM,WMREMOVE
          GOTO      CHKESE90 IF NOT EQUAL
.
          IF        WMSTAT=6
            MATCH     WTEEINSD,WTTXCTYP
            GOTO      CHKESE90 IF NOT EQUAL
          ENDIF
.
          MATCH     WTEERDAT,WMDATE1
          GOTO      CHKESE90 IF NOT EQUAL
.
          MATCH     WTEEREMD,WMDATE4
          GOTO      CHKESE90 IF NOT EQUAL
.
          MATCH     WTEESREF,WTWMSRCR
          GOTO      CHKESE90 IF NOT EQUAL
.
          MATCH     WTEESSPC,WMUNIT
          GOTO      CHKESE90 IF NOT EQUAL
.
          MATCH     WTEEREFR,WTRTREFR
          GOTO      CHKESE90 IF NOT EQUAL
.
          MOVE      ONE,EXIT                 * No Changes
.
CHKESE90  GOTO      CHKESE99
.
CHKESE99  RETURN
.
.******************************************************************************
.*                                 PRMAS000                                   *
.*                Update patient age at death                                 *
.******************************************************************************
PRMAS000  SCAN      "WEB",PRGID
          IF        !@EQUAL
            DISPLAY   *P1:24,*EL,"Updating Patient Age.";
          ENDIF
          RESET     PRGID
.
          PACK      KEY8,PURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,PRMAS999
.
          PACK      KEY8,PURNO,SP70
          CALL      RDPMPX21
          IF        OVRCD = 1
            CALL      CLPMSPX2
          ENDIF
.
          MATCH     SP70,PDECDTE
          GOTO      PRMAS999 IF EQUAL
          GOTO      PRMAS999 IF EOS
.
          MOVE      PDECDTE,AGEDATE
          REP       " 0",AGEDATE
.
          CALL      CALCAGE
          MOVE      PSAGEYRS,PSAGE
.
          CALL      UPMAST1
.
PRMAS999  RETURN
.
.------------------------------------------------------------------------
. Clear pre admission from expected patient table
.------------------------------------------------------------------------
PRCL0000  OPEN      PMSEPBA2,"pmsepbaf"
.
          PACK      KEY28,PVIURNO,PVIBILL,SP70
          CALL      RSPMEPB2
PRCL1000  CALL      RKPMEPB2
          BRANCH    OVRCD,PRCL9999
.
          MATCH     PVIURNO,PMEBURNO
          GOTO      PRCL9999 IF NOT EQUAL
.
          MOVE      PVIBILL,KEY8
          MATCH     KEY8,PMEBADMN
          GOTO      PRCL9999 IF NOT EQUAL
.
          PACK      KEY28,PMEBURNO,PMEBADMN,PMEBHOSP,PMEBWARD:
                          PMEBROOM,PMEBBEDD,SP70
          CALL      DEPMEPB2
          GOTO      PRCL0000
.
PRCL9999  RETURN
.
.------------------------------------------------------------------------
. Clear booking from expected patient table
.------------------------------------------------------------------------
BKCL0000  OPEN      PMSEPBA2,"pmsepbaf"
.
          PACK      KEY28,BKREURNO,BKREBOOK,SP70
          CALL      RSPMEPB2
BKCL1000  CALL      RKPMEPB2
          BRANCH    OVRCD,BKCL9999
.
          MATCH     BKREURNO,PMEBURNO
          GOTO      BKCL9999 IF NOT EQUAL
.
          MOVE      BKREBOOK,KEY8
          MATCH     KEY8,PMEBADMN
          GOTO      BKCL9999 IF NOT EQUAL
.
          PACK      KEY28,PMEBURNO,PMEBADMN,PMEBHOSP,PMEBWARD:
                          PMEBROOM,PMEBBEDD,SP70
          CALL      DEPMEPB2
          GOTO      BKCL0000
.
BKCL9999  RETURN
+
.*****************************************************************************
.*                             WRPOL000            Called by: WLIST000       *
.*                   Write a HL7 message trigger to ibapolaf  PRMIS000       *
.* Requires: POLLTYPE - type of record                        OUTPT000       *
.*                      1 = W/L cancel booking                CNOPR000       *
.*                      2 = O/P cancel booking                               *
.*                      3 = Cancel pre-admission                             *
.*                      4 = Cancel theatre booking                           *
.*****************************************************************************
.
WRPOL000  MATCH     "0",IBCNUCIS                 * using HL7 ?
          GOTO      WRPOL999 IF EQUAL            * no - finished
.
          MATCH     "HL7RECVR",PRGID             * update from HL7 Receiver ?
          GOTO      WRPOL999 IF EQUAL            * yes - finished
.
          OPEN      IBAPOLA1,"ibapolaf"
.
          MOVE      ZERO,FORM10                  * initialise unique count
.
          MOVE      TILDA10,KEY10
          CALL      RSIBPOL1                     * position at end of file
          CALL      RPIBPOL1                     * read previous record
          BRANCH    OVRCD,WRPOL100               * eof - finished
.
          MOVE      IBPLUNIQ,FORM10              * load counter
WRPOL100  ADD       ONE,FORM10                   * increment counter
.
          MOVE      FORM10,IBPLUNIQ
          MOVE      SP70,IBPLSKEY
          MOVE      SP70,IBPLSPAR
.
          BRANCH    POLLTYPE,WRPOL200:           * W/L cancel booking
                             WRPOL300:           * O/P cancel booking
                             WRPOL400:           * cancel pre-admission
                             WRPOL500            * cancel theatre booking
          GOTO      WRPOL999
.
WRPOL200  MOVE      "002",IBPLTYPE
          MOVE      WMURNO,IBPLURNO
          MOVE      WMBOOK,IBPLVISN
          GOTO      WRPOL900
.
WRPOL300  MOVE      "003",IBPLTYPE
          MOVE      OBAURNO,IBPLURNO
          MOVE      OBAOUTNO,IBPLVISN
          GOTO      WRPOL900
.
WRPOL400  MOVE      "004",IBPLTYPE
          MOVE      AURNO,IBPLURNO
          MOVE      AADMNO,IBPLVISN
          GOTO      WRPOL900
.
WRPOL500  MOVE      "015",IBPLTYPE
          MOVE      OPDAURNO,IBPLURNO
          MOVE      OPDAADMN,IBPLVISN
          MOVE      OPDAUNIQ,IBPLSKEY
.
WRPOL900  MOVE      FORM10,KEY10
          CALL      RAIBPOL1                     * record already on file ?
          IF        OVRCD = 1
            MOVE      ZERO,OVRCD                 * no - write new record
            TRAP      OVERCOND IF IO
            CALL      WRIBPOL1
            TRAPCLR   IO
            BRANCH    OVRCD,WRPOL100
          ELSE
            GOTO      WRPOL100                   * yes - get next counter
          ENDIF
.
WRPOL999  RETURN
+
.------------------------------------------------------------------------
. DGCLIRWL  -  Broadcast HL7 Remove Waiting List Entry (I14)
.------------------------------------------------------------------------
DGCLIRWL  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,HUND11;*34,WTCNI14M
.
          MATCH     "1",IBCNUCIS                 * using HL7 messaging ?
          GOTO      DGRWL999 IF LESS             * no - finished
.
.         Check if we are using this feature
.
          MATCH     "1",WTCNI14M                 * sending I14 messages ?
          GOTO      DGRWL999 IF NOT EQUAL        * no - finished
.
.         Open the relevant broadcast message holding tables
.
          OPEN      HL7CISA1,"hl7cisaf"
          OPEN      PMSQPTA1,"pmsqptaf"
          OPEN      PMSQPXA1,"pmsqpxaf"
          OPEN      WATQUEA1,"watqueaf"
.
          MOVE      ONE,CISMFLAG                 * yes- set flag for CIS message
.
          CALL      DGMESSID                     * get the unique message id
.
.         Write a record to pmsqptaf (holding patient details)
.
          MOVE      MESSAGID,PMQPMESI
          MOVE      SP8,PMQPOURN
          PACK      PMQPSPAR,SP70,SP70
          CALL      WRPMQPT1
.
.         Write a record to watqueaf (holding W/L details)
.
          MOVE      MESSAGID,WTQUMESI
          PACK      WTQUSPAR,SP70
          CALL      WRWTQUE1
.
.         Write header record (hl7cisaf)
.
          MOVE      "I14",H7CIMETP
          MOVE      HL7TRGID,H7CITRID
          MOVE      HL7INCLD,H7CIINCL
          CALL      WRCIS000
.
          CLOSE     HL7CISA1
          CLOSE     PMSQPTA1
          CLOSE     PMSQPXA1
          CLOSE     WATQUEA1
.
DGRWL999  RETURN
+
.*********************************************************************
.
          INC       OUTCANIO/INC            * bookings cancellations
.
. dummy labels so it will compile - these should hopefully not get called
.
AUDEB000
AUDEC000
DEAI0000
OPDSPCUR
OPDSPSHD
OPROPRDS
PATDSITM
RDICO1
PATITMRD
RDOPOPR1
PATICOKY             * dummy for OPRDEA01
DEMWD000  RETURN     * dummy for OPCANBOK  (NZHIS)
+
. ------- I/O includes -------
.
          INC       ALLENCIO/INC
          INC       ALLQUEIO/INC
          INC       ALLLNKIO/INC            * allied health outpatient link
          INC       BOKCANIO/INC            * bookings cancellation file
          INC       BOKSTAIO/INC            * bookings stats file
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       IBAOVRIO/INC
          INC       IBAPOLIO/INC
.
          INC       BRHLCOMN
          INC       CLPMSQPX
          INC       COUNTSLT
          INC       OPADDTIM
          INC       OPRDEA01
          INC       GENBOKNO
          INC       CALCAGE
          INC       CLWATTX1
          INC       DGCLII13
          INC       OPRDEAIO/INC            * theatre file
          INC       OPRDEBIO/INC            * theatre team details file
          INC       OPRDECIO/INC            * theatre usage details file
          INC       OPRSES01
          INC       OPRSESIO/INC            * session file
          INC       OPRSIUIO/INC            * special items
          INC       OUTSITIO/INC            * site file
          INC       OUTBOAIO/INC            * bookings file
          INC       OUTBB1IO/INC            * bookings file
          INC       OUTBOCIO/INC            * bookings file
          INC       OUTDIAIO/INC            * cancellation file
          INC       OUTRF1IO/INC            * outpatient bookings
          INC       OUTARTIO/INC            * outpatient Appointment Request
          INC       WATCATIO/INC            * W/L cat. change file
          INC       WATTR1IO/INC            * W/L treatment file
          INC       WATQUEIO/INC
          INC       WATRTDIO/INC
          INC       WATTX1IO/INC            * W/L treatment file
          INC       WATESEIO/INC
          INC       WATHISIO/INC            * W/L treatment file
          INC       WATNBRIO/INC            * W/L nbrs file
          INC       WATPROIO/INC            * W/L procedure file
          INC       OPRHISIO/INC            * W/L treatment file
          INC       OUTHISIO/INC            * W/L treatment file
          INC       WATSUSIO/INC            * W/L suspension file
          INC       PATCODKY
          INC       KCODNQST
          INC       PATSNDX
          INC       EOCREFIO/INC            * EOC Referral File   
          INC       EOCLNKIO/INC            * EOC Link File   
          INC       PATCODIO/INC            * Category code file
          INC       PMSEPBIO/INC
          INC       PMSQPTIO/INC
          INC       ALLREFIO/INC            * Allied Health Referrals
          INC       ALLGRPIO/INC            * Group bookings
          INC       ALLRLNIO/INC            * Linked referrals
          INC       ALLSTSIO/INC            * Referral Status History
          INC       ALLAUDIO/INC            * Referral Audit Table
          INC       OUTLKBIO/INC            * Outpatient linked bookings
          INC       OUTSESIO/INC            * Outpatient sessions
          INC       OUTMA1IO/INC            * Outpatient Clinic Masters
.
PTDTH999  ENDPROC
.............................................................................
