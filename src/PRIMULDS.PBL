.  ****************************************************************************
.  *                                                                          *
.  *  Program Name     :  PRIMULDS.PBL                                        *
.  *                                                                          *
.  *  Function         :  Displays the multiple services codes on file        *
.  *                                                                          *
.  *  Author           :  Stephen Armstrong      28/12/90                     *
.  *                                                                          *
.  *  Keys             :  KEY16                                               *
.  *                                                                          *
.  *  Requirements     :  SERDATE    DIM 8 - date of service                  *
.  *                      MULDESC    DIM 30 - description                     *
.  *                      MFLAG      FORM 1 - date flag                       *
.  *                                   0 = display codes for given date       *
.  *                                   1 = display codes regardless           *
.  *                      ZED11      INIT "zzzzzzzzzzz"                       *
.  *                      SHTDESC    DIM   25 - short description             *
.  *                                                                          *
.  *  Returns          :  OVRCD =1 if nothing on file                         *
.  *                                                                          *
.  *  Modifications    :                                                      *
.  *                                                                          *
.  ****************************************************************************
.
PRIMULDS 
.
          MOVE       ONE,CCOL
          DISPLAY    *P1:4,*EF,*P30:4,*V2LON,*ULON,"MULTIPLE SERVICES CODES"
.
          KEYIN      *P5:6,"Description : ",*DV,UNDLN30:
                     *P19:6,*V2LON,MULDESC
.
          RESET      MULDESC
          IF         @EOS
            PACK     KEY46,SP30,SP20
          ELSE
            PACK     KEY46,MULDESC,SP30,SP20
          ENDIF
.
          DISPLAY    *P1:5,*EF,*V2LON,"Description",*P29:5,"Code":
                     *P40:5,*EF,*V2LON,"Description",*P68:5,"Code"
.
          MOVE       FIVE,CVERT
.
          CALL       RSPRMU2                     * anything on file ?
          CALL       RKPRMU2
          BRANCH     OVRCD,MULDS950              * no
.
          GOTO       MULDS110
.
MULDS100  CALL       RKPRMU2                     * read next record
          BRANCH     OVRCD,MULDS300
.
MULDS110  BRANCH     MFLAG,MULDS130              * ignore code date
          MATCH      PRMUDATE,SERDATE            * date < file date ?
          GOTO       MULDS100 IF LESS            * yes - get next record
.
MULDS130  ADD        ONE,CVERT
          COMPARE    TWENTY3,CVERT               * page full ?
          GOTO       MULDS150 IF LESS            * no
.
          COMPARE    ONE,CCOL                    * second half of screen full ?
          GOTO       MULDS200 IF NOT EQUAL       * yes
.
          MOVE       FORTY,CCOL                  * setup for second half
.                                                  of screen display
.         MOVE       SIX,CVERT
.
MULDS150  MOVE       PRMUDESC,SHTDESC
          DISPLAY    *PCCOL:CVERT,SHTDESC,SP3,PRMUCODE
.
          PACK       KEY46,PRMUDESC,PRMUCODE,ZED11
          CALL       RSPRMU2                     * position in file
          GOTO       MULDS100
.
.         Page full
.
MULDS200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, ":
                    "(",*V2LON,"F",*HOFF,")irst Screen, ":
                    "(",*V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
          REP       UPPLOW,ANS
.
          CMATCH    ANSC,ANS                     * continue ?
          GOTO      MULDS300 IF EQUAL
.
          CMATCH    ANSF,ANS                     * first screen ?
          GOTO      PRIMULDS IF EQUAL
.
          CMATCH    ANSN,ANS                     * next screen ?
          GOTO      MULDS400 IF EQUAL
.
          BEEP
          GOTO      MULDS200
.
MULDS300  MOVE      ZERO,OVRCD
          GOTO      MULDS9999
.
.         Clear the screen
.
MULDS400  MOVE      SIX,CVERT
          MOVE      ONE,CCOL 
          DISPLAY   *P1:CVERT,*EF
.
          GOTO      MULDS150
.
MULDS950  DISPLAY   *P1:24,*EL,*B,"No Multiple service codes found. ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL
.
MULDS999  RETURN
