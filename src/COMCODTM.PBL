.------------------------------------------------------------
.  Standard Coded Field Option 
.  
.  Parameters    CATEGORY - Common Category
.                CODE     - Common Code
.                CODEDATE - Date used to compare effective from & to dates
.
.  Tag Parameter 0=Description
.                1=Code
.                2=Selection List
.                3=Code with Indicators
.                4=Selection List With A Default Code Selected (PTCDDEFT)
.                  This will not select the code in the parameter CODE
.                  (Use for Add Screens)
.                5=Selection List With A Default Code Selected (PTCDDEFT)
.                  This will select the code in the parameter CODE if it is
.                  not blank (Use for Update Screens)
.
.  Modifications
.
.  V11.05.02 20.12.2024 DA Sarkies      TSK 0954547
.            Commented out <cat> tags to prevent browser incompatibility
.  V11.05.01 26.11.2024  Ebon Clements  Task 0954547
.            Added <cat-c> start and end tag to category=XX tag
.------------------------------------------------------------
. Common Codes Field Item option
.------------------------------------------------------------
COMITM00  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,COMITM10,COMITM20,COMITM30,COMITM40,COMITM50
          CALL      GTCMCD00
          WRITE     HTMLFILE;CMCDDESC;
          GOTO      COMITM99
.
COMITM10  PACK      CODE,CODE,SP70
          MATCH     SP70,CODE
          GOTO      COMITM99 IF EQUAL
          WRITE     HTMLFILE;CODE;
          GOTO      COMITM99
.
COMITM20  CALL      CMCDSL00
          GOTO      COMITM99
.
COMITM30  CALL      GTCMCD00
          WRITE     HTMLFILE;CMCDACOD,CMCDSI01,CMCDSI02,CMCDSI03,CMCDSI04:
                             CMCDSI05,CMCDSI06,CMCDSI07,CMCDSI08,CMCDSI09:
                             CMCDSI10,CMCDSI11,CMCDHDPE,CMCDNATE,CMCDASSC;
.
          GOTO      COMITM99
.
COMITM40  CALL      CMCDDS00        * Selection list with default code selected
          GOTO      COMITM99
.
COMITM50  CALL      CMCDUS00        * Selection list with default code selected
          GOTO      COMITM99        * if the variable CODE is blank
.
COMITM99  RETURN
.------------------------------------------------------------
. Get Code Description
.------------------------------------------------------------
GTCMCD00  MOVE      SP70,CMCDDESC
          MOVE      SP70,CMCDACOD
          UNPACK    SP70,CMCDSI01,CMCDSI02,CMCDSI03,CMCDSI04,CMCDSI05:
                         CMCDSI06,CMCDSI07,CMCDSI08,CMCDSI09,CMCDSI10:
                         CMCDSI11
          MATCH     SP70,CODE
          GOTO      GTCMCD99 IF EQUAL
.
          CALL      MPCMCT00     * Map Category to Equivelent Code
.
          PACK      KEY5,CATEGORY,CODE,SP70
          CALL      RDCMCOD1
          BRANCH    OVRCD,GTCMCD20
.
          MATCH     SP70,CMCDFMDT       * if form date exists
          GOTO      GTCMCD99 IF EQUAL
          MATCH     CMCDFMDT,CODEDATE   * check from date is before code date
          GOTO      GTCMCD20 IF LESS
.
          MATCH     SP70,CMCDTODT       * if to date exists
          GOTO      GTCMCD99 IF EQUAL
          MATCH     CODEDATE,CMCDTODT   * check to date is after code date
          GOTO      GTCMCD20 IF LESS
.
          GOTO      GTCMCD99
.
GTCMCD20  MOVE      SP70,CMCDACOD
          MOVE      SP70,CMCDDESC
          UNPACK    SP70,CMCDSI01,CMCDSI02,CMCDSI03,CMCDSI04,CMCDSI05:
                         CMCDSI06,CMCDSI07,CMCDSI08,CMCDSI09,CMCDSI10:
                         CMCDSI11
GTCMCD99  RETURN
.------------------------------------------------------------
. Add Selection Options to Form (code value)
.------------------------------------------------------------
CMCDSL00  MOVE      SP70,CMCDDESC
          PACK      KEY5,CATEGORY,SP70
          CALL      RDCMCOD1
          BRANCH    OVRCD,CMCDSL11
.
          MATCH     SP70,CMCDFMDT       * if form date exists
          GOTO      CMCDSL12 IF EQUAL
          MATCH     CMCDFMDT,CODEDATE   * check from date is before code date
          GOTO      CMCDSL99 IF LESS
.
          MATCH     SP70,CMCDTODT       * if to date exists
          GOTO      CMCDSL12 IF EQUAL
          MATCH     CODEDATE,CMCDTODT   * check to date is after code date
          GOTO      CMCDSL99 IF LESS
.
          GOTO      CMCDSL12
.
CMCDSL11  MOVE      CATEGORY,CMCDTCOD
          MOVE      "Invalid Category",CMCDDESC

CMCDSL12  REP       " _",CMCDTCOD
          REP       " _",CMCDHDPE
          WRITE     HTMLFILE;"<!--<cat-c category=#"",CMCDTCOD,"#" name=#"":
                             CMCDDESC:
                             "#" map=#"",CMCDHDPE,"#"></cat-c>-->"
.  
          CALL      MPCMCT00     * Map Category to Equivelent Code
.
          PACK      KEY25,CATEGORY,SP70
          CALL      RSCMCOD2
CMCDSL20  CALL      RKCMCOD2
          BRANCH    OVRCD,CMCDSL99
.
          MATCH     CATEGORY,CMCDTCOD
          GOTO      CMCDSL99 IF NOT EQUAL
          MATCH     SP10,CMCDACOD        * ignore category row
          GOTO      CMCDSL20 IF EQUAL
.
          MATCH     SP70,CMCDFMDT       * if form date exists
          GOTO      CMCDSL21 IF EQUAL
          MATCH     CMCDFMDT,CODEDATE   * check from date is before code date
          GOTO      CMCDSL20 IF LESS
.
          MATCH     SP70,CMCDTODT       * if to date exists
          GOTO      CMCDSL21 IF EQUAL
          MATCH     CODEDATE,CMCDTODT   * check to date is after code date
          GOTO      CMCDSL20 IF LESS
.
CMCDSL21  PACK      KEY11,CMCDSI01,CMCDSI02,CMCDSI03,CMCDSI04,CMCDSI05:
                          CMCDSI06,CMCDSI07,CMCDSI08,CMCDSI09,CMCDSI10:
                          CMCDSI11
          PACK      KEY14,CMCDHDPE,CMCDNATE,CMCDASSC
          PACK      KEY30,QUOTE,CMCDACOD,KEY11,KEY14,QUOTE
.
          MATCH     CODE,CMCDACOD
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY30," selected>":
                               CMCDDESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY30,">",CMCDDESC,"</option>"
          ENDIF
          GOTO      CMCDSL20
.
CMCDSL99  RETURN
.------------------------------------------------------------
. Map Common Category based on THCSCOD in Category Record
.------------------------------------------------------------
MPCMCT00  PACK      KEY5,CATEGORY,SP70
          CALL      RDCMCOD1
          BRANCH    OVRCD,MPCMCT99
.
          MATCH     SP70,CMCDFMDT       * if form date exists
          GOTO      MPCMCT10 IF EQUAL
          MATCH     CMCDFMDT,CODEDATE   * check from date is before code date
          GOTO      MPCMCT99 IF LESS
.
          MATCH     SP70,CMCDTODT       * if to date exists
          GOTO      MPCMCT10 IF EQUAL
          MATCH     CODEDATE,CMCDTODT   * check to date is after code date
          GOTO      MPCMCT99 IF LESS
.
MPCMCT10  MATCH     SP70,CMCDHDPE
          GOTO      MPCMCT99 IF EQUAL
.
          MOVE      CMCDHDPE,KEY2
          PACK      KEY5,KEY2,SP70
          CALL      RDCMCOD1
          BRANCH    OVRCD,MPCMCT99
.
          MATCH     SP70,CMCDFMDT       * if form date exists
          GOTO      MPCMCT20 IF EQUAL
          MATCH     CMCDFMDT,CODEDATE   * check from date is before code date
          GOTO      MPCMCT99 IF LESS
.
          MATCH     SP70,CMCDTODT       * if to date exists
          GOTO      MPCMCT20 IF EQUAL
          MATCH     CODEDATE,CMCDTODT   * check to date is after code date
          GOTO      MPCMCT99 IF LESS
.
MPCMCT20  MOVE      CMCDTCOD,CATEGORY
.
MPCMCT99  RETURN
.
.-------------------------------------------------------------------
. Common Code Default Selection List
.-------------------------------------------------------------------
CMCDDS00  MOVE      ZERO,FORM1
          MOVE      SP70,CMCDDESC
          PACK      KEY5,CATEGORY,SP70
          CALL      RDCMCOD1
          BRANCH    OVRCD,CMCDDS11
.
          MATCH     SP70,CMCDFMDT       * if form date exists
          GOTO      CMCDDS12 IF EQUAL
          MATCH     CMCDFMDT,CODEDATE   * check from date is before code date
          GOTO      CMCDDS99 IF LESS
.
          MATCH     SP70,CMCDTODT       * if to date exists
          GOTO      CMCDDS12 IF EQUAL
          MATCH     CODEDATE,CMCDTODT   * check to date is after code date
          GOTO      CMCDDS99 IF LESS
.
          GOTO      CMCDDS12
.
CMCDDS11  MOVE      CATEGORY,CMCDTCOD
          MOVE      "Invalid Category",CMCDDESC
.
CMCDDS12  REP       " _",CMCDTCOD
          REP       " _",CMCDHDPE
          WRITE     HTMLFILE;"<!--<cat-c category=#"",CMCDTCOD,"#" name=#"":
                             CMCDDESC:
                             "#" map=#"",CMCDHDPE,"#"></cat-c>-->"
.
          CALL      MPCMCT00     * Map Category to Equivalent Code
.
          PACK      KEY25,CATEGORY,SP70
          CALL      RSCMCOD2
CMCDDS20  CALL      RKCMCOD2
          BRANCH    OVRCD,CMCDDS99
.
          MATCH     CATEGORY,CMCDTCOD
          GOTO      CMCDDS99 IF NOT EQUAL
          MATCH     SP10,CMCDACOD
          GOTO      CMCDDS20 IF EQUAL
.
          MATCH     SP70,CMCDFMDT       * if form date exists
          GOTO      CMCDDS30 IF EQUAL
          MATCH     CMCDFMDT,CODEDATE   * check from date is before code date
          GOTO      CMCDDS20 IF LESS
.
          MATCH     SP70,CMCDTODT       * if to date exists
          GOTO      CMCDDS30 IF EQUAL
          MATCH     CODEDATE,CMCDTODT   * check to date is after code date
          GOTO      CMCDDS20 IF LESS
.
CMCDDS30  PACK      KEY11,CMCDSI01,CMCDSI02,CMCDSI03,CMCDSI04,CMCDSI05:
                          CMCDSI06,CMCDSI07,CMCDSI08,CMCDSI09,CMCDSI10:
                          CMCDSI11
          PACK      KEY14,CMCDHDPE,CMCDNATE,CMCDASSC
          PACK      KEY30,QUOTE,CMCDACOD,KEY11,KEY14,QUOTE
.
          BRANCH    FORM1,CMCDDS40     * Only 1 code can be selected in list
.
          MATCH     "1",CMCDDEFT
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY30," selected>":
                               CMCDDESC,"</option>"
            MOVE      ONE,FORM1
            GOTO      CMCDDS20
          ENDIF
.
CMCDDS40  WRITE     HTMLFILE;"<option value=",KEY30,">",CMCDDESC,"</option>"
          GOTO      CMCDDS20
.
CMCDDS99  RETURN
.
.-------------------------------------------------------------------
. Common Codes Selection List with User defined Default (CODE)
.-------------------------------------------------------------------
CMCDUS00  MOVE      ZERO,FORM1
          MOVE      SP70,CMCDDESC
          PACK      KEY5,CATEGORY,SP70
          CALL      RDCMCOD1
          BRANCH    OVRCD,CMCDUS11
.
          MATCH     SP70,CMCDFMDT       * if form date exists
          GOTO      CMCDUS12 IF EQUAL
          MATCH     CMCDFMDT,CODEDATE   * check from date is before code date
          GOTO      CMCDUS99 IF LESS
.
          MATCH     SP70,CMCDTODT       * if to date exists
          GOTO      CMCDUS12 IF EQUAL
          MATCH     CODEDATE,CMCDTODT   * check to date is after code date
          GOTO      CMCDUS99 IF LESS
.
.
CMCDUS11  MOVE      CATEGORY,CMCDTCOD
          MOVE      "Invalid Category",CMCDDESC
.
CMCDUS12  REP       " _",CMCDTCOD
          REP       " _",CMCDHDPE
          WRITE     HTMLFILE;"<!--<cat-c category=#"",CMCDTCOD,"#" name=#"":
                             CMCDDESC:
                             "#" map=#"",CMCDHDPE,"#"></cat-c>-->"
.
          CALL      MPCMCT00     * Map Category to Equivelent Code
.
          PACK      KEY25,CATEGORY,SP70
          CALL      RSCMCOD2
CMCDUS20  CALL      RKCMCOD2
          BRANCH    OVRCD,CMCDUS99
.
          MATCH     CATEGORY,CMCDTCOD
          GOTO      CMCDUS99 IF NOT EQUAL
          MATCH     SP70,CMCDACOD
          GOTO      CMCDUS20 IF EQUAL
.
          MATCH     SP70,CMCDFMDT       * if form date exists
          GOTO      CMCDUS30 IF EQUAL
          MATCH     CMCDFMDT,CODEDATE   * check from date is before code date
          GOTO      CMCDUS20 IF LESS
.
          MATCH     SP70,CMCDTODT       * if to date exists
          GOTO      CMCDUS30 IF EQUAL
          MATCH     CODEDATE,CMCDTODT   * check to date is after code date
          GOTO      CMCDUS20 IF LESS
.
CMCDUS30  PACK      KEY11,CMCDSI01,CMCDSI02,CMCDSI03,CMCDSI04,CMCDSI05:
                          CMCDSI06,CMCDSI07,CMCDSI08,CMCDSI09,CMCDSI10:
                          CMCDSI11
          PACK      KEY14,CMCDHDPE,CMCDNATE,CMCDASSC
          PACK      KEY30,QUOTE,CMCDACOD,KEY11,KEY14,QUOTE
.
          BRANCH    FORM1,CMCDUS40     * Only 1 code can be selected in list
.
          MATCH     SP70,CODE
          IF        !@EQUAL
            MATCH     CODE,ACODE
            IF        @EQUAL
              WRITE     HTMLFILE;"<option value=",KEY30," selected>":
                                 CMCDDESC,"</option>"
              GOTO      CMCDUS20
            ENDIF
            GOTO        CMCDUS40
          ENDIF
.
          MATCH     "1",CMCDDEFT
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY30," selected>":
                               CMCDDESC,"</option>"
            MOVE      ONE,FORM1
            GOTO      CMCDUS20
          ENDIF
.
CMCDUS40  WRITE     HTMLFILE;"<option value=",KEY30,">",CMCDDESC,"</option>"
          GOTO      CMCDUS20
.
CMCDUS99  RETURN
