.------------------------------------------------------------
.  File          :  PATKEOPR.PBL
.
.  Function      :  Update Keyword Index File for Doctors Table
.
.  Parameters    :  PTERCODE - Key to Doctors Table
.
.  Modifications :  
.                   V10.05.01 12/09/2014  J.Tan         CAR 274958
.                   Added Organisation code keyword
.                   V9.11.00  22/01/2009  Mike Laming   CAR 188016
.                   New Program - External Organisation Table Keyword generator
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Procedure to Update
.  a Key Word Index Table that can be used for searching 
.  Masterfiles. The procedure should be called from the 
.  Add, Update and Delete functions in the Masterfile 
.  Maintenance Function.
.  
.  A Key Word Index Table is Defined in the following Manner
.                              
.  PTDKxxxx    DIM       xx    * Code Field Segment 1
.  PTDKyyyy    DIM       xx    * Code Field Segment n
.  PTDKKWD     DIM       15    * Key Word
.  PTDKSPA     DIM       10    * Spare
.
.  Index 1   PTDKxxxx, PTDKyyyy, PTDKKWD
.  Index 2   PTDKKWD, PTDKxxxx, PTDKyyyy
.
.  To convert this Procedure the following should be performed
.    Global Change PAT to the System ID eg :%s/PAT/PAT/g
.    Global Change CKI to the File   ID eg :%s/CKI/CKI/g
.    Global Change PTDK to the IO Call ID eg :%s/PTDK/PTDK/g
.    Modify the KEYINDEX defintion to combined Code Field Lengths eg 6
.    Global Change KEY21 to the Key for the Key Word Table eg KEY21
.    Change xxxxxxx to the Code Fields eg PTERCODEhh
.    Change the GENR0000 routine to call the BWORD000 for each
.    string of key words to be indexed for the Coded Field
.    eg MOVE    DSNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    DGNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    TDESC,KEYWORDS
.       CALL    BWORD000
.------------------------------------------------------------
          DEFPROC   PATKEOUP
.
          INC       PATKEOFD/INC
.
KEYINDEX  DIM       7         * Type/Code Index in Table
KEYWORDS  DIM       60        * String Containing the Key Words to be Indexed
SIXTY     FORM      "60"
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
CATEI     INIT      "EI"
.------------------------------------------------------------
. Generate Word Index
.------------------------------------------------------------
GENR0000  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     PATKEOA1,"patkeoaf"
          TRAPCLR  IO
          BRANCH   OVRCD,GENR9999
.
          PACK     KEYINDEX,PTERTYPE,PTERCODE,SP10
          CALL     CLEAR000                 * Remove Current Key Word Index
          BRANCH   EXIT,GENR9999
.
          PACK     KEY7,PTERTYPE,PTERCODE   * Validate Record on File
          CALL     RDPTEOR1
          BRANCH   OVRCD,GENR1900           * If Not then it's a Delete/Clear
.
          PACK     KEYINDEX,PTERTYPE,PTERCODE,SP10
.
          MOVE     PTERADD3,KEYWORDS        * Suburb keywords
          CALL     BWORD000
.
          MATCH    SP70,PTERUTYP
          IF       !@EQUAL
            MOVE     "Cat.EI-",KEY7
            PACK     TDESC,KEY7,PTERUTYP,SP30
            PACK     KEY5,CATEI,PTERUTYP,SP5  * Unit (Business) Type
            CALL     RDCODE1                  * Cat."EI"
            MOVE     TDESC,KEYWORDS
            CALL     BWORD000
          ENDIF
.
          MOVE     PTERCODE,KEYWORDS        * Organisation Code keywords
          CALL     BWORD000
          MOVE     PTERNAME,KEYWORDS        * Organisation Name keywords
          CALL     BWORD000
.
GENR1900  CLOSE    PATKEOA1
.
GENR9999  GOTO     PATKEOEP
.------------------------------------------------------------
. Clear Current Key Words
.------------------------------------------------------------
CLEAR000  PACK     KEY22,KEYINDEX,SP70
          CALL     RSPTKEO1
          IF       OVRCD = 0
            CALL     RPPTKEO1
          ENDIF
          CALL     RKPTKEO1
          BRANCH   OVRCD,CLEAR999
          PACK     KEY7,PTKOTYPE,PTKOCODE,SP10
          MATCH    KEYINDEX,KEY7
          GOTO     CLEAR999 IF NOT EQUAL
.
          PACK     KEY22,PTKOTYPE,PTKOCODE,PTKOKWRD,SP30
          CALL     DEPTKEO1
          GOTO     CLEAR000
.
CLEAR999  RETURN
.-------------------------------------------------
. Determine Words to Index
.-------------------------------------------------
BWORD000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      BWORD000 IF NOT EOS
            GOTO      BWORD999               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWORDS,SP30      * reset form pointer
          MOVE      KEY60,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      BWORD100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Save this Word
.
BWORD100  MOVE      KEYWORDS,KEY16
          REP       UPPLOW,KEY16             * Always Upper Case
          SQUEEZE   KEY16
          MOVELPTR  KEY16,F3
          COMPARE   "3",F3
          GOTO      BWORD190 IF LESS         * get rid of short words
          MATCH     "SCHOOL",KEY16
          GOTO      BWORD190 IF EQUAL        * get rid of useless words
.
          PACK      KEY15,KEY16,SP70
          PACK      KEY22,KEYINDEX,KEY15
          UNPACK    KEY22,PTKOTYPE,PTKOCODE,PTKOKWRD
          CALL      RDPTKEO1
          IF        OVRCD=1
            CALL      WRPTKEO1
          ENDIF
          GOTO      BWORD190
.
.         Check for any more words
.
BWORD190  SETLPTR   KEYWORDS,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY60,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY60,KEYWORDS
          GOTO      BWORD000
.
BWORD999  RETURN
.
          INC       PATKEOIO/INC
          INC       IBAOVRIO/INC
.
PATKEOEP  ENDPROC                          * End of Procedure
.
