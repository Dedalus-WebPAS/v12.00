.******************************************************************************
.* Include Name   : COPYMCOD.PBL                                              *
.*                                                                            *
.* Function       : Copy coding from previous admission                       *
.*                                                                            *
.* Author         : Jill Parkinson 23/5/2011                                  *
.*                                                                            *
.* Parameters     : ADMISSNO        New Admission number                      *
.*                  CPYADMNO        Previous admission to copy from           *
.*                                                                            *
.* Modifications  :                                                           *
.* V10.05.01 17/07/2014  Ebon Clements    CAR 251384                          *
.*           Clear sub system key for ibapolaf write                          *
.* V10.03.07 16/04/2013  Steve Armstrong  CAR 281702                          *
.*           Removed code to execute Grouper and instead replaced with        *
.*           code to write a record to ibapolaf to Group in the background.   *
.*           Also removed all associated FD's, IO's, OPEN and CLOSE           *
.*           statements as well as code INCLUDES which were previously used   *
.*           for Grouping.                                                    *
.* V10.03.06 08/08/2012  Davin        CAR 270114                              *
.*           Added WIES19 and removed open/close of patw15af                  *
.* V10.03.05 07/06/2012  Mike Laming    CAR 266421                            *
.*           Added include CLPATCHC - for webgroup(CPDATS00) for Grouper Dates*
.* V10.03.04 31/05/2012  Jill Parkinson CAR 265258                            *
.*           Added open of patmx1af and packing of ENCSTDAT,ENCSTTIM,ENCENDAT,*
.*           ENCENTIM                                                         *
.* V10.03.03 01/02/2012  Ebon Clements     CAR 257648                         *
.*           Set procedure date to admission date - OPRCPY00                  *
.* V10.03.02 15/12/2011  Jill Parkinson    CAR 257475                         *
.*           Added include of PATMA1FD                                        *
.* V10.03.01 06/12/2011  Ebon Clements     CAR 253911                         *
.*           Don't update acoddte if there was no coding to copy              *
.* V10.02.05 12/10/2011  Steve Armstrong   CAR 238236                         *
.*           Added WIES18                                                     *
.* V10.02.04 29/08/2011  Jill Parkinson CAR 248100                            *
.*           Mods to not update acoddte if an error occurred                  *
.* V10.02.03 24/08/2011  Jill Parkinson CAR 249209                            *
.*           Added close of files                                             *
.* V10.02.02 03/08/2011  Jill Parkinson CAR 240712                            *
.*           Added warning message                                            *
.* V10.02.01 23/05/2011  Jill Parkinson CAR 240712                            *
.*           Created                                                          *
.******************************************************************************
.
          DEFPROC   COPYMCOD
.
          INC       PATDSCFD/INC
          INC       PATECDFD/INC
          INC       PATECOFD/INC
          INC       PATECMFD/INC
          INC       PATICOFD/INC
          INC       PATICDFD/INC
          INC       PATMA1FD/INC
          INC       PATMI1FD/INC
          INC       PATVISFD/INC
.
.*********************************************************************
.         mainline code
.*********************************************************************
.
CMDLINE   DIM       200
CMPCNT    FORM      1
CODED     INIT      "D "
CODEDD    INIT      "DD"
COD1CNT   FORM      2
COD9CNT   FORM      2
CODETYPE  DIM       1       * Type of code, O - operation
.                                           D - disease
COPYCODE  FORM      1
COUNTER3  FORM      3
CURRDATE  DIM       8       * Current Date
DISCOUNT  FORM      3       * Diagnosis Code Counter      
DISECODE  DIM       5
DISTYPE   DIM       1       * Disease type
DRGCODE   DIM       3                       * DRG code
DRGINP    INIT      "drginp"
DRGOUTFN  DIM       12
DSSCODE   DIM       9       * Disease/operation code
ENCOUTAF  FILE      ASCII,FIXED=256
ENCOUTFN  DIM       12
ENCFILEF  FILE      ASCII,FIXED=256
ENCDSP    DIM       1
ENCINP    INIT      "encinp"
ENCODERF  DIM       1
EPSCD001  FORM      2 
EPSNUMBR  DIM       2       * Episode Number
FILENAM1  DIM       8
FILENAM2  DIM       8
ICDCODE   DIM       9       * Disease/operation code
LASTDIAG  FORM      3       * Last Item of Diagnosis array
LASTPROC  FORM      3       * Last Item of Procedure array
LOADFILE  FILE      ASCII,FIXED=200
LOADNAME  DIM       8
LOADCNT   FORM      3
MDCCODE   DIM       3                       * MDC code
NOOFCODE  FORM      2
OPERCLIN  DIM       10      * Operation Clinician Code  
OPERDATE  DIM       8       * Operation date (CCYYMMDD)
OPERSTME  DIM       5       * Operation start time
OPERETME  DIM       5       * Operation finish time
OPERTYPE  DIM       1       * Operation type
OPRCODE   DIM       9       * Operation code
PROCCODE  DIM       7
SAVKEY13  DIM       13
SVEOCLIN  DIM       10      * Save field for Oper Clinician
SVEODATE  DIM       8       * Save field for Oper Date
TYPOFCOD  DIM       2
VISITNUM  DIM       8
.
CATU1     INIT      "U1"
CATU2     INIT      "U2"
CATU3     INIT      "U3"
CATU4     INIT      "U4"
CATU5     INIT      "U5"
CATU6     INIT      "U6"
CATU7     INIT      "U7"
CATVI     INIT      "VI"
.
ENCERR01  INIT      "Backup at Key Word Prompt               "
ENCERR02  INIT      "Abort at Key Word Prompt                "
ENCERR03  INIT      "No Entry at Key Word Prompt             "
ENCERR04  INIT      "Next at Key Word Prompt                 "
ENCERR05  INIT      "Invalid File Format                     "
ENCERR06  INIT      "Bad Command or Command Unauthorised     "
ENCERR07  INIT      "Error Opening Interface Input File      "
ENCERR08  INIT      "Error Opening Interface Output File     "
ENCERR09  INIT      "Output File Already Exists              "
ENCERR10  INIT      "Invalid DRG / Fiscal Year               "
ENCERR12  INIT      "Invalid Codes Passed to Application     "
ENCERR34  INIT      "Not Authorised                          "
ENCERR37  INIT      "Invalid or No Age in Input File         "
ENCERR38  INIT      "Invalid or No Gender in Input File      "
ENCERR39  INIT      "Invalid or Not Integrated in Input File "
ENCERR40  INIT      "Invalid or No State in Input File       "
ENCERR41  INIT      "Invalid or No Payor in Input File       "
ENCERR42  INIT      "Invalid Discharge Date in Input File    "
TILDA10   INIT      "~~~~~~~~~~"
.
.
COPYC000  READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          OPEN      PATDSCH1,"patdschf"
          OPEN      PATECDA1,"patecdaf"
          OPEN      PATECOA1,"patecoaf"
          OPEN      PATICDO1,"paticdof"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATVISA1,"patvisaf"
          OPEN      PATECMA1,"patecmaf"
          CALL      OPICD1
          CALL      OPICO1
.
.
.    ***** Copy previous discharge coding *****
.
COPYC100  CALL      PRVDSN00                * Get previous discharge record
          BRANCH    EXIT,COPYC900
.
          CALL      SETACC00        * Set acoddte
          CALL      DISSQE00        * Resequence Disease Codes & MDS Data
          CALL      PROSQE00        * Resequence Procedure Codes & MDS Data
.
.         Write a record to ibapolaf to group the patient so that
.         PATWEB89 is not waiting around for the grouper to finish.
.         Instead, COMWEB70 will process the grouping.
.
          OPEN      IBAPOLA1,"ibapolaf"
          MOVE      ZERO,FORM10                  * initialise unique count
.
          MOVE      TILDA10,KEY10
          CALL      RSIBPOL1                     * position at end of file
          CALL      RPIBPOL1                     * read previous record
          BRANCH    OVRCD,COPYC200               * eof - finished
.
          MOVE      IBPLUNIQ,FORM10              * load counter
COPYC200  ADD       ONE,FORM10                   * increment counter
.
          MOVE      FORM10,IBPLUNIQ
          MOVE      SP70,IBPLSKEY
          MOVE      SP70,IBPLSPAR
          MOVE      "005",IBPLTYPE
          MOVE      URNUMBER,IBPLURNO
          MOVE      ADMISSNO,IBPLVISN
.
          MOVE      FORM10,KEY10
          CALL      RAIBPOL1                     * record already on file ?
          IF        OVRCD = 1
            MOVE      ZERO,OVRCD                 * no - write new record
            TRAP      OVERCOND IF IO
            CALL      WRIBPOL1
            TRAPCLR   IO
            BRANCH    OVRCD,COPYC200
          ELSE
            GOTO      COPYC200                   * yes - get next counter
          ENDIF
.
          CLOSE     IBAPOLA1
          MOVE      ZERO,EXIT
          GOTO      COPYC999
.
COPYC900  MOVE      ZERO,EXIT
          CLOSE     PATDSCH1
          CLOSE     PATECDA1
          CLOSE     PATECOA1
          CLOSE     PATICDO1
          CLOSE     PATMA1A1
          CLOSE     PATMI1A1
          CLOSE     PATVISA1
          CLOSE     PATECMA1
          CALL      CLICD1
          CALL      CLICO1
.
          GOTO      COPYC999
.
.-------------------------------------------------------------------------------
.                         ******* PRVDSN00 ********
.    Copy the selected discharge coding record for this patient?
.    First read the visit file by UR and then check Discharge file for
.    this admission
.-------------------------------------------------------------------------------
PRVDSN00  MOVE      ZERO,EXIT
          MOVE      ZERO,EPSNUMBR
          MOVE      ONE,EPSCD001
          MOVE      ADMISSNO,VISITNUM     * Visit no save variable
.
          PACK      KEY8,CPYADMNO,SP70
          CALL      RDPTVIS1
          BRANCH    OVRCD,PRVDSN90
.
          PACK      KEY8,DPVIBILL,SP70 * Read Discharge File for a valid record
          CALL      RDADSCH1
          BRANCH    OVRCD,PRVDSN90     * Record not found read next visit
.
          PACK      KEY8,DPVIBILL,SP70 * Read Admission File for a valid record
          CALL      RDMISS1            * and check ACODDTE if blank?
          BRANCH    OVRCD,PRVDSN90     * Record not found read next visit
.
.   Check for an existing episode with Coding for this patient
.
          MOVE      DPVIBILL,VISITNUM  * Set for GETEPS00 & Last Visit found
          CALL      GETEPS00           * Get first existing pisodes with Coding
          MATCH     " 0",EPSNUMBR
          GOTO      PRVDSN90 IF EQUAL  * No Episode found for this Admission
.
.   Clear the current Episode we are copying to.
.
          CALL      DELLCK00  * Delete Diagnosis Lock Record if found
          CALL      DELECD00  * Delete all records from Diagnosis Coding file
          CALL      DELECO00  * Delete all records from Procedure Coding file
.
.   Read and Copy the Previous Discharge Episode
.
          MOVE      ZERO,COPYCODE                * Set coding copied to no
.
          PACK      SAVKEY13,VISITNUM,EPSNUMBR,SP70
          CALL      OPRCPY00  * Copy Procedures Episode File
.
          PACK      SAVKEY13,VISITNUM,EPSNUMBR,SP70
          CALL      DISCPY00  * Copy Diagnosis Episode File
.
          COMPARE   ZERO,COPYCODE                * No coding copied
          GOTO      PRVDSN90 IF EQUAL
.
          MOVE      ZERO,EXIT
          GOTO      PRVDSN99
.
PRVDSN90  MOVE      ONE,EXIT
          GOTO      PRVDSN99
.
PRVDSN99  RETURN
.
+
.*****************************************************************************
.  Resequence Morbidity Data Free Format File & patecdaf
.  Write Unique Counter to Disease file if records exist for this Admission
.*****************************************************************************
DISSQE00  MOVE      ZERO,COUNTER3
.
          MOVE      ZERO,COUNTER3
.
          PACK      KEY13,ADMISSNO,SP70
          CALL      RSPTECD1              * Episode disease file record
DISSQE10  CALL      RKPTECD1              * Episode disease file record
          BRANCH    OVRCD,DISSQE99
.
          MATCH     ADMISSNO,DPTEDADM
          GOTO      DISSQE99 IF NOT EQUAL * Different admin no
.
          ADD       ONE,COUNTER3
          MOVE      COUNTER3,PTEDUNQN      * Write Unique Counter
.
          CALL      UPPTECD1              * Update an eps dis coding file rec
.
          GOTO      DISSQE10             * Read Next Record
.
DISSQE99  CALL      PTHCS000                                            *214276
          RETURN
.*****************************************************************************
.  Resequence Morbidity Data Free Format File & patecoaf
.  Write Unique Counter to Procedure file if records exist for this Admission
.*****************************************************************************
PROSQE00  MOVE      ZERO,COUNTER3
.
          MOVE      ZERO,COUNTER3
.
          PACK      KEY13,ADMISSNO,SP70
          CALL      RSPTECO1              * Episode Procedure File
PROSQE10  CALL      RKPTECO1              * Episode Procedure File
          BRANCH    OVRCD,PROSQE99
.
          MATCH     ADMISSNO,DPTEOADM
          GOTO      PROSQE99 IF NOT EQUAL * Different admin no
.
          ADD       ONE,COUNTER3
          MOVE      COUNTER3,PTEOUNQN      * Write Unique Counter
.
          CALL      UPPTECO1              * Update an eps pro coding file rec
.
          GOTO      PROSQE10             * Read Next Record
.
PROSQE99  CALL      PTHCS000                                           * 214276
          RETURN
+
.----------------------------------------------------------------
.  Get a Patients first existing Episode for this admission no.
.  Else return " 0" in EPSNUMBR
.----------------------------------------------------------------
GETEPS00  MOVE      SP70,EPSNUMBR  * Episode No - Return Variable
          MOVE      ZERO,ECDFLAG   * Diagnosis Episode Flag
          MOVE      ZERO,ECOFLAG   * Procedure Episode Flag
.
          PACK      KEY13,VISITNUM,SP70   * Check Diagnosis Coding file
          CALL      RSPTECD1
GETEPS10  CALL      RKPTECD1
          IF        OVRCD=1
            MOVE      ONE,ECDFLAG
            GOTO      GETEPS20
          ENDIF
.
          MATCH     VISITNUM,DPTEDADM    * Check for Admission No
          IF        !@EQUAL
            MOVE      ONE,ECDFLAG
            GOTO      GETEPS20
          ENDIF
.
          MATCH     SP70,PTEDCODE
          IF        !@EQUAL
            MOVE      PTEDEPNO,EPSNUMBR  * Only need First Episode found!
            GOTO      GETEPS99
          ENDIF
.
GETEPS20  PACK      KEY13,VISITNUM,SP70   * Check Procedures Coding File
          CALL      RSPTECO1
GETEPS21  CALL      RKPTECO1
          IF        OVRCD=1
            MOVE      ONE,ECOFLAG
            GOTO      GETEPS90
          ENDIF
.
          MATCH     VISITNUM,DPTEOADM    * Check for Admission No
          IF        !@EQUAL
            MOVE      ONE,ECOFLAG
            GOTO      GETEPS90
          ENDIF
.
          MATCH     SP70,PTEOCODE
          IF        !@EQUAL
            MOVE      PTEOEPNO,EPSNUMBR  * Only need First Episode found
            GOTO      GETEPS99
          ENDIF
.
          IF        ECDFLAG=1
            GOTO    GETEPS21
          ELSE
            GOTO    GETEPS10
          ENDIF
.
GETEPS90  MATCH     SP70,EPSNUMBR    * If nothing is found then default values
          IF        @EQUAL
            RESET     EPSNUMBR
            MOVE      " 0",EPSNUMBR
          ENDIF
.
GETEPS99  RETURN
+
.-------------------------------------------------------------------------------
.                        ******* DELLCK00 ********
. ----- Delete Lock record for Coding Screen if found -----
.-------------------------------------------------------------------------------
DELLCK00  MOVE      ZERO,PTEDCNT
          MOVE      ZERO,FORM2
          PACK      KEY13,ADMISSNO,FORM2,PTEDCNT,SP70
          CALL      RDPTECD1
          BRANCH    OVRCD,DELLCK99
.
          PACK      KEY13,PTEDADMN,PTEDEPNO,PTEDCNT,SP70
          CALL      DEPTECD1                * Delete an eps dis coding file rec
.
DELLCK99  RETURN
.-------------------------------------------------------------------------------
.                        ******* DELECD00 ********
. ----- Delete all the records from the medical records disease file ----
.-------------------------------------------------------------------------------
DELECD00  PACK      KEY13,ADMISSNO,EPSCD001,SP70
DELECD10  CALL      RSPTECD1                * Position on & read a patient
          CALL      RKPTECD1                  episode disease file record
          BRANCH    OVRCD,DELECD99
.
          MATCH     ADMISSNO,DPTEDADM
          GOTO      DELECD99 IF NOT EQUAL   * Different admin no
.
          COMPARE   EPSCD001,PTEDEPNO
          GOTO      DELECD99 IF NOT EQUAL   * Different episode no
.
          PACK      KEY13,PTEDADMN,PTEDEPNO,PTEDCNT,SP70
          CALL      DEPTECD1                * Delete an eps dis coding file rec
          GOTO      DELECD10
.
DELECD99  RETURN
.-------------------------------------------------------------------------------
.                        ******* DELECO00 ********
. ----- Delete all the records from the medical records Procedure file ----
.-------------------------------------------------------------------------------
DELECO00  UNPACK    SP70,SVEOCLIN,SVEODATE
          PACK      KEY13,ADMISSNO,EPSCD001,SP70
DELECO10  CALL      RSPTECO1                * Position on & read a patient
          CALL      RKPTECO1                * episode operation file record
          BRANCH    OVRCD,DELECO99
.
          MATCH     ADMISSNO,DPTEOADM
          GOTO      DELECO99 IF NOT EQUAL   * Different admin no
.
          COMPARE   EPSCD001,PTEOEPNO
          GOTO      DELECO99 IF NOT EQUAL   * Different episode no
.
          PACK      KEY13,PTEOADMN,PTEOEPNO,PTEOCNT
          CALL      DEPTECO1                * Delete an eps opr coding file rec
.
          MATCH     "0",MRCNCLIN                                      *I-240103
          IF        !@EQUAL
            MATCH     SP10,PTEOCLIN                                   *I-240103
            IF        @EQUAL
              MOVE      PTEOCLIN,SVEOCLIN     * save first Opr Clinician & Date
              MOVE      PTEODATE,SVEODATE
            ENDIF
          ENDIF
          GOTO      DELECO10
.
DELECO99  RETURN
+
.-----------------------------------------------------------------
.   Read and Copy the Previous Discharge Episode - Procedures Episode File
.-----------------------------------------------------------------
OPRCPY00  PACK      KEY13,SAVKEY13,SP70                               *C-240107
          CALL      RSPTECO1                * Position on & read a patient
          CALL      RKPTECO1                * episode operation file record
          BRANCH    OVRCD,OPRCPY99
.
          MATCH     VISITNUM,DPTEOADM       * Admission not found
          GOTO      OPRCPY99 IF NOT EQUAL
.
          MATCH     EPSNUMBR,DPTEOEPN       * Episode Not found
          GOTO      OPRCPY99 IF NOT EQUAL
.
          PACK      SAVKEY13,DPTEOADM,DPTEOEPN,DPTEOCNT,SP70  * Save Key
.
          MOVE      ADMISSNO,PTEOADMN
          MOVE      EPSCD001,PTEOEPNO
.
          MOVE      SP70,PTEOCLIN           * Clear Opr Clinician code *I-240103
.
          MOVE      SP70,PTEODATE           * Clear Operation date
          MATCH     Z70,PTMIS001
          IF        !@EQUAL
            MOVE      PTMIS001,PTEODATE     * Set operation date to adm date
          ENDIF
.
          MOVE      SP70,PTEOSTTM           * Clear Operation start time
          MOVE      SP70,PTEOEDTM           * Clear Operation end time
.
          CALL      IBACLOCK
          PACK      PTEODTCD,CCC,CYY,CMM,CDD
          REP       " 0",PTEODTCD            * Date of coding
          MOVE      CTIMEIS,PTEOTIME         * Time of coding
.
          MOVE      USERID,PTEOUSID          * Web User ID
.
          PACK      ICDRDDTE,ADATE,SP70
          PACK      KEY9,PTEOCODE
          CALL      RDPTICO1
          IF        OVRCD=1 | ICDRDVER=1
            MOVE      ONE,CODERROR
          ENDIF
.
          PACK      KEY13,PTEOADMN,PTEOEPNO,PTEOCNT,SP70
          CALL      RAPTECO1
          IF        OVRCD=1
            CALL      WRPTECO1       * Write record to destination
            MOVE      ONE,COPYCODE   * Coding copied yes
          ENDIF
.
          GOTO      OPRCPY00
.
OPRCPY99  RETURN
.-----------------------------------------------------------------
.   Read and Copy the Previous Discharge Episode - Diagnosis Episode File
.-----------------------------------------------------------------
DISCPY00  PACK      KEY13,SAVKEY13,SP70
          CALL      RSPTECD1                * Position on & read a patient
          CALL      RKPTECD1                * episode disease file record
          BRANCH    OVRCD,DISCPY99
.
          MATCH     VISITNUM,DPTEDADM       * Admission not found
          GOTO      DISCPY99 IF NOT EQUAL
.
          MATCH     EPSNUMBR,DPTEDEPN       * Episode Not found
          GOTO      DISCPY99 IF NOT EQUAL
.
          PACK      SAVKEY13,DPTEDADM,DPTEDEPN,DPTEDCNT,SP70  * Save Key
.
          MOVE      ADMISSNO,PTEDADMN
          MOVE      EPSCD001,PTEDEPNO
.
          CALL      IBACLOCK
          PACK      PTEDDTCD,CCC,CYY,CMM,CDD
          REP       " 0",PTEDDTCD            * Date of coding
          MOVE      CTIMEIS,PTEDTIME         * Time of coding
.
          MOVE      USERID,PTEDUSID          * Web User ID
          PACK      ICDRDDTE,ADATE,SP70
          PACK      KEY9,PTEDCODE
          CALL      RDPTICD1
          IF        OVRCD=1 | ICDRDVER=1
            PACK      CODERROR,PTEDCODE,SP70
          ENDIF
.
          PACK      KEY13,PTEDADMN,PTEDEPNO,PTEDCNT,SP70
          CALL      RAPTECD1
          IF        OVRCD=1
            CALL      WRPTECD1       * Write record to destination
            MOVE      ONE,COPYCODE   * Copy coding yes
          ENDIF
.
          GOTO      DISCPY00
.
DISCPY99  RETURN
+
.******************************************************************************
.*                                  PTHCS000              Called by: MEDCOD00 *
.*             ----- Update The Episode Morbidity Changes File -----          *
.******************************************************************************
PTHCS000  PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
.
          COMPARE   THREE,PTCNHDPS          * State Indicator
          GOTO      PTHCS999 IF NOT EQUAL   * Not Using Morbidity Changes File
.
          MOVE      SP1,CKYIDEF1            * 1st record flag - default
          UNPACK    CURRDATE,CKYIDEF6
.
          PACK      KEY18,ADMISSNO,EPSCD001,SP20
PTHCS100  CALL      RSPTECM1                * Position on & read an episode
PTHCS200  CALL      RKPTECM1                * morbidity changes file record
          BRANCH    OVRCD,PTHCS300
.
          MATCH     ADMISSNO,DPTEMADM
          GOTO      PTHCS300 IF NOT EQUAL   * Different admin no
.
          COMPARE   EPSCD001,PTEMEPNO
          GOTO      PTHCS300 IF NOT EQUAL   * Different Episode no
.
          UNPACK    PTEMCDTE,KEY6
          MATCH     CKYIDEF6,KEY6
          GOTO      PTHCS200 IF NOT EQUAL   * Different change month
.
          PACK      KEY18,PTEMADMN,PTEMEPNO,PTEMCDTE
          CALL      DEPTECM1                * Delete an eps morb change file rec
          MOVE      ANSC,CKYIDEF1           * 1st record flag - changed
.
          GOTO      PTHCS100
.
PTHCS300  MOVE      ONE,FORM3
          PACK      KEY13,ADMISSNO,EPSCD001,FORM3,SP20                *C-240107
          CALL      RDPTECD1         * Read episode disease file record
          BRANCH    OVRCD,PTHCS400
.
          CALL      WTECM000         * Write a record of eps changes file
.
PTHCS400  MOVE      ONE,FORM3
          PACK      KEY13,ADMISSNO,EPSCD001,FORM3,SP20                *C-240107
          CALL      RDPTECO1         * Read episode operation file record
          BRANCH    OVRCD,PTHCS999
.
          MOVE      PTEOADMN,PTEDADMN
          MOVE      PTEOEPNO,PTEDEPNO
          CALL      WTECM000        * Write a record of eps changes file
.
PTHCS999  RETURN
+
.-------------------------------------------------------------------------------
.                        ******** SETACC00 ********
. ----- Set ACODDTE VARIABLE to current date ----
.-------------------------------------------------------------------------------
SETACC00  MATCH     SP70,CODERROR
          GOTO      SETACC99 IF NOT EQUAL
.
          PACK      KEY8,ADMISSNO
          CALL      RLPTMIS1            * Read with lock
          BRANCH    OVRCD,SETACC99                                    *I-145686
.
          PACK      ACODDTE,CCC,CYY,CMM,CDD
          REP       " 0",ACODDTE        * Date Coding Completed
          CALL      UPPTMIS1            * Update & unlock
          CALL      UUPTMIS1            * Read with lock
.
SETACC99  RETURN
.
PATGNFEE  RETURN
WTECM000  RETURN
+
          INC       CLPATDSC
          INC       CLPATCHC
          INC       PATGILOS
          INC       PATDSCIO/INC
          INC       PATECDIO/INC
          INC       PATECOIO/INC
          INC       PATECMIO/INC
          INC       PATICOIO/INC
          INC       PATICDIO/INC
          INC       PATMA1IO/INC
          INC       PATMI1IO/INC
          INC       PATVISIO/INC
          INC       IBAOVRIO/INC
.
COPYC999  ENDPROC
+
