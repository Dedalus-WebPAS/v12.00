. **********************************************************************
. *              P R O G R A M  S U M M A R Y                          *
. *              -------------  -------------                          *
. *                                                                    *
. * Program Name:           IBAPAT51                                   *
. *                                                                    *
. * Function:               CODES MASTER REGISTER LISTING              *
. *                                                                    *
. * Date Written:           12/06/85                                   *
. *                                                                    *
. * Author:                 MALCOLM HAYSE (I.B.A)                      *
. *                                                                    *
. * Modifications:                                                     *
. *               V9.03.01  15/04/2004  Davin        CAR 18702         *
. *                         Added category security groups to report   *
. **********************************************************************
. *               V9.02.01  18/07/2002  Dean Cramer  srf 19727         *
. *                         Added PTCOACTV & PTCDDEFT to report        *
. *               V5.08.01  14/09/2000  Jill Habasque                  *
. *                         Added PTCDGRPV - Grouper Version           *
. *               V5.08.B01 04/03/2000  Steve Armstrong                *
. *                         Added TCINDC11                             *
. **********************************************************************
. *                V5.07.01 05/03/1999  Jill Habasque                  *
. *                         Added TCINDC6 to TCINDC10                  *
. *                V5.01.02 22/02/94 Matt Surridge                     *
. *                         Added HEAD132, CONTQST, DISPHEAD and       *
. *                         generaly upgraded standards.               *
. *                V5.01.03 11/04/94 Sandra Barcham                    *
. *                         New Keyins                                 *
. **********************************************************************
.
          INC     STD001FD
.
          INC     PATCODFD/INC
          INC     IBACCSFD/INC
.
.       WORK AREA
.
BTYPE     DIM       10
.
CDESC     DIM       20
CHCSCOD   DIM       4
CODE      DIM       2
.
ENDCAT    DIM       2
.
JNLDESC   DIM       21
.
PRIVATE   INIT      "(Private) "
.
SAVCOD    DIM       3
SECGRPS   DIM       100
SHARED    INIT      "(Shared)  "
STARTCT   DIM       2
STCODE    DIM       2
.
PRGID     INIT      "IBAPAT51"
PRGNAM    INIT      "Patient Codes Register Listing"
VERSION   INIT      "V12.00.00"
.
.         START OF PROGRAM
.
         CALL      DISPHEAD
         DISPLAY   *P56:24,"Opening patcodes";
         OPEN      PATCODE1,"patcodes"
         DISPLAY   *P56:24,"Opening ibaccsaf";
         OPEN      IBACCSA2,"ibaccsaf"
.
START    MOVE      ZERO,CPAGENO
         MOVE      ONE,CNOUNDLN
         MOVE      ZERO,CPAGENO
         CLOCK     TIME,CTIMEIS
.
         HLINE     *G37,2,55,80
         DISPLAY   *P1:3,*EF:
                   *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                   *P1:5,*V2LON,ONE,*HOFF," = Category Sequence"
.
START0   KEYIN     *P1:7,*EL,"Select Option : ",*V2LON,OPTION;
.
         COMPARE   ZERO,OPTION
         GOTO      ENDIT IF EQUAL
.
         BRANCH    OPTION OF CATSEQ
.
         BEEP 
         GOTO      START0
+
.        Get the range of categories to print
.
CATSEQ   MOVE      SP3,SAVCOD
         DISPLAY   *P1:9,*EF,"Category From : ":
                   *P1:10,*EL,"Category To   : "
         MOVE      TEN7,ECOL
         MOVE      NINE,EVERT
         MOVE      ONE,CNEWDS
         CALL      PATCATKY
         BRANCH    EXIT,CKALL,START
         MOVE      CODE,STARTCT
         GOTO      CKCAT
.   
CKALL    MOVE         SP2,STARTCT
         MOVE         "START               ",TDESC
.
CKCAT    DISPLAY      *P26:9,TDESC
.
.        Get the ending category
.
KEYEND   MOVE         TEN,EVERT
         CALL         PATCATKY
         BRANCH       EXIT,KEYEND1,START
         MOVE         CODE,ENDCAT
         GOTO         CKECAT
.
KEYEND1  MOVE         "ZZ",ENDCAT
         MOVE         "FINISH              ",TDESC
.
CKECAT   DISPLAY      *P26:10,TDESC
.
         MATCH        STARTCT,ENDCAT
         GOTO         REKEY IF NOT LESS
.
         DISPLAY      *P1:24,*EL,*B:
                      "Start Must Be Less Than End. ";
         CALL         HITENTER
         GOTO         CATSEQ
.
REKEY    CALL         CONTQST
         BRANCH       CEXIT,PRTSEQ,CATSEQ,START 
.
PRTSEQ   PACK         KEY5 WITH STARTCT,SP3
.
        DISPLAY    *P1:24,*EL,"Category : ":
                   *P40:24,"Code : ";
.
         CALL         RDCODE1
         BRANCH       OVRCD OF POSRD
         GOTO         NEXTCC
.
POSRD    CALL         RDSCODE1
         MOVE         ZERO,CLNO
.
NEXTC    CALL         RDKCODE1
         BRANCH       OVRCD OF ENDP
.
NEXTCC   MATCH        TCODE,ENDCAT
         GOTO         ENDP IF LESS
.
         MATCH        TCODE,SAVCOD
         GOTO         PRTC IF EQUAL
.
         MOVE         TCODE,SAVCOD
         MOVE         TDESC,CDESC
         MOVE         THCSCOD,CHCSCOD
.
         CALL         GETSEC00              * get security groups
.
         DISPLAY      *P15:24,*V2LON,TCODE,SP1,CDESC;
         CALL         HEAD
.
PRTC     MATCH        SP3,ACODE 
         GOTO         NEXTC IF EQUAL
.
         DISPLAY      *P50:24,*V2LON,ACODE,SP1,TDESC;
         CALL         DISPDA
         GOTO         NEXTC
.
.           OVER CONDITION FOUND..PRINT LAST LINE OF -- AND DISPLAY MESSAGE
.
ENDP     COMPARE      ZERO,CPAGENO
         GOTO         NOPAGE IF EQUAL
         CALL         PAGEND
         PRINT        *N,"*** End of Report ***"
         GOTO         START
.
NOPAGE   DISPLAY      *P1:24,*EL,*B:
                      "No Data To Be Printed. ";
         CALL         HITENTER
         GOTO         START
+
.
.     PRINT ACTUAL DATA 
.
DISPDA    COMPARE   FIFTY6,CLNO
          CALL      HEAD IF NOT LESS
.
          MATCH     SP1,PTCOACTV
          IF    @EQUAL
             MOVE   "A",PTCOACTV
          ENDIF
.
          MATCH     "1",PTCDDEFT
          IF    @EQUAL
             MOVE   "Y",PTCDDEFT
            ELSE
             MOVE   SP1,PTCDDEFT
          ENDIF     
.
          MATCH     "BT",TCODE
          GOTO      DBEDTYP IF EQUAL
.
          MATCH     "J ",TCODE
          GOTO      DISPDA1 IF NOT EQUAL
.
          MOVE      THCSCOD,ANS
          MATCH     ANSA,ANS
          GOTO      DISPADJ IF EQUAL
          MATCH     ANSB,ANS
          GOTO      DISPBAD IF EQUAL
          MATCH     ANSR,ANS
          GOTO      DISPREB IF EQUAL
          GOTO      DISPNOR
.
DISPDA1   PRINT     *1,"|  ",ACODE,*11,"|  ",TDESC:
                    *46,"| ",THCSCOD,*61,"|  ",TCFORM6:
                    *76,"| ",TCINDC1,TCINDC2,TCINDC3,TCINDC4:
                                     TCINDC5,TCINDC6,TCINDC7:
                                     TCINDC8,TCINDC9,TCINDC10:
                             TCINDC11,TCINDC12,TCINDC13,TCINDC14:
                                     TCINDC15,TCINDC16,TCINDC17:
                                     TCINDC18,TCINDC19,TCINDC20:
                                     TCINDC21,*100,"|  ",PTCDGRPV,*111,"|":
                    *115,PTCOACTV,*120,"|",*125,PTCDDEFT,*130,"|"
          ADD       ONE,CLNO
          RETURN
.
DISPADJ   MOVE      " (Adjustment Journal)" TO JNLDESC
          GOTO      DISPJNL
.
DISPBAD   MOVE      " (Bad Debt Journal)  " TO JNLDESC
          GOTO      DISPJNL
.
DISPREB   MOVE      " (Rebate Adjustment) " TO JNLDESC
          GOTO      DISPJNL
.
DISPNOR   MOVE      "                     " TO JNLDESC
.
DISPJNL   PRINT     *1,"|  ",ACODE,*19,"|  ",TDESC,JNLDESC:
                    *61,"|  ",TCFORM6:
                    *76,"| ",TCINDC1,TCINDC2,TCINDC3,TCINDC4:
                                     TCINDC5,TCINDC6,TCINDC7:
                                     TCINDC8,TCINDC9,TCINDC10:
                             TCINDC11,TCINDC12,TCINDC13,TCINDC14:
                                     TCINDC15,TCINDC16,TCINDC17:
                                     TCINDC18,TCINDC19,TCINDC20:
                                     TCINDC21,*100,"|  ",PTCDGRPV,*111,"|":
                    *115,PTCOACTV,*120,"|",*125,PTCDDEFT,*130,"|"
          ADD       ONE,CLNO
          RETURN
.
DBEDTYP   MOVE      SP10,BTYPE
          LOAD      BTYPE USING TCFORM6 OF PRIVATE,SHARED
          PRINT     *10,"|  ",ACODE,*11,"|  ",TDESC,SP1,BTYPE:
                    *46,"| ",THCSCOD,*61,"|  ",TCFORM6:
                    *76,"| ",TCINDC1,TCINDC2,TCINDC3,TCINDC4:
                                     TCINDC5,TCINDC6,TCINDC7:
                                     TCINDC8,TCINDC9,TCINDC10:
                             TCINDC11,TCINDC12,TCINDC13,TCINDC14:
                                     TCINDC15,TCINDC16,TCINDC17:
                                     TCINDC18,TCINDC19,TCINDC20:
                                     TCINDC21,*100,"|  ",PTCDGRPV,*111,"|":
                    *115,PTCOACTV,*120,"|",*125,PTCDDEFT,*130,"|"
          ADD       ONE,CLNO
          RETURN
+
.
.          LAST LINE ON THE PAGE
.
PAGEND    PRINT     *1,"*----------------------------------------------":
                       "------------------------------------------------":
                       "----------------------------------*"
          ADD       ONE,CLNO
          RETURN
+
.
.                 HEADINGS FOR OUTPUT
.
HEAD      COMPARE   ZERO,CPAGENO
          CALL      PAGEND IF NOT EQUAL
.
          CALL      HEAD132
.
          PRINT     *N,*40,"Category  : ",TCODE,"  ",CDESC:
                    *N,*40,"Default HDP Code : ",CHCSCOD:
                    *N,*40,"Security Groups : ",SECGRPS:
                    *N
          CALL      PAGEND
          PRINT     *1,"|  Code":
                    *11,"|  Description",*46,"| HDP Equiv.":
                    *61,"| Assoc. No.",*76,"|     Indicators",*100,"| ":
                    "Grouper",*111,"| Active | Default |"
          CALL      PAGEND
.
          MOVE      SEVEN,CLNO
          RETURN
+
. Get Security Groups for a category
.
GETSEC00  PACK      SECGRPS,SP70,SP70,SP70
          STRIP     SECGRPS
          PACK      KEY5,TCODE,SP70
          CALL      RSIBCCS2
GETSEC10  CALL      RKIBCCS2
          BRANCH    OVRCD,GETSEC99
.
          MATCH     TCODE,IBCCCATG
          GOTO      GETSEC99 IF NOT EQUAL
.
          STRIP     IBCCSGRP
          PACK      SECGRPS,SECGRPS,IBCCSGRP,COMMA
          STRIP     SECGRPS
          GOTO      GETSEC10
.
GETSEC99  RETURN
+
.
ENDIT    CHAIN      PGM
.
         INC        PATCATKY
         INC       PATCODIO/INC
         INC       IBACCSIO/INC
.
         INCLUDE    STD001IO 
.
