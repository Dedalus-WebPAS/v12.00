. *----------------------------------------------------------------------
. * Include Name  :   OUTCDCTM.PBL
. *
. * Function      :   CGI variables for OUTCDCFD - outcdcaf.dat
. *
. * Includes Needed : 
. *                   
. * Modifications   :
. *                   V11.03.01 06/12/2022  Bella Turco  TSK 0921957
. *                             Added PTCNNRTD & NOMORE00 to CDTAB000 routine 
. *                   V10.11.01 14/11/2017  Don Nguyen   TSK 0816814
. *                             Added OTCD1800 - VMTTAB00
. *                   V10.05.01 22/09/2014 Ebon Clements CAR 304958
. *                            Changed norecord of 99 to show all records
. *                            in the diagnosis search CDTAB000
. *                   V10.04.01 10/04/2014 Ebon Clements CAR 298805
. *                            Changed norecord test to 10 and
. *                            added next and prev functionality
. *                   V9.09.01 23/08/2007 Peter Vela CAR 145689
. *                            Modified maximum amount of Clinic Type diagnosis
. *                   V9.03.01 07/11/2003 Davin          
. *                            Created include
. *----------------------------------------------------------------------
.
.------------------------------------------------------------------------
.   Clear Parameters for Outpatients CDC Table
.------------------------------------------------------------------------
.
CPOTCD00  MOVE      Z70,OTCDC001
          MOVE      Z70,OTCDC002
          MOVE      Z70,OTCDC003
          MOVE      Z70,OTCDC004
          MOVE      Z70,OTCDC005
          MOVE      Z70,OTCDC006
          MOVE      Z70,OTCDC007
          MOVE      Z70,OTCDC008
          MOVE      Z70,OTCDC009
          MOVE      Z70,OTCDC010
          MOVE      Z70,OTCDC011
          MOVE      Z70,OTCDC012
.
CPOTCD99  RETURN
.
.------------------------------------------------------------
. Add fields for Outpatients CDC Table
.------------------------------------------------------------
OTCD0000  BRANCH    FLDITMNO,OTCD0100,OTCD0200,OTCD0300,OTCD0400,OTCD0500:
                             OTCD0600,OTCD0700,OTCD0800,OTCD0900,OTCD1000:
                             OTCD1100,OTCD1200,OTCD1300,OTCD1400,OTCD1500:
                             OTCD1600,OTCD1700,OTCD1800
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      OTCD9999
.
OTCD0100  WRITE     HTMLFILE;OTCDSITE;
          GOTO      OTCD9999
.
OTCD0200  WRITE     HTMLFILE;CLINTYPE;
          GOTO      OTCD9999
.
OTCD0300  WRITE     HTMLFILE;OTCDDIAG;
          GOTO      OTCD9999
.
OTCD0400  WRITE     HTMLFILE;OTCDDESC;
          GOTO      OTCD9999
.
OTCD0500  WRITE     HTMLFILE;OTCDICD;
          GOTO      OTCD9999
.
OTCD0600  WRITE     HTMLFILE;OTCDACTV;
          GOTO      OTCD9999
.
OTCD0700  MATCH     OTCDCDAT,SP70
          GOTO      OTCD9999 IF EQUAL
          UNPACK    OTCDCDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      OTCD9999
.
OTCD0800  WRITE     HTMLFILE;OTCDCTIM; 
          GOTO      OTCD9999
.
OTCD0900  MATCH     SP70,OTCDCUID
          GOTO      OTCD9999 IF EQUAL
          PACK      KEY10,OTCDCUID
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      OTCDCUID,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM;
          GOTO      OTCD9999
.
OTCD1000  MATCH     OTCDUDAT,SP70
          GOTO      OTCD9999 IF EQUAL
          UNPACK    OTCDUDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          GOTO      OTCD9999
.
OTCD1100  WRITE     HTMLFILE;OTCDUTIM;
          GOTO      OTCD9999
.
OTCD1200  MATCH     SP70,OTCDUUID
          GOTO      OTCD9999 IF EQUAL
          PACK      KEY10,OTCDUUID
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      OTCDUUID,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM;
          GOTO      OTCD9999
.
OTCD1300  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          IF        F1=1
            MOVE      ONE,CLSTFLAG          * display list for 'lookup'
          ELSE
            MOVE      ZERO,CLSTFLAG         * display table for maintenance
          ENDIF
.
          CALL      CDTAB000
          GOTO      OTCD9999
.
OTCD1400  CALL      NEXTCD00
          GOTO      OTCD9999
.
OTCD1500  CALL      PREVCD00
          GOTO      OTCD9999
.
OTCD1600  MATCH     SP70,OTCDICD
          IF        !@EQUAL
            PACK      KEY9,OTCDICD,SP70
            CALL      RDPT10D1
            IF        OVRCD=1
              MOVE    SP70,DDESC
            ENDIF
            WRITE     HTMLFILE;DDESC;
          ELSE
            MOVE      SP70,DDESC
            WRITE     HTMLFILE;DDESC;
          ENDIF
          GOTO      OTCD9999
.
OTCD1700  UNPACK    DIM127,D1,KEY1,DIM127    * Clinic Type
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,OTCD1710,OTCD1720
          PACK      KEY12,WBSESIT,CLINTYPE,SP70
          CALL      RDCTYA1                  * Read Clinic Type File
          BRANCH    OVRCD,OTCD9999
          WRITE     HTMLFILE;OCTDESC;        * Description
          GOTO      OTCD9999
.
OTCD1710  WRITE     HTMLFILE;CLINTYPE;       * Code
          GOTO      OTCD9999
.
OTCD1720  CALL      OTSCTY00                 * clinic type selection list
          GOTO      OTCD9999
.
OTCD1800  PACK      MAPTBIND,OTCDSITE,OTCDCTYP,OTCDDIAG,SP70
          CALL      VMTTAB00           * Clinical Coding System Mappings Table
          GOTO      OTCD9999
.
OTCD9999  RETURN
.
.------------------------------------------------------------
. Clinic Type List
.------------------------------------------------------------
OTSCTY00  PACK      KEY12,WBSESIT,SP70
          CALL      RDSCTYA1
OTSCTY10  CALL      RDKCTYA1
          BRANCH    OVRCD,OTSCTY99
.
          MATCH     OCTSITE,WBSESIT
          GOTO      OTSCTY10 IF NOT EQUAL
.
          MATCH     CLINTYPE,OCTCTYP
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=#"",OCTCTYP,"#" selected>":
                               OCTDESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=#"",OCTCTYP,"#">":
                               OCTDESC,"</option>"
          ENDIF
          GOTO      OTSCTY10
.
OTSCTY99  RETURN
.------------------------------------------------------------
. Table of Outpatients Clinic Type Diagnoses
.------------------------------------------------------------
CDTAB000  IF        CLSTFLAG=1
            WRITE     HTMLFILE;"<tr align=center>":
                               "<td class=HeadingCell colspan=2><b>":
                               CLINTYPE,"</td>":
                               "</tr>":
                               "<tr>":
                               "<td class=HeadingCell><b>Diagnosis</b></td>":
                               "<td class=HeadingCell><b>Description</b></td>":
                               "</tr>"
          ENDIF
.
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY21,WBSESIT,CLINTYPE,STARTKEY,SP70
          CALL      RSOTCDC1
          CALL      RPOTCDC1
.
CDTAB100  CALL      RKOTCDC1
          BRANCH    OVRCD,CDTAB900
.
          MATCH     WBSESIT,OTCDSITE
          GOTO      CDTAB900 IF NOT EQUAL
.
          MATCH     CLINTYPE,OTCDCTYP
          GOTO      CDTAB900 IF NOT EQUAL
.
          IF        NORECORD<>99
            ADD       ONE,RECNUMB
            IF        STARTNUM>=RECNUMB
              GOTO     CDTAB100
            ENDIF
          ENDIF
.
          CALL      CDROW000                * Display CDC Row
.
          COMPARE   NINTY9,NORECORD         * Display all records
          GOTO      CDTAB100 IF EQUAL
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      CDTAB100 IF NOT EQUAL
          GOTO      CDTAB999
.
CDTAB900  CALL      NOMORE00     * Display No More Records Message
.
CDTAB999  RETURN
.
.------------------------------------------------------------
.  Outpatients Clinic Type Diagnoses (TABLE ROW)
.------------------------------------------------------------
CDROW000  BRANCH  CLSTFLAG,CDROW100
.
          MATCH   "1",OTCDACTV
          IF      @EQUAL
             MOVE    "ACTIVE  ",ACTIDESC
          ELSE
             MOVE    "INACTIVE",ACTIDESC
          ENDIF
.
          MATCH   SP70,OTCDICD
          IF      @EQUAL
             MOVE    "&nbsp",OTCDICD
          ENDIF
.
          MATCH   SP70,ACTIDESC
          IF      @EQUAL
             MOVE    "&nbsp",ACTIDESC
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail09":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&clintype=",CLINTYPE:
                             "&otcdc003=",OTCDDIAG,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             OTCDDIAG,"</td>":
                             "<td>",OTCDDESC,"</td>":
                             "<td align=center>",OTCDICD,"</td>":
                             "<td align=center>",ACTIDESC,"</td>":
                             "</tr>"
          GOTO      CDROW999
.
CDROW100  MATCH   "1",OTCDACTV
          GOTO    CDROW999 IF NOT EQUAL
.
          WRITE     HTMLFILE;"<tr valign=top>":
                             "<td><a href=#"":
                    "javascript:updateParent('",OTCDDIAG,"','",OTCDDESC,"')#">":
                             OTCDDIAG,"</a>":
                             "</td><td>",OTCDDESC,"</td>";
.
CDROW999  RETURN
.
.-----------------------------------------------------------
. Link to Next Group
.-----------------------------------------------------------
NEXTCD00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,D3,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",CLINTYPE
          REP       " +",OTCDDIAG
          REP       " +",STARTKEY
.
          WRITE     HTMLFILE;"outweb05.pbl":
                                 "?reportno=",F2:
                                 "&template=",D3:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&clintype=",CLINTYPE:
                                 "&otcdc003=",OTCDDIAG:
                                 "&startkey=",STARTKEY;
.
          REP       "+ ",CLINTYPE
          REP       "+ ",OTCDDIAG
          REP       "+ ",STARTKEY
.
NEXTCD99  RETURN
.
.-------------------------------------------------------------
. Link to Prev Group
.-------------------------------------------------------------
PREVCD00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",OTCDDIAG
          REP       " +",CLINTYPE
          REP       " +",STARTKEY
.
          WRITE     HTMLFILE;"outweb05.pbl":
                                "?reportno=",F2:
                                "&template=",FLDPARA:
                                "&startnum=",KEY3:
                                "&norecord=",KEY2:
                                "&clintype=",CLINTYPE:
                                "&otcdc003=",OTCDDIAG:
                                "&startkey=",STARTKEY;
.
          REP       "+ ",OTCDDIAG
          REP       "+ ",CLINTYPE
          REP       "+ ",STARTKEY
.
PREVCD99  RETURN
.
.------------------------------------------------------------
.   Set Parameters for OUtpatients Clinic Type Diagnoses
.------------------------------------------------------------
SPOTCD00  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPOTCD01,SPOTCD02,SPOTCD03,SPOTCD04,SPOTCD05:
                       SPOTCD06,SPOTCD07,SPOTCD08,SPOTCD09,SPOTCD10:
                       SPOTCD11,SPOTCD12
.
          MOVE      ONE,EXIT
          GOTO      SPOTCD99
.
SPOTCD01  MOVE      QRYDATA,OTCDC001
          GOTO      SPOTCD99
.
SPOTCD02  MOVE      QRYDATA,OTCDC002
          GOTO      SPOTCD99
.
SPOTCD03  MOVE      QRYDATA,OTCDC003
          GOTO      SPOTCD99
.
SPOTCD04  MOVE      QRYDATA,OTCDC004
          GOTO      SPOTCD99
.
SPOTCD05  MOVE      QRYDATA,OTCDC005
          GOTO      SPOTCD99
.
SPOTCD06  MOVE      QRYDATA,OTCDC006
          GOTO      SPOTCD99
.
SPOTCD07  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      OTCDC007,CCENT,CYEAR,CMON,CDAY
          GOTO      SPOTCD99
.
SPOTCD08  MOVE      QRYDATA,OTCDC008
          GOTO      SPOTCD99
.
SPOTCD09  MOVE      QRYDATA,OTCDC009
          GOTO      SPOTCD99
.
SPOTCD10  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      OTCDC010,CCENT,CYEAR,CMON,CDAY
          GOTO      SPOTCD99
.
SPOTCD11  MOVE      QRYDATA,OTCDC011
          GOTO      SPOTCD99
.
SPOTCD12  MOVE      QRYDATA,OTCDC012
          GOTO      SPOTCD99
.
SPOTCD99  RETURN
.------------------------------------------------------------
.  Update outpatients clinic type diagnosis table
.------------------------------------------------------------
MVOTCD00  MOVE      WBSESIT,OTCDSITE
.>>>>     MATCH     OTCDC001,Z70
.>>>>     IF        !@EQUAL
.>>>>       MOVE      OTCDC001,OTCDSITE
.>>>>     ENDIF
.
          MATCH     OTCDC002,Z70
          IF        !@EQUAL
            MOVE      OTCDC002,OTCDCTYP
          ENDIF
.
          MATCH     OTCDC003,Z70
          IF        !@EQUAL
            MOVE      OTCDC003,OTCDDIAG
          ENDIF
.
          MATCH     OTCDC004,Z70
          IF        !@EQUAL
            MOVE      OTCDC004,OTCDDESC
          ENDIF
.
          MATCH     OTCDC005,Z70
          IF        !@EQUAL
            MOVE      OTCDC005,OTCDICD
          ENDIF
.
          MATCH     OTCDC006,Z70
          IF        !@EQUAL
            MOVE      OTCDC006,OTCDACTV
          ELSE
            MOVE      ZERO,OTCDACTV
          ENDIF
.
          MATCH     OTCDC007,Z70
          IF        !@EQUAL
            MOVE      OTCDC007,OTCDCDAT
          ENDIF
.
          MATCH     OTCDC008,Z70
          IF        !@EQUAL
            MOVE      OTCDC008,OTCDCTIM 
          ENDIF
.
          MATCH     OTCDC009,Z70
          IF        !@EQUAL
            MOVE      OTCDC009,OTCDCUID
          ENDIF
.
          MATCH     OTCDC010,Z70
          IF        !@EQUAL
            MOVE      OTCDC010,OTCDUDAT
          ENDIF
.
          MATCH     OTCDC011,Z70
          IF        !@EQUAL
            MOVE      OTCDC011,OTCDUTIM
          ENDIF
.
          MATCH     OTCDC012,Z70
          IF        !@EQUAL
            MOVE      OTCDC012,OTCDUUID
          ENDIF
.
MVOTCD99  RETURN
