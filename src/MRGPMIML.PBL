.******************************************************************************
.*         MRGPMIML                                                           *
.*  Mods:                                                                     *
.*                                                                            *
.******************************************************************************
.*        V10.11.02 21/09/2017  Thanh T.       TSK 0821710                    *
.*                  Audit Amission/outpatient visit comments                  *
.*        V10.11.01 13/09/2017  Thanh T.       TSK 0821710                    *
.*                  Audit Amission/outpatient visit comments                  *
.******************************************************************************
.*        V10.08.02 09/08/2016  Jill Parkinson TSK 0823914                    *
.*                  Added set of patma1af to XXFILE so correct error message  *
.*                  displays                                                  *
.*        V10.08.01 08/08/2016  Davin             TSK 0321234                 *
.*                  Added chronic condition alert (pmpxsn11)                  *
.******************************************************************************
.*        V10.06.01 29/07/2015  Don Nguyen       CAR 319786                   *
.*                  Fixed ALRT0000 & CHKALR00 to use the correct KEY16        *
.******************************************************************************
.*        V10.04.01 22/01/2014  Don Nguyen        CAR 295972                  *
.*                  Modified PRAD0000 - changed KEY16 to KEY24                *
.******************************************************************************
.*        V10.02.03 13/07/2011  Steve Armstrong   CAR 240722                  *
.*                  Further mods to cater for second given name as            *
.*                  part of PATGSRFD keys                                     *
.*        V10.02.02 22/06/2011  Steve Armstrong   CAR 240722                  *
.*                  Mods to cater for changes to PATGSRFD:                    *
.*                       - GSRSNAM & GSRGNAM extended to 30 chars.            *
.*                       - PTGSGNM2 added.                                    *
.*                       - all indexes changed in length.                     *
.*        V10.02.01 09/05/2011  Davin         CAR 239539                      *
.*                  Changed to allow 3 alert indicators to be saved (CHKALR00)*
.******************************************************************************
.*        V10.01.01 02/02/2011  Steve Armstrong   CAR 237520                  *
.*                  Mods to remove Detente interface (PATDETNT)               *
.******************************************************************************
.*        V10.00.02 12/04/2010  Steve Armstrong   CAR 219045                  *
.*                  Mods to populate PTMRSTIM.                                *
.*        V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                  *
.*                  Added HL7TRGID & HL7INCLD setting for all broadcast       *
.*                  messages                                                  *
.******************************************************************************
.*        V9.12.01  05/11/2009  Davin          CAR  209534                    *
.*                  Added call to PMIGTNID before merge broadcast in dgclicmr *
.******************************************************************************
.*        V9.10.01  01/07/2008  Jill Habasque  CAR  162660                    *
.*                  Mods for new alert functionality - added CHKALR00         *
.******************************************************************************
.*        V9.08.01  03/10/2006  Jill Habasque  CAR  107243                    *
.*                  Fixed update of alerts if CMERGEUR=1                      *
.******************************************************************************
.*        V9.07.01  21/09/2006  Ebon Clements CAR 107243                      *
.*                  Added deleted alert functionality                         *
.******************************************************************************
.*        V9.04.01  18/03/2005  Ebon Clements CAR 58443                       *
.*                  Added security alerts to set PTMXSIN1 to 2 if security    *
.*                  alerts  exist TCINDC4 = S                                 *
.******************************************************************************
.*        V9.03.02  29/04/2004  CAR 48411 Jill Habasque                       *
.*                  Added print for successfully merged UR numbers            *
.*        V9.03.01  15/07/2003  CAR 37193   Mike Laming                       *
.*                  Port V5.10 changes for PMS05 -                            *
.*                  Changes to copy Results Indicator to merged Master        *
.*                  as per the Case Notes Indicators (PHCNOTES)               *
.******************************************************************************
.*        V9.02.03  12/12/2002  Lina Vo                                       *
.*                  Added pmspx2af to MAST0000                                *
.*        V9.02.02  08/11/2001  Mike Laming                                   *
.*                  Rename DGCLIMRG to DGCLICMR                               *
.*        V9.02.01  08/09/2001  Glenn Saunders                                *
.*                  Change keys for patgsrn reads to reflect enhanced indexes.*
.******************************************************************************
.*        V5.10.B01 28/03/2001  Glenn Saunders                                *
.*                  Remove access to PTCNSNDX and associated processing.      *
.*                  Remove all references to PATSUR file (old surname file).  *
.*----------------------------------------------------------------------------*
.*                  08/10/1998  Glenn Berry      5.07 changes                 *
.*         08/01/1998  Steve Armstrong   SRF 643927                           *
.*         Mods to set OVRCD to "1" in WERR0000 to avoid infinite             *
.*         loop.                                                              *
.******************************************************************************
.
.******************************************************************************
.*                            COMM0000                                        *
.*                 Update Files Common to all Hospitals                       *
.******************************************************************************
.
COMM0000  CALL      OPNT0000
COMM1000  CALL      READ0000
          BRANCH    EXIT,COMM9999
.
          CALL      PRAD0000                * Patient Previous Addres File
COMM2000  CALL      GSRN0000                * double soundex surname file
COMM3000  CALL      URAU0000                * UR Audit File
          CALL      MAST0000                * Patient Master File
          CALL      NMPA0000                * National Master Patient
          CALL      LINK0000                * Linked UR File
          CALL      ALRT0000                * Alerts File
          CALL      CHKALR00                * Check alerts ptmxsin1
          CALL      UCSL0000                * case notes location file
          CALL      FDNR0000                * donor details file
          CALL      UKWC0000                * kiwicard details file
          PROC      PATCOM00                * U/R comments file
          PROC      PATAXE00                * Admission extension file
.
          CALL      PMIGTNID                * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      ONE,HL7TRGID
          MOVE      "MRGPMIML",HL7INCLD
          PROC      DGCLICMR                * send merge details to DG *C-90202
          PROC      MRGUREOC                * EOC & ACC
.
. -----   Update the Status in the Merge UR File  -----
.
          CALL      UPDT0000           * Update Request Merge UR File Status
          PRINT     *N,"UR Number",OLDURNO," merged to ",NEWURNO;
          GOTO      COMM1000
.
COMM9000  PRINT     *N,"Error in Merge UR File UR's ",OLDURNO,SP1,NEWURNO;
.
COMM9999  RETURN
+
.**********************************************************************
.*                            OPNT0000                                *
.*                  Open Temp Merge List Temp File                    *
.**********************************************************************
.
OPNT0000  MOVE      FALSE,OVRCD
          CLOSE     TEMP1                   * Position at Top of File
          TRAP      OVERCOND IF IO
          OPEN      TEMP1,FNAMER
          TRAPCLR   IO
OPNT9999  RETURN
+
.**********************************************************************
.*                            READ0000                                *
.*                 Read Next Temp Merge List Record                   *
.**********************************************************************
.
READ0000  READ      TEMP1,SEQ;OLDURNO,NEWURNO
          GOTO      READ9000 IF OVER
          MOVE      FALSE,EXIT
          GOTO      READ9999
READ9000  MOVE      TRUE,EXIT
READ9999  RETURN
.
.**********************************************************************
.*                            PRAD0000                                *
.*                  Update Previous Address File                      *
.**********************************************************************
.
PRAD0000  DISPLAY   *P1:24,*EL,*P30:24,"Fixing Previous Address File ";
.
          MOVE      "patpa1af",XXFILE
          TRAP      WERR0000 NORESET GIVING XXDESC IF IO
.
.       Change all the previous address records for this U/R number
.
PRAD1000  PACK      KEY24,OLDURNO,SP20
          CALL      RDSPADD1
          CALL      RDKPADD1
          BRANCH    OVRCD OF PRAD9999
.
          MATCH     PAURNO,OLDURNO
          GOTO      PRAD9999 IF NOT EQUAL
.
.       Check if new key already exists
.
          PACK      KEY24,NEWURNO,PADATE,PATIME
          CALL      RDAPADD1
          BRANCH    OVRCD OF PRAD2000
.
.       Delete current record
.
          PACK      KEY24,PAURNO,PADATE,PATIME
          CALL      DEPADD1
          GOTO      PRAD1000
.
.       Change U/R number for this record
.
PRAD2000  PACK      KEY24,PAURNO,PADATE,PATIME
          CALL      RDPADD1
          BRANCH    OVRCD OF PRAD3000
.
          MOVE      NEWURNO,PAURNO
          CALL      UPPADD1
          GOTO      PRAD1000
PRAD3000  PRINT     *N,"** Corrupted Previous Address Index for UR ",OLDURNO;
PRAD9999  RETURN
.
.**********************************************************************
.*                            GSRN0000                                *
.*                    Update Patient GSRName File                     *
.**********************************************************************
.
GSRN0000  DISPLAY   *P1:24,*EL,*P30:24,"Fixing surname Index File ";
.
.       Change all the surname index records for this U/R number
.
          MOVE      "patgsrnf",XXFILE
          TRAP      WERR0000 NORESET GIVING XXDESC IF IO
.
GSRN1000  MOVE      OLDURNO,GSRURNO
          PACK      KEY115,GSRURNO,SP70,SP70
          CALL      RSPTGSR1
          CALL      RKPTGSR1
          BRANCH    OVRCD OF GSRN9999
.
          MATCH     GSRURNO,OLDURNO
          GOTO      GSRN9999 IF NOT EQUAL
.
.       Check if this name combo already exists
.
          PACK      KEY115,NEWURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                           GSRSEX
          CALL      RDPTGSR1
          BRANCH    OVRCD OF GSRN2000
.
.       New name already exists - just delete old record
.
          PACK      KEY115,OLDURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                           GSRSEX
          CALL      DEPTGSR1
          GOTO      GSRN1000
.
.       Update this record with the new U/R number
.
GSRN2000  PACK      KEY115,GSRURNO,GSRBILL,GSRSNAM,GSRGNAM,PTGSGNM2,GSRDOB:
                           GSRSEX
          CALL      RDPTGSR1
          BRANCH    OVRCD OF GSRN3000
.
          MOVE      NEWURNO,GSRURNO
          CALL      UPPTGSR1
          GOTO      GSRN1000
.
GSRN3000  PRINT     *N,"Corrupted surname Index for UR ",OLDURNO;
GSRN9999  RETURN
.
.**********************************************************************
.*                            URAU0000                                *
.*                     Update UR Audit File                           *
.**********************************************************************
.
URAU0000  DISPLAY   *P1:24,*EL,*P30:24,"Fixing U/R Changes File ";
.
          MOVE      "paturadf",XXFILE
          TRAP      WERR0000 NORESET GIVING XXDESC IF IO
.
.       Update the PMI Change Audit file
.       The problems here are that there should only be up to two records
.       in the file, one with URRTYPE="A" and one with URRTYPE="C"
.
URAU1000  PACK      KEY17,OLDURNO,SP20
          CALL      RDSURAD2
          CALL      RDKURAD2
          BRANCH    OVRCD OF URAU3000
.
          MATCH     OLDURNO,URURNO
          GOTO      URAU3000 IF NOT EQUAL
.
.       Check if the new record exists already. If so, delete it.
.
          PACK      KEY17,URRTYPE,URDATE,NEWURNO
          CALL      RDAURAD1
          COMPARE   ZERO TO OVRCD
          CALL      DEURAD1 IF EQUAL
.
          PACK      KEY17,URRTYPE,URDATE,OLDURNO
          CALL      RDURAD1
          BRANCH    OVRCD OF URAU2000
.
          MOVE      NEWURNO,URURNO
          CALL      UPURAD1
          GOTO      URAU1000
URAU2000  PRINT     *N,"** Corrupted U/R Alteration Index for UR ",OLDURNO;
.
          GOTO      URAU9999
.
.       Second loop to get rid of the excess "A" and "C" records. We want the
.       First "A" record and the last "C" record
.
URAU3000  PACK      KEY17,NEWURNO,SP20
          CALL      RDSURAD2
          CALL      RDKURAD2            * This will read the first "A" record
          CALL      RDKURAD2            * This will read the second record
          BRANCH    OVRCD,URAU9999      * Nothing left in file -> Okay
.
          MATCH     URURNO,OLDURNO      * Still the same U/R ?
          GOTO      URAU9999 IF NOT EQUAL
.
.       If this is an "A" record then delete it. If it is a "C" record, then
.       loop until we find the last "C" record
.
          MATCH     ANSA,URRTYPE
          GOTO      URAU4000 IF NOT EQUAL
.
          PACK      KEY17,URRTYPE,URDATE,URURNO
          CALL      DEURAD1             * Delete this excess "A" record
          GOTO      URAU3000            * Check for anymore "A" records
.
.       Loop over the "C" records, and delete all but the last
.
URAU4000  PACK      KEY17,URRTYPE,URDATE,URURNO
          CALL      RDKURAD2            * Get the next record
          BRANCH    OVRCD,URAU9999      * Nothing more in file -> okay
.
          MATCH     URURNO,PURNO    * Still the same U/R number
          GOTO      URAU9999 IF NOT EQUAL
.
.       We have another record. Delete the old one
.
          CALL      DEURAD1         * Key saved above
          GOTO      URAU3000        * Check for any more "C" records
.
URAU9999  TRAPCLR   IO
          RETURN
.
.**********************************************************************
.*                            MAST0000                                *
.*                   Update Master File Details                       *
.**********************************************************************
.
MAST0000  DISPLAY   *P1:24,*EL,*P30:24,"Fixing Patient Master File ";
          MOVE      "patma1af",XXFILE
          TRAP      WERR0000 NORESET GIVING XXDESC IF IO
.
. *************************** These comments are wrong ***********************
. **---   If CMERGEUR = 0 then keep New UR Master file details   -----      **
. **---   If CMERGEUR = 1 then keep Old UR Master file details   -----      **
. *************************** They should read -  ****************************
.
. -----   If CMERGEUR = 1   -  save Old UR Master file details   -----
. -----   If CMERGEUR = 2   -  save New UR Master file details   -----
.
          BRANCH    CMERGEUR,MAST5000
.
.....     CALL      NMPI0000                * Merge NMPI numbers
.
MAST1000  MOVE      OLDURNO,KEY8            * Position on Old UR Record
          CALL      RDMAST1
          BRANCH    OVRCD,MAST4000
.
          MOVE      NEWURNO,PPSNAME         * Save New UR Number for KURN
          MOVE      ONE,PSTAT               * Update Status of old Record to
.
          MOVE      PTMXSIN2,DIM1           * save Results Ind.        *I-90301
          RESET     PHCNOTES
          MOVE      PHCNOTES,OLDCASE        * Save Old Case Notes Indicator
......    CALL      CASE0000                * Merge Case Notes Inds.    D-90301
.
          CALL      UPMAST1                 * say it has been merged
.
. ******************************************* Start of additional code *I-90301
          MOVE      NEWURNO,KEY8            * read the new UR
          CALL      RDMAST1
.                                           * Update the special fields
          MATCH     "1",PTMXSIN2            * Check old Results Ind.
          GOTO      MAST1500 IF EQUAL
          MOVE      DIM1,PTMXSIN2           * move new Results Ind.
MAST1500  CALL      CASE0000                * Merge Case Notes Inds.
.
          CALL      UPMAST1                 * update the new UR
. ******************************************* End of additional code   *I-90301
.
          MOVE      NEWURNO,KEY8            * check new UR record exists
          CALL      RDPMPX21                * read PMI extention file
          BRANCH    OVRCD,MAST3000
          GOTO      MAST9999
.
MAST3000  MOVE      OLDURNO,KEY8
          CALL      RDPMPX21                * read copy of old UR record
          MOVE      NEWURNO,PMPXURNO
          MOVE      NEWURNO,KEY8     
          CALL      WRPMPX21                * write new UR record
.
          GOTO      MAST9999
.
MAST4000  PRINT     *N,"Old UR ",PTMROLUR," Not on Master File";
          GOTO      MAST9999
.
MAST5000  
....      CALL      NMPI0000                * Merge NMPI numbers
.
MAST6000  MOVE      NEWURNO,KEY8            * Delete New UR Details
          PRINT     *N,"Replacing New UR ",NEWURNO," with Old UR ",OLDURNO;
          CALL      RDMAST1
          BRANCH    OVRCD,MAST9100
.
          MOVE      PTMXSIN2,DIM1           * save Results Ind.        *I-90301
          RESET     PHCNOTES
          MOVE      PHCNOTES,OLDCASE        * Save New UR's Case Notes
          CALL      DEMAST1
.
          MOVE      OLDURNO,KEY8            * Get Old UR Details
          CALL      RDMAST1
          BRANCH    OVRCD,MAST9000
.
          MOVE      NEWURNO,PURNO           * Write a New Record with New
          MOVE      NEWURNO,KEY8            * UR Number and Old UR Details
.
          CALL      CASE0000                * Merge Case Notes Indicator
.
.                                           * Merge Results Indicator  *I-90301
          MATCH     "1",PTMXSIN2            * Check old Results Ind.   *I-90301
          GOTO      MAST7000 IF EQUAL                                  *I-90301
          MOVE      DIM1,PTMXSIN2           * move new Results Ind.    *I-90301
.
MAST7000  PRINT     *N,"Writing UR ",KEY8;
          CALL      WRMAST1                                            *C-90301
.
          MOVE      OLDURNO,KEY8            * Position on old record again
          CALL      RDMAST1
          BRANCH    OVRCD,MAST4000
.
          MOVE      NEWURNO,PPSNAME         * Save New UR Number for KURN
          MOVE      ONE,PSTAT               * Update Status of old Record
.
          CALL      UPMAST1                 * to say it has been merged
.   
.     Check if new UR PMI record
.
          MOVE      NEWURNO,KEY8
          CALL      RDPMPX21                * read PMI extention file
          BRANCH    OVRCD,MAST8000
.
.     Delete new UR record and replace with old UR record
.
          MOVE      NEWURNO,KEY8
          CALL      DEPMPX21                * delete PMI record 
.
MAST8000  MOVE      OLDURNO,KEY8
          CALL      RDPMPX21
          MOVE      NEWURNO,PMPXURNO
          MOVE      NEWURNO,KEY8     
          CALL      WRPMPX21 
          GOTO      MAST9999
.
MAST9000  PRINT     *N,"Old UR ",OLDURNO," Not on Master File";
          GOTO      MAST9999
.
MAST9100  PRINT     *N,"New UR ",NEWURNO," Not on Master File";
          GOTO      MAST9999
.
MAST9999  TRAPCLR   IO
          RETURN
.**********************************************************************
.*                            NMPA0000                                *
.*                  Merge The National Patient Index file             *
.**********************************************************************
.
NMPA0000  COMPARE   PTCNNMPI,ZERO
          GOTO      NMPA9999 IF EQUAL
          DISPLAY   *P1:24,"Updating : ",*+,PTCNNMDX,*EL
.
          OPEN      PATNIDA1,"patnidaf"
.
.         Change the nmpi number record for this U/R number
.         We are merging if CMERGURS is true else changing u/r.
.
          MOVE      SP20,SAVNMPI
.
.         Check if new key already exists
.
          PACK      KEY10,CHOSINDX,NEWURNO
          CALL      RDPTNID1
          IF        OVRCD=0
            GOTO      NMPA4000             * delete old nat id.
          ENDIF
.
          PACK      KEY10,CHOSINDX,OLDURNO
          CALL      RDPTNID1
          BRANCH    OVRCD,NMPA9999
.
NMPA3000
. move old u/r id to new u/r id.
.
          MOVE      NEWURNO,PTNILNUM
          CALL      UPPTNID1
.
          GOTO      NMPA9999
.
. Delete old u/r national id.
.
NMPA4000  PACK      KEY10,CHOSINDX,OLDURNO
          CALL      RDPTNID1
          BRANCH    OVRCD,NMPA9999
.
          CALL      DEPTNID1
.
          GOTO      NMPA9999
.
NMPA9999  CLOSE     PATNIDA1
          RETURN
+
.**********************************************************************
.*                            CASE0000                                *
.*                  Merge The Case Notes Indicators                   *
.**********************************************************************
.
CASE0000
          MATCH     PHCNOTES,OLDCASE
          GOTO      CASE9999 IF EQUAL
.
          CLEAR     DIM20
          MOVE      ZERO,COUNT
CASE1000
          ADD       ONE,COUNT
.
          COMPARE   COUNT,TWENTY
          GOTO      CASE9000 IF LESS
.
          RESET     OLDCASE,COUNT
          RESET     PHCNOTES,COUNT
.
          CMATCH    "1",OLDCASE
          GOTO      CASE2000 IF EQUAL
.
          CMATCH    "1",PHCNOTES
          GOTO      CASE2000 IF EQUAL
.
          APPEND    ZERO,DIM20
          GOTO      CASE1000
CASE2000
          APPEND    ONE,DIM20
          GOTO      CASE1000
CASE9000
          RESET     DIM20
          MOVE      DIM20,PHCNOTES
CASE9999
          RETURN
.
.**********************************************************************
.*                            LINK0000                                *
.*                  Merge Linked UR Number File                       *
.**********************************************************************
LINK0000  MOVE      "patlinkf",XXFILE
          TRAP      WERR0000 NORESET GIVING XXDESC IF IO
.
LINK1000  PACK      KEY16,OLDURNO,SP20
          CALL      RDSLINK1
          CALL      RDKLINK1
          BRANCH    OVRCD,LINK9999
.
          MATCH     OLDURNO,LINKFUR
          GOTO      LINK9999 IF NOT EQUAL
.
          MOVE      LINKTUR,SAVEURNO
          PACK      KEY16,LINKFUR,LINKTUR
          CALL      DELINK1
.
          MOVE      NEWURNO,LINKFUR
.
          MATCH     LINKFUR,SAVEURNO
          GOTO      LINK1000 IF EQUAL
.
          PACK      KEY16,LINKFUR,SAVEURNO
          CALL      RDALINK1
          COMPARE   ZERO,OVRCD
          GOTO      LINK1000 IF EQUAL
.
          PACK      KEY16,SAVEURNO,LINKFUR
          CALL      RDALINK1
          IF        OVRCD=0
            CALL      DELINK1
          ENDIF
.
          PACK      KEY16,LINKFUR,SAVEURNO
          CALL      WRLINK1
          GOTO      LINK1000
.
LINK9999  RETURN
.
.**********************************************************************
.*                            ALRT0000                                *
.*                  Merge Alert U/R Number File                       *
.**********************************************************************
.
ALRT0000  DISPLAY   *P1:24,*EL,*P30:24,"Fixing Alert File ";
.
          MOVE      "patalrtf",XXFILE
          TRAP      WERR0000 NORESET GIVING XXDESC IF IO
.
ALRT1000  PACK      KEY16,OLDURNO,SP10
          CALL      RSPTALR1
          CALL      RKPTALR1
          BRANCH    OVRCD,ALRT8000
.
          MATCH     PTALURNO,OLDURNO
          GOTO      ALRT8000 IF NOT EQUAL
.
.         Check if new key already exists
.
          PACK      KEY16,NEWURNO,PTALCATG,PTALCODE
          CALL      RDPTALR1
          BRANCH    OVRCD,ALRT2000
.
.         Delete current record
.
          PACK      KEY16,PTALURNO,PTALCATG,PTALCODE
          CALL      DEPTALR1
.
.         Change U/R number for this record
.
ALRT2000  PACK      KEY16,OLDURNO,PTALCATG,PTALCODE
          CALL      RDPTALR1
          BRANCH    OVRCD,ALRT3000
.
          MOVE      NEWURNO,PTALURNO
          CALL      UPPTALR1
.
          CALL      UUPTMAS1
          GOTO      ALRT1000
.
ALRT3000  PRINT     *N,"** Corrupted Alert Index for UR ",OLDURNO;
          GOTO      ALRT9999
.
ALRT8000  BRANCH    CAUDA1,ALRT9999         * Using alert audits
.
ALRT8100  PACK      KEY27,OLDURNO,SP70
          CALL      ASPTALR2                
          CALL      AKPTALR2
          BRANCH    OVRCD,ALRT8400
.         
          MATCH     OLDURNO,PTALURNO 
          GOTO      ALRT8400 IF NOT EQUAL
.
          MOVE      NEWURNO,PTALURNO
          CALL      AUPTALR2
          GOTO      ALRT8100
.
ALRT8400  PACK      KEY8,NEWURNO,SP70
          CALL      RDMAST1
          BRANCH    OVRCD,ALRT9999
.
          MOVE      NEWURNO,KEY8
          CALL      RDPMPX21
          BRANCH    OVRCD,ALRT8450
.
          MATCH     "1",PMPXSIN7
          GOTO      ALRT9999 IF EQUAL       * current alert exists
.
          MATCH     "1",PMPXSIN8
          GOTO      ALRT9999 IF EQUAL       * current alert exists
.
          MATCH     "1",PMPXSIN9
          GOTO      ALRT9999 IF EQUAL       * current alert exists
.
          MATCH     "1",PMPXSN11
          GOTO      ALRT9999 IF EQUAL       * current alert exists
.
ALRT8450  MATCH     "0",PTMXSIN1              * Any current alerts
          IF        !@EQUAL
            MATCH     " ",PTMXSIN1            * Any current alerts
            GOTO      ALRT9999 IF NOT EQUAL
          ENDIF
.
          PACK      KEY27,NEWURNO,SP70
          CALL      ASPTALR2
ALRT8500  CALL      AKPTALR2
          BRANCH    OVRCD,ALRT9999
.
          MATCH     NEWURNO,PTALURNO
          GOTO      ALRT9999 IF NOT EQUAL
.
          MATCH     ANSD,PTALAUDR           * Delete
          GOTO      ALRT8500 IF NOT EQUAL
.
          MOVE      "3",PTMXSIN1            * Yes there are deleted alerts
.
          CALL      UPMAST1
.
ALRT9999  RETURN
+
.**************************************************************************
.*                               CHKALR00              Called by: PROC0000*
.*                                                                        *
.**************************************************************************
CHKALR00  MOVE      NEWURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,CHKALR99
.
          MOVE      NEWURNO,KEY8
          CALL      RDPMPX21
          IF        OVRCD=1
            CALL      CLPMSPX2
          ENDIF
.
          MOVE      PTMXSIN1,SAVESIN1
          MOVE      PMPXSIN7,SAVESIN7
          MOVE      PMPXSIN8,SAVESIN8
          MOVE      PMPXSIN9,SAVESIN9
          MOVE      PMPXSN11,SAVESN11
          UNPACK    SP70,PTMXSIN1,PMPXSIN7,PMPXSIN8,PMPXSIN9,PMPXSN11
.
          PACK      KEY16,NEWURNO,SP70
          CALL      RSPTALR1
CHKALR10  CALL      RKPTALR1
          BRANCH    OVRCD,CHKALR90
.
          MATCH     NEWURNO,PTALURNO
          GOTO      CHKALR90 IF NOT EQUAL
.
          MATCH     SP70,PTALEDAT
          GOTO      CHKALR10 IF NOT EQUAL        * show alert as inactive
.
          PACK      KEY5,PTALCATG,PTALCODE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CHKALR10
.
          MATCH     ANSM,TCINDC5
          IF        @EQUAL
            MOVE      "1",PMPXSIN7               * wahealth medical alert
            GOTO      CHKALR10
          ENDIF
.
          MATCH     ANSB,TCINDC5
          IF        @EQUAL
            MOVE      "1",PMPXSIN8               * wahealth micro alert
            GOTO      CHKALR10
          ENDIF
.
          MATCH     ANSR,TCINDC5
          IF        @EQUAL
            MOVE      "1",PMPXSIN9               * wahealth risk alert
            GOTO      CHKALR10
          ENDIF
.
          MATCH     ANSC,TCINDC5
          IF        @EQUAL
            MOVE      "1",PMPXSN11               * wahealth chronic alert
            GOTO      CHKALR10
          ENDIF
.
.         Standard alert functionality
.
          REP       "S2 1",TCINDC4
.
          MATCH     "2",TCINDC4        * check for security alert, if yes stop
          IF        @EQUAL
            PACK      PTMXSIN1,TCINDC4
            GOTO      CHKALR90
          ENDIF
.
          MATCH     PTMXSIN1,TCINDC4
          IF        @LESS
            PACK      PTMXSIN1,TCINDC4
          ENDIF
          MATCH     SP70,PTMXSIN1
          IF        @EQUAL
            PACK      PTMXSIN1,TCINDC4
          ENDIF
.
          GOTO      CHKALR10
.
CHKALR90  MATCH     PTMXSIN1,SAVESIN1
          IF        !@EQUAL
            CALL      UPMAST1
          ENDIF
.
          MATCH     PMPXSIN7,SAVESIN7
          IF        @EQUAL
            MATCH     PMPXSIN8,SAVESIN8
            IF        @EQUAL
              MATCH     PMPXSIN9,SAVESIN9
              IF        @EQUAL
                MATCH     PMPXSN11,SAVESN11
                IF        @EQUAL
                  GOTO      CHKALR99        * no wahealth indicators changed
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          CALL      UPPMPX21                * update for wahealth alerts (indc5)
.
CHKALR99  RETURN
+
.**********************************************************************
.*  UCSL0000  :  Update case notes location file                      *
.**********************************************************************
UCSL0000  COMPARE   ONE,PTCNUCSL                  * using case notes loc file?
          GOTO      UCSL9999 IF NOT EQUAL
.
          OPEN      PATCSLA1,"patcslaf"
.
          MOVE      "patcslaf",XXFILE
          TRAP      WERR0000 NORESET GIVING XXDESC IF IO
.
UCSL1000  PACK      KEY11,OLDURNO,SP10
          CALL      RSPTCSL1
          CALL      RKPTCSL1
          BRANCH    OVRCD,UCSL9999
.
          MATCH     OLDURNO,PTCLURNO                * same U/R No.?
          GOTO      UCSL9999 IF NOT EQUAL
.
. check if new U/R already exists
.
          PACK      KEY11,NEWURNO,PTCLUNIQ
          CALL      RAPTCSL1
          BRANCH    OVRCD,UCSL3000
.
          PACK      KEY11,OLDURNO,PTCLUNIQ
          CALL      DEPTCSL1
          GOTO      UCSL1000
.
. update the record with the new U/R No.
.
UCSL3000  PACK      KEY11,OLDURNO,PTCLUNIQ
          CALL      RDPTCSL1
          MOVE      NEWURNO,PTCLURNO
          CALL      UPPTCSL1
          GOTO      UCSL1000
.
UCSL9999  CLOSE     PATCSLA1
          RETURN
+
.**************************************************************************
.*  FDNR0000  :  Fix up Donor file                                        *
.**************************************************************************
FDNR0000  COMPARE   ONE,PTCNDONR                  * using Donor details file?
          GOTO      FDNR9999 IF NOT EQUAL
.
          DISPLAY   *P30:24,*EL,"Donor details    : ",*V2LON,OLDURNO;
.
          OPEN      PATDNRA1,"patdnraf"
.
.         Loop thru Donor details File
.
          MOVE      OLDURNO,PTDNURNO
.
          MOVE      "patdnraf",XXFILE
          TRAP      WERR0000 NORESET GIVING XXDESC IF IO
.
FDNR1000  PACK      KEY10,OLDURNO,SP10
          CALL      RSPTDNR1
          CALL      RKPTDNR1
          BRANCH    OVRCD,FDNR9000
.
          MATCH     PTDNURNO,OLDURNO
          GOTO      FDNR9000 IF NOT EQUAL
.
          PACK      KEY10,OLDURNO,PTDNCNT
          CALL      DEPTDNR1                      * delete old U/R
.
          MOVE      NEWURNO,PTDNURNO
FDNR2000  PACK      KEY10,PTDNURNO,PTDNCNT
          CALL      RAPTDNR1
          IF        OVRCD=1
            CALL      WRPTDNR1                    * write New U/R
            GOTO      FDNR1000
          ELSE
            ADD       ONE,PTDNCNT
            GOTO      FDNR2000
          ENDIF
.
FDNR9000  CLOSE     PATDNRA1
.
FDNR9999  RETURN
+
.**********************************************************************
.*  UKWC0000  :  Update kiwicard details file                         *
.**********************************************************************
UKWC0000  IF        PTCNUCRD <> 1 & PTCNUFML <> 1
            GOTO      UKWC9999
          ENDIF
.
.         We want to check the current year, and the previous year only
.
          MOVE      CYY,FORM2                * current year
          SUB       ONE,FORM2                * start with last year
.
.         Open the appropriate data file
.
UKWC1000  PACK      CFNAME,KWCFIL,FORM2
          REP       " 0",CFNAME
.
          MOVE      ZERO,OVRCD               * assume file exists
          TRAP      OVERCOND IF IO
          OPEN      PATKWCA1,CFNAME
          TRAPCLR   IO
          BRANCH    OVRCD,UKWC8000           * error openning file
.
          MOVE      "patkwc01",XXFILE
          TRAP      WERR0000 NORESET GIVING XXDESC IF IO
.
.         Check if the U/R number exists on this file
.
          PACK      KEY28,SP20,OLDURNO
          CALL      RDPTKWC1
          BRANCH    OVRCD,UKWC7000
.
.         Check if new U/R number is already on file
.
          PACK      KEY28,SP20,NEWURNO
          CALL      RAPTKWC1
          BRANCH    OVRCD,UKWC3000
.
.         Already exists. Update new U/R number with latest details
.
          MOVE      NEWURNO,PTKWURNO
          CALL      UPPTKWC1
.
          PACK      KEY28,SP20,OLDURNO
          CALL      DEPTKWC1                 * delete old U/R number
          GOTO      UKWC7000
.
.         New U/R number not on file yet
.
UKWC3000  PACK      KEY28,SP20,OLDURNO
          CALL      RDPTKWC1
          BRANCH    OVRCD,UKWC7000
.
          MOVE      NEWURNO,PTKWURNO
          CALL      UPPTKWC1
.
.         Finished update
.
UKWC7000  CLOSE     PATKWCA1
.
.         Check for the next year
.
UKWC8000  COMPARE   FORM2,CYY
          GOTO      UKWC9999 IF EQUAL
.
          ADD       ONE,FORM2                * go to the next year
.
          MOVE      "patkwc02",XXFILE
          TRAP      WERR0000 NORESET GIVING XXDESC IF IO
.
          GOTO      UKWC1000
.
UKWC9999  RETURN
+
.====================================================================== 
. PROC PATCOM00 : Change U/R for PATCOMFD
.====================================================================== 
          DEFPROC   PATCOM00
.
          INC       PMSUCHFD/INC
          INC       PMSUCDFD/INC
          INC       PATMASTD/INC
.
SAVKEY17  DIM       17
SAVKEY20  DIM       20
MAXNOTEN  FORM      6
COMTFLAG  FORM      1
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PMSUCHA1,"pmsuchaf"
          OPEN      PMSUCDA1,"pmsucdaf"
          TRAPCLR   IO
          BRANCH    OVRCD,PATCOM99           * file not found
.
          MOVE      "pmsuchaf",XXFILE
          TRAP      WERR0000 NORESET GIVING XXDESC IF IO
.
          DISPLAY   *P30:24,*EL,"U/R comments    : ",*V2LON,OLDURNO;
.
          CALL      PATCOM10
          GOTO      PATCOM99
.
          INC       PATCOM10
          INC       IBAOVRIO/INC
          INC       PMSUCHIO/INC
          INC       PMSUCDIO/INC
.
PATCOM99  ENDPROC
+
.*********************************************************************
.                   PATAXE00
.         Subroutine to update the pataxeaf file with national number 
.         (By U/R number)
.*********************************************************************
.
          DEFPROC   PATAXE00  
.
          INC       PATAXEFD/INC
          INC       PATVISFD/INC
.
          COMPARE   PTCNNMPI,ZERO                * link for U/R - NMPI ?
          GOTO      PATAXE99 IF EQUAL            * no - finished
.
          COMPARE   ONE,PTCNAXEU                 * using pataxeaf file ?
          GOTO      PATAXE99 IF NOT EQUAL        * no - finished
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATAXEA1,"pataxeaf"
          TRAPCLR   IO
          BRANCH    OVRCD,PATAXE99           * file not found
.
          OPEN      PATVISA2,"patvisaf"
.
.         See if there is a national number for the new U/R
.
          OPEN      PATNIDA1,"patnidaf"
          PACK      KEY10,CHOSINDX,NEWURNO,SP10
          CALL      RDPTNID1                     * record on file
          BRANCH    OVRCD,PATAXE99               * no - finished
.
.         Get each visit record for the new ur (as the records will already
.         have been updated by PMS05), and update the pataxeaf file for any
.         admissions
.
          MOVE      "pataxeaf",XXFILE
          TRAP      WERR0000 NORESET GIVING XXDESC IF IO
.
          PACK      KEY24,NEWURNO,SP20,SP20
          CALL      RDSVISA2                     * position in file
PATAXE50  CALL      RDKVISA2                     * read next record
          BRANCH    OVRCD,PATAXE99               * eof - finished
. 
          MATCH     NEWURNO,PVIURNO              * same U/R still ?
          GOTO      PATAXE99 IF NOT EQUAL        * no - finished
.
          COMPARE   PVITYPE,THREE                * I/P visit ?
          GOTO      PATAXE50 IF NOT EQUAL        * no - ignore
.
.         See if there is a pataxeaf record on file and if so, update the
.         NMPI number
.
          PACK      KEY8,PVIBILL,SP10
          CALL      RDPTAXE1                     * record on file ?
          BRANCH    OVRCD,PATAXE50               * no - ignore
.    
          IF        PTAXDAMI < 9 
            MOVE      PTNINMPI,PTAXNRIC
            IF        PTAXDAMI =7 | PTAXDAMI =8
              MOVE      "21",PTAXAI1C
            ENDIF
            CALL      UPPTAXE1                   * update record
          ENDIF
.
          GOTO      PATAXE50                     * get next record
.
          INC       IBAOVRIO/INC
          INC       PATAXEIO/INC
          INC       PATVISIO/INC
.            
PATAXE99  CLOSE     PATAXEA1
          ENDPROC
+
.********************************************************************** 
.*                            UPDT0000                                *
.*                   Update Request Merge UR File                     *
.********************************************************************** 
UPDT0000  PACK      KEY17,TWO,OLDURNO,NEWURNO
          CALL      RDPTMRG1
          BRANCH    OVRCD,UPDT9999
.
          MOVE      THREE,PTMRSTAT
          PACK      PTMRSDTE,CCC,CYY,CMM,CDD
          REP       " 0",PTMRSDTE
          CLOCK     TIME,CTIMEIS
          MOVE      CTIMEIS,PTMRSTIM
          REP       " 0",PTMRSTIM
.
          CALL      UPPTMRG1
.
UPDT9999  RETURN
+
.**********************************************************************
.*                       Temp File I/O Routines                       *
.**********************************************************************
.
WRTEMP1   WRITE     TEMP1,SEQ;PTMROLUR,PTMRNWUR
.
          RETURN
+
.**********************************************************************
.*                            WERR0000                                *
.*                  Write the Details of the IO error to log          *
.**********************************************************************
WERR0000  PACK      XXDESC,XXDESC,SP30,SP30,SP30
          WRITE     TEMP3,SEQ;OLDURNO,NEWURNO,CHOSPNUM,XXFILE,XXDESC
          MOVE      ONE,OVRCD      *SRF 643927
.
WERR9999  RETURN 
.
          INC       DGCLICMR                                           *C-90202
          INC       MRGUREOC
