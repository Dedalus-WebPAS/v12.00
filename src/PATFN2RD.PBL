.******************************************************************************
.*                                  PATFN2RD                                  *
.* Common routine to read Health Fund Extention file record (patfn2af)        *
.*                                                                            *
.* Requires:   KEY17 populated with -                                         *
.*                      - Health Fund (usually AFUNDH)                        *
.*                      - Hospital Code                                       *
.*                      - Admission Date (defaults to current date)           *
.*                                                                            *
.* Returns:    OVRCD 0 = valid health fund in "patfn2af"                      *
.*                   1 = health fund not found                                *
.*                                                                            *
.* Modifications:                                                             *
.*        V11.02.02 17/06/2022  J.Tan         TSK 0918312                     *
.*                  Mod to use OVRCD instead of EXIT                          *
.*        V11.02.01 20/05/2022  J.Tan         TSK 0814566                     *
.*                  Added TRAP opening patfn2af file                          *
.******************************************************************************
PATFN2RD  MOVE      ZERO,OVRCD
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO          * trap for error opening file
          OPEN      PATFN2A1,"patfn2af"
          TRAPCLR   IO
          BRANCH    OVRCD,FNDRE920
.
          UNPACK    KEY17,WKFNAFND,WKFNHOSP,WKFNDATE
          MATCH     SP8,WKFNDATE            * check blank date
          IF        @EQUAL
            PACK      WKFNDATE,CCC,CYY,CMM,CDD,SP10 * default to current date
            REP       " 0",WKFNDATE
          ENDIF
.
FNDRE100  PACK      KEY17,WKFNAFND,WKFNHOSP,WKFNDATE,SP70
          CALL      RDPTFN21
          COMPARE   ZERO,OVRCD
          GOTO      FNDRE900 IF EQUAL
.
          CALL      RKPTFN21                * read next record
          BRANCH    OVRCD,FNDRE300
.
          MATCH     PTFNHFND,WKFNAFND
          GOTO      FNDRE300 IF NOT EQUAL   * different health fund
.
          MATCH     PTFNHOSP,WKFNHOSP
          GOTO      FNDRE300 IF NOT EQUAL   * different hospital
.
          GOTO      FNDRE900
.
.         Try with blank Effective date (for Current record)
.
FNDRE300  PACK      KEY17,WKFNAFND,WKFNHOSP,SP70
          CALL      RDPTFN21
          COMPARE   ZERO,OVRCD
          GOTO      FNDRE900 IF EQUAL
.
.         Try with Blank Hospital
.
          MATCH     SP70,WKFNHOSP
          GOTO      FNDRE920 IF EQUAL
.
          MOVE      SP70,WKFNHOSP
          GOTO      FNDRE100
.
FNDRE900  MOVE      ZERO,OVRCD
          GOTO      FNDRE999
.
FNDRE920  MOVE      ONE,OVRCD
          MOVE      ZERO,PTFNBMVH
          MOVE      ZERO,PTFNMVRT
          MOVE      ZERO,PTFNHRMV
.
FNDRE999  RETURN
.
