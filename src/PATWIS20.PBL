.******************************************************************************
.* System         : Patient Management                                        *
.* Include        : PATWIS20.PBL                                              *
.* Description    : Calculate WIES20 value for an individual admission        *
.******************************************************************************
.* Date           : 07/08/2013                                                *
.* Author         : Davin Sloan                                               *
.*                                                                            *
.* Vars Required  : VARWIS20 - Variables required for WIES20                  *
.*                                                                            *
.* Files Required Open :                                                      *
.*        PATASFFD  PATASFA1 - Admission special funding arrangement file     *
.*        PATCODFD  PATCODE1 - Codes file                                     *
.*        PATECDFD  PATECDA2 - Episode disease coding file                    *
.*        PATECOFD  PATECOA2 - Episode operation coding file                  *
.*        PAT10OFD  PATI10O1 - ICD10 operations file                          *
.*        PATICUFD  PATICUA1 - ICU file                                       *
.*        PATTRNFD  PATTRAN2 - Transfer file                                  *
.*        PATW20FD  PATW20A1 - Victorian cost weights (for WIES20) file       *
.*                                                                            *
.* Parameters Req : Successful read on admission file                         *
.*                  Successful read on discharge file                         *
.*                  HIHLOS   - for Hopsipta in Home LOS (from RHIHDAYS)       *
.*                  NBRDAYS  - for LOS calculation (from NHOSPDAY)            *
.*                  PNSWRACE - User defined field for race                    *
.*                  WIS20DRG - DRG to calculate WIES20 for                    *
.*                                                                            *
.* Returns        : TOTWIS20 - Total WIES20 value                             *
.*                  WIS20DRG - DRG used to calculate WIES20                   *
.*                  WIS20LOS - LOS used to calculate WIES20                   *
.*                  WIS20WGT - Weight used to calculate WIES20                *
.*                  WIS20DSC - Description of DRG weight used                 *
.*                                                                            *
.* Modifications  :                                                           *
.*       V12.00.01  26/05/2025 Nikitha B        TSK 0955096                   *
.*                  Alphanumeric changes                                      *
.*       V10.03.02  04/10/2013  Peter Vela       CAR 284261                   *
.*                   Fixed positional read of patasfaf @ C20TR000             *
.*       V10.03.01  30/08/2013  Davin            CAR 290919                   *
.*                  Changed key14 to key16 for reads on patecdaf & patecoaf   *
.*       V10.03.00  07/08/2013  Davin            CAR 284960                   *
.*                  Created for 2013/2014 WIES20 changes:                     *
.******************************************************************************
+
PATWIS20  CALL      IN20W000                * Initialise variables
          BRANCH    EXIT,WIS20950
.
          CALL      C20TY000                * Determine and Check Care Type
          BRANCH    EXIT,WIS20950
.
          CALL      C20TR000                * Check for Contracted Patient
          BRANCH    EXIT,WIS20950
.
          CALL      M20RG000                * Modify DRG if necessary
.
          CALL      C20RG000                * Check for problem DRG
          BRANCH    EXIT,WIS20950
.
          CALL      M20CH000                * Check for Mechanical Ventilation
.
          CALL      T20AL000                * Check for Thalessaemia
.
          CALL      A20AA000                * Check for AAA
.
          CALL      A20SD000                * Check for ASD
.
          CALL      B20SW000                * Calculate Base WIES20
.
          CALL      C20AB000                * Check for aboriginality
.
. ---- Calculate WIES20 value ----
.
          MOVE      TOTBWIES,TOTWIS20       * base WIES
          ADD       MVCOPAY,TOTWIS20        * mechv copayment
          ADD       THCOPAY,TOTWIS20        * thal  copayment
          ADD       AAACOPAY,TOTWIS20       * AAA   copayment
          ADD       ASDCOPAY,TOTWIS20       * ASD   copayment
          ADD       ATSIWIES,TOTWIS20       * ATSI  copayment
.
          MOVE      ZERO,EXIT
          GOTO      WIS20999
.
WIS20950  MOVE      ONE,EXIT
WIS20999  RETURN
+
.******************************************************************************
.*                                  IN20W000              Called by: PATWIS20 *
.*                         Initialise WIES20 variables                        *
.******************************************************************************
IN20W000  MOVE      ZERO,TOTWIS20
          MOVE      ZERO,WIS20LOS
          MOVE      ZERO,WIS20WGT
          MOVE      SP20,WIS20DSC
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATW20A1,"patw20af"
          TRAPCLR   IO
          BRANCH    OVRCD,IN20W950
.
IN20W900  MOVE      ZERO,EXIT
          GOTO      IN20W999
.
IN20W950  MOVE      ONE,EXIT
IN20W999  RETURN
+
.******************************************************************************
.*                                  C20TY000              Called by: PATWIS20 *
.*                 Check for valid Care Type & Incomplete Coding              *
.******************************************************************************
C20TY000  PACK     KEY5,ANSC,ANSC,ACARE     * use admission care type
          CALL     RDCODE1
          BRANCH   OVRCD,C20TY900
.
          MATCH    "6",TCINDC4              * Check Program Funding Source
          GOTO     C20TY900 IF NOT EQUAL
.
          MATCH    "0   ",THCSCOD           * check care types
          GOTO     C20TY900 IF EQUAL
          MATCH    "1   ",THCSCOD
          GOTO     C20TY900 IF EQUAL
          MATCH    "2   ",THCSCOD
          GOTO     C20TY900 IF EQUAL
          MATCH    "6   ",THCSCOD
          GOTO     C20TY900 IF EQUAL
          MATCH    "7   ",THCSCOD
          GOTO     C20TY900 IF EQUAL
          MATCH    "8   ",THCSCOD
          GOTO     C20TY900 IF EQUAL
          MATCH    "9   ",THCSCOD
          GOTO     C20TY900 IF EQUAL
          MATCH    "5   ",THCSCOD
          GOTO     C20TY900 IF EQUAL
          MATCH    "3   ",THCSCOD
          GOTO     C20TY900 IF EQUAL
          MATCH    "E   ",THCSCOD
          GOTO     C20TY900 IF EQUAL
          MATCH    "F   ",THCSCOD
          GOTO     C20TY900 IF EQUAL
          MATCH    "J   ",THCSCOD
          GOTO     C20TY900 IF EQUAL
          MATCH    "K   ",THCSCOD
          GOTO     C20TY900 IF EQUAL
          MATCH    "U   ",THCSCOD
          GOTO     C20TY900 IF EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     C20TY999
.
C20TY900  MOVE     ONE,EXIT                 * Not valid
C20TY999  RETURN
+
.******************************************************************************
.*                                  C20TR000              Called by: PATWIS20 *
.*                        Check for Contracted Patient                        *
.******************************************************************************
C20TR000  PACK     KEY25,AADMNO,SP70
          CALL     RSPTASF1
C20TR100  CALL     RKPTASF1                 * Read adm. special funding file
          BRANCH   OVRCD,C20TR800           * still process
.
          MATCH    AADMNO,PTAFADMN
          GOTO     C20TR800 IF NOT EQUAL    * still process
.
          MATCH    ANSA,PTAFTYPE            * special fund. arrang. type = adm
          GOTO     C20TR100 IF NOT EQUAL
.
          MATCH    ANSB,PTAFCROL            * Ignore if contract role = 'B'
          GOTO     C20TR900 IF EQUAL
.
C20TR800  MOVE     ZERO,EXIT                * valid
          GOTO     C20TR999
.
C20TR900  MOVE     ONE,EXIT                 * invalid
C20TR999  RETURN
+
.******************************************************************************
.*                                  M20RG000              Called by: PATWIS20 *
.*                      Modify DRG if necessary for WIES20                    *
.******************************************************************************
M20RG000  CALL      R20IO000           * Radiotherapy
M20RG999  RETURN
+
.******************************************************************************
.*                                  R20IO000              Called by: M20RG000 *
.*                         Modify DRG if Radiotherapy                         *
.******************************************************************************
.>>>>     Don't modify these DRGs:
.
R20IO000  MATCH     "B61A",WIS20DRG
          GOTO      R20IO999 IF EQUAL
.
          MATCH     "B61B",WIS20DRG
          GOTO      R20IO999 IF EQUAL
.
          MATCH     "A40Z",WIS20DRG
          GOTO      R20IO999 IF EQUAL
.
          MATCH     "W60Z",WIS20DRG
          GOTO      R20IO999 IF EQUAL
.
          MATCH     "W61A",WIS20DRG
          GOTO      R20IO999 IF EQUAL
.
          MATCH     "W61B",WIS20DRG
          GOTO      R20IO999 IF EQUAL
.
          MATCH     "S65A",WIS20DRG
          GOTO      R20IO999 IF EQUAL
.
          MATCH     "S65B",WIS20DRG
          GOTO      R20IO999 IF EQUAL
.
          MATCH     "S65C",WIS20DRG
          GOTO      R20IO999 IF EQUAL
.
          MATCH     "B60A",WIS20DRG
          GOTO      R20IO999 IF EQUAL
.
          MATCH     "B60B",WIS20DRG
          GOTO      R20IO999 IF EQUAL
.
          MATCH     "B82A",WIS20DRG
          GOTO      R20IO999 IF EQUAL
.
          MATCH     "B82B",WIS20DRG
          GOTO      R20IO999 IF EQUAL
.
          MATCH     "B82C",WIS20DRG
          GOTO      R20IO999 IF EQUAL
.
.>>>>     Check if operation code exists:
.
          PACK      ICDRDDTE,DDATE,SP10
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
R20IO050  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,R20IO999
.
          MATCH     AADMNO,PTEOADMN         * same admission?
          GOTO      R20IO999 IF NOT EQUAL
.
.>>>>     Check if block number is in range 1786-1792, 1794 or 1795:
.
          PACK      KEY9,PTEOCODE,SP9
          CALL      RDPTICO1
          BRANCH    OVRCD,R20IO050
.
          MATCH     "1786",PT0OBLKN         * block code >= 1786?
          GOTO      R20IO050 IF LESS
.
          MATCH     "1796",PT0OBLKN         * block code <=1795?
          GOTO      R20IO050 IF NOT LESS
.
          MATCH     "1793",PT0OBLKN         * block code = 1793?
          GOTO      R20IO050 IF EQUAL
.
.>>>>     Check if disease code exists in range C00-D48:
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
R20IO100  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,R20IO999
.
          MATCH     AADMNO,PTEDADMN         * same admission?
          GOTO      R20IO999 IF NOT EQUAL
.
          MATCH     "C00",PTEDCODE          * neoplasm code >= C00?
          GOTO      R20IO100 IF LESS
.
          MATCH     "D49",PTEDCODE          * neoplasm code <= D48.9?
          GOTO      R20IO100 IF NOT LESS
.
          MOVE      "R64Z",WIS20DRG         * modified for WIES20
R20IO999  RETURN
+
.******************************************************************************
.*                                  C20RG000              Called by: PATWIS20 *
.*                            Check for Problem DRG                           *
.******************************************************************************
C20RG000  PACK     KEY4,WIS20DRG
          CALL     RDPT201                  * check WIES20 weights file
          BRANCH   OVRCD,C20RG900
.
          MATCH    "960Z",PT20DRGC
          GOTO     C20RG900 IF EQUAL
.
          MATCH    "961Z",PT20DRGC
          GOTO     C20RG900 IF EQUAL
.
          MATCH    "962Z",PT20DRGC
          GOTO     C20RG900 IF EQUAL
.
          MATCH    "963Z",PT20DRGC
          GOTO     C20RG900 IF EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     C20RG999
.
C20RG900  MOVE     ONE,EXIT
C20RG999  RETURN
+
.******************************************************************************
.*                                  M20CH000              Called by: PATWIS20 *
.*                Calculate Mechanical Ventialtion Co-payment                 *
.******************************************************************************
M20CH000  MOVE      ZERO,ADJMVDAY
          MOVE      ZERO,MVCOPAY
.
          PACK      KEY8,AADMNO
          CALL      RDPTICU1                * get mech. vent. hours
          BRANCH    OVRCD,M20CH999
.
          MATCH     "D",PT20MVEL            * check mech. vent. eligibility
          IF        @EQUAL
            IF        PTICUMEC > 6
              ASSIGN    ((PTICUMEC+12)/24),FORM12P4    * days receiving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7659)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
            GOTO      M20CH999
          ENDIF
.
          MATCH     "4",PT20MVEL
          IF        @EQUAL
            IF        PTICUMEC > 96
              ASSIGN    (((PTICUMEC+12)/24)-4),FORM12P4   * days recving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7659)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
          ENDIF
.
M20CH999  RETURN
+
.******************************************************************************
.*                                  T20AL000              Called by: PATWIS20 *
.*                      Calculate Thalessaemia Co-payment                     *
.******************************************************************************
T20AL000  MOVE      ZERO,THCOPAY
          MATCH     "T",PT20CPAY            * check WIES20 weights file
          IF        @EQUAL
            PACK      KEY16,AADMNO,SP20
            CALL      RSPTECD2              * Position on & read a patient
T20AL100    CALL      RKPTECD2                episode disease file record
            BRANCH    OVRCD,T20AL999
.
            MATCH     AADMNO,PTEDADMN
            GOTO      T20AL999 IF NOT EQUAL
.
            MATCH     "D56",PTEDCODE        * check ICD-10 code
            IF        !@EQUAL
              MATCH     "D57.2",PTEDCODE    * check ICD-10 code
              GOTO      T20AL100 IF NOT EQUAL
            ENDIF
            MOVE      "0.2648",THCOPAY      * eligible for thal. copayment
          ENDIF
T20AL999  RETURN
+
.******************************************************************************
.*                                  A20AA000              Called by: PATWIS20 *
.*                          Calculate AAA Co-payment                          *
.******************************************************************************
A20AA000  MOVE      ZERO,AAACOPAY
          MATCH     "AAA",PT20CPAY          * check WIES20 weights file
          GOTO      A20AA999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
A20AA100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,A20AA999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      A20AA999 IF NOT EQUAL
.
          MATCH     "33116-00",PTEOCODE     * check ICD-10 code
          GOTO      A20AA100 IF NOT EQUAL
.
          MOVE      "3.1421",AAACOPAY       * eligible for AAA copayment
A20AA999  RETURN
+
.******************************************************************************
.*                                  A20SD000              Called by: PATWIS20 *
.*                          Calculate ASD Co-payment                          *
.******************************************************************************
A20SD000  MOVE      ZERO,ASDCOPAY
          MATCH     "ASD",PT20CPAY          * check WIES20 weights file
          GOTO      A20SD999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
A20SD100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,A20SD999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      A20SD999 IF NOT EQUAL
.
          MATCH     "38742-00",PTEOCODE     * check ICD-10 code
          GOTO      A20SD100 IF NOT EQUAL
.
          MOVE      "2.4713",ASDCOPAY       * eligible for ASD copayment
A20SD999  RETURN
+
.******************************************************************************
.*                                  B20SW000              Called by: PATWIS20 *
.*                            Calculate Base WIES20                           *
.******************************************************************************
B20SW000  CALL      D20SC000                * length of stay category
          CALL      C20ST000                * calculate inlier status
.
          MATCH     "L",W20NLIER            * low outlier
          CALL      BW20L000 IF EQUAL       * calculate base WIES20
.
          MATCH     "I",W20NLIER            * inlier
          CALL      BW20N000 IF EQUAL       * calculate base WIES20
.
          MATCH     "H",W20NLIER            * high outlier
          CALL      BW20H000 IF EQUAL       * calculate base WIES20
.
B20SW999  RETURN
+
.******************************************************************************
.*                                  D20SC000              Called by: B20SW000 *
.*                      Determine Length Of Stay Category                     *
.******************************************************************************
D20SC000  MOVE      NBRDAYS,WIS20LOS        * length of stay from NHOSPDAY
          MOVE      "M",LOSCAT              * set LOS category to multiday
          MATCH     ADATE,DDATE             * check for sameday
          IF        @EQUAL
            MOVE      ONE,SAMEDAY           * sameday
            MOVE      ONE,WIS20LOS          * LOS = 1
            MOVE      "S",LOSCAT            * set LOS category to sameday
            GOTO      D20SC999
          ELSE
            MOVE      ZERO,SAMEDAY          * not sameday
          ENDIF
.
          IF        SAMEDAY = 0 & WIS20LOS = 1
            MOVE      "O",LOSCAT            * set LOS category to oneday
            GOTO      D20SC999
          ENDIF
.
          IF        WIS20LOS > 1825
            MOVE      "1825",WIS20LOS       * maximum LOS is 5 years
          ENDIF
.
D20SC999  RETURN
+
.******************************************************************************
.*                                  C20ST000              Called by: B20SW000 *
.*                           Calculate Inlier Status                          *
.******************************************************************************
C20ST000  MOVE      "I",W20NLIER            * set status to inlier
          IF        WIS20LOS < PT20LIBP
            MOVE      "L",W20NLIER          * set status to low outlier
            GOTO      C20ST999
          ENDIF
.
          ASSIGN    (PT20HIBP+ADJMVDAY),FORM12P4   * high bound. + mech.v days
          IF        WIS20LOS > FORM12P4
            MOVE      "H",W20NLIER          * set status to high outlier
          ENDIF
C20ST999  RETURN
+
.******************************************************************************
.*                                  BW20L000              Called by: B20SW000 *
.*                     Calculate Base WIES20 for Low Outlier                  *
.******************************************************************************
BW20L000  MATCH     "S",LOSCAT            * sameday
          IF        @EQUAL
            MOVE      PT20TSDW,TOTBWIES             * base WIES20
            ASSIGN    (TOTBWIES/PT20TIMW),TOTIES20  * IES20
.
            MOVE      PT20TSDW,WIS20WGT             * for display
            MOVE      "Same Day Wgt ",WIS20DSC
            GOTO      BW20L999
          ENDIF
.
          MATCH     "O",LOSCAT            * oneday
          IF        @EQUAL
            MOVE      PT20TODW,TOTBWIES             * base WIES20
            ASSIGN    (TOTBWIES/PT20TIMW),TOTIES20  * IES20
.
            MOVE      PT20TODW,WIS20WGT             * for display
            MOVE      "One Day Wgt  ",WIS20DSC
            GOTO      BW20L999
          ENDIF
.
          MATCH     "M",LOSCAT            * multiday
          IF        @EQUAL
            ASSIGN    (PT20TODW+((WIS20LOS-1)*PT20TLOM)),TOTBWIES
.                                                    * base WIES20
            ASSIGN    (TOTBWIES/PT20TIMW),TOTIES20   * IES20
.
            MOVE      PT20TLOM,WIS20WGT             * for display
            MOVE      "Low Outlr Wgt",WIS20DSC
            GOTO      BW20L999
          ENDIF
BW20L999  RETURN
+
.******************************************************************************
.*                                  BW20N000              Called by: B20SW000 *
.*                      Calculate Base WIES20 for Inlier                      *
.******************************************************************************
BW20N000  MOVE      ONE,TOTIES20          * IES20 = 1
          MATCH     "S",LOSCAT            * sameday
          IF        @EQUAL
            MOVE      PT20TSDW,TOTBWIES   * base WIES20
.
            MOVE      PT20TSDW,WIS20WGT             * for display
            MOVE      "Same Day Wgt ",WIS20DSC
            GOTO      BW20N999
          ENDIF
.
          MATCH     "O",LOSCAT            * oneday
          IF        @EQUAL
            MOVE      PT20TODW,TOTBWIES   * base WIES20
.
            MOVE      PT20TODW,WIS20WGT             * for display
            MOVE      "One Day Wgt  ",WIS20DSC
            GOTO      BW20N999
          ENDIF
.
          MATCH     "M",LOSCAT            * multiday
          IF        @EQUAL
            MOVE      PT20TIMW,TOTBWIES   * base WIES20
.
            MOVE      PT20TIMW,WIS20WGT             * for display
            MOVE      "Inlier Wgt   ",WIS20DSC
            GOTO      BW20N999
          ENDIF
BW20N999  RETURN
+
.******************************************************************************
.*                                  BW20H000              Called by: B20SW000 *
.*                    Calculate Base WIES20 for High Outlier                  *
.******************************************************************************
BW20H000  ASSIGN    ((WIS20LOS-PT20HIBP)-ADJMVDAY),HIGHDAYS  * high outlr days
          COMPARE   ZERO,HIGHDAYS
          IF        @LESS
            MOVE      ZERO,HIGHDAYS         * highdays must be >= zero
          ENDIF
.
          COMPARE   HIGHDAYS,HIHLOS
          IF        @LESS
            MOVE      HIHLOS,HITHDAYS       * get lesser of highdays and
          ELSE                              *  HIHLOS
            MOVE      HIGHDAYS,HITHDAYS
          ENDIF
.
          ASSIGN    (HIGHDAYS-HITHDAYS)*PT20THOM,TOTBWIES    *high WIES
          ASSIGN    (HITHDAYS*PT20THIH)+TOTBWIES,TOTBWIES    *HITH WIES
          ASSIGN    (PT20TIMW+TOTBWIES),TOTBWIES             *base WIES20
          ASSIGN    (TOTBWIES/PT20TIMW),TOTIES20             * IES20
.
          MOVE      PT20THOM,WIS20WGT                        * for display
          MOVE      "High Outl Wgt",WIS20DSC
BW20H999  RETURN
+
.******************************************************************************
.*                                  C20AB000              Called by: PATWIS20 *
.*                          Check for Aboriginality                           *
.******************************************************************************
C20AB000  MOVE      ZERO,ATSIWIES                * set ATSI WIES to zero
          IF        PNSWRACE<1 | PNSWRACE>14
            GOTO      C20AB999              * Invalid parameter
          ENDIF
.
          LOAD      TCODE,PNSWRACE,CATP1,CATP2,CATP3,CATP4,CATP5:
                                   CATP6,CATA1,CATA2,CATA3,CATA4:
                                   CATA5,CATA6,CATA7,CATVA
          LOAD      ACODE,PNSWRACE,PUSR1,PUSR2,PUSR3,PUSR4,PUSR5:
                                   PUSR6,AUSR1,AUSR2,AUSR3,AUSR4:
                                   AUSR5,AUSR6,AUSR7,PMPXABRG
.
          MATCH     ACODE,SP3
          GOTO      C20AB999 IF EQUAL
.
          PACK      KEY5,TCODE,ACODE
          CALL      RDCODE1                 * Read a code file record
          BRANCH    OVRCD,C20AB999
.
          UNPACK    THCSCOD,DIM1,KEY1       * use 2nd char of HDP Equiv
.>>>>     MOVE      THCSCOD,KEY1
          TYPE      KEY1
          GOTO      C20AB999 IF NOT EQUAL   * Non numeric, error
.
          MOVE      KEY1,FORM1
.
. ---- Calculate ATSI Loading if aboriginality = 1, 2, or 3 ----
.
          IF        FORM1=1 | FORM1=2 | FORM1=3
            MOVE      TOTBWIES,FORM12P4       * base WIES
            ADD       MVCOPAY,FORM12P4        * mechv copayment
            ADD       THCOPAY,FORM12P4        * thal  copayment
            ADD       AAACOPAY,FORM12P4       * AAA   copayment
            ADD       ASDCOPAY,FORM12P4       * ASD   copayment
            ASSIGN    (0.3*FORM12P4),ATSIWIES
          ENDIF
.
C20AB999  RETURN
+
