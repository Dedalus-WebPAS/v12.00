. *****************************************************************************
.* Module  :        PRINTEMR
.*
.* Modifications:
.******************************************************************************
.*        V12.00.01 19/05/2025  J.Tan          TSK 0955096
.*                  Added alpanumeric visit number generation
.*         V11.03.01 20/09/2023  Thanh T       TSK 0914124
.*                   Added SAVKEY10 to save WBSEUID
.*         V11.02.01 02/08/2022  Thanh T       TSK 0914124
.*                   Added working varaibles to be used in EMRMR5N2        
.*         V10.11.01 11/09/2017  Ania P        TSK 0839053
.*                   Recompiled for EMRMR5N2
.*         V10.03.04 19/04/2013  Peter Vela    CAR 281388
.*                   Added "BHSCLN" Ballarat Health Services
.*         V10.03.03 11/02/2013  Don Nguyen    CAR 277835
.*                   Added "WAPCLN" WA Patient Clinical Notes
.*         V10.03.02 19/06/2012  Peter Vela    CAR 264245
.*                   Added "HSPCL2" Healthscope Clinical Notes
.*         V10.03.01 21/02/2012  Ebon Clements CAR 248285
.*                   Fixed invalid visit error when admission number is blank
.*         V10.02.01 05/07/2011  B.G.Ackland   CAR 243247
.*                   Added "HSPCLN" HealthScope Hospital Layout
.*         V10.01.01 01/03/2011  Ebon Clements CAR 223822
.*                   Added clear of EMDCSPAR
.*         V9.12.02  03/08/2009  Ebon Clements CAR 202829
.*                   Fixed population of DOB for unknown patients UNKDET00
.*         V9.12.01  01/07/2009  Mike Laming   CAR 196463
.*                   Modify HDH report (HDHCLIN0) to print Head and Tail details
.******************************************************************************
.
          DEFPROC   PRINTEMR
.
          INC       PATGSRFD/INC
.
CURRENT   INIT      "Current Patient"
INCOMPLE  INIT      "Discharged Incomplete"
COMPLETE  INIT      "Discharged Complete"
CANCLLED  INIT      "Cancelled"
.
DISPFRST  DIM       40                      * First given name (for display)
DISPSECN  DIM       40                      * Second given name (for display)
DISPSURN  DIM       40                      * Surname (for display)
HDCURPAG  FORM      3                       * Current page number (header)
HDTOTPAG  FORM      3                       * Total page count (header)
PRINTURN  DIM       8
PRINTADM  DIM       8 
SADACLS   DIM       3
SADACOMP  DIM       3
SADAINS   DIM       3
SADAMODE  DIM       3
SADAPREV  DIM       3
SADASIT   DIM       3
SADASRC   DIM       3
SADAUSR1  DIM       3
SADAUSR2  DIM       3
SADAUSR3  DIM       3
SADAUSR4  DIM       3
SADAUSR5  DIM       3
SAVNOTEN  FORM      6                       * previously read Note Number
SAVKEY10  DIM       10
SAVKEY17  DIM       17                      * previously used key
.
ALRTDESC  DIM       20
WRKDTE8   DIM       8
WRKDTE10  DIM       10
WORKCATG  DIM       2
WORKVAR   DIM       100
SEXCODE   DIM       1
.
ARYALIAS  DIM       30[50]
CNTALIAS  FORM      2
ALISREAD  FORM      1
.
SP100     INIT      "                                                  ":
                    "                                                  "
ZERO4     INIT      "04"

.------------------------------------------------------------
.  Print EMR Forms
.------------------------------------------------------------
PRTEMR00  CALL      CLPATMAS                * clear patmasfd
          CALL      CLPMSPX2                * clear pmspx2fd           *I-54627
          CALL      INTPRA00                * clear patresfd
.
          MOVE      "01",SCRID              * set for PMI screen
.
          MOVE      ONE,RTAOVRCD            * set as no RTA details
          MOVE      ZERO,PRIDSYET           * no need to print Id sheet warn.
.
          OPEN      PATGSRN1,"patgsrnf"
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD  * set current date
          REP       " 0",CURRDATE
.
          MATCH     SP70,ADMISSNO
          GOTO      PRTEMR90 IF EQUAL
.
          MOVE      ADMISSNO,EMVIADMN
          CALL      EMRDET00                * read in required emr variables
          BRANCH    EXIT,PRTEMR90           
.
          MATCH     URNUMBER,ZEROUR   
          IF        @EQUAL
            CALL      UNKDET00                * read required unknown varaibles
            BRANCH    EXIT,PRTEMR99
.           
            MOVE      SP70,PMCDCNUM
            MOVE      SP70,PMCDCTYP
            MOVE      SP70,PMCDEXDT
.
          ELSE
            CALL      DSPEMR00                * Read the master file variables
            BRANCH    EXIT,PRTEMR99
.
            PACK      KEY8,URNUMBER                                    *I-54627
            CALL      RDPMPX21
            COMPARE   ONE,PTCNNHII
            IF        @EQUAL
              OPEN      NHIMASA2,"nhimasaf"
              MOVE      PURNO,KEY8
              CALL      RDNHMAS2
            ENDIF
.
            CALL      GCCSJG00
.
          ENDIF
.
          MOVE      STATNCOD,HEMR5COD
          CALL      PRTMR500
          BRANCH    EXIT,PRTEMR99
.
          MOVE      PURNO,KEY8
          CALL      UUPTMAS1
.
          GOTO      PRTEMR99
.
PRTEMR90  MOVE      "Not an EMR patient",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PRTEMR99
.
PRTEMR99  GOTO      PRTEMRND
.------------------------------------------------------------
. MR5
.------------------------------------------------------------
PRTMR500  BRANCH    HEPMR5B,PRTMR510
          GOTO      PRTMR590
.
PRTMR510  MOVE      CPPAPER2,CPPAPER        * Set up Second Printer
          MOVE      CDEFPRT2,CDEFPRT
          MOVE      ZERO,PRIDSYET           *flag saying Id sheet printed
.
          MATCH     "CLINIC",STATNCOD
          IF        @EQUAL
            CALL      SETLMR5
            CALL      EMRCLIN0              * St Vincents Printing of Clinical
            GOTO      PRTMR599              * Screen
          ENDIF
.
          MATCH     "HWKCLI",STATNCOD
          IF        @EQUAL
            CALL      SETLMR5
            CALL      SETACC5
            CALL      HWKCLIN0              * HWK Printing of Clinical
            GOTO      PRTMR599              * Screen
          ENDIF
.
.                                           * HDH Printing of Clinical
          MATCH     "HDHCLN",STATNCOD       * Hawkesbury Dist.Health (HDH)
          IF        @EQUAL
            CALL      SETLMR5
            MOVE      ONE,PASSNO            * first pass -count pages *I-196463
            CALL      HDHCLIN0
            GOTO      PRTMR599              * Screen
          ENDIF
.
          MATCH     "HSPCLN",STATNCOD       * HealthScope Hospital
          IF        @EQUAL
            CALL      SETLMR5
            MOVE      ONE,PASSNO            * first pass -count pages *I-196463
            CALL      HSPFP000
            GOTO      PRTMR599              * Screen
          ENDIF
.
          MATCH     "HSPCL2",STATNCOD       * HealthScope Hospital
          IF        @EQUAL
            CALL      SETLMR5
            MOVE      ONE,PASSNO            * first pass -count pages
            CALL      HSPF2000
            GOTO      PRTMR599              * Screen
          ENDIF
.
          MATCH     "WAPCLN",STATNCOD       * WA Hospital
          IF        @EQUAL
            CALL      SETLMR5
            MOVE      ONE,PASSNO            * first pass -count pages
            CALL      WACLN000
            GOTO      PRTMR599              * Screen
          ENDIF
.
          MATCH     "BHSCLN",STATNCOD
          IF        @EQUAL
            CALL      SETLMR5
            CALL      BHSCLIN0              * BHS Printing of Clinical
            GOTO      PRTMR599              * Screen
          ENDIF
.
          MOVE      ONE,HEMR5L
          BRANCH    HEMR5L,PRTMR520         * branch on MR5 layout
          CALL      SETLMR5
          CALL      PRTMR5                  * Print the MR-5 Form
          GOTO      PRTMR599
.
PRTMR520  MOVE      HEMR5COD,KEY6
          CALL      RDIBTH1                 * read header details file
          BRANCH    OVRCD,PRTMR591
.
          MOVE      IBTHDESC,SCHEDDSC       * Description for schedule record
.
          CALL      PRTMR5N2                * print the MR5
.
          COMPARE   ZERO,EXIT
          GOTO      PRTMR599 IF EQUAL
.
          MOVE      "No Template details on file.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PRTMR599
.
PRTMR590  MOVE      "Not using MR-5 Forms.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      PRTMR599
.
PRTMR591  MOVE      "User defined Stationary Code not on file",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
PRTMR599  RETURN
.------------------------------------------------------------
. Read EMRVIS details     
.------------------------------------------------------------
EMRDET00  MOVE      EMVIADMN,KEY8
          CALL      RDEMVIS1
          MOVE      OVRCD,EXIT
.
          MOVE      EMVIADMN,KEY8
          CALL      RDEMDCM1
          IF        OVRCD=1
            MOVE      SP70,EMDCTXT1
            MOVE      SP70,EMDCTXT2
            MOVE      SP70,EMDCTXT3
            MOVE      SP70,EMDCTXT4
            MOVE      SP70,EMDCTXT5
            MOVE      SP70,EMDCTXT6
            MOVE      SP70,EMDCTXT7
            MOVE      SP70,EMDCTXT8
            MOVE      SP70,EMDCTXT9
            MOVE      SP70,EMDCSPAR
          ENDIF
.
EMRDET99  RETURN
.------------------------------------------------------------
. Read unknown details     
.------------------------------------------------------------
UNKDET00  MOVE      EMVIADMN,KEY8
          CALL      RDEMUNK1  
          BRANCH    OVRCD,UNKDET90
.
          MOVE      EMUNDET1,PSNAME
          MOVE      EMUNDET2,PGNAME
          MOVE      EMUNSEX,PSEX  
          MOVE      EMUNAGE,PSAGE 
          MOVE      EMUNBDAT,PBDATE
          GOTO      UNKDET99
.
UNKDET90  MOVE      "Invalid Unknown Patient Admission Number",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
.
UNKDET99  RETURN
.------------------------------------------------------------
.  DISPEMR00 :
.  Modified DSPAAE00 to use EMR variables instead of AAE
.  DISPAAE1 routine modified from IBAAAE01
.------------------------------------------------------------
DSPEMR00  MOVE      EMVIURNO,PURNO
          MOVE      PURNO,KEY8
          CALL      RLPTMAS1
          BRANCH    OVRCD,DSPMER90,DSPMER91
.
          MOVE      SP3,SAVTYP
          CALL      DSPFIELD
.
          MOVE      EMVIURNO,PURNO
          MOVE      PURNO,KEY8
          CALL      UUPTMAS1
          MOVE      ZERO,EXIT
          GOTO      DSPMER99
.
DSPMER90  MOVE      "Invalid U/R Number.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      DSPMER99
.
DSPMER91  MOVE      "Patient Locked",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      DSPMER99
DSPMER99  RETURN
.------------------------------------------------------------
.  Routine from IBAAAE01 to clear patresfd
.------------------------------------------------------------
INTPRA00  MOVE      ZEROVISN,PKADMN
          PACK      PKNAME,SP70,SP10
          PACK      PKADD1,SP30,SP10
          PACK      PKADD2,SP30,SP10
          PACK      PKSUBR,SP30,SP10
...          PACK      PKADD4,SP30,SP10
          MOVE      SP8,PKPOST
          MOVE      SP20,PKTELEP
          MOVE      SP20,PKTELEB
          MOVE      SP10,PKRELP
INTPRA99  RETURN
.------------------------------------------------------------
DISPVARS  MOVE      SCRID,FORM2
          BRANCH    FORM2,DVARS100,DVARS100,DVARS200,DVARS200
          GOTO      DVARS999
DVARS100  CALL      DXPATMAS
          GOTO      DVARS999
DVARS200  CALL      WEBEMRDX
DVARS999  RETURN
.------------------------------------------------------------
.         Calculate dates difference
.         Routine from IBAAAE01
.------------------------------------------------------------
CALC      REP       " 0",WDATE1
          REP       " 0",WDATE2
          UNPACK    WDATE1,CCENT,XYEAR,XMON,XDAY
          MOVE      XYEAR,YY
          MOVE      XMON,MM
          MOVE      XDAY,DD
          CALL      DMYCON
          MOVE      JULDAY,CALDAYS
          MOVE      JULYR,JYEAR
.
          UNPACK    WDATE2,CCENT,XYEAR,XMON,XDAY
          MOVE      XYEAR,YY
          MOVE      XMON,MM
          MOVE      XDAY,DD
          CALL      DMYCON
.
          COMPARE   JULYR,JYEAR
          GOTO      NEXT1 IF EQUAL
          ADD       "365" TO CALDAYS
          ADD       LEAPYR TO CALDAYS
NEXT1     SUB       JULDAY,CALDAYS
          RETURN
.------------------------------------------------------------
. DUMMY ROUTINE
KFREEF00
KFREEF99  RETURN
.------------------------------------------------------------
          INC       EMRMR5N2
          INC       EMRMR5
          INC       DSPFIELD
          INC       DXPATMAS
          INC       DXPATCOD
          INC       DXPATDOC
          INC       WEBEMRDX
          INC       IBAOVRIO/INC
          INC       PATCODKY
          INC       SCRCKFLD
          INC       CLPATMAS
          INC       CLPMSPX2                                           *I-54627
          INC       PATGSRIO/INC
.
PRTEMRND  ENDPROC
