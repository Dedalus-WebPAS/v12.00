.******************************************************************************
.* Include Name   : KYNAEDOC.PBL                                              *
.*                                                                            *
.* Description    : Keyin of an A&E Doctor code                               *
.*                                                                            *
.* Author         : LOFTY         09/09/1993                                  *
.*                                                                            *
.* Parameters     : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF6 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  PATCODFD index 1 needs to be open                         *
.*                                                                            *
.* Returns        : DCODE    - Keyed in code                                  *
.*                  PATDO1FD record                                           *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.* On return      : You must display the description yourself.                *
.*                                                                            *
.******************************************************************************
.
KYNAEDOC  MOVE      ONE,CQUEST                   * set ? as not entered
          MOVE      ECOL,CCOL
          MOVE      EVERT,CVERT
.
KADCT100  MATCH     SP6,CKYIDEF6
          GOTO      KADCT120 IF EQUAL            * no default
.
          MOVE      CKYIDEF6,DCODE
          KEYIN     *PCCOL:CVERT,*V2LON,*RV,DCODE:
                    *PCCOL:CVERT,*DV,DCODE
          GOTO      KADCT150
.
KADCT120  KEYIN     *PCCOL:CVERT,*V2LON,DCODE:
                    *PCCOL:CVERT,*DV,DCODE
.
KADCT150  PACK      DCODE,DCODE,SP6
          MATCH     SP6,DCODE
          GOTO      KADCT790 IF EQUAL            * nothing entered
          MATCH     EXITCHAR,DCODE
          GOTO      KADCT820 IF EQUAL            * exitchar entered
          MATCH     QUEST,DCODE
          GOTO      KADCT200 IF NOT EQUAL        * ? not entered
.
.         ? entered
.
          IF        CQUEST = ONE
            MOVE      "0",HLEF                   * save all of the screen
            CALL      GETSCR00                   * save screen
            MOVE      ZERO,CQUEST                * set as ? performed
          ENDIF
.
          PROC      AAEDOCQY                     * display valid entries
          BRANCH    EXIT,KADCT790                * exit selected
.
          MOVE      DOCCODE,DCODE                * load selected doctor code
.
.         validate what was entered
.
KADCT200  MOVE      DCODE,KEY6
          CALL      RDDOCT1  
          BRANCH    OVRCD,KADCT400               * invalid code
.
.         check if an A&E doctor
.
          PACK      KEY5,ANSD,ANST,DRTYPE
          CALL      RDCODE1
          BRANCH    OVRCD,KADCT400               * invalid doctor type
.
          MATCH     "A",TCINDC5
          IF        !@EQUAL
            DISPLAY   *P1:24,*B,*EL,*V2LON,"WARNING",*HOFF,": This is not a ":
                      "current A&E doctor.  ",*EL;
            CALL      HITENTER
          ENDIF
          MOVE      ZERO,EXIT                    * set as valid code
          GOTO      KADCT800
.
.         invalid code
.
KADCT400  DISPLAY   *P1:24,*B,"Invalid Code.  ",*EL;
          CALL      HITENTER
          BRANCH    CQUEST,KADCT100
          DISPLAY   *P1:24,"Input the code : ",*EL;
          GOTO      KADCT100
.
.         nothing entered
.
KADCT790  MOVE      SP6,DCODE
          MOVE      ONE,EXIT                     * set exit flag
          COMPARE   ZERO,CKYIMAND
          GOTO      KADCT800 IF EQUAL            * valid entry
.
.         field is mandatory
.
          DISPLAY   *P1:24,*B,"Field is mandatory.  ",*EL;
          CALL      HITENTER
          BRANCH    CQUEST,KADCT100
          DISPLAY   *P1:24,"Input the code : ",*EL;
          GOTO      KADCT100
.
.         valid entry
.
KADCT800  IF        CQUEST = ZERO
            CALL      PUTSCR00                   * redisplay screen
          ENDIF
          DISPLAY   *PECOL:EVERT,*V2LON,DCODE
          GOTO      KADCT999 
.
.         exitchar entered
.
KADCT820  MOVE      TWO,EXIT                     * EXITCHAR entered
          MOVE      SP6,DCODE                    * clear code
          IF        CQUEST = ZERO
            CALL      FRESCR00                   * free the screen
          ENDIF
.
KADCT999  RETURN
+
          INC       AAEDOCQY
