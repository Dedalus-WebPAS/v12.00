.******************************************************************************
.*  Include Name  : PATIC9KY.PBL                                              *
.*  Description   : Keyin of operation codes                                  *
.*  Author        : Ian Rutt                                                  *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF9 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMODE - is keyin mode                                  *
.*                                                                            *
.*  Returns       : OPCODE   - Keyed in code                                  *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Modifications :                                                           *
.*        V5.07.02  27/07/1999  Caleb Sharman SRF 632440                      *
.*                  Mods for IBABIL77                                         *
.*        V5.07.01  05/07/1999  Jill Habasque SRF 645594                      *
.*                  Copied PATICOKY and changed to use IC9KYWSR instead of    *
.*                  ICOKYWSR so that only ICD9 files are opened               *
.*        V6.05.02  25/09/1998    Katie Nelson                                *
.*                  Added procedure HPTOIA00 to key word search               *
.*        V6.05.01  03/06/1998  Greg Horvat                                   *
.*                  Changed to auto allocate the - for ICD10 codes            *
.*        V6.03.00  26/09/1996  Greg Horvat      Holy Spirit Mods             *
.*                  Ported from release 5.04                                  *
.*                                                                            *
.******************************************************************************
PATIC9KY
P9IOKY00  CALL      GETSCR00
          MATCH     SP9,CKYIDEF9                  * using a default ?
          GOTO      P9IOKY10 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN9:      * no default used
                    *PECOL:EVERT,*V2LON,OPCODE:
                    *PECOL:EVERT,*DV,OPCODE
          GOTO      P9IOKY20
.
P9IOKY10  MOVE      CKYIDEF9,OPCODE                 * default
          KEYIN     *PECOL:EVERT,*DV,OPCODE:     * default used
                    *PECOL:EVERT,*V2LON,*RV,OPCODE:
                    *PECOL:EVERT,*DV,OPCODE
.
.         check what was entered
.
P9IOKY20  CALL      IC9CK000
          BRANCH    EXIT,P9IOKY25,P9IOKY31,P9IOKY41,P9IOKY95
.                        "?"      valid    nothing  exitchar
          GOTO      P9IOKY00
.
.         a "?" was input
.
P9IOKY25  CALL      PATICODS
          BRANCH    OVRCD,P9IOKY00
          IF        CKYIMODE=1
            CALL      REXISTS
            GOTO      P9IOKY00
          ENDIF
          KEYIN     *P1:24,*EL,*P20:24," Enter op. code :":
                    *P47:24,*V2LON,OPCODE
          PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          PACK      KEY9,OPCODE,SP9
          CALL      RDICO1   
.
P9IOKY31  MOVE      FALSE,EXIT                    * valid input
          DISPLAY   *PECOL:EVERT,*V2LON,OPCODE,*P48:EVERT,ODESC
          GOTO      P9IOKY99 
.
.         nothing was input
.
P9IOKY41  MOVE      ONE,EXIT                * Nothing or Spaces entered
          GOTO      P9IOKY99 
.
.         EXITCHAR input
.
P9IOKY95  MOVE      TWO,EXIT                * EXITCHAR
.
P9IOKY99  RETURN
+
.*********************************************************************
.*                  IC9CK000                    Called by : PATIC9KY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
IC9CK000  ENDSET    OPCODE
          APPEND    SP9,OPCODE
          RESET     OPCODE
.
          MATCH     EXITCHAR,OPCODE
          GOTO      IC9CK600 IF EQUAL       * exit char
.
          MATCH     SP9,OPCODE
          GOTO      IC9CK700 IF EQUAL       * nothing
.
          MATCH     QUEST,OPCODE
          GOTO      IC9CK800 IF EQUAL       * ? mark
.
.         a code was entered so validate
.
IC9CK500  PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MATCH     PTCNI10D,KEY8
          CALL      FTIC9000 IF NOT LESS
. 
          PACK      KEY9,OPCODE,SP9
          CALL      RDICO1
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,IC9CK900          * invalid code
          ELSE
            COMPARE   ZERO,OVRCD   
            GOTO      IC9CK900 IF EQUAL
          ENDIF  
.
          MOVE      TWO,EXIT                * valid code
          GOTO      IC9CK999
.
.         exitchar entered
.
IC9CK600  MOVE      FOUR,EXIT
          MOVE      SP9,OPCODE
          GOTO      IC9CK999
.
.         nothing entered
.
IC9CK700  BRANCH    CKYIMAND,IC9CK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP9,OPCODE
          GOTO      IC9CK999
.
.         "?" entered
.
IC9CK800  MOVE      ONE,EXIT
          GOTO      IC9CK999
.
.         invalid code entered
.
IC9CK900  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
.
          IF        CKYIMODE = 0
            DISPLAY   *P1:24,*EL,*B,"Invalid Operation Code. ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
          GOTO      IC9CK999
.
.         field is mandatory
.
IC9CK950  MOVE      ONE,HLEF
          MOVE      "24",HTOP
          MOVE      "80",HRIG
          MOVE      "24",HBOT
          CALL      GETSCR00
          DISPLAY   *P1:24,*B,"Field is mandatory. ",*EL;
          CALL      HITENTER 
          CALL      PUTSCR00
          MOVE      ZERO,EXIT
.
IC9CK999  RETURN
+
.******************************************************************************
.*                                  FTIC9000              Called by: IC9CK000 *
.*                              Format ICD10 Code                             *
.******************************************************************************
FTIC9000  SCAN      "-",OPCODE
          GOTO      FTIC9100 IF EQUAL       * Code already formatted
.
          MOVE      "-",KEY1
          UNPACK    OPCODE,KEY5,KEY4
          PACK      OPCODE,KEY5,KEY1,KEY4
.
FTIC9100  RESET     OPCODE
FTIC9900  MOVE      ZERO,EXIT
FTIC9999  RETURN
.
..         INCLUDE   PATICODS
          INC       IC9KYWSR
+
