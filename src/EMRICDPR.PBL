.------------------------------------------------------------
.  File          :  EMRICDPR.PBL
.
.  Function      :  Update Keyword Index File for ICD9 Disease Code
.
.  Parameters    :  EMIC9COD
.
.  Modifications : 16.08.2022 Alina Bhari  TSK 0903886 V11.02.01
.                             Emergency diagnosis maintance keyword search by
.                             70 character instead of 60 character 
.                             - SEVENTY,KEYWRD00,KEYWORDS,,BWORD000,BWORD190
.                : 05.04.2002 Pat Dirito
.                             Added EMICVEMD to Keywrord Generation
.                  27.03.2002 Bronko Sosic
.                             Changed Keyword creation due to table modification
.                             Now only used EMICKEYW for keyword 
.                  10.07.2000 B.G.Ackland
.                             Removed Close (Causing EMRWEB05 to I * C)
.
.------------------------------------------------------------
          DEFPROC   EMRICDUP
.
KEYINDEX  DIM       9         * Code Index in Table
KEYWORDS  DIM       70        * String Containing the Key Words to be Indexed
SEVENTY   FORM      "70"
KEYWRDNO  FORM      2
KEYWRD00  DIM       70
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
.------------------------------------------------------------
. Generate Word Index
.------------------------------------------------------------
GENR0000  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     EMRKEDA1,"emrkedaf"
          TRAPCLR  IO
          BRANCH   OVRCD,GENR9999
.
          MOVE     EMIC9COD,KEYINDEX
          CALL     CLEAR000           * Remove Current Key Word Index
          BRANCH   EXIT,GENR9999
.
          MOVE     EMIC9COD,KEYINDEX
          MOVE     EMICKEYW,KEYWORDS  
          CALL     BWORD000
.
          MOVE     EMICVEMD,KEYWORDS   * VEMD Code
          CALL     BWORD000
.
..          MOVE     EMIC9COD,KEYWORDS
..          CALL     BWORD000
.
..          MOVE     EMICDESC,KEYWORDS
..          CALL     BWORD000
.
GENR1900  
.
GENR9999  GOTO     EMRICDEP
.------------------------------------------------------------
. Clear Current Key Words
.------------------------------------------------------------
CLEAR000  PACK     KEY24,KEYINDEX,SP70
          CALL     RSEMKED1
          CALL     RKEMKED1
          BRANCH   OVRCD,CLEAR999
          MATCH    KEYINDEX,EMEDITM
          GOTO     CLEAR999 IF NOT EQUAL
.
          PACK     KEY24,EMEDITM,EMEDKWD,SP70
          CALL     DEEMKED1
          GOTO     CLEAR000
.
CLEAR999  RETURN
.-------------------------------------------------
. Determine Words to Index
.-------------------------------------------------
BWORD000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      BWORD000 IF NOT EOS
            GOTO      BWORD999               * entire name if blank
          ENDIF
          PACK      KEY70,KEYWORDS,SP30      * reset form pointer
          MOVE      KEY70,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      BWORD100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Save this Word
.
BWORD100  MOVE      KEYWORDS,KEY15
          PACK      KEY15,KEY15,SP70
          REP       UPPLOW,KEY15             * Always Upper Case
          PACK      KEY24,KEYINDEX,KEY15
          UNPACK    KEY24,EMEDITM,EMEDKWD
          CALL      RDEMKED1
          IF        OVRCD=1
            CALL      WREMKED1
          ENDIF
          GOTO      BWORD190
.
.         Check for any more words
.
BWORD190  SETLPTR   KEYWORDS,SEVENTY       * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY70,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY70,KEYWORDS
          GOTO      BWORD000
.
BWORD999  RETURN
.
          INC       EMRICDIO/INC
          INC       IBAOVRIO/INC
.
EMRICDEP  ENDPROC                          * End of Procedure
.
