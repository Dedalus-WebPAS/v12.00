.  ********************************************************************
.  *                                                                  *
.  *     Include Name   : IBATMSDS                                    *
.  *                                                                  *
.  *     Function       : To display the valid stationery codes for   *
.  *                      a particular stationery type.               *
.  *                                                                  *
.  *                      Requires - IBTMHSTP to be initialised       *
.  *                                 to required stationery type      *
.  *                                                                  *
.  *                                 IBADTMSD include which defines   *
.  *                                 the variables used in this inc.  *
.  *                                                                  *
.  *     Programmer     : Peter Eddey  09/07/90                       *
.  *                                                                  *
.  *     Modifications  :                                             *
.  *                      17/05/1996  Steve Armstrong                 *
.  *                      Fixed Next option                           *
.  *                                                                  *
.  *                                                                  *
.  **************************************************************************
.
IBATMDDS  
          DISPLAY   *P1:3,*EF
          DISPLAY   *P20:4,*V2LON,*ULON,"STATIONERY TYPES"
.
IBTMH050  MOVE      ZERO,FORM2        * set screen number
.
          DISPLAY   *P1:5,*EF           
          MOVE      ONE,CCOL         * set column
          MOVE      FIVE,CVERT       * set row
          MOVE      ZERO,FORM1        * set screen full flag
          ADD       ONE,FORM2         * increment the screen number
.
          PACK      KEY6,SP6
          CALL      RSIBTH1
.
IBTMH200
          CALL      RKIBTH1
          BRANCH    OVRCD,IBTMH400
.
          ADD       ONE,CVERT         * increment the row          
.
          DISPLAY   *PCCOL:CVERT,*V2LON,IBTHSCOD,*HOFF:
                    SP2,IBTHDESC
.
          COMPARE   TWENTY2,CVERT    * test if column is full
          GOTO      IBTMH300 IF EQUAL
.
          GOTO      IBTMH200
.
IBTMH300
          BRANCH    FORM1,IBTMH500   * if screen is full
          MOVE      ONE,FORM1        * set screen full flag
          MOVE      FIVE,CVERT      * reset row
          MOVE      FORTY,CCOL      * reset column
          GOTO      IBTMH200
.
.-----  the following controls the display of prompts ---
.-----  depending on the number of screens or if their --
.-----  is another screen etc.                        ---
.
IBTMH400
          COMPARE   ONE,FORM2
          GOTO      IBTMH999 IF EQUAL
.
          DISPLAY   *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst screen ? "
IBTMH450
          KEYIN     *P30:24,*DV,UNDLN1:
                    *P30:24,*V2LON,ANS:
                    *P30:24,*DV,ANS
.
          REP       UPPLOW,ANS 
          MATCH     ANS,ANSC
          GOTO      IBTMH999 IF EQUAL
.
          MATCH     ANS,ANSF
          GOTO      IBTMH050 IF EQUAL
.
          BEEP
          GOTO      IBTMH450
.
IBTMH500
.
.----  test if their is a "next" screen            -----
.
.
          CALL      RKIBTH1
          BRANCH    OVRCD,IBTMH400
.
          CALL      RPIBTH1
.
          COMPARE   ONE,FORM2
          GOTO      IBTMH600 IF EQUAL
.
          DISPLAY   *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst screen, (",*V2LON,"N":
                    *HOFF,")ext screen ? "
IBTMH550
          KEYIN     *P45:24,*DV,UNDLN1:
                    *P45:24,*V2LON,ANS:
                    *P45:24,*DV,ANS
.
          REP       UPPLOW,ANS
          MATCH     ANS,ANSC
          GOTO      IBTMH999 IF EQUAL
.
          MATCH     ANS,ANSF
          GOTO      IBTMH050 IF EQUAL
.
          MATCH     ANS,ANSN
          IF        @EQUAL
            DISPLAY   *P1:5,*EF           
            MOVE      ONE,CCOL         * set column
            MOVE      FIVE,CVERT       * set row
            MOVE      ZERO,FORM1        * set screen full flag
            ADD       ONE,FORM2         * increment the screen number
            GOTO      IBTMH200
          ENDIF
.
          BEEP                
          GOTO      IBTMH550
.  
IBTMH600
          DISPLAY   *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"N",*HOFF,")ext screen ? "
IBTMH650
          KEYIN     *P29:24,*DV,UNDLN1:
                    *P29:24,*V2LON,ANS:
                    *P29:24,*DV,ANS
.
          REP       UPPLOW,ANS 
          MATCH     ANS,ANSC
          GOTO      IBTMH999 IF EQUAL
.
          MATCH     ANS,ANSN
          IF        @EQUAL
            DISPLAY   *P1:5,*EF           
            MOVE      ONE,CCOL         * set column
            MOVE      FIVE,CVERT       * set row
            MOVE      ZERO,FORM1        * set screen full flag
            ADD       ONE,FORM2         * increment the screen number
            GOTO      IBTMH200
          ENDIF
.
          BEEP                
          GOTO      IBTMH650
.
IBTMH999
          RETURN
