.  ****************************************************************************
.  *                                                                          *
.  *  Program Name     :  PRIPRADS.PBL                                        *
.  *                                                                          *
.  *  Function         :  Displays the medical practices on file              *
.  *                      using PRIPRAC1                                      *
.  *                                                                          *
.  *  Author           :  Stephen Armstrong      28/12/90                     *
.  *                                                                          *
.  *  Keys             :  KEY6, KEY19                                         *
.  *                                                                          *
.  *  Returns          :  OVRCD = 1 if nothing on file                        *
.  *                                                                          *
.  *  Modifications    :                                                      *
.  *      V9.09.01  27/06/2007  Mike Laming   CAR 143531                      *
.  *                Add UPPLOW to (C)ontinue KEYIN                            *
.  *      .......   18/06/91  DIG   
.  *                                Changed to return OVRCD = 1 if nothing    *
.  *                                on file.
.  *                                                                          *
.  ****************************************************************************
PRIPRADS  DISPLAY    *P1:4,*EF,*P34:4,*V2LON,*ULON,"Medical Practices"
          MOVE       FIVE,CVERT
.
          MOVE       SP6,KEY6
          CALL       RSPRPR1                * see if anything is on file
          CALL       RKPRPR1  
          BRANCH     OVRCD,PRADS950
.
          GOTO       PRADS150
.        
.------ loop through the medical practice file ------
.
PRADS100  CALL       RKPRPR1  
          BRANCH     OVRCD,PRADS900
.
          ADD        ONE,CVERT
.
          COMPARE    TWENTY3,CVERT          * Page full ?
          GOTO       PRADS150 IF LESS       * no
          GOTO       PRADS200               * yes
.
.------ display the current record ------
.
PRADS150  DISPLAY    *P5:CVERT,*V2LON,PRPRCODE,SP4,*HOFF,PRPRDESC;
.
          GOTO       PRADS100
.
.------ Page full ------
.
PRADS200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, ":
                    "(",*V2LON,"F",*HOFF,")irst Screen, ":
                    "(",*V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
          REP       UPPLOW,ANS
.
          CMATCH    ANSC,ANS                * continue ?
          GOTO      PRADS900 IF EQUAL
.
          CMATCH    ANSF,ANS                * first screen ?
          GOTO      PRIPRADS IF EQUAL
.
          CMATCH    ANSN,ANS                * next screen ?
          GOTO      PRADS400 IF EQUAL
.
          BEEP                              * invalid option
          GOTO      PRADS200
.
.------ Clear the screen ------
.
PRADS400  MOVE      FIVE,CVERT
          DISPLAY   *P1:CVERT,*EF;
.
          GOTO      PRADS150
.
.------ end of file reached or Continue selected so reset OVRCD flag ------
.
PRADS900  MOVE      ZERO,OVRCD
.
          GOTO      PRADS999
.
.------ medical practices file is empty ------
.
PRADS950  DISPLAY   *P1:24,*EL,*B,"No Medical Practices on file - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
.         
PRADS999  RETURN
+
