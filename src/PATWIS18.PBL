.******************************************************************************
.* System         : Patient Management                                        *
.* Include        : PATWIS18.PBL                                              *
.* Description    : Calculate WIES18 value for an individual admission        *
.******************************************************************************
.* Date           : 11/08/2011                                                *
.* Author         : Davin Sloan                                               *
.*                                                                            *
.* Vars Required  : VARWIS18 - Variables required for WIES18                  *
.*                                                                            *
.* Files Required Open :                                                      *
.*        PATASFFD  PATASFA1 - Admission special funding arrangement file     *
.*        PATCODFD  PATCODE1 - Codes file                                     *
.*        PATECDFD  PATECDA2 - Episode disease coding file                    *
.*        PATECOFD  PATECOA2 - Episode operation coding file                  *
.*        PAT10OFD  PATI10O1 - ICD10 operations file                          *
.*        PATICUFD  PATICUA1 - ICU file                                       *
.*        PATTRNFD  PATTRAN2 - Transfer file                                  *
.*        PATW18FD  PATW18A1 - Victorian cost weights (for WIES18) file       *
.*                                                                            *
.* Parameters Req : Successful read on admission file                         *
.*                  Successful read on discharge file                         *
.*                  HIHLOS   - for Hopsipta in Home LOS (from RHIHDAYS)       *
.*                  NBRDAYS  - for LOS calculation (from NHOSPDAY)            *
.*                  PNSWRACE - User defined field for race                    *
.*                  WIS18DRG - DRG to calculate WIES18 for                    *
.*                                                                            *
.* Returns        : TOTWIS18 - Total WIES18 value                             *
.*                  WIS18DRG - DRG used to calculate WIES18                   *
.*                  WIS18LOS - LOS used to calculate WIES18                   *
.*                  WIS18WGT - Weight used to calculate WIES18                *
.*                  WIS18DSC - Description of DRG weight used                 *
.*                                                                            *
.* Modifications  :                                                           *
.*       V12.00.01  14/05/2025  Peter Vela      TSK 0955096                   *
.*                  Alphanumeric Visit Number changes                         *
.*       V10.03.04  30/08/2013  Davin            CAR 290919                   *
.*                  Changed key14 to key16 for reads on patecdaf & patecoaf   *
.*       V10.03.03  12/10/2012  Mike Laming      CAR 271756                   *
.*                  Added check for "Victoria" in (V18IN000)
.*       V10.03.02  08/12/2011  Davin            CAR 256959                   *
.*                  Mods to VIC-DRG modification for obstetrics (V18IN000)    *
.*       V10.03.01  02/12/2011  Davin            CAR 251748                   *
.*                  Mods to VIC-DRG modification for obstetrics (V18IN000)    *
.*       V10.02.00  11/08/2011  Davin            CAR 238236                   *
.*                  Created for 2011/2012 WIES18 changes:                     *
.******************************************************************************
+
PATWIS18  CALL      IN18W000                * Initialise variables
          BRANCH    EXIT,WIS18950
.
          CALL      C18TY000                * Determine and Check Care Type
          BRANCH    EXIT,WIS18950
.
          CALL      C18TR000                * Check for Contracted Patient
          BRANCH    EXIT,WIS18950
.
          CALL      M18RG000                * Modify DRG if necessary
.
          CALL      C18RG000                * Check for problem DRG
          BRANCH    EXIT,WIS18950
.
          CALL      M18CH000                * Check for Mechanical Ventilation
.
          CALL      T18AL000                * Check for Thalessaemia
.
          CALL      A18AA000                * Check for AAA
.
          CALL      A18SD000                * Check for ASD
.
          CALL      B18SW000                * Calculate Base WIES18
.
          CALL      C18AB000                * Check for aboriginality
.
. ---- Calculate WIES18 value ----
.
          MOVE      TOTBWIES,TOTWIS18       * base WIES
          ADD       MVCOPAY,TOTWIS18        * mechv copayment
          ADD       THCOPAY,TOTWIS18        * thal  copayment
          ADD       AAACOPAY,TOTWIS18       * AAA   copayment
          ADD       ASDCOPAY,TOTWIS18       * ASD   copayment
          ADD       ATSIWIES,TOTWIS18       * ATSI  copayment
.
          MOVE      ZERO,EXIT
          GOTO      WIS18999
.
WIS18950  MOVE      ONE,EXIT
WIS18999  RETURN
+
.******************************************************************************
.*                                  IN18W000              Called by: PATWIS18 *
.*                         Initialise WIES18 variables                        *
.******************************************************************************
IN18W000  MOVE      ZERO,TOTWIS18
          MOVE      ZERO,WIS18LOS
          MOVE      ZERO,WIS18WGT
          MOVE      SP20,WIS18DSC
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATW18A1,"patw18af"
          TRAPCLR   IO
          BRANCH    OVRCD,IN18W950
.
IN18W900  MOVE      ZERO,EXIT
          GOTO      IN18W999
.
IN18W950  MOVE      ONE,EXIT
IN18W999  RETURN
+
.******************************************************************************
.*                                  C18TY000              Called by: PATWIS18 *
.*                 Check for valid Care Type & Incomplete Coding              *
.******************************************************************************
C18TY000  PACK     KEY5,ANSC,ANSC,ACARE     * use admission care type
          CALL     RDCODE1
          BRANCH   OVRCD,C18TY900
.
          MATCH    "6",TCINDC4              * Check Program Funding Source
          GOTO     C18TY900 IF NOT EQUAL
.
          MATCH    "0   ",THCSCOD           * check care types
          GOTO     C18TY900 IF EQUAL
          MATCH    "1   ",THCSCOD
          GOTO     C18TY900 IF EQUAL
          MATCH    "2   ",THCSCOD
          GOTO     C18TY900 IF EQUAL
          MATCH    "6   ",THCSCOD
          GOTO     C18TY900 IF EQUAL
          MATCH    "7   ",THCSCOD
          GOTO     C18TY900 IF EQUAL
          MATCH    "8   ",THCSCOD
          GOTO     C18TY900 IF EQUAL
          MATCH    "9   ",THCSCOD
          GOTO     C18TY900 IF EQUAL
          MATCH    "5   ",THCSCOD
          GOTO     C18TY900 IF EQUAL
          MATCH    "3   ",THCSCOD
          GOTO     C18TY900 IF EQUAL
          MATCH    "E   ",THCSCOD
          GOTO     C18TY900 IF EQUAL
          MATCH    "F   ",THCSCOD
          GOTO     C18TY900 IF EQUAL
          MATCH    "J   ",THCSCOD
          GOTO     C18TY900 IF EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     C18TY999
.
C18TY900  MOVE     ONE,EXIT                 * Not valid
C18TY999  RETURN
+
.******************************************************************************
.*                                  C18TR000              Called by: PATWIS18 *
.*                        Check for Contracted Patient                        *
.******************************************************************************
C18TR000  PACK     KEY25,AADMNO,ANSA,SP30
          CALL     RSPTASF1
C18TR100  CALL     RKPTASF1                 * Read adm. special funding file
          BRANCH   OVRCD,C18TR800           * still process
.
          MATCH    AADMNO,PTAFADMN
          GOTO     C18TR800 IF NOT EQUAL    * still process
.
          MATCH    ANSA,PTAFTYPE            * special fund. arrang. type = adm
          GOTO     C18TR100 IF NOT EQUAL
.
          MATCH    ANSB,PTAFCROL            * Ignore if contract role = 'B'
          GOTO     C18TR900 IF EQUAL
.
C18TR800  MOVE     ZERO,EXIT                * valid
          GOTO     C18TR999
.
C18TR900  MOVE     ONE,EXIT                 * invalid
C18TR999  RETURN
+
.******************************************************************************
.*                                  M18RG000              Called by: PATWIS18 *
.*                      Modify DRG if necessary for WIES18                    *
.******************************************************************************
M18RG000  CALL      R18IO000           * Radiotherapy
          CALL      V18IN000           * Vaginal Delivery
M18RG999  RETURN
+
.******************************************************************************
.*                                  R18IO000              Called by: M18RG000 *
.*                         Modify DRG if Radiotherapy                         *
.******************************************************************************
.>>>>     Don't modify these DRGs:
.
R18IO000  MATCH     "B61A",WIS18DRG
          GOTO      R18IO999 IF EQUAL
.
          MATCH     "B61B",WIS18DRG
          GOTO      R18IO999 IF EQUAL
.
          MATCH     "A40Z",WIS18DRG
          GOTO      R18IO999 IF EQUAL
.
          MATCH     "W60Z",WIS18DRG
          GOTO      R18IO999 IF EQUAL
.
          MATCH     "W61A",WIS18DRG
          GOTO      R18IO999 IF EQUAL
.
          MATCH     "W61B",WIS18DRG
          GOTO      R18IO999 IF EQUAL
.
          MATCH     "S65A",WIS18DRG
          GOTO      R18IO999 IF EQUAL
.
          MATCH     "S65B",WIS18DRG
          GOTO      R18IO999 IF EQUAL
.
          MATCH     "S65C",WIS18DRG
          GOTO      R18IO999 IF EQUAL
.
          MATCH     "B60A",WIS18DRG
          GOTO      R18IO999 IF EQUAL
.
          MATCH     "B60B",WIS18DRG
          GOTO      R18IO999 IF EQUAL
.
          MATCH     "B82A",WIS18DRG
          GOTO      R18IO999 IF EQUAL
.
          MATCH     "B82B",WIS18DRG
          GOTO      R18IO999 IF EQUAL
.
          MATCH     "B82C",WIS18DRG
          GOTO      R18IO999 IF EQUAL
.
.>>>>     Check if operation code exists:
.
          PACK      ICDRDDTE,DDATE,SP10
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
R18IO050  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,R18IO999
.
          MATCH     AADMNO,PTEOADMN         * same admission?
          GOTO      R18IO999 IF NOT EQUAL
.
.>>>>     Check if block number is in range 1786-1792, 1794 or 1795:
.
          PACK      KEY9,PTEOCODE,SP9
          CALL      RDPTICO1
          BRANCH    OVRCD,R18IO050
.
          MATCH     "1786",PT0OBLKN         * block code >= 1786?
          GOTO      R18IO050 IF LESS
.
          MATCH     "1796",PT0OBLKN         * block code <=1795?
          GOTO      R18IO050 IF NOT LESS
.
          MATCH     "1793",PT0OBLKN         * block code = 1793?
          GOTO      R18IO050 IF EQUAL
.
.>>>>     Check if disease code exists in range C00-D48:
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
R18IO100  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,R18IO999
.
          MATCH     AADMNO,PTEDADMN         * same admission?
          GOTO      R18IO999 IF NOT EQUAL
.
          MATCH     "C00",PTEDCODE          * neoplasm code >= C00?
          GOTO      R18IO100 IF LESS
.
          MATCH     "D49",PTEDCODE          * neoplasm code <= D48.9?
          GOTO      R18IO100 IF NOT LESS
.
          MOVE      "R64Z",WIS18DRG         * modified for WIES18
R18IO999  RETURN
+
.******************************************************************************
.*                                  V18IN000              Called by: M18RG000 *
.*                         Modify DRG if Vaginal Delivery                     *
.******************************************************************************
.>>>>     Modify DRG O60Z to O60A/O60B/O60C:
.
V18IN000  MATCH     "O60Z",WIS18DRG
          GOTO      V18IN999 IF NOT EQUAL
.
          COMPARE   "3",PTCNHDPS            * check for Victoria
          GOTO      V18IN999 IF NOT EQUAL
.
.>>>>     Default to DRG O60A or O60B
          MOVE      PTPGPCCL,F1
          IF        F1 > 2
            MOVE      "O60A",WIS18DRG       * clinical complexity level > 2
          ELSE
            MOVE      "O60B",WIS18DRG       * clinical complexity level <= 2
          ENDIF
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
V18IN100  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,V18IN999
.
          MATCH     AADMNO,PTEDADMN         * same admission?
          GOTO      V18IN999 IF NOT EQUAL
.
          CMATCH    "O",PTEDCODE
          GOTO      V18IN100 IF NOT EQUAL   * obstetrics code?
.
          MATCH     "O60C",WIS18DRG
          GOTO      V18IN500 IF EQUAL       * multiple obstetrics codes exist
.
          MATCH     CMDISP,PTEDTYPE
          GOTO      V18IN500 IF NOT EQUAL   * Secondary obstetrics codes exist
.
.>>>>     Principal diagnosis is O80 or O83 ?
          MATCH     "O80",PTEDCODE
          IF        @EQUAL
            MOVE      "O60C",WIS18DRG       * modified for WIES18
          ELSE
            MATCH     "O83",PTEDCODE
            IF        @EQUAL
              MOVE      "O60C",WIS18DRG     * modified for WIES18
            ELSE
              GOTO      V18IN500            * obstetrics code is not O80 or O83
            ENDIF
          ENDIF
          GOTO      V18IN100                * check for more obstetrics codes
.
.>>>>     Secondary obstetrics codes exist
V18IN500  MOVE      PTPGPCCL,F1
          IF        F1 > 2
            MOVE      "O60A",WIS18DRG       * clinical complexity level > 2
          ELSE
            MOVE      "O60B",WIS18DRG       * clinical complexity level <= 2
          ENDIF
.
V18IN999  RETURN
+
.******************************************************************************
.*                                  C18RG000              Called by: PATWIS18 *
.*                            Check for Problem DRG                           *
.******************************************************************************
C18RG000  PACK     KEY4,WIS18DRG
          CALL     RDPT181                  * check WIES18 weights file
          BRANCH   OVRCD,C18RG900
.
          MATCH    "960Z",PT18DRGC
          GOTO     C18RG900 IF EQUAL
.
          MATCH    "961Z",PT18DRGC
          GOTO     C18RG900 IF EQUAL
.
          MATCH    "962Z",PT18DRGC
          GOTO     C18RG900 IF EQUAL
.
          MATCH    "963Z",PT18DRGC
          GOTO     C18RG900 IF EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     C18RG999
.
C18RG900  MOVE     ONE,EXIT
C18RG999  RETURN
+
.******************************************************************************
.*                                  M18CH000              Called by: PATWIS18 *
.*                Calculate Mechanical Ventialtion Co-payment                 *
.******************************************************************************
M18CH000  MOVE      ZERO,ADJMVDAY
          MOVE      ZERO,MVCOPAY
.
          PACK      KEY8,AADMNO
          CALL      RDPTICU1                * get mech. vent. hours
          BRANCH    OVRCD,M18CH999
.
          MATCH     "D",PT18MVEL            * check mech. vent. eligibility
          IF        @EQUAL
            IF        PTICUMEC > 6
              ASSIGN    ((PTICUMEC+12)/24),FORM12P4    * days receiving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7659)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
            GOTO      M18CH999
          ENDIF
.
          MATCH     "4",PT18MVEL
          IF        @EQUAL
            IF        PTICUMEC > 96
              ASSIGN    (((PTICUMEC+12)/24)-4),FORM12P4   * days recving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7659)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
          ENDIF
.
M18CH999  RETURN
+
.******************************************************************************
.*                                  T18AL000              Called by: PATWIS18 *
.*                      Calculate Thalessaemia Co-payment                     *
.******************************************************************************
T18AL000  MOVE      ZERO,THCOPAY
          MATCH     "T",PT18CPAY            * check WIES18 weights file
          IF        @EQUAL
            PACK      KEY16,AADMNO,SP20
            CALL      RSPTECD2              * Position on & read a patient
T18AL100    CALL      RKPTECD2                episode disease file record
            BRANCH    OVRCD,T18AL999
.
            MATCH     AADMNO,PTEDADMN
            GOTO      T18AL999 IF NOT EQUAL
.
            MATCH     "D56",PTEDCODE        * check ICD-10 code
            IF        !@EQUAL
              MATCH     "D57.2",PTEDCODE    * check ICD-10 code
              GOTO      T18AL100 IF NOT EQUAL
            ENDIF
            MOVE      "0.2648",THCOPAY      * eligible for thal. copayment
          ENDIF
T18AL999  RETURN
+
.******************************************************************************
.*                                  A18AA000              Called by: PATWIS18 *
.*                          Calculate AAA Co-payment                          *
.******************************************************************************
A18AA000  MOVE      ZERO,AAACOPAY
          MATCH     "AAA",PT18CPAY          * check WIES18 weights file
          GOTO      A18AA999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
A18AA100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,A18AA999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      A18AA999 IF NOT EQUAL
.
          MATCH     "33116-00",PTEOCODE     * check ICD-10 code
          GOTO      A18AA100 IF NOT EQUAL
.
          MOVE      "3.1421",AAACOPAY       * eligible for AAA copayment
A18AA999  RETURN
+
.******************************************************************************
.*                                  A18SD000              Called by: PATWIS18 *
.*                          Calculate ASD Co-payment                          *
.******************************************************************************
A18SD000  MOVE      ZERO,ASDCOPAY
          MATCH     "ASD",PT18CPAY          * check WIES18 weights file
          GOTO      A18SD999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
A18SD100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,A18SD999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      A18SD999 IF NOT EQUAL
.
          MATCH     "38742-00",PTEOCODE     * check ICD-10 code
          GOTO      A18SD100 IF NOT EQUAL
.
          MOVE      "2.4713",ASDCOPAY       * eligible for ASD copayment
A18SD999  RETURN
+
.******************************************************************************
.*                                  B18SW000              Called by: PATWIS18 *
.*                            Calculate Base WIES18                           *
.******************************************************************************
B18SW000  CALL      D18SC000                * length of stay category
          CALL      C18ST000                * calculate inlier status
.
          MATCH     "L",W18NLIER            * low outlier
          CALL      BW18L000 IF EQUAL       * calculate base WIES18
.
          MATCH     "I",W18NLIER            * inlier
          CALL      BW18N000 IF EQUAL       * calculate base WIES18
.
          MATCH     "H",W18NLIER            * high outlier
          CALL      BW18H000 IF EQUAL       * calculate base WIES18
.
B18SW999  RETURN
+
.******************************************************************************
.*                                  D18SC000              Called by: B18SW000 *
.*                      Determine Length Of Stay Category                     *
.******************************************************************************
D18SC000  MOVE      NBRDAYS,WIS18LOS        * length of stay from NHOSPDAY
          MOVE      "M",LOSCAT              * set LOS category to multiday
          MATCH     ADATE,DDATE             * check for sameday
          IF        @EQUAL
            MOVE      ONE,SAMEDAY           * sameday
            MOVE      ONE,WIS18LOS          * LOS = 1
            MOVE      "S",LOSCAT            * set LOS category to sameday
            GOTO      D18SC999
          ELSE
            MOVE      ZERO,SAMEDAY          * not sameday
          ENDIF
.
          IF        SAMEDAY = 0 & WIS18LOS = 1
            MOVE      "O",LOSCAT            * set LOS category to oneday
            GOTO      D18SC999
          ENDIF
.
          IF        WIS18LOS > 1825
            MOVE      "1825",WIS18LOS       * maximum LOS is 5 years
          ENDIF
.
D18SC999  RETURN
+
.******************************************************************************
.*                                  C18ST000              Called by: B18SW000 *
.*                           Calculate Inlier Status                          *
.******************************************************************************
C18ST000  MOVE      "I",W18NLIER            * set status to inlier
          IF        WIS18LOS < PT18LIBP
            MOVE      "L",W18NLIER          * set status to low outlier
            GOTO      C18ST999
          ENDIF
.
          ASSIGN    (PT18HIBP+ADJMVDAY),FORM12P4   * high bound. + mech.v days
          IF        WIS18LOS > FORM12P4
            MOVE      "H",W18NLIER          * set status to high outlier
          ENDIF
C18ST999  RETURN
+
.******************************************************************************
.*                                  BW18L000              Called by: B18SW000 *
.*                     Calculate Base WIES18 for Low Outlier                  *
.******************************************************************************
BW18L000  MATCH     "S",LOSCAT            * sameday
          IF        @EQUAL
            MOVE      PT18TSDW,TOTBWIES             * base WIES18
            ASSIGN    (TOTBWIES/PT18TIMW),TOTIES18  * IES18
.
            MOVE      PT18TSDW,WIS18WGT             * for display
            MOVE      "Same Day Wgt ",WIS18DSC
            GOTO      BW18L999
          ENDIF
.
          MATCH     "O",LOSCAT            * oneday
          IF        @EQUAL
            MOVE      PT18TODW,TOTBWIES             * base WIES18
            ASSIGN    (TOTBWIES/PT18TIMW),TOTIES18  * IES18
.
            MOVE      PT18TODW,WIS18WGT             * for display
            MOVE      "One Day Wgt  ",WIS18DSC
            GOTO      BW18L999
          ENDIF
.
          MATCH     "M",LOSCAT            * multiday
          IF        @EQUAL
            ASSIGN    (PT18TODW+((WIS18LOS-1)*PT18TLOM)),TOTBWIES
.                                                    * base WIES18
            ASSIGN    (TOTBWIES/PT18TIMW),TOTIES18   * IES18
.
            MOVE      PT18TLOM,WIS18WGT             * for display
            MOVE      "Low Outlr Wgt",WIS18DSC
            GOTO      BW18L999
          ENDIF
BW18L999  RETURN
+
.******************************************************************************
.*                                  BW18N000              Called by: B18SW000 *
.*                      Calculate Base WIES18 for Inlier                      *
.******************************************************************************
BW18N000  MOVE      ONE,TOTIES18          * IES18 = 1
          MATCH     "S",LOSCAT            * sameday
          IF        @EQUAL
            MOVE      PT18TSDW,TOTBWIES   * base WIES18
.
            MOVE      PT18TSDW,WIS18WGT             * for display
            MOVE      "Same Day Wgt ",WIS18DSC
            GOTO      BW18N999
          ENDIF
.
          MATCH     "O",LOSCAT            * oneday
          IF        @EQUAL
            MOVE      PT18TODW,TOTBWIES   * base WIES18
.
            MOVE      PT18TODW,WIS18WGT             * for display
            MOVE      "One Day Wgt  ",WIS18DSC
            GOTO      BW18N999
          ENDIF
.
          MATCH     "M",LOSCAT            * multiday
          IF        @EQUAL
            MOVE      PT18TIMW,TOTBWIES   * base WIES18
.
            MOVE      PT18TIMW,WIS18WGT             * for display
            MOVE      "Inlier Wgt   ",WIS18DSC
            GOTO      BW18N999
          ENDIF
BW18N999  RETURN
+
.******************************************************************************
.*                                  BW18H000              Called by: B18SW000 *
.*                    Calculate Base WIES18 for High Outlier                  *
.******************************************************************************
BW18H000  ASSIGN    ((WIS18LOS-PT18HIBP)-ADJMVDAY),HIGHDAYS  * high outlr days
          COMPARE   ZERO,HIGHDAYS
          IF        @LESS
            MOVE      ZERO,HIGHDAYS         * highdays must be >= zero
          ENDIF
.
          COMPARE   HIGHDAYS,HIHLOS
          IF        @LESS
            MOVE      HIHLOS,HITHDAYS       * get lesser of highdays and
          ELSE                              *  HIHLOS
            MOVE      HIGHDAYS,HITHDAYS
          ENDIF
.
          ASSIGN    (HIGHDAYS-HITHDAYS)*PT18THOM,TOTBWIES    *high WIES
          ASSIGN    (HITHDAYS*PT18THIH)+TOTBWIES,TOTBWIES    *HITH WIES
          ASSIGN    (PT18TIMW+TOTBWIES),TOTBWIES             *base WIES18
          ASSIGN    (TOTBWIES/PT18TIMW),TOTIES18             * IES18
.
          MOVE      PT18THOM,WIS18WGT                        * for display
          MOVE      "High Outl Wgt",WIS18DSC
BW18H999  RETURN
+
.******************************************************************************
.*                                  C18AB000              Called by: PATWIS18 *
.*                          Check for Aboriginality                           *
.******************************************************************************
C18AB000  MOVE      ZERO,ATSIWIES                * set ATSI WIES to zero
          IF        PNSWRACE<1 | PNSWRACE>14
            GOTO      C18AB999              * Invalid parameter
          ENDIF
.
          LOAD      TCODE,PNSWRACE,CATP1,CATP2,CATP3,CATP4,CATP5:
                                   CATP6,CATA1,CATA2,CATA3,CATA4:
                                   CATA5,CATA6,CATA7,CATVA
          LOAD      ACODE,PNSWRACE,PUSR1,PUSR2,PUSR3,PUSR4,PUSR5:
                                   PUSR6,AUSR1,AUSR2,AUSR3,AUSR4:
                                   AUSR5,AUSR6,AUSR7,PMPXABRG
.
          MATCH     ACODE,SP3
          GOTO      C18AB999 IF EQUAL
.
          PACK      KEY5,TCODE,ACODE
          CALL      RDCODE1                 * Read a code file record
          BRANCH    OVRCD,C18AB999
.
          UNPACK    THCSCOD,DIM1,KEY1       * use 2nd char of HDP Equiv
.>>>>     MOVE      THCSCOD,KEY1
          TYPE      KEY1
          GOTO      C18AB999 IF NOT EQUAL   * Non numeric, error
.
          MOVE      KEY1,FORM1
.
. ---- Calculate ATSI Loading if aboriginality = 1, 2, or 3 ----
.
          IF        FORM1=1 | FORM1=2 | FORM1=3
            MOVE      TOTBWIES,FORM12P4       * base WIES
            ADD       MVCOPAY,FORM12P4        * mechv copayment
            ADD       THCOPAY,FORM12P4        * thal  copayment
            ADD       AAACOPAY,FORM12P4       * AAA   copayment
            ADD       ASDCOPAY,FORM12P4       * ASD   copayment
            ASSIGN    (0.3*FORM12P4),ATSIWIES
          ENDIF
.
C18AB999  RETURN
+
