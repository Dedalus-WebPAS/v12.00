.--------------------------------------------------------------------------
. Ward Bed File
.
. Inputs  -  Valid record from the patwrdaf
. Outputs -  Field specified by FLDITMNO & DIM127 for any parameters
.
.
. Modifications :
.--------------------------------------------------------------------------
. V12.00.01 16/05/2025 Thanh T         TSK 0955096
.           Changed for alphanumeric visit number
.------------------------------------------------------------------------
. V10.03.01 23/11/2011  Ebon Clements   CAR 255738
.           Show ward beds in short description order
. V10.02.02 18.10.2011  Peter McMullen 250289    
.           Omit propercase conversion at WRDF0200                      
. V10.02.01 09.05.2011  Peter McMullen 240725     
.           Added tag for Ward Short Descripion (wrff4100)              
. V10.00.01 22/10/2010  Davin         CAR 231481
.           Added tag for Ward Provider Number (wrdf4000)
.  V9.09.02 22/12/2007  Mike Laming   CAR 156741
.           Alter PTWDDFVS & PTWDDFPH tags for No/Yes selection
.  V9.09.01 13/12/2007  Mike Laming   CAR 156741
.           Added tags for PTWDDFVS & PTWDDFPH
.  V9.04.02 07.12.2004 Ebon Clements CAR 55406
.           Added new tag for PTWDCOMM
.  V9.04.01 19.10.2004 Ebon Clements CAR 54412
.           Added new tag for PTWDSEX code
.  V9.03.02 22.10.2003 Ebon Clements CAR 44497
.           Added new tag for PTWDHOSN
.  V9.03.01 22/09/2003 Sylvek Litewka CAR 41826
.           Added display for '5 Day Ward', 'Infection Status'
.           and 'House Keeping Status' fields.
.  V9.02.01 05.02.2002 Add DOSA Ward
.  V9.00.00 07.05.2001 Ebon Clements 
.           Added selection list of empty beds for the 
.            admitted ward of a patient
.  V5.07.00 03.12.1999 Pat Dirito
.           Modifications for PATWEB95
.           Now check each variable for blanks and
.           Added 0=Description & 1=Code for WINPUT, WACTIVE
.           and PTWDDNIN. PTWDHOSN was incorrect there never
.           was a HU Category so it now reads the Hospital file
.
.--------------------------------------------------------------------------
. Add Field
.--------------------------------------------------------------------------
WRDF0000  BRANCH    FLDITMNO,WRDF0100,WRDF0200,WRDF0300,WRDF0400,WRDF0500: 
                             WRDF0600,WRDF0700,WRDF0800,WRDF0900,WRDF1000:
                             WRDF1100,WRDF1200,WRDF1300,WRDF1400,WRDF1500:
                             WRDF1600,WRDF1700,WRDF1800,WRDF1900,WRDF2000:
                             WRDF2100,WRDF2200,WRDF2300,WRDF2400,WRDF2500:
                             WRDF2600,WRDF2700,WRDF2800,WRDF2900,WRDF3000:
                             WRDF3100,WRDF3200,WRDF3300,WRDF3400,WRDF3500:
                             WRDF3600,WRDF3700,WRDF3800,WRDF3900,WRDF4000:
                             WRDF4100
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      WRDF9999
.
WRDF0100  REP       " +",WARDCODE
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
          WRITE     HTMLFILE;LINKPRG,".pbl?":
                             "&reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&wardcode=",WARDCODE;
          REP       "+ ",WARDCODE
          GOTO      WRDF9999
.
.WRDF0200  CLEAR     DISPNAME
.          PACK      NAME35,WBDESC,SP70
.          CALL      NAMSTR00
.          RESET     DISPNAME
.          MOVE      DISPNAME,KEY20
.          WRITE     HTMLFILE;KEY20;
WRDF0200  WRITE     HTMLFILE;WBDESC;
          GOTO      WRDF9999
.
WRDF0300  WRITE     HTMLFILE;WWARD;
          GOTO      WRDF9999
.
WRDF0400  WRITE     HTMLFILE;WBED;
          GOTO      WRDF9999
.
WRDF0500  MATCH     SP70,WBDESC
          GOTO      WRDF9999 IF EQUAL 
          WRITE     HTMLFILE;WBDESC;
          GOTO      WRDF9999
.
WRDF0600  MATCH     ZEROVISN,WADMNO
          GOTO      WRDF9999 IF EQUAL 
          WRITE     HTMLFILE;WADMNO;
          GOTO      WRDF9999
.
WRDF0700  MATCH     SP70,WEXTN
          GOTO      WRDF9999 IF EQUAL 
          WRITE     HTMLFILE;WEXTN;
          GOTO      WRDF9999
.
WRDF0800  COMPARE   ZERO,WBLINE 
          GOTO      WRDF9999 IF EQUAL 
          WRITE     HTMLFILE;WBLINE;
          GOTO      WRDF9999
.
WRDF0900  MOVE      "RT",CATEGORY
          MOVE      WRTYPE,CODE
          CALL      CODITM00
          GOTO      WRDF9999
.
WRDF1000  MOVE      "BT",CATEGORY
          MOVE      WEFRBT,CODE
          CALL      CODITM00
          GOTO      WRDF9999
.
WRDF1100  COMPARE   ZERO,WNBEDS
          GOTO      WRDF9999 IF EQUAL 
          WRITE     HTMLFILE;WNBEDS;
          GOTO      WRDF9999
.
WRDF1200  COMPARE   ZERO,WHCSSUB
          GOTO      WRDF9999 IF EQUAL 
          WRITE     HTMLFILE;WHCSSUB;
          GOTO      WRDF9999
.
WRDF1300  MATCH     SP70,WSERV
          GOTO      WRDF9999 IF EQUAL 
          WRITE     HTMLFILE;WSERV;
          GOTO      WRDF9999
.
WRDF1400  COMPARE   ZERO,WPLUR
          GOTO      WRDF9999 IF EQUAL 
          WRITE     HTMLFILE;WPLUR;
          GOTO      WRDF9999
.
.  0=Description, 1=Code
.
WRDF1500  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      KEY1,F1 
          BRANCH    F1,WRDF1550
          IF        WINPUT=0
            WRITE     HTMLFILE;"No";
          ELSE
            WRITE     HTMLFILE;"Yes";
          ENDIF
          GOTO      WRDF9999
.
WRDF1550  WRITE     HTMLFILE;WINPUT;
          GOTO      WRDF9999
.            
WRDF1600  COMPARE   ZERO,WOCCBED
          GOTO      WRDF9999 IF EQUAL 
          WRITE     HTMLFILE;WOCCBED;
          GOTO      WRDF9999
.
WRDF1700  MOVE      "CG",CATEGORY
          MOVE      WCLASS,CODE
          CALL      CODITM00
          GOTO      WRDF9999
.
WRDF1800  MATCH     ZEROVISN,WSTBY
          GOTO      WRDF9999 IF EQUAL 
          WRITE     HTMLFILE;WSTBY;
          GOTO      WRDF9999
.
.  0=Description, 1=Code
.
WRDF1900  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      KEY1,F1 
          BRANCH    F1,WRDF1950
          IF        WACTIVE=0
            WRITE     HTMLFILE;"Yes";  * Active
          ELSE
            WRITE     HTMLFILE;"No";   * Not Active
          ENDIF
          GOTO      WRDF9999
WRDF1950  IF        WACTIVE=0
            WRITE     HTMLFILE;"0";   * Active
          ELSE
            WRITE     HTMLFILE;"1";   * Not Active
          ENDIF
          GOTO      WRDF9999
.
WRDF2000  
..          MOVE      "BT",CATEGORY
..          MOVE      PTWRABTY,CODE
..          CALL      CODITM00
          GOTO      WRDF9999
.
WRDF2100  
..          MOVE      "CW",CATEGORY
..          MOVE      PTWRCCTR,CODE
..          CALL      CODITM00
          GOTO      WRDF9999
.
WRDF2200  
..          MATCH     SP70,PTWRGLDG 
..          GOTO      WRDF9999 IF EQUAL 
..          WRITE     HTMLFILE;PTWRGLDG;
          GOTO      WRDF9999
.
WRDF2300  CALL      HOSSEL00            * Hospital Code Selection List 
          GOTO      WRDF9999
.
.  0=Description, 1=Code
.
WRDF2400  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      KEY1,F1 
          BRANCH    F1,WRDF2430
.
          BRANCH    PTWDDNIN,WRDF2410,WRDF2420
          WRITE     HTMLFILE;"Day Only";
          GOTO      WRDF9999
WRDF2410  WRITE     HTMLFILE;"Day & Night";
          GOTO      WRDF9999
WRDF2420  WRITE     HTMLFILE;"Night Only";
          GOTO      WRDF9999
.
WRDF2430  BRANCH    PTWDDNIN,WRDF2440,WRDF2450
          WRITE     HTMLFILE;"0";
          GOTO      WRDF9999
WRDF2440  WRITE     HTMLFILE;"1";
          GOTO      WRDF9999
WRDF2450  WRITE     HTMLFILE;"2";
          GOTO      WRDF9999
.
.
WRDF2500  MOVE      "IA",CATEGORY
          MOVE      PTWDIAGE,CODE
          CALL      CODITM00
          GOTO      WRDF9999
.
WRDF2600  MOVE      "IC",CATEGORY
          MOVE      PTWDICRE,CODE
          CALL      CODITM00
          GOTO      WRDF9999
.
WRDF2700  MATCH     ANSM,PTWDSEX
          IF        @EQUAL
            WRITE     HTMLFILE;"Male";
          ENDIF
          MATCH     ANSF,PTWDSEX
          IF        @EQUAL
            WRITE     HTMLFILE;"Female";
          ENDIF
          MATCH     ANSE,PTWDSEX
          IF        @EQUAL
            WRITE     HTMLFILE;"Either";
          ENDIF
          GOTO      WRDF9999
.
WRDF2800  MOVE      PTWDLOCN,LOCNCODE
          CALL      MRTLOC00
          GOTO      WRDF9999
.
WRDF2900  CALL      BDSL0000          * Selection list of empty beds for a ward
          GOTO      WRDF9999
.
WRDF3000  CALL      NRST0000          * Selection list of Nursing station
          GOTO      WRDF9999
.
WRDF3100  WRITE     HTMLFILE;PTWDDOSA;
          GOTO      WRDF9999
.
WRDF3200  WRITE     HTMLFILE;PTWDFVDW;      * 5 Day Ward
          GOTO      WRDF9999
.
WRDF3300  MOVE      "BW",CATEGORY           * Infection Status
          MOVE      PTWDIFST,CODE
          CALL      CODITM00
          GOTO      WRDF9999
.
WRDF3400  MOVE      "BV",CATEGORY           * House Keeping Status
          MOVE      PTWDHKST,CODE
          CALL      CODITM00
          GOTO      WRDF9999
.
WRDF3500  WRITE     HTMLFILE;PTWDHOSN;      * Hospital code
          GOTO      WRDF9999
.
WRDF3600  WRITE     HTMLFILE;PTWDSEX;       * Intended patient sex code
          GOTO      WRDF9999
.
WRDF3700  MATCH     SP70,PTWDCOMM
          GOTO      WRDF9999 IF EQUAL
.
          WRITE     HTMLFILE;PTWDCOMM;      * Ward / Bed Comments
          GOTO      WRDF9999
.
WRDF3800  UNPACK    DIM127,D1,KEY1,DIM127   * Default Visitors
          MOVE      KEY1,F1 
          BRANCH    F1,WRDF3820,WRDF3830
.
WRDF3810  MOVE      NO,KEY3
          MATCH     "1",PTWDDFVS
          GOTO      WRDF3815 IF NOT EQUAL
          MOVE      YES,KEY3
WRDF3815  WRITE     HTMLFILE;KEY3;          * Default Visitor description 
          GOTO      WRDF9999
.
WRDF3820  MATCH     "1",PTWDDFVS
          GOTO      WRDF3825 IF EQUAL
          MOVE      "0",PTWDDFVS
WRDF3825  WRITE     HTMLFILE;PTWDDFVS;      * Default Visitor code
          GOTO      WRDF9999
.
WRDF3830  MATCH     "1",PTWDDFVS
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=#"0#">No</option>";
            WRITE     HTMLFILE;"<option value=#"1#"selected>Yes</option>";
          ELSE
            WRITE     HTMLFILE;"<option value=#"0#"selected>No</option>";
            WRITE     HTMLFILE;"<option value=#"1#">Yes</option>";
          ENDIF                             * Default Visitor selection
          GOTO      WRDF9999
.
WRDF3900  UNPACK    DIM127,D1,KEY1,DIM127  * Default Phone Calls
          MOVE      KEY1,F1 
          BRANCH    F1,WRDF3920,WRDF3930
.
WRDF3910  MOVE      NO,KEY3
          MATCH     "1",PTWDDFPH
          GOTO      WRDF3915 IF NOT EQUAL
          MOVE      YES,KEY3
WRDF3915  WRITE     HTMLFILE;KEY3;          * Default Phone Calls description
          GOTO      WRDF9999
.
WRDF3920  MATCH     "1",PTWDDFPH
          GOTO      WRDF3925 IF EQUAL
          MOVE      "1",PTWDDFPH
WRDF3925  WRITE     HTMLFILE;PTWDDFPH;      * Default Phone Calls code
          GOTO      WRDF9999
.
WRDF3930  MATCH     "1",PTWDDFPH
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=#"0#">No</option>";
            WRITE     HTMLFILE;"<option value=#"1#"selected>Yes</option>";
          ELSE
            WRITE     HTMLFILE;"<option value=#"0#"selected>No</option>";
            WRITE     HTMLFILE;"<option value=#"1#">Yes</option>";
          ENDIF                             * Default Phone Calls selection
          GOTO      WRDF9999
.
WRDF4000  WRITE     HTMLFILE;PTWDGLDG;       * Ward Provider Number
          GOTO      WRDF9999
.
WRDF4100 WRITE     HTMLFILE;PTWDSDES;       * Ward Short Description 
          GOTO      WRDF9999
.
WRDF9999  RETURN
.------------------------------------------------------------
. MRT Location Details
.
.  Parameters    LOCNCODE
.
.  Tag Parameter 0=Description
.                1=Code
.                2=Selection List
.------------------------------------------------------------
MRTLOC00  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,MRTLOC10,MRTLOC20
.
          MOVE      SP70,MRLODESC
          MATCH     SP70,LOCNCODE
          GOTO      MRTLOC99 IF EQUAL
          PACK      KEY4,LOCNCODE,SP70
          CALL      RDMRLOC1
          BRANCH    OVRCD,MRTLOC99 
          WRITE     HTMLFILE;TDESC;
          GOTO      MRTLOC99
.
MRTLOC10  WRITE     HTMLFILE;LOCNCODE;
          GOTO      MRTLOC99
.
MRTLOC20  PACK      KEY4,SP70
          CALL      RSMRLOC1
MRTLOC30  CALL      RKMRLOC1
          BRANCH    OVRCD,MRTLOC99
          COMPARE   "1",MRLOSTAT
          GOTO      MRTLOC30 IF EQUAL
          PACK      KEY6,QUOTE,MRLOCODE,QUOTE
          MATCH     MRLOCODE,LOCNCODE
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY6," selected>":
                               MRLODESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY6,">",MRLODESC,"</option>"
          ENDIF
          GOTO      MRTLOC30
.
MRTLOC99  RETURN
.------------------------------------------------------------
. Hospital Selection List
.------------------------------------------------------------
HOSSEL00  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          MATCH     SP70,PTWDHOSN
          GOTO      HOSSEL20 IF EQUAL
          PACK      KEY3,PTWDHOSN
          CALL      RDPTHSP1
          BRANCH    OVRCD,HOSSEL20
          BRANCH    F1,HOSSEL10,HOSSEL20,HOSSEL20
          WRITE     HTMLFILE;PTHSHOSP;
          GOTO      HOSSEL99
.
HOSSEL10  WRITE     HTMLFILE;PTHSNAME;
          GOTO      HOSSEL99
.
HOSSEL20  COMPARE   THREE,F1 
          IF        @EQUAL
            CALL      HSELA000
          ELSE
            CALL      HSEL0000
          ENDIF
.
HOSSEL99  RETURN
.
.------------------------------------------------------------
. Hospital Selection list - default WBSEHOSP 
.------------------------------------------------------------
HSELA000  PACK      KEY3,SP70
          CALL      RSPTHSP1
HSELA100  CALL      RKPTHSP1
          BRANCH    OVRCD,HSEL9999
.
          WRITE     HTMLFILE;"<option value=#"",PTHSHOSP,"#"";
          MATCH     WBSEHOSP,PTHSHOSP
          IF        @EQUAL
            WRITE     HTMLFILE;" selected";
          ENDIF
          WRITE     HTMLFILE;">",PTHSNAME,"</option>"
          GOTO      HSELA100
.
HSELA999  RETURN
.
.------------------------------------------------------------
. Hospital Selection list - default current
.------------------------------------------------------------
HSEL0000  PACK      KEY3,SP70
          CALL      RSPTHSP1
HSEL0100  CALL      RKPTHSP1
          BRANCH    OVRCD,HSEL9999
.
          WRITE     HTMLFILE;"<option value=#"",PTHSHOSP,"#"";
          MATCH     PTWDHOSN,PTHSHOSP
          IF        @EQUAL
            WRITE     HTMLFILE;" selected";
          ENDIF
          WRITE     HTMLFILE;">",PTHSNAME,"</option>"
          GOTO      HSEL0100
.
HSEL9999  RETURN
.

.------------------------------------------------------------
. Empty Bed selection list for a patients admitted ward 
.------------------------------------------------------------
BDSL0000  READ      CONTROLF,HUND12;*104,PTCNWBDS
          MOVE      PTCNWBDS,F1  * dsc: 0=code, 1=long name, 2=shortname 
.
          PACK      KEY16,AWARD,SP70
          CALL      RDSWARD8
BDSL0100  CALL      RDKWARD8
          BRANCH    OVRCD,BDSL9999
.
          MATCH     AWARD,WWARD              * Same ward as admission
          GOTO      BDSL9999 IF NOT EQUAL
.
          MATCH     SP70,WBED
          GOTO      BDSL0100 IF EQUAL
.
          BRANCH    WACTIVE,BDSL0100         * Do not display inactive bed
          MATCH     ZEROVISN,WADMNO
          GOTO      BDSL0100 IF NOT EQUAL    * Only add empty beds to selection
.
          IF        F1 > 0 
            IF        F1 = 2
              MOVE      PTWDSDES,KEY20
            ELSE
              MOVE      WBDESC,KEY20
            ENDIF
          ELSE
            MOVE      WBED,KEY20
          ENDIF
.
          WRITE     HTMLFILE;"<option value=#"",WBED,"#">",KEY20,"</option>"
.
          GOTO      BDSL0100
.
BDSL9999  RETURN
+
.------------------------------------------------------------
. Nursing station selection list
.------------------------------------------------------------
NRST0000  OPEN      PATNURS1,"patnursm"
          UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,NRST1000,NRST2000
.
          MATCH     SP70,PTWDNSTA
          GOTO      NRST9999 IF EQUAL
.
          PACK      KEY6,PTWDNSTA,SP70
          CALL      RDNURS1
          BRANCH    OVRCD,NRST9999
.
          WRITE     HTMLFILE;NSDESC;
          GOTO      NRST9999
.
NRST1000  WRITE     HTMLFILE;PTWDNSTA;
          GOTO      NRST9999
.
NRST2000  PACK      KEY6,SP70
          CALL      RDSNURS1
NRST2200  CALL      RDKNURS1
          BRANCH    OVRCD,NRST9999
.
          MATCH     SP3,NWARD
          GOTO      NRST2200 IF NOT EQUAL
.
          PACK      KEY5,QUOTE,NSTAT,QUOTE
.
          WRITE     HTMLFILE;"<option value=#"",NSTAT,"#"";
          MATCH     NSTAT,PTWDNSTA
          IF        @EQUAL
            WRITE     HTMLFILE;" selected";
          ENDIF
          WRITE     HTMLFILE;">",NSDESC,"</option>"
.
          GOTO      NRST2200
.
NRST9999  RETURN
+
