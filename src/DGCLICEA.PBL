.************************************************************************
.*  Procedure: DGCLICEA  -  Send an Allied Health Encounter             *
.*                                                                      *
.*  Author   : Mike Laming  (11/07/2001)                                *
.*                                                                      *
.*  Requires : PATMA1FD (Patient Master & Extension)                    *
.*             PMSPX2FD (Patient Master Extn.2)                         *
.*             ALLENCFD (Allied Health Encounter)                       *
.*                                                                      *
.*  Requirement removed (V9.02.01)                                      *
.*             PATCGPFD (Registered GP/Practice Changes)                *
.*             PATNIDFD (Patient National ID Number)                    *
.*             NHIMASFD (NZ Health Interface)                           *
.*             PMIGTNID.PBL (Get the National ID Number)                *
.*                                                                      *
.*  Mods     :                                                          *
.*                                                                      *
.************************************************************************
.*  V11.04.01 09/01/2024 Thanh T           TSK 0936263                  *
.*            Added OPEN/CLOSE PMSQPXA1 index                           *
.************************************************************************
.*  V10.03.01 17/04/2013  Davin S          CAR 280425                   *
.*            Changed IBCNUCIS - value 1 or greater will write trigger  *
.*  V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                  *
.*            Added setting of H7CITRID & H7CIINCL                      *
.*            09/03/2010  Steve Armstrong   135505                      *
.*            Mods to add in HL7 A03 message.                           *
.*            Mods to write proprietary messages directly to hl7inbaf   *
.*            instead of hl7bmtaf.                                      *
.************************************************************************
.*  V9.02.03  11/12/2001  Mike Laming                                   *
.*            Correct fields into key field HLBMKEY                     *
.*  V9.02.02  02/10/2001  Davin                                         *
.*            Mods to use message holding file                          *
.*  V9.02.01  31/08/2001  Mike Laming                                   *
.*            Add Logic to get PATCGPFD as this is not available in     *
.*            Calling Program. Also add NHIMAS-- and PATNID-- INCs      *
.*            into this Procedure.                                      *
.*                                                                      *
.************************************************************************
          DEFPROC   DGCLICEA
.
          INC       ALLQUEFD/INC
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
          INC       PMSQPTFD/INC
.
CISMFLAG  FORM      1             * CIS message flag
.                                    0 = don't send HL7 CIS message
.                                    1 = send HL7 CIS message
IBAMFLAG  FORM      1             * IBA proprietary message flag
.                                    0 = don't send IBA proprietary message
.                                    1 = send IBA proprietary message
MESSAGID  DIM       20
.
DGCLI000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,HUND06;*4,ALCNDGEA,*221,ALCNA03L
.
          MOVE      ZERO,CISMFLAG                * set default interface flag
          MATCH     "1",IBCNUCIS                 * using CIS interface ?
          IF        !@LESS
            MATCH     "1",ALCNA03L               * yes - sending A03 ?
            IF        @EQUAL
              MOVE      ONE,CISMFLAG             * yes- set flag for CIS message
            ENDIF
          ENDIF
.
.         Check if we are sending any messages.
.
          IF        ALCNDGEA <> 1 & CISMFLAG <> 1
            GOTO      DGCLI999
          ENDIF
.
.         We are sending IBA Proprietary and/or CIS HL7 messages, so
.         first open the relevant holding tables
.
          MOVE      ZERO,IBAMFLAG
          IF        ALCNDGEA = 1
            OPEN      HL7INB01,"hl7inbaf"
            MOVE      ONE,IBAMFLAG
          ENDIF
          IF        CISMFLAG = 1
            OPEN      HL7CISA1,"hl7cisaf"
          ENDIF
          OPEN      PMSQPTA1,"pmsqptaf"
          OPEN      PMSQPXA1,"pmsqpxaf"
          OPEN      ALLQUEA1,"allqueaf"
.
          CALL      DGMESSID                     * get the unique message id
.
.         Write a record to pmsqptaf (holding patient details)
.
          MOVE      MESSAGID,PMQPMESI
          MOVE      SP8,PMQPOURN
          PACK      PMQPSPAR,SP70,SP70
          CALL      WRPMQPT1
.
.         Write a record to allqueaf (holding A/H details)
.
          MOVE      MESSAGID,ALQUMESI
          PACK      ALQUSPAR,SP70,SP70
          CALL      WRALQUE1
.
          COMPARE   ONE,CISMFLAG                 * sending CIS HL7 A03 message?
          IF        @EQUAL
            MOVE      "A03",H7CIMETP
            MOVE      ALREVISN,H7CIVISN
            MOVE      HL7TRGID,H7CITRID
            MOVE      HL7INCLD,H7CIINCL
            CALL      WRCIS000                   * yes - write hl7cisaf record
          ENDIF
.
          COMPARE   ONE,ALCNDGEA                 * sending proprietary message?
          IF        @EQUAL
            MOVE      "CEA",H7INMETP
            CALL      WRINB000                   * yes - write hl7inbaf record
          ENDIF
.
          CLOSE     HL7INB01
          CLOSE     HL7CISA1
          CLOSE     PMSQPTA1
          CLOSE     PMSQPXA1
          CLOSE     ALLQUEA1
.
DGCLI999  GOTO      DGCLIEND                        * end procedure
.
.      I/O's go in here
.
          INC       BRHLCOMN
          INC       CLPMSQPX
.
          INC       ALLQUEIO/INC
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       IBAOVRIO/INC
          INC       PMSQPTIO/INC
.
DGCLIEND  ENDPROC
