.-------------------------------------------------------------
. SBACK - Display Screen Background
.-------------------------------------------------------------
SBACK000  
          DISPLAY   *P1:3,*EF
          MOVE      ZERO,EXIT
          PACK      KEY12,PRGID,SCRID,SP70
          CALL      RSSCSB1
SBACK100  CALL      RKSCSB1
          BRANCH    OVRCD,SBACK999
          MATCH     PRGID,SCSBPID
          GOTO      SBACK999 IF NOT EQUAL
          MATCH     SCRID,SCSBSID
          GOTO      SBACK999 IF NOT EQUAL
          MOVE      SCSBLIN,CVERT
          PUTVAR    SCSBDAT,SCSBATT,1,CVERT
          GOTO      SBACK100
SBACK999  RETURN
.-------------------------------------------------------------
. VBACK - Display Screen Background Clear Box
.-------------------------------------------------------------
VBACK000  
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,EXIT
          PACK      KEY12,PRGID,SCRID,SP70
          CALL      RSSCSB1
VBACK100  CALL      RKSCSB1
          BRANCH    OVRCD,VBACK999
          MATCH     PRGID,SCSBPID
          GOTO      VBACK999 IF NOT EQUAL
          MATCH     SCRID,SCSBSID
          GOTO      SBACK999 IF NOT EQUAL
          MOVE      SCSBLIN,CVERT
          SETLPTR   SCSBDAT,CRIG
          SETLPTR   SCSBATT,CRIG
          RESET     SCSBDAT,CLEF
          RESET     SCSBATT,CLEF
          PUTVAR    SCSBDAT,SCSBATT,CLEF,CVERT
          GOTO      VBACK100
.
VBACK999  RETURN
.-------------------------------------------------------------
. General Screen Prompt Keyin
.-------------------------------------------------------------
MTSTQ000  
          MOVE      PRCOL,CCOL
          MOVE      PRVERT,CVERT
          MOVE      ZERO,CKEYTYP
          MOVE      PRGID,SCSLPID
          MOVE      SCRID,SCSLSID
          CALL      KSCSLA00
          IF        EXIT=1|EXIT=2
            BEEP
            GOTO      MTSTQ000
          ENDIF
.
MTSTQ999  RETURN
.-------------------------------------------------------------
. General Screen Prompt Keyin
.-------------------------------------------------------------
MDELQ000  
          MOVE      "27",CCOL
          MOVE      "24",CVERT
          MOVE      ZERO,CKEYTYP
          MOVE      PRGID,SCSLPID
          MOVE      SCRID,SCSLSID
          CALL      KSCSLA00
          RETURN
.
.--------------------------------
. Display an Item
.--------------------------------
DISPITEM  
          MOVE      SCPSROW,CVERT       * Setup Display Position
          MOVE      SCPSCOL,CCOL 
          MOVE      SCPSLEN,LLCNT
          COMPARE   ZERO,LLCNT
          GOTO      DISITM99 IF EQUAL
          MATCH     SP70,SCPSFMT
          IF        !@EQUAL
            MATCH     SP70,VAR
            IF        !@EQUAL
              CALL      FORMATRD
            ENDIF
          ENDIF
          PACK      DIM2,SP1,SCPSATT
          PACK      ATT,SP70,SP70
          REP       DIM2,ATT
          SETLPTR   ATT,LLCNT
          SETLPTR   VAR,LLCNT
          PUTVAR    VAR,ATT,CCOL,CVERT
          PACK      PRINTVAR,SP70,SP70
          MOVE      VAR,PRINTVAR
          PACK      VAR,SP70,SP70
.
DISITM99  RETURN
.--------------------------------
. Format a Field - Truncate
.--------------------------------
FORMATVA  STRIP     VAR
          MOVELPTR  VAR,BEFDECP
          MOVELPTR  VAR,AFTDECP
          SCAN      DECIMALP,VAR
          IF        @EQUAL
            MOVEFPTR  VAR,BEFDECP
            SUB       BEFDECP,AFTDECP
            SUB       ONE,BEFDECP
          ELSE  
            MOVE      ZERO,AFTDECP
          ENDIF
          NFORMAT   FORM21,BEFDECP,AFTDECP
          RESET     VAR
          STRIP     VAR
          MOVE      VAR,FORM21
.
          MOVE      SCPSFMT,VAR
          STRIP     VAR
          MOVELPTR  VAR,BEFDECPF
          MOVELPTR  VAR,AFTDECPF
          SCAN      DECIMALP,VAR
          IF        @EQUAL
            MOVEFPTR  VAR,BEFDECPF
            SUB       BEFDECPF,AFTDECPF
            SUB       ONE,BEFDECPF
          ELSE  
            MOVE      ZERO,AFTDECPF
            IF        AFTDECP>0
              STRIP     VAR
              ENDSET    VAR
              APPEND    DECIMALP,VAR
              RESET     VAR
            ENDIF
          ENDIF
.
          WHILE     AFTDECPF<AFTDECP
            STRIP     VAR
            ENDSET    VAR
            ADD       ONE,AFTDECPF
            APPEND    "9",VAR
          DO
.
          ENDSET    VAR
          RESET     VAR
          SCAN      LEFTBRAK,VAR
          IF        @EQUAL
            STRIP     VAR
            ENDSET    VAR
            APPEND    SP1,VAR
            RESET     VAR
          ELSE 
            STRIP     VAR
          ENDIF
          FEDIT     FORM21,VAR
          SCAN      LEFTBRAK,VAR
          IF        @EQUAL
            RESET     VAR,LLCNT
            CMOVE     ")",VAR
            ENDSET    VAR
            RESET     VAR
          ENDIF
          RETURN
.
.--------------------------------
. Format a Field - Round
.--------------------------------
FORMATRD  STRIP     VAR
          MOVELPTR  VAR,BEFDECP
          MOVELPTR  VAR,AFTDECP
          SCAN      DECIMALP,VAR
          IF        @EQUAL
            MOVEFPTR  VAR,BEFDECP
            SUB       BEFDECP,AFTDECP
            SUB       ONE,BEFDECP
          ELSE  
            MOVE      ZERO,AFTDECP
          ENDIF
          NFORMAT   FORM21,BEFDECP,AFTDECP
          RESET     VAR
          STRIP     VAR
          MOVE      VAR,FORM21
.
          MOVE      SCPSFMT,VAR
          STRIP     VAR
          MOVELPTR  VAR,BEFDECPF
          MOVELPTR  VAR,AFTDECPF
          SCAN      DECIMALP,VAR
          IF        @EQUAL
            MOVEFPTR  VAR,BEFDECPF
            SUB       BEFDECPF,AFTDECPF
            SUB       ONE,BEFDECPF
          ELSE  
            MOVE      ZERO,AFTDECPF
          ENDIF
          NFORMAT   FORMAT21,BEFDECPF,AFTDECPF
          MOVE      FORM21,FORMAT21
.
          ENDSET    VAR
          RESET     VAR
          SCAN      LEFTBRAK,VAR
          IF        @EQUAL
            STRIP     VAR
            ENDSET    VAR
            APPEND    SP1,VAR
            RESET     VAR
          ELSE 
            STRIP     VAR
          ENDIF
.
          FEDIT     FORMAT21,VAR
.
          MATCH     "**",VAR
          IF        @EQUAL
            MOVE      FORMAT21,VAR
          ENDIF
.
          SCAN      LEFTBRAK,VAR
          IF        @EQUAL
            RESET     VAR,LLCNT
            CMOVE     ")",VAR
            ENDSET    VAR
            RESET     VAR
          ENDIF
          RETURN
.---------------------------------------------------------------------------
. Display an Wp Item Pass - Open file TEMPWORD and Normal Display Variables
.---------------------------------------------------------------------------
DISPWORD  
          MOVE      SCPSROW,CTOP
          MOVE      SCPSCOL,CLEF 
          MOVE      SCPSLEN,LLCNT
          MOVE      SCPSNOR,CVERT
          ASSIGN    CLEF+LLCNT+1,CRIG
          ASSIGN    CTOP+CVERT+1,CBOT
          MOVE      SCPSCOL,CCOL 
          BOX       16,CLEF,CTOP,CRIG,CBOT
.
          ADD       ONE,CTOP
          ADD       ONE,CLEF
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TEMPWORD,WORDNAME
          TRAPCLR   IO
          BRANCH    OVRCD,DISWOR99
DISWOR10  READ      TEMPWORD,SEQ;VAR
          GOTO      DISWOR80 IF OVER
          SETLPTR   VAR,LLCNT
          DISPLAY   *PCLEF:CTOP,*+,VAR
          ADD       ONE,CTOP
          COMPARE   CTOP,CBOT
          GOTO      DISWOR10 IF NOT EQUAL
          ADD       ONE,CLEF
          DISPLAY   *PCLEF:CBOT,*G29," more ",*G28
          GOTO      DISWOR90
DISWOR80  
          PACK      VAR,SP70,SP70
          SETLPTR   VAR,LLCNT
          DISPLAY   *PCLEF:CTOP,*+,VAR
          ADD       ONE,CTOP
          COMPARE   CTOP,CBOT
          GOTO      DISWOR80 IF NOT EQUAL
.
DISWOR90  CLOSE     TEMPWORD
DISWOR99  RETURN
.---------------------------------------------------------------------------
. Edit Word Processor Screen
.---------------------------------------------------------------------------
EDITWORD  MOVE      SCPSROW,CTOP
          MOVE      SCPSCOL,CLEF 
          MOVE      SCPSLEN,LLCNT
          MOVE      SCPSNOR,CVERT
          ASSIGN    CLEF+LLCNT+1,CRIG
          ASSIGN    CTOP+CVERT+1,CBOT
          MOVE      SCPSCOL,CCOL 
.
          WORD      WORDNAME,999,CLEF,CTOP,CRIG,CBOT,WORDTYPE
          IF        WORDTYPE=0
            CALL      DISPWORD
          ENDIF
.
          MOVE      SCPSNOR,CVERT
          MOVE      SCPSCOL,CCOL 
          RETURN
.--------------------------------------------
. Get Selected Item Details
.--------------------------------------------
SELECTED  PACK     KEY20,PRGID,SCRID,SCSLITM,SCSLROW,SCSLCOL
          MOVE     ZERO,EXIT
          CALL     RDSCPS2
          IF       OVRCD=1
            MOVE     ONE,EXIT
          ENDIF
          MOVE     SCPSCOL,CCOL
          MOVE     SCPSROW,CVERT
          RETURN
.--------------------------------------------
. Fatal Error Getout of Program
.--------------------------------------------
FATALERR  CHAIN    PGM
.--------------------------------------------
. Get The position of an Item
.--------------------------------------------
GETPOSIT  PACK     KEY20,PRGID,SCRID,FLDID,SP70
          CALL     RSSCPS2
          CALL     RKSCPS2
          BRANCH   OVRCD,GETPOS90
          MATCH    PRGID,SCPSPID
          GOTO     GETPOS90 IF NOT EQUAL
          MATCH    SCRID,SCPSSID
          GOTO     GETPOS90 IF NOT EQUAL
          MATCH    FLDID,SCPSITM
          GOTO     GETPOS90 IF NOT EQUAL
          MOVE     SCPSROW,CVERT
          MOVE     SCPSCOL,CCOL 
          MOVE     ZERO,EXIT
          GOTO     GETPOS99
GETPOS90  MOVE     ONE,EXIT
GETPOS99  RETURN
.*****************************************************************************
. YORN - get "yes" or "no"                            Called by lots
.    Requires - CCOL, CVERT  (screen address to get input)
.               DEFYESNO     (Default answer. Set to "_" if no default)
.    Returns  - FYESNO       (1=yes entered, 0=no entered)
.               YESNO        (string var of entry ie. "Yes" or "No"
. N.L.S Mod Required
.   KEY2  - Replace with RESPYORN. Pass through Common
.   YESNO - Expand to DIM ??
.   YES   - Pass through common expand to DIM  ??
.   NO    - Pass through common expand to DIM  ??
.*****************************************************************************
YORN0000  MOVE      "NY",KEY2
          MOVE      SP70,YESNO
          MOVE      DEFYESNO,ANS                 * get default
          KEYIN     *PCCOL:CVERT,*DV,ANS,"   ":  * read in description
                    *PCCOL:CVERT,*V2LON,*RV,ANS;
          REP       UPPLOW,ANS
          SCAN      ANS,KEY2
          IF        @EQUAL
            MOVEFPTR  KEY2,FYESNO
            LOAD      YESNO,FYESNO,NO,YES
            SUB       ONE,FYESNO
            DISPLAY   *PCCOL:CVERT,*V2LON,YESNO
            MOVE      "_",DEFYESNO
          ELSE
            BEEP
            GOTO      YORN0000
          ENDIF
          RETURN
.----------------------------------------
. Alternative Program Heading should be replaced in programs with DISPHEAD
.----------------------------------------
.HEADDISP 
.         STRIP     CNAME
.         MOVELPTR  CNAME,CCOL
.         DIV       TWO,CCOL
.         MULT      SEQ,CCOL                                     Date : 12/12/12
.         ADD       "40",CCOL
.         DISPLAY   *ES,*HON,SP70,SP10:
.                   *P1:1,PRGID,"/",VERSION,*PCCOL:1,CNAME:
.                   *P66:1,"Date:",CDATE
.         HLINE     *G37,2,1,80
.         STRIP     PRGNAM
.         MOVELPTR  PRGNAM,CCOL
.         DIV       TWO,CCOL
.         MULT      SEQ,CCOL
.         ADD       "38",CCOL
.         DISPLAY   *PCCOL:2,SP1,*+,PRGNAM,SP1
.         RETURN
.
.------------------------------------------------------------
. Display Message on Line 24 (No Hit Enter)
.------------------------------------------------------------
MESSAGE2  GETVAR    LINE24A,ATTR24A,1,24
          RESET     DISPMSG
          STRIP     DISPMSG
          ENDSET    DISPMSG
          APPEND    SP1,DISPMSG
          RESET     DISPMSG
          DISPLAY   *P1:24,*EL,*+,DISPMSG;
          RETURN
.
.------------------------------------------------------------
. Display Restore Line 24 after Message2
.------------------------------------------------------------
MESSAGE3  PUTVAR    LINE24A,ATTR24A,1,24
          RETURN
.------------------------------------------------------------
. Delete Yes or No
.------------------------------------------------------------
DELTYORN  GETVAR    LINE24A,ATTR24A,1,24
          DISPLAY   *P1:24,*EL,*+,"Delete (",*V2LON,"Y",*HOFF,"/":
                                             *V2LON,"N",*HOFF,") ? "
          MOVE      "16",CCOL
          MOVE      "24",CVERT
          CALL      YORN0000
          IF        FYESNO=0
            DISPLAY   *P1:24,"Deletion Aborted - ";
            CALL      HITENTER
          ENDIF
          PUTVAR    LINE24A,ATTR24A,1,24
          RETURN
.--------------------------------------------
. Get The position of an Item
.--------------------------------------------
GETMLPOS
          PACK     DIM15,SCPSPID,SCPSSID,SCPSROW,SCPSCOL
          MOVE     SKSCPS15,KEY15
          CALL     RDSCPS1
.
GMPOS010  CALL     RKSCPS1
          BRANCH   OVRCD,GMPOS090
          MATCH    PRGID,SCPSPID
          GOTO     GMPOS090 IF NOT EQUAL
          MATCH    SCRID,SCPSSID
          GOTO     GMPOS090 IF NOT EQUAL
          MATCH    FIRSTITM,SCPSITM
          GOTO     GMPOS090 IF EQUAL
          MATCH    FLDID,SCPSITM
          GOTO     GMPOS010 IF NOT EQUAL

          MOVE     SCPSROW,CVERT
          MOVE     SCPSCOL,CCOL 
          MOVE     ZERO,EXIT
          GOTO     GMPOS099
.
GMPOS090  MOVE     ONE,EXIT
GMPOS099  
          MOVE     DIM15,KEY15
          CALL     RASCPS1
          RETURN
.----------------------------------------------------------------------
. Generated Program Standard I/O Includes
.----------------------------------------------------------------------
          INC       SCRSBGIO/INC
          INC       SCRSLTKY    
          INC       SCRSLTIO/INC
          INC       SCRPSTIO/INC
.
          INC       STDHLPCD         
          INC       STD001IO
