.        PRTIPROV : Print Insurance Provider details for Johns Hopkins invoice
.
.        VERSION  :
.
.        V10.03.01  11/08/2011 J.Tan          CAR 237245
.                   Mods to change policy number to policy amount
.        V9.09.01   16/06/2011 J.Tan          CAR 237245
.                   Mods to allow changing insurance prov.after invoiced
.        V9.09.00   19/04/2011 J.Tan     CAR 237245
.                   Created a new routine to print Insurance provider
.
.******************************************************************************
.        Print Insurance provider
.******************************************************************************
PRTIPROV
.
PRIP0000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,NINTY8;*130,PTCNAGST
          OPEN      IBAGEDA1,"ibagedaf"
          OPEN      PATIPRA1,"patipraf"    * Johns Hopkins Insurance provider
.
.         Check if there is any Insurance Provider
.
          UNPACK    KEY19,KEY8,DIM8
          CALL      RSPTIPA1
          CALL      RKPTIPA1
          BRANCH    OVRCD,PRIP9999
          MATCH     KEY8,PTIRVISN          * Same visit number?
          GOTO      PRIP9999 IF NOT EQUAL
          MATCH     DIM8,PTIRINVN
          GOTO      PRIP9999 IF NOT EQUAL
.
.         MOVE      MAXLINE,FORM2
.         SUB       FIVE,FORM2
.         COMPARE   FORM2,COUNT               * Check For A Full Page
.         CALL      TAIL IF NOT LESS
.
          WHILE     COUNT<MAXLINE
            PRINT     *N;
            ADD       ONE,COUNT
          DO
          CALL      TAIL
.
          PRINT     *N,*3,"FOR INFORMATION":
                    *N,*N,*3,"INSURANCE PROVIDER":
                       *59,"POLICY AMOUNT":
                    *N,*3,"_______________________________________":
                       *42,"_______________":
                       *57,"_______________"
          ADD       FIVE,COUNT
.
          UNPACK    KEY19,KEY8,DIM8
          CALL      RSPTIPA1
PRIP1000  CALL      RKPTIPA1
          BRANCH    OVRCD,PRIP4000
.
          UNPACK    KEY19,KEY8,DIM8
          MATCH     KEY8,PTIRVISN          * Same visit number?
          GOTO      PRIP4000 IF NOT EQUAL
          MATCH     DIM8,PTIRINVN
          GOTO      PRIP4000 IF NOT EQUAL
.
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ADATE
          CALL      PACDATE
.
          COMPARE   MAXLINE,COUNT               * Check For A Full Page
          CALL      TAIL IF NOT LESS
.
          PRINT     *3,PTIRMEDI,*57,PTIRPOLA
          ADD       ONE,COUNT
          GOTO      PRIP1000
.
....................
.         Print the next header
.
PRIP4000  MOVE      MAXLINE,FORM2
          SUB       THREE,FORM2
          COMPARE   FORM2,COUNT               * Check For A Full Page
          CALL      TAIL IF NOT LESS
.
          PRINT     *N,*3,"MEDISAVE A/C HOLDER",*42,"CPF NO":
                    *60,"AMT DEDUCTED":
                    *N,*3,"_______________________________________":
                       *42,"_______________":
                       *57,"_______________"
          ADD       THREE,COUNT
.
          OPEN      PATIPRA1,"patipraf"    * Johns Hopkins Insurance provider
          UNPACK    KEY19,KEY8,DIM8
          CALL      RSPTIPA1
PRIP5000  CALL      RKPTIPA1
          BRANCH    OVRCD,PRIP6000
.
          UNPACK    KEY19,KEY8,DIM8
          MATCH     KEY8,PTIRVISN          * Same visit number?
          GOTO      PRIP6000 IF NOT EQUAL
          MATCH     DIM8,PTIRINVN
          GOTO      PRIP6000 IF NOT EQUAL
.
          COMPARE   MAXLINE,COUNT               * Check For A Full Page
          CALL      TAIL IF NOT LESS
          PRINT     *3,PTIRCHOL,*42,PTIRCPFN,*57,PTIRAMNT
.
          ADD       ONE,COUNT
          GOTO      PRIP5000
.
PRIP6000  MOVE      MAXLINE,FORM2
          SUB       "3",FORM2
          COMPARE   FORM2,COUNT               * Check For A Full Page
          CALL      TAIL IF NOT LESS
.
          MOVE      GSTAMNT,FORM7P2
.
          PACK      KEY14,PTCNAGST,PINVDTE,SP70
          CALL      RDIBGE1
          COMPARE   ZERO,OVRCD
          GOTO      PRIP9000 IF EQUAL       * found GST amount for current date
.
          CALL      RPIBGE1                 * read previous record
          BRANCH    OVRCD,PRIP8000
.
          MATCH     PTCNAGST,IBGECODE       * Same code ?
          GOTO      PRIP9000 IF EQUAL
.
PRIP8000  MOVE      SEVEN,IBGEAMNT          * default to 7%
.
PRIP9000  MOVE      MAXLINE,FORM2
          SUB       FOUR,FORM2
          COMPARE   FORM2,COUNT               * Check For A Full Page
          CALL      TAIL IF NOT LESS
.
          PRINT     *N,*3,"Total GST at",IBGEAMNT,"% is $",FORM7P2:
                    " for this bill.":
                    *N,*N,*3,"THIS TAX INVOICE SUPERSEDES ALL PREVIOUS TAX ":
                             "INVOICES FOR YOUR VISIT ON ":
                       *N,*3,CPCDATE
          ADD       FIVE,COUNT
.
PRIP9999  RETURN
+
