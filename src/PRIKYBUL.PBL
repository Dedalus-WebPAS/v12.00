.******************************************************************************
.*                                                                            *
.*                                PRIKYBUL                                    *
.*                                --------                                    *
.*                                                                            *
.*    Include to keyin the Bulk Bill Code and Insurance Code/Health Fund/Govt *
.*    Code, if appropriate, for Private Practice Bulk Billing in Cash         *
.*    Receipting.                                                             *
.*                                                                            *
.*    AUTHOR           : DIG                                                  *
.*                                                                            *
.*    DATE             : 18/03/92                                             *
.*                                                                            *
.*    FILES NEEDED     : PATCODDS - and associated variables                  *
.*                       PATINSDS - and associated variables                  *
.*                       PATGOVDS - and associated variables                  *
.*                       PATFNDDS - and associated variables                  *
.*                       PATCODFD + IO      INC       PATIN1FD + IO                     *
.*                       INC       PATGO1FD + IO      INC       PATFN1FD + IO                     *
.*                                                                            *
.*    VARIABLES NEEDED : HIGCODE  - DIM  6                                    *
.*                       HIGDESC  - DIM  30                                   *
.*                       INDICATR - DIM  1                                    *
.*                       KCOL     - FORM 3                                    *
.*                       KVERT    - FORM 2                                    *
.*                                                                            *
.*    PARAMETERS       : KVERT    - Vertical Position for Bulk Bill Code OR   *
.*                                  Insurance Code, etc, keyin                *
.*                       KCOL     - Horizontal Position for Bulk Bill Code OR *
.*                                  Insurance Code, etc, keyin                *
.*                       THCSCOD  - From the read of the codes file for the   *
.*                                  Bulk Bill Code entered. Must be a H, I,   *
.*                                  or G.                                     *
.*                                                                            *
.*    RETURNS          : EXIT     = 0 If we have a valid Bulk Bill Code OR    *
.*                                    Insurance Code, etc.                    *
.*                                = 1 If Exitchar is entered for Bulk Bill    *
.*                                    Code OR Insurance Code, etc.            *
.*                                = 2 If no Bulk Bill Code OR Insurance Code, *
.*                                    etc, is entered                         *
.*                       ACODE    - The bulk bill code entered if Exit = 0    *
.*                                  and we have called PRIKYBUL.              *
.*                       HIGCODE  - The insurance code/Health Fund/Govt code  *
.*                                  entered if Exit = 0 and we have called    *
.*                                  PRIKYHIG.                                 *
.*                       INDICATR - The first indicator set on the codes file *
.*                                  for bulk billing, if we have a valid bulk *
.*                                  bill code, otherwise spaces.              *
.*                                                                            *
.*    COMMENTS         : If the HDP equivalent on the patient codes file for  *
.*                       the Bulk Bill Code entered is not a G, H or I then   *
.*                       we do not want to enter a Government Code, Health    *
.*                       Fund, or Insurance Code respectively. This is        *
.*                       provided a Bulk Bill Code has been entered.          *
.*                                                                            *
.*    MODULES          : PRIKYBUL - Keyin the Bulk Bill Code                  *
.*                       PRIKYHIG - Keyin the Health Fund/Insurance code/Govt *
.*                                  code                                      *
.*                                                                            *
.*    MODIFICATIONS    :                                                      *
.*        V9.03.01  04/03/2003  J.Tan                                         *
.*                  Mods for WEB private practice invoice                     *
.*        V5.08.01  06/06/2000  Jill Habasque                                 *
.*                  Changed from key10 to key14 for the health fund file      *
.*                                                                            *
.******************************************************************************
PRIKYBUL  MOVE      SP1,INDICATR
          MOVE      SP3,ACODE
          DISPLAY   *PKCOL:KVERT,*EL,UNDLN3;
          KEYIN     *PKCOL:KVERT,*V2LON,ACODE;
          DISPLAY   *PKCOL:KVERT,*V2LON,ACODE;
.
          CALL      CHKBL000                * see what was entered
          BRANCH    EXIT,KYBUL999:          * exitchar entered
                         KYBUL999:          * nothing entered
                         KYBUL400           * ? entered
.
          PACK      KEY5,CATGB,ACODE
          CALL      RDCODE1                 * read the patient codes file
          BRANCH    OVRCD,KYBUL800          
.
          MOVE      ZERO,EXIT
.
          CALL      GETBL000                * get the indicator for bulk 
.                                             billing
          GOTO      KYBUL999
.
.------ ? entered for bulk bill code so save screen ------
.
KYBUL400  DISPLAY   *PKCOL:KVERT,*EL
.
          CALL      GETSCR00                * save the screen
.
.------ ? entered for bulk bill code after a ? already done ------
.
KYBUL410  MOVE      CATGB,CODE
.         CALL      PATCODDS                * list codes on file
.         COMPARE   ZERO,OVRCD              * see if codes existed
.         GOTO      KYBUL450 IF EQUAL
.
          MOVE      TRUE,EXIT
.
          GOTO      KYBUL900
.
.------ keyin bulk bill code after a ? option ------
.
KYBUL450  MOVE      SP3,ACODE
          DISPLAY   *P1:24,*EL,"Bulk Bill Code : ",UNDLN3;
          KEYIN     *P18:24,*V2LON,ACODE;
          DISPLAY   *P18:24,*V2LON,ACODE;
.
          CALL      CHKBL000                * see what was entered
          BRANCH    EXIT,KYBUL900:          * exitchar entered
                         KYBUL950:          * nothing entered
                         KYBUL410           * ? entered
.
          PACK      KEY5,CATGB,ACODE
          CALL      RDCODE1                 * read the patient codes file
          BRANCH    OVRCD,KYBUL850          * code not on file
.
          MOVE      ZERO,EXIT
.
          CALL      GETBL000                * get the indicator for bulk 
.                                             billing
          GOTO      KYBUL950
.
.------ bulk bill code not on file ------
.
KYBUL800  DISPLAY   *P1:24,*EL,*B,"Bulk Bill Code not on file - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      PRIKYBUL
.
.------ bulk bill code not on file after ? option ------
.
KYBUL850  DISPLAY   *P1:24,*EL,*B,"Bulk Bill Code not on file - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
.
          GOTO      KYBUL450
.
.------ free the screen ------
.
KYBUL900  CALL      FRESCR00                * free the previously saved screen
.
          GOTO      KYBUL999
.
.------ restore the screen ------
.
KYBUL950  CALL      PUTSCR00                * restore the screen
.
KYBUL999  RETURN            
+
.**************************************************************************
.*                                  CHKBL000                              *
.*               Check what was entered for the Bulk Bill Code            *
.* RETURNS - EXIT  = 0  Bulk Bill Code entered                            *
.*                 = 1  Exitchar entered                                  *
.*                 = 2  No Code entered                                   *
.*                 = 3  ? entered                                         *
.**************************************************************************
CHKBL000  ENDSET    ACODE
          APPEND    SP3,ACODE
          RESET     ACODE
.
          MATCH     SP3,ACODE                 * see if anything was entered 
          GOTO      CHKBL900 IF EQUAL         * no
.
          MATCH     EXITCHAR,ACODE            * see if exitchar was entered 
          GOTO      CHKBL950 IF EQUAL         * yes
.
          MATCH     QUEST,ACODE               * ? entered 
          GOTO      CHKBL800 IF EQUAL         * yes
.
          MOVE      ZERO,EXIT                 * code entered
.
          GOTO      CHKBL999
.
.------ ? entered ------
.
CHKBL800  MOVE      THREE,EXIT
.
          GOTO      CHKBL999
.
.------ Nothing entered ------
.
CHKBL900  MOVE      TWO,EXIT
.
          GOTO      CHKBL999
.
.------ Exitchar entered ------
.
CHKBL950  MOVE      ONE,EXIT
.
CHKBL999  RETURN
+
.**************************************************************************
.*                                GETBL000                                *
.*        Routine to get the first indicator set for bulk billing         *
.**************************************************************************
GETBL000  MOVE      ONE,COUNTR
.
          WHILE     COUNTR < SIX
             LOAD   TEMPINDC,COUNTR,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
.
             TYPE   TEMPINDC                * see if we have a number
             GOTO   GETBL100 IF EQUAL
.
             REP    "C1W2V3H4S5M6",TEMPINDC
.
             TYPE   TEMPINDC                * see if we have a number
             GOTO   GETBL100 IF NOT EQUAL
.
             REP    "1C2W3V4H5S6M",TEMPINDC
             MOVE   TEMPINDC,INDICATR
.
             GOTO   GETBL999
.
.------ increment the counter and continue loop ------
.
GETBL100     ADD    ONE,COUNTR
.
          DO
.
GETBL999  RETURN
+
.
.------ module to keyin the health fund/insurance code/govt code ------
.
PRIKYHIG  MOVE      SP6,HIGCODE
          DISPLAY   *PKCOL:KVERT,*EL,UNDLN6;
          KEYIN     *PKCOL:KVERT,*V2LON,HIGCODE;
          DISPLAY   *PKCOL:KVERT,*V2LON,HIGCODE;
.
          CALL      CHHIG000                * see what was entered
          BRANCH    EXIT,KYHIG999:          * exitchar entered
                         KYHIG999:          * nothing entered
                         KYHIG400           * ? entered
.
          CALL      READF000                * read the appropriate file
          BRANCH    CEXIT,PRIKYHIG
.
          MOVE      ZERO,EXIT
.
          GOTO      KYHIG999
.
.------ ? entered for code so save screen ------
.
KYHIG400  DISPLAY   *PKCOL:KVERT,*EL;
.
          CALL      GETSCR00                * save the screen
.
.------ ? entered for code after a ? already done ------
.
KYHIG410  MOVE      THCSCOD,ANS
          REP       "I1H2G3",ANS
          MOVE      ANS,FORM1
.
          BRANCH    FORM1,KYHIG420,KYHIG430,KYHIG440
.
.------ display all of the insurance codes ------
.
KYHIG420  
.         CALL      PATINSDS                * list codes on file
.         COMPARE   ZERO,OVRCD              * see if codes existed
.         GOTO      KYHIG450 IF EQUAL
.
          MOVE      TRUE,EXIT
.
          GOTO      KYHIG900
.
.------ display all of the health funds ------
.
KYHIG430  
.         CALL      PATDSFN1                * list codes on file
.         COMPARE   ZERO,OVRCD              * see if codes existed
.         GOTO      KYHIG450 IF EQUAL
.
          MOVE      TRUE,EXIT
.
          GOTO      KYHIG900
.
.------ display all the government codes ------
.
KYHIG440  
.         CALL      PATGOVDS                * list codes on file
.         COMPARE   ZERO,OVRCD              * see if codes existed
.         GOTO      KYHIG450 IF EQUAL
.
          MOVE      TRUE,EXIT
.
          GOTO      KYHIG900
.
.------ keyin insurance code/health fund/govt code after a ? option ------
.
KYHIG450  MOVE      SP6,HIGCODE
          DISPLAY   *P1:24,*EL,"Enter Code : ",UNDLN6;
          KEYIN     *P14:24,*V2LON,HIGCODE;
          DISPLAY   *P14:24,*V2LON,HIGCODE;
.
          CALL      CHHIG000                * see what was entered
          BRANCH    EXIT,KYHIG900:          * exitchar entered
                         KYHIG950:          * nothing entered
                         KYHIG410           * ? entered
.
          CALL      READF000                * read the appropriate file
          BRANCH    CEXIT,KYHIG450
.
          MOVE      ZERO,EXIT
.
          GOTO      KYHIG950
.
.------ free the screen ------
.
KYHIG900  CALL      FRESCR00                * free the previously saved screen
.
          GOTO      KYHIG999
.
.------ restore the screen ------
.
KYHIG950  CALL      PUTSCR00                * restore the screen
.
KYHIG999  RETURN            
+
.**************************************************************************
.*                                  CHHIG000                              *
.*   Check what was entered for the Health Fund/Insurance Co./Govt Code   *
.* RETURNS - EXIT  = 0  Health Fund/Insurance Co./Govt Code entered       *
.*                 = 1  Exitchar entered                                  *
.*                 = 2  No Code entered                                   *
.*                 = 3  ? entered                                         *
.**************************************************************************
CHHIG000  ENDSET    HIGCODE
          APPEND    SP6,HIGCODE
          RESET     HIGCODE
.
          MATCH     SP6,HIGCODE             * see if anything was entered 
          GOTO      CHHIG900 IF EQUAL       * no
.
          MATCH     EXITCHAR,HIGCODE        * see if exitchar was entered 
          GOTO      CHHIG950 IF EQUAL       * yes
.
          MATCH     QUEST,HIGCODE           * ? entered 
          GOTO      CHHIG800 IF EQUAL       * yes
.
          MOVE      ZERO,EXIT               * code entered
.
          GOTO      CHHIG999
.
.------ ? entered ------
.
CHHIG800  MOVE      THREE,EXIT
.
          GOTO      CHHIG999
.
.------ Nothing entered ------
.
CHHIG900  MOVE      TWO,EXIT
.
          GOTO      CHHIG999
.
.------ Exitchar entered ------
.
CHHIG950  MOVE      ONE,EXIT
.
CHHIG999  RETURN
+
.**************************************************************************
.                                  READF000                               *
.*  Read the appropriate file for the Health Fund/Insurance Co./Govt Code *
.**************************************************************************
READF000  MOVE      ZERO,CEXIT
          MOVE      THCSCOD,ANS
          REP       "I1H2G3",ANS
          MOVE      ANS,FORM1
.
          BRANCH    FORM1,READF100,READF200,READF300
.
.------ read the insurance file ------
.
READF100  MOVE      HIGCODE,KEY6
          CALL      RDINSR1                 * read the insurance file
          BRANCH    OVRCD,READF900
.
          MOVE      INAME,HIGDESC
.
          GOTO      READF999
.
.------ read the health fund file ------
.
READF200  PACK      KEY14,HIGCODE,ZERO,ZERO,ZERO,ZERO,SP10
          CALL      RDFUND1                 * read the health fund file
          BRANCH    OVRCD,READF930
.
          MOVE      HFNAME,HIGDESC
.
          GOTO      READF999
.
.------ read the government file ------
.
READF300  MOVE      HIGCODE,KEY6
          CALL      RDGOVR1                 * read the government file
          BRANCH    OVRCD,READF960
.
          MOVE      GNAME,HIGDESC
.
          GOTO      READF999
.
.------ insurance code not on file ------
.
READF900  DISPLAY   *P1:24,*EL,*B,"Insurance Code not on file - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
          MOVE      TRUE,CEXIT
.
          GOTO      READF999
.
.------ health fund not on file ------
.
READF930  DISPLAY   *P1:24,*EL,*B,"Health Fund not on file - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
          MOVE      TRUE,CEXIT
.
          GOTO      READF999
.
.------ government code not on file ------
.
READF960  DISPLAY   *P1:24,*EL,*B,"Government Code not on file - Hit <":
                           *V2LON,"ENTER",*HOFF,"> to continue";
          KEYIN     ANS;
          DISPLAY   *P1:24,*EL;
          MOVE      TRUE,CEXIT
.
READF999  RETURN
+
