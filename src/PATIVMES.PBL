.
.*********************************************************************
.*                          PATIVMES                                 *
.*                  Print messages on invoice                        *
.*********************************************************************
.         V9.08.02  12/08/2008  J.Tan      CAR 175967
.                   Mods checking for blank health fund prior printing moeity
.         V9.08.01  27/02/2007  Peter Vela CAR sjog-inv
.                   Check for EOS at printing patient moeity
.         V9.05.B01 01/02/2006  J.Tan      CAR 85787
.                   Fixed printing moiety
.         V9.02.01  11/11/2002  J.Tan
.                   Mods to read controlf file       
.         V6.09.01  27/03/2002  Raghu,HPS  SRF12042                     
.                   Fixed invoice printing alignment,
.                   Fixed PMOIETY display 
.         V6.09.00  14/12/2000  Jill Habasque                     
.                   Casemix mods - added printing of patient moiety
.
.         V6.07.01  07/07/2000  Steve Armstrong      
.                   Changed %REBPORTN to %REBTPORTN and %PATPORTN to %PATNPORTN
.         V6.06.01  19/05/1999 J Habasque  SRF 631315
.                   Fixed move of NETTOT to DIM10 instead of DIM9
.         V6.04.13  08/03/98 J.Tan  SRF 619732
.                   Mods to write to a text file
.*** This routine prints a message on the invoice depending on the ***
.*** system (inpatient) and the claim code. The system parameter   ***
.*** PTCNPCOM switches the printing of this message on and off     ***
.*** 0 = no don't print messages, 1 = print messages.              ***
.*********************************************************************
.
.
PATIVMES  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND05;*194,PTCNPCOM
          BRANCH    PTCNPCOM,PMESG100    * branch if printing messages
          GOTO      PMESG999               else print no messages
.
PMESG100  OPEN      PATINMA1,"patinmaf"
          PACK      KEY4,PINVSYS,PINVCLM * set up key (system,claim)
          CALL      RDPTINM1             * read message file
          BRANCH    OVRCD,PMESG999       * print no message if EOF
.
          MOVE      MAXLINE,FORM2
          SUB       TWO,FORM2
          COMPARE   FORM2,COUNT
          CALL      TAIL IF NOT LESS
.
          CALL      PPORSCAN             * scan for patient portion
          CALL      RPORSCAN             * scan for rebate portion
.
          ADD       ONE,COUNT
          IF        PTCNITXT = 1
            PRINT     *1,PTIMMES1
          ELSE
            PRINT     *2,PTIMMES1;
          ENDIF
          CALL      PEOL0000
          SUB       ZERO,PMOIETY         * format moiety for printing
.
          MATCH     SP70,AFUNDH
          GOTO      PMESG200 IF EQUAL
.
          MATCH     SP70,PTHFDSC1
          IF        !@EQUAL & !@EOS
            IF        PTCNITXT = 1
              PRINT     *1,PTHFDSC1,*46,PMOIETY      
            ELSE
              PRINT     *2,PTHFDSC1,*47,PMOIETY;
            ENDIF
          ENDIF
.
PMESG200  CALL      PEOL0000
          IF        PTCNITXT = 1
            PRINT     *1,PTIMMES2
          ELSE
            PRINT     *2,PTIMMES2;
          ENDIF
          CALL      PEOL0000
          GOTO      PMESG999
.
PMESG999  RETURN
.
.**********************************************************************
.*                      PPORSCAN                                      *
.**********************************************************************
.*** This routine searches the message to be printed for the string ***
.*** "%PATNPORTN" and replaces it with the actual patient portion.   ***
.**********************************************************************
.
.         First, move the net total to a DIM field
.
PPORSCAN
          MOVE      NETTOT,DIM10
.
.         Check if "%PATNPORTN" appears in the 1st message line
.
          SCAN      PATSCAN,PTIMMES1         * search for string
          GOTO      PSCAN500 IF NOT EQUAL    * string not found
.
.         Copy the net total over the top of the string. This works
.         because we have fudged it so they are the same length.
.
          RESET     DIM10
PSCAN100  CMOVE     DIM10,PTIMMES1           * copy the next character
          BUMP      PTIMMES1                 * move to the next position
          GOTO      PSCAN200 IF EOS
          BUMP      DIM10                    * move to the next position
          GOTO      PSCAN200 IF EOS
          GOTO      PSCAN100
.
.         Finished copying the net total. Check if they want it to appear
.         a second time on the same line.
.
PSCAN200  RESET     PTIMMES1
          GOTO      PPORSCAN
.
.         Now check if "%PATNPORTN" appears in the 2nd message line
.
PSCAN500  MOVE      NETTOT,DIM10
          SCAN      PATSCAN,PTIMMES2
          GOTO      PSCAN999 IF NOT EQUAL
.
.         Copy the net total over the top of the string. This works
.         because we have fudged it so they are the same length.
.
          RESET     DIM10
PSCAN600  CMOVE     DIM10,PTIMMES2           * copy the next character
          BUMP      PTIMMES2                 * move to the next char
          GOTO      PSCAN700 IF EOS
          BUMP      DIM10                    * move to the next char
          GOTO      PSCAN700 IF EOS
          GOTO      PSCAN600
.
.         Finished copying the net total. Check if they want it to appear
.         a second time on the same line.
.
PSCAN700  RESET     PTIMMES2
          GOTO      PSCAN500
.
PSCAN999  RETURN
          
.**********************************************************************
.*                      RPORSCAN                                      *
.**********************************************************************
.*** This routine searches the message to be printed for the string ***
.*** "%REBTPORTN" and replaces it with the actual rebate portion.    ***
.**********************************************************************
.
.         First, move the net total to a DIM field
.
RPORSCAN
          SUB       PMOIETY,REBTOT
          MOVE      REBTOT,DIM10
.
.         Check if "%REBTPORTN" appears in the 1st message line
.
          SCAN      REBSCAN,PTIMMES1         * search for string
          GOTO      RSCAN500 IF NOT EQUAL    * string not found
.
.         Copy the net total over the top of the string. This works
.         because we have fudged it so they are the same length.
.
          RESET     DIM10
RSCAN100  CMOVE     DIM10,PTIMMES1           * copy the next character
          BUMP      PTIMMES1                 * move to the next position
          GOTO      RSCAN200 IF EOS
          BUMP      DIM10                    * move to the next position
          GOTO      RSCAN200 IF EOS
          GOTO      RSCAN100
.
.         Finished copying the net total. Check if they want it to appear
.         a second time on the same line.
.
RSCAN200  RESET     PTIMMES1
          GOTO      RPORSCAN
.
.         Now check if "%REBTPORTN" appears in the 2nd message line
.
RSCAN500  MOVE      REBTOT,DIM10
          SCAN      REBSCAN,PTIMMES2
          GOTO      RSCAN999 IF NOT EQUAL
.
.         Copy the net total over the top of the string. This works
.         because we have fudged it so they are the same length.
.
          RESET     DIM10
RSCAN600  CMOVE     DIM10,PTIMMES2           * copy the next character
          BUMP      PTIMMES2                 * move to the next char
          GOTO      RSCAN700 IF EOS
          BUMP      DIM10                    * move to the next char
          GOTO      RSCAN700 IF EOS
          GOTO      RSCAN600
.
.         Finished copying the net total. Check if they want it to appear
.         a second time on the same line.
.
RSCAN700  RESET     PTIMMES2
          GOTO      RSCAN500
.
RSCAN999  RETURN
          
