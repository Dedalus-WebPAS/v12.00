.----------------------------------------------------------------------
. Modifications
.
.----------------------------------------------------------------------
. V11.00.01  12/11/2020  Thanh T        TSK 0878747
.            Added PTAR2800 and PTAR2900  
.----------------------------------------------------------------------
. V10.14.01  03/04/2019  Tracey Nguyen  TSK 0871443
.            Added ATYPE017, ATYPE018  
. V10.10.01  24/03/2017  Tracey Nguyen TSK 0833454
.            Added ATYPE014/ATYPE015/ATYPE016 and 
.            GETCDV00/GETVAV00/GETTXV00 for a full key read
. V10.08.02  25/05/2016  Don Nguyen    TSK 0316489
.            Removed PTATR005 from key for RSPTATR3
.            23/05/2016  Peter Vela    TSK 0321157
.            Added GETAVV00, UPDAVV00
. V10.08.01  28/04/2016  Don Nguyen    TSK 0316489
.            Added GETACV00, UPDACV00, GETTFV00, UPDATV00 & GETNTE00
. V10.03.01  03/12/2013  Peter Vela    CAR 286158
.            Created include
.
.------------------------------------------------------------------------
.   Clear Parameters for Patient Attributes Table
.------------------------------------------------------------------------
CLRATR00  MOVE      SP70,PTATR001
          MOVE      SP70,PTATR002
          MOVE      SP70,PTATR003
          MOVE      SP70,PTATR004
          MOVE      SP70,PTATR005
          MOVE      SP70,PTATR006
          MOVE      SP70,PTATR007
          MOVE      SP70,PTATR008
          MOVE      SP70,PTATR009
          MOVE      SP70,PTATR010
          MOVE      SP70,PTATR011
          MOVE      SP70,PTATR012
          MOVE      SP70,PTATR013
          MOVE      SP70,PTATR014
          MOVE      SP70,PTATR015
          MOVE      SP70,PTATR016
          MOVE      SP70,PTATR017
          MOVE      SP70,PTATR018
          MOVE      SP70,PTATR019
          MOVE      SP70,PTATR020
          MOVE      SP70,PTATR021
          MOVE      SP70,PTATR022
          MOVE      SP70,PTATR023
          MOVE      SP70,PTATR024
          MOVE      SP70,PTATR025
          MOVE      SP70,PTATR026
          MOVE      SP70,PTATR027
.
CLRATR99  RETURN
+
.------------------------------------------------------------
. Output Patient Attribute Field
.------------------------------------------------------------
PTAR0000  BRANCH    FLDITMNO,PTAR0100,PTAR0200,PTAR0300,PTAR0400,PTAR0500:
                             PTAR0600,PTAR0700,PTAR0800,PTAR0900,PTAR1000:
                             PTAR1100,PTAR1200,PTAR1300,PTAR1400,PTAR1500:
                             PTAR1600,PTAR1700,PTAR1800,PTAR1900,PTAR2000:
                             PTAR2100,PTAR2200,PTAR2300,PTAR2400,PTAR2500:
                             PTAR2600,PTAR2700,PTAR2800,PTAR2900
.
PTAR0100  WRITE     HTMLFILE;PTARURNO;
          GOTO      PTAR9999
.
PTAR0200  WRITE     HTMLFILE;PTARVISN;
          GOTO      PTAR9999
.
PTAR0300  MATCH     SP70,PTARDATE
          GOTO      PTAR9999 IF EQUAL
          UNPACK    PTARDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      PTAR9999
.
PTAR0400  WRITE     HTMLFILE;PTARTIME;    
          GOTO      PTAR9999        
.
PTAR0500  WRITE     HTMLFILE;PTARTYPE;
          GOTO      PTAR9999
.
PTAR0600  WRITE     HTMLFILE;PTARVAL1;
          GOTO      PTAR9999
.
PTAR0700  WRITE     HTMLFILE;PTARVAL2;
          GOTO      PTAR9999
.
PTAR0800  WRITE     HTMLFILE;PTARVAL3;
          GOTO      PTAR9999
.
PTAR0900  WRITE     HTMLFILE;PTARVAL4;
          GOTO      PTAR9999
.
PTAR1000  WRITE     HTMLFILE;PTARCOD1;
          GOTO      PTAR9999
.
PTAR1100  WRITE     HTMLFILE;PTARCOD2;
          GOTO      PTAR9999
.
PTAR1200  WRITE     HTMLFILE;PTARCOD3;
          GOTO      PTAR9999
.
PTAR1300  WRITE     HTMLFILE;PTARCOD4;
          GOTO      PTAR9999
.
PTAR1400  WRITE     HTMLFILE;PTARTXT1;
          GOTO      PTAR9999
.
PTAR1500  WRITE     HTMLFILE;PTARTXT2;
          GOTO      PTAR9999
.
PTAR1600  WRITE     HTMLFILE;PTARTXT3;
          GOTO      PTAR9999
.
PTAR1700  WRITE     HTMLFILE;PTARTXT4;
          GOTO      PTAR9999
.
PTAR1800  WRITE     HTMLFILE;PTARDELR;
          GOTO      PTAR9999
.
PTAR1900  WRITE     HTMLFILE;PTARCUSR;
          GOTO      PTAR9999
.
PTAR2000  MATCH     SP70,PTARCDTE
          GOTO      PTAR9999 IF EQUAL
          UNPACK    PTARCDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      PTAR9999
.
PTAR2100  WRITE     HTMLFILE;PTARCTME;
          GOTO      PTAR9999
.
PTAR2200  WRITE     HTMLFILE;PTARUUSR;
          GOTO      PTAR9999
.
PTAR2300  MATCH     SP70,PTARUDTE
          GOTO      PTAR9999 IF EQUAL
          UNPACK    PTARUDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      PTAR9999
.
PTAR2400  WRITE     HTMLFILE;PTARUTME;
          GOTO      PTAR9999
.
PTAR2500  MATCH     SP70,PTARFDTE
          GOTO      PTAR9999 IF EQUAL
          UNPACK    PTARFDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      PTAR9999
.
PTAR2600  WRITE     HTMLFILE;PTARFTME;
          GOTO      PTAR9999
.
PTAR2700  WRITE     HTMLFILE;PTARFUSR;
          GOTO      PTAR9999
.
PTAR2800  MOVE      "mX",KEY2
          PACK      KEY5,KEY2,PTARCOD1
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE      PTARCOD1,TDESC
          ENDIF
          WRITE     HTMLFILE;TDESC;
          GOTO      PTAR9999
.
PTAR2900  MOVE      "Order Not Stated",KEY16
          MATCH     SP70,PTARVAL1
          IF        !@EQUAL
            MATCH     "0",PTARVAL1
            IF        !@EQUAL
              MOVE      PTARVAL1,KEY16
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;KEY16;
          GOTO      PTAR9999
.
PTAR9999  RETURN
+
.------------------------------------------------------------
.   Set Parameters for patient attributes table
.------------------------------------------------------------
SPTATR00  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPTATR01,SPTATR02,SPTATR03,SPTATR04,SPTATR05:
                       SPTATR06,SPTATR07,SPTATR08,SPTATR09,SPTATR10:
                       SPTATR11,SPTATR12,SPTATR13,SPTATR14,SPTATR15:
                       SPTATR16,SPTATR17,SPTATR18,SPTATR19,SPTATR20:
                       SPTATR21,SPTATR22,SPTATR23,SPTATR24,SPTATR25:
                       SPTATR26,SPTATR27
.
          MOVE      ONE,EXIT
          GOTO      SPTATR99
.
SPTATR01  MOVE      QRYDATA,PTATR001
          GOTO      SPTATR99
.
SPTATR02  MOVE      QRYDATA,PTATR002
          GOTO      SPTATR99
.
SPTATR03  MATCH     SP70,QRYDATA
          GOTO      SPTATR99 IF EQUAL
          GOTO      SPTATR99 IF EOS
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      PTATR003,CCENT,CYEAR,CMON,CDAY
          GOTO      SPTATR99
.
SPTATR04  MOVE      QRYDATA,PTATR004
          GOTO      SPTATR99
.
SPTATR05  MOVE      QRYDATA,PTATR005
          GOTO      SPTATR99
.
SPTATR06  MOVE      QRYDATA,PTATR006
          GOTO      SPTATR99
.
SPTATR07  MOVE      QRYDATA,PTATR007
          GOTO      SPTATR99
.
SPTATR08  MOVE      QRYDATA,PTATR008
          GOTO      SPTATR99
.
SPTATR09  MOVE      QRYDATA,PTATR009
          GOTO      SPTATR99
.
SPTATR10  MOVE      QRYDATA,PTATR010
          GOTO      SPTATR99
.
SPTATR11  MOVE      QRYDATA,PTATR011
          GOTO      SPTATR99
.
SPTATR12  MOVE      QRYDATA,PTATR012
          GOTO      SPTATR99
.
SPTATR13  MOVE      QRYDATA,PTATR013
          GOTO      SPTATR99
.
SPTATR14  MOVE      QRYDATA,PTATR014
          GOTO      SPTATR99
.
SPTATR15  MOVE      QRYDATA,PTATR015
          GOTO      SPTATR99
.
SPTATR16  MOVE      QRYDATA,PTATR016
          GOTO      SPTATR99
.
SPTATR17  MOVE      QRYDATA,PTATR017
          GOTO      SPTATR99
.
SPTATR18  MOVE      QRYDATA,PTATR018
          GOTO      SPTATR99
.
SPTATR19  MOVE      QRYDATA,PTATR019
          GOTO      SPTATR99
.
SPTATR20  MATCH     SP70,QRYDATA
          GOTO      SPTATR99 IF EQUAL
          GOTO      SPTATR99 IF EOS
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      PTATR020,CCENT,CYEAR,CMON,CDAY
          GOTO      SPTATR99
.
SPTATR21  MOVE      QRYDATA,PTATR021
          GOTO      SPTATR99
.
SPTATR22  MOVE      QRYDATA,PTATR022
          GOTO      SPTATR99
.
SPTATR23  MATCH     SP70,QRYDATA
          GOTO      SPTATR99 IF EQUAL
          GOTO      SPTATR99 IF EOS
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      PTATR023,CCENT,CYEAR,CMON,CDAY
          GOTO      SPTATR99
.
SPTATR24  MOVE      QRYDATA,PTATR024
          GOTO      SPTATR99
.
SPTATR25  MATCH     SP70,QRYDATA
          GOTO      SPTATR99 IF EQUAL
          GOTO      SPTATR99 IF EOS
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      PTATR025,CCENT,CYEAR,CMON,CDAY
          GOTO      SPTATR99
.
SPTATR26  MOVE      QRYDATA,PTATR026
          GOTO      SPTATR99
.
SPTATR27  MOVE      QRYDATA,PTATR027
          GOTO      SPTATR99
.
SPTATR99  RETURN
+
.------------------------------------------------------------------------
.         Move work variables to patient attributes file variables
.------------------------------------------------------------------------
MVPATATR  PACK      PTARURNO,PTATR001,SP70
          PACK      PTARVISN,PTATR002,SP70
          PACK      PTARDATE,PTATR003,SP70
          PACK      PTARTIME,PTATR004,SP70
          PACK      PTARTYPE,PTATR005,SP70
          PACK      PTARVAL1,PTATR006,SP70
          PACK      PTARVAL2,PTATR007,SP70
          PACK      PTARVAL3,PTATR008,SP70
          PACK      PTARVAL4,PTATR010,SP70
          PACK      PTARCOD1,PTATR009,SP70
          PACK      PTARCOD2,PTATR011,SP70
          PACK      PTARCOD3,PTATR012,SP70
          PACK      PTARCOD4,PTATR013,SP70
          PACK      PTARTXT1,PTATR014,SP70,SP70
          PACK      PTARTXT2,PTATR015,SP70,SP70
          PACK      PTARTXT3,PTATR016,SP70,SP70
          PACK      PTARTXT4,PTATR017,SP70,SP70
          PACK      PTARDELR,PTATR018,SP70
          PACK      PTARCUSR,PTATR019,SP70
          PACK      PTARCDTE,PTATR020,SP70
          PACK      PTARCTME,PTATR021,SP70
          PACK      PTARUUSR,PTATR022,SP70
          PACK      PTARUDTE,PTATR023,SP70
          PACK      PTARUTME,PTATR024,SP70
          PACK      PTARFDTE,PTATR025,SP70
          PACK      PTARFTME,PTATR026,SP70
          PACK      PTARFUSR,PTATR027,SP70
.
          RETURN
+
.------------------------------------------------------------
.         Get the Coded Field (1-4) value for the specified 
.         Attribute Type (1-18).
.
.         Parameters:
.           PTATR001  U/R Number
.           PTATR002  Visit Number
.           ATTYPIND  Attribute Type index (1-18)
.           ATCODIND  Coded Field index (1-4)
.
.         Return:
.           ATCODVAL  Coded Field Value (DIM3)
.------------------------------------------------------------
GETACV00  MOVE      SP10,ATCODVAL
          MOVE      SP3,PTATR005
.
          LOAD      PTATR005,ATTYPIND,ATYPE001,ATYPE002,ATYPE003,ATYPE004:
                                      ATYPE005,ATYPE006,ATYPE007,ATYPE008:
                                      ATYPE009,ATYPE010,ATYPE011,ATYPE012:
                                      ATYPE013,ATYPE014,ATYPE015,ATYPE016:
                                      ATYPE017,ATYPE018

.
          PACK      KEY35,PTATR001,PTATR002,SP70
          CALL      RSPTATR3
GETACV10  CALL      RKPTATR3
          BRANCH    OVRCD,GETACV91
.
          MATCH     PTATR001,PTARURNO
          GOTO      GETACV91 IF NOT EQUAL
.
          MATCH     PTATR002,PTARVISN
          GOTO      GETACV91 IF NOT EQUAL
.
          MATCH     PTATR005,PTARTYPE
          GOTO      GETACV10 IF NOT EQUAL
.
          BRANCH    ATCODIND,GETACV21,GETACV22,GETACV23,GETACV24
          GOTO      GETACV91
.
GETACV21  MOVE      PTARCOD1,ATCODVAL
          GOTO      GETACV90
.
GETACV22  MOVE      PTARCOD2,ATCODVAL
          GOTO      GETACV90
.
GETACV23  MOVE      PTARCOD3,ATCODVAL
          GOTO      GETACV90
.
GETACV24  MOVE      PTARCOD4,ATCODVAL
          GOTO      GETACV90
.
GETACV90  MOVE      ZERO,EXIT
          GOTO      GETACV99
.
GETACV91  MOVE      ONE,EXIT
GETACV99  RETURN
+
.------------------------------------------------------------------------------
.         Updates the Coded Field (1-4) value for the specified 
.         Attribute Type (1-18); first matching Patient Attribute record.
.         If a record does not exist for the specified attribute type, a new 
.         record will be written to file.
.
.         Parameters:
.           PTATR001  U/R Number
.           PTATR002  Visit Number
.           ATTYPIND  Attribute Type index (1-18)
.           ATCODIND  Coded Field index (1-4)
.           ATCODVAL  Coded Field value (DIM3)
.
.         Return:
.           EXIT      0-Updated, 1-Added
.------------------------------------------------------------------------------
UPDACV00  MOVE      SP3,PTATR005
          CALL      IBACLOCK
.
          LOAD      PTATR005,ATTYPIND,ATYPE001,ATYPE002,ATYPE003,ATYPE004:
                                      ATYPE005,ATYPE006,ATYPE007,ATYPE008:
                                      ATYPE009,ATYPE010,ATYPE011,ATYPE012:
                                      ATYPE013,ATYPE014,ATYPE015,ATYPE016:
                                      ATYPE017,ATYPE018
.
          RJUSTIFY  ATCODVAL
.
          PACK      KEY35,PTATR001,PTATR002,SP70
          CALL      RSPTATR3
UPDACV10  CALL      RKPTATR3
          BRANCH    OVRCD,UPDACV91
.
          MATCH     PTATR001,PTARURNO
          GOTO      UPDACV91 IF NOT EQUAL
.
          MATCH     PTATR002,PTARVISN
          GOTO      UPDACV91 IF NOT EQUAL
.
          MATCH     PTATR005,PTARTYPE
          GOTO      UPDACV10 IF NOT EQUAL
.
          BRANCH    ATCODIND,UPDACV21,UPDACV22,UPDACV23,UPDACV24
          GOTO      UPDACV90
.
UPDACV21  MOVE      ATCODVAL,PTARCOD1
          GOTO      UPDACV90
.
UPDACV22  MOVE      ATCODVAL,PTARCOD2
          GOTO      UPDACV90
.
UPDACV23  MOVE      ATCODVAL,PTARCOD3
          GOTO      UPDACV90
.
UPDACV24  MOVE      ATCODVAL,PTARCOD4
          GOTO      UPDACV90
.
UPDACV90  MOVE      USERID,PTARUUSR
          PACK      PTARUDTE,CCC,CYY,CMM,CDD
          REP       " 0",PTARUDTE
          MOVE      CTIMEIS,PTARUTME
          CALL      UPPTATR3
          MOVE      ZERO,EXIT
          GOTO      UPDACV99
.
UPDACV91  CALL      CLPATATR
          MOVE      PTATR001,PTARURNO
          MOVE      PTATR002,PTARVISN
          MOVE      PTATR005,PTARTYPE
          STORE     ATCODVAL,ATCODIND,PTARCOD1,PTARCOD2,PTARCOD3,PTARCOD4
          PACK      PTARDATE,CCC,CYY,CMM,CDD
          REP       " 0",PTARDATE
          MOVE      CTIMEIS,PTARTIME
          MOVE      USERID,PTARCUSR
          MOVE      PTARDATE,PTARCDTE
          MOVE      CTIMEIS,PTARCTME
.
          PACK      KEY35,PTARURNO,PTARVISN,PTARDATE,PTARTIME,PTARTYPE,SP70
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRPTATR2
          TRAPCLR   IO
          MOVE      ONE,EXIT
          GOTO      UPDACV99
.
UPDACV99  RETURN
+
.------------------------------------------------------------------------------
.         Get the Text Field (1-4) value for the specified Attribute Type (1-18)
.
.         Parameters:
.           PTATR001  U/R Number
.           PTATR002  Visit Number
.           ATTYPIND  Attribute Type index (1-18)
.           ATTXTIND  Text Field index (1-4)
.
.         Return:
.           ATTXTVAL  Text Field Value (DIM80)
.------------------------------------------------------------------------------
GETTFV00  PACK      ATTXTVAL,SP70,SP70
          MOVE      SP3,PTATR005
.
          LOAD      PTATR005,ATTYPIND,ATYPE001,ATYPE002,ATYPE003,ATYPE004:
                                      ATYPE005,ATYPE006,ATYPE007,ATYPE008:
                                      ATYPE009,ATYPE010,ATYPE011,ATYPE012:
                                      ATYPE013,ATYPE014,ATYPE015,ATYPE016:
                                      ATYPE017,ATYPE018
.
          PACK      KEY35,PTATR001,PTATR002,SP70
          CALL      RSPTATR3
GETTFV10  CALL      RKPTATR3
          BRANCH    OVRCD,GETTFV91
.
          MATCH     PTATR001,PTARURNO
          GOTO      GETTFV91 IF NOT EQUAL
.
          MATCH     PTATR002,PTARVISN
          GOTO      GETTFV91 IF NOT EQUAL
.
          MATCH     PTATR005,PTARTYPE
          GOTO      GETTFV10 IF NOT EQUAL
.
          BRANCH    ATCODIND,GETTFV21,GETTFV22,GETTFV23,GETTFV24
          GOTO      GETTFV91
.
GETTFV21  MOVE      PTARTXT1,ATTXTVAL
          GOTO      GETTFV90
.
GETTFV22  MOVE      PTARTXT2,ATTXTVAL
          GOTO      GETTFV90
.
GETTFV23  MOVE      PTARTXT3,ATTXTVAL
          GOTO      GETTFV90
.
GETTFV24  MOVE      PTARTXT4,ATTXTVAL
          GOTO      GETTFV90
.
GETTFV90  MOVE      ZERO,EXIT
          GOTO      GETTFV99
.
GETTFV91  MOVE      ONE,EXIT
GETTFV99  RETURN
+
.------------------------------------------------------------------------------
.         Updates the Text Field (1-4) value for the specified 
.         Attribute Type (1-18); first matching Patient Attribute record.
.         If a record does not exist for the specified attribute type, a new 
.         record will be written to file.
.
.         Parameters:
.           PTATR001  U/R Number
.           PTATR002  Visit Number
.           ATTYPIND  Attribute Type index (1-18)
.           ATTXTIND  Text Field index (1-4)
.           ATTXTVAL  Text Field value (DIM80)
.
.         Return:
.           EXIT      0-Updated, 1-Added
.------------------------------------------------------------------------------
UPDATV00  MOVE      SP3,PTATR005
          CALL      IBACLOCK
.
          LOAD      PTATR005,ATTYPIND,ATYPE001,ATYPE002,ATYPE003,ATYPE004:
                                      ATYPE005,ATYPE006,ATYPE007,ATYPE008:
                                      ATYPE009,ATYPE010,ATYPE011,ATYPE012:
                                      ATYPE013,ATYPE014,ATYPE015,ATYPE016:
                                      ATYPE017,ATYPE018
.
          PACK      KEY35,PTATR001,PTATR002,SP70
          CALL      RSPTATR3
UPDATV10  CALL      RKPTATR3
          BRANCH    OVRCD,UPDATV91
.
          MATCH     PTATR001,PTARURNO
          GOTO      UPDATV91 IF NOT EQUAL
.
          MATCH     PTATR002,PTARVISN
          GOTO      UPDATV91 IF NOT EQUAL
.
          MATCH     PTATR005,PTARTYPE
          GOTO      UPDATV10 IF NOT EQUAL
.
          BRANCH    ATTXTIND,UPDATV21,UPDATV22,UPDATV23,UPDATV24
          GOTO      UPDATV90
.
UPDATV21  MOVE      ATTXTVAL,PTARTXT1
          GOTO      UPDATV90
.
UPDATV22  MOVE      ATTXTVAL,PTARTXT2
          GOTO      UPDATV90
.
UPDATV23  MOVE      ATTXTVAL,PTARTXT3
          GOTO      UPDATV90
.
UPDATV24  MOVE      ATTXTVAL,PTARTXT4
          GOTO      UPDATV90
.
UPDATV90  MOVE      USERID,PTARUUSR
          PACK      PTARUDTE,CCC,CYY,CMM,CDD
          REP       " 0",PTARUDTE
          MOVE      CTIMEIS,PTARUTME
          CALL      UPPTATR3
          MOVE      ZERO,EXIT
          GOTO      UPDATV99
.
UPDATV91  CALL      CLPATATR
          MOVE      PTATR001,PTARURNO
          MOVE      PTATR002,PTARVISN
          MOVE      PTATR005,PTARTYPE
          STORE     ATTXTVAL,ATTXTIND,PTARTXT1,PTARTXT2,PTARTXT3,PTARTXT4
          PACK      PTARDATE,CCC,CYY,CMM,CDD
          REP       " 0",PTARDATE
          MOVE      CTIMEIS,PTARTIME
          MOVE      USERID,PTARCUSR
          MOVE      PTARDATE,PTARCDTE
          MOVE      CTIMEIS,PTARCTME
.
          PACK      KEY35,PTARURNO,PTARVISN,PTARDATE,PTARTIME,PTARTYPE,SP70
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRPTATR2
          TRAPCLR   IO
          MOVE      ONE,EXIT
          GOTO      UPDATV99
.
UPDATV99  RETURN
+
.------------------------------------------------------------------------------
.         Updates the notes (Text Fields 1-3) for the specified Attribute type.
.         If an Attribute record does not exist for the specified type, a new 
.         record will be written to file.
.
.         Parameters:
.           PTATR001  U/R Number
.           PTATR002  Visit Number
.           PTATR005  Attribute Type
.           PTATR014  Text Field 1 value
.           PTATR015  Text Field 2 value
.           PTATR016  Text Field 3 value
.
.         Return:
.           EXIT      0 - Updated
.                     1 - Added
.------------------------------------------------------------------------------
UPDNOT00  CALL      IBACLOCK
.
          PACK      KEY35,PTATR001,PTATR002,SP70
          CALL      RSPTATR3
UPDNOT10  CALL      RKPTATR3
          BRANCH    OVRCD,UPDNOT91
.
          MATCH     PTATR001,PTARURNO
          GOTO      UPDNOT91 IF NOT EQUAL
.
          MATCH     PTATR002,PTARVISN
          GOTO      UPDNOT91 IF NOT EQUAL
.
          MATCH     PTATR005,PTARTYPE
          GOTO      UPDNOT10 IF NOT EQUAL
.
          MOVE      USERID,PTARUUSR
          PACK      PTARUDTE,CCC,CYY,CMM,CDD
          REP       " 0",PTARUDTE
          MOVE      CTIMEIS,PTARUTME
          MOVE      PTATR014,PTARTXT1
          MOVE      PTATR015,PTARTXT2
          MOVE      PTATR016,PTARTXT3
          CALL      UPPTATR3
.
UPDNOT90  MOVE      ZERO,EXIT
          GOTO      UPDNOT99
.
UPDNOT91  CALL      CLPATATR
          MOVE      PTATR001,PTARURNO
          MOVE      PTATR002,PTARVISN
          MOVE      PTATR005,PTARTYPE
          MOVE      PTATR014,PTARTXT1
          MOVE      PTATR015,PTARTXT2
          MOVE      PTATR016,PTARTXT3
.
          PACK      PTARDATE,CCC,CYY,CMM,CDD
          REP       " 0",PTARDATE
          MOVE      CTIMEIS,PTARTIME
          MOVE      USERID,PTARCUSR
          MOVE      PTARDATE,PTARCDTE
          MOVE      CTIMEIS,PTARCTME
.
          PACK      KEY35,PTARURNO,PTARVISN,PTARDATE,PTARTIME,PTARTYPE,SP70
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRPTATR2
          TRAPCLR   IO
          MOVE      ONE,EXIT
          GOTO      UPDNOT99
.
UPDNOT99  RETURN
+
.------------------------------------------------------------
.         Output Attribute Notes (Text Fields 1-3)
.
.         Parameters:
.           PTATR001  U/R Number
.           PTATR002  Visit Number
.           PTATR005  Attribute Type
.
.         Return:
.           EXIT      0 - output written to HTMLFILE (DIM80)
.                     1 - No output
.------------------------------------------------------------
GETNTE00  PACK      KEY35,PTATR001,PTATR002,SP70
          CALL      RSPTATR3
GETNTE10  CALL      RKPTATR3
          BRANCH    OVRCD,GETNTE91
.
          MATCH     PTATR001,PTARURNO
          GOTO      GETNTE91 IF NOT EQUAL
.
          MATCH     PTATR002,PTARVISN
          GOTO      GETNTE91 IF NOT EQUAL
.
          MATCH     PTATR005,PTARTYPE
          GOTO      GETNTE10 IF NOT EQUAL
.
          STRIP     PTARTXT1
          MOVELPTR  PTARTXT1,LENGTH
          IF        LENGTH = 0
            MOVE      "80",LENGTH
          ENDIF
          SFORMAT   VAR,LENGTH
          MOVE      PTARTXT1,VAR
          WRITE     HTMLFILE;VAR
          SFORMAT   VAR,127
.
          STRIP     PTARTXT2
          MOVELPTR  PTARTXT2,LENGTH
          IF        LENGTH = 0
            MOVE      "80",LENGTH
          ENDIF
          SFORMAT   VAR,LENGTH
          MOVE      PTARTXT2,VAR
          WRITE     HTMLFILE;VAR
          SFORMAT   VAR,127
.
          STRIP     PTARTXT3
          MOVELPTR  PTARTXT3,LENGTH
          IF        LENGTH = 0
            MOVE      "80",LENGTH
          ENDIF
          SFORMAT   VAR,LENGTH
          MOVE      PTARTXT3,VAR
          WRITE     HTMLFILE;VAR
          SFORMAT   VAR,127
.
GETNTE90  MOVE      ZERO,EXIT
          GOTO      GETNTE99
.
GETNTE91  MOVE      ONE,EXIT
          GOTO      GETNTE99
.
GETNTE99  RETURN
+
.------------------------------------------------------------
.         Get the Value Field (1-4) value for the specified
.         Attribute Type (1-18).
.
.         Parameters:
.           PTATR001  U/R Number
.           PTATR002  Visit Number
.           ATTYPIND  Attribute Type index (1-18)
.           ATVALIND  Value Field index (1-4)
.
.         Return:
.           ATVALVAL  Value Field Value (DIM6)
.------------------------------------------------------------
GETAVV00  MOVE      SP10,ATVALVAL
          MOVE      SP3,PTATR005
.
          LOAD      PTATR005,ATTYPIND,ATYPE001,ATYPE002,ATYPE003,ATYPE004:
                                      ATYPE005,ATYPE006,ATYPE007,ATYPE008:
                                      ATYPE009,ATYPE010,ATYPE011,ATYPE012:
                                      ATYPE013,ATYPE014,ATYPE015,ATYPE016:
                                      ATYPE017,ATYPE018
.
          PACK      KEY35,PTATR001,PTATR002,SP70
          CALL      RSPTATR3
GETAVV10  CALL      RKPTATR3
          BRANCH    OVRCD,GETAVV91
.
          MATCH     PTATR001,PTARURNO
          GOTO      GETAVV91 IF NOT EQUAL
.
          MATCH     PTATR002,PTARVISN
          GOTO      GETAVV91 IF NOT EQUAL
.
          MATCH     PTATR005,PTARTYPE
          GOTO      GETAVV10 IF NOT EQUAL
.
          BRANCH    ATVALIND,GETAVV21,GETAVV22,GETAVV23,GETAVV24
          GOTO      GETAVV91
.
GETAVV21  MOVE      PTARVAL1,ATVALVAL
          GOTO      GETAVV90
.
GETAVV22  MOVE      PTARVAL2,ATVALVAL
          GOTO      GETAVV90
.
GETAVV23  MOVE      PTARVAL3,ATVALVAL
          GOTO      GETAVV90
.
GETAVV24  MOVE      PTARVAL4,ATVALVAL
          GOTO      GETAVV90
.
GETAVV90  MOVE      ZERO,EXIT
          GOTO      GETAVV99
.
GETAVV91  MOVE      ONE,EXIT
GETAVV99  RETURN
+
.------------------------------------------------------------------------------
.         Updates the Value Field (1-4) value for the specified
.         Attribute Type (1-18); first matching Patient Attribute record.
.         If a record does not exist for the specified attribute type, a new
.         record will be written to file.
.
.         Parameters:
.           PTATR001  U/R Number
.           PTATR002  Visit Number
.           ATTYPIND  Attribute Type index (1-13)
.           ATVALIND  Coded Field index (1-4)
.           ATVALVAL  Coded Field value (DIM6)
.
.         Return:
.           EXIT      0-Updated, 1-Added
.------------------------------------------------------------------------------
UPDAVV00  MOVE      SP3,PTATR005
          CALL      IBACLOCK
.
          LOAD      PTATR005,ATTYPIND,ATYPE001,ATYPE002,ATYPE003,ATYPE004:
                                      ATYPE005,ATYPE006,ATYPE007,ATYPE008:
                                      ATYPE009,ATYPE010,ATYPE011,ATYPE012:
                                      ATYPE013,ATYPE014,ATYPE015,ATYPE016:
                                      ATYPE017,ATYPE018
.
.         RJUSTIFY  ATVALVAL
.
          PACK      KEY35,PTATR001,PTATR002,SP70
          CALL      RSPTATR3
UPDAVV10  CALL      RKPTATR3
          BRANCH    OVRCD,UPDAVV91
.
          MATCH     PTATR001,PTARURNO
          GOTO      UPDAVV91 IF NOT EQUAL
.
          MATCH     PTATR002,PTARVISN
          GOTO      UPDAVV91 IF NOT EQUAL
.
          MATCH     PTATR005,PTARTYPE
          GOTO      UPDAVV10 IF NOT EQUAL
.
          BRANCH    ATVALIND,UPDAVV21,UPDAVV22,UPDAVV23,UPDAVV24
          GOTO      UPDAVV90
.
UPDAVV21  MOVE      ATVALVAL,PTARVAL1
          GOTO      UPDAVV90
.
UPDAVV22  MOVE      ATVALVAL,PTARVAL2
          GOTO      UPDAVV90
.
UPDAVV23  MOVE      ATVALVAL,PTARVAL3
          GOTO      UPDAVV90
.
UPDAVV24  MOVE      ATVALVAL,PTARVAL4
          GOTO      UPDAVV90
.
UPDAVV90  MOVE      USERID,PTARUUSR
          PACK      PTARUDTE,CCC,CYY,CMM,CDD
          REP       " 0",PTARUDTE
          MOVE      CTIMEIS,PTARUTME
          CALL      UPPTATR3
          MOVE      ZERO,EXIT
          GOTO      UPDAVV99
.
UPDAVV91  CALL      CLPATATR
          MOVE      PTATR001,PTARURNO
          MOVE      PTATR002,PTARVISN
          MOVE      PTATR005,PTARTYPE
          STORE     ATVALVAL,ATVALIND,PTARVAL1,PTARVAL2,PTARVAL3,PTARVAL4
          PACK      PTARDATE,CCC,CYY,CMM,CDD
          REP       " 0",PTARDATE
          MOVE      CTIMEIS,PTARTIME
          MOVE      USERID,PTARCUSR
          MOVE      PTARDATE,PTARCDTE
          MOVE      CTIMEIS,PTARCTME
.
          PACK      KEY35,PTARURNO,PTARVISN,PTARDATE,PTARTIME,PTARTYPE,SP70
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRPTATR2
          TRAPCLR   IO
          MOVE      ONE,EXIT
          GOTO      UPDAVV99
.
UPDAVV99  RETURN
+
.-------------------------------------------------------------------------------
.         Get the Coded Field (1-4) value for the specified
.         Attribute Type (1-18) for a given attribute date/time
.
.         Parameters:
.           PTATR001  U/R Number
.           PTATR002  Visit Number
.           PTATR003  Date of Attribute
.           PTATR003  Time of Attribute
.           ATTYPIND  Attribute Type index (1-18)
.           ATCODIND  Coded Field index (1-4)
.
.         Return:
.           ATCODVAL  Coded Field Value (DIM3)
.------------------------------------------------------------------------------
GETCDV00  MOVE      SP10,ATCODVAL
          MOVE      SP3,PTATR005
.
          LOAD      PTATR005,ATTYPIND,ATYPE001,ATYPE002,ATYPE003,ATYPE004:
                                      ATYPE005,ATYPE006,ATYPE007,ATYPE008:
                                      ATYPE009,ATYPE010,ATYPE011,ATYPE012:
                                      ATYPE013,ATYPE014,ATYPE015,ATYPE016:
                                      ATYPE017,ATYPE018
.
          PACK      KEY35,PTATR001,PTATR003,PTATR004,PTATR005,PTATR002,SP70
          CALL      RDPTATR1
          BRANCH    OVRCD,GETCDV91
.
          BRANCH    ATCODIND,GETCDV21,GETCDV22,GETCDV23,GETCDV24
          GOTO      GETCDV91
.
GETCDV21  MOVE      PTARCOD1,ATCODVAL
          GOTO      GETCDV90
.
GETCDV22  MOVE      PTARCOD2,ATCODVAL
          GOTO      GETCDV90
.
GETCDV23  MOVE      PTARCOD3,ATCODVAL
          GOTO      GETCDV90
.
GETCDV24  MOVE      PTARCOD4,ATCODVAL
          GOTO      GETCDV90
.
GETCDV90  MOVE      ZERO,EXIT
          GOTO      GETCDV99
.
GETCDV91  MOVE      ONE,EXIT
GETCDV99  RETURN
+
.------------------------------------------------------------------------------
.         Get the Value Field (1-4) value for the specified
.         Attribute Type (1-18) for a given attribute date/time
.
.         Parameters:
.           PTATR001  U/R Number
.           PTATR002  Visit Number
.           PTATR003  Date of Atribute
.           PTATR004  Time of Attribute
.           ATTYPIND  Attribute Type index (1-18)
.           ATVALIND  Value Field index (1-4)
.
.         Return:
.           ATVALVAL  Value Field Value (DIM6)
.------------------------------------------------------------------------------
GETVAV00  MOVE      SP10,ATVALVAL
          MOVE      SP3,PTATR005
.
          LOAD      PTATR005,ATTYPIND,ATYPE001,ATYPE002,ATYPE003,ATYPE004:
                                      ATYPE005,ATYPE006,ATYPE007,ATYPE008:
                                      ATYPE009,ATYPE010,ATYPE011,ATYPE012:
                                      ATYPE013,ATYPE014,ATYPE015,ATYPE016:
                                      ATYPE017,ATYPE018
.
          PACK      KEY35,PTATR001,PTATR003,PTATR004,PTATR005,PTATR002,SP70
          CALL      RDPTATR1
          BRANCH    OVRCD,GETVAV91
.
          BRANCH    ATVALIND,GETVAV21,GETVAV22,GETVAV23,GETVAV24
          GOTO      GETAVV91
.
GETVAV21  MOVE      PTARVAL1,ATVALVAL
          GOTO      GETVAV90
.
GETVAV22  MOVE      PTARVAL2,ATVALVAL
          GOTO      GETVAV90
.
GETVAV23  MOVE      PTARVAL3,ATVALVAL
          GOTO      GETVAV90
.
GETVAV24  MOVE      PTARVAL4,ATVALVAL
          GOTO      GETVAV90
.
GETVAV90  MOVE      ZERO,EXIT
          GOTO      GETVAV99
.
GETVAV91  MOVE      ONE,EXIT
GETVAV99  RETURN
+

.------------------------------------------------------------------------------
.         Get the Text Field (1-4) value for the specified
.         Attribute Type (1-18) for a given attribute date/time
.         Parameters:
.           PTATR001  U/R Number
.           PTATR002  Visit Number
.           PTATR003  Date of Atribute
.           PTATR004  Time of Attribute
.           ATTYPIND  Attribute Type index (1-18)
.           ATTXTIND  Text Field index (1-4)
.
.         Return:
.           ATTXTVAL  Text Field Value (DIM80)
.------------------------------------------------------------------------------
GETTXV00  PACK      ATTXTVAL,SP70,SP70
          MOVE      SP3,PTATR005
.
          LOAD      PTATR005,ATTYPIND,ATYPE001,ATYPE002,ATYPE003,ATYPE004:
                                      ATYPE005,ATYPE006,ATYPE007,ATYPE008:
                                      ATYPE009,ATYPE010,ATYPE011,ATYPE012:
                                      ATYPE013,ATYPE014,ATYPE015,ATYPE016:
                                      ATYPE017,ATYPE018
.
          PACK      KEY35,PTATR001,PTATR003,PTATR004,PTATR005,PTATR002,SP70
          CALL      RDPTATR1
          BRANCH    OVRCD,GETTXV91
.
          BRANCH    ATTXTIND,GETTXV21,GETTXV22,GETTXV23,GETTXV24
          GOTO      GETTFV91
.
GETTXV21  MOVE      PTARTXT1,ATTXTVAL
          GOTO      GETTXV90
.
GETTXV22  MOVE      PTARTXT2,ATTXTVAL
          GOTO      GETTXV90
.
GETTXV23  MOVE      PTARTXT3,ATTXTVAL
          GOTO      GETTXV90
.
GETTXV24  MOVE      PTARTXT4,ATTXTVAL
          GOTO      GETTXV90
.
GETTXV90  MOVE      ZERO,EXIT
          GOTO      GETTXV99
.
GETTXV91  MOVE      ONE,EXIT
GETTXV99  RETURN
+

.
