.******************************************************************************
.*  System        :  Operating Rooms                                          *
.*                                                                            *
.*  Include Name  :  OPRITEDS.PBL                                             *
.*                                                                            *
.*  Function      :  Display the codes from the Item file OPRITEFD.INC        *
.*                                                                            *
.*  Index used    :  OPRITEA2                                                 *
.*                                                                            *
.*  File          :  opriteaf                                                 *
.*                                                                            *
.*  Author        :  Justin Coates 10/10/1990                                 *
.*                                                                            *
.*  Modifications :                                                           *
.*  V9.04.01  17/02/2005  Lina Vo     CAR 56404                               *
.*            Changed program to read Operating Room item file with new key   *
.*            size.                                                           *
.*                                                                            *
.******************************************************************************
.
OPRITEDS  MOVE      FIVE,CVERT              * line counter
          PACK      KEY65,SP70              * key of blanks
          CALL      RSOPITE2                * positional read
.
          DISPLAY   *P1:3,*EF,*V2LON,*P30:4,"ITEM CODES":
                    *P1:5,*ULON," Code ":
                    *P8:5,"                    Description                   "
.
. -------------------------------------
. ------- next read on item file ------
. -------------------------------------
.
ITEDS100  CALL      RKOPITE2  
          BRANCH    OVRCD,ITEDS999          * end of file
.
. ----------------------------------
. ------- increment counters -------
. ----------------------------------
.
ITEDS200  ADD       ONE,CVERT               * inc line counter
.
          COMPARE   "23",CVERT
          GOTO      ITEDS400 IF NOT LESS    * need a new page
.
. -------------------------------------------
. ------- display all the information -------
. -------------------------------------------
.
ITEDS300  DISPLAY   *P1:CVERT,*V2LON,OPITCODE,*HOFF,*P8:CVERT,OPITDESC
          GOTO      ITEDS100
.
. -------------------------------
. ------- new page needed -------
. -------------------------------
.
ITEDS400  DISPLAY   *P1:24,"( )ontinue, ( )irst, ( )ext ? ",*EL:
                    *V2LON,*P2:24,ANSC,*P14:24,ANSF,*P23:24,ANSN
.
          KEYIN     *P31:24,*DV,UNDLN1:
                    *P31:24,*V2LON,ANS
.
          REP       UPPLOW,ANS
.
          MATCH     ANSC,ANS
          GOTO      ITEDS999 IF EQUAL       * Continue selected
.
          MATCH     ANSF,ANS
          GOTO      OPRITEDS IF EQUAL       * First selected
.
. --------------------------------------
. ------- (N)ext screen selected -------
. --------------------------------------
.
ITEDS700  DISPLAY   *P1:6,*EF;
          MOVE      SIX,CVERT               * first display row 
          GOTO      ITEDS300                * display the data
.
ITEDS999  RETURN
+
