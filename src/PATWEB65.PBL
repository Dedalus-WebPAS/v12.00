.----------------------------------------------------------------------
. Program       : PATWEB65
.
. Function      : SMS Polling Server - Read in SMS Sent details
.
. Modifications :
.
.     V11.04.02   14/10/2024  J.Tan           TSK 0951331
.                 Mod to update Quote Accepted/Patient Contacted
.     V11.04.01   21/12/2023  Don Nguyen      TSK 0893290
.                 Fixed the processing of messages marked as "Waiting to be
.                 sent" so that the status is updated accordinlgy as per the
.                 sent details file.
.     V10.08.01   14/09/2016  Ebon Clements   TSK 0294154
.                 Set contact type - PMSMCTYP - Patient or Contact
.     V10.08.00   10/02/2016  Ebon Clements   TSK 0294154
.                 Created program
.----------------------------------------------------------------------
.
          INC       IBAWEBDF
          INC       WEBDATDF
.
          INC       PATCONFD/INC
          INC       PATILTFD/INC
          INC       PATELGFD/INC
          INC       PMSSRRFD/INC
          INC       PMSSMHFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
.
SMSSENT1  FILE      HL7, FIXED=4000         * smssent1.txt
.
.         Fields must be pipe delimited and in the following sequence.
.         Field lengths are irrelevant other than the fact that where
.         a field is longer than allowed, the extra data will be ignored.
.
SENTMNUM  DIM       20            * Phone Number
.PIPE     DIM       1             * Pipe Delimiter
SENTUMID  DIM       16            * Message Id
.PIPE     DIM       1             * Pipe Delimiter
SENTDATE  DIM       8             * Sent Date
.PIPE     DIM       1             * Pipe Delimiter
SENTTIME  DIM       8             * Sent Time
.PIPE     DIM       1             * Pipe Delimiter
SENTUSER  DIM       10            * Sent by User ID
.PIPE     DIM       1             * Pipe Delimiter
SENTURNO  DIM       8             * U/R
.PIPE     DIM       1             * Pipe Delimiter
SENTSCOD  DIM       6             * SMS Message Code
.PIPE     DIM       1             * Pipe Delimiter
SENTRKEY  DIM       30            * Record Key
.PIPE     DIM       1             * Pipe Delimiter
SENTMESA  DIM       160           * Message details A
.PIPE     DIM       1             * Pipe Delimiter
SENTMESB  DIM       160           * Message details B
.PIPE     DIM       1             * Pipe Delimiter
SENTMESC  DIM       160           * Message details C
.PIPE     DIM       1             * Pipe Delimiter
SENTMESD  DIM       160           * Message details D
.PIPE     DIM       1             * Pipe Delimiter
SENTMESE  DIM       160           * Message details E
.PIPE     DIM       1             * Pipe Delimiter
SENTMESF  DIM       160           * Message details F
.PIPE     DIM       1             * Pipe Delimiter
SENTSTAT  DIM       2             * Message status 
.PIPE     DIM       1             * Pipe Delimiter
SENTCONT  DIM       1             * Contact Type    
.PIPE     DIM       1             * Pipe Delimiter
SENTCTYP  DIM       3             * Contact Type    
.
. End of record                  
.
SENTNAME  FILE      ASCII, FIXED=127
.
. ----- Program Variables -----
.
CFILEPRE  DIM       3   * Current Open File Prefix for Outpatient Files
CMDLINE   DIM       200
CURRDATE  DIM       8                  * Current Date
FORM16    FORM      16
VISITKEY  DIM       30
.
.----------------------------------------------------------------------
PRGID     INIT      "PATWEB65"
VERSION   INIT      "V12.00.00"
PRGNAM    INIT      "SMS Polling Server"
.----------------------------------------------------------------------
.
          CALL      WEBINT00       * Initialise Fifo Etc.
.
          CALL      INIT0000       * Open Files
.
MAIN0000  COMMIT                   * Match BEGIN in WEBINT00 for Transactions
.
          BEGIN
.
          DISPLAY   *W10
.
          CALL      SENT0000       * load sent SMS detail
.
          GOTO      MAIN0000
.------------------------------------------------------------
. Open Files and Initialize Variables
.------------------------------------------------------------
INIT0000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND20;*243,PTCNSMSN
          READ      CONTROLF,HUND20;*160,PTCNIFCL
          OPEN      PMSSMHA1,"pmssmhaf"
          OPEN      PATLETR1,"patletrf"
          OPEN      PATELGA1,"patelgaf"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
.
INIT9999  RETURN
.
.------------------------------------------------------------
. Check for any sms sent details
.------------------------------------------------------------
SENT0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      SENTNAME,"sentsms1"     * See if old sent text
          TRAPCLR   IO                      * file exists
          IF        OVRCD=0
            CLOSE     SENTNAME,DELETE       * Delete old sent name text file
          ENDIF
.
          CLEAR     CMDLINE
          APPEND    "patweb65.us1",CMDLINE  * get sms sent file names
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
          MOVE      ZERO,OVRCD              * Open file of SMS sent message
          TRAP      OVERCOND IF IO          * files to process
          OPEN      SENTNAME,"sentsms1"
          TRAPCLR   IO
          BRANCH    OVRCD,SENT9999
.
          MOVE      SP70,KEY12
SENT1000  READ      SENTNAME,SEQ;KEY12      * No more sent sms files to precess
          GOTO      SENT9999 IF OVER
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      SMSSENT1,KEY12    * No sent text file found
          TRAPCLR   IO                * so exit
          BRANCH    OVRCD,SENT1000
.
SENT2000  READ      SMSSENT1,SEQ;SENTMNUM,SENTUMID,SENTDATE,SENTTIME:
                                 SENTUSER,SENTURNO,SENTSCOD:
                                 SENTRKEY,SENTMESA,SENTMESB,SENTMESC:
                                 SENTMESD,SENTMESE,SENTMESF,SENTSTAT:
                                 SENTCONT,SENTCTYP
          GOTO      SENT9000 IF OVER
.
          CALL      IBACLOCK                     * load current date
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE
          CLOCK     TIME,CTIMEIS
.
          PACK      SENTMNUM,SENTMNUM,SP70  * Pad fields out to full length
.
          MOVE      ZERO,FORM16
          SQUEEZE   SENTUMID
          MOVE      SENTUMID,FORM16
          MOVE      FORM16,SENTUMID
.
          PACK      SENTDATE,SENTDATE,SP70
          PACK      SENTTIME,SENTTIME,SP70
          PACK      SENTUSER,SENTUSER,SP70
          PACK      SENTURNO,SENTURNO,SP70
          PACK      SENTRKEY,SENTRKEY,SP70
          PACK      SENTMESA,SENTMESA,SP70
          PACK      SENTMESB,SENTMESB,SP70
          PACK      SENTMESC,SENTMESC,SP70
          PACK      SENTMESD,SENTMESD,SP70
          PACK      SENTMESE,SENTMESE,SP70
          PACK      SENTMESF,SENTMESF,SP70
          PACK      SENTCONT,SENTCONT,SP70
          PACK      SENTCTYP,SENTCTYP,SP70
.
          MOVE      ZERO,FORM2
          SQUEEZE   SENTSTAT
          MOVE      SENTSTAT,FORM2
          MOVE      FORM2,SENTSTAT
.
          CALL      CLPMSSMH                * Clear sms history file vars
.
          MOVE      SENTMNUM,PMSMMNUM
          MOVE      SENTUMID,PMSMUMID
          MOVE      SENTDATE,PMSMSDAT
          MOVE      SENTTIME,PMSMSTIM
          MOVE      SENTUSER,PMSMSUID
          MOVE      SENTURNO,PMSMURNO
          UNPACK    SENTRKEY,PMSMMTYP,PMSMRKEY
          MOVE      SENTSCOD,PMSMSCOD
.
          MOVE      SENTMESA,PMSMMESA
          MOVE      SENTMESB,PMSMMESB
          MOVE      SENTMESC,PMSMMESC
          MOVE      SENTMESD,PMSMMESD
          MOVE      SENTMESE,PMSMMESE
          MOVE      SENTMESF,PMSMMESF
          MOVE      SENTSTAT,PMSMSTAT
          MOVE      SENTCONT,PMSMCONT
          MOVE      SENTCTYP,PMSMCTYP
.
          PACK      KEY16,PMSMUMID,SP70
          CALL      RAPMSMH1                
          IF        OVRCD=1
            CALL      WRPMSMH1              * Write SMS history file record
            CALL      ELGL0000              * updating Elibility check ?
          ELSE
            PACK      KEY16,PMSMUMID,SP70
            CALL      RDPMSMH1              * Sent history exists
            IF        OVRCD=0
              MATCH     " 4",PMSMSTAT       * Waiting to be sent? (is a re-send)
              IF        @EQUAL
                MOVE      SENTDATE,PMSMSDAT      * set send date/time/user
                MOVE      SENTTIME,PMSMSTIM
                MOVE      SENTUSER,PMSMSUID
                MOVE      SENTSTAT,PMSMSTAT      * update status
                CALL      UPPMSMH1
              ENDIF
            ENDIF
          ENDIF
          GOTO      SENT2000
.
SENT9000  CLOSE     SMSSENT1,DELETE         * Delete sent text file
          GOTO      SENT1000
.
SENT9999  RETURN
.
.------------------------------------------------------------
.        Check for Eligibility Check type of letter
.------------------------------------------------------------
ELGL0000  MATCH     "1",PTCNIFCL
          GOTO      ELGL9999 IF NOT EQUAL   * Not using HEA IFC
.
          MATCH     " 3",PMSMMTYP
          GOTO      ELGL9999 IF NOT EQUAL   * Not inpatient
.
          UNPACK    PMSMRKEY,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,ELGL9999
          COMPARE   ONE,ASTAT
          GOTO      ELGL9999 IF NOT EQUAL
.
          MOVE      "  0",KEY3               * read letter file header
          PACK      KEY6,PMSMSCOD,KEY3      * record
          CALL      RDPTLET1
          BRANCH    OVRCD,ELGL9999
.
.         Write to SMS History file if necessary
.
          MATCH     "1",PTLECONF
          GOTO      ELGL9999 IF NOT EQUAL   * Not Eligibility Letter
.
          PACK      KEY8,AADMNO,SP70
          CALL      RDPTELG1
          BRANCH    OVRCD,ELGL9999
.
          MOVE      "1",PTELQACC            * Quote Accepted/Patient Contacted
          CALL      UPPTELG1

ELGL9999  RETURN
+
.------------------------------------------------------------
. Clear Parameters
.------------------------------------------------------------
CLRPAR00  RETURN
.
.------------------------------------------------------------
. Set Parameters
.------------------------------------------------------------
SETPAR00  RETURN
.
.------------------------------------------------------------
. Process Fields
.------------------------------------------------------------
PROFLD00  RETURN
.
.----------------------------------------------------------------------
.
          INC       IBAWEBCD
          INC       CLPMSSMH
          INC       CLPMSSRR
.
          INC       PATILTIO/INC
          INC       PATELGIO/INC
          INC       PMSSRRIO/INC
          INC       PMSSMHIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
+
