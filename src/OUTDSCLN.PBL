.********************************************************************** 
.     OUTDSCLN : Display Clinics details.
.
.     This include displays entries in the CLINIC ID file.  
.
.********************************************************************** 
.     Modifications: 
.     V10.07.01   30/10/2015  Ebon Clements  CAR 268970
.                 Change to use OUTCLIFD index 1 instead of index 2
.                   19/09/2000  Steve Armstrong   SRF 647263
.                   Added check on site code when getting latest clinic record
.                   06/03/2000  Steve Armstrong
.                   Mods for effective date
.                   25/01/1995  Steve Armstrong 
.                   Fixed at end of file to go to ENDCLN1
.                   24/06/1994  bez          acc rehab mods 
.                   Rename "Clinic Id" if parameter set
.                    28/11/89 Graeme Williams
.                    Added the parameter CNEWDS to determine if the include
.                    is to use the new standards for "?" routines
.********************************************************************** 
.
OUTDSCLN  
.-------- Rename "Clinic Id" if parameter set
          OPEN      CONTROLF,"controlf"          
          READ      CONTROLF,THIRTY2;*2,FORM1:
                                     *16,KEY12
          IF        FORM1=0
            MOVE      "Clinic Id   ",KEY12    * parameter not set
          ENDIF
          STRIP      KEY12
.
DSCLN000  DISPLAY    *P1:3,*EF
          KEYIN      *P1:4,*DV,*+,KEY12," for search : ": 
                            *V2LON,OCACLIN
.
          ENDSET     OCACLIN
          APPEND     SP6,OCACLIN
          RESET      OCACLIN
.
.         Display Category Heading, and then loop over entries
.
          DISPLAY    *P1:6,*EF,*ULON,"Code  ",*P9:6,"Name                    ": 
                     "     ",*P41:6,"Code  ",*P49:6,"Name                      "
.
          MOVE       SEVEN,CVERT
          MOVE       ONE,CCOL
          MOVE       SP30,OCADESC
.
          PACK       KEY20,IDDD,OCACLIN,SP20
          CALL       RDSCLIA1
.
.         Loop over file
.
DISPCLN1  CALL      RDKCLIA1
          BRANCH    OVRCD,ENDCLN1
.
          MATCH     IDDD,OCASITE
          GOTO      ENDCLN1 IF NOT EQUAL
.
.         A clinic id has been found, so get the latest record to display
.
          PACK      KEY20,IDDD,OCACLIN,Z70
          CALL      RDSCLIA1
          CALL      RDPCLIA1
          BRANCH    OVRCD,ENDCLN1                * shouldn't happen
.
          MATCH     IDDD,OCASITE                 * shouldn't happen
          GOTO      ENDCLN1 IF NOT EQUAL
.
          MATCH     SP6,OCADOCT
          GOTO      DISPDATA IF EQUAL
.
          MOVE      OCADOCT,KEY6
          CALL      RDDOCT1
          COMPARE   OVRCD,ONE
          GOTO      DISPDOC IF NOT EQUAL
.
          MOVE      SP30,OCADESC
          GOTO      DISPDATA
.
DISPDOC   CALL      CONCATDR   
          MOVE      DIM40,OCADESC
.
DISPDATA  DISPLAY   *PCCOL:CVERT,*V2LON,OCACLIN,*HOFF,SP2,OCADESC
.
          ADD       ONE,CVERT
.
          COMPARE   TWENTY4,CVERT
          GOTO      DISPCLN1 IF LESS
.
          MOVE      SEVEN,CVERT
          ADD       "40",CCOL
          COMPARE   "70",CCOL
          GOTO      DISPCLN1 IF LESS
.
.         Check for new standards question, or old question
.
DSCLN100  BRANCH    CNEWDS,DSCLN200
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       "eEfFnN",ANS
          CMATCH    ANSE,ANS
          GOTO      ENDCLN2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      DSCLN000 IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      DSCLN400 IF EQUAL
.
          BEEP
          GOTO      DSCLN100
.
.         Use the new standards question
.
DSCLN200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       "eEfFnNcC",ANS
          CMATCH    ANSC,ANS
          GOTO      ENDCLN2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      DSCLN000 IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      DSCLN400 IF EQUAL
.
          BEEP
          GOTO      DSCLN200
.
DSCLN400  MOVE      SEVEN,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *PCCOL:CVERT,*EF
          GOTO      DISPCLN1
.
.      End of codes
.
ENDCLN1    BRANCH   CNEWDS,ENDCLN2
.
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
.
ENDCLN2   MOVE      SP30,OCADESC
          MOVE      ZERO,OVRCD
          RETURN
.
CONCATDR PACK       DIM40 WITH SP20,SP20
         MOVE       DTITL,DIM40
         ENDSET     DIM40
         CALL       BUMPCHD
         LENSET     DIM40
         APPEND     SP1,DIM40
         APPEND     DSNAME,DIM40
         CALL       BUMPCHD
         APPEND     ", ",DIM40
         APPEND     DGNAME,DIM40
         RESET      DIM40
         CMATCH     DIM40,SP1
         RETURN     IF NOT EQUAL
         MOVE       SP30,DIM40
         RETURN
.
BUMPCHD  CMATCH     SP1,DIM40
         RETURN     IF NOT EQUAL
         BUMP       DIM40 BY -1
         RETURN     IF EOS
         GOTO       BUMPCHD
