.******************************************************************************
.*                                  EMRACPVL                                  *
.* Validate EMRACTFD effective from and to dates for overlaps                 *
.*                                                                            *
.* Requires:   KEY38 populated with -                                         *
.*                      - ECDG Code                                           *
.*                      - Service Date (defaults to current date)             *
.*             VALFDATE - Effictive date from date                            *
.*             VALTDATE - Effictive date to date                              *
.*                                                                            *
.* Returns:    EXIT  0 = Valid effective from and to dates                    *
.*                   1 = Invalid effective from and to dates                  *
.*             ENDKEY38 = Key to record that requires an auto end date update *
.*                                                                            *
.* Modifications:                                                             *
.* V11.01.01  20/08/2021  Thanh T         TSK 0905305                         *
.*            Initial Created. Copy from EMRECHVL.PBL to be consistent with   *
.*            table name                                                      *
.******************************************************************************
.
RPVL0000  MOVE      SP70,ENDKEY38           * Clear auto end key
          MOVE      KEY38,SAVKEY30          * Save ECDG Subcategory
.
          CALL      RDEMACP1                * Record on file so must be an
          BRANCH    OVRCD,RPVL5000          * update
.
          MATCH     VALTDATE,EMAPTDAT       * No change in effective from and
          GOTO      RPVL8000 IF EQUAL       * to dates so exit
.
RPVL5000  CALL      RSEMACP1                * Check for the same ECDG sub cat
          CALL      RPEMACP1                * with an earlier effective from
          BRANCH    OVRCD,RPVL6000          * date
.
          MATCH     SAVKEY30,EMAPCODE       * Same ECDG Subcategory
          GOTO      RPVL6000 IF NOT EQUAL
.
          MATCH     SP70,EMAPTDAT           * No effective to date prev  record
          IF        @EQUAL
            PACK      ENDKEY38,EMAPCODE,EMAPFDAT,SP70 * auto end date record key
            GOTO      RPVL8000 
          ENDIF
.
          MATCH     VALFDATE,EMAPTDAT
          IF        !@LESS
            GOTO      RPVL9000              * Effective from date overlap
          ENDIF
.
          MATCH     SP70,VALTDATE           * Blank effective to date
          GOTO      RPVL6000 IF EQUAL
.
          MATCH     VALTDATE,EMAPTDAT
          IF        !@LESS
            GOTO      RPVL9000              * Effective to date overlap
          ENDIF
.
RPVL6000  CALL      RSEMACP1                * Check for the same ECDG sub cat
          CALL      RKEMACP1                * with an earlier effective from
          BRANCH    OVRCD,RPVL8000          * date
.
          MATCH     SAVKEY30,EMAPCODE       * Same ECDG Subcategory
          GOTO      RPVL8000 IF NOT EQUAL
.
          MATCH     SP70,VALTDATE           * Blank effective to date
          GOTO      RPVL9000 IF EQUAL
.
          MATCH     EMAPFDAT,VALFDATE
          IF        !@LESS
            GOTO      RPVL9000              * Effective from date overlap
          ENDIF
.
          MATCH     EMAPTDAT,VALTDATE
          IF        !@LESS
            GOTO      RPVL9000              * Effective to date overlap
          ENDIF
.
RPVL8000  MOVE      ZERO,EXIT               * OK
          GOTO      RPVL9999
.
RPVL9000  MOVE      SP70,ENDKEY38
          MOVE      ONE,EXIT                * Error end date overlap
          GOTO      RPVL9999
.
RPVL9999  RETURN

