. *----------------------------------------------------------------------
. * Include Name  :   NZPPTYTM.PBL
. *
. * Function      :   Common CGI functions for NZ Related Provider Types
. *
. * Modifications :
. *
.------------------------------------------------------------------------
. V11.04.01 26/06/2024  Thanh T       TSK 0947684
.           Changed PRVT0000 to display the number of rows depending on
.           system parameter
.------------------------------------------------------------------------
. V9.08.00  02/01/2006 Ebon Clements  CAR 126779
.           Created Include
.------------------------------------------------------------------------
.   Clear CGI Parameters for NZPPTYFD
.------------------------------------------------------------------------
CPNZPT00  MOVE      Z70,NZPTY001
          MOVE      Z70,NZPTY002
          MOVE      Z70,NZPTY003
          MOVE      Z70,NZPTY004
          MOVE      Z70,NZPTY005
          MOVE      Z70,NZPTY006
          MOVE      Z70,NZPTY007
          MOVE      Z70,NZPTY008
          MOVE      Z70,NZPTY009
          MOVE      Z70,NZPTY010
.
CPNZPT99  RETURN
.
.------------------------------------------------------------
.   Set Parameters for NZPPTYFD
.------------------------------------------------------------
SPNZPT00  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPNZPT01,SPNZPT02,SPNZPT03,SPNZPT04,SPNZPT05:
                       SPNZPT06,SPNZPT07,SPNZPT08,SPNZPT09,SPNZPT10
.
          MOVE      ONE,EXIT
          GOTO      SPNZPT99
.
SPNZPT01  PACK      NZPTY001,QRYDATA,SP70
          GOTO      SPNZPT99
.
SPNZPT02  MOVE      QRYDATA,NZPTY002
          GOTO      SPNZPT99
.
SPNZPT03  MOVE      QRYDATA,NZPTY003
          GOTO      SPNZPT99
.
SPNZPT04  MOVE      QRYDATA,NZPTY004
          GOTO      SPNZPT99
.
SPNZPT05  MOVE      QRYDATA,NZPTY005
          GOTO      SPNZPT99
.
SPNZPT06  MOVE      QRYDATA,NZPTY006
          GOTO      SPNZPT99
.
SPNZPT07  MOVE      QRYDATA,NZPTY007 
          GOTO      SPNZPT99
.
SPNZPT08  MOVE      QRYDATA,NZPTY008
          GOTO      SPNZPT99
.
SPNZPT09  MOVE      QRYDATA,NZPTY009
          GOTO      SPNZPT99
.
SPNZPT10  MOVE      QRYDATA,NZPTY010
          GOTO      SPNZPT99
.
SPNZPT99  RETURN
.
.--------------------------------------------------------------------------
. NZ Private Provider type Information
.--------------------------------------------------------------------------
NZPT0000  BRANCH    FLDITMNO,NZPT0100,NZPT0200,NZPT0300,NZPT0400,NZPT0500:
                             NZPT0600,NZPT0700,NZPT0800,NZPT0900,NZPT1000:
                             NZPT1100,NZPT1200,NZPT1300
          GOTO      NZPT9999
.
NZPT0100  WRITE     HTMLFILE;NZPTPTYP;
          GOTO      NZPT9999
.
NZPT0200  WRITE     HTMLFILE;NZPTDESC;
          GOTO      NZPT9999
.
NZPT0300  WRITE     HTMLFILE;NZPTPIND;
          GOTO      NZPT9999
.
NZPT0400  WRITE     HTMLFILE;NZPTCOST;               
          GOTO      NZPT9999
.
NZPT0500  WRITE     HTMLFILE;NZPTACOD;
          GOTO      NZPT9999
.
NZPT0600  WRITE     HTMLFILE;NZPTSCOD;
          GOTO      NZPT9999
.
NZPT0700  WRITE     HTMLFILE;NZPTGSTC;
          GOTO      NZPT9999
.
NZPT0800  WRITE     HTMLFILE;NZPTGSTA;
          GOTO      NZPT9999
.
NZPT0900  WRITE     HTMLFILE;NZPTGSTS;
          GOTO      NZPT9999
.
NZPT1000  WRITE     HTMLFILE;NZPTACTV;
          GOTO      NZPT9999
.
NZPT1100  CALL      PRVT0000                * Provider type list    
          GOTO      NZPT9999
.
NZPT1200  CALL      NEXTPT00                * Next
          GOTO      NZPT9999
.
NZPT1300  CALL      PREVPT00                * Prev
          GOTO      NZPT9999
.
NZPT9999  RETURN
.
.------------------------------------------------------------
.  Update NZPPTYFD file
.------------------------------------------------------------
MVNZPT00  MATCH     Z70,NZPTY001
          IF        !@EQUAL
            MOVE      NZPTY001,NZPTPTYP
          ENDIF
.
          MATCH     Z70,NZPTY002
          IF        !@EQUAL
            MOVE      NZPTY002,NZPTDESC
          ENDIF
.
          MATCH     Z70,NZPTY003
          IF        !@EQUAL
            MOVE      NZPTY003,NZPTPIND
          ENDIF
.
          MATCH     Z70,NZPTY004
          IF        !@EQUAL
            MOVE      NZPTY004,NZPTCOST
          ENDIF
.
          MATCH     Z70,NZPTY005
          IF        !@EQUAL
            MOVE      NZPTY005,NZPTACOD
          ENDIF
.
          MATCH     Z70,NZPTY006
          IF        !@EQUAL
            MOVE      NZPTY006,NZPTSCOD
          ENDIF
.
          MATCH     Z70,NZPTY007
          IF        !@EQUAL
            MOVE      NZPTY007,NZPTGSTC
          ENDIF
.
          MATCH     Z70,NZPTY008
          IF        !@EQUAL
            MOVE      NZPTY008,NZPTGSTA
          ENDIF
.
          MATCH     Z70,NZPTY009
          IF        !@EQUAL
            MOVE      NZPTY009,NZPTGSTS
          ENDIF
.
          MATCH     Z70,NZPTY010
          IF        !@EQUAL
            MOVE      NZPTY010,NZPTACTV
          ENDIF
.
MVNZPT99  RETURN
.
.-----------------------------------------------------------
. Link to Next page of provider types
.-----------------------------------------------------------
NEXTPT00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",STARTKEY
.
          WRITE     HTMLFILE;"patweb77.pbl":
                                 "?reportno=",F2:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&startkey=",STARTKEY;
.
          REP       "+ ",STARTKEY
.
NEXTPT99  RETURN
.-----------------------------------------------------------
. Link to Prev page of provider types
.-----------------------------------------------------------
PREVPT00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",STARTKEY
.
          WRITE     HTMLFILE;"patweb77.pbl":
                                 "?reportno=",F2:
                                 "&template=",FLDPARA:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&startkey=",STARTKEY;
.
          REP       "+ ",STARTKEY
.
PREVPT99  RETURN
.
.------------------------------------------------------------
. Table of Provider Types
.------------------------------------------------------------
PRVT0000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          CALL      PHED0000              * Output Table Heading
.         
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD   * Set Default No Records to Display
          ENDIF
.
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
. 
          PACK      KEY3,STARTKEY,SP70
          CALL      RDNZPTY1
          IF        OVRCD=0
            GOTO      PRVT1100
          ENDIF
PRVT1000  CALL      RKNZPTY1
          BRANCH    OVRCD,PRVT9900
.
PRVT1100  ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      PRVT1000
          ENDIF
.
          CALL      PTRW0000           * Display Mantenance Table Details Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      PRVT1000 IF NOT EQUAL
.
          GOTO      PRVT9999
.
PRVT9900  CALL      NOMORE00     * Display No More Records Message
.
PRVT9999  RETURN
.
.------------------------------------------------------------
. Provider Type Maintenance Table Details (TABLE HEADING)
.------------------------------------------------------------
PHED0000  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=15%>":
                               "Provider Type Code</td>":
                               "<td class=HeadingCell>":
                               "Description</td>":
                               "<td class=HeadingCell width=15%>":
                               "Active</td></tr>";
.
PHED9999  RETURN
.
.------------------------------------------------------------
. Provider Type Maintenance (TABLE ROW)
.------------------------------------------------------------
PTRW0000  MOVE      DYES,ACTIDESC
          MATCH     "1",NZPTACTV
          IF        @EQUAL
            MOVE      DNO,ACTIDESC
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail13":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&nzpty001=",NZPTPTYP,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             NZPTPTYP,"</td>":
                             "<td>",NZPTDESC,"</td>":
                             "<td>",ACTIDESC
.
         WRITE      HTMLFILE;"</tr>";
.
PTRW9999 RETURN
