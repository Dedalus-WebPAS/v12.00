.-------------------------------------------------------------------------------
.  File          :  EMRDRCHR.PBL
.
.  Function      :  Routine to check Doctor Billing complete by looping through
.                   EMR history for each doctor
.
.  Parameters    :  
.
.  Modifications : V10.06.01 24.06.2015 Ebon Clements CAR 318514
.                  Defined KEY22
.                  V10.06.00 01.04.2015 J.Tan    CAR 244314
.                  Created new routine to check for Doctor billing completion
.
.-------------------------------------------------------------------------------
.
          DEFPROC   EMRDRCHR
.
          INC       EMRVISFD/INC
          INC       EMRHISFD/INC
.
KEY22     DIM       22
.
          OPEN      EMRHISA1,"emrhisaf"
          OPEN      EMRVISA1,"emrvisaf"
.
          MOVE      ADMISSNO,KEY8
          CALL      RDEMVIS1
          BRANCH    OVRCD,DRCHR900
.
          MOVE      ZERO,EMVIYN09            * Billing In-complete
.
          PACK      KEY22,ADMISSNO,SP70
          CALL      RSEMHIS1
DRCHR100  CALL      RKEMHIS1
          BRANCH    OVRCD,DRCHR800
.
          MATCH     ADMISSNO,EMHIVIS
          GOTO      DRCHR800 IF NOT EQUAL
.
          MATCH     "ALDOC",EMHIUPT
          GOTO      DRCHR100 IF NOT EQUAL   * Not Allocated doctor type
.
          MATCH     "2",EMHIDRBC            * No Charge ?
          GOTO      DRCHR200 IF EQUAL
.
          MATCH     "1",EMHIDRBC            * Billing Complete ?
          GOTO      DRCHR600 IF NOT EQUAL   * found a Billing Incomplete doctor
.
          MOVE      ONE,EMVIYN09            * Doctor Billing complete
          GOTO      DRCHR100
.
DRCHR200  MOVE      TWO,EMVIYN09            * No Doctor Charge
          GOTO      DRCHR100
.
DRCHR600  MOVE      ZERO,EMVIYN09           * Billing Incomplete
.
DRCHR800  CALL      UPEMVIS1                * update EMR visit
.
DRCHR900  CLOSE     EMRHISA1
          GOTO      DRCHR999
.
          INC       IBAOVRIO/INC
          INC       EMRVISIO/INC
          INC       EMRHISIO/INC
.
DRCHR999  ENDPROC
.
