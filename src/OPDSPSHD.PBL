.*******************************************************************************
.    System          : Theatre System
.    Include Function: OPDSPSHD.PBL
.    Description     : Display session header details
.*******************************************************************************
.    Date            : 09/10/1990 Almost the end of a good year's programming.
.    Author          : Bill Dew
.    Modifications   : 12/02/91  ROD
.    V11.03.01 07/03/2023  Ebon Clements     TSK 0909393     
.               Added hospital to oprsesaf(1-6) and oprdetfa(1&5) indexes 
.                      Display session end time in heading
.                      17/05/94  Whirlpool  srf 129175
.                      Changed so it doesn't update the suspended flag
.*******************************************************************************
.
.    FUNCTION DESCRIPTION.
.
.    The function Displays a standard session header.
.
.    INCLUDES AND FILES.
.
.    FD's  : OPR001FD, OPRCONFD, PATCODFD, PATDO1FD, OPRSESFD, OPRCLIFD
.                                1         1                   1
.
.    I/O's : PATIOCOD, PATIODOC, OPRCLIIO
.
.    PBL's : STDOPR01 
.  
.    PARAMETERS.
.  
.    SESNCODE, SESNDATE and SESNTIME must be set to the clinic/doctor, session
.    date and session time.  Thus this routine requires a successful read
.    on the session file.  In some cases the theatre includes already cater
.    for it.  Else OPSEANAE and OPSECORD must be set to Anaesthetist and
.    Session coordinator respectively.
.    CURBKTYP : Must be set to booking type (cat BK)
.
.    RETURN PARAMETERS.
.
.    None
.
.*******************************************************************************
.                                 OPDSPSHD
.*******************************************************************************
OPDSPSHD  PACK      KEY22,SESNHOSP,SESNDATE,SESNTIME,SESNCODE
          CALL      RDOPSES1
.
          DISPLAY   *P1:4,*EF
.
.    Get Booking type.  If the code is blank then dont display anything on 
.    line 4 else show code and desc.
.
          MATCH     SP3,CURBKTYP       * test for a entered booking type
          GOTO      DSPSHD11 IF EQUAL
.
          MOVE      MISSCODE,TDESC     * default desc to missing 
          PACK      KEY5,ANSB,ANSK,CURBKTYP
          CALL      RDCODE1            * get description for booking type
          DISPLAY   *P5:4,"Booking Type     : ":
                    *V2LON,CURBKTYP,*HOFF,*P32:4,TDESC
.
DSPSHD11  BRANCH    OPCNCLSU,DSPSHD22  * check if using clinic or doctor
.
.    Using clinic : then get clinic desc from oprcliaf.  If the clinic desc
.    is blank then use OPCLDOCT for clinic desc.
.
          PACK      DESC40,MISSCODE,SP30    * default missing code
          PACK      KEY6,SESNCODE,SP6
          CALL      RDOPCLI1           * get clinic description
          BRANCH    OVRCD,DSPSHD33
          MOVE      OPCLDESC,DESC40
.
          MATCH     SP20,OPCLDESC      * test the description is non blank
          GOTO      DSPSHD33 IF NOT EQUAL
          PACK      KEY6,OPCLDOCT,SP6
          CALL      RDDOCT1            * get doctors details
          BRANCH    OVRCD,DSPSHD15
          CALL      DOCNM000           * setup doctors name in DESC40
          GOTO      DSPSHD33
.
DSPSHD15  PACK      DESC40,MISSCODE,SP30
          GOTO      DSPSHD33
.
.    Get doctor name 
.
DSPSHD22
          PACK      KEY6,SESNCODE,SP6
          CALL      RDDOCT1            * get doctors details
          BRANCH    OVRCD,DSPSHD15
          CALL      DOCNM000           * setup doctors name
.
DSPSHD33  DISPLAY   *P5:5,OPCNCDSC,"  : ",*V2LON,SESNCODE,*HOFF,SP2,DESC40
.
.    Display Session date and time and signal if session is SUSPENDED.
.
          UNPACK    SESNDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          DISPLAY   *P5:6,"Session Date     : ",*V2LON,CPCDATE,*HOFF:
                    *P50:6,"Session Time : ",*V2LON,SESNTIME,*HOFF," - ":
                    *V2LON,OPSEENDT
.
          COMPARE   ONE,OPSESUSP            * see if suspended
          CALL      DSPSUSPD IF EQUAL       * blink suspend if so
.
DSPSHD44
.
.    Display on line 7 either the Session Coordinator if using session 
.    coordinator OPCNSESS or the default Anaesthetist for that session
.
          COMPARE   ONE,OPCNSESS       * test if using session coordinator
          GOTO      DSPSHD55 IF NOT EQUAL
.
          PACK      KEY6,OPSECORD,SP6
          DISPLAY   *P5:7,"Session Co-ord.  : ";
          GOTO      DSPSHD66
DSPSHD55
          PACK      KEY6,OPSEANAE
          DISPLAY   *P5:7,"Anaesthetist     : ";
DSPSHD66
          CALL      RDDOCT1            * get doctors details
          BRANCH    OVRCD,DSPSHD77
          CALL      DOCNM000           * setup doctors name
          MOVE      DESC40,DESC25      * truncate
          DISPLAY   *V2LON,*V2LON,DESC25,*HOFF,*P50:7,OPCNODSC:
                    *P63:7,": ",*V2LON,OPSETHET
          GOTO      DSPSHD99
DSPSHD77
          DISPLAY   *V2LON,*HOFF,SP20,SP5,*HOFF,*P50:7,OPCNODSC:
                    *P63:7,": ",*V2LON,OPSETHET
          GOTO      DSPSHD99
.
DSPSHD99  RETURN
.*******************************************************************************
