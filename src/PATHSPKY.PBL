.******************************************************************************
.*  Include Name  : PATHSPKY.PBL                                              *
.*  Description   : Keyin of a Hospital Id/code                               *
.*  Author        : John Smith                                                *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF3 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMODE - is keyin mode                                  *
.*                                                                            *
.*  Returns       : KEY3     - Keyin in Hospital code                         *
.*                  PTHSNAME - Description   (PTHSxxxx  record)               *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Modifications : 12/08/93  ROD                                             *
.*                  When do a GETSCR, save whole screen (ie HLEF=0)           *
.*                  10/03/94  Sandra Barcham                                  *
.*                  Make Standard and Record Locking                          *
.*                  Changed name from PATHSPKY to PATHSPKY                    *
.*                  Changed CCOL - ECOL & CVERT - EVERT                       *
.*                                                                            *
.******************************************************************************
PATHSPKY
KHSPD000  MATCH     SP3,CKYIDEF3                  * using a default?
          GOTO      KHSPD050 IF NOT EQUAL
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN3:
                    *PECOL:EVERT,*V2LON,KEY3:
                    *PECOL:EVERT,*DV,KEY3;
          GOTO      KHSPD080
.
KHSPD050  MOVE      CKYIDEF3,KEY3
          KEYIN     *PECOL:EVERT,*DV,KEY3:      * keyin with default
                    *PECOL:EVERT,*V2LON,*RV,KEY3:
                    *PECOL:EVERT,*DV,KEY3;
.
KHSPD080  PACK      KEY3,KEY3,SP3
.
KHSPD090  CALL      HSPCK000
          BRANCH    EXIT,KHSPD500,KHSPD760,KHSPD790,KHSPD950
.                        "?"      valid    nothing  exitchar
          GOTO      KHSPD000
.
KHSPD500  MOVE      ZERO,HLEF               * save whole screen
          CALL      GETSCR00 
.
KHSPD550  CALL      PATHSPDS                * display all hospital on file
.
KHSPD600  MATCH     SP3,CKYIDEF3                  * using a default?
          GOTO      KHSPD650 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,"Hospital ID. :":
                    *P16:24,*DV,UNDLN3:
                    *P16:24,*V2LON,KEY3:
                    *P16:24,*DV,KEY3;
          GOTO      KHSPD700
.
KHSPD650  MOVE      CKYIDEF3,KEY3
          KEYIN     *P1:24,*EL,"Hospital ID. :":
                    *P16:24,*DV,KEY3:        * keyin with default
                    *P16:24,*V2LON,*RV,KEY3:
                    *P16:24,*DV,KEY3;
.
KHSPD700  PACK      KEY3,KEY3,SP3
          CALL      HSPCK000
          BRANCH    EXIT,KHSPD550,KHSPD750,KHSPD780,KHSPD900
.                        "?"      valid    nothing  exitchar
          GOTO      KHSPD600
.
. a valid code was entered
.
KHSPD750  CALL      PUTSCR00                 * Restore Screen
          DISPLAY   *PECOL:EVERT,*V2LON,KEY3
.
KHSPD760  MOVE      ZERO,EXIT                * Valid Input
          GOTO      KHSPD999
.
. nothing was input
.
KHSPD780  CALL      PUTSCR00                 * Restore Screen
          DISPLAY   *PECOL:EVERT,*V2LON,KEY3
.
KHSPD790  MOVE      ONE,EXIT                 * Nothing or Spaces Entered
          GOTO      KHSPD999
.
. EXITCHAR input
.
KHSPD900  CALL      FRESCR00                 * Free the Screen Saved
.
KHSPD950  MOVE      TWO,EXIT                 * EXITCHAR
.
KHSPD999  RETURN
.
.*********************************************************************
.*                  HSPCK000                    Called by : PATHSPKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
HSPCK000  ENDSET    KEY3
          APPEND    SP3,KEY3
          RESET     KEY3
.
          MATCH     EXITCHAR,KEY3
          GOTO      HSPCK600 IF EQUAL       * exit char
.
          MATCH     SP3,KEY3
          GOTO      HSPCK700 IF EQUAL       * nothing
.
          MATCH     QUEST,KEY3
          GOTO      HSPCK800 IF EQUAL       * ? mark
.
.         a code was entered so validate
. 
HSPCK500  CALL      RDPTHSP1
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,HSPCK900          * invalid code
          ELSE
            COMPARE   ZERO,OVRCD   
            GOTO      HSPCK900 IF EQUAL
          ENDIF  
.
          MOVE      TWO,EXIT                * valid code
          GOTO      HSPCK999
.
.         exitchar entered
.
HSPCK600  MOVE      FOUR,EXIT
          MOVE      SP3,PTHSHOSP
          MOVE      SP20,PTHSNAME           * clear desc field
          GOTO      HSPCK999
.
.         nothing entered
.
HSPCK700  BRANCH    CKYIMAND,HSPCK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP3,PTHSHOSP
          MOVE      SP20,PTHSNAME            * clear desc field
          GOTO      HSPCK999
.
.         "?" entered
.
HSPCK800  MOVE      ONE,EXIT
          GOTO      HSPCK999
.
.         invalid code entered
.
HSPCK900  IF        CKYIMODE = 0
            DISPLAY   *P1:24,*EL,*B,"Invalid Hospital Id. ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      HSPCK999
.
.         field is mandatory
.
HSPCK950  DISPLAY   *P1:24,*B,*EL,"Field is mandatory. ";
          CALL      HITENTER 
          MOVE      ZERO,EXIT
.
HSPCK999  RETURN
.
          INCLUDE   PATHSPDS

