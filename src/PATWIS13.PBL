.******************************************************************************
.* System         : Patient Management                                        *
.* Include        : PATWIS13.PBL                                              *
.* Description    : Calculate WIES13 value for an individual admission        *
.******************************************************************************
.* Date           : 23/07/2004                                                *
.* Author         : Henry Son                                                 *
.*                                                                            *
.* Vars Required  : VARWIS13 - Variables required for WIES13                  *
.*                                                                            *
.* Files Required Open :                                                      *
.*        PATASFFD  PATASFA1 - Admission special funding arrangement file     *
.*        PATCODFD  PATCODE1 - Codes file                                     *
.*        PATECDFD  PATECDA2 - Episode disease coding file                    *
.*        PATECOFD  PATECOA2 - Episode operation coding file                  *
.*        PAT10OFD  PATI10O1 - ICD10 operations file                          *
.*        PATICUFD  PATICUA1 - ICU file                                       *
.*        PATTRNFD  PATTRAN2 - Transfer file                                  *
.*        PATW13FD  PATW13A1 - Victorian cost weights (for WIES13) file       *
.*                                                                            *
.* Parameters Req : Successful read on admission file                         *
.*                  Successful read on discharge file                         *
.*                  HIHLOS   - for Hopsipta in Home LOS (from RHIHDAYS)       *
.*                  NBRDAYS  - for LOS calculation (from NHOSPDAY)            *
.*                  PNSWRACE - User defined field for race                    *
.*                  WIS13DRG - DRG to calculate WIES13 for                    *
.*                                                                            *
.* Returns        : TOTWIS13 - Total WIES13 value                             *
.*                  WIS13DRG - DRG used to calculate WIES13                   *
.*                  WIS13LOS - LOS used to calculate WIES13                   *
.*                  WIS13WGT - Weight used to calculate WIES13                *
.*                  WIS13DSC - Description of DRG weight used                 *
.*                                                                            *
.* Modifications  :                                                           *
.******************************************************************************
.         V12.00.01 15.05.2025  DA Sarkies     TSK 0955096
.                   Updated for Alpha-Numeric Visit Number
.*        V10.02.01 28/03/2011  Mike Laming   CAR 240107                      *
.*                  Mods to PATECDaf & PATECOaf variables & Keys - file change*
.*        V9.07.01  10/07/2006  Davin S          CAR 111764                   *
.*                  Created for 2006/2007 WIES13 changes                      *
.*        V9.04.03  25/07/2005  Peter Vela       CAR 66960                    *
.*                  Added 2005/2006 WIES12 file (patw12bf)                    *
.*        V9.04.02  01/09/2004  Henry son        CAR 53126                    *
.*                  Further changes for WIES12.                               *
.*                  Diagnosis or procedure incompatible with SEX code.        *
.*        V9.03.01  23/07/2004  Henry son        CAR 51504                    *
.*                  Modified from PATWIS11 for 2004/2005 changes              *
.******************************************************************************
+
PATWIS13
WIS13000  CALL      IN13W000                * Initialise variables
          BRANCH    EXIT,WIS13950
.
          CALL      C13TY000                * Determine and Check Care Type
          BRANCH    EXIT,WIS13950
.
          CALL      C13TR000                * Check for Contracted Patient
          BRANCH    EXIT,WIS13950
.
          CALL      M13RG000                * Modify DRG if necessary
.
          CALL      C13RG000                * Check for problem DRG
          BRANCH    EXIT,WIS13950
.
          CALL      M13CH000                * Check for Mechanical Ventilation
.
          CALL      T13AL000                * Check for Thalessaemia
.
          CALL      A13AA000                * Check for AAA
.
          CALL      A13SD000                * Check for ASD
.
          CALL      C13OL000                * Check for Colonoscopy
.
          CALL      B13SW000                * Calculate Base WIES13
.
          CALL      C13AB000                * Check for aboriginality
.
. ---- Calculate WIES13 value ----
.
          MOVE      TOTBWIES,TOTWIS13       * base WIES
          ADD       MVCOPAY,TOTWIS13        * mechv copayment
          ADD       THCOPAY,TOTWIS13        * thal  copayment
          ADD       AAACOPAY,TOTWIS13       * AAA   copayment
          ADD       ASDCOPAY,TOTWIS13       * ASD   copayment
          ADD       COLCOPAY,TOTWIS13       * colon copayment
          ADD       ATSIWIES,TOTWIS13       * ATSI  copayment
.
          MOVE      ZERO,EXIT
          GOTO      WIS13999
.
WIS13950  MOVE      ONE,EXIT
WIS13999  RETURN
+
.******************************************************************************
.*                                  IN13W000              Called by: WIS13000 *
.*                         Initialise WIES13 variables                        *
.******************************************************************************
IN13W000  MOVE      ZERO,TOTWIS13
          MOVE      ZERO,WIS13LOS
          MOVE      ZERO,WIS13WGT
          MOVE      SP20,WIS13DSC
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATW13A1,"patw13af"
          TRAPCLR   IO
          BRANCH    OVRCD,IN13W950
.
IN13W900  MOVE      ZERO,EXIT
          GOTO      IN13W999
.
IN13W950  MOVE      ONE,EXIT
IN13W999  RETURN
+
.******************************************************************************
.*                                  C13TY000              Called by: WIS13000 *
.*                 Check for valid Care Type & Incomplete Coding              *
.******************************************************************************
C13TY000  PACK     KEY5,ANSC,ANSC,ACARE     * use admission care type
          CALL     RDCODE1
          BRANCH   OVRCD,C13TY900
.
          MATCH    "6",TCINDC4              * Check Program Funding Source
          GOTO     C13TY900 IF NOT EQUAL
.
          MATCH    "0   ",THCSCOD           * check care types
          GOTO     C13TY900 IF EQUAL
          MATCH    "1   ",THCSCOD
          GOTO     C13TY900 IF EQUAL
          MATCH    "2   ",THCSCOD
          GOTO     C13TY900 IF EQUAL
          MATCH    "6   ",THCSCOD
          GOTO     C13TY900 IF EQUAL
          MATCH    "7   ",THCSCOD
          GOTO     C13TY900 IF EQUAL
          MATCH    "8   ",THCSCOD
          GOTO     C13TY900 IF EQUAL
          MATCH    "9   ",THCSCOD
          GOTO     C13TY900 IF EQUAL
          MATCH    "5   ",THCSCOD
          GOTO     C13TY900 IF EQUAL
          MATCH    "3   ",THCSCOD
          GOTO     C13TY900 IF EQUAL
          MATCH    "E   ",THCSCOD
          GOTO     C13TY900 IF EQUAL
          MATCH    "F   ",THCSCOD
          GOTO     C13TY900 IF EQUAL
          MATCH    "J   ",THCSCOD
          GOTO     C13TY900 IF EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     C13TY999
.
C13TY900  MOVE     ONE,EXIT                 * Not valid
C13TY999  RETURN
+
.******************************************************************************
.*                                  C13TR000              Called by: WIS13000 *
.*                        Check for Contracted Patient                        *
.******************************************************************************
C13TR000  PACK     KEY25,AADMNO,ANSA,SP30
          CALL     RSPTASF1
C13TR100  CALL     RKPTASF1                 * Read adm. special funding file
          BRANCH   OVRCD,C13TR800           * still process
.
          MATCH    AADMNO,PTAFADMN
          GOTO     C13TR800 IF NOT EQUAL    * still process
.
          MATCH    ANSA,PTAFTYPE            * special fund. arrang. type = adm
          GOTO     C13TR100 IF NOT EQUAL
.
          MATCH    ANSB,PTAFCROL            * Ignore if contract role = 'B'
          GOTO     C13TR900 IF EQUAL
.
C13TR800  MOVE     ZERO,EXIT                * valid
          GOTO     C13TR999
.
C13TR900  MOVE     ONE,EXIT                 * invalid
C13TR999  RETURN
+
.******************************************************************************
.*                                  M13RG000              Called by: WIS13000 *
.*                      Modify DRG if necessary for WIES13                    *
.******************************************************************************
M13RG000  CALL      P13IT000                * Peritoneal Dialysis
          CALL      R13IO000                * Radiotherapy
          CALL      B13EM000                * Bone Marrow Transplants
          CALL      H13TE000                * Hysterscopy Sterilisation
          CALL      M13TO000                * Mastoid Procedure
          CALL      E13RA000                * Extra Corporal Life Support
          CALL      D13PS000                * Diag. Proc. Sex incompat *I-53126
M13RG999  RETURN
+
.******************************************************************************
.*                                  P13IT000              Called by: M13RG000 *
.*                      Modify DRG if Peritoneal Dialysis                     *
.******************************************************************************
P13IT000  PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
P13IT100  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,P13IT999
.
          MATCH     AADMNO,PTEDADMN
          GOTO      P13IT999 IF NOT EQUAL
.
          MATCH     CMDISP,PTEDTYPE         * want principal diagnosis
          GOTO      P13IT100 IF NOT EQUAL
.
          MATCH     "Z49.2",PTEDCODE
          GOTO      P13IT100 IF NOT EQUAL
.
          MOVE      "L61Y",WIS13DRG         * modified for WIES13
P13IT999  RETURN
+
.******************************************************************************
.*                                  R13IO000              Called by: M13RG000 *
.*                         Modify DRG if Radiotherapy                         *
.******************************************************************************
R13IO000  PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
R13IO100  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,R13IO999
.
          MATCH     AADMNO,PTEDADMN         * same admission?
          GOTO      R13IO999 IF NOT EQUAL
.
          MATCH     "Z51.0",PTEDCODE        * radiotherapy?
          GOTO      R13IO100 IF NOT EQUAL
.
          MOVE      "R64Z",WIS13DRG         * modified for WIES13
R13IO999  RETURN
+
.******************************************************************************
.*                                  B13EM000              Called by: M13RG000 *
.*                    Modify DRG if Bone Marrow Transplant                    *
.******************************************************************************
B13EM000  MATCH     "A04Z",WIS13DRG
          GOTO      B13EM999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
B13EM100  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,B13EM900
.
          MATCH     AADMNO,PTEOADMN
          GOTO      B13EM900 IF NOT EQUAL
.
          MATCH     "13706-00",PTEOCODE
          IF        !@EQUAL
            MATCH     "13706-06",PTEOCODE
            IF        !@EQUAL
              MATCH     "13706-09",PTEOCODE
              IF        !@EQUAL
                MATCH     "13706-10",PTEOCODE
                GOTO      B13EM100 IF NOT EQUAL
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      "A04A",WIS13DRG         * new DRG = A04A
          GOTO      B13EM999
.
B13EM900  MOVE      "A04B",WIS13DRG         * new DRG = A04B
.
B13EM999  RETURN
+
.******************************************************************************
.*                                  H13TE000              Called by: M13RG000 *
.*                    Modify DRG if Hysterscopy Sterilisation                 *
.******************************************************************************
H13TE000  MATCH     "N09Z",WIS13DRG
          IF        !@EQUAL
            MATCH     "N10Z",WIS13DRG
            IF        !@EQUAL
              MATCH     "N11B",WIS13DRG
              IF        !@EQUAL
                MATCH     "O40Z",WIS13DRG
                IF        !@EQUAL
                  MATCH     "N08Z",WIS13DRG
                  IF        !@EQUAL
                    MATCH     "O05Z",WIS13DRG
                    GOTO      H13TE999 IF NOT EQUAL
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
H13TE100  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,H13TE999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      H13TE999 IF NOT EQUAL
.
          MATCH     "35688-01",PTEOCODE
          IF        !@EQUAL        
            GOTO      H13TE100 IF NOT EQUAL
          ENDIF
.
          MOVE      "N11C",WIS13DRG         * new DRG = N11C
          GOTO      H13TE999
.
H13TE999  RETURN
+
.******************************************************************************
.*                                  M13TO000              Called by: M13RG000 *
.*                    Modify DRG if Mastoid procedure                         *
.******************************************************************************
M13TO000  MATCH     "D06Z",WIS13DRG
          GOTO      M13TO999 IF NOT EQUAL
.
          MOVE      "D06B",WIS13DRG         * Other Sinus/mid/ear Proc.
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
M13TO100  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,M13TO999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      M13TO999 IF NOT EQUAL
.
          MATCH     "41545-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS13DRG       * Mastoid Procedure.
            GOTO      M13TO999
          ENDIF
.
          MATCH     "41551-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS13DRG       * Mastoid Procedure.
            GOTO       M13TO999
          ENDIF
.
          MATCH     "41554-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS13DRG       * Mastoid Procedure.
            GOTO      M13TO999
          ENDIF
.
          MATCH     "41557-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS13DRG       * Mastoid Procedure.
            GOTO      M13TO999
          ENDIF
.
          MATCH     "41557-03",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS13DRG       * Mastoid Procedure.
            GOTO      M13TO999
          ENDIF
.
          MATCH     "41560-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS13DRG       * Mastoid Procedure.
            GOTO      M13TO999
          ENDIF
.
          MATCH     "41563-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS13DRG       * Mastoid Procedure.
            GOTO      M13TO999
          ENDIF
.
          MATCH     "41564-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS13DRG       * Mastoid Procedure.
            GOTO      M13TO999
          ENDIF
.
          MATCH     "41566-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS13DRG       * Mastoid Procedure.
            GOTO      M13TO999
          ENDIF
.
          MATCH     "41566-01",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS13DRG       * Mastoid Procedure.
            GOTO      M13TO999
          ENDIF
.
          MATCH     "41566-02",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS13DRG       * Mastoid Procedure.
            GOTO      M13TO999
          ENDIF
.
          GOTO      M13TO100
.
M13TO999  RETURN
+
.******************************************************************************
.*                                  E13RA000              Called by: M13RG000 *
.*                    Modify DRG if Extra Corporeal Life Support              *
.******************************************************************************
E13RA000  MATCH     "A01Z",WIS13DRG
          GOTO      E13RA999 IF EQUAL
.
          MATCH     "A03Z",WIS13DRG
          GOTO      E13RA999 IF EQUAL
.
          MATCH     "A05Z",WIS13DRG
          GOTO      E13RA999 IF EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
E13RA100  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,E13RA999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      E13RA999 IF NOT EQUAL
.
          MATCH     "90225-00",PTEOCODE
          IF        @EQUAL
            MOVE      "A40Z",WIS13DRG   
            GOTO      E13RA999
          ENDIF
.
          MATCH     "38615-00",PTEOCODE
          IF        @EQUAL
            MOVE      "A40Z",WIS13DRG   
            GOTO      E13RA999
          ENDIF
.
          MATCH     "38615-01",PTEOCODE
          IF        @EQUAL
            MOVE      "A40Z",WIS13DRG   
            GOTO      E13RA999
          ENDIF
.
          MATCH     "38618-00",PTEOCODE
          IF        @EQUAL
            MOVE      "A40Z",WIS13DRG   
            GOTO      E13RA999
          ENDIF
.
          GOTO      E13RA100
.
E13RA999  RETURN
+
.******************************************************************************
.*                                  D13PS000              Called by: M13RG000 *
.*                    Modify DRG if Diagnosis or Procedure incompatible Sex   *
.******************************************************************************
D13PS000  MATCH     "901Z",WIS13DRG                                    *I-53126
          GOTO      D13PS999 IF NOT EQUAL
.
D13PS100  PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
D13PS200  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,D13PS999
.
          MATCH     AADMNO,PTEDADMN         * same admission?
          GOTO      D13PS999 IF NOT EQUAL
.
          MATCH     "E25.0",PTEDCODE        * is there a sex explin.
          GOTO      D13PS800 IF EQUAL       * yes
.
          MATCH     "E25.8",PTEDCODE        * is there a sex explin.
          GOTO      D13PS800 IF EQUAL       * yes
.
          MATCH     "E29.1",PTEDCODE        * is there a sex explin.
          GOTO      D13PS800 IF EQUAL       * yes
.
          MATCH     "E34.5",PTEDCODE        * is there a sex explin.
          GOTO      D13PS800 IF EQUAL       * yes
.
          MATCH     "F64.0",PTEDCODE        * is there a sex explin.
          GOTO      D13PS800 IF EQUAL       * yes
.
          MATCH     "Q56.0",PTEDCODE        * is there a sex explin.
          GOTO      D13PS800 IF EQUAL       * yes
.
          MATCH     "Q56.1",PTEDCODE        * is there a sex explin.
          GOTO      D13PS800 IF EQUAL       * yes
.
          MATCH     "Q56.2",PTEDCODE        * is there a sex explin.
          GOTO      D13PS800 IF EQUAL       * yes
.
          MATCH     "Q56.3",PTEDCODE        * is there a sex explin.
          GOTO      D13PS800 IF EQUAL       * yes
.
          MATCH     "Q56.4",PTEDCODE        * is there a sex explin.
          GOTO      D13PS800 IF EQUAL       * yes
.
          MATCH     "Q99.0",PTEDCODE        * is there a sex explin.
          GOTO      D13PS800 IF EQUAL       * yes
.
          MATCH     "Q99.1",PTEDCODE        * is there a sex explin.
          GOTO      D13PS800 IF EQUAL       * yes
.
          GOTO      D13PS200                * no
.
D13PS800  MOVE      "964Z",WIS13DRG
.
D13PS999  RETURN
+
.******************************************************************************
.*                                  C13RG000              Called by: WIS13000 *
.*                            Check for Problem DRG                           *
.******************************************************************************
C13RG000  PACK     KEY4,WIS13DRG
          CALL     RDPT131                  * check WIES13 weights file
          BRANCH   OVRCD,C13RG900
.
          MATCH    "960Z",PT13DRGC
          GOTO     C13RG900 IF EQUAL
.
          MATCH    "961Z",PT13DRGC
          GOTO     C13RG900 IF EQUAL
.
          MATCH    "962Z",PT13DRGC
          GOTO     C13RG900 IF EQUAL
.
          MATCH    "963Z",PT13DRGC
          GOTO     C13RG900 IF EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     C13RG999
.
C13RG900  MOVE     ONE,EXIT
C13RG999  RETURN
+
.******************************************************************************
.*                                  M13CH000              Called by: WIS13000 *
.*                Calculate Mechanical Ventialtion Co-payment                 *
.******************************************************************************
M13CH000  MOVE      ZERO,ADJMVDAY
          MOVE      ZERO,MVCOPAY
.
          PACK      KEY8,AADMNO
          CALL      RDPTICU1                * get mech. vent. hours
          BRANCH    OVRCD,M13CH999
.
          MATCH     "D",PT13MVEL            * check mech. vent. eligibility
          IF        @EQUAL
            IF        PTICUMEC > 6
              ASSIGN    ((PTICUMEC+12)/24),FORM12P4    * days receiving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7729)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
            GOTO      M13CH999
          ENDIF
.
          MATCH     "N",PT13MVEL            * check mech. vent. eligibility
          IF        @EQUAL
            IF        PTICUMEC > 6
              ASSIGN    ((PTICUMEC+12)/24),FORM12P4    * days receiving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7729)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
            GOTO      M13CH999
          ENDIF
.
          MATCH     "4",PT13MVEL
          IF        @EQUAL
            IF        PTICUMEC > 96
              ASSIGN    (((PTICUMEC+12)/24)-4),FORM12P4   * days recving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7729)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
          ENDIF
.
M13CH999  RETURN
+
.******************************************************************************
.*                                  T13AL000              Called by: WIS13000 *
.*                      Calculate Thalessaemia Co-payment                     *
.******************************************************************************
T13AL000  MOVE      ZERO,THCOPAY
          MATCH     "T",PT13CPAY            * check WIES13 weights file
          IF        @EQUAL
            PACK      KEY16,AADMNO,SP20
            CALL      RSPTECD2              * Position on & read a patient
T13AL100    CALL      RKPTECD2                episode disease file record
            BRANCH    OVRCD,T13AL999
.
            MATCH     AADMNO,PTEDADMN
            GOTO      T13AL999 IF NOT EQUAL
.
            MATCH     "D56",PTEDCODE        * check ICD-10 code
            IF        !@EQUAL
              MATCH     "D57.2",PTEDCODE    * check ICD-10 code
              GOTO      T13AL100 IF NOT EQUAL
            ENDIF
            MOVE      "0.2648",THCOPAY      * eligible for thal. copayment
          ENDIF
T13AL999  RETURN
+
.******************************************************************************
.*                                  A13AA000              Called by: WIS13000 *
.*                          Calculate AAA Co-payment                          *
.******************************************************************************
A13AA000  MOVE      ZERO,AAACOPAY
          MATCH     "AAA",PT13CPAY          * check WIES13 weights file
          GOTO      A13AA999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
A13AA100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,A13AA999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      A13AA999 IF NOT EQUAL
.
          MATCH     "33116-00",PTEOCODE     * check ICD-10 code
          GOTO      A13AA100 IF NOT EQUAL
.
          MOVE      "3.1421",AAACOPAY       * eligible for AAA copayment
A13AA999  RETURN
+
.******************************************************************************
.*                                  A13SD000              Called by: WIS13000 *
.*                          Calculate ASD Co-payment                          *
.******************************************************************************
A13SD000  MOVE      ZERO,ASDCOPAY
          MATCH     "ASD",PT13CPAY          * check WIES13 weights file
          GOTO      A13SD999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
A13SD100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,A13SD999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      A13SD999 IF NOT EQUAL
.
          MATCH     "38742-00",PTEOCODE     * check ICD-10 code
          GOTO      A13SD100 IF NOT EQUAL
.
          MOVE      "2.4713",ASDCOPAY       * eligible for ASD copayment
A13SD999  RETURN
+
.******************************************************************************
.*                                  C13OL000              Called by: WIS13000 *
.*                       Calculate Colonoscopy Co-payment                     *
.******************************************************************************
C13OL000  MOVE      ZERO,COLCOPAY
          MATCH     "Col",PT13CPAY          * check WIES13 weights file
          GOTO      C13OL999 IF NOT EQUAL
.
          CALL      LDCB1300                * load col. copayment codes
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
C13OL100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,C13OL999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      C13OL999 IF NOT EQUAL
.
          MOVE      ZERO,COUNT
C13OL200  ADD       ONE,COUNT
          COMPARE   TEN2,COUNT
          GOTO      C13OL100 IF EQUAL       * check next code on patecoaf
.
          MATCH     COLCODES[COUNT],PTEOCODE     * check ICD-10 code
          GOTO      C13OL200 IF NOT EQUAL
.
          MOVE      "0.1765",COLCOPAY       * eligible for Colono. copayment
C13OL999  RETURN
+
.******************************************************************************
.*                                  LDCB1300              Called by: C13OL000 *
.*                      Load Colonoscopy Copayment Codes                      *
.******************************************************************************
LDCB1300  MOVE      "30479-01",COLCODES[1]
          MOVE      "30479-02",COLCODES[2]
          MOVE      "90308-00",COLCODES[3]
          MOVE      "90312-01",COLCODES[4]
          MOVE      "32084-00",COLCODES[5]
          MOVE      "32084-01",COLCODES[6]
          MOVE      "32087-00",COLCODES[7]
          MOVE      "32090-00",COLCODES[8]
          MOVE      "32090-01",COLCODES[9]
          MOVE      "32093-00",COLCODES[10]
          MOVE      "32094-00",COLCODES[11]
LDCB1399  RETURN
+
.******************************************************************************
.*                                  B13SW000              Called by: WIS13000 *
.*                            Calculate Base WIES13                           *
.******************************************************************************
B13SW000  CALL      D13SC000                * length of stay category
          CALL      C13ST000                * calculate inlier status
.
          MATCH     "L",W13NLIER            * low outlier
          CALL      BW13L000 IF EQUAL       * calculate base WIES13
.
          MATCH     "I",W13NLIER            * inlier
          CALL      BW13N000 IF EQUAL       * calculate base WIES13
.
          MATCH     "H",W13NLIER            * high outlier
          CALL      BW13H000 IF EQUAL       * calculate base WIES13
.
B13SW999  RETURN
+
.******************************************************************************
.*                                  D13SC000              Called by: B13SW000 *
.*                      Determine Length Of Stay Category                     *
.******************************************************************************
D13SC000  MOVE      NBRDAYS,WIS13LOS        * length of stay from NHOSPDAY
          MOVE      "M",LOSCAT              * set LOS category to multiday
          MATCH     ADATE,DDATE             * check for sameday
          IF        @EQUAL
            MOVE      ONE,SAMEDAY           * sameday
            MOVE      ONE,WIS13LOS          * LOS = 1
            MOVE      "S",LOSCAT            * set LOS category to sameday
            GOTO      D13SC999
          ELSE
            MOVE      ZERO,SAMEDAY          * not sameday
          ENDIF
.
          IF        SAMEDAY = 0 & WIS13LOS = 1
            MOVE      "O",LOSCAT            * set LOS category to oneday
            GOTO      D13SC999
          ENDIF
.
          IF        WIS13LOS > 1825
            MOVE      "1825",WIS13LOS       * maximum LOS is 5 years
          ENDIF
.
D13SC999  RETURN
+
.******************************************************************************
.*                                  C13ST000              Called by: B13SW000 *
.*                           Calculate Inlier Status                          *
.******************************************************************************
C13ST000  MOVE      "I",W13NLIER            * set status to inlier
          IF        WIS13LOS < PT13LIBP
            MOVE      "L",W13NLIER          * set status to low outlier
            GOTO      C13ST999
          ENDIF
.
          ASSIGN    (PT13HIBP+ADJMVDAY),FORM12P4   * high bound. + mech.v days
          IF        WIS13LOS > FORM12P4
            MOVE      "H",W13NLIER          * set status to high outlier
          ENDIF
C13ST999  RETURN
+
.******************************************************************************
.*                                  BW13L000              Called by: B13SW000 *
.*                     Calculate Base WIES13 for Low Outlier                  *
.******************************************************************************
BW13L000  MATCH     "S",LOSCAT            * sameday
          IF        @EQUAL
            MOVE      PT13TSDW,TOTBWIES             * base WIES13
            ASSIGN    (TOTBWIES/PT13TIMW),TOTIES13  * IES13
.
            MOVE      PT13TSDW,WIS13WGT             * for display
            MOVE      "Same Day Wgt ",WIS13DSC
            GOTO      BW13L999
          ENDIF
.
          MATCH     "O",LOSCAT            * oneday
          IF        @EQUAL
            MOVE      PT13TODW,TOTBWIES             * base WIES13
            ASSIGN    (TOTBWIES/PT13TIMW),TOTIES13  * IES13
.
            MOVE      PT13TODW,WIS13WGT             * for display
            MOVE      "One Day Wgt  ",WIS13DSC
            GOTO      BW13L999
          ENDIF
.
          MATCH     "M",LOSCAT            * multiday
          IF        @EQUAL
            ASSIGN    (PT13TODW+((WIS13LOS-1)*PT13TLOM)),TOTBWIES
.                                                    * base WIES13
            ASSIGN    (TOTBWIES/PT13TIMW),TOTIES13   * IES13
.
            MOVE      PT13TLOM,WIS13WGT             * for display
            MOVE      "Low Outlr Wgt",WIS13DSC
            GOTO      BW13L999
          ENDIF
BW13L999  RETURN
+
.******************************************************************************
.*                                  BW13N000              Called by: B13SW000 *
.*                      Calculate Base WIES13 for Inlier                      *
.******************************************************************************
BW13N000  MOVE      ONE,TOTIES13          * IES13 = 1
          MATCH     "S",LOSCAT            * sameday
          IF        @EQUAL
            MOVE      PT13TSDW,TOTBWIES   * base WIES13
.
            MOVE      PT13TSDW,WIS13WGT             * for display
            MOVE      "Same Day Wgt ",WIS13DSC
            GOTO      BW13N999
          ENDIF
.
          MATCH     "O",LOSCAT            * oneday
          IF        @EQUAL
            MOVE      PT13TODW,TOTBWIES   * base WIES13
.
            MOVE      PT13TODW,WIS13WGT             * for display
            MOVE      "One Day Wgt  ",WIS13DSC
            GOTO      BW13N999
          ENDIF
.
          MATCH     "M",LOSCAT            * multiday
          IF        @EQUAL
            MOVE      PT13TIMW,TOTBWIES   * base WIES13
.
            MOVE      PT13TIMW,WIS13WGT             * for display
            MOVE      "Inlier Wgt   ",WIS13DSC
            GOTO      BW13N999
          ENDIF
BW13N999  RETURN
+
.******************************************************************************
.*                                  BW13H000              Called by: B13SW000 *
.*                    Calculate Base WIES13 for High Outlier                  *
.******************************************************************************
BW13H000  ASSIGN    ((WIS13LOS-PT13HIBP)-ADJMVDAY),HIGHDAYS  * high outlr days
          COMPARE   ZERO,HIGHDAYS
          IF        @LESS
            MOVE      ZERO,HIGHDAYS         * highdays must be >= zero
          ENDIF
.
          COMPARE   HIGHDAYS,HIHLOS
          IF        @LESS
            MOVE      HIHLOS,HITHDAYS       * get lesser of highdays and
          ELSE                              *  HIHLOS
            MOVE      HIGHDAYS,HITHDAYS
          ENDIF
.
          ASSIGN    (HIGHDAYS-HITHDAYS)*PT13THOM,TOTBWIES    *high WIES
          ASSIGN    (HITHDAYS*PT13THIH)+TOTBWIES,TOTBWIES    *HITH WEIS
          ASSIGN    (PT13TIMW+TOTBWIES),TOTBWIES             *base WEIS13
          ASSIGN    (TOTBWIES/PT13TIMW),TOTIES13             * IES13
.
          MOVE      PT13THOM,WIS13WGT                        * for display
          MOVE      "High Outl Wgt",WIS13DSC
BW13H999  RETURN
+
.******************************************************************************
.*                                  C13AB000              Called by: WIS13000 *
.*                          Check for Aboriginality                           *
.******************************************************************************
C13AB000  MOVE      ZERO,ATSIWIES                * set ATSI WIES to zero
          IF        PNSWRACE<1 | PNSWRACE>14
            GOTO      C13AB999              * Invalid parameter
          ENDIF
.
          LOAD      TCODE,PNSWRACE,CATP1,CATP2,CATP3,CATP4,CATP5:
                                   CATP6,CATA1,CATA2,CATA3,CATA4:
                                   CATA5,CATA6,CATA7,CATVA
          LOAD      ACODE,PNSWRACE,PUSR1,PUSR2,PUSR3,PUSR4,PUSR5:
                                   PUSR6,AUSR1,AUSR2,AUSR3,AUSR4:
                                   AUSR5,AUSR6,AUSR7,PMPXABRG
.
          MATCH     ACODE,SP3
          GOTO      C13AB999 IF EQUAL
.
          PACK      KEY5,TCODE,ACODE
          CALL      RDCODE1                 * Read a code file record
          BRANCH    OVRCD,C13AB999
.
          MOVE      THCSCOD,KEY1
          TYPE      KEY1
          GOTO      C13AB999 IF NOT EQUAL   * Non numeric, error
.
          MOVE      KEY1,FORM1
.
. ---- Calculate ATSI Loading if aboriginality = 5, 6, or 7 ----
.
          IF        FORM1=5 | FORM1=6 | FORM1=7
            MOVE      TOTBWIES,FORM12P4       * base WIES
            ADD       MVCOPAY,FORM12P4        * mechv copayment
            ADD       THCOPAY,FORM12P4        * thal  copayment
            ADD       AAACOPAY,FORM12P4       * AAA   copayment
            ADD       ASDCOPAY,FORM12P4       * ASD   copayment
            ADD       COLCOPAY,FORM12P4       * colon copayment
            ASSIGN    (0.3*FORM12P4),ATSIWIES
          ENDIF
.
C13AB999  RETURN
+
