.*********************************************************************
.*                                                                   *
.*                              DATECONV                             *
.*                              --------                             *
.*                                                                   *
.*    Include to convert a date to a day number :   1 = Monday       *
.*                                                  through to       *
.*                                                  7 = Sunday       *
.*                                                                   *
.*    AUTHOR           :  DIG                                        *
.*                                                                   *
.*    VARIABLES NEEDED :  The date in CDAY, CMON, CYEAR and          *
.*                        CCENT variables                            *
.*                                                                   *
.*    FILES NEEDED     :  PATIOYER                                   *
.*                                                                   *
.*    RETURNS          :  EXIT = 1   if Year is not set up in        *
.*                                   patient years file              *
.*                        WEEKDAY    the day number of the week (1-7)*
.*                                                                   *
.*********************************************************************
.* Mods    :                                                         *
.*           V10.03.02  17/10/2012 Jill Parkinson CAR 273375         *
.*                      Fixed YEARS000 from previous change          *
.*           V10.03.01  27/03/2010 Peter McMullen   259587           *
.*                      Remove reference to patyear1 file            *
.*            V5.07.02  05/01/2000 Bronko Sosic SRF 636815           *
.*                      Modification of date in YEARS000             *
.*            V5.07.01  09/02/1999 Jim Stathopoulos                  *
.*                      Modified date manipulation in YEARS000       *
.*            V5.07.00  06/10/1998 Jim Stathopoulos                  *
.*                      507 Changes                                  *
.*********************************************************************
DATECONV  PACK      KEY4,CCENT,CYEAR
          CALL      YEARS000                    * get first day of given year
          BRANCH    EXIT,DATEC999
.          
.------ First day of year exists ------
.
          MOVE      CDAY,DD
          MOVE      CMON,MM
          MOVE      CYEAR,YY
          MOVE      CCENT,CC
          CALL      DMYCON                      * convert to a julian date
.
.------ Calculate the day of the week ------
.
          MOVE      JULDAY,FSTJULDY
          DIV       SEVEN,FSTJULDY
          MULT      SEVEN,FSTJULDY
.
          SUB       FSTJULDY,JULDAY
.
          MOVE      JULDAY,FSTJULDY
          ADD       FSTDAY,FSTJULDY
          SUB       ONE,FSTJULDY
          MOVE      FSTJULDY,WEEKDAY
.
.------ Change to a number from 1 to 7 ------
.
          COMPARE   ZERO,FSTJULDY
          GOTO      DATEC900 IF EQUAL
.
          COMPARE   EIGHT,FSTJULDY
          GOTO      DATEC999 IF LESS
.
          SUB       SEVEN,FSTJULDY
          MOVE      FSTJULDY,WEEKDAY
          GOTO      DATEC999
.
DATEC900  MOVE      SEVEN,WEEKDAY
.
DATEC999  RETURN
+
.**********************************************************************
.*                               YEARS000                             *
.*        Routine to get the starting day of a given Year > 1900      *
.**********************************************************************
YEARS000  MOVE      ONE,EXIT     
          REP       " 0",KEY4
          MOVE      KEY4,FORM4 
.
          MOVE      "1900",F4      * start from 1900
          MOVE      ONE,FSTDAY     * 01/01/1900 was Monday  
.
          IF        FORM4 > 1999
            MOVE      "2000",F4   * start from 2000
            MOVE       SIX,FSTDAY * first day was Saturday
          ENDIF
.
          COMPARE   F4,FORM4
          GOTO      YEARS999 IF LESS
          MOVE 	    ZERO,EXIT
.
YEARS100  COMPARE   F4,FORM4
          GOTO      YEARS999 IF EQUAL
.
          MOVE      F4,KEY4     * check if working year is a leap year
          UNPACK    KEY4,DIM2,KEY2
          MOVE      DIM2,CC
          MOVE      KEY2,YY
          MOVE      ZERO,LEAPYR
          ASSIGN    YY%FOUR,F1
          IF        F1 = 0
            IF        YY = 0
              ASSIGN    CC%FOUR,F1
              IF        F1 = 0
                MOVE      ONE,LEAPYR
              ENDIF
            ELSE
              MOVE       ONE,LEAPYR
            ENDIF 
          ENDIF
.
          ADD       ONE,FSTDAY
          ADD       LEAPYR,FSTDAY
.
	  IF        FSTDAY > 7
            SUB       SEVEN,FSTDAY
          ENDIF  
          ADD       ONE,F4
          GOTO      YEARS100
.
YEARS999  RETURN
