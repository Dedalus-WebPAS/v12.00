.******************************************************************************
.* System         : Patient/Billing/Medical Records                           *
.*                                                                            *
.* Include        : EXCLNEWB.PBL                                              *
.*                                                                            *
.* Function       : Check if patient is an excluded newborn                   *
.*                                                                            *
.* Requires       : Admission file record in context                          *
.*                                                                            *
.* Returns        : EXIT - 0 - Not an excluded newborn                        *
.*                         1 - Excluded newborn                               *
.*                                                                            *
.* Modifications  :                                                           *
.* V12.00.01 15.05.2025  DA SArkies        TSK 0955096                        *
.*            Updated for Alpha-Numeric Visit Number                          *
.******************************************************************************
.* V10.11.00  08/09/2017  Ebon Clements  TSK 0818097                          *
.*            Created include                                                 *
.******************************************************************************
EXNB0000  MATCH     SP70,ACLSS                   * Blank admission class
          GOTO      EXNB8000 IF EQUAL

          PACK      KEY5,ANSP,SP1,ACLSS,SP70
          CALL      RDCODE1                      * Read admission class
          BRANCH    OVRCD,EXNB8000
.
          MATCH     "B",TCINDC4                  * Newborn
          GOTO      EXNB8000 IF NOT EQUAL
.
          PACK      KEY30,AADMNO,SP70
          CALL      RDSTRAN2
EXNB1000  CALL      RDKTRAN2                     * Read trasnfer records
          BRANCH    OVRCD,EXNB9000
.
          MATCH     AADMNO,TADMN                 * Correct admission
          GOTO      EXNB9000 IF NOT EQUAL
.
          MATCH     SP70,TATYPE                  * Blank admission type
          GOTO      EXNB1000 IF EQUAL
.
          PACK      KEY5,ANSA,SP1,TATYPE,SP70
          CALL      RDCODE1                      * Read admission type
          BRANCH    OVRCD,EXNB1000
.
          MATCH     "X",TCINDC5                  * Exclude
          GOTO      EXNB8000 IF NOT EQUAL
.
          GOTO      EXNB1000                     * next record
.
EXNB8000  MOVE      ZERO,EXIT                    * Not excluded newborn
          GOTO      EXNB9999
.
EXNB9000  MOVE      ONE,EXIT                     * Excluded new born
          GOTO      EXNB9999
.
EXNB9999  RETURN
