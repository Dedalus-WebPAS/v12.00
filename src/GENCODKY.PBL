.******************************************************************************
.* System         : Patient Management                                        *
.*                                                                            *
.* Include Name   : GENCODKY.PBL                                              *
.*                                                                            *
.* Function       : To display and select a code from TEMPCODE                *
.*                                                                            *
.* Author         : Steve O'Gorman          19/01/1993                        *
.*                  Adapted from Lofty's NSM Routine                          *
.*                                                                            *
.* Parameters     : CKYIMAND                1=code is mandatory               *
.*                  BOXHDR                  header for box                    *
.*                  CCOL & CVERT            keyin position                    *
.*                  HLEF,HTOP,HRIG,HBOT     positions for outside box         *
.*                                                                            *
.* Returns        : EXIT = 0                continue                          *
.*                  EXIT = 1                exit selected                     *
.*                                                                            *
.* Modifications  :                                                           *
.******************************************************************************
          DEFPROC   GENCODKY
.
BCLEF     FORM      3         * box clear left
BCTOP     FORM      2         * box clear top 
BCRIG     FORM      3         * box clear right
BCBOT     FORM      2         * box clear bottom
CALLPOSN  FORM      1         * which bottom line prompt
CNTCODE   FORM      2         * count of codes displayed
CODES     DIM       38[19]    * array for holding codes
HLCOL     FORM      3         * highlight column
HLROW     FORM      2         * highlight row
MAXCODE   FORM      2         * maximum number of codes per screen
MINWDTH   INIT      "                                            "
PRMPTLN   FORM      2         * prompt line
SELCODE   FORM      2         * selected code
SRCHDESC  DIM       30        * search description
.
.*********************************************************************
.                   SPTCD000
.         Select Item From Patient Codes File
.         Para's  : HLEF,HTOP,HRIG,HBOT
.*********************************************************************
SPTCD000  CALL      GETSCR00
          CALL      PATDRBOX                * display the box
          CALL      CPTCD000                * clear codes file array
          ASSIGN    (HLEF+1),BCLEF          * box clear left
          ASSIGN    (HTOP+2),BCTOP          * box clear top
          ASSIGN    (HRIG-1),BCRIG          * box clear right
          ASSIGN    (HBOT-3),BCBOT          * box clear bottom
          ASSIGN    (BCBOT-BCTOP+1),MAXCODE * maximum number on screen
          ASSIGN    (HBOT-1),PRMPTLN        * set prompt line
.
          ASSIGN    (HTOP+1),EVERT          * set row
          MOVE      BCLEF,ECOL
          DISPLAY   *PECOL:EVERT,*V2LON,*ULON,"Item" 
          ADD       FIVE,ECOL
          DISPLAY   *PECOL:EVERT,*V2LON,*ULON,"Description         "
          ASSIGN    (HLEF+2),HLCOL          * set column to start display
.
          MOVE      SP30,SRCHDESC           * clear search desc
          MOVE      ONE,CPAGENO             * set page number
          MOVE      ZERO,CNTCODE            * clear counter
          PACK      KEY38,SP30
          CALL      RDSTEMP2
.
SPTCD100  CALL      RDKTEMP2
          BRANCH    OVRCD,SPTCD500          * end of file
.
          MATCH     SP20,SRCHDESC
          GOTO      SPTCD200 IF EQUAL       * display all codes
          MATCH     SRCHDESC,TEMPDESC
          GOTO      SPTCD100 IF NOT EQUAL   * description is not the same
.
SPTCD200  ADD       ONE,EVERT
          COMPARE   EVERT,BCBOT
          GOTO      SPTCD400 IF LESS        * need a new screen
          ADD       ONE,CNTCODE
.
SPTCD300  DISPLAY   *PHLCOL:EVERT,*V2LON,CNTCODE,*HOFF,". ",TEMPDESC
          PACK      CODES[CNTCODE],TEMPDESC,TEMPCODE
          GOTO      SPTCD100
.
. ------- need a new page -------
.
SPTCD400  BRANCH    CPAGENO,SPTCD450        * on first screen
.
SPTCD410  MOVE      ONE,CALLPOSN            * Next & Previous
          CALL      KPTCD000                * keyin item
          BRANCH    EXIT,SPTCD910,SPTCD700,SPTCD800,SPTCD100
.                        eXit     Next     preVious Search
          GOTO      SPTCD600
.
SPTCD450  MOVE      TWO,CALLPOSN            * Next
          CALL      KPTCD000                * keyin item
          BRANCH    EXIT,SPTCD910,SPTCD700,SPTCD590,SPTCD100
.                        eXit     Next     preVious Search
          GOTO      SPTCD600
.
. ------- end of data reached -------
.
SPTCD500  BRANCH    CPAGENO,SPTCD550        * on first screen
.
SPTCD510  MOVE      THREE,CALLPOSN          * Previous
          CALL      KPTCD000                * keyin item
          BRANCH    EXIT,SPTCD910,SPTCD590,SPTCD800,SPTCD100
.                        eXit     Next     preVious Search
          GOTO      SPTCD600
.
SPTCD550  MOVE      FOUR,CALLPOSN           * no options
          CALL      KPTCD000                * keyin item
          BRANCH    EXIT,SPTCD910,SPTCD590,SPTCD590,SPTCD100
.                        eXit     Next     preVious Search
          GOTO      SPTCD600
.
SPTCD590  BEEP
          BRANCH    CALLPOSN,SPTCD410,SPTCD450,SPTCD510,SPTCD550
.
. ------- item selected -------
.
SPTCD600  MOVE      CODES[SELCODE],KEY38
          CALL      RDTEMP2
          BRANCH    OVRCD,SPTCD590          * invalid code
.
          MOVE      ZERO,EXIT
          GOTO      SPTCD950
.
. ------- next screen chosen -------
.
SPTCD700  MOVE      CODES[MAXCODE],KEY38
          CALL      RDTEMP2
          CALL      RDKTEMP2
          BRANCH    OVRCD,SPTCD590          * no next screen
.
          CALL      CPTCD000                * clear variables
          BOXCLR    BCLEF,BCTOP,BCRIG,BCBOT
          MOVE      BCTOP,EVERT             * set row
          MOVE      ONE,CNTCODE             * set counter
          ADD       ONE,CPAGENO             * add to page number
          GOTO      SPTCD300
.
. ------- previous screen chosen -------
.
SPTCD800  MOVE      CODES[ONE],KEY38
          CALL      RDTEMP2
          MOVE      ONE,FORM2               * doing first read
.
SPTCD810  COMPARE   FORM2,MAXCODE
          GOTO      SPTCD820 IF LESS        * have read enough
.
          CALL      RDPTEMP2
          BRANCH    OVRCD,SPTCD820          * end of file
          ADD       ONE,FORM2
          GOTO      SPTCD810
.
SPTCD820  CALL      CPTCD000                * clear variables
          MOVE      BCTOP,EVERT             * set row
          MOVE      ONE,CNTCODE             * set counter
          SUB       ONE,CPAGENO             * sub from page number
          GOTO      SPTCD300
.
. ------- eXit selected -------
.
SPTCD910  BRANCH    CKYIMAND,SPTCD590       * code is mandatory
          CALL      PUTSCR00
          GOTO      SPTCD999
+
.*********************************************************************
.                   CCFA0000                    Called by : SPTCD000 
.         clear the codes file array
.*********************************************************************
CPTCD000  MOVE      ONE,FORM2
          WHILE     FORM2 <= 19
            PACK      CODES[FORM2],SP30,SP30
            ADD       ONE,FORM2
          DO
          MOVE      ONE,SELCODE             * default to item one
          RETURN
+
.*********************************************************************
.                   KPTCD000                    Called by : SPTCD000 
.         Keyin Item from screen
.*********************************************************************
KPTCD000  DISPLAY   *PBCLEF:PRMPTLN,MINWDTH,*PBCLEF:PRMPTLN;
          MOVE      HLEF,ECOL               * set column
          DISPLAY   "e",*V2LON,ANSX,*HOFF,"it, ",*V2LON,ANSS,*HOFF,"earch";
          ADD       "12",ECOL
          IF        CALLPOSN = 1 | CALLPOSN = 3
            DISPLAY   ", ",*V2LON,ANSU,*HOFF,"p";
            ADD       FOUR,ECOL
          ENDIF         
          IF        CALLPOSN = 1 | CALLPOSN = 2
            DISPLAY   ", ",*V2LON,ANSD,*HOFF,"own";
            ADD       SIX,ECOL
          ENDIF         
          DISPLAY   " : ";
          ADD       "4",ECOL
.
KPTCD100  ASSIGN    (SELCODE+1+HTOP),HLROW  * set highlight row
          DISPLAY   *PHLCOL:HLROW,*V2LON,*HON,SELCODE
          ASSIGN    (ECOL+3),FORM2          * make sure dont display in box
          KEYIN     *PECOL:PRMPTLN,*DV,SP2:
                    *PECOL:PRMPTLN,*NOEDIT,*V2LON,*JR,DIM2
          REP       UPPLOW,DIM2
          PACK      DIM2,DIM2,SP2
          UNPACK    DIM2,ANS,ANS
.
          COMPARE   ZERO,CNTCODE
          GOTO      KPTCD110 IF EQUAL       * limited options available
.
          GOTO      KPTCD300 IF UP          * UP arrow
          GOTO      KPTCD400 IF DOWN        * DOWN arrow
          GOTO      KPTCD920 IF PAGEDOWN    * PAGEDOWN entered
          GOTO      KPTCD930 IF PAGEUP      * PAGEUP entered
          GOTO      KPTCD150 IF LEFT        * LEFT arrow
          GOTO      KPTCD150 IF RIGHT       * RIGHT arrow
.
          TYPE      DIM2
          GOTO      KPTCD800 IF EQUAL       * number entered
.
          MATCH     SP1,ANS
          GOTO      KPTCD900 IF EQUAL       * return hit
          MATCH     ANSD,ANS
          GOTO      KPTCD920 IF EQUAL       * Down / next screen
          MATCH     ANSU,ANS
          GOTO      KPTCD930 IF EQUAL       * Up   / prev screen
.
KPTCD110  MATCH     ANSX,ANS
          GOTO      KPTCD910 IF EQUAL       * eXit
          MATCH     ANSS,ANS
          GOTO      KPTCD500 IF EQUAL       * Search
.
KPTCD150  BEEP
          GOTO      KPTCD100
.
. ------- UP entered -------
.
KPTCD300  DISPLAY   *PHLCOL:HLROW,*V2LON,SELCODE
          IF        SELCODE = ONE
            MOVE      CNTCODE,SELCODE         * set to last code displayed
          ELSE
            SUB       ONE,SELCODE             * go back one code
          ENDIF
          GOTO      KPTCD100
.
. ------- DOWN entered -------
.
KPTCD400  DISPLAY   *PHLCOL:HLROW,*V2LON,SELCODE
          IF        SELCODE = CNTCODE
            MOVE      ONE,SELCODE             * set to first code displayed
          ELSE
            ADD       ONE,SELCODE             * go forward one code
          ENDIF
          GOTO      KPTCD100
.
. ------- SEARCH entered -------
.
KPTCD500  KEYIN     *PBCLEF:PRMPTLN,*DV,MINWDTH:
                    *PBCLEF:PRMPTLN,"Description : ",*V2LON,SRCHDESC
          RESET     SRCHDESC
          GOTO      KPTCD000 IF EOS         * nothing entered
.
          CALL      CPTCD000                * clear storage array
          BOXCLR    BCLEF,BCTOP,BCRIG,BCBOT
          ASSIGN    HTOP+1,EVERT            * set row
          MOVE      ONE,CPAGENO             * set page number
          MOVE      ZERO,CNTCODE            * clear counter
          PACK      KEY38,SRCHDESC,SP30
          CALL      RDSTEMP2
          GOTO      KPTCD940
.
. ------- have entered a number -------
.
KPTCD800  MOVE      DIM2,FORM2
          COMPARE   FORM2,CNTCODE
          GOTO      KPTCD150 IF LESS        * number is too high
          COMPARE   FORM2,ZERO
          GOTO      KPTCD150 IF NOT LESS    * invalid
.
          ASSIGN    (SELCODE+1+HTOP),HLROW  * set de-highlight row
          DISPLAY   *PHLCOL:HLROW,*V2LON,SELCODE
          MOVE      FORM2,SELCODE
          ASSIGN    (SELCODE+1+HTOP),HLROW  * set highlight row
          DISPLAY   *PHLCOL:HLROW,*V2LON,*HON,SELCODE
.
. ------- set exit flags -------
.
KPTCD900  MOVE      ZERO,EXIT               * code chosen
          GOTO      KPTCD999
.
KPTCD910  MOVE      ONE,EXIT                * cancel/exit    
          GOTO      KPTCD999
.
KPTCD920  MOVE      TWO,EXIT                * Next screen / Page Down chosen
          GOTO      KPTCD999
.
KPTCD930  MOVE      THREE,EXIT              * Previous screen / Page Up chosen
          GOTO      KPTCD999
.
KPTCD940  MOVE      FOUR,EXIT               * Search chosen
.
KPTCD999  RETURN
+
.******************************************************************************
.* Function       : To draw a box for the new selection method.               *
.* Parameters     : HLEF,HTOP,HRIG,HBOT     positions for box                 *
.*                  BOXHDR                  heading to display on top line    *
.*                                                                            *
.******************************************************************************
PATDRBOX  SUSPEND
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,*V2LON,HLEF,HTOP,HRIG,HBOT
          ASSIGN    (HBOT-2),EVERT
          DISPLAY   *V2LON,*PHLEF:EVERT,*G33,*PHRIG:EVERT,*G34
          ASSIGN    (HLEF+1),ECOL
          ASSIGN    (HRIG-1),CCOL1
          HLINE     *G37,*V2LON,EVERT,ECOL,CCOL1
.
          ASSIGN    (HRIG-HLEF),CCITEM      * box width
          MOVE      ZERO,FORM2
          STRIP     BOXHDR
          MOVELPTR  BOXHDR,FORM2            * get the actual length of header
          ADD       TWO,FORM2               * allow for blanks each side
          IF        FORM2 <= CCITEM & FORM2 <> 2
            ASSIGN    (((CCITEM-FORM2)/2)+HLEF+1),ECOL   * display position
            DISPLAY   *V2LON,*HON,*PECOL:HTOP,SP1,*+,BOXHDR,*-,SP1
          ENDIF
.       
          FLUSH     HLEF,HTOP,HRIG,HBOT
          SETLPTR   BOXHDR
          RETURN
+
SPTCD950  STRIP     TEMPCODE
          CALL      PUTSCR00
          DISPLAY   *PCCOL:CVERT,*V2LON,TEMPDESC;
SPTCD999  ENDPROC
+
