.******************************************************************************
.*                                                                            *
.*  BOKHEAD.PBL    :  Include to display the standard 3 line header for a     *
.*                    given booking record.                                   *
.*                    This include is calls PMIHEAD to print the standard     *
.*                    2-line PMI heading.                                     *
.*                    It calls PATHEAD if the patient has an admission record.*
.*  DATA FILES     :  PATMA1FD    PMI master file                             *
.*                    PATMI1FD    Admissions file                             *
.*                    BOKRC1FD    Booking file                                *
.*                    PATDO1FD    Patient doctors file                        *
.*                    PATDHEAD    Special variables for this include          *
.*  PARAMETERS     :  CCNOTES     Case Notes Indicator (System Parameter)     *
.*                    A PATMA1FD record                                       *
.*                    A PATMI1FD record if past the booking status            *
.*                    DDATE      if the patient has been discharged           *
.*                    A BOKRC1FD record                                       *
.*  OPEN FILES     :  PATDO1A1    Patient doctors file                        *
.*                                                                            *
.*  MODIFICATIONS  :                                                          *
.*        V5.07.01  18/01/1999  Nicole Harrington 507 rebound 093             *
.*                  Changed to not include century in admission status date   *
.*        V5.07.00  12/10/1998  Jim Stathopoulos                              *
.*                  507 Changes                                               *
.******************************************************************************
.*        V6.00.00  24/12/1992  Graeme Williams                               *
.*                  Written for release 6                                     *
.*        V5.01.00  25/02/1993  i chung           SRF 119402                  *
.*                  Convert to release 5.01                                   *
.*                                                                            *
.******************************************************************************
BOKHEAD   COMPARE   ZERO,BKRESTAT
          GOTO      BOKHD100 IF EQUAL
          COMPARE   TWO,BKRESTAT
          GOTO      BOKHD100 IF EQUAL
          CALL      PATHEAD                 * use standard inpatient heading
          GOTO      BOKHD999
.
. -----   Initialise display variables  -----
.
BOKHD100  MOVE      SP30,PATHDADM
          MOVE      SP30,PATHDDOC
.
. -----  format doctor's name -----
.
BOKHD200  MOVE      BKREADOC,KEY6                    
          CALL      RDDOCT1
          BRANCH    OVRCD,BOKHD500
.
          MOVE      DSNAME,PATHDDOC                                      
          ENDSET    PATHDDOC
.
BOKHD300  CMATCH    SP1,PATHDDOC
          GOTO      BOKHD400 IF NOT EQUAL
          BUMP      PATHDDOC,-1
          GOTO      BOKHD300 IF NOT EOS
.
BOKHD400  LENSET    PATHDDOC
          APPEND    ", ",PATHDDOC
          APPEND    DGNAME,PATHDDOC
          RESET     PATHDDOC
.
. ----- get ward description -----
.
BOKHD500  MOVE      BKREWARD,PATHDWRD
          MOVE      BKREEBED,PATHDBED
.
          PACK      PATHDWB,PATHDWRD,PATHDBED
          MATCH     SP6,PATHDWB
          GOTO      BOKHD510 IF NOT EQUAL
          MOVE      SP7,PATHDWB             * No ward/bed allocated
          GOTO      BOKHD600
.
BOKHD510  MATCH     SP3,PATHDBED
          GOTO      BOKHD520 IF EQUAL
.
          PACK      PATHDWB,PATHDWRD,SLASH,PATHDBED
          GOTO      BOKHD600
.
BOKHD520  PACK      PATHDWB,PATHDWRD,SP4
.
. ----- format admission status -----
.
BOKHD600  CLEAR     PATHDADM
          APPEND    "(",PATHDADM
          APPEND    BKRETYPE,PATHDADM
.
BOKHD610  CMATCH    SP1,PATHDADM
          GOTO      BOKHD620 IF NOT EQUAL
          BUMP      PATHDADM,-1
          GOTO      BOKHD610 IF NOT EOS
.
BOKHD620  LENSET    PATHDADM
          APPEND    ")",PATHDADM
          RESET     PATHDADM
.
          BRANCH    BKRESTAT,BOKHD700,BOKHD710,BOKHD720,BOKHD730
.
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          ENDSET    PATHDADM
          APPEND    " Booked for ",PATHDADM
          APPEND    CPDATE,PATHDADM
          GOTO      BOKHD900
.
BOKHD700  ENDSET    PATHDADM
          APPEND    " Pre-admitted",PATHDADM
          GOTO      BOKHD900
.
BOKHD710  ENDSET    PATHDADM
          APPEND    " Cancelled",PATHDADM
          GOTO      BOKHD900
.
BOKHD720  ENDSET    PATHDADM
          APPEND    " Admitted",PATHDADM
          GOTO      BOKHD900
.
BOKHD730  ENDSET    PATHDADM
          APPEND    " Discharged",PATHDADM
.
BOKHD900  RESET     PATHDADM
          CALL      PMIHEAD                 * Display PMI details
          DISPLAY   *P1:5,"Booking No.  : ",*V2LON,BKREBOOK:
                    *P1:6,PATHDADM,*HOFF:
                    *P26:6,"Ward : ",*V2LON,PATHDWB,*HOFF:
                    *P42:6,"Doct : ",*V2LON,PATHDDOC;
.
BOKHD999  RETURN
+
