. *----------------------------------------------------------------------------
. * Include Name  :   VISACMCD.INC
. *
. * Requires      :   VISACMDF.PBL
. *                   VISACMTD.INC
. *                   VISACMFD.INC
. *
. * Function      :   Visit Based Comment Field Definitions for AN-SNAP
. *                    Web Based Processing
. *
. * Mods          :   V10.03.00 20/03/2012 Patrick Adair   CAR 257776
. *                             Created include
. *
. * CLRACM00 - Call from CLRPAR00 Routine to Initialise Comments Input File
. * SETACM00 - Call from SETPAR00 Routine when input parameter matches "vsamc"
. * UPACM000 - Call to Update Comments for Add or Update
. * ACMHTM00 - Call to Output Comments to HTML
. *-----------------------------------------------------------------------------
.
.-------------------------------------------------------------------------------
. Clear AN-SNAP Visit Comment Parameters
.-------------------------------------------------------------------------------
CLRACM00  CLEAR     VSACTFNM
          APPEND    "VISACM",VSACTFNM
          APPEND    PORT,VSACTFNM
          RESET     VSACTFNM
          REP       " 0",VSACTFNM
          PREP      VSACTFIL,VSACTFNM
          RETURN
.-------------------------------------------------------------------------------
. Set AN-SNAP Visit Comments Parameters
.-------------------------------------------------------------------------------
SETACM00  MOVE      ZERO,BLNKFLAG
          UNPACK    QRYNAME,KEY5,KEY3
          WRITE     VSACTFIL,SEQ;"%START:",KEY3
          PACK      QRYDATA,QRYDATA,SP70,SP70
          WRITE     VSACTFIL,SEQ;QRYDATA
.
SETACM10  READ      CONFFILE,SEQ;QRYNAME,QRYDATA * Read CONFFILE sequentially
          GOTO      SETACM99 IF OVER             * If end of file
          MATCH     SP70,QRYNAME                 * If next CGI parameter
          GOTO      SETACM80 IF NOT EQUAL
.
          PACK      QRYDATA,QRYDATA,SP70,SP70
.
          MATCH     SP70,QRYDATA
          IF        @EQUAL
            BRANCH    BLNKFLAG,SETACM10
            MOVE      ONE,BLNKFLAG               * Blank line yes
            SQUEEZE   QRYDATA
            GOTO      SETACM70                   * don't write second blank line
          ENDIF
          IF        @EOS
            BRANCH    BLNKFLAG,SETACM10          * don't write second blank line
            MOVE      ONE,BLNKFLAG               * Blank line yes
            SQUEEZE   QRYDATA
            GOTO      SETACM70
          ENDIF
.
          MOVE      ZERO,BLNKFLAG                * Blank line no
.
SETACM70  WRITE     VSACTFIL,SEQ;QRYDATA
          GOTO      SETACM10
.
SETACM80  MOVE      ONE,TEXTAREA                 * Flag for Next CGI Parameter
.
SETACM99  RETURN
.-------------------------------------------------------------------------------
. Add/Update AN-SNAP Visit Comments of a Particular Type
.-------------------------------------------------------------------------------
UPACM000  OPEN      VSACTFIL,VSACTFNM
UPACM100  READ      VSACTFIL,SEQ;VSACTLIN
          GOTO      UPACM999 IF OVER
.
          MATCH     "%START:",VSACTLIN
          IF        @EQUAL
            UNPACK    VSACTLIN,KEY7,VSACTTYP
            CALL      CLACM000
            MOVE      ZERO,VSACTCNT
            GOTO      UPACM100
          ENDIF
.
UPACM110  ADD       ONE,VSACTCNT
          PACK      KEY30,UPDATEKY,VSACTTYP,VSACTCNT,SP70
          UNPACK    KEY30,VSACVISN,VSACADAT,VSACATIM,VSACTYPE,VSACLINE
          CALL      RDVSACM1
          COMPARE   ZERO,OVRCD
          GOTO      UPACM110 IF EQUAL
.
          MOVE      VSACTLIN,VSACDATA
          CALL      WRVSACM1
.
          GOTO      UPACM100
.
UPACM999  RETURN
.-------------------------------------------------------------------------------
. Clear/Delete AN-SNAP Visit Comments of a Particular Type
.-------------------------------------------------------------------------------
CLACM000  PACK      KEY30,UPDATEKY,VSACTTYP,SP70
          CALL      RSVSACM1
CLACM100  CALL      RKVSACM1
          BRANCH    OVRCD,CLACM999
.
          PACK      SAVKEY27,VSACVISN,VSACADAT,VSACATIM,VSACTTYP
          MATCH     SAVKEY27,KEY30
          GOTO      CLACM999 IF NOT EQUAL
.
          PACK      KEY30,VSACVISN,VSACADAT,VSACATIM,VSACTYPE,VSACLINE,SP70
          CALL      DEVSACM1
.
          GOTO      CLACM000
.
CLACM999  RETURN
.-------------------------------------------------------------------------------
. Output AN-SNAP Visit Comments to HTMLFILE
.-------------------------------------------------------------------------------
ACMHTM00  UNPACK    DIM127,ANS,VSACTTYP,DIM127
          PACK      KEY30,UPDATEKY,VSACTTYP,SP70
          CALL      RSVSACM1
ACMHTM10  CALL      RKVSACM1
          BRANCH    OVRCD,ACMHTM99
.
          PACK      SAVKEY27,VSACVISN,VSACADAT,VSACATIM,VSACTTYP
          MATCH     SAVKEY27,KEY30
          GOTO      ACMHTM99 IF NOT EQUAL
.
          STRIP     VSACDATA
          MOVELPTR  VSACDATA,LENGTH
.
          COMPARE   LENGTH,ZERO
          IF        @EQUAL
            MOVE      ONE,LENGTH
          ENDIF
.
          SFORMAT   VAR,LENGTH
          MOVE      VSACDATA,VAR
          WRITE     HTMLFILE;VAR
          SFORMAT   VAR,127
.
          GOTO      ACMHTM10
.
ACMHTM99  RETURN
.
