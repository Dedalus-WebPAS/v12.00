.PATSBTYP.PBL
.
         DEFPROC    PATSBTYP
.
         INC        PATTRNFD/INC
.
.******************************************************************************
.        PATSBTYP : Calculate the number of bed days for a specialty bed type
.                   indicator 6 category BT
.
.        Parameter: SAVIND6 - selected bed type indicator
.                   TODATE  - ending date
.        Return   : NBRDAYS - Number of bed days
.
.        Modifications :
.         V12.00.01 20/05/2025  Ebon Clements   TSK 0955096
.                   Alphanueric visit number changes
.         V9.01.00  12/08/2002 J.Tan        
.                   Ported from 5.10  
.
.*******************************************************************************
DBTP0000 OPEN       PATTRAN2,"pattranf"
         MOVE       SP10,WDATE1             * initialise the start date
         MOVE       SP10,WDATE2             * initialise the end date
         MOVE       ZERO,NBRDAYS
.
         COMPARE    THREE,ASTAT             * discharged ?
         GOTO       DBTP1000 IF NOT EQUAL   * no.
.
         MATCH      ADATE,DDATE             * day case patient ?
         GOTO       DBTP1000 IF NOT EQUAL   * no.
.
         PACK       KEY30,AADMNO,Z70
         CALL       RDSTRAN2
         CALL       RDPTRAN2
         BRANCH     OVRCD,DBTP9000
.
         MATCH      TADMN,AADMNO            * same admission ?
         GOTO       DBTP9000 IF NOT EQUAL  * No.
.
         PACK       KEY5,ANSB,ANST,TRBTYP
         CALL       RDCODE1
         BRANCH     OVRCD,DBTP9000
         MATCH      SAVIND6,TCINDC6         * same bed type indicator ?
         GOTO       DBTP9000 IF NOT EQUAL
.
         MATCH      ADATE,TODATE
         GOTO       DBTP9000 IF LESS
.
         MOVE       ONE,NBRDAYS             * no of bed days is one for daycase
         GOTO       DBTP9000
.
DBTP1000 PACK       KEY30,AADMNO,SP20,SP10
         CALL       RDSTRAN2
DBTP1200 CALL       RDKTRAN2
         BRANCH     OVRCD,DBTP4000
.
         MATCH      TADMN,AADMNO            * same admission ?
         GOTO       DBTP4000 IF NOT EQUAL
.
         REP        " 0",TDATE
         MOVE       TDATE,STDATE
         MATCH      ANSA,TMOVE              * Admission record ?
         GOTO       DBTP2000 IF EQUAL
.
         MATCH      ANSR,TMOVE              * Return record ?
         GOTO       DBTP2000 IF EQUAL
.
         CMATCH     ANSC,TMOVE              * Change record ?
         GOTO       DBTP2200 IF EQUAL
         GOTO       DBTP3000
.
.        Admission/Return record
.
DBTP2000 
         MATCH      TDATE,TODATE
         GOTO       DBTP9000 IF LESS
         PACK       KEY5,ANSB,ANST,TRBTYP
         CALL       RDCODE1
         BRANCH     OVRCD,DBTP1200
         MATCH      SAVIND6,TCINDC6         * same bed type indicator ?
         GOTO       DBTP1200 IF NOT EQUAL
         MOVE       TDATE,WDATE1            * set from date
         GOTO       DBTP1200                * get next trans record
.
.        Change
.
DBTP2200 
         MATCH      TDATE,TODATE
         IF         @LESS
           MATCH      SP6,WDATE1              * Check with the fromdate
           GOTO       DBTP9000 IF EQUAL
           MOVE       TODATE,WDATE2           * set to date
           CALL       SBDY0000                * Calculate bed days
           GOTO       DBTP9000
         ENDIF
.
         PACK       KEY5,ANSB,ANST,TRBTYP
         CALL       RDCODE1
         BRANCH     OVRCD,DBTP2400
         MATCH      SAVIND6,TCINDC6         * check if it's the selected bedtype
         GOTO       DBTP2400 IF NOT EQUAL
.
.        Change to the selected bed type
.
         MATCH      SP8,WDATE1            * check if it was a real change of
         GOTO       DBTP1200 IF NOT EQUAL   * bed type
.
         MOVE       TDATE,WDATE1          * set fromdate of the change bedtype
         GOTO       DBTP1200
.
.        Change to a different bed type, check if it was from selected bedtype
.
DBTP2400 MATCH      SP8,WDATE1           
         GOTO       DBTP1200 IF EQUAL
.
         MOVE       TDATE,WDATE2
         CALL       SBDY0000                * Calculate bed days
         GOTO       DBTP1200                * get next trans record
.
.        Discharge/Leave record always on the same bed type as previous record
.
DBTP3000 MATCH      SP8,WDATE1
         GOTO       DBTP3200 IF EQUAL       * Was not on selected bed type
.
         MATCH      TDATE,TODATE
         IF         @LESS
           MOVE       TODATE,WDATE2         * set to end date
         ELSE
           MOVE       TDATE,WDATE2          * Set to date
         ENDIF
         REP        " 0",WDATE2
         CALL       SBDY0000                * Calculate bed days
.
DBTP3200 CMATCH     "D",TMOVE
         GOTO       DBTP1200 IF NOT EQUAL   * get next record
         GOTO       DBTP9000
.
.        check the last record
.
DBTP4000 MATCH      SP8,WDATE1
         GOTO       DBTP9000 IF EQUAL       * Was not on selected bed type
.
         MOVE       TODATE,WDATE2
         REP        " 0",WDATE2
         CALL       SBDY0000                * Calculate bed days
         GOTO       DBTP9000
.
DBTP9000 CLOSE      PATTRAN2
         GOTO       DBTP9999
.
         INC       IBAOVRIO/INC
         INC       PATTRNIO/INC
.
DBTP9999 ENDPROC
+
.******************************************************************************
.        SBDY0000 - Calculate dates difference
.******************************************************************************
SBDY0000 PACK       CDYSFDTE,WDATE1
         PACK       CDYSTDTE,WDATE2
         CALL       CALCDAYS
         MOVE       CDYSDAYS,CALDAYS
         SUB        ONE,CALDAYS
         ADD        CALDAYS,NBRDAYS
         MOVE       SP10,WDATE1
         MOVE       SP10,WDATE2
SBDY9999 RETURN
+
