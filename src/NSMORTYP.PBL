.  ********************************************************************
.  *                                                                  *
.  *     Include Name   : NSMORTYP                                    *
.  *                                                                  *
.  *     Function       : Provides two routines to handle order       *
.  *                      type descriptions.                          *
.  *                                                                  *
.  *                      NSMORDES                                    *
.  *                         requires - NSORTYPE                      *
.  *                         returns  - NSORDESC (the description)    *
.  *                                    OVRCD                         *
.  *                                                                  *
.  *                      NSMORTDS    - question mark routine for     *
.  *                                    order type descriptions.      *
.  *                                                                  *
.  *                                                                  *
.  *                                                                  *
.  *     Programmer     : Peter Eddey  15/04/90                       *
.  *                                                                  *
.  *     Modifications  : V5.01.002 15/10/1991 Graeme Williams        *
.  *                                Changed for redesign              *
.  *                                                                  *
.  ********************************************************************
.
.-----  routine to return a order type descrition  -----
.
NSMORDES  COMPARE   NSORTYPE,NSORTNUM    * test if <= number of types
          GOTO      NSDES950 IF LESS
.
          COMPARE   ZERO,NSORTYPE        *  test > zero
          GOTO      NSDES950 IF LESS
.
.-----  load the appropriate description -----
.  
          MOVE      NSORTY00,NSORDESC
          LOAD      NSORDESC,NSORTYPE,NSORTY01,NSORTY02,NSORTY03,NSORTY04:
                             NSORTY05,NSORTY06,NSORTY07,NSORTY08,NSORTY09:
                             NSORTY10,NSORTY11,NSORTY12,NSORTY13,NSORTY14:
                             NSORTY15,NSORTY16,NSORTY17,NSORTY18,NSORTY19:
                             NSORTY20,NSORTY21,NSORTY22,NSORTY23,NSORTY24:
                             NSORTY25,NSORTY26,NSORTY27,NSORTY28,NSORTY29:
                             NSORTY30,NSORTY31,NSORTY32,NSORTY33,NSORTY34:
                             NSORTY35,NSORTY36,NSORTY37,NSORTY38,NSORTY39:
                             NSORTY40
.
          RESET     NSORDESC
          MOVE      ZERO,OVRCD
          GOTO      NSDES999
. 
NSDES950  MOVE      ONE,OVRCD
          MOVE      "Invalid",NSORDESC
.
NSDES999  RETURN     
.
.
.**********************************************************************
.*                          NSMORTDS                                  *
.*                                                                    *
.*          Question mark routine for order  type description         *
.**********************************************************************

NSMORTDS  
          MOVE      NSORTYPE,NSORTTMP    * store current order type to
.                                          temporary variable
          DISPLAY   *P1:3,*EF
          DISPLAY   *P25:6,*V2LON,*ULON,"Order Type Codes & Descriptions",*HOFF
.
NSORT050  MOVE      ZERO,NSORTSCR        * set screen number
          MOVE      "-1",NSORTYPE        * set order type
.
NSORT100
          DISPLAY   *P1:7,*EF           
          MOVE      ONE,NSORTCOL         * set column
          MOVE      SEVEN,NSORTROW       * set row
          MOVE      ZERO,NSORTFLG        * set screen full flag
          ADD       ONE,NSORTSCR         * increment the screen number
.
NSORT200
          ADD       ONE,NSORTROW         * increment the row          
NSORT250  ADD       ONE,NSORTYPE         * increment the order type   
. 
          CALL      NSMORDES             * get the description
          BRANCH    OVRCD,NSORT400
.
.
          DISPLAY   *PNSORTCOL:NSORTROW,*V2LON,NSORTYPE,*HOFF,".  ":
                    NSORDESC
.
          COMPARE   TWENTY2,NSORTROW    * test if column is full
          GOTO      NSORT300 IF EQUAL
.
          GOTO      NSORT200
.
NSORT300
          BRANCH    NSORTFLG,NSORT500   * if screen is full
          MOVE      ONE,NSORTFLG        * set screen full flag
          MOVE      SEVEN,NSORTROW      * reset row
          MOVE      FORTY,NSORTCOL      * reset column
          GOTO      NSORT200
.
.-----  the following controls the display of prompts ---
.-----  depending on the number of screens or if their --
.-----  is another screen etc.                        ---
.
NSORT400
          COMPARE   ONE,NSORTSCR
          GOTO      NSORT999 IF EQUAL
.
          DISPLAY   *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst screen ? "
NSORT450
          KEYIN     *P30:24,*DV,UNDLN1:
                    *P30:24,*V2LON,ANS:
                    *P30:24,*DV,ANS
          REP       UPPLOW,ANS
.
          MATCH     ANS,ANSC
          GOTO      NSORT999 IF EQUAL
.
          MATCH     ANS,ANSF
          GOTO      NSORT050 IF EQUAL
.
          BEEP
          GOTO      NSORT450
.
NSORT500
.
.----  test if their is a "next" screen            -----
.
          ADD       ONE,NSORTYPE
          CALL      NSMORDES
          SUBTRACT  ONE,NSORTYPE
          BRANCH    OVRCD,NSORT400
.
          COMPARE   ONE,NSORTSCR
          GOTO      NSORT600 IF EQUAL
.
          DISPLAY   *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst screen, (",*V2LON,"N":
                    *HOFF,")ext screen ? "
NSORT550
          KEYIN     *P45:24,*DV,UNDLN1:
                    *P45:24,*V2LON,ANS:
                    *P45:24,*DV,ANS
          REP       UPPLOW,ANS
.
          MATCH     ANS,ANSC
          GOTO      NSORT999 IF EQUAL
.
          MATCH     ANS,ANSF
          GOTO      NSORT050 IF EQUAL
.
          MATCH     ANS,ANSN
          GOTO      NSORT100 IF EQUAL
.
          BEEP                
          GOTO      NSORT550
.  
NSORT600
          DISPLAY   *P1:24,*EL,"(",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"N",*HOFF,")ext screen ? "
NSORT650
          KEYIN     *P29:24,*DV,UNDLN1:
                    *P29:24,*V2LON,ANS:
                    *P29:24,*DV,ANS
          REP       UPPLOW,ANS
.
          MATCH     ANS,ANSC
          GOTO      NSORT999 IF EQUAL
.
          MATCH     ANS,ANSN
          GOTO      NSORT100 IF EQUAL
.
          BEEP                
          GOTO      NSORT650
.
NSORT999
          MOVE      NSORTTMP,NSORTYPE    * restore the original order
.                                          type
          RETURN
