.------------------------------------------------------------
. Include Name  : PMSCURTH.PBL 
.
. Function      : Update the current patient file theatre status
.                 and location fields for an admissions theatre
.                 booking
.----------------------------------------------------------------------
. Modifications
. V11.00.00  23/04/2020  Ebon Clements  Task 0850105
.            Created include
.------------------------------------------------------------
          DEFPROC   PMSCURTH
.
          INC       OPRCONFD/INC
          INC       OPRDEAFD/INC
          INC       OPRARDFD/INC
          INC       OPRSRGFD/INC
          INC       PMSCURFD/INC
.
BOKFOUND  FORM      1                        * Non cancelled booking found
CANCLOOP  FORM      1                        * Ccncelled booking loop
CURRDATE  DIM       8
THETSTAT  DIM       2                        * Theatre status
THETLOCN  DIM       6                        * Theatre location
.
PMCURT00  READ      CONTROLF,HUND13;*67,OPCNTHED
          MATCH     "1",OPCNTHED             * Display theatre status in
          GOTO      PMCURT99 IF NOT EQUAL    * the patient header
.
          OPEN      OPRDETA2,"oprdetaf"
          OPEN      OPRARDA1,"oprardaf"
          OPEN      OPRSRGA1,"oprsrgaf"
          OPEN      PMSCURA1,"pmscuraf"
.
          MOVE      SP70,THETSTAT            * Clear theatre status
          MOVE      SP70,THETLOCN            * Clear theatre location
          MOVE      ZERO,BOKFOUND            * Clear Non cancelled booking found
          MOVE      ZERO,CANCLOOP            * Not cancelled booking loop
.
          CALL      IBACLOCK
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE            * Current date
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDPMCUR1                 * Exit no a current patient
          BRANCH    OVRCD,PMCURT90
.
          PACK      KEY31,ADMISSNO,FOUR,CURRDATE,Z70
          CALL      RSOPDEA2
PMCURT10  CALL      RPOPDEA2
          BRANCH    OVRCD,PMCURT80
.
          MATCH     OPDAADMN,ADMISSNO            * Same admission ?
          GOTO      PMCURT80 IF NOT EQUAL
.
          IF        CANCLOOP=1
            COMPARE   THREE,OPDASTAT               * Cancelled ?
            GOTO      PMCURT90 IF NOT EQUAL         * Yes
.     
            MATCH     CURRDATE,OPDADATE            * Today's date ?
            GOTO      PMCURT90 IF NOT EQUAL
.
            MOVE      "SC",CATEGORY
            MOVE      OPDACANC,CODE
            CALL      GETCOD00
            MATCH     ANST,TCINDC3
            IF        @EQUAL
              MOVE      "12",THETSTAT              * Transferred
            ELSE
              MOVE      "13",THETSTAT              * Cancelled
            ENDIF
            MOVE      ONE,BOKFOUND                 * Booking found
            GOTO      PMCURT80
          ELSE
            COMPARE   THREE,OPDASTAT               * Cancelled ?
            GOTO      PMCURT10 IF EQUAL            * Yes
.
            MATCH     CURRDATE,OPDADATE            * Today's date ?
            GOTO      PMCURT10 IF NOT EQUAL
.
            MOVE      ONE,BOKFOUND                 * Booking found
          ENDIF
.
          MOVE      OPDAUNIQ,KEY10
          CALL      RDOPARD1                     * Read arrival recovery
          BRANCH    OVRCD,PMCURT80
.
          PACK      KEY11,OPDAUNIQ,ONE,SP70
          CALL      RDOPSRG1                     * Read surgical details
          IF        OVRCD=1
            CALL      CLOPRSRG
          ENDIF
.
          MATCH     SP70,OPARATIM
          IF        !@EQUAL
            MATCH     "1",OPSGUY05
            IF        @EQUAL
              MOVE      "04",THETSTAT       * Abandoned
            ELSE
              MOVE      "01",THETSTAT       * Holding
            ENDIF
          ENDIF
.
          MATCH     SP70,OPARANAS
          IF        !@EQUAL
            MATCH     "1",OPSGUY05
            IF        @EQUAL
              MOVE      "04",THETSTAT       * Abandoned
            ELSE
              MOVE      "02",THETSTAT       * Anaes
            ENDIF
          ENDIF
.
          MATCH     SP70,OPSGTIPS
          IF        !@EQUAL
            MATCH     "1",OPSGUY05
            IF        @EQUAL
              MOVE      "04",THETSTAT       * Abandoned
            ELSE
              MOVE      "03",THETSTAT       * Theatre
            ENDIF
          ENDIF
.
          MATCH     SP70,OPSGTISS
          IF        !@EQUAL
            MATCH     "1",OPSGUY05
            IF        @EQUAL
              MOVE      "04",THETSTAT       * Abandoned
            ELSE
              MOVE      "03",THETSTAT       * Theatre
            ENDIF
          ENDIF
.
          MATCH     SP70,OPARTIRF
          IF        !@EQUAL
            MOVE      "05",THETSTAT         * Recovery-Front
          ENDIF
.
          MATCH     SP70,OPARTIRB
          IF        !@EQUAL
            MOVE      "06",THETSTAT         * Recovery-Back
          ENDIF
.
          MATCH     SP70,OPARTIRD
          IF        !@EQUAL
            MOVE      "07",THETSTAT         * Recovery-Day 
          ENDIF
.
          MATCH     SP70,OPARTIDP
          IF        !@EQUAL
            MOVE      "08",THETSTAT         * Ready To Depart
          ENDIF
.
          MATCH     SP70,OPARTETC
          IF        !@EQUAL
            MOVE      "09",THETSTAT         * Exit
          ENDIF
.
          MATCH     SP70,OPARTICU
          IF        !@EQUAL
            MOVE      "10",THETSTAT         * ICU
          ENDIF
.
          MATCH     SP70,OPARTEXP
          IF        !@EQUAL
            MOVE      "11",THETSTAT         *  Yes, set to "Deceased".
          ENDIF
.
PMCURT80  MATCH     SP70,THETSTAT
          IF        !@EQUAL
            MOVE      OPDATHET,THETLOCN     * Save theatre location
          ENDIF
.
          PACK      KEY8,ADMISSNO,SP70
          CALL      RDPMCUR1                * Read current patient file 
          IF        OVRCD=0
            MOVE      THETSTAT,PMCUTSTA     * New Theatre Status
            MOVE      THETLOCN,PMCUTLOC     * New Theatre Location
            CALL      UPPMCUR1
          ELSE
            GOTO    PMCURT90                * Exit record locked or not found 
          ENDIF
.
          BRANCH    BOKFOUND,PMCURT90        * theatre booking found
          BRANCH    CANCLOOP,PMCURT90         * Finished
.
          MOVE      ONE,CANCLOOP              * Check for cancelled bookin
.
          PACK      KEY31,ADMISSNO,THREE,CURRDATE,Z70
          CALL      RSOPDEA2                 * Check for a can
          GOTO      PMCURT10
.
PMCURT90  CLOSE     OPRDETA2
          CLOSE     OPRARDA1
          CLOSE     OPRSRGA1
          CLOSE     PMSCURA1
          GOTO      PMCURT99
.
          INC       CLOPRSRG
.
          INC       OPRDEAIO/INC
          INC       OPRARDIO/INC
          INC       OPRSRGIO/INC
          INC       PMSCURIO/INC
          INC       IBAOVRIO/INC
.
PMCURT99  ENDPROC

