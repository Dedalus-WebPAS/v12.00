.******************************************************************************
.*  File          :  PAT107MN.PBL                                             *
.*                                                                            *
.*  Function      :  ICD10 Ed.7 Coding Maintenance Routines for PATWEB88      *
.*                                                                            *
.*  Parameters    :                                                           *
.*                                                                            *
.*  Modifications :                                                           *
.*                                                                            *
.         V11.02.01 19/07/2022  Bella Turco    TSK 0921957                    *
.                   Added NOMORE00 - Display "No more records" at end of list *
.                   Added PTCNNRTD - Displays number of rows set by parameter *
.                   value                                                     *
.         V10.03.01 22/11/2011  Mike Laming    CAR 253821                     *
.                   Added ICD10EDN variable to pass Edition no. to PATWEB88   *
.         V10.00.00 06/04/2010  Mike Laming     CAR 219246   HDP 2010         *
.                   Created - cloned from PAT106MN.PBL and then all the "6"s  *
.                   changed to "7"s                                           *
.******************************************************************************
.
.------------------------------------------------------------------------------
. ICD10 Edition 7 Diagnosis Coding Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------------------------
ICD7DI00  RESET     PTICD001
          MOVE      "7",ICD10EDN
.                                           * make sure files are open
          OPEN      PATI17D1,"pat10d7f"     * Open ICD10 Ed.7 Diseases
          OPEN      PATK7DA1,"patkcd7f"     * ICD10 Ed.7 Key Word Diag.
          OPEN      PATK7DA2,"patkcd7f"     * ICD10 Ed.7 Key Word Diag.
.
          RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      ICD7DI40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      ICD7DI10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      ICD7DI20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      ICD7DI30 IF EQUAL
.
          MATCH     "V",UPDTTYPE    * Validate Formaction
          IF        @EQUAL
            CALL      VAL7CD00
            GOTO      ICD7DI99
          ENDIF
.
          GOTO      ICD7DI93
.
ICD7DI10  CALL      CHKDIS00  * Checks mandatory fields & checks for blank
          BRANCH    EXIT,ICD7DI99
          PACK      KEY9,PTICD001,SP70  * ADD FORMACTION
          CALL      RDPT17D1
          COMPARE   ZERO,OVRCD
          GOTO      ICD7DI92 IF EQUAL
          CALL      DISSAV00      * Moves input variables to file variables
          CALL      WRPT17D1      * Writes away the data
          PROC      PATK7DUP      * Update Keyword table
.
          CALL      ADDCAN00      * Add Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICD7DI99
.
ICD7DI20  PACK      KEY9,PTICD001,SP70  * UPDATE FORMACTION
          CALL      RDPT17D1
          BRANCH    OVRCD,ICD7DI91
          CALL      CHKDIS00      * Checks mandatory fields
          BRANCH    EXIT,ICD7DI99
          CALL      DISSAV00      * Moves input variables to file variables
          CALL      UPPT17D1      * Writes away the data
          PROC      PATK7DUP      * Update Key word Table
.
          CALL      UPDCAN00      * Update Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICD7DI99
.
ICD7DI30  PACK      KEY9,PTICD001,SP70   * DELETE FORMACTION
          CALL      RDPT17D1
          BRANCH    OVRCD,ICD7DI91
          CALL      DEPT17D1      * Deletes the data
          PROC      PATK7DUP      * Update Key word Table
.
          CALL      DELCAN00      * Delete Cancer Code Record
.
          MOVE      ZERO,EXIT
          GOTO      ICD7DI99
.
ICD7DI40  PACK      KEY9,PTICD001,SP70
          CALL      RDPT17D1
          IF        OVRCD=1
            CALL      CLRDIS00    * Clear all the file variables
          ENDIF
.
          MOVE      ONE,CANCERCD
          PACK      KEY9,PTICD001,SP70
          CALL      RDPTCAN1
          IF        OVRCD=1
            MOVE     ZERO,CANCERCD
            MOVE     ZERO,PTCCLATR
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "ICD10 Edition 7 Diagnosis Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ICD7DI99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      ICD7DI99
.
ICD7DI91  MOVE      "ICD10 Edition 7 Diagnosis Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD7DI99
.
ICD7DI92  MOVE      "ICD10 Edition 7 Diagnosis Code exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD7DI99
.
ICD7DI93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD7DI99
.
ICD7DI99  RETURN
+
.------------------------------------------------------------------------------
. ICD10 Edition 7 Operation Coding Maintenance: Display,Add,Update,Delete
.------------------------------------------------------------------------------
ICD7OP00  RESET     PTICO001
          MOVE      "7",ICD10EDN
.
          OPEN      PATI17O1,"pat10o7f"     * Open ICD10 Ed.7 Operations
          OPEN      PATK7OA1,"patkco7f"     * ICD10 Ed.7 Key Word Proc.
          OPEN      PATK7OA2,"patkco7f"     * ICD10 Ed.7 Key Word Proc.
.
          RESET     UPDTTYPE
          MATCH     SP1,UPDTTYPE
          GOTO      ICD7OP40 IF EQUAL
.
          MATCH     "A",UPDTTYPE    * Add formaction
          GOTO      ICD7OP10 IF EQUAL
.
          MATCH     "U",UPDTTYPE    * Update formaction
          GOTO      ICD7OP20 IF EQUAL
.
          MATCH     "D",UPDTTYPE    * Delete formaction
          GOTO      ICD7OP30 IF EQUAL
.
          MATCH     "V",UPDTTYPE    * Validate formaction
          IF        @EQUAL
            CALL      VAL7CD00
            GOTO      ICD7OP99
          ENDIF
.
          GOTO      ICD7OP93
.
ICD7OP10  CALL      CHKICD00  * Checks mandatory fields & checks for blank
          BRANCH    EXIT,ICD7OP99
          PACK      KEY9,PTICO001,SP70  * ADD FORMACTION
          CALL      RDPT17O1
          COMPARE   ZERO,OVRCD
          GOTO      ICD7OP92 IF EQUAL
          CALL      ICDSAV00      * Moves input variables to file variables
          CALL      WRPT17O1      * Writes away the data
          PROC      PATK7OUP      * Update Keyword table
          MOVE      ZERO,EXIT
          GOTO      ICD7OP99
.
ICD7OP20  PACK      KEY9,PTICO001,SP70  * UPDATE FORMACTION
          CALL      RDPT17O1
          BRANCH    OVRCD,ICD7OP91
          CALL      CHKICD00      * Checks mandatory fields
          BRANCH    EXIT,ICD7OP99
          CALL      ICDSAV00      * Moves input variables to file variables
          CALL      UPPT17O1      * Writes away the data
          PROC      PATK7OUP      * Update Key word Table
          MOVE      ZERO,EXIT
          GOTO      ICD7OP99
.
ICD7OP30  PACK      KEY9,PTICO001,SP70   * DELETE FORMACTION
          CALL      RDPT17O1
          BRANCH    OVRCD,ICD7OP91
          CALL      DEPT17O1      * Deletes the data
          PROC      PATK7OUP      * Update Key word Table
          MOVE      ZERO,EXIT
          GOTO      ICD7OP99
.
ICD7OP40  PACK      KEY9,PTICO001,SP70
          CALL      RDPT17O1
          IF        OVRCD=1
            CALL      CLRICD00    * Clear all the file variables
          ENDIF
.
          CLEAR     WEBTITLE
          MOVE      "ICD10 Edition 7 Procedure Maintenance",WEBTITLE
          RESET     WEBTITLE
.
          CALL      WEBHED00   * Create Output File and Write HTML Page Header
          BRANCH    EXIT,ICD7OP99
          CALL      WEBBOD00   * Process Web Body
          CALL      WEBEND00   * Process End of HTML File
          MOVE      ONE,EXIT
          GOTO      ICD7OP99
.
ICD7OP91  MOVE      "ICD10 Edition 7 Procedure Code does not exist",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD7OP99
.
ICD7OP92  MOVE      "ICD10 Edition 7 Procedure Code exists",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD7OP99
.
ICD7OP93  MOVE      "Invalid Form Action.",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      ICD7OP99
.
ICD7OP99  RETURN
+
.------------------------------------------------------------------------------
. Online ICD10 Edition 7 Code Validation
.------------------------------------------------------------------------------
VAL7CD00  MOVE      SP70,VALDNAME
          MATCH     SP70,VALDCODE
          GOTO      VAL7CD90 IF EQUAL
.
          MOVE      "Invalid Code Entered",VALDNAME
          BRANCH    VALDTYPE,VAL7CD10,VAL7CD20
          MOVE      "Invalid Validation Type",VALDNAME
          GOTO      VAL7CD90
.
VAL7CD10  MOVE      VALDCODE,KEY9
          CALL      RDPT17O1
          IF        OVRCD=0
            MOVE      PT0ODESC,VALDNAME
          ENDIF
          GOTO      VAL7CD90
.
VAL7CD20  MOVE      VALDCODE,KEY9
          CALL      RDPT17D1
          IF        OVRCD=0
            MOVE      PT0DDESC,VALDNAME
          ENDIF
          GOTO      VAL7CD90
.
VAL7CD90  CALL      OPENHTML
          BRANCH    EXIT,VAL7CD99
          WRITE     HTMLFILE;"<h3 align=center>Checking Database</h3>":
                             "<script type=#"text/javascript#">":
                             "{ opener.ValidateName.value=#"",VALDNAME,"#";":
                             "  opener.ValidateCode.value=#"",VALDCODE,"#";"
.
          MATCH     "Invalid",VALDNAME
          IF        @EQUAL
            WRITE     HTMLFILE;"  alert(#"Invalid Code Entered#");";
          ENDIF
.
          WRITE     HTMLFILE;"  close(); } </script>"
          CALL      CLOSHTML
.
VAL7CD99  RETURN
+
.-----------------------------------------------------------
. ICD10 Edition 7 Procedure Code: Search Table
.-----------------------------------------------------------
SPT7OA00  WRITE     HTMLFILE;"<tr align=center>":
                             "<td class=HeadingCell colspan=2>":
                             "<b>You Searched for ":
                               KEYWORDS,"</td>":
                             "</tr>":
                             "<tr>":
                             "<td class=HeadingCell width=15%><b>Code</b></td>":
                             "<td class=HeadingCell><b>Description</b></td>":
                             "</tr>"
.
          CALL      GETWRD00
.
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD       * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
....      UNPACK    PTCNGDV7,KEY4,KEY5
....      MATCH     "9999",KEY4          * is ICD10 Ed.7 in use yet?
....      GOTO      SPT7OA90 IF EQUAL
.
          STRIP     KEYWRD01
          PACK      KEY24,KEYWRD01,SP70
          CALL      RSPT7O2
SPT7OA10  CALL      RKPT7O2
          BRANCH    OVRCD,SPT7OA90
.
          CALL      SPT7OB00
          BRANCH    OVRCD,SPT7OA10,SPT7OA90
.
          MOVE      PTCOITM,KEY9
          CALL      RDPT17O1
          BRANCH    OVRCD,SPT7OA10
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      SPT7OA10
          ENDIF
.
          BRANCH    LNK2PRT,SPT7OA20,SPT7OA30
          GOTO      SPT7OA99
.
SPT7OA20  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:showICD02":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&ptico001=",OPCODE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             OPCODE,"</td>":
                             "<td>",PT0ODESC,"</td>":
                             "</tr>"
.
          GOTO      SPT7OA80
.
SPT7OA30  WRITE     HTMLFILE;"<tr valign=top>":
                              "<td><a href=#"#" ":
                              "OnClick=#"updateParent('",PT0ODESC:
                              "','",OPCODE,"')#">":
                              "<img src=../images/MaintenanceIcon.gif hspace=4":
                              " border=0 align=absmiddle></a>":
                              OPCODE,"</td>":
                              "<td>",PT0ODESC,"</td>":
                             "</tr>"
.
SPT7OA80  ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      SPT7OA10 IF NOT EQUAL
          GOTO      SPT7OA99
.
SPT7OA90  CALL      NOMORE00       * Display No More Records Message
.
SPT7OA99  RETURN
.------------------------------------------------------------
. Check ICD10 Edition 7 Procedure Record
.------------------------------------------------------------
SPT7OB00  MATCH     KEYWRD01,PTCOKWD
          GOTO      SPT7OB92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY24,PTCOKWD,PTCOITM
            CALL      SPT7OC00
            BRANCH    EXIT,SPT7OB91
            MOVE      SAVKEY24,KEY24
            CALL      RDPT7O2
          ENDIF
.
. Insert Other Check for Valid Record Here i.e. Suspended or In Active
.   eg    COMPARE    ONE,INACTIVE
.         GOTO       SPT7OB91 IF EQUAL
.
          GOTO      SPT7OB90
.
SPT7OB90  MOVE      ZERO,OVRCD
          GOTO      SPT7OB99
SPT7OB91  MOVE      ONE,OVRCD
          GOTO      SPT7OB99
SPT7OB92  MOVE      TWO,OVRCD
SPT7OB99  RETURN
.------------------------------------------------------------
. Check for Matching ICD10 Edition 7 Procedure Words
.------------------------------------------------------------
SPT7OC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY9,PTCOITM,SP70
.
SPT7OC10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      SPT7OC99 IF EQUAL     * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY24,PTCOITM,KEYWRDXX
          CALL      RDPT7O1               * Check for Exact Match
          COMPARE   ZERO,OVRCD            * Ok so Continue Loop
          GOTO      SPT7OC10 IF EQUAL
.
          CALL      RKPT7O1               * Not Exact so Read Next Record
          BRANCH    OVRCD,SPT7OC90        * No Match So Exit No Match
          PACK      KEY9,PTCOITM,SP70
          MATCH     SAVKEY9,KEY9        * Match Key Fields
          GOTO      SPT7OC90 IF NOT EQUAL * No Match So Exit
          STRIP     KEYWRDXX
          MATCH     PTCOKWD,KEYWRDXX         * Check Key Word
          GOTO      SPT7OC90 IF NOT EQUAL * No Match So Exit
          GOTO      SPT7OC10              * Check Next Search Word
.
SPT7OC90  MOVE      ONE,EXIT
SPT7OC99  RETURN
+
.-----------------------------------------------------------
. ICD10 Edition 7 Diagnosis Code: Search Table
.-----------------------------------------------------------
SPT7DA00  WRITE     HTMLFILE;"<tr align=center>":
                             "<td class=HeadingCell colspan=2>":
                             "<b>You Searched for ":
                               KEYWORDS,"</td></tr><tr>":
                               "<td class=HeadingCell width=15%><b>Code</b>":
                               "</td>":
                               "<td class=HeadingCell><b>Description</b></td>":
                             "</tr>"
.
          CALL      GETWRD00
.
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
....      UNPACK    PTCNGDV7,KEY4,KEY5
....      MATCH     "9999",KEY4          * is ICD10 Ed.7 in use yet?
....      GOTO      SPT7DA90 IF EQUAL
.
          STRIP     KEYWRD01
          PACK      KEY24,KEYWRD01,SP70
          CALL      RSPT7D2
SPT7DA10  CALL      RKPT7D2
          BRANCH    OVRCD,SPT7DA90
.
          CALL      SPT7DB00
          BRANCH    OVRCD,SPT7DA10,SPT7DA90
.
          MOVE      PTCDITM,KEY9
          CALL      RDPT17D1
          BRANCH    OVRCD,SPT7DA10
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      SPT7DA10
          ENDIF
.
          BRANCH    LNK2PRT,SPT7DA20,SPT7DA30
          GOTO      SPT7DA99
.
SPT7DA20  WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:showICD03":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&pticd001=",DPCODE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             DPCODE,"</td>":
                             "<td>",PT0DDESC,"</td>":
                             "</tr>"
.
          GOTO      SPT7DA80
.
SPT7DA30  WRITE     HTMLFILE;"<tr valign=top>":
                              "<td><a href=#"#" ":
                              "OnClick=#"updateParent('",PT0DDESC:
                              "','",DPCODE,"')#">":
                              "<img src=../images/MaintenanceIcon.gif hspace=4":
                              " border=0 align=absmiddle></a>":
                              DPCODE,"</td>":
                              "<td>",PT0DDESC,"</td>":
                             "</tr>"
.
SPT7DA80  ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      SPT7DA10 IF NOT EQUAL
          GOTO      SPT7DA99
.
SPT7DA90  CALL      NOMORE00         * Display No More Records Message
.
SPT7DA99  RETURN
.------------------------------------------------------------
. Check ICD10 Edition 7 Diagnosis Record
.------------------------------------------------------------
SPT7DB00  MATCH     KEYWRD01,PTCDKWD
          GOTO      SPT7DB92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY24,PTCDKWD,PTCDITM
            CALL      SPT7DC00
            BRANCH    EXIT,SPT7DB91
            MOVE      SAVKEY24,KEY24
            CALL      RDPT7D2
          ENDIF
.
. Insert Other Check for Valid Record Here i.e. Suspended or In Active
.   eg    COMPARE    ONE,INACTIVE
.         GOTO       SPT7DB91 IF EQUAL
.
          GOTO      SPT7DB90
.
SPT7DB90  MOVE      ZERO,OVRCD
          GOTO      SPT7DB99
SPT7DB91  MOVE      ONE,OVRCD
          GOTO      SPT7DB99
SPT7DB92  MOVE      TWO,OVRCD
SPT7DB99  RETURN
.------------------------------------------------------------
. Check for Matching ICD10 Edition 7 Diagnosis Words
.------------------------------------------------------------
SPT7DC00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY9,PTCDITM,SP70
.
SPT7DC10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      SPT7DC99 IF EQUAL     * Exit OK
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY24,PTCDITM,KEYWRDXX
          CALL      RDPT7D1               * Check for Exact Match
          COMPARE   ZERO,OVRCD            * Ok so Continue Loop
          GOTO      SPT7DC10 IF EQUAL
.
          CALL      RKPT7D1               * Not Exact so Read Next Record
          BRANCH    OVRCD,SPT7DC90        * No Match So Exit No Match
          PACK      KEY9,PTCDITM,SP70
          MATCH     SAVKEY9,KEY9        * Match Key Fields
          GOTO      SPT7DC90 IF NOT EQUAL * No Match So Exit
          STRIP     KEYWRDXX
          MATCH     PTCDKWD,KEYWRDXX         * Check Key Word
          GOTO      SPT7DC90 IF NOT EQUAL * No Match So Exit
          GOTO      SPT7DC10              * Check Next Search Word
.
SPT7DC90  MOVE      ONE,EXIT
SPT7DC99  RETURN
