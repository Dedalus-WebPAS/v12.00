.******************************************************************************
.* System         : Waiting List Letters                                      *
.*                                                                            *
.* Include Name   : WATLETCD.PBL                                              *
.*                                                                            *
.* Function       : Common code for printing letters.                         *
.*                                                                            *
.* Modifications  :                                                           *
.*                                                                            *
.*----------------------------------------------------------------------------*
.* V12.00.01 05/09/2025  Thanh T           TSK 0949457                        *
.*           Added LXPUSRAC-LXPUSRIL fields                                   *
.*----------------------------------------------------------------------------*
.* V11.04.01 17/07/2024  Thanh T           TSK 0939648                        *
.*           Added LXPXSN18-LXPXSN30 fields                                   *
.* V11.02.02 19/04/2022  Thanh T          TSK 090345                          *
.*           Added %idengend (LCPXUCC4/LXPXUCC4)                              *
.*                 %idgendsc (LCUCC4LD/LXUCC4LD)                              *
.*                 %idgentxt (LCPXATF1/LXPXATF1)                              *
.*                 %pronoun  (LCPXUCC5/LXPXUCC5)                              *
.*                 %prondesc (LCUCC5LD/LXUCC5LD)                              *
.*                 %prontext (LCPXATF2/LXPXATF2)                              *
.* V11.02.01 21/03/2022  Sunny            TSK 0905914                         *
.*           Added %admregdat (LCARDATE/LXARDATE)                             *
.* V11.01.01 28/06/2021  Sunny            TSK 0893817                         *
.*           Added %systype (LCSYSTYP/LXSYSTYP) - PAS Server Environment      *
.* V11.00.02 15/07/2020  Tracey Nguyen    TSK 0893621                         *
.*           Added %smsusrnm (LCCSMSUN/LXXSMSUN)                              *
.* V11.00.01 23/03/2020  Peter Vela       TSK 0889143                         *
.*           Fixed WATLET IO calls                                            *
.* V10.13.01 23/01/2019  Nicole Torrisi   TSK 0868849                         *
.*           Added %paclocn and %paslocn                                      *
.* V10.12.01 07/05/2018  Richa Phogat     TSK 0852348                         *
.*           Added %wremldes - Removal Reason Long Description(Cat WR)        *
.* V10.11.01 30/10/2017  Peter Vela       TSK 0843931                         *
.*           Added %wtsufdat, %wtsutdat, %wtsureac, %wtsuread                 *
.*                 %wrwlcadr1, %wrwlcadr2, %wrwlcadr3, %wrwlcadr4             *
.*                 %wrwlcpost, %wrwlcstat                                     *
.* V10.10.01 2333/05/2017  Thanh T.         TSK 0825240                       * 
.*           Added Days Not Ready for surgery for Patient and Clinical fields *
.*----------------------------------------------------------------------------*
.*                  V10.09.01 28/12/2016  Jill Parkinson TSK 0828825          *
.*                            Commented out match for EOS in MOD5000 so that  *
.*                            if lines with data fields go over it will       *
.*                            continue to print the rest of the line anyway   *
.*                  V10.08.04 17/11/2016  Peter Vela       TSK 0828936        *
.*                            Added LXWXIDUS LXWTSTAY                         *
.*                  V10.08.03 15/09/2016  Don Nguyen       TSK 0294154        *
.*                            Modified PRIN0000 to not print unnecessary      *
.*                            blank lines (PAGE0000).                         *
.*                  V10.08.02 04/08/2016  Don Nguyen       TSK 0294154        *
.*                            Added PRIN9050 - skip line count check for SMS  *
.*                  V10.08.01 20/07/2016  Don Nguyen       TSK 0294154        *
.*                            Added PRSMSH00 & PRSMSF00 - print SMS metatags  *
.*                  V10.07.01 09/03/2016  Ebon Clements    TSK 0294154        *
.*                            Added %smsid, %smscode, %smskey, %smsurno,      *
.*                            %smsuser, %smsphone                             *
.*                  V10.04.02 08/07/2014  Don Nguyen      CAR 302950          *
.*                            Added test for Page Break variable (83) before  *
.*                            MOD4600.                                        *
.*                  V10.04.01 24/02/2014  Don Nguyen      CAR 291879          *
.*                            Added field 377 (LXIHINUM) - IHI Number         *
.*                  V10.03.03 22/03/2013  Ania P          CAR 281663          *
.*                            Added: LXGPPNAM,LXGPPAD1,LXGPPAD2,LXGPPAD3,     *
.*                            LXGPPAD4,LXGPPPOS,LXGPPTEL,LXGPPFAX,LXGPPEML    * 
.*                  V10.03.02 30/01/2012  Peter Vela      CAR 257930          *
.*                            Added LXWTCHRC - Reason Code Urgency Update     *
.*                            Added LXWTCHRD - Reason Desc Urgency Update     *
.*                            Added LXWTCHOV - Original Value Urgency Update  *
.*                  V10.03.01 30/01/2012  Davin           CAR 257512          *
.*                            Added LXPTLSNM - Patient Long Surname           *
.*                  V10.02.04 25/10/2011  Ebon Clements   CAR 253354          *
.*                            Added LXWRSRCR - WL source of referral          *
.*                  V10.02.03 21/09/2011  Davin           CAR 248749          *
.*                            Added LXOCTITL to LXPOSLET                      *
.*                  V10.02.02 15/09/2011  Ebon Clements   CAR 250738          *
.*                            Added LXRCLINF to LXRCLPST                      *
.*                  V10.02.01 01/09/2011  Peter McMullen  CAR 248749          *
.*                            Added LCPGTITL to  LCRDAD4                      *
.*                  V10.01.01 23/02/2011  Ebon Clements   CAR 233571          *
.*                            Added LXWRCPCC and LXWRCPCD                     *
.*                  V9.12.02  16/10/2009  Ebon Clements   CAR 208060          *
.*                            Added LXWRPCNT                                  *
.*                  V9.12.01  16/10/2009  Ebon Clements   CAR 208060          *
.*                            Added LXWRPCOD                                  *
.*                  V9.11.02  27/04/2009  Saroeun Soeur   CAR 193622          *
.*                            Added LXWLPSTA (cate VL)  and LXWLPSTD          *
.*                  V9.11.01  02/03/2009  Ebon Clements   CAR 189616 187103   *
.*                            Removal reason code, description, date and      *
.*                            consultant fax                                  *
.*                  V9.09.05  12/12/2007  Ebon Clements    CAR 157081         *
.*                            Added patient email address                     *
.*                  V9.09.04  18/10/2007  Steve Armstrong  CAR 149755         *
.*                            Mods to use new RGGP... & RGPR... variables     *
.*                            instead of the LX... variables.                 *
.*                  V9.09.03  07/08/2007  Peter Vela      CAR 143567          *
.*                            Added Waiting List consultant email             *
.*                            address (pmshcpaf,patdo1af)                     *
.*                  V9.09.02  03/07/2007  Peter Vela      CAR 143555          *
.*                            Added Reg GP Prac Email Address                 *
.*                  V9.09.01  30/07/2007  Peter Vela      CAR 143555          *
.*                            Added Reg GP Phone Number                       *
.*                                  Reg GP Prac Fax Num                       *
.*                  V9.07.01  30/08/2006  Peter Vela      CAR 117576          *
.*                            Added Patient's Date of Birth                   *
.*                            Added Local GP Preferred Method of Contact      *
.*                  V9.06.01  08/06/2006  Ramesh VK       CAR 86021           *
.*                            Added patient sex %psex                         *
.*                  V9.05.01  31/03/2006  Peter Vela      CAR 86019           *
.*                            Added procedure description (WMDESC.wattr1af)   *
.*                  V9.04.11  15/11/2005  Peter Vela      CAR 83840           *
.*                            Added Admit Ward, Admit Date, Admit Time        *
.*                            (patmi1af)                                      *
.*                  V9.04.10  05/10/2005  Mike Laming     CAR 52354           *
.*                            Added W/L Consent and Consent Changed Date      *
.*                  V9.04.09  18/08/2005  Peter Vela      CAR 69456           *
.*                            Added Local GP Formatted Full Name (PMI)        *
.*                  V9.04.08  09/08/2005  Peter Vela      CAR 67204           *
.*                            Added Ref/Reg. GP Mobile Number                 *
.*                            Added Ref/Reg. GP Pager Telephone Number        *
.*                            Added Ref/Reg. GP Pager Number                  *
.*                            Added Ref/Reg. GP Provider Number               *
.*                  V9.04.07  27/07/2005  Peter Vela      CAR 68536           *
.*                            Added Waiting List Entry Diagnosis              *
.*                  V9.04.06  19/07/2005  Peter Vela      CAR 63023           *
.*                            Added Local GP Practice Fax Number              *
.*                            Added Local GP Practice Email Address           *
.*                  V9.04.05  24/06/2005  Peter Vela      CAR 62391           *
.*                            Added Pre Admission Clinic Code                 *
.*                            Added Pre Anaesthetic Clinic Code               *
.*                            Added Waiting List Consultant Code              *
.*                            Added Waiting List Consultant Phone No          *
.*                  V9.04.04  21/06/2005  Ebon Clements   CAR 62391           *
.*                            Added Pre Anaesthetic clinic,date and time      *
.*                            Added Pre Admission clinic                      *
.*                  V9.04.03  29/04/2005  Peter Vela      CAR 60667           *
.*                            Added Waiting List Variables                    *
.*                            - Proposed Admission Date                       *
.*                            - Proposed Admission Time                       *
.*                            - Proposed Operation Date                       *
.*                            - Proposed Operation Time                       *
.*                  V9.04.02  07/04/2005  Peter Vela      CAR 59792           *
.*                            Added Waiting List variables                    *
.*                            - Pre Adm Date                                  *
.*                            - Pre Adm Time                                  *
.*                            - Admitting Point Ward                          *
.*                  V9.04.01  04/03/2005  Jill Habasque CAR 59773             *
.*                            Removed open of TEMP1 after error message       *
.*                  V9.03.02  24/12/2003  Peter Vela      CAR 45149           *
.*                            Fixed layout of Waiting List Letters            *
.*                  V9.03.01  24/11/2003  Peter Vela      CAR 45249           *
.*                            Added letter variable %age                      *
.*                  V9.02.02  19/10/2001  B.G.Ackland                         *
.*                            Added ability to Execute a Unix Command (VCQ)   *
.*                  V9.02.01  27/08/2001  Ebon Clements                       *
.*                            Added more variables from WATLETCD for printing *
.*                  V5.09.00  25/07/2000  Pat Dirito                          *
.*                            Created Program                                 *
.*                                                                            *
.******************************************************************************
.************************************************************************
.*  WHIS0000  :  Write a record to the history file                     *
.************************************************************************
WHIS0000  COMPARE   ONE,WTCNULHI
          GOTO      WHIS9999 IF NOT EQUAL
          PACK      KEY19,URDIM,WMCODE,WMCNT
          CALL      RDTREA1
          BRANCH    OVRCD,WHIS9999
.
          MOVE      WMURNO,WTLHURNO
          MOVE      WMCODE,WTLHCODE
          MOVE      WMCNT,WTLHCONT
          PACK      WTLHDATE,CCC,CYY,CMM,CDD
          REP       " 0",WTLHDATE
          MOVE      LETTFORM,WTLHLNUM
          MOVE      SP3,WTLHRPLY
          MOVE      SP3,WTLHACTN
.
          PACK      KEY30,WTLHURNO,WTLHCODE,WTLHCONT,WTLHDATE,WTLHLNUM
          CALL      RAWTLHI1
          IF        OVRCD=1
            CALL      WRWTLHI1
          ENDIF
.
WHIS9999  RETURN
.**********************************************************************
.*                              PRIN0000                              *
.*             set up and PRINt letter one line at a time             *
.**********************************************************************
PRIN0000  CALL      LETEXE00
          COMPARE   ZERO,EXIT
          GOTO      PRIN9999 IF EQUAL
.
          MOVE      FALSE,EXIT
.         MOVE      ONE,COUNT
          MOVE      ZERO,COUNT              * I CAR 45149
          MOVE      LETTER,LETTFORM
          MOVE      ZERO,FORM3              * read letter file header record
          PACK      KEY6,LETTFORM,FORM3
          CALL      RDLET1
.
          MOVE      WLETPPAG,PHYSPAGE
          MOVE      WLETPTOP,TOPMARG
          MOVE      WLETPBOT,BOTTMARG       * set up margins etc.
          SUBTRACT  WLETPBOT,WLETPPAG
          MOVE      WLETPPAG,WLETPLEN
          MOVE      WLETPTAB,LEFTMARG
.
          READ      CONTROLF,HUND20;*243,PTCNSMSN
          MATCH     "1",PTCNSMSN            * Using SMS Notifications?
          GOTO      PRIN0500 IF NOT EQUAL
.
          MOVE      WLETCOMM,LETRCOMM       * Store our Communication Method
          MATCH     "1",LETRCOMM            * SMS Notification?
          IF        @EQUAL
            MOVE      ONE,TOPMARG           * Default top margin to 1
            MOVE      ONE,BOTTMARG          * Default bottom margin to 1
            MOVE      ONE,LEFTMARG          * Default left margin to 1
            CALL      PRSMSH00              * Print SMS Message XML Header Tags
            GOTO      PRIN1000
          ENDIF
.
.------ print top margin ------
.
PRIN0500  WHILE     COUNT < TOPMARG
            PRINT     *N;
            ADD       ONE,COUNT
          DO
.
.------ print letter one line at a time ------
.
PRIN1000  CALL      RDKLET1
          BRANCH    OVRCD,PRIN9000          * end of letter reached
          COMPARE   LETTFORM,WLETNO
          GOTO      PRIN9000 IF NOT EQUAL
          BRANCH    WLVAR,PRIN2000
.         PRINT     *N,*LEFTMARG,WLTEXT;    * print text if no % vars
          PRINT     *LEFTMARG,WLTEXT;       * I CAR 45149
          PRINT     *N;                     * I CAR 45149
          ADD       ONE,COUNT
.
          MATCH     "1",LETRCOMM            * SMS Notification?
          GOTO      PRIN1000 IF EQUAL
.
          IF        COUNT < WLETPLEN
            GOTO      PRIN1000
          ENDIF
          CALL      PAGE0000
          GOTO      PRIN1000
.
PRIN2000  CALL      MOD0000                 * substitute % var value
.         PRINT     *N,*LEFTMARG,PRTSTRNG;  * print text with % vars
          PRINT     *LEFTMARG,PRTSTRNG;     * I CAR 45149
          PRINT     *N;                     * I CAR 45149
          ADD       ONE,COUNT
.
          MATCH     "1",LETRCOMM            * SMS Notification?
          GOTO      PRIN1000 IF EQUAL
.
          IF        COUNT < WLETPLEN
            GOTO      PRIN1000
          ENDIF
          CALL      PAGE0000
          GOTO      PRIN1000
.
PRIN9000  MATCH     "1",LETRCOMM            * SMS Notification?
          IF        @EQUAL
            CALL      PRSMSF00              * Print SMS Message XML Footer Tag
            GOTO      PRIN9050
          ENDIF
.
          COMPARE   TOPMARG,COUNT
          GOTO      PRIN9500 IF EQUAL
.
PRIN9050  MOVE      TWO,EXIT
          DISPLAY   *P60:24,*EL,*V2LON,COUNTER,*HOFF," Printed";
          MOVE      PHYSPAGE,TEMP3          * set up variables for paging
          SUBTRACT  COUNT,PHYSPAGE
          ADD       WLETPTOP,PHYSPAGE
          MOVE      ONE,COUNT
.
.------ paginate at end of each letter ------
.
          WHILE     COUNT < PHYSPAGE
            PRINT     *N;
            ADD       ONE,COUNT
          DO   
.
PRIN9400  MOVE      TEMP3,PHYSPAGE
          GOTO      PRIN9999
.
.------ letter header is on file but nothing else ------
.
PRIN9500  DISPLAY   *P1:24,*EL,*B,*V2LON:
                    "** Letter has not been set up yet **    ";
          CALL      HITENTER
          MOVE      TRUE,EXIT
.
PRIN9999  RETURN
.**********************************************************************
.*                              PAGE0000                              *
.*        routine to PAGinatE if a new page is required               *
.**********************************************************************
PAGE0000  MOVE      WLETNO,FORM3
          MOVE      WLINNO,FORM3A
          CALL      RDKLET1
          BRANCH    OVRCD,PAGE8000          * check to see that another line of
          COMPARE   LETTFORM,WLETNO           the letter exists
          GOTO      PAGE8000 IF NOT EQUAL
          MOVE      TOPMARG,TEMP2
          ADD       BOTTMARG,TOPMARG
          MOVE      ONE,COUNT
.
.------ loop to paginate ------
.
          WHILE     COUNT < TOPMARG
            PRINT     *N;
            ADD       ONE,COUNT
          DO
          GOTO      PAGE9000
.
.------ no need to paginate ------
.
PAGE8000  PACK      KEY6,FORM3,FORM3A
          CALL      RDLET1
          GOTO      PAGE9999
.
.------ reset read to previous record to continue processing ------
.
PAGE9000  MOVE      TEMP2,COUNT
          MOVE      TEMP2,TOPMARG
          PACK      KEY6,FORM3,FORM3A
          CALL      RDLET1
.
PAGE9999  RETURN
.**********************************************************************
.*                              MOD0000                               *
.*             MODify a line to substitute "%" variable values        *
.**********************************************************************
MOD0000   MOVE      WLTEXT,DIM70
          PACK      PRTSTRNG,SP30,SP30,SP10 * initialise PRTSTRNG & pointer
          RESET     PRTSTRNG,0                positions
          MOVE      ONE,STARTSTR
          MOVE      ONE,ENDSTR
.
MOD2000   SCAN      SPECCHAR,DIM70          * Scan the Input Line for a "%" sign
          GOTO      MOD8000 IF NOT EQUAL
.
MOD3000   MOVEFPTR  DIM70,PERCPOS
          BUMP      DIM70,-1                * Go to the char. before "%" sign.
          GOTO      MOD3100 IF NOT EOS        Test if the % sign is the 1st
          RESET     DIM70,0                   char. on the line.  Set f.P. to 0
.                                             if it is
MOD3100   MOVEFPTR  DIM70,ENDSTR            * store endtsr at position 1 char
          COMPARE   STARTSTR,ENDSTR           before the % sign & ensure that
          GOTO      MOD3200 IF NOT LESS       end & start positions dont overlap
.
          RESET     DIM70,STARTSTR          * set pointer to next % sign if end
          GOTO      MOD9999 IF EOS            < start
          GOTO      MOD3300
.
MOD3200   RESET     DIM70,STARTSTR          * reset the pointers to extract text
          SETLPTR   DIM70,ENDSTR              preceding the % sign
          APPEND    DIM70,PRTSTRNG
.
MOD3300   SETLPTR   DIM70,70                * set the pointers to start at % &
          RESET     DIM70,PERCPOS             go to position 70.  Delete pre-
.
.         SRF 642798
.
          MOVE      DIM70,TEMP70              ceding text from the DIM70 string
          PACK      DIM70,TEMP70,SP30,SP30,SP30
.
MOD4000   CALL      GETV0000                * extract the % var
          MOVEFPTR  DIM70,STARTSTR          * store finish pos. of variable
          BRANCH    EXIT,MOD5000
          RESET     SRCHVAR
.
.------  search for the % var in the list of constants ------
.
          SEARCH    SRCHVAR,LCADDA,LCNOVARS,SRCHNUM 
.
          GOTO      MOD5000 IF NOT EQUAL
.
          COMPARE   EIGHTY3,SRCHNUM         * Page Break variable?
          GOTO      MOD7000 IF EQUAL        * yes; print page break
.
.------  load DISPSTRN with the actual value of the % var ------
.
MOD4600   LOAD      DISPSTRN,SRCHNUM,PADD1,PADD2,PADD4,BTYPE,FDATE:        * 5
                             DOCTNAM,XXDATE,XTIME,FULLNAME,PGNAME:         * 10
                             LDATE,PDATE,PTIME,PPOST,PTELEP:               * 15
                             PACCOM,PDESC,RESPNAME,RESPADDA,RESPADDB:      * 20
                             RESPPOST,RESPSUBR,RESPADDD,LRDATE,PSNAME:     * 25
                             PSUBRB,PTITL,REFUNIT,URDIM,DESCCAT:           * 30
                             RFDADD1,RFDADD2,FULLNAME,PADD1,PADD2:         * 35
                             PSUBRB,PADD4,PPOST,RGGPSNAM,RGGPGNAM:         * 40
                             RGGPADD1,RGGPADD2,RGGPADD3,RGGPADD4,RGGPPOST: * 45
                             RFGPSNAM,RFGPGNAM,RFGPADD1,RFGPADD2,RFGPADD3: * 50
                             RFGPADD4,RFGPPOST,DADATE,GADATE,DNABDATE:     * 55
                             NAFDATE,NATDATE,EADMDESC,BOOKNO,PTHSNAME:     * 60
                             PTHSADD1,PTHSADD2,PTHSADD3,PTHSADD4,PTHSPCOD: * 65
                             PTHSTELE,RGPRSURG,RGPRSAD1,RGPRSAD2,RGPRSAD3: * 70
                             RGPRSAD4,RGPRSPCD,RGPRTELE,RFPRSURG,RFPRSAD1: * 75
                             RFPRSAD2,RFPRSAD3,RFPRSAD4,RFPRSPCD,RFPRTELE: * 80
                             WMPTY,REFPRTY,LXPAGE,LXCELL,LXPADDA:          * 85
                             LXPADDB,LXPADDC,LXPADDD,LXPPOST,LXUDEF5:      * 90
                             LXUDEF6,LXUDEF7,LXUDEF8,LXPRANK,LXPXMSNA:     * 95
                             LXPXMGNA,LXPXMTLE,LXPXMAD1,LXPXMAD2,LXPXMAD3: * 100
                             LXPXMAD4,LXPXMPOS,LXPXMPNO,LXPXMBNO,LXPXMMNO: * 105
                             LXPXMEML,LXPXMCNT,LXPXMCRP,LXPXMLAB,LXPXMREL: * 110
                             LXPXFSNA,LXPXFGNA,LXPXFTLE,LXPXFAD1,LXPXFAD2: * 115
                             LXPXFAD3,LXPXFAD4,LXPXFPOS,LXPXFPNO,LXPXFBNO: * 120
                             LXPXFMNO,LXPXFEML,LXPXFCNT,LXPXFCRP,LXPXFLAB: * 125
                             LXPXFREL,LXPXDVAC,LXPXABRG,LXPXPRVI,LXVXABRG: * 130
                             LXVXCADM,LXVXIRAD,LXVXIDUS,LXVXCRAV,LXVXRCCT: * 135
                             LXVXAPWD,LXVXAPBD,LXKREBOO,LXREURNO,LXREADOC: * 140
                             LXRESDOC,LXREEDAT,LXREATIM,LXREPDAT,LXREPTIM: * 145
                             LXREADAT,LXRETYPE,LXRECANC,LXREACCM,LXREPROC: * 150
                             LXKRECNT,LXREWARD,LXREEBED,LXRECLSS,LXREATYP: * 155
                             LXREDEPT,LXRETDOC,LXRECLMT,LXRERFGP,LXREGPPC: * 160
                             LXRERESI,LXREEATY,LXKREELO,LXREBKDT,LXRXPOWD: * 165
                             LXRXADWD,LXRXCDTE,LXRXPSTY,LXRXCNDT,LXRXUFF1: * 170
                             LXRXUFF2,LXRXUFF3,LXRXODTE,LXRXOPRD,LXRXASRC: * 175
                             LXRXBLDT,LXRXCTTY,LXRXSGBK,DIMAGE,LXWRPRED:   * 180
                             LXWXPRET,LXWXADMW,LXWRPRAD,LXWXPRAT,LXWRPROD: * 185
                             LXWXPROT,LXWRPAND,LXWXPANT,LXWRPRAC,LXWRPANC: * 190
                             LXWRPRCC,LXWRPACC,LXWRWLCC,LXWRWLCP,RGGPFAXN: * 195
                             RGGPSEML,LXWRWLDI,RGGPMOBN,RGGPPAGR,RGGPPAGN: * 200
                             RGGPPRV1,LXWRLDFN,LXWRCSST,LXWRCSDT,LXPMIXWD: * 205
                             LXPMIATM,LXPMIADT,LXWRDESC,RESPSEX,LXPMIDOB:  * 210
                             LXWRLDPC,LXWRLPCC,RGGPSTEL,RGPRFAXN,RGPRPEML: * 215
                             LXWLCHEM,LXWLCDEM,LXPXPEML,LXREMDAT,LXREMCOD: * 220
                             LXREMDES,LXWLCFAX,LXWLPSTA,LXWLPSTD,LXWRPCOD: * 225
                             LXWRPCNT,LXTBCANC,LXPREVPA,LXWRCPCD,LXWRCPCC: * 230
                             PTITL,PMPXFNAM,PMPXSNAM,RFDTITL,RFDADD3:      * 235
                             RFDADD4,LXRCLINF,LXRCLTLE,LXRCLSNM,LXRCLGNM:  * 240
                             LXRCLAD1,LXRCLAD2,LXRCLAD3,LXRCLAD4,LXRCLPST: * 245
                             LXOCTITL,LXOCSNAM,LXOCGNAM,LXOCGNM2,LXOCNAME: * 250
                             LXOCADD1,LXOCADD2,LXOCADD3,LXOCADD4,LXOCPOST: * 255
                             LXOCTELP,LXOCTELB,LXOCTELM,LXOCRELP,LXOCEMAI: * 260
                             LXOCSLET,LXP2TITL,LXP2SNAM,LXP2GNAM,LXP2GNM2: * 265
                             LXP2NAME,LXP2ADD1,LXP2ADD2,LXP2ADD3,LXP2ADD4: * 270
                             LXP2POST,LXP2TELP,LXP2TELB,LXP2TELM,LXP2RELP: * 275
                             LXP2EMAI,LXP2SLET,LXPCTITL,LXPCSNAM,LXPCGNAM: * 280
                             LXPCGNM2,LXPCNAME,LXPCADD1,LXPCADD2,LXPCADD3: * 285
                             LXPCADD4,LXPCPOST,LXPCTELP,LXPCTELB,LXPCTELM: * 290
                             LXPCRELP,LXPCEMAI,LXPCSLET,LXECTITL,LXECSNAM: * 295
                             LXECGNAM,LXECGNM2,LXECNAME,LXECADD1,LXECADD2: * 300
                             LXECADD3,LXECADD4,LXECPOST,LXECTELP,LXECTELB: * 305
                             LXECTELM,LXECRELP,LXECEMAI,LXECSLET,LXPLTITL: * 310
                             LXPLSNAM,LXPLGNAM,LXPLGNM2,LXPLNAME,LXPLADD1: * 315
                             LXPLADD2,LXPLADD3,LXPLADD4,LXPLPOST,LXPLTELP: * 320
                             LXPLTELB,LXPLTELM,LXPLRELP,LXPLEMAI,LXPLSLET: * 325
                             LXPRTITL,LXPRSNAM,LXPRGNAM,LXPRGNM2,LXPRNAME: * 330
                             LXPRADD1,LXPRADD2,LXPRADD3,LXPRADD4,LXPRPOST: * 335
                             LXPRTELP,LXPRTELB,LXPRTELM,LXPRRELP,LXPREMAI: * 340
                             LXPRSLET,LXP1TITL,LXP1SNAM,LXP1GNAM,LXP1GNM2: * 345
                             LXP1TELP,LXP1TELB,LXP1TELM,LXP1RELP,LXP1EMAI: * 350
                             LXP1SLET,LXPOTITL,LXPOSNAM,LXPOGNAM,LXPOGNM2: * 355
                             LXPONAME,LXPOTELP,LXPOTELB,LXPOTELM,LXPORELP: * 360
                             LXPOEMAI,LXPOSLET,LXWRSRCR,LXPTLSNM,LXWTCHRC: * 365
                             LXWTCHRD,LXWTCHOV,LXGPPNAM,LXGPPAD1,LXGPPAD2: * 370
                             LXGPPAD3,LXGPPAD4,LXGPPPOS,LXGPPTEL,LXGPPFAX: * 375
                             LXGPPEML,LXIHINUM,LXXSMSID,LXXSMSKY,LXSMSCOD: * 380
                             LXXSMSPH,LXXSMSUR,LXXSMSUS,LXWXIDUS,LXWTSTAY: * 385
                             LXWNRCDY,LXWNRCDP,LXWNRCDC,LXWTSUFT,LXWTSUTT: * 390
                             LXWTSURC,LXWTSURD,LXWTWLA1,LXWTWLA2,LXWTWLA3: * 395
                             LXWTWLA4,LXWTWLPT,LXWTWLST,LXRELDES,LXPACLOC: * 400
                             LXPASLOC,LXXSMSUN,PTCNSTYP,LXARDATE,LXPXUCC4: * 405
                             LXUCC4LD,LXPXATF1,LXPXUCC5,LXUCC5LD,LXPXATF2: * 410
                             LXPXSN18,LXPXSN19,LXPXSN20,LXPXSN21,LXPXSN22: * 415
                             LXPXSN23,LXPXSN24,LXPXSN25,LXPXSN26,LXPXSN27: * 420
                             LXPXSN28,LXPXSN29,LXPXSN30,LXPUSRAC,LXPUSRAD: * 425
                             LXPUSRAL,LXPUSRBC,LXPUSRBD,LXPUSRBL,LXPUSRCC: * 430
                             LXPUSRCD,LXPUSRCL,LXPUSRDC,LXPUSRDD,LXPUSRDL: * 435
                             LXPUSREC,LXPUSRED,LXPUSREL,LXPUSRFC,LXPUSRFD: * 440
                             LXPUSRFL,LXPUSRGC,LXPUSRGD,LXPUSRGL,LXPUSRHC: * 445
                             LXPUSRHD,LXPUSRHL,LXPUSRIC,LXPUSRID,LXPUSRIL  * 450  
.
.         * Special case for minors          
          IF        PSAGE < 16
            IF        SRCHNUM > 32 & SRCHNUM < 39
.              * Minors get mail to next of kin
              SUBTRACT  THIRTY2,SRCHNUM
              LOAD      DISPSTRN,SRCHNUM,PNKNAME,PNKADD1,PNKADD2,PNKSUBR,PNKADD4,PNKPOST
            ENDIF
          ENDIF
.
          APPEND    DISPSTRN,PRTSTRNG
....      GOTO      MOD8000 IF EOS     * removed task 0828825
          CALL      COMX0000                * compress trailing blanks in
          GOTO      MOD2000                   PRTSTRNG
.
MOD5000   RESET     SRCHVAR                 * add string starting with a % if
          APPEND    SRCHVAR,PRTSTRNG          not found in the list of constants
          GOTO      MOD2000
.
.------  % page variable found ------
.
MOD7000   MOVE      PHYSPAGE,TEMP3
          SUBTRACT  COUNT,PHYSPAGE
          MOVE      ONE,COUNT
          ADD       TOPMARG,PHYSPAGE
          SUBTRACT  ONE,PHYSPAGE
.
MOD7100   PRINT     *N;
          COMPARE   COUNT,PHYSPAGE
          GOTO      MOD7500 IF EQUAL
          ADD       ONE,COUNT
          GOTO      MOD7100
.
MOD7500   MOVE      TEMP3,PHYSPAGE
          MOVE      TOPMARG,COUNT
          SUBTRACT  ONE,COUNT
          PACK      PRTSTRNG,SP30,SP30,SP10
          GOTO      MOD9999
.
MOD8000   APPEND    DIM70,PRTSTRNG          * add remaining text to PRTSTRNG if
          RESET     PRTSTRNG                  no % signs left
.
MOD9999   RETURN
.**********************************************************************
.*                              GETV0000                              *
.*                GET the "%" Variable from the line                  *
.**********************************************************************
GETV0000  MOVE      FALSE,EXIT
          PACK      SRCHVAR,SP30,SP30,SP10
          RESET     SRCHVAR,0
          MOVE      DIM70,ANS
          APPEND    ANS,SRCHVAR
          RESET     SRCHVAR,0
          BUMP      DIM70
          MOVE      DIM70,ANS
          REPLACE   REPSTR,ANS              * check to see if first char after %
          MATCH     "0",ANS                   is a lower case letter
          GOTO      GETV9000 IF NOT EQUAL
          BUMP      DIM70,-1
          PACK      SRCHVAR,SP30,SP30,SP10  *initialise SRCHVAR value & pointers
          RESET     SRCHVAR,0
.
.------  append % var to SRCHVAR until anything but ------
.------  a lower case letter is found ------
.
GETV1000  MOVE      DIM70,ANS
          APPEND    ANS,SRCHVAR
          BUMP      DIM70
          GOTO      GETV9999 IF EOS
          MOVE      DIM70,ANS
          REPLACE   REPSTR,ANS
          MATCH     "0",ANS
          GOTO      GETV9999 IF NOT EQUAL
          GOTO      GETV1000
.
.------  string found is not a valid % var ------
.
GETV9000  MOVE      TRUE,EXIT
.
GETV9999  RETURN
.**********************************************************************
.*                              COMX0000                              *
.*         routine to COMpress trailing blanks in PRTSTRNG            *
.**********************************************************************
COMX0000  ENDSET    PRTSTRNG
.
COMX1000  CMATCH    SP1,PRTSTRNG
          GOTO      COMX9999 IF NOT EQUAL
          BUMP      PRTSTRNG,-1
          GOTO      COMX9999 IF EOS
          GOTO      COMX1000
.
COMX9999  RETURN
.------------------------------------------------------------
. Routine   : LETEXE00
.
. Funtion   : Execute a UNIX Shell Command to a Print Letter 
.
. Inputs    : Valid Record Read from WATTR1FD
.             SPLFILE - Spool File Name
.
. Returns   : EXIT = 0 Letter Printed,  1 Letter Not Printed
.------------------------------------------------------------
LETEXE00  MOVE      ONE,FORM3              * Check for UNIX Shell Required
          MOVE      LETTER,LETTFORM
          PACK      KEY6,LETTFORM,FORM3
          CALL      RDLET1
          BRANCH    OVRCD,LETEXE90
          MATCH     "!sh",WLTEXT
          GOTO      LETEXE90 IF NOT EQUAL
.
          MOVE      TWO,FORM3              * Get UNIX Command
          PACK      KEY6,LETTFORM,FORM3
          CALL      RDLET1
          BRANCH    OVRCD,LETEXE99
          MATCH     SP70,WLTEXT
          GOTO      LETEXE90 IF EQUAL
.
          CLEAR     VAR
          STRIP     WLTEXT
          APPEND    WLTEXT,VAR
          APPEND    SP1,VAR
          APPEND    "$TBSPOOL/",VAR         * Spool File Directory
          APPEND    SPLFILE,VAR             * Spool File Name
          APPEND    ".prt ",VAR
          APPEND    " '",VAR
          APPEND    LETTFORM,VAR            * Letter Number
          APPEND    "' '",VAR
          APPEND    WMURNO,VAR              * U/R
          APPEND    "' '",VAR
          APPEND    WMCODE,VAR              * Procedure
          APPEND    "' '",VAR
          APPEND    WMCNT,VAR               * Procedure Count
          APPEND    "'",VAR
          RESET     VAR
          EXECUTE   VAR,TASKID              * Execute
          MOVE      ZERO,EXIT               * Letter Executed
          GOTO      LETEXE99
.
LETEXE90  MOVE      ONE,EXIT                * No Letter Executed
.
LETEXE99  RETURN
.
