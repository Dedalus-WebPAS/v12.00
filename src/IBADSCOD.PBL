.
.     DSCODINC : Display Codes File Entries
.
.     This include displays entries in the codes file for a given
.      category.
.                   Input variable : CODE  contains category
.
IBADSCOD  PACK      KEY5 WITH CODE,SP5
          MOVE      SP2,PRXCATA
          CALL      RDICOD1
          BRANCH    OVRCD OF DISPRET
.
.         Display Category Heading, and then loop over entries
.
          DISPLAY   *P1:3,*EF,*P30:4,*V2LON,*ULON,PRXDESC
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          CALL      RDSICOD1
.
DISPLP1   CALL      RDKICOD1
          BRANCH    OVRCD OF ENDLP1
.
          MATCH     PRXCATA,CODE
          GOTO      ENDLP1 IF NOT EQUAL
.
          DISPLAY   *PCCOL:CVERT,*V2LON,PRXCODE,SP2,*HOFF,PRXDESC;
          ADD       ONE,CVERT
          COMPARE   "24",CVERT
          GOTO      DISPLP1 IF LESS
.
          MOVE      FIVE,CVERT
          ADD       "26",CCOL
          COMPARE   "70",CCOL
          GOTO      DISPLP1 IF LESS
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    "E",ANS
          GOTO      ENDLP2 IF EQUAL
.
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
          GOTO      DISPLP1
.
.      End of codes
.
ENDLP1    KEYIN     *P1:24,*EL,"Hit ",*V2LON,"<RETURN>",*HOFF:
                               " to continue ",*EOFF,ANS;
ENDLP2    MOVE      ZERO,OVRCD
DISPRET   RETURN
