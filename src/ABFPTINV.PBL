. Program  : ABFPTINV
.
. Function : This include produces the Next invoice for ABF Inpatient.
.
.            PROGFLAG is used to determine if the invoice is to be displayed,
.            printed or just have the total calculated
.
.        IF PROGFLAG = 1  THEN WORK OUT THE AMOUNT TO BE INVOICED
.
.        IF PROGFLAG = 2  THEN DISPLAY NEXT INVOICE
.
.        IF PROGFLAG = 3  THEN PRINT NEXT INVOICE
.
.        IF PROGFLAG = 4  THEN UPDATE PATFINS WITH AMOUNTS TO BE INVOICED
.
.*******************************************************************************
. Modification : 
.            V12.00.02 13/08/2025  J.Tan          TSK 0964813
.                      Mod for ICD10v13 for HAC generation (patd13af)
.            V12.00.01 14/05/2025  J.Tan          TSK 0955096
.                      Added alphanueric visit number changes
.            V11.05.02 16/04/2025  J.Tan          TSK 0951578
.                      Fixed LOSNOICU for less than 24 hour to be one
.                      Set zero LOS for Acute Care to be one day 
.            V11.05.01 16/01/2025  J.Tan          TSK 0951578
.                      Fixed LOSNOICU for less than 24 hour
.            V11.04.09 09/12/2024  J.Tan          TSK 0954876
.                      Fixed Covid adjustmnet
.            V11.04.08 16/10/2024  J.Tan          TSK 0951578
.                      Added parameter for Minutes to round up ICU Hours
.            V11.04.07 23/09/2024  J.Tan          TSK 0949848
.                      Mod DRG List used in for COVID Adjustor (CALCFLAG=4)
.            V11.04.06 13/09/2024  J.Tan          TSK 0947315
.                      Mod Minimum ICU hours is one if LOS = ICU hours/day
.                      Fixed 'Total Unqualified'
.            V11.04.05 07/08/2024  J.Tan          TSK 0947315
.                      NO LOS ICU Adjusted (074) if ICUHOURS=0
.            V11.04.04 11/07/2024  J.Tan          TSK 0947315
.                      Added CALCFLAG=3 (LOS ICU adjusted & unqualified Newborn)
.            V11.04.03 28/03/2024  J.Tan          TSK 0943340
.                      Mod to default CEFFDATE to Current date for 'As at Eff.'
.            V11.04.02 19/02/2024  J.Tan          TSK 0939019
.                      Fixed checking ICD10 Edition
.            V11.04.01 22/12/2023  J.Tan          TSK 0939019
.                      Mod checking for ICD10 Edition ICDE0000
.                      12/01/2024  J.Tan          TSK 0941779
.                      Mod percentage of  APPSAMNT & checking for Priv.Patient
.            V11.03.07 17/11/2023  J.Tan          TSK 0940089
.                      Mod checking for blank Discharged DRG
.            V11.03.06 13/11/2023  J.Tan          TSK 0939019
.                      Mod for ICD10 Edition 12 Diagnosis Codes (HAC mapping)
.            V11.03.05 20/09/2023  J.Tan          TSK 0916870
.                      Mod defaulting NEP value with blank code set/indicator 
.            V11.03.04 28/08/2023  J.Tan          TSK 0916870
.                      Added Claim code Cat CL Multiple NEP value
.            V11.03.03 13/07/2023  J.Tan          TSK 0923703
.                      Fixed checking for Daycase Private patient
.            V11.03.02 07/07/2023  J.Tan          TSK 0923703
.                      Fixed checking for COVID19 and Hospital Remoteness
.            V11.03.01 18/05/2023  J.Tan          TSK 0923703
.                      Mod for 2023/2024
.                        - Billable LOS to exclude ICU hours (2023/2024)
.                        - Set APPSAMNT - Private Patient service adjustment
.            V11.01.03 11/11/2021  J.Tan          TSK 0913621
.                      Mod to allow blank Set code for Multiple NEP Values
.            V11.01.02 14/09/2021  J.Tan          TSK 0906222
.                      Mod for Multiple NEP Values
.            V11.01.01 27/04/2021  J.Tan          TSK 0863287
.                      Mod for New Patient Portion Inv.functionality(PTCNPPIN=1)
.            V11.00.08 15/07/2020 J.Tan            TSK 0892319
.                      Added Adjustment type 026 and 027 and Cat CG for Psychia.
.            V11.00.07 30/06/2020  J.Tan           TSK 0886178
.                      Mod to write to Invoice pending detail file (patinpaf)
.            V11.00.06 01/07/2020  J.Tan           TSK 0892746
.                      Mod CALCAGE to use Admission date
.            V11.00.05 29/06/2020  J.Tan           TSK 0893767
.                      Mod cutoff date CALCFLAG to 01/01/2021
.            V11.00.04 11/06/2020  J.Tan           TSK 0892746
.                      Mod CALCAGE to use Discharged date
.            V11.00.03 05/05/2020  J.Tan           TSK 0891341
.                      Added TRAP prior WRPMABF
.            V11.00.02 20/04/2020 J.Tan           TSK 0890733
.                      Mod for new 2020 calculation
.            V11.00.01 06/03/2020  J.Tan           TSK 0889110
.                      Added RAPMABF prior WRPMABF
.            V10.15.03 31/12/2019  J.Tan           TSK 0886176
.                      Mod for Total Unqualified Event adjustment
.            V10.15.02 29/11/2019  J.Tan           TSK 0857392
.                      Mod for IBAHMS58 - ABF Comparison report
.            V10.15.01 30/10/2019 J.Tan    TSK 0857392
.                      Fixed Looping on ICU hours (CPPW1000)
.            V10.14.01 26/08/2018 J.Tan    TSK 0857392
.                      Mod to use TCINDC14=A for ICU bed
.                      Removed Adjust=006(Priv.Pat) and 007 (paediatric)
.                      Mod LOS to exclude Leave days
.            V10.14.00 08/05/2018 J.Tan    TSK 0857392
.                      Created
.*******************************************************************************
.
          DEFPROC   ABFPTINV
.
          INC       PATCALAG/INC
          INC       PMSPX2FD/INC
          INC       IBAPOSFD/INC
          INC       BOKRX1FD/INC
          INC       EMRPWDFD/INC
          INC       EMRURGFD/INC
          INC       EMRVCDFD/INC
          INC       OUTPWOFD/INC
          INC       PATDADFD/INC
          INC       PMSVMTFD/INC
          INC       PATECOFD/INC
          INC       PATCHCFD/INC
          INC       PATACCFD/INC
.
          INC       PMSNEPFD/INC
          INC       PMSADJFD/INC
          INC       PMSPWAFD/INC
          INC       PMSHACFD/INC
          INC       PMSHDEFD/INC
          INC       PMSHADFD/INC
          INC       PMSABFFD/INC
          INC       PMSRDIFD/INC
          INC       EMRVISFD/INC
.
ATYPCODE  DIM       3[99]
ATYPBDAY  FORM      3[99]
CALCFLAG  FORM      1
CALCTIME  FORM      6
COUNTER   FORM      3
FIRSTDAT  DIM       8
FIRSTIME  DIM       4
LASTDATE  DIM       8
LASTTIME  DIM       4
MINUTE1   FORM      3
HOUR1     FORM      6
MINUTE2   FORM      3
HOUR2     FORM      6
LASTHOUR  DIM       2
LASTMIN   DIM       2
FIRSTHR   DIM       2
FIRSTMIN  DIM       2
HOUR      DIM       2
MIN       DIM       2
DIM15     DIM       15
FORM15    FORM      15
DDRGCODE  DIM       4
DRGSDESC  DIM       80
NEPVALUE  FORM      10.4
NEWBORN   FORM      1
.
FORM2A    FORM      2
FORM6     FORM      6
FORM6A    FORM      6
FORM102   FORM      10.2
TTRNDATE  DIM       8
TTRNTIME  DIM       8
SAVATYPE  DIM       3
SAVDATE   DIM       8
SAVTIME   DIM       8
STMOVE    DIM       1
.
IPIV0000  
          CALL      IVAR0000                  * Initialise variables
.
          OPEN      PMSPX2A1,"pmspx2af"
          OPEN      PATECDA1,"patecdaf"
          OPEN      PMSVMTA1,"pmsvmtaf"
          OPEN      PATDADA1,"patdadaf"
          OPEN      EMRVISA3,"emrvisaf"
          OPEN      BOKRX1A1,"bokrx1af"
          OPEN      PATCHCO1,"patchcof"
          OPEN      PATACCA1,"pataccaf"
.
          OPEN      PMSADJA1,"pmsadjaf"
          OPEN      PMSPWAA1,"pmspwaaf"
          OPEN      PMSPWAA2,"pmspwaaf"
          OPEN      PMSHACA1,"pmshacaf"
          OPEN      PMSHDEA1,"pmshdeaf"
          OPEN      PMSHADA1,"pmshadaf"
          OPEN      PATICUA1,"paticuaf"
          OPEN      PMSRDIA4,"pmsrdiaf"
          OPEN      PATTRAN2,"pattranf"
          OPEN      PATWR1A1,"patwr1af"
          OPEN      PATECOA1,"patecoaf"
.
          MATCH     "IBAHMS58",PRGID
          IF        @EQUAL
            OPEN      PMSABFA1,FNAMEP
          ELSE
            OPEN      PMSABFA1,"pmsabfaf"
          ENDIF
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND24;*121,PTCNDGED
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,HUND30;*66,PTCNDRND
.
          MOVE      SP70,PMVXMHOS
          OPEN      PMSVX1A1,"pmsvx1af"
          PACK      KEY8,AADMNO
          CALL      RDPMVX11
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          CALL      RDPMPX21
.
          MOVE      ADATE,AGEDATE      * use Admission date
          REP       " 0",AGEDATE
          CALL      CALCAGE            * recalculate patient's age
          MOVE      PSAGEYRS,PSAGE
.
          CALL      DABF0000                  * Delete existing Patient ABF det.
.
          MOVE      SP70,PTPGSDRG
          MOVE      AFUNDH,PENDFUND           * Save patient health fund
          MOVE      SP70,AFUNDH               * Blank HF, use Claim Grouper Ver.
          CALL      GETDRG
          MOVE      PENDFUND,AFUNDH           * restore HF
          IF        OVRCD=0
            MOVE      DDRGCODE,PDRGCODE
          ENDIF
.
          MATCH     SP70,PDRGCODE
          IF        @EQUAL
            MOVE      PTMIPDRG,PDRGCODE       * Default to Admission DRG
            IF        ASTAT=3
              MATCH     SP70,PTDSDDRG
              IF        !@EQUAL
                MOVE      PTDSDDRG,PDRGCODE   * Use Discharged DRG
              ENDIF
            ENDIF
          ENDIF
          MATCH     SP70,PDRGCODE
          GOTO      IPIV9000 IF EQUAL         * DRG Codes not setup
.
          IF        ASTAT=3
            MATCH     ADATE,DDATE
            IF        @EQUAL
              MOVE      ONE,ACDAYCS           * flag for daycase
            ENDIF
          ENDIF
          CALL      CPRV0000                  * Check for Private patient
.
          MOVE      ADATE,FROMDATE
          REP       " 0",FROMDATE
          PACK      TODATE,CCC,CYY,CMM,CDD    * Use current date
.
          MOVE      ADATE,KEY8                * Use Adm.date
          IF        ASTAT=3
            MOVE      DDATE,KEY8              * Discharged date
            MOVE      DDATE,TODATE
          ENDIF
          REP       " 0",TODATE
.
          MATCH     "20210101",KEY8
          GOTO      IPIV1200 IF LESS
.
          MOVE      ONE,CALCFLAG             * New ABF calculation

          MATCH     "20220701",KEY8
          GOTO      IPIV1200 IF LESS
.
          MOVE      TWO,CALCFLAG           * 2023/2024 ABF calculation
          MATCH     "20230701",KEY8
          GOTO      IPIV1200 IF LESS
.
.         COVID19 Adjustment for events from 1/7/2023
.
          MOVE      THREE,CALCFLAG         * Disc.from 01 July 2023
.
          MATCH     "20240701",KEY8
          GOTO      IPIV1000 IF LESS
.
          MOVE      FOUR,CALCFLAG         * Disc.from 01 July 2024
.
IPIV1000  CALL      CCOV0000               * Check COVID19 ICD/DRG
.
IPIV1200  CALL      LCNT0000                  * look for a Contract ID
          BRANCH    EXIT,IPIV9000
.
          PACK      CONTRCID,CONTRCID,SP70
          MATCH     SP70,CONTRCID
          GOTO      IPIV9000 IF EQUAL         * Can not find Contract
          MOVE      CONTRCID,SAVCONTR
.
          PACK      KEY10,CONTRCID,PDRGCODE,SP70
          CALL      RDPMPWA1
          BRANCH    OVRCD,IPIV9000
.
.         Get the length of stay
.
          CALL      NHOSPDAY
          MOVE      NBRDAYS,CALDAYS
.
          MOVE      NBRDAYS,TOTDAYS           * Actual LOS
          MATCH     ADATE,DDATE
          IF        @EQUAL
            MOVE      ONE,TOTDAYS             * Daycase LOS=1
          ENDIF
.
          MOVE      NBRDAYS,LOSNDAYS          * Patient LOS
          CALL      CLOS0000                  * Check for sameday LOS
.
          CALL      UQUA0000                  * Check for Unqualified Event
.
          PACK      LSDESC1,PMPWDRGD,SP70
          PACK      LSDESC2,PDRGCODE,SP70
.
          UNPACK    IDATEF INTO FCENT1,FYEAR1,FMON1,FDAY1
          PACK      IDATET,CCC,CYY,CMM,CDD
          IF        ASTAT=3
            MOVE      DDATE,IDATET
          ENDIF
          REP       " 0",IDATET
.
          CALL      SMDC0000                  * Check State DRG and MDC
          BRANCH    EXIT,IPIV9000
          MOVE      ONE,ABFFLAG
.
          CALL      SNPW0000                  * Save Price weight value
.
.         Check for default Multiple NEP value (blank code set/indicator)
.
          MOVE      SP70,KEY2
          MOVE      SP70,D3
          CALL      MNEP0000
.
          CALL      GADJ0000                  * Get Adjustment value
.
.         Check Multiple NEP value for matching indicator on Care class
.
          MOVE      "CC",KEY2                 * Care class
          MOVE      ACARE,D3
          CALL      MNEP0000                  * Check Care Class mult.NEP values
.
.         If Multiple NEP value for matching indicator on Claim code exists,
.         this will override the NEP Value from Care class and IND22 of Cat CL
.
          MOVE      "CL",KEY2                 * Claim code
          MOVE      ACLAIM,D3
          CALL      MNEP0000                  * Check Claim code mult.NEP values
.
.         Write NEP Value to ABF file
.
          MOVE      NEPVALUE,NEPAMNTS         * NEP value Adjust.
          MOVE      NEPVALUE,FORM104          * Adjustment value
          PACK      ADJMDESC,SP70,SP70
          MOVE      "061",KEY3                * NEP adjust.
          CALL      WABF0000                  * Write to ABF Patient details
.
          CALL      CPPW0000                  * Calculate Patient weight
          CALL      GHAC0000                  * Get HAC Complexity value
.
.
          CALL      CABF0000                  * Calculate ABF amount
          MULT      NEPAMNTS,ABFCHRGE         * ABF Charge= NWAU x NEP 
          MOVE      ABFCHRGE,GROSSTOT
          MOVE      GROSSTOT,NETTOT
.
          MOVE      ZERO,EXIT
          MOVE      ZERO,NOFEE
.
          GOTO      IPIV9999
.
IPIV9000  MOVE      ONE,NOFEE                 * No ABF charge setup
          MOVE      ONE,EXIT
.
IPIV9999  GOTO      NDIP9999
+
.*******************************************************************************
.         Initialise variables
.*******************************************************************************
IVAR0000  MOVE     ZERO,PRIVFLAG
          MOVE     ZERO,COVIDFLG
          MOVE     ZERO,TUAAMNTS
          MOVE     ZERO,UQUAFLAG
          MOVE     ZERO,ATREFLAG
          MOVE     ZERO,ASPAFLAG
          MOVE     ZERO,ACCMFLAG
          MOVE     ZERO,APPSAMNT
          MOVE     ZERO,AACCAMNT
          MOVE     ZERO,ARESFLAG
          MOVE     ZERO,ASPAAMNT
          MOVE     ZERO,AINDAMNT
          MOVE     ZERO,ARESAMNT
          MOVE     ZERO,ARTAMNTS
          MOVE     ZERO,ATAAMNTS
          MOVE     ZERO,ADIAAMNT
          MOVE     ZERO,AICUAMNT
          MOVE     ZERO,AHACAMNT
          MOVE     ZERO,AC19AMNT
          MOVE     ZERO,ABFCHRGE
          MOVE     ZERO,ABFFLAG
          MOVE     ZERO,CALCFLAG
          MOVE     ZERO,NEWBORN
.
          MOVE     ZERO,CHLSCORE
          MOVE     SP70,DRGPTYPE
          PACK     DRGSDESC,SP70,SP70
          MOVE     SP70,HACGROUP
          MOVE     ZERO,ICUHOURS
          MOVE     ZERO,LOSNDAYS
          MOVE     ZERO,LOSNOICU   * LOS ICU adjusted
          MOVE     SP70,MDCNUMBR
          MOVE     ZERO,NEPAMNTS
          MOVE     ZERO,NEPVALUE
          MOVE     ZERO,PWAMOUNT
          MOVE     ZERO,PSYCBDAY
          MOVE     ZERO,OUTLFLAG
.
          MOVE     ZERO,BASELVAL   * Baseline
          MOVE     ZERO,EMRADVAL   * Emergency Admission
          MOVE     ZERO,ICUHRVAL   * ICU Hours
          MOVE     ZERO,ADTRNVAL   * Admission transfer status
          MOVE     ZERO,DRGTYVAL   * DRG Type
          MOVE     ZERO,PTSEXVAL   * Patient Sex
          MOVE     ZERO,CHSCOVAL   * Charlson scores
          MOVE     ZERO,PTAGEVAL   * Age
          MOVE     ZERO,MDCVALUE   * MDC
.
          MOVE     ZERO,ICUFLAG
          MOVE     ZERO,PPAYABLE   * Patient Balance payable
          MOVE     ZERO,REBTOT
          MOVE     ZERO,TOTXTRA
          MOVE     ZERO,SAMTG
          MOVE     ZERO,ACDAYCS    * Assume not a day case
.
          MOVE      ZERO,LINETOT
          MOVE      ZERO,LINEGST
          MOVE      ZERO,LSTRATE
          PACK      LSDESC1,SP70
          PACK      LSDESC2,SP70
          MOVE      SP70,PDRGCODE
          PACK      ADJMDESC,SP70,SP70
.
          MOVE      ONE,F3
          WHILE     F3 <= 100
            MOVE      ZERO,COMPLVAL[F3]
            ADD       ONE,F3
          DO
.
          MOVE      ONE,F2
          WHILE     F2 < 99 
            MOVE      SP70,ATYPCODE[F2]
            MOVE      SP70,ATYPBDAY[F2]
            ADD       ONE,F2
          DO
.
IVAR9999  RETURN
+
.*******************************************************************************
.         Check for COVID19 ICD10/DRG
.         ICD10-AM ED 12 Disease code/DRGs
.*******************************************************************************
CCOV0000  PACK      KEY13,AADMNO,SP70
          CALL      RSPTECD1
CCOV1000  CALL      RKPTECD1
          BRANCH    OVRCD,CCOV9999
.
          MATCH     DAADMNO,DPTEDADM
          GOTO      CCOV9999 IF NOT EQUAL    * Different admission
.
          MATCH     "U07.12",PTEDCODE   * Coronavirus Disease(virus identified)?
          GOTO      CCOV2000 IF EQUAL  
          MATCH     "U07.2",PTEDCODE    * Coronavirus disease(not identified)?
          GOTO      CCOV1000 IF NOT EQUAL
.
.         Discharges from 01/07/2024 either DRG T63A, T63B
.
CCOV2000  COMPARE   FOUR,CALCFLAG
          GOTO      CCOV4000 IF NOT LESS
.
.         Check DRGs
.
          MATCH     "A40Z",PDRGCODE
          GOTO      CCOV9000 IF EQUAL  
          MATCH     "D63A",PDRGCODE
          GOTO      CCOV9000 IF EQUAL  
          MATCH     "D63B",PDRGCODE
          GOTO      CCOV9000 IF EQUAL  
          MATCH     "D66A",PDRGCODE
          GOTO      CCOV9000 IF EQUAL  
          MATCH     "D66B",PDRGCODE
          GOTO      CCOV9000 IF EQUAL  
          MATCH     "E67A",PDRGCODE
          GOTO      CCOV9000 IF EQUAL  
          MATCH     "E67B",PDRGCODE
          GOTO      CCOV9000 IF EQUAL  
          MATCH     "T62A",PDRGCODE
          GOTO      CCOV9000 IF EQUAL  
          MATCH     "T62B",PDRGCODE
          GOTO      CCOV9000 IF EQUAL  
.
CCOV4000  MATCH     "T63A",PDRGCODE
          GOTO      CCOV9000 IF EQUAL  
          MATCH     "T63B",PDRGCODE
          GOTO      CCOV9999 IF NOT EQUAL
.
CCOV9000  MOVE      ONE,COVIDFLG
CCOV9999  RETURN
+
.*******************************************************************************
.         Look for a Contract ID for the DRG Code
.*******************************************************************************
LCNT0000  PACK      KEY9,ACLAIM,AFUNDH,SP70
          CALL      GETCNEFF               * Get Contract Effective From
          BRANCH    EXIT,LCNT9000
.
          PACK      CEFFDATE,CCC,CYY,CMM,CDD   * default to Current date
          REP       " 0",CEFFDATE
          IF        ASTAT=3
            MOVE      DDATE,CEFFDATE       * use Discharged date
          ENDIF
.
          IF        ASTAT=3
            MOVE      DDATE,KEY8           * Discharged date
          ELSE
            PACK      KEY8,CCC,CYY,CMM,CDD * Use Current date
            REP       " 0",KEY8
          ENDIF
          LOAD      CEFFDATE,CNTRCEFR,ADATE,KEY8
.
          PACK      KEY10,PDRGCODE,SP70
          CALL      RSPMPWA2
LCNT1000  CALL      RKPMPWA2
          BRANCH    OVRCD,LCNT9000
.
          MATCH     PMPWDRGC,PDRGCODE
          GOTO      LCNT9000 IF NOT EQUAL     * Different DRG Code
.
          MOVE      PMPWCNTR,KEY6
          CALL      VALCONTR                * Validate Contract ID
          BRANCH    EXIT,LCNT1000
.
          MOVE      PMPWCNTR,CONTRCID
          MOVE      ZERO,EXIT
          GOTO      LCNT9999
.
LCNT9000  MOVE      ONE,EXIT
LCNT9999  RETURN
+
.*******************************************************************************
.         Deleting existing ABF Patient details
.*******************************************************************************
DABF0000  PACK      KEY19,AADMNO,SP70
          CALL      RSPMABF1
          CALL      RKPMABF1
          BRANCH    OVRCD,DABF9999
.
          MATCH     DAADMNO,PMABADMN       * Same admission no ?
          GOTO      DABF9999 IF NOT EQUAL
.
          MATCH     SP70,PMABINVN
          GOTO      DABF9999 IF NOT EQUAL  * Invoice must be blank
.
          PACK      KEY19,PMABADMN,PMABINVN,PMABADJT
          CALL      DEPMABF1
          GOTO      DABF0000
.
DABF9999  RETURN
+
.*******************************************************************************
.         Check State DRG and MDC
.*******************************************************************************
SMDC0000  OPEN      PATPGRA1,"patpgraf"
          OPEN      PATDGWA1,"patdgwaf"
.
          MATCH     "1",PTCNDGED
          GOTO      SMDC1000 IF EQUAL
.
          MATCH     SP6,AFUNDH
          IF        !@EQUAL
            OPEN      PATFN1A1,"patfn1af"
            PACK      KEY14,AFUNDH,ZERO,ZERO,ZERO,ZERO,SP20
            CALL      RDFUND1                 * Read a Health Fund file record
          ENDIF
.
.         Get the grouper version from health fund
.
          PACK      KEY5,ANSC,ANSL,ACLAIM
          CALL      RDCODE1
          IF        OVRCD=0
            MOVE      PTCDGRPV,PTHFGRPV      * Use claim code grouper version
          ENDIF
          GOTO      SMDC2000
.
.        Get DRG Version using 'Effective DRG' table (NEW)
.
SMDC1000  MOVE      AFUNDH,D6                 * Save patient health fund
          MOVE      SP70,AFUNDH               * Blank HF, use Claim Grouper Ver.
          CALL      GTEDRG00
          MOVE      D6,AFUNDH                 * restore HF
          IF        EXIT=0
            MOVE      KEY3,PTHFGRPV
          ENDIF
.
SMDC2000  MATCH     SP70,PTHFGRPV
          IF        @EQUAL
            READ      CONTROLF,HUND05;*158,PTCNDGPV
            MOVE      PTCNDGPV,PTHFGRPV         * Use the Default Grouper vers.
          ENDIF
.
          COMPARE   THREE,ASTAT
          GOTO      SMDC3000 IF EQUAL
.
SMDC2200  MATCH     SP70,PTMIPDRG
          GOTO      SMDC9000 IF EQUAL       * Blank Provisional DRG
.
          PACK      KEY7,PTMIPDRG,PTHFGRPV,SP70
          CALL      RDPTDGW1
          BRANCH    OVRCD,SMDC9000
.
          SQUEEZE   PTDWMDCC
          UNPACK    PTDWMDCC,MDCNUMBR       * Patient MDC number
          GOTO      SMDC8000
.
.         Patient is discharged, check if coded.
.
SMDC3000  PACK      KEY13,AADMNO,SP70
          CALL      RSPTECD1
          CALL      RKPTECD1
          BRANCH    OVRCD,SMDC2200          * not coded, use Provisional DRG
.
          MATCH     DAADMNO,DPTEDADM
          GOTO      SMDC2200 IF NOT EQUAL   * Different admission
.
          MOVE      AADMNO,PTPGADMN
          MOVE      ONE,PTPGEPNO
          PACK      KEY13,AADMNO,PTPGEPNO,PTHFGRPV,SP70
          CALL      RDPTPGR1
          BRANCH    OVRCD,SMDC2200
.
          SQUEEZE   PTPGNMDC
          UNPACK    PTPGNMDC,MDCNUMBR       * Patient MDC number
.
          MATCH     SP70,PTPGSDRG           * State DRG
          IF        !@EQUAL
            PACK      KEY7,PTPGSDRG,PTHFGRPV,SP70
            CALL      RDPTDGW1
            IF        OVRCD=0
              PACK      DRGSDESC,PTDWDESC,SP70   * State DRG description
            ENDIF
          ENDIF
.
SMDC8000  PACK      MDCNUMBR,MDCNUMBR,SP70
          MATCH     SP70,MDCNUMBR
          GOTO      SMDC9000 IF EQUAL       * MDC number not setup
.
          MOVE      PTDWDRGT,DRGPTYPE       * Patient DRG Partition Type
          MOVE      ZERO,EXIT
          GOTO      SMDC9999
.
SMDC9000  MOVE      ONE,EXIT
SMDC9999  RETURN
+
.*******************************************************************************
.         Check for Unqualified Event
.*******************************************************************************
UQUA0000  MATCH     SP70,ACARE
          GOTO      UQUA8000 IF EQUAL
.
          MOVE      "CC",KEY2
          PACK      KEY5,KEY2,ACARE
          CALL      RDCODE1
          BRANCH    OVRCD,UQUA4000
.
          COMPARE   THREE,CALCFLAG
          GOTO      UQUA1200 IF LESS        * Discharged before 01/07/2023
.
          MATCH     "7",PTCDUDF4            * Check if this is a newborn
          GOTO      UQUA1000 IF EQUAL
          MATCH     "7",PTCDUDF6
          GOTO      UQUA1200 IF NOT EQUAL
.
UQUA1000  MOVE      ONE,NEWBORN
.
UQUA1200  MATCH     "5",TCINDC9
          GOTO      UQUA4000 IF NOT EQUAL   * qualified
.
.         Current status is unqualified
.         Check if the patient were unqualified throughout the stay
.
          PACK      KEY24,AADMNO,SP70
          CALL      RDSCHCO1
UQUA2000  CALL      RDKCHCO1
          BRANCH    OVRCD,UQUA9000     * no change of Care class
.
          MATCH     AADMNO,CHADMN
          GOTO      UQUA9000 IF NOT EQUAL
.
          MATCH     "CC",CHCATG
          GOTO      UQUA2000 IF NOT EQUAL * Not a care class change
.
          PACK      KEY5,CHCATG,CHCODE
          CALL      RDCODE1
          BRANCH    OVRCD,UQUA2000
.
          MATCH     "5",TCINDC9
          GOTO      UQUA2000 IF EQUAL  * Unqualified care type
.
.         found a qualified care type
.
UQUA4000  COMPARE   ONE,NEWBORN
          GOTO      UQUA8000 IF NOT EQUAL
.
.         A newborn with qualified care
.
.         for same day patient with any qualified care type during the period
.         causes the whole period to be qualified
.
          MATCH     ADATE,DDATE
          GOTO      UQUA8000 IF EQUAL
.
.         Billable LOS will be just the Qualified days
.
          CALL      QUAD0000           * Calculate Qualified days
          BRANCH    OVRCD,UQUA9200     * Total unqualified
.
UQUA8000  MOVE      ZERO,UQUAFLAG      * Qualified
          MOVE      ONE,TUAAMNTS
          GOTO      UQUA9999
.
.         Total Unqualified
.
UQUA9000  IF        NEWBORN=1
            MOVE      ZERO,LOSNDAYS    * Newborn without Qualified period
          ENDIF
.
UQUA9200  MOVE      ZERO,FORM104       * Unqualified event value adjust.
          MOVE      "066",KEY3         * Unqualified event adjustment
          MOVE      "Total Unqualified Admission Adjustment",ADJMDESC
          CALL      WABF0000           * Write to ABF Patient details
          PACK      ADJMDESC,SP70,SP70
.
          MOVE      ONE,UQUAFLAG       * Total Unqualified event
          MOVE      ZERO,ABFCHRGE      * zero ABF Charge
.
UQUA9999  RETURN
+
.*******************************************************************************
.         Calculate Qualified days for Newborn 
.         Cat CC TCINDC9 = 5 for Unqualified, <> 5 for Qualified
.*******************************************************************************
QUAD0000  MOVE      ZERO,LOSNDAYS      * Billable LOS
          MOVE      ADATE,FROMDATE     * Admission date
          MOVE      SP70,SAVDATE
          MOVE      SP70,KEY1
.
          MOVE      ZERO,F1
          PACK      KEY24,AADMNO,SP70
          CALL      RDSCHCO1
QUAD2000  CALL      RDKCHCO1
          BRANCH    OVRCD,QUAD6000     * no change of Care class
.
          MATCH     AADMNO,CHADMN
          GOTO      QUAD6000 IF NOT EQUAL
.
          MATCH     "CC",CHCATG
          GOTO      QUAD2000 IF NOT EQUAL * Not a care class change
.
          PACK      KEY5,CHCATG,CHCODE
          CALL      RDCODE1
          BRANCH    OVRCD,QUAD2000
          MOVE      SP70,D1
          MATCH     "5",TCINDC9
          IF        @EQUAL
            UNPACK    TCINDC9,D1          * save the unqualified indicator
          ENDIF
.
          MOVE      ONE,F1                * found a care class change record
          MATCH     SAVDATE,CHDATE
          GOTO      QUAD3000 IF NOT EQUAL
.
          MATCH     KEY1,D1
          GOTO      QUAD2000 IF EQUAL   * same movement
.
QUAD3000  MATCH     "5",D1
          GOTO      QUAD3200 IF EQUAL  * unqualified
.
          MOVE      CHDATE,TODATE      * end of Qualified day
          MATCH     FROMDATE,TODATE
          IF        !@EQUAL
            CALL      NHOSPDAY         * LOS without leave
            ADD       NBRDAYS,LOSNDAYS * Qualified days
          ENDIF
.
QUAD3200  MOVE      CHDATE,FROMDATE    * save end date 
          MOVE      CHDATE,SAVDATE
          MOVE      D1,KEY1
          GOTO      QUAD2000
.
QUAD6000  PACK      TODATE,CCC,CYY,CMM,CDD
          IF        ASTAT=3
            MOVE      DDATE,TODATE         * Discharged date
          ENDIF
          REP       " 0",TODATE
.
          MOVE      "CC",KEY2
          PACK      KEY5,KEY2,ACARE
          CALL      RDCODE1
          BRANCH    OVRCD,QUAD9000
.
          COMPARE   ZERO,F1
          GOTO      QUAD6200 IF EQUAL
.
          COMPARE   ZERO,LOSNDAYS
          GOTO      QUAD8000 IF NOT EQUAL    * found qualified days
.
QUAD6200  MATCH     "5",TCINDC9          * check for qualified care
          GOTO      QUAD9200 IF EQUAL    * total Unqualified
.
          GOTO      QUAD8800
.
.         the number of Qualified days from Change record file is not zero
.
QUAD8000  MATCH     "5",TCINDC9          * check for qualified care
          GOTO      QUAD9000 IF EQUAL    * currently unqualified
.
QUAD8800  MATCH     FROMDATE,TODATE
          IF        !@EQUAL
            CALL      NHOSPDAY           * LOS without leave
            ADD       NBRDAYS,LOSNDAYS
          ENDIF
.
QUAD9000  MOVE      ZERO,OVRCD
          GOTO      QUAD9999
.
QUAD9200  MOVE      ONE,OVRCD          * Total unqualified
.
QUAD9999  RETURN
+
.*******************************************************************************
.         Save Price weight value to Patient details
.*******************************************************************************
SNPW0000  MOVE      "001",KEY3             * Same-day Payment list Adjustment
          MOVE      ZERO,FORM104           * 0 for No
          MATCH     "1",PMPWSDPY           * Same day payment list ?
          IF        @EQUAL
            MOVE      ONE,FORM104          * 1 for Yes
          ENDIF
          CALL      WABF0000               * Write to ABF Patient details
.
          MOVE      "002",KEY3             * Bundle ICU Adjustment
          MOVE      ZERO,FORM104           * 0 for No
          MATCH     "1",PMPWBICU           * Bundle ICU ?
          IF        @EQUAL
            MOVE      ONE,FORM104          * 1 for Yes
          ENDIF
          CALL      WABF0000               * Write to ABF Patient details
.
          MOVE      "003",KEY3             * LOS Adjustment
          MOVE      PMPWALOS,FORM104
          CALL      WABF0000               * Write to ABF Patient details
.
          MOVE      "004",KEY3             * Lower bound Adjustment
          MOVE      PMPWLOWB,FORM104
          CALL      WABF0000               * Write to ABF Patient details
.
          MOVE      "005",KEY3             * Upper bound Adjustment
          MOVE      PMPWUPPB,FORM104
          CALL      WABF0000               * Write to ABF Patient details
.
          IF        PRIVFLAG=1
            MOVE      "006",KEY3           * Private Patient Service Adjustment
            MOVE      PMPWPSRV,FORM104
            IF        FORM104<>0
              MOVE      "Private Pt Service Adjustment",ADJMDESC
              CALL      WABF0000           * Write to ABF Patient details
              PACK      ADJMDESC,SP70,SP70
              MOVE      PMPWPSRV,APPSAMNT  * Private Patient Service adjust.
              IF        APPSAMNT>0
                DIV       "100",APPSAMNT
              ENDIF
            ENDIF
          ENDIF
.
.         MOVE      "007",KEY3             * Paediatric Adjustment
.         MOVE      PMPWPAED,FORM104
.         IF        FORM104<>0
.           CALL      WABF0000             * Write to ABF Patient details
.         ENDIF
.
          MOVE      "008",KEY3             * Same day
          MOVE      PMPWSDAY,FORM104
          CALL      WABF0000               * Write to ABF Patient details
.
          MOVE      "009",KEY3             * Short Stay PW Adjustment
          MOVE      PMPWSSOB,FORM104
          CALL      WABF0000               * Write to ABF Patient details
.
          MOVE      "010",KEY3             * Short Stay perdiem PW Adjustment
          MOVE      PMPWSSOP,FORM104
          CALL      WABF0000               * Write to ABF Patient details
.
          MOVE      "011",KEY3             * Inlier PW Adjustment
          IF        PMPWINLI<>0
            MOVE      PMPWINLI,FORM104
            CALL      WABF0000             * Write to ABF Patient details
          ENDIF
.
          MOVE      "012",KEY3             * Long stay per diem PW Adjustment
          MOVE      PMPWLSOP,FORM104
          CALL      WABF0000               * Write to ABF Patient details
.
SNPW9999  RETURN
+
.*******************************************************************************
.         Check LOS for Same day LOS
.*******************************************************************************
CLOS0000  COMPARE   ZERO,LOSNDAYS
          GOTO      CLOS9999 IF NOT EQUAL
.
          COMPARE   TWO,ASTAT
          GOTO      CLOS9999 IF NOT EQUAL    * Not currently admitted 
.
.         LOS =0, if Intended Stay is 'Same day' (Cat VI ind1=2)
.         LOS = Planned LOS (ASTAY) if 'Overnight' (Cat VI ind1=1)
.
          MOVE      PMVXIDUS,KEY3
          MATCH     SP70,PMVXIDUS
          IF        @EQUAL
            PACK      KEY8,AADMNO
            CALL      RDBKRX11
            IF        OVRCD=0
              MOVE      BKRXUDF5,KEY3
            ENDIF
          ENDIF
.
          MATCH     SP70,KEY3            * Check Intended Stay
          GOTO      CLOS9999 IF EQUAL
.
          MOVE      "VI",KEY2
          PACK      KEY5,KEY2,KEY3
          CALL      RDCODE1
          BRANCH    OVRCD,CLOS9999
.
          MATCH     "2",TCINDC1
          GOTO      CLOS9000 IF EQUAL   * Same day patient
          MATCH     "1",TCINDC1
          GOTO      CLOS9999 IF NOT EQUAL
.
          MOVE      ASTAY,LOSNDAYS    * Use Planned LOS
          GOTO      CLOS9999
.
CLOS9000  MOVE      ZERO,LOSNDAYS     * Same day
CLOS9999  RETURN
+
.*******************************************************************************
.         Calculate Patient PW
.*******************************************************************************
CPPW0000  COMPARE   ZERO,LOSNDAYS
          GOTO      CPPW0200 IF NOT EQUAL
.
          IF        NEWBORN=1
            MATCH     ADATE,DDATE
            GOTO      CPPW0200 IF NOT EQUAL
.
.         for same day patient with any qualified care type during the period
.         causes the whole period to be qualified
.
            BRANCH    UQUAFLAG,CPPW9000 * Daycase Newborn without Qualified days
          ENDIF
.
          IF        ASTAT=3
            MOVE      "CC",KEY2
            PACK      KEY5,KEY2,ACARE
            CALL      RDCODE1
            IF        OVRCD=0
              MATCH     "1",PTCDUDF6          * Acute Care
              IF        @EQUAL
                MOVE      ONE,LOSNDAYS
              ENDIF
            ENDIF
          ENDIF
.
.         if Payment list = No and Patient is admitted today, 
.           do not display ABF charges
.
          MATCH     "1",PMPWSDPY             * Same day payment list ?
          IF        !@EQUAL
            PACK      KEY8,CCC,CYY,CMM,CDD
            REP       " 0",KEY8
            MATCH     ADATE,KEY8
            GOTO      CPPW9000 IF EQUAL      * Patient admitted today
            MOVE      ONE,LOSNDAYS           * LOS =1
            GOTO      CPPW0200
          ELSE
            MOVE      PMPWSDAY,PWAMOUNT      * Use same day PW
            MOVE      ONE,LOSNDAYS           * LOS =1
            MOVE      ONE,LOSNOICU
            GOTO      CPPW9000
          ENDIF
.
CPPW0200  MOVE      LOSNDAYS,LOSNOICU
          COMPARE   THREE,CALCFLAG
          GOTO      CPPW1000 IF LESS
.
          IF        ICUHOURS<>0 & LOSNDAYS <>0
            MOVE      ICUHOURS,FORM104
            DIV       "24",FORM104
            MOVE      ZERO,F8
            CALL      RMDP0000         * ignore decimal places after hours
            SUB       F8,LOSNOICU      * LOS with ICU adjusted
            IF        LOSNOICU<=0
              MOVE      ONE,LOSNOICU
            ENDIF
          ENDIF
.
.         Check Lower and Upper bound
.
CPPW1000  COMPARE   PMPWLOWB,LOSNOICU
          GOTO      CPPW4000 IF LESS       * less than lower bound
.
          COMPARE   LOSNOICU,PMPWUPPB
          GOTO      CPPW6000 IF LESS       * > Upper bound
.
.         If LOS within Lower bound and Upper bound, use the Inlier PW
.
          MOVE      PMPWINLI,PWAMOUNT      * Inlier PW
          GOTO      CPPW9000
.
.         If LOS < Lower bound, use Short stay outlier PW + (Short Stay Perdiem
.                   rate PW x LOS)
.
CPPW4000  MOVE      PMPWSSOP,PWAMOUNT      * Short Stay perdiem
          IF        CALCFLAG<3
            MULT      LOSNDAYS,PWAMOUNT
          ELSE
            MULT      LOSNOICU,PWAMOUNT    * Use LOS ICU adjusted
            MOVE      ONE,OUTLFLAG
          ENDIF
          ADD       PMPWSSOB,PWAMOUNT      * Short Stay Outlier PW
.
          GOTO      CPPW9000
.
.         If LOS > Upper bound, use the Inlier PW + (Long Stay perdiem 
.                   rate PW x Additional days)
.             Addtional days = LOS - Upper Bound
.
CPPW6000  MOVE      ZERO,FORM4
          IF        CALCFLAG<3
            MOVE      LOSNDAYS,FORM4
          ELSE
            MOVE      LOSNOICU,FORM4       * Use LOS ICU adjusted
            MOVE      ONE,OUTLFLAG
          ENDIF
          SUB       PMPWUPPB,FORM4         * Additional days
.
          MOVE      PMPWLSOP,PWAMOUNT      * Long Stay perdiem
          IF        FORM4>0
            MULT      FORM4,PWAMOUNT
          ENDIF
          ADD       PMPWINLI,PWAMOUNT      * Inlier PW
.
CPPW9000  MOVE      PWAMOUNT,FORM104
          MOVE      "Price Weight",ADJMDESC
          MOVE      "062",KEY3             * Patient PW
          CALL      WABF0000               * Write to ABF Patient details
.
          MOVE      LOSNDAYS,FORM104
          MOVE      "Billable LOS",ADJMDESC
          MOVE      "064",KEY3
          CALL      WABF0000               * Write to ABF Patient details
          PACK      ADJMDESC,SP70,SP70
.
          COMPARE   THREE,CALCFLAG
          GOTO      CPPW9999 IF LESS
.
          COMPARE   ZERO,ICUHOURS
          GOTO      CPPW9999 IF EQUAL      * No ICU hours
.
          MOVE      LOSNOICU,FORM104
          PACK      ADJMDESC,SP70,SP70
          CLEAR     ADJMDESC
          APPEND    "LOS ICU Adjusted",ADJMDESC
          IF        OUTLFLAG=1
            APPEND    " (Short/Long-stay outlier)",ADJMDESC
          ENDIF
          APPEND    SP70,ADJMDESC
          RESET     ADJMDESC
          MOVE      "074",KEY3
          CALL      WABF0000               * Write to ABF Patient details
          PACK      ADJMDESC,SP70,SP70
.
CPPW9999  RETURN
+
.*******************************************************************************
.         Remove decimal places after hours
.*******************************************************************************
RMDP0000  MOVE      SP70,KEY15
          MOVE      FORM104,KEY15
.
          SQUEEZE   KEY15
          PACK      KEY15,KEY15,SP70
          RESET     KEY15
.
          CLEAR     DIM15
          GOTO      RMDP1200
.
RMDP1000  BUMP      KEY15,1
          GOTO      RMDP3000 IF EOS
.
RMDP1200  MOVE      KEY15,ANS
.
          CMATCH    SP1,KEY15
          GOTO      RMDP1000 IF EQUAL
.
          CMATCH    ".",KEY15
          GOTO      RMDP3000 IF EQUAL
.
          APPEND    ANS,DIM15             * append the hour
          GOTO      RMDP1000
.
RMDP3000  APPEND    SP70,DIM15
          RESET     DIM15
.
          MOVE      ZERO,F8
          MATCH     SP70,DIM15
          GOTO      RMDP9999 IF EQUAL
.
          SQUEEZE   DIM15
          MOVE      ZERO,FORM15
          MOVE      DIM15,FORM15
          MOVE      FORM15,F8
.
RMDP9999  RETURN
+
.*******************************************************************************
.         Get Adjustment value
.*******************************************************************************
.         No Psychiatric age Adj.after 1/7/2022
.
GADJ0000  IF        CALCFLAG<2
            CALL      CPSY0000              * Check Psychiatric day
          ENDIF
.
          PACK      KEY9,CONTRCID,SP70
          CALL      RSPMADJ1
GADJ1000  CALL      RKPMADJ1
          BRANCH    OVRCD,GADJ9999
.
          MATCH     CONTRCID,PMAJCNTR      * Same contract id ?
          GOTO      GADJ9999 IF NOT EQUAL
.
          CALL      CADJ0000               * Check adjustment value
          GOTO      GADJ1000
.
GADJ9999  RETURN
+
.*******************************************************************************
.         Check Bed transfer, Cat CG for Psychiatric
.*******************************************************************************
CPSY0000  MOVE      ZERO,FORM2
.
CPSY0200  ADD       ONE,FORM2
          BRANCH    FORM2,CPSY1000,CPSY2000
          GOTO      CPSY9000
.
.         Check bed type transfer - Cat BT Indic 3 = A
.
CPSY1000  MOVE      ONE,F1                 * calculate number of days
          CALL      CBTR0000               * check bed trans for Psychiatric
          GOTO      CPSY8000
.
.         Check Ward/Bed - Cat CG Indic 26 = A
.
CPSY2000  MOVE      THREE,F1               * Check for Cat CG (ward class.)
          CALL      CBTR0000               * check bed trans for Psychiatric
          GOTO      CPSY8000
.
CPSY8000  MATCH     ADATE,DDATE
          IF        @EQUAL
            IF       ASPAFLAG=1
              MOVE     ONE,NBRDAYS
              MOVE     ONE,PSYCBDAY        * Psychiatric day case
              GOTO     CPSY9000
            ENDIF
          ELSE
            IF        NBRDAYS>0
              MOVE      NBRDAYS,PSYCBDAY
              GOTO      CPSY9000
            ENDIF
          ENDIF
          BRANCH    FORM2,CPSY0200,CPSY0200
.
CPSY9000  MOVE      ZERO,ASPAFLAG          * reinitialise ASPA Flag
CPSY9999  RETURN
+
.*******************************************************************************
.         Adjustment
.*******************************************************************************
CADJ0000  MOVE      ZERO,ADJTYVAL
          MOVE      PMAJADJT,ADJTYVAL         * Adjustment type
.
          BRANCH    ADJTYVAL,CADJ1000,CADJ1000,CADJ1000,CADJ2000,CADJ2000:
                             CADJ9999,CADJ2000,CADJ9999,CADJ3000,CADJ4000:
                             CADJ5000,CADJ6000,CADJ6200,CADJ6200
.
          MOVE      ADJTYVAL,F3
          SUB       "22",F3
          BRANCH    F3,CADJ6300,CADJ6300:
                       CADJ7000,CADJ7000,CADJ7000,CADJ7200 * NEP
.
          GOTO      CADJ9999
.
.         Specialist Psychiatric Age adjustment
.
CADJ1000  COMPARE   ZERO,PSYCBDAY
          GOTO      CADJ9999 IF EQUAL      * Zero Psychiatric days
.
          BRANCH    ASPAFLAG,CADJ9999
          CALL      ASPA0000         * Psychiatric Age Adjustment
          GOTO      CADJ9999
.
CADJ2000  BRANCH    ARESFLAG,CADJ9999
          CALL      ARES0000        * Residential Remoteness Area adjustment
          GOTO      CADJ9999
.
CADJ3000  CALL      AIND0000        * Indigenous Adjustment
          GOTO      CADJ9999
.
CADJ4000  MOVE      " 1",DIM2       * 1 for Radiotherapy
          CALL      ARTD0000        * Radiotherapy/Dialysis Adjustment
          GOTO      CADJ9999
.
CADJ5000  MOVE      " 2",DIM2       * 2 for Dialysis
          CALL      ARTD0000        * Radiotherapy/Dialysis Adjustment
          GOTO      CADJ9999
.
CADJ6000  CALL      AICU0000        * Intensive Care Unit Adjustment
          GOTO      CADJ9999
.
CADJ6200  IF        PRIVFLAG=1
            CALL      ACCM0000       * Private Patient Accommodation Adjustment
          ENDIF
          GOTO      CADJ9999
.
CADJ6300  BRANCH    ATREFLAG,CADJ9999
          CALL      ETRE0000            * Patient Treatment remoteness area adj.
          GOTO      CADJ9999
.
CADJ7000  CALL      ANEP0000        * National Efficient Price (NEP)
          GOTO      CADJ9999
.
CADJ7200  CALL      COVD0000        * COVID19 Adjustment
          GOTO      CADJ9999
.
CADJ9999  RETURN
+
.*******************************************************************************
.         Check Psychiatric Age Adjustment
.*******************************************************************************
ASPA0000  MOVE      PMAJADJV,FORM104       * Adjustment value
.
          COMPARE   "17",PSAGE
          GOTO      ASPA3000 IF EQUAL
          GOTO      ASPA3000 IF LESS       * Patient age < 17
.
          MATCH     "19",MDCNUMBR
          GOTO      ASPA9999 IF EQUAL
          MATCH     "20",MDCNUMBR
          GOTO      ASPA9999 IF EQUAL
.
          COMPARE   THREE,ADJTYVAL
          GOTO      ASPA9999 IF NOT EQUAL
.
          MOVE      PMAJADJV,ASPAAMNT      * Age value adjust.
          MOVE      "015",KEY3             * Psychiatric >17 no MDC Adj.
          CALL      WABF0000               * Write to ABF Patient details
          MOVE      ONE,ASPAFLAG
          GOTO      ASPA9999
.
.         Age <=17
.
ASPA3000  MATCH     "19",MDCNUMBR
          GOTO      ASPA4000 IF EQUAL
          MATCH     "20",MDCNUMBR
          GOTO      ASPA4000 IF EQUAL
.
          COMPARE   TWO,ADJTYVAL
          GOTO      ASPA9999 IF NOT EQUAL
.
          MOVE      PMAJADJV,ASPAAMNT      * Age value adjust.
          MOVE      "014",KEY3             * Psychiatric <=17 with MDC Adj.
          CALL      WABF0000               * Write to ABF Patient details
          MOVE      ONE,ASPAFLAG
          GOTO      ASPA9999
.
ASPA4000  COMPARE   ONE,ADJTYVAL
          GOTO      ASPA9999 IF NOT EQUAL
.
          MOVE      PMAJADJV,ASPAAMNT      * Age value adjust.
          MOVE      "013",KEY3             * Psychiatric <=17 no MDC Adj.
          CALL      WABF0000               * Write to ABF Patient details
          MOVE      ONE,ASPAFLAG
.
ASPA9999  RETURN
+
.*******************************************************************************
.         Check Residential remoteness area adjustment
.*******************************************************************************
ARES0000  MOVE      PMAJADJV,FORM104       * Adjustment value
.
          OPEN      IBAPOST1,"ibapostf"
          PACK      KEY45,PSUBRB,SP70
          PACK      KEY56,PMVXPOST,KEY45,PADD4,SP70
          CALL      RDIBPOS1
          COMPARE   ZERO,OVRCD
          GOTO      ARES2000 IF EQUAL
.
          PACK      KEY56,PMVXPOST,KEY45,SP70
          CALL      RSIBPOS1
          CALL      RKIBPOS1
          BRANCH    OVRCD,ARES9999
.
          MATCH     PMVXPOST,IBPOPCOD
          GOTO      ARES9999 IF NOT EQUAL     * Different postcode
.
ARES2000  MATCH     SP70,IBPORRAV
          GOTO      ARES9999 IF EQUAL
.
          MATCH     "R2   ",IBPORRAV
          IF        @EQUAL
            IF        ADJTYVAL=4
              MOVE      "016",KEY3           * Patient remoteness area adjust.
              CALL      WABF0000             * Write to ABF Patient details
              MOVE      PMAJADJV,ARESAMNT    * Residential value adjust.
              MOVE      ONE,ARESFLAG
            ENDIF
          ELSE
            MATCH     "RA3  ",IBPORRAV
            IF        @EQUAL
              IF        ADJTYVAL=5
                MOVE      "017",KEY3         * Patient remoteness area adjust.
                CALL      WABF0000           * Write to ABF Patient details
                MOVE      PMAJADJV,ARESAMNT  * Residential value adjust.
                MOVE      ONE,ARESFLAG
              ENDIF
            ELSE
              MATCH     "RA4  ",IBPORRAV
              IF        @EQUAL
                IF        ADJTYVAL=7
                  MOVE      "019",KEY3       * Patient remoteness area adjust.
                  CALL      WABF0000         * Write to ABF Patient details
                  MOVE      PMAJADJV,ARESAMNT  * Residential value adjust.
                  MOVE      ONE,ARESFLAG
                ENDIF
              ENDIF
            ENDIF
          ENDIF
ARES9999  RETURN
+
.*******************************************************************************
.         Check Indigenous Adjustment
.*******************************************************************************
AIND0000  MOVE      PMAJADJV,FORM104       * Adjustment value
          MATCH     SP70,PMVXABRG          * Aboriginality
          GOTO      AIND9999 IF EQUAL
          PACK      KEY5,ANSV,ANSA,PMVXABRG
          CALL      RDCODE1
          BRANCH    OVRCD,AIND9999
.
          MATCH     "4",TCINDC2
          GOTO      AIND9999 IF EQUAL
          MATCH     "9",TCINDC2
          GOTO      AIND9999 IF EQUAL
.
          MOVE      PMAJADJV,AINDAMNT  * Indigeneous value adjust.
          MOVE      "021",KEY3         * Indigeneous adjust.
          CALL      WABF0000           * Write to ABF Patient details
.
AIND9999  RETURN
+
.*******************************************************************************
.         Check COVID19 treatment Adjustment
.*******************************************************************************
COVD0000  COMPARE   ONE,COVIDFLG
          GOTO      COVD9999 IF NOT EQUAL
.
          MOVE      PMAJADJV,FORM104   * Adjustment value
          MOVE      PMAJADJV,AC19AMNT  * Covid19 value adjust.
          MOVE      "071",KEY3         * Covid19 adjust.
          MOVE      "COVID19 Treatment Adjustment",ADJMDESC
          CALL      WABF0000           * Write to ABF Patient details
          PACK      ADJMDESC,SP70,SP70
.
COVD9999  RETURN
+
.*******************************************************************************
.         Check Radiotherapy/Dialysis Adjustment
.*******************************************************************************
ARTD0000  MOVE      PMAJADJV,FORM104       * Adjustment value
          PACK      KEY13,AADMNO,SP70
          CALL      RSPTECO1
ARTD1000  CALL      RKPTECO1
          BRANCH    OVRCD,ARTD9999
          MATCH     DPTEOADM,DAADMNO
          GOTO      ARTD9999 IF NOT EQUAL
.
          MOVE      "1",KEY1           * for ICD10
          PACK      KEY29,CONTRCID,KEY1,PTEOCODE,SP70
          CALL      RSPMRDI4
ARTD2000  CALL      RKPMRDI4
          BRANCH    OVRCD,ARTD1000
.
          MATCH     CONTRCID,PMRICNTR
          GOTO      ARTD1000 IF NOT EQUAL    * same contract id ?
          MATCH     KEY1,PMRISTYP            * ICD10 ?
          GOTO      ARTD1000 IF NOT EQUAL
          MATCH     PTEOCODE,PMRIICDC        * same code ?
          GOTO      ARTD1000 IF NOT EQUAL
.
          MATCH     DIM2,PMRICTYP
          GOTO      ARTD2000 IF NOT EQUAL    * different type of code
.
          MATCH     " 1",DIM2
          IF        @EQUAL
            MOVE      PMAJADJV,ARTAMNTS  * Radiotherapy value adjust.
            MOVE      "022",KEY3         * Radiotherapy adjust.
          ELSE
            MATCH     "L61Z",PDRGCODE    * Exclude Haemodialysis
            GOTO      ARTD1000 IF EQUAL
            MATCH     "L68Z",PDRGCODE    * Exclude Peritoneal Dialysis
            GOTO      ARTD1000 IF EQUAL
.
            MOVE      PMAJADJV,ADIAAMNT  * Dialysis value adjust.
            MOVE      "023",KEY3         * Dialysis adjust.
          ENDIF
          CALL      WABF0000           * Write to ABF Patient details
          GOTO      ARTD1000
.
ARTD9999  RETURN
+
.*******************************************************************************
.         Check Intensive Care Unit Adjustment
.*******************************************************************************
.         If Bundled ICU equal to yes, ignore ICU Days
.
AICU0000  MATCH     "1",PMPWBICU
          GOTO      AICU1000 IF EQUAL
.
          MOVE      TWO,F1                 * calculate ICUA hours
          CALL      CBTR0000
.
          IF        FORM6<>0
            MOVE      FORM6,ICUHOURS     * Patient ICUA Hours
            MOVE      PMAJADJV,AICUAMNT  * ICU value adjust.
            MOVE      PMAJADJV,FORM104   * Adjustment value
            MOVE      "024",KEY3         * ICU adjust.
.
            MOVE      FORM6,KEY6
            SQUEEZE   KEY6  
            CLEAR     ADJMDESC
            APPEND    "(",ADJMDESC
            APPEND    KEY6,ADJMDESC
            APPEND    " Hrs)",ADJMDESC
            APPEND    SP70,ADJMDESC
            RESET     ADJMDESC
.
            CALL      WABF0000           * Write to ABF Patient details
            PACK      ADJMDESC,SP70,SP70
          ENDIF
.
AICU1000  MOVE      AADMNO,KEY8
          CALL      RDPTICU1
          BRANCH    OVRCD,AICU9999
.
          PACK      PTICCHSC,PTICCHSC,SP70
          MATCH     SP70,PTICCHSC
          GOTO      AICU9999 IF EQUAL    * No ICU hours
.
          MOVE      ZERO,FORM3
          SQUEEZE   PTICCHSC
          MOVE      PTICCHSC,FORM3
.
          MOVE      ONE,ICUFLAG
          MOVE      FORM3,CHLSCORE       * Patient Charlson score
.
          MOVE      FORM3,FORM104
          MOVE      "059",KEY3             * Charlson Score
          CALL      WABF0000               * Write to ABF Patient details
.
AICU9999  RETURN
+
.*******************************************************************************
.         Private Patient Accommodation Adjustment
.*******************************************************************************
ACCM0000  BRANCH    ACCMFLAG,ACCM9999
.
.         ADJTYVAL = 013 for sameday, 014 for Overnight
.
          IF        ADJTYVAL=13
            COMPARE   ONE,ACDAYCS           * daycase
            GOTO      ACCM9999 IF NOT EQUAL
            MOVE      PMAJADJV,FORM104      * Adjustment value
            MOVE      "025",KEY3
            MOVE      "Private Pt Accom Sameday",ADJMDESC
          ELSE
            IF          ADJTYVAL=14
              BRANCH      ACDAYCS,ACCM9999
              MOVE        PMAJADJV,FORM104  * Adjustment value
              MOVE        "026",KEY3
              MOVE        "Private Pt Accom Overnight",ADJMDESC
            ENDIF
          ENDIF
.
          CALL      WABF0000                 * Write to ABF Patient details
          PACK      ADJMDESC,SP70,SP70
          MOVE      FORM104,AACCAMNT
          MOVE      ONE,ACCMFLAG
.
ACCM9999  RETURN
+
.*******************************************************************************
.         Patient Treatment remoteness (Hospital)
.*******************************************************************************
ETRE0000  MOVE      PMAJADJV,FORM104       * Adjustment value
          UNPACK    SP70,KEY8,KEY45,KEY3
.
          READ      CONTROLF,TEN3;CHADD1,CHADD2,CHPOST
          PACK      KEY8,CHPOST,SP70      * Postcode
          PACK      KEY45,CHADD2,SP70     * Suburb
.
          OPEN      PATHSPA1,"pathspaf"
          PACK      KEY3,SP70
          CALL      RSPTHSP1
          CALL      RKPTHSP1
          BRANCH    OVRCD,ETRE1000
.
          IF        IBCNMHOS=1
            PACK      KEY3,PMVXMHOS,SP70
            CALL      RDPTHSP1
          ENDIF
.
          PACK      KEY8,PTHSPCOD,SP7 0   * Postcode
          PACK      KEY45,PTHSADD2,SP70   * Suburb
          PACK      KEY3,PTHSADD3,SP70    * State
.
ETRE1000  OPEN      IBAPOST1,"ibapostf"
          PACK      KEY56,KEY8,KEY45,KEY3,SP70
          CALL      RDIBPOS1
          COMPARE   ZERO,OVRCD
          GOTO      ETRE2000 IF EQUAL
.
          PACK      KEY56,KEY8,KEY45,SP70
          CALL      RSIBPOS1
          CALL      RKIBPOS1
          BRANCH    OVRCD,ETRE9999
.
          MATCH     KEY8,IBPOPCOD
          GOTO      ETRE9999 IF NOT EQUAL     * Different postcode
.
ETRE2000  MATCH     SP70,IBPORRAV
          GOTO      ETRE9999 IF EQUAL
.
          MATCH     "RA3  ",IBPORRAV
          IF        @EQUAL
            IF        ADJTYVAL=23
                MOVE      "036",KEY3         * Pat.Treatment remoteness area adj
                MOVE      "Hospital Area Remote",ADJMDESC
                CALL      WABF0000           * Write to ABF Patient details
                PACK      ADJMDESC,SP70,SP70
                MOVE      PMAJADJV,ATAAMNTS  * value adjust.
                MOVE      ONE,ATREFLAG
            ENDIF
          ELSE
            MATCH     "RA4  ",IBPORRAV
            IF        @EQUAL
              IF        ADJTYVAL=24
                MOVE      "037",KEY3       * Pat.Treatment remoteness area adj.
                MOVE      "Hospital Area Very Remote",ADJMDESC
                CALL      WABF0000           * Write to ABF Patient details
                PACK      ADJMDESC,SP70,SP70
                MOVE      PMAJADJV,ATAAMNTS  * value adjust.
                MOVE      ONE,ATREFLAG
              ENDIF
            ENDIF
          ENDIF
.
ETRE9999  RETURN
+
.*******************************************************************************
.         NEP Adjustment
.*******************************************************************************
ANEP0000  MOVE      "CL",KEY2
          PACK      KEY5,KEY2,ACLAIM
          CALL      RDCODE1
          BRANCH    OVRCD,ANEP9999
.
          MATCH     "B",TCINDC22
          IF        @EQUAL
            COMPARE   "26",ADJTYVAL
            GOTO      ANEP8000 IF EQUAL
          ENDIF
.
          MATCH     "C",TCINDC22
          IF        @EQUAL
            COMPARE   "27",ADJTYVAL
            GOTO      ANEP8000 IF EQUAL
          ENDIF
.
          MATCH     SP70,TCINDC22
          GOTO      ANEP2000 IF EQUAL
          MATCH     "A",TCINDC22
          GOTO      ANEP9999 IF NOT EQUAL
.
ANEP2000  COMPARE   "25",ADJTYVAL
          GOTO      ANEP9999 IF NOT EQUAL
.
ANEP8000  MOVE      PMAJADJV,NEPVALUE  * Save default NEP value adjustment
ANEP9999  RETURN
+
.*******************************************************************************
.         Get HAC Complexity adjustment % (Only for State NOT Queensland)
.*******************************************************************************
GHAC0000  READ      CONTROLF,EIGHTY;*250,PTCNHDPS
.
          COMPARE   "4",PTCNHDPS
          GOTO      GHAC9999 IF EQUAL        * Queensland - No HAC Component
.
          CALL      ICDE0000                 * Check ICD10 Edition
.
          MOVE      ZERO,HACCOUNT
          PACK      KEY13,AADMNO,SP70
          CALL      RSPTECD1
GHAC1000  CALL      RKPTECD1
          BRANCH    OVRCD,GHAC4000
.
          MATCH     DAADMNO,DPTEDADM
          GOTO      GHAC4000 IF NOT EQUAL    * Different admission
.
          MATCH     "1",PTEDOSET
          GOTO      GHAC1000 IF NOT EQUAL    * Not 'Began in Hospital'
.
          PACK      KEY30,PTEDCODE,SP70      * Disease code
          MOVE      "HAC",PMVMMVSI           * Set ID
          PACK      KEY41,KEY8,KEY30,PMVMMVSI
          CALL      RDPMVMT1
          BRANCH    OVRCD,GHAC1000
.
          UNPACK    PMVMMCOD,HACGROUP        * HAC Group
          CALL      HACC0000                 * Get HAC Complexity file
.
.         Add up the complexity values for each ICD10
.
          MOVE      BASELVAL,FORM102     * Baseline
          ADD       PTAGEVAL,FORM102     * Age
          ADD       CHSCOVAL,FORM102     * Charlson scores
          ADD       DRGTYVAL,FORM102     * DRG Type
          ADD       PTSEXVAL,FORM102     * Patient Sex
          ADD       EMRADVAL,FORM102     * Emergency Admission
          ADD       ICUHRVAL,FORM102     * ICU Hours
          ADD       MDCVALUE,FORM102     * MDC
          ADD       ADTRNVAL,FORM102     * Admission transfer status
.
          MOVE      ZERO,FORM3A
          MOVE      FORM102,FORM3A
.
.         Get the Complexity Adjustment percentage for the HAC Group
.
          PACK      KEY12,CONTRCID,HACGROUP,SP70
          CALL      RSPMHAD1
GHAC2000  CALL      RKPMHAD1
          BRANCH    OVRCD,GHAC1000
.
          MATCH     CONTRCID,PMHDCNTR
          GOTO      GHAC1000 IF NOT EQUAL     * Different contract id
          MATCH     HACGROUP,PMHDHACG
          GOTO      GHAC1000 IF NOT EQUAL     * Different HAC Group
.
          SQUEEZE   PMHDVALF
          MOVE      PMHDVALF,FORM3
          SQUEEZE   PMHDVALT
          MOVE      PMHDVALT,F3
.
          IF        FORM3A>=FORM3 & FORM3A<=F3
            ADD       ONE,HACCOUNT
            MOVE      PMHDADJV,COMPLVAL[HACCOUNT]   * total Complexity value
.
            CALL      CMPV0000             * save complexity values
.
            CALL      TADJ0000             * Set type of adjustment
            MOVE      PMHDADJV,FORM104       * HAC adjustment
            PACK      ADJMDESC,KEY80,SP70
            CALL      WABF0000               * Write to ABF Patient details
            PACK      ADJMDESC,SP70,SP70
            GOTO      GHAC1000
          ENDIF
          GOTO      GHAC2000
.
.         Get highest complexity adjustment %
.
GHAC4000  MOVE      ZERO,AHACAMNT
          MOVE      ZERO,F3
.
GHAC4200  ADD       ONE,F3
          COMPARE   F3,HACCOUNT
          GOTO      GHAC9000 IF LESS
.
          MOVE      COMPLVAL[F3],FORM104
.
.         Get the Complexity level - by comparing with range of HAC 
.
          COMPARE   FORM104,AHACAMNT
          GOTO      GHAC4200 IF NOT LESS
.
          MOVE      FORM104,AHACAMNT        * New score value
          GOTO      GHAC4200
.
GHAC9000  MOVE      AHACAMNT,FORM104        * HAC adjustment
          MOVE      "063",KEY3
          CALL      WABF0000                * Write to ABF Patient details
.
GHAC9999  RETURN
+
.*******************************************************************************
.         Check the Edition of ICD10 code
.*******************************************************************************
ICDE0000  PACK      D8,ADATE,SP70           * Default to Admission date
          IF        ASTAT=3
            MOVE      DDATE,D8              * use Discharged date
          ENDIF
          REP       " 0",D8
.
          MOVE      "patd13af",KEY8         * Set version ind to ICD10 Ed13
.
          READ      CONTROLF,HUND10;*85,PTCNGDVX
          READ      CONTROLF,HUND17;*220,PTCNGDX1
          READ      CONTROLF,HUND28;*231,PTCNGDX2
          READ      CONTROLF,HUND30;*86,PTCNGDX3
.
          MATCH     D8,PTCNGDVX             * Is Date >= ICD10 Ed10 Go-Live Date
          IF        (@EQUAL | @LESS)
            MOVE      "pat10dxf",KEY8       * edition 10
          ENDIF
.
          MATCH     D8,PTCNGDX1             * Is Ed.11
          IF        (@EQUAL | @LESS)
            MOVE      "patd11af",KEY8       * edition 11
          ENDIF
.
          MATCH     D8,PTCNGDX2             * Is Ed.12
          IF        (@EQUAL | @LESS)
            MOVE      "patd12af",KEY8       * edition 12
          ENDIF
.
          MATCH     D8,PTCNGDX3             * Is Ed.13
          IF        (@EQUAL | @LESS)
            MOVE      "patd13af",KEY8       * edition 13
          ENDIF
.
ICDE9999  RETURN
+
.*******************************************************************************
.         Save complexity values
.*******************************************************************************
CMPV0000  PACK      KEY80,SP70,SP70
          CLEAR     KEY80
.
          MOVE      ZERO,F2P2
          MOVE      BASELVAL,F2P2        * Baseline
          MOVE      F2P2,KEY5
          SQUEEZE   KEY5
          APPEND    KEY5,KEY80
          APPEND    "+",KEY80
.
          MOVE      ZERO,F2P2
          MOVE      PTAGEVAL,F2P2        * Age
          MOVE      F2P2,KEY5
          SQUEEZE   KEY5
          APPEND    KEY5,KEY80
          APPEND    "+",KEY80
.
          MOVE      ZERO,F2P2
          MOVE      CHSCOVAL,F2P2        * Charlson scores
          MOVE      F2P2,KEY5
          SQUEEZE   KEY5
          APPEND    KEY5,KEY80
          APPEND    "+",KEY80
.
          MOVE      ZERO,F2P2
          MOVE      DRGTYVAL,F2P2        * DRG Type
          MOVE      F2P2,KEY5
          SQUEEZE   KEY5
          APPEND    KEY5,KEY80
          APPEND    "+",KEY80
.
          MOVE      ZERO,F2P2
          MOVE      PTSEXVAL,F2P2        * Patient Sex
          MOVE      F2P2,KEY5
          SQUEEZE   KEY5
          APPEND    KEY5,KEY80
          APPEND    "+",KEY80
.
          MOVE      ZERO,F2P2
          MOVE      EMRADVAL,F2P2        * Emergency Admission
          MOVE      F2P2,KEY5
          SQUEEZE   KEY5
          APPEND    KEY5,KEY80
          APPEND    "+",KEY80
.
          MOVE      ZERO,F2P2
          MOVE      ICUHRVAL,F2P2        * ICU Hours
          MOVE      F2P2,KEY5
          SQUEEZE   KEY5
          APPEND    KEY5,KEY80
          APPEND    "+",KEY80
.
          MOVE      ZERO,F2P2
          MOVE      MDCVALUE,F2P2        * MDC
          MOVE      F2P2,KEY5
          SQUEEZE   KEY5
          APPEND    KEY5,KEY80
          APPEND    "+",KEY80
.
          MOVE      ZERO,F2P2
          MOVE      ADTRNVAL,F2P2        * Admission transfer status
          MOVE      F2P2,KEY5
          SQUEEZE   KEY5
          APPEND    KEY5,KEY80
          APPEND    "=",KEY80
.
          MOVE      FORM102,F2P2
          MOVE      F2P2,KEY5
          SQUEEZE   KEY5
          APPEND    KEY5,KEY80
.
          APPEND    SP70,KEY80
          RESET     KEY80
.
CMPV9999  RETURN
+
.*******************************************************************************
.         Get type of Adjustment for HAC
.*******************************************************************************
TADJ0000  MOVE      ZERO,F3
          MOVE      HACGROUP,D3
          SQUEEZE   D3
          MOVE      D3,F3     * HAC Group
          BRANCH    F3,TADJ0100,TADJ0200,TADJ0300,TADJ0400,TADJ9999:
                       TADJ0600,TADJ0700,TADJ0800,TADJ0900,TADJ1000:
                       TADJ1100,TADJ1200,TADJ1300,TADJ1400,TADJ1500
          GOTO      TADJ9999
.
TADJ0100  MOVE      "038",KEY3         * HAC 1
          GOTO      TADJ9999
.
TADJ0200  MOVE      "039",KEY3         * HAC 2
          GOTO      TADJ9999
.
TADJ0300  MOVE      "040",KEY3         * HAC 3
          GOTO      TADJ9999
.
TADJ0400  MOVE      "041",KEY3         * HAC 4
          GOTO      TADJ9999
.
TADJ0600  MOVE      "042",KEY3         * HAC 6
          GOTO      TADJ9999
.
TADJ0700  MOVE      "043",KEY3         * HAC 7
          GOTO      TADJ9999
.
TADJ0800  MOVE      "044",KEY3         * HAC 8
          GOTO      TADJ9999
.
TADJ0900  MOVE      "045",KEY3         * HAC 9
          GOTO      TADJ9999
.
TADJ1000  MOVE      "046",KEY3         * HAC 10
          GOTO      TADJ9999
.
TADJ1100  MOVE      "047",KEY3         * HAC 11
          GOTO      TADJ9999
.
TADJ1200  MOVE      "048",KEY3         * HAC 12
          GOTO      TADJ9999
.
TADJ1300  MOVE      "049",KEY3         * HAC 13
          GOTO      TADJ9999
.
TADJ1400  MOVE      "050",KEY3         * HAC 14
          GOTO      TADJ9999
.
TADJ1500  MOVE      "051",KEY3         * HAC 15
          GOTO      TADJ9999
.
TADJ9999  RETURN
+
.*******************************************************************************
.         Get HAC Complexity values  
.*******************************************************************************
HACC0000  PACK      KEY12,CONTRCID,HACGROUP,SP70
          CALL      RSPMHAC1
HACC1000  CALL      RKPMHAC1
          BRANCH    OVRCD,HACC2000
.
          MATCH     CONTRCID,PMHACNTR
          GOTO      HACC2000 IF NOT EQUAL     * Different contract id
          MATCH     HACGROUP,PMHAHACG
          GOTO      HACC2000 IF NOT EQUAL     * Different HAC Group
.
          MOVE      ZERO,FORM3
          MOVE      PMHAHACF,FORM3
.
          MOVE      SP70,TCINDC1
          IF        FORM3=5 | FORM3=6
            MATCH     SP70,DRGPTYPE           * Check DRG Partition type
            IF        !@EQUAL
              MOVE      "Dt",KEY2
              PACK      KEY5,KEY2,DRGPTYPE
              CALL      RDCODE1
            ENDIF
          ENDIF
.
          BRANCH    FORM3,HACC1100,HACC1200,HACC1300,HACC1400,HACC1500:
                          HACC1600,HACC1700,HACC1800

HACC1100  MOVE      PMHAHACV,BASELVAL       * Baseline complexity value
          GOTO      HACC1000
.
HACC1200  CALL      CEMR0000                * Check if pat.linked to EMR Adm.
          BRANCH    EXIT,HACC1000
.
          MOVE      PMHAHACV,EMRADVAL       * EMR Adm. complexity value
          GOTO      HACC1000
.
HACC1300  COMPARE   ZERO,ICUHOURS
          GOTO      HACC1000 IF EQUAL       * No ICU Hours
.
          MOVE      PMHAHACV,ICUHRVAL       * ICU Hours complexity value
          GOTO      HACC1000
.
HACC1400  PACK      KEY8,AADMNO
          CALL      RDPTDAD1
          BRANCH    OVRCD,HACC1000
          MATCH     SP70,PTDAADTS
          GOTO      HACC1000 IF EQUAL       * not transferred from another hosp.
.
          MOVE      PMHAHACV,ADTRNVAL       * Adm.Transfer complexity value
          GOTO      HACC1000
.
HACC1500  MATCH     "M",TCINDC1             * Patient DRG Type - Medical ?
          IF        @EQUAL
            MOVE      PMHAHACV,DRGTYVAL     * DRG Type complexity value
          ENDIF
          GOTO      HACC1000
.
HACC1600  MATCH     "I",TCINDC1             * Patient DRG Type - Intervention ?
          IF        @EQUAL
            MOVE      PMHAHACV,DRGTYVAL     * DRG Type complexity value
          ENDIF
          GOTO      HACC1000
.
HACC1700  MATCH     "M",PSEX                * Check Patient Sex - Male ?
          IF        @EQUAL
            MOVE      PMHAHACV,PTSEXVAL       * Sex complexity value
          ENDIF
          GOTO      HACC1000
.
HACC1800  MATCH     "F",PSEX                * Check Patient Sex - Female ?
          IF        @EQUAL
            MOVE      PMHAHACV,PTSEXVAL     * Sex complexity value
          ENDIF
          GOTO      HACC1000
. 
.         HAC Complexity type for Charlson, Age and MDC
.
HACC2000  IF        ICUFLAG=1
            CALL      CCHR0000              * Charlson score Complexity
          ENDIF
          CALL      CAGE0000                * Age Complexity
          CALL      CMDC0000                * MDC Complexity
.
HACC9999  RETURN
+
.*******************************************************************************
.          Check if pat.linked to EMR Adm.
.*******************************************************************************
CEMR0000  PACK      KEY16,AURNO,Z70
          CALL      RSEMVIS3
CEMR1000  CALL      RPEMVIS3
          BRANCH    OVRCD,CEMR9000
.
          MATCH     AURNO,EMVIURNO          * Same U/R No ?
          GOTO      CEMR9000 IF NOT EQUAL
.
          MATCH     EMVIPADM,DAADMNO        * check patient system adm.no
          GOTO      CEMR1000 IF NOT EQUAL
.
          MOVE      ZERO,EXIT               * admission linked to EMR
          GOTO      CEMR9999
.
CEMR9000  MOVE      ONE,EXIT
CEMR9999  RETURN
+
.*******************************************************************************
.         Charlson score Complexity
.*******************************************************************************
CCHR0000  MOVE      "001",KEY3        * for Charlson score
.
          MOVE      SP70,D3
          IF        CHLSCORE=0
            MOVE      "66",F3
            GOTO      CCHR1000 IF EQUAL   * Zero Chalson score
          ENDIF
.
          MOVE      ZERO,F3
          LOAD      F3,CHLSCORE,NINE,TEN,TEN1,TEN2,TEN3,TEN4,TEN5,TEN6,TEN7:
                                TEN8,TEN9,TWENTY,TWENTY1,TWENTY2,TWENTY3,TWENTY4
CCHR1000  MOVE      F3,D3
          REP       " 0",D3
          PACK      KEY15,CONTRCID,HACGROUP,KEY3,D3,SP70
          CALL      RDPMHDE1
          BRANCH    OVRCD,CCHR9999
          MOVE      PMHECOMV,CHSCOVAL      * Charlson Score complexity value
.
CCHR9999  RETURN
+
.*******************************************************************************
.         Age Complexity
.*******************************************************************************
CAGE0000  MOVE      "002",KEY3         * For Age
.
.         19 Age ranges with Complexity field from 025 to 043
.
          MOVE      ZERO,F2
CAGE1000  COMPARE   "21",F2
          GOTO      CAGE9999 IF NOT LESS
.
          ADD       ONE,F2
          MOVE      "100",F3
          LOAD      F3,F2,FIVE,TEN,TEN5,TWENTY,TWENTY5:
                          THIRTY,THIRTY5,FORTY,FORTY5,FIFTY:
                          FIFTY5,SIXTY,SIXTY5,SEVENTY,SEVENTY5:
                          EIGHTY,EIGHTY5,NINTY,NINTY5
          COMPARE   F3,PSAGE
          GOTO      CAGE1000 IF NOT LESS
.
          MOVE      ZERO,F3
          LOAD      F3,F2,TWENTY5,TWENTY6,TWENTY7,TWENTY8,TWENTY9:
                          THIRTY,THIRTY1,THIRTY2,THIRTY3,THIRTY4:
                          THIRTY5,THIRTY6,THIRTY7,THIRTY8,THIRTY9:
                          FORTY,FORTY1,FORTY2,FORTY3,FORTY4
.
          MOVE      F3,D3
          REP       " 0",D3
          PACK      KEY15,CONTRCID,HACGROUP,KEY3,D3,SP70
          CALL      RDPMHDE1
          BRANCH    OVRCD,CAGE9999
          MOVE      PMHECOMV,PTAGEVAL     * Age complexity value
.
CAGE9999  RETURN
+
.*******************************************************************************
.         MDC Complexity
.*******************************************************************************
CMDC0000  MOVE      "003",KEY3         * for MDC
.
.         16 MDC records with Complexity field from 045 to 065
.
          MATCH     "00",MDCNUMBR
          IF        @EQUAL
            MOVE      "069",D3         * Ventilation
            GOTO      CMDC9000
          ENDIF
          MATCH     "01",MDCNUMBR
          IF        @EQUAL
            MOVE      "045",D3         * Nervous system
            GOTO      CMDC9000
          ENDIF
          MATCH     "02",MDCNUMBR
          IF        @EQUAL
            MOVE      "046",D3         * Eye
            GOTO      CMDC9000
          ENDIF
          MATCH     "03",MDCNUMBR
          IF        @EQUAL
            MOVE      "047",D3         * Ear,Nose,Mouth & Throat
            GOTO      CMDC9000
          ENDIF
          MATCH     "04",MDCNUMBR
          IF        @EQUAL
            MOVE      "048",D3         * Respiratory System
            GOTO      CMDC9000
          ENDIF
          MATCH     "05",MDCNUMBR
          IF        @EQUAL
            MOVE      "049",D3         * Circulatory System
            GOTO      CMDC9000
          ENDIF
          MATCH     "06",MDCNUMBR
          IF        @EQUAL
            MOVE      "050",D3         * Digestive System
            GOTO      CMDC9000
          ENDIF
          MATCH     "07",MDCNUMBR
          IF        @EQUAL
            MOVE      "051",D3         * Hepatobiliary System & Pancreas
            GOTO      CMDC9000
          ENDIF
          MATCH     "08",MDCNUMBR
          IF        @EQUAL
            MOVE      "052",D3         * Musculoskeletal System
            GOTO      CMDC9000
          ENDIF
          MATCH     "09",MDCNUMBR
          IF        @EQUAL
            MOVE      "053",D3         * Skin,Subcutaneous Tissue & Breast
            GOTO      CMDC9000
          ENDIF
          MATCH     "10",MDCNUMBR
          IF        @EQUAL
            MOVE      "054",D3         * Endocrine,Nutritional & Metabolic
            GOTO      CMDC9000
          ENDIF
          MATCH     "11",MDCNUMBR
          IF        @EQUAL
            MOVE      "055",D3         * Kidney & Urinary Tract
            GOTO      CMDC9000
          ENDIF
          MATCH     "12",MDCNUMBR
          IF        @EQUAL
            MOVE      "056",D3         * Male Reproductive System
            GOTO      CMDC9000
          ENDIF
          MATCH     "13",MDCNUMBR
          IF        @EQUAL
            MOVE      "057",D3         * Female Reproductive System
            GOTO      CMDC9000
          ENDIF
          MATCH     "14",MDCNUMBR
          IF        @EQUAL
            MOVE      "058",D3         * Pregnancy,Childbirt & the Puerperium
            GOTO      CMDC9000
          ENDIF
          MATCH     "15",MDCNUMBR
          IF        @EQUAL
            MOVE      "059",D3         * Newborns & Other Neonates
            GOTO      CMDC9000
          ENDIF
          MATCH     "16",MDCNUMBR
          IF        @EQUAL
            MOVE      "060",D3         * Blood,Blood Forming Organs
            GOTO      CMDC9000
          ENDIF
          MATCH     "17",MDCNUMBR
          IF        @EQUAL
            MOVE      "061",D3         * Neoplastic Disorders
            GOTO      CMDC9000
          ENDIF
          MATCH     "18",MDCNUMBR
          IF        @EQUAL
            MOVE      "062",D3         * Infectious & Parasitic Disease
            GOTO      CMDC9000
          ENDIF
          MATCH     "19",MDCNUMBR
          IF        @EQUAL
            MOVE      "067",D3         * Mental Diseases & Disorders
            GOTO      CMDC9000
          ENDIF
          MATCH     "20",MDCNUMBR
          IF        @EQUAL
            MOVE      "068",D3         * Alcohol/Drug Use&Induced Organic
            GOTO      CMDC9000
          ENDIF
          MATCH     "21",MDCNUMBR
          IF        @EQUAL
            MOVE      "063",D3         * Injuries,Poisonings & Toxic Effects
            GOTO      CMDC9000
          ENDIF
.
          MATCH     "22",MDCNUMBR
          IF        @EQUAL
            MOVE      "064",D3         * Burns
            GOTO      CMDC9000
          ENDIF
          MATCH     "23",MDCNUMBR
          IF        @EQUAL
            MOVE      "065",D3         * Factors Influencing Health Status 
            GOTO      CMDC9000
          ENDIF
          MATCH     "24",MDCNUMBR
          IF        @EQUAL
            MOVE      "070",D3         * Unknown
            GOTO      CMDC9000
          ENDIF
.
CMDC9000  PACK      KEY15,CONTRCID,HACGROUP,KEY3,D3,SP70
          CALL      RDPMHDE1
          BRANCH    OVRCD,CMDC9999
.
          MOVE      PMHECOMV,MDCVALUE     * MDC complexity value
CMDC9999  RETURN
+
.*******************************************************************************
.         Calculate ABF amount
.*******************************************************************************
CABF0000  BRANCH    CALCFLAG,CABF1000,CABF2000,CABF2000,CABF2000
.
.{[PW x APaed x (1 + ASPA) x (1 + AInd + ARes + ATA + ART + ADia) + 
.(AICU x ICU hours)] - [(PW + AICU x ICU hours) x APPS + LOS x AAcc] - 
.PW x AHAC} x NEP
.
          MOVE      ONE,F106A
          ADD       ASPAAMNT,F106A           * A_SPA
          MOVE      ZERO,ATAAMNTS            * A_ATA - Patient treatment remote.
.
          MOVE      ONE,F106B
          ADD       AINDAMNT,F106B           * A_Ind
          ADD       ARESAMNT,F106B           * A_Res
          ADD       ATAAMNTS,F106B           * A_ATA
          ADD       ARTAMNTS,F106B           * A_RT
          ADD       ADIAAMNT,F106B           * A_Dia
.
          MOVE      ZERO,F106C
          MOVE      AICUAMNT,F106C         * A_ICU
          MULT      ICUHOURS,F106C         * Total ICU Hours
.
          MOVE      PWAMOUNT,ABFCHRGE        * PW
          MULT      F106A,ABFCHRGE           * x A_SPA
          MULT      F106B,ABFCHRGE           * x A_Ind+A_Res+A_RT+A_Dia
          ADD       F106C,ABFCHRGE           * + ICU
.
          GOTO      CABF1800
.
.         New ABF calculation (2021)
.
.{[PW x APaed x (1 + ASPA + AInd + ARes + ATA + ART + ADia) +
.(AICU x ICU hours)] - [(PW + AICU x ICU hours) x APPS + LOS x AAcc] - 
.PW x AHAC} x NEP
.
CABF1000  MOVE      ZERO,ATAAMNTS            * A_ATA - Patient treatment remote.
.
          MOVE      ONE,F106B
          ADD       ASPAAMNT,F106B           * A_SPA
          ADD       AINDAMNT,F106B           * A_Ind
          ADD       ARESAMNT,F106B           * A_Res
          ADD       ATAAMNTS,F106B           * A_ATA/A_Treat
          ADD       ARTAMNTS,F106B           * A_RT
          ADD       ADIAAMNT,F106B           * A_Dia
.
          MOVE      ZERO,F106C
          MOVE      AICUAMNT,F106C         * A_ICU
          MULT      ICUHOURS,F106C         * Total ICU Hours
.
          MOVE      PWAMOUNT,ABFCHRGE        * PW
          MULT      F106B,ABFCHRGE           * x (1+A_SPA+A_Ind+A_Res+A_RT+A_Dia
          ADD       F106C,ABFCHRGE           * + ICU
.
CABF1800  MOVE      ZERO,APPSAMNT            * APPS
          MOVE      ZERO,AACCAMNT            * AAcc
.
          MOVE      PWAMOUNT,F106A           * PW
          ADD       F106C,F106A              * + ICU Hours
          MULT      APPSAMNT,F106A           * APPS
.
          MOVE      LOSNDAYS,F106B           * LOS
          MULT      AACCAMNT,F106B           * AAcc
          ADD       F106B,F106A
.
          SUB       F106A,ABFCHRGE            * - (PW + ICU)xAPPS+LOSxAACC
.
          MOVE      PWAMOUNT,F106B           * PW
          MULT      AHACAMNT,F106B           * A_HAC
          IF        F106B>0
            DIV       "100",F106B
          ENDIF
.
          SUB       F106B,ABFCHRGE          * - (PW x Ahac) = NWAU
          GOTO      CABF9000
.
.         ABF Calculation for 2023/2024
.
.{[PW x APaed x (1 + ARes + AInd +  ART + ADia) + (1 + ATreat) x (1 + AC19) +
.(AICU x ICU hours)] - [(PW + AICU x ICU hours) x APPS + LOS x AAcc] - 
.PW x AHAC} x TUA x NEP
.         Note: APaed=1
.
CABF2000  MOVE      ONE,F106A
          ADD       ARESAMNT,F106A           * A_Res
          ADD       AINDAMNT,F106A           * A_Ind
          ADD       ARTAMNTS,F106A           * A_RT
          ADD       ADIAAMNT,F106A           * A_Dia
.
          MOVE      ONE,F106B
          ADD       ATAAMNTS,F106B           * A_ATA/A_Treat
.
          MOVE      ZERO,F106C
          MOVE      AICUAMNT,F106C           * A_ICU
          MULT      ICUHOURS,F106C           * Total ICU Hours
.
          MOVE      PWAMOUNT,ABFCHRGE        * PW
          MULT      F106A,ABFCHRGE           * x (1+A_Res+A_Ind+A_RT+A_Dia
          MULT      F106B,ABFCHRGE           * x (1 + ATreat)
.
          MOVE      AC19AMNT,F106D
          ADD       ONE,F106D
.
          MULT      F106D,ABFCHRGE           * x (1 + AC19)
          ADD       F106C,ABFCHRGE           * + ICU
.
          MOVE      PWAMOUNT,F106A           * PW
          ADD       F106C,F106A              * + ICU Hours
          MULT      APPSAMNT,F106A           * APPS
.
          MOVE      LOSNDAYS,F106B           * LOS
          MULT      AACCAMNT,F106B           * AAcc
          ADD       F106B,F106A
.
          SUB       F106A,ABFCHRGE           * - (PW + ICU)xAPPS+LOSxAACC
.
          MOVE      PWAMOUNT,F106B           * PW
          MULT      AHACAMNT,F106B           * A_HAC
          IF        F106B>0
            DIV       "100",F106B
          ENDIF
.
          SUB       F106B,ABFCHRGE           * - (PW x Ahac) = NWAU
          MULT      TUAAMNTS,ABFCHRGE        * TUA
.
CABF9000  IF        ABFCHRGE<0
            MOVE      ZERO,ABFCHRGE         * No negative NWAU
          ENDIF
          MOVE      ABFCHRGE,NWAUAMNT       * save NWAU amount with 6 decimal
.
          MOVE      SP70,KEY19
          MOVE      NWAUAMNT,KEY19          * NWAU amount in 6 decimal places
          SQUEEZE   KEY19
          PACK      ADJMDESC,KEY19,SP70,SP20
.
          MOVE      ABFCHRGE,FORM104        * NWAU amount
          MOVE      "053",KEY3
          CALL      WABF0000                * Write to ABF Patient details
          PACK      ADJMDESC,SP70,SP70
.
CABF9999  RETURN
+
.*******************************************************************************
.         Write to ABF Patient details
.*******************************************************************************
WABF0000  MOVE      AADMNO,PMABADMN          * Admission number
          MOVE      SP70,PMABINVN            * Blank Inv.no for tobe invoiced
          MOVE      KEY3,PMABADJT            * Type of adjustment
.
          PACK      KEY19,PMABADMN,PMABINVN,PMABADJT,SP70
          CALL      RDPMABF1
          BRANCH    OVRCD,WABF1000
.
          MOVE      FORM104,PMABADJV       * Value of adjustment
          MATCH     SP70,ADJMDESC
          IF        !@EQUAL
            PACK      PMABCDES,ADJMDESC,SP70    * Description of adjustment
          ENDIF
.
          PACK      PMABUDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMABUDAT
          CLOCK     TIME,PMABUTIM
          MOVE      WBSEUID,PMABUUID
          CALL      UPPMABF1
.
          GOTO      WABF9999
.
WABF1000  CALL      CLPMSABF                 * Clear fields
          MOVE      "3",PMABABFT             * Type of ABF Details
          MOVE      " 1",PMABEPSN            * Episode Number
          PACK      PMABADRG,PDRGCODE,SP70   * AR-DRG
.
          PACK      PMABDRGD,PMPWDRGD,SP70
          MATCH     SP70,DRGSDESC
          IF        !@EQUAL
            PACK      PMABDRGD,DRGSDESC,SP70   * State DRG desc
          ENDIF
.
          UNPACK    KEY19,PMABADMN,PMABINVN,PMABADJT
          MOVE      FORM104,PMABADJV       * Value of adjustment
          MATCH     SP70,ADJMDESC
          IF        !@EQUAL
            PACK      PMABCDES,ADJMDESC,SP70    * Description of adjustment
          ENDIF
.
          PACK      PMABCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMABCDAT
          CLOCK     TIME,PMABCTIM
          MOVE      WBSEUID,PMABCUID
.
          CALL      RAPMABF1
          IF        OVRCD=1
            MOVE      ZERO,OVRCD
            TRAP      OVERCOND IF IO
            CALL      WRPMABF1
            TRAPCLR   IO
          ENDIF
.
WABF9999  RETURN
+
.*******************************************************************************
.         Get Psychiatric days
.*******************************************************************************
CBTR0000  MOVE       ZERO,NBRDAYS
          UNPACK     SP70,SAVDATE,SAVTIME,TTRNDATE,TTRNTIME
          MOVE       ZERO,FORM6A
.
          PACK       KEY30,AADMNO,SP20,SP10
          CALL       RDSTRAN2
CBTR1200  CALL       RDKTRAN2
          BRANCH     OVRCD,CBTR4000
.
          MATCH      TADMN,AADMNO            * same admission ?
          GOTO       CBTR4000 IF NOT EQUAL
.
          MATCH      ANSA,TMOVE              * Admission record ?
          GOTO       CBTR2000 IF EQUAL
.
          MATCH      ANSR,TMOVE              * Return record ?
          GOTO       CBTR2000 IF EQUAL
.
          CMATCH     ANSC,TMOVE              * Change record ?
          GOTO       CBTR2200 IF EQUAL
          GOTO       CBTR3000
.
.        Admission/Return record
.
CBTR2000  CALL       RBTY0000                * Read bed type
          BRANCH     EXIT,CBTR1200
.
          MOVE       TDATE,SAVDATE           * set from date
          MOVE       TTIME,SAVTIME
          GOTO       CBTR1200                * get next trans record
.
.         Change
.
CBTR2200  CALL       RBTY0000                * Read the new bed type
          IF         EXIT=0
            MATCH      SP70,SAVDATE
            GOTO       CBTR1200 IF NOT EQUAL * still Psychiatric bed
            MOVE       TDATE,SAVDATE
            MOVE       TTIME,SAVTIME
            GOTO       CBTR1200
          ENDIF
.
.         Change to a different bed type
.
          MATCH      SP8,SAVDATE
          GOTO       CBTR1200 IF EQUAL
.
          MOVE       TDATE,TTRNDATE
          MOVE       TTIME,TTRNTIME
          CALL       CDUR0000                * Calculate bed days
          GOTO       CBTR1200                * get next trans record
.
.         Discharge/Leave record always on the same bed type as previous record
.
CBTR3000  MATCH     SP8,SAVDATE
          GOTO      CBTR3200 IF EQUAL       * Was not on selected bed type
.
          MOVE      TDATE,TTRNDATE          * Set to date
          MOVE      TTIME,TTRNTIME
          CALL      CDUR0000                * Calculate bed days
.
          CMATCH    "D",TMOVE
          IF        @EQUAL
            MATCH     ADATE,DDATE
            IF        @EQUAL
              MOVE      ONE,ASPAFLAG        * flag for Psychiatric
            ENDIF
          ENDIF
.
CBTR3200  CMATCH    "D",TMOVE
          GOTO      CBTR1200 IF NOT EQUAL   * get next record
          GOTO      CBTR9000
.
.         check the last record
.
CBTR4000  MATCH     SP8,SAVDATE
          GOTO      CBTR9000 IF EQUAL       * Was not on selected bed type
.
          CALL      IBACLOCK
          PACK      TTRNDATE,CCC,CYY,CMM,CDD
          REP       " 0",TTRNDATE
          MOVE      CTIMEIS,TTRNTIME
          CALL      CDUR0000                * Calculate bed days
.
CBTR9000  MOVE      ZERO,FORM6
          MOVE      ZERO,F6
          MOVE      ZERO,F2
          COMPARE   ZERO,FORM6A
          GOTO      CBTR9999 IF EQUAL
.
          CALL      RTIM0000                * Rounding the time
CBTR9999  RETURN
+
.******************************************************************************
.        Read bed type
.        F1=1 - Calculate Psychiatric day from bed type transfer - Cat BT
.           2 - Calculate ICUA hours
.           3 - Calculate Psychiatric day from Ward/Bed Class.- Cat CG
.******************************************************************************
RBTY0000  BRANCH     F1,RBTY0400,RBTY0400,RBTY0200
          GOTO       RBTY9000
.
.         F1=3 for Checking change of Ward
.
RBTY0200  MATCH      SP70,TWARD
          GOTO       RBTY9000 IF EQUAL       * Blank ward
.
          PACK       KEY6,TWARD,SP70
          CALL       RDWARD1
          BRANCH     OVRCD,RBTY9000
.
          MATCH      SP70,WCLASS
          GOTO       RBTY9000 IF EQUAL       * Blank ward class.
.
          MOVE       "CG",KEY2
          PACK       KEY5,KEY2,WCLASS
          CALL       RDCODE1
          BRANCH     OVRCD,RBTY9000
.
          MATCH      "A",TCINDC26
          GOTO       RBTY9000 IF NOT EQUAL   * Not Psychiatric Class.
          GOTO       RBTY8000
.
.         Checking change of bed type
.
RBTY0400  MATCH      SP70,TRBTYP
          GOTO       RBTY9000 IF EQUAL       * Blank bed type
.
          PACK       KEY5,ANSB,ANST,TRBTYP
          CALL       RDCODE1
          BRANCH     OVRCD,RBTY9000
.
.         F1=1 for Psychiatric days,  F1=2 for ICUA hours
.
          BRANCH     F1,RBTY1000,RBTY2000
          GOTO       RBTY9000
.
RBTY1000  MATCH      "A",TCINDC3             * Check bed type
          GOTO       RBTY9000 IF NOT EQUAL   * Not Psychiatric bed
          GOTO       RBTY8000
.
RBTY2000  MATCH      "A",TCINDC14            * Check bed type
          GOTO       RBTY9000 IF NOT EQUAL   * Not ICUA bed
.
RBTY8000  MOVE       ZERO,EXIT
          GOTO       RBTY9999
.
RBTY9000  MOVE       ONE,EXIT
RBTY9999  RETURN
+
.******************************************************************************
.        CDUR0000 - Calculate dates/times difference
.        F1=1 - Calculate Psychiatric day from bed type transfer - Cat BT
.           2 - Calculate ICUA hours
.           3 - Calculate Psychiatric day from Ward/Bed Class.- Cat CG
.******************************************************************************
CDUR0000  BRANCH    F1,CDUR1000,CDUR2000,CDUR1000
          GOTO      CDUR9000
.
.         Calculate the dates difference
.
CDUR1000  PACK      CDYSFDTE,SAVDATE
          PACK      CDYSTDTE,TTRNDATE
.
          MOVE      ZERO,CDYSDAYS
          CALL      CALCDAYS
          MOVE      CDYSDAYS,CALDAYS
          SUB       ONE,CALDAYS
          ADD       CALDAYS,NBRDAYS          * number of days
          GOTO      CDUR9000
.
.         Calculate the times difference
.
CDUR2000  MOVE      SAVDATE,FIRSTDAT            * Previous transfer date
          MOVE      TTRNDATE,LASTDATE           * New Transfer date
.
          UNPACK    SAVTIME,CHOUR,KEY1,CMIN
          PACK      FIRSTIME,CHOUR,CMIN         * Previous Transfer Time
.
          UNPACK    TTRNTIME,CHOUR,KEY1,CMIN
          PACK      LASTTIME,CHOUR,CMIN         * New Transfer Time
          CALL      TIMEDIFF                    * Calculate Time Diff in Mins
          ADD       CALCTIME,FORM6A             * Save the total time
.
CDUR9000  UNPACK    SP70,SAVDATE,SAVTIME,TTRNDATE,TTRNTIME
CDUR9999  RETURN
+
.******************************************************************************
.         Rounding the time difference
.******************************************************************************
RTIM0000  ASSIGN    FORM6A/SIXTY,CALCTIME
          MOVE      CALCTIME,FORM6              * Set to Hrs
          MOVE      FORM6,F6                    * save the actual hrs
.
.         F1 = 2 for Calculating ICU hours
.         If ICU Hours is less than 1 hour, count as 1 hour
.
          IF        F1=2 & FORM6<1
            GOTO      RTIM8000
          ENDIF
.
          ASSIGN    CALCTIME*SIXTY,CALCTIME     * Set hrs back to mins
          ASSIGN    (FORM6A-CALCTIME),FORM6A    * now subtract from sav var
          MOVE      FORM6A,F2                   * Now have remainding mins
.
.         Check for the Minutes to round up ICU Hours
.
          REP       " 0",PTCNDRND
          MOVE      ZERO,FORM2
          MOVE      PTCNDRND,FORM2
          IF        FORM2<=0
            MOVE      "30",FORM2             * default to 30 minutes
          ENDIF
.
.         Check if the minutes equal to the parameter for Minutes to round up
.
          COMPARE   FORM2,F2
          GOTO      RTIM3000 IF NOT EQUAL
.
.         if the hours accompanied by the minutes is an odd number = round up
.         if the hours accompanied by the minutes is an even number = round down
.
          MOVE      ZERO,FORM2A
          ASSIGN    (FORM6%2),FORM2A
          COMPARE   ZERO,FORM2A
          GOTO      RTIM9999 IF EQUAL           * Even
.
          GOTO      RTIM8000                    * Odd hours, round up
.
.         round down if minutes are less than the value of parameter
.         round up to one hour if minutes are greater than the parameter
.
RTIM3000  COMPARE   FORM2,F2
          GOTO      RTIM9999 IF LESS
          
RTIM8000  ADD       ONE,FORM6                 * Round up mins to 1 hr
RTIM9999 RETURN
+
.******************************************************************************
.        Check for Claim Code/Care class Multiple NEP Values
.******************************************************************************
MNEP0000  MATCH     SP70,KEY2
          IF        !@EQUAL
            MATCH     SP70,D3
            GOTO      MNEP9999 IF EQUAL
            PACK      KEY5,KEY2,D3
            CALL      RDCODE1
            BRANCH    OVRCD,MNEP9999
          ENDIF
.
          MOVE      ZERO,OVRCD               * assume file exists
          TRAP      OVERCOND IF IO           * trap for error openning file
          OPEN      PMSNEPA1,"pmsnepaf"      * open kiwicard file
          TRAPCLR   IO
          BRANCH    OVRCD,MNEP9999           * open failed
.
          MOVE      " 3",DIM2                * Inpatients
          PACK      KEY58,CONTRCID,DIM2,KEY2,Z70
          CALL      RSPMNEP1
MNEP1000  CALL      RPPMNEP1
          BRANCH    OVRCD,MNEP9999
.
          MATCH     PMNECNTR,CONTRCID        * Same contract id ?
          GOTO      MNEP9999 IF NOT EQUAL
.
          MATCH     DIM2,PMNEVTYP            * Check type of visit
          GOTO      MNEP9999 IF NOT EQUAL
.
          MATCH     KEY2,PMNECODE            * Check Code set
          GOTO      MNEP9999 IF NOT EQUAL
.
          MATCH     "1",PMNEACTV
          GOTO      MNEP1000 IF EQUAL        * Inactive
.
          CALL      CIND0000                 * Check indicators
          BRANCH    EXIT,MNEP1000            * Check next record
.
MNEP9999  RETURN
+
.******************************************************************************
.        Check for Indicator number and value
.        Exit = 1 (Not valid)
.******************************************************************************
CIND0000  MATCH     SP70,PMNEINDC          * check for blank Indicator number
          GOTO      CIND2000 IF EQUAL
.
          STRIP     PMNEINDC
          MOVE      ZERO,F2
          MOVE      PMNEINDC,F2
.
.         Check indicator number and value of patient care class
.
          MOVE      SP70,KEY1
          LOAD      KEY1,F2,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5:
                            TCINDC6,TCINDC7,TCINDC8,TCINDC9,TCINDC10:
                            TCINDC11,TCINDC12,TCINDC13,TCINDC14,TCINDC15:
                            TCINDC16,TCINDC17,TCINDC18,TCINDC19,TCINDC20:
                            TCINDC21,TCINDC22,TCINDC23,TCINDC24,TCINDC25:
                            TCINDC26,TCINDC27,TCINDC28,TCINDC29,TCINDC30:
                            TCINDC31,TCINDC32,TCINDC33,TCINDC34,TCINDC35:
                            TCINDC36,TCINDC37,TCINDC38,TCINDC39,TCINDC40:
                            TCINDC41
.
          STRIP     PMNEINDV
          PACK      D1,PMNEINDV,SP70
          MATCH     KEY1,D1
          GOTO      CIND9000 IF NOT EQUAL
.
CIND2000  MOVE      PMNENEPP,NEPVALUE  * NEP value Adjust.
          MOVE      ZERO,EXIT
          GOTO      CIND9999
.
CIND9000  MOVE      ONE,EXIT
CIND9999  RETURN
+
.*******************************************************************************
.         Check for Private Patient
.*******************************************************************************
CPRV0000  MOVE      ADATE,FROMDATE
          MOVE      SP70,SAVATYPE
          MOVE      ZERO,COUNTER
          MOVE      SP70,STMOVE
.
          PACK      KEY30,AADMNO,SP70
          CALL      RDSTRAN2
CPRV1000  CALL      RDKTRAN2
          BRANCH    OVRCD,CPRV2000
.
          MATCH     TADMN,AADMNO             * same admission ?
          GOTO      CPRV2000 IF NOT EQUAL    * No.
.
          MOVE      TMOVE,STMOVE
          MATCH     "A",TMOVE
          GOTO      CPRV1200 IF EQUAL
.
          MATCH     "R",TMOVE
          GOTO      CPRV1400 IF NOT EQUAL        * Return from leave
.
CPRV1200  MOVE      TDATE,FROMDATE
          MOVE      TATYPE,SAVATYPE
          GOTO      CPRV1000
.
CPRV1400  MATCH     "L",TMOVE
          GOTO      CPRV3000 IF EQUAL             * leave record
          MATCH     "D",TMOVE
          GOTO      CPRV3000 IF EQUAL             * discharge record
.
.         must be Change record, check if Adm.type has changed
.
          BRANCH    ACDAYCS,CPRV4000              * daycase use the last tatype
.
          MATCH     TATYPE,SAVATYPE
          GOTO      CPRV1000 IF EQUAL
          GOTO      CPRV3000
.
.         Check previous movement
.
CPRV2000  MOVE      ONE,OVRCD
          MATCH     "D",STMOVE
          GOTO      CPRV5000 IF EQUAL
          MATCH     "L",STMOVE
          GOTO      CPRV5000 IF EQUAL             * Currently on leave
.
          PACK      TDATE,CCC,CYY,CMM,CDD
          REP       " 0",TDATE
.
CPRV3000  CALL      ABDY0000                      * store no of days
          BRANCH    OVRCD,CPRV5000
.
          MATCH     "D",STMOVE
          GOTO      CPRV5000 IF EQUAL
.
CPRV4000  MOVE      TATYPE,SAVATYPE
          MOVE      TDATE,FROMDATE
.
          GOTO      CPRV1000
.
.         Loop through the array for No of bed days for each Adm.type
.
CPRV5000  COMPARE   ZERO,COUNTER
          GOTO      CPRV9999 IF EQUAL
.
          MOVE      ZERO,F2
CPRV6000  ADD       ONE,F2
          COMPARE   F2,COUNTER
          GOTO      CPRV9999 IF LESS
.
          MOVE      ZERO,FORM3
          MOVE      ATYPCODE[F2],KEY3   * adm.type
          MOVE      ATYPBDAY[F2],FORM3  * Bed days
.
          IF        ACDAYCS=1
            MOVE      ZERO,FORM3             * day case
          ENDIF
          PACK      KEY9,ACLAIM,KEY3,FORM3
          CALL      RDPTACC1                 * read the account status file
          COMPARE   ZERO,OVRCD
          GOTO      CPRV6400 IF EQUAL
.
CPRV6200  CALL      RKPTACC1                 * get the next record on the
          BRANCH    OVRCD,CPRV6000           * account status file
.
          MATCH     ACLAIM,PTASCLAM          * match claim codes
          GOTO      CPRV6000 IF NOT EQUAL
          MATCH     KEY3,PTASATYP            * match admission types
          GOTO      CPRV6000 IF NOT EQUAL
.
CPRV6400  CMATCH    "P",PTASACCS             * Check 1st char =P for Private
          GOTO      CPRV6000 IF NOT EQUAL
.
          MOVE      ONE,PRIVFLAG             * Flag for Private Patient
.
CPRV9999  RETURN
+
.******************************************************************************
.         Add bed days to the array
.******************************************************************************
ABDY0000  MOVE      ZERO,NBRDAYS
          MOVE      TDATE,TODATE
          DAYSEP    FROMDATE,TODATE,NBRDAYS
.
          MATCH     FROMDATE,TODATE
          IF        @EQUAL
            COMPARE   ONE,ACDAYCS
            GOTO      ABDY9999 IF NOT EQUAL
            MOVE      ZERO,NBRDAYS           * daycase
          ENDIF
.
          MOVE      ZERO,F2
ABDY1000  COMPARE   COUNTER,F2
          GOTO      ABDY2000 IF NOT LESS
.
          ADD       ONE,F2
.
          MATCH     SAVATYPE,ATYPCODE[F2]
          GOTO      ABDY1000 IF NOT EQUAL
.
          ADD       NBRDAYS,ATYPBDAY[F2]
          GOTO      ABDY9999
.
ABDY2000  ADD       ONE,F2
          MOVE      SAVATYPE,ATYPCODE[F2]   * adm.type
          MOVE      NBRDAYS,ATYPBDAY[F2]  * Bed days
          ADD       ONE,COUNTER
ABDY9999  RETURN
+
.******************************************************************************
          INC       CALCAGE
          INC       CLPMSABF
          INC       TIMEDIFF
          INC       GETDRG
.
          INC       PMSPX2IO/INC
          INC       IBAPOSIO/INC
          INC       BOKRX1IO/INC
          INC       EMRPWDIO/INC
          INC       EMRURGIO/INC
          INC       EMRVCDIO/INC
          INC       OUTPWOIO/INC
          INC       PATDADIO/INC
          INC       PMSVMTIO/INC
          INC       PATECOIO/INC
          INC       PATCHCIO/INC
          INC       PATACCIO/INC
.
          INC       PMSNEPIO/INC
          INC       PMSADJIO/INC
          INC       PMSPWAIO/INC
          INC       PMSHACIO/INC
          INC       PMSHDEIO/INC
          INC       PMSHADIO/INC
          INC       PMSABFIO/INC
          INC       PMSRDIIO/INC
          INC       EMRVISIO/INC
          INC       IBAOVRIO/INC
.
NDIP9999  ENDPROC
+
.******************************************************************************
.        Display/Print ABF Charges
.******************************************************************************
SABF0000  PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          MATCH     ADATE,KEY8
          GOTO      SABF1000 IF NOT EQUAL  * Patient not admitted today
.
          COMPARE   ZERO,LOSNDAYS
          GOTO      SABF9999 IF EQUAL
.
SABF1000  BRANCH    PROGFLAG,SABF1100,SABF2000,SABF3000,SABF4000
          GOTO      SABF9999
.
SABF1100  IF        INVDFLAG=1
            CALL      UINP0000                * Update Invoice Pending details
          ENDIF
          GOTO      SABF9999
.
SABF2000  CALL      DSAB0000         * Display ABF Charges
          GOTO      SABF9999
.
SABF3000  MOVE      CNEXTINV,PINVNO
          MOVE      AADMNO,PINVADM
          MOVE      AURNO,PINVUR
          PACK      PINVDTE WITH CCC,CYY,CMM,CDD
          REP       " 0",PINVDTE
          MOVE      THREE,PINVSYS
.
          CALL      PRAB0000         * Print ABF Charges
.
SABF4000  CALL      ABFN0000         * Set up key for a call to PATCALF
          GOTO      SABF9999
.
SABF9999  RETURN
+
.
.******************************************************************************
.        Update Invoice Pending details
.******************************************************************************
UINP0000  CALL      CLPATINP                * clear patinpaf vars
.
          PACK      KEY30,AADMNO,Z70
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,UINP9999
.
          MATCH     TADMN,AADMNO            * same admission ?
          GOTO      UINP9999 IF NOT EQUAL
.
          MOVE      AADMNO,PTIPADMN         * admission number
          MOVE      AURNO,PTIPURNO          * u/r number
          MOVE      "3A",PTIPRTYP           * Record type ABF
          MOVE      ACLAIM,PTIPCLAM         * claim type
          MOVE      AFUNDH,PTIPHFND         * health fund
          MOVE      AFNDTB,PTIPFTBL         * health fund table
          MOVE      PDRGCODE,PTIPITEM       * Admission/Disc. DRG
          MOVE      FOUR,PTIPTTYP           * transaction type
.
          MOVE      TATYPE,PTIPATYP         * admission type
          MOVE      TWARD,PTIPWARD          * ward
          MOVE      TBED,PTIPWBED           * bed
.
          MOVE      ONE,PTIPTRNN            * Transaction number
          MATCH     SP3,TWARD
          IF        @EQUAL
            MOVE      NODAYS,PTIPLDAY       * leave days
            MOVE      ZERO,PTIPBDAY
          ELSE
            MOVE      TOTDAYS,PTIPBDAY      * days in bed
            MOVE      ZERO,PTIPLDAY
            MOVE      TRBTYP,PTIPBTYP       * bed type
          ENDIF
.
          MOVE      IDATEF,PTIPACCF         * accommodation date from
          MOVE      IDATET,PTIPACCT         * accommodation date to
          MATCH     ANSA,TMOVE
          IF        @EQUAL
            MOVE      ADOCTA,PTIPADOC
          ELSE
            MOVE      TADOCT,PTIPADOC      * attending doctor code
          ENDIF
          PACK      KEY6,PTIPADOC
          CALL      RDDOCT1
          IF        OVRCD<>1
            MOVE      DSNAME,PTIPFNAM
            STRIP     PTIPFNAM
            ENDSET    PTIPFNAM
            APPEND    DGNAME,PTIPFNAM
            RESET     PTIPFNAM
            STRIP     PTIPFNAM
            ENDSET    PTIPFNAM
            APPEND    DTITL,PTIPFNAM
            RESET     PTIPFNAM              * attending doctor name
            MOVE      DRTYPE,PTIPDTYP       * attending doctor specialty
          ENDIF
          MOVE      GROSSTOT,PTIPGREV       * Gross revenue
.
          CALL      GDTA0000                * get date/time & operator id
.
          PACK      KEY16,PTIPADMN,PTIPRTYP,PTIPTRNN
          CALL      RAPTINP1
          IF        OVRCD=1
            CALL      WRPTINP1
          ENDIF
.
UINP9999  RETURN
+
.******************************************************************************
.        Write ABF charges to Financial file
.******************************************************************************
ABFN0000  MOVE      ABFCHRGE,LINETOT
          PACK      PINVDTE WITH CCC,CYY,CMM,CDD
          REP       " 0",PINVDTE
.
          MOVE      LINETOT,FINAMT
          MOVE      "A",FINTYPE
          PACK      FINCODE,ANSM,ACLAIM,SP20    
          CALL      WFINS000                * Write a record to patfinsf
.
ABFN9999  RETURN
+
.******************************************************************************
.        Display ABF Charges
.******************************************************************************
DSAB0000
          REP       " 0",ADATE
          UNPACK    ADATE,XCENT,XYEAR,XMON,XDAY
.
          MOVE      ZERO,FORM2
          MOVE      XMON,FORM2
          LOAD      MTHNAM3,FORM2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG:
                                  NSEP,NOCT,NNOV,NDEC
.
          WRITE     HTMLFILE;"<tr><td>",XDAY,SP1,MTHNAM3,SP1,XCENT,XYEAR:
                               " to ";
.
          PACK      TODATE,CCC,CYY,CMM,CDD
          IF        ASTAT=3
            MOVE      DDATE,TODATE
          ENDIF
          REP       " 0",TODATE
.
          UNPACK    TODATE,XCENT,XYEAR,XMON,XDAY
          MOVE      ZERO,FORM2
          MOVE      XMON,FORM2
          LOAD      MTHNAM3,FORM2,NJAN,NFEB,NMAR,NAPR,NMAY,NJUN,NJUL,NAUG:
                                  NSEP,NOCT,NNOV,NDEC
.
          WRITE     HTMLFILE;XDAY,SP1,MTHNAM3,SP1,XCENT,XYEAR;
.
          UNPACK    LSDESC2,KEY9        * DRG Code
          STRIP     KEY9
.
          MOVE      SP70,KEY30
          CLEAR     KEY30
.
          MOVE      TOTDAYS,KEY4
          STRIP     KEY4
          APPEND    KEY4,KEY30
          APPEND    " Days  ",KEY30
.
          APPEND    KEY9,KEY30          * DRG code
          APPEND    SP1,KEY30
          APPEND    LSDESC1,KEY30       * PW Description
          APPEND    SP70,KEY30
          RESET     KEY30
.
          WRITE     HTMLFILE;"</td><td><table width=100%><tr><td>",KEY30;
.
          PACK      KEY8,FCENT1,FYEAR1,FMON1,FDAY1,SP70
          REP       " 0",KEY8
          WRITE     HTMLFILE;"</td></tr></table>";
.
          WRITE     HTMLFILE;"</td><td>&nbsp;",SAVCONTR:  * contract id
                               "</td><td>&nbsp;":
                               "</td><td>&nbsp;":
                               "</td><td>&nbsp;";
.
          MATCH     "1",PTCNPPIN
          IF        @EQUAL
            WRITE     HTMLFILE;"</td><td>&nbsp;";
          ENDIF
.
          WRITE     HTMLFILE;"</td><td>&nbsp;":
                             "</td><td>&nbsp;":
                             "</td><td align=right>",GROSSTOT;
.
          WRITE     HTMLFILE;"</td></tr>"
.
DSAB9999 RETURN
+
.******************************************************************************
.        Print ABF Charges
.******************************************************************************
PRAB0000  PACK      KEY30 WITH AADMNO,Z70
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD OF PRAB9999
.
          MATCH     TADMN,AADMNO
          GOTO      PRAB9999 IF NOT EQUAL
.
          MOVE      GROSSTOT,LINETOT
          MOVE      GROSSTOT,LSTRATE
          MOVE      ZERO,LINEGST             * No GST for ABF
.
          CALL      CLPATDTR
          MOVE      "A" TO TTYPEDEB
          MOVE      "9",TRECTYPE      (9 = ABF)
          MOVE      LINETOT,TPATAMTT
          MOVE      LINETOT,TPATAMTI
          MOVE      LINETOT,TPATAMTH
          MOVE      LINETOT,TREBATE
.
          PACK      TDDESC,LSDESC1,SP70
          PACK      PTDTDES2,LSDESC2,SP70
          MOVE      TOTDAYS,TNODAYS
.
          MOVE      ACLAIM,TCLMTYP
          MOVE      STRBTYP,PTDTBTYP
          UNPACK    IDATET,TCENT1,TYEAR1,TMON1,TDAY1
.
          MOVE      ACLAIM,PTTRCLTY
          MOVE      CONTRCID,PTDTCNID      * Contract ID
          CALL      WRITETRN
.
          MOVE      TOTDAYS,NODAYS
          PACK      DFDATE,FCENT1,FYEAR1,FMON1,FDAY1,SP70
          PACK      DTDATE,TCENT1,TYEAR1,TMON1,TDAY1,SP70
.
          MOVE      ONE,RECFLAG     * flag for printing accommodation
          CALL      PRLN0000        * Print line of invoice
.
PRAB9999 RETURN
+
