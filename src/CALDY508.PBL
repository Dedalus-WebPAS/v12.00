. ***************************************************************************
. *              I N C L U D E  S U M M A R Y                               *
. *              -------------  -------------                               *
. *                                                                         *
. *     INCLUDE NAME:     NHOSPDAY  (CALDY508.PBL)                          *
. *                                                                         *
. *     FUNCTION:         CALCULATE DAYS IN HOSPITAL                        *
. *                                                                         *
. *     DATE WRITTEN:     24/06/86                                          *
. *                                                                         *
. *     AUTHOR:           PAUL LO                                           *
. *                                                                         *
. *     MODIFICATIONS:                                                      *

. *   V9.02.00   18.06.2002   Glenn Saunders   V9.02 port                   *
. *              Export to new release i.e. v9.02 from vf.09                *
. *              Remove PSN/OSN prefixed PAS cloned includes                *
. *-------------------------------------------------------------------------*
. *   V6.03.02   27/10/1995   Greg Horvat      SRF 612064 612205 612206     *
. *              Changed to sub one from the total no. of days in CALC      *
. *   V6.03.01   11/10/1995   Greg Horvat      SRF 611811                   *
. *              Changed to use CALCDAYS instead of CALC                    *
. *   V5.01.01   11/06/92     J.Tan            SRF 114981                   *
. *              Fixed if the 2 dates have  more than one year difference   *
. *              02/11/87     G.Williams 02/11/87                           *
. *              New Transfer File format                                   *
. *              M.HAYSE DOES HANDLE MULTPLY DAYS NRBDAYS GETS WAPPED IN    *
. *              MULTY TRANSFERS AND DOESN'T ADD UP CURRENT PATIENTS DOESNT *
. *              INCLUDE TODAY AS NUMBER OF DAYS                            *
. *                                                                         *
. ***************************************************************************
.
.
.                READ TRANSFER FILE
.
NHOSPDAY MOVE           SP8,STDATE
        MOVE            SP1,STMOVE
        MOVE            ZERO,NBRDAYS
        MOVE            ZERO,CALDAYS
.
        MATCH           "3",DASTAT                                     * V90200
        GOTO            NOSAME IF NOT EQUAL
.
        MATCH           ADATE,DDATE                                    * V90200
        GOTO            NOSAME IF NOT EQUAL
.
        MATCH           FROMDATE,ADATE                                 * V90200
        RETURN          IF LESS
.
        MATCH           ADATE,TODATE                                   * V90200
        RETURN          IF LESS
.
        MOVE            ONE,NBRDAYS
        RETURN
.
NOSAME  PACK            KEY30 WITH DAADMNO,SP20
        CALL            RDSTRAN2                                       * V90200
NEXTTN  CALL            RDKTRAN2                                       * V90200
        BRANCH          OVRCD OF XXXX
.
        MATCH           DTADMN,DAADMNO                                 * V90200
        GOTO            XXXX IF NOT EQUAL
.
        MOVE            TMOVE,STMOVE                                   * V90200
        REP             " 0",TDATE                                     * V90200
.
        MATCH           "A",TMOVE                                      * V90200
        GOTO            ARITYPE IF EQUAL
        MATCH           "R",TMOVE                                      * V90200
        GOTO            ARITYPE IF EQUAL
        MATCH           "L",TMOVE                                      * V90200
        GOTO            LOTYPE IF EQUAL
        MATCH           "D",TMOVE                                      * V90200
        GOTO            NEXTTN IF NOT EQUAL
.
        MATCH           FROMDATE,TDATE                                 * V90200
        RETURN          IF LESS
        MATCH           TODATE,TDATE                                   * V90200
        GOTO            XXXX IF LESS
        MOVE            TODATE,TDATE                                   * V90200
        GOTO            XXXX
.
.
.
ARITYPE MATCH           "A",TMOVE                                      * V90200
        GOTO            CONARI IF NOT EQUAL
.
        MATCH           TDATE,ADATE                                    * V90200
        GOTO            CONARI IF EQUAL
.
.       PRINT           *N,"*** Admission : ",DAADMNO," Date is ",ADATE:
.                       "   Transfer File Admit Date is ",TDATE:
.                       "Contact I.B.A *** NOW ***":
.                       " (NHOSPDAY)"
.
        DISPLAY         *P1:23,*EL,*V2LON,*B:
                        "*** Admission : ",DAADMNO:
                        "   Transfer and Admission date DIFFERENT ***",*W
.
CONARI  MATCH           TODATE,TDATE                                   * V90200
        RETURN          IF NOT LESS
        MATCH           FROMDATE,TDATE                                 * V90200
        GOTO            CONT30 IF NOT LESS
        MOVE            FROMDATE,STDATE
        GOTO            NEXTTN 
.
CONT30  MOVE            TDATE,STDATE                                   * V90200
        GOTO            NEXTTN
.
LOTYPE  MATCH           FROMDATE,TDATE                                 * V90200
        GOTO            NEXTTN IF LESS
.
        MATCH           TODATE,TDATE                                   * V90200
        GOTO            CONT8 IF LESS
.
        MOVE            TODATE,WDATE1
        MOVE            STDATE,WDATE2
.
        CALL            CALC
        RETURN
.
CONT8   MOVE            TDATE,WDATE1                                   * V90200
        MOVE            STDATE,WDATE2
        CALL            CALC
        GOTO            NEXTTN
.
XXXX    MATCH           "L",STMOVE
        RETURN          IF EQUAL
        MATCH           " ",STMOVE
        RETURN          IF EQUAL
.
.         this is the last record
.
        MATCH           "D",STMOVE
        GOTO            CONT31 IF NOT EQUAL
.
.       PATIENT IS DISCHARGED
.
        MOVE            TDATE,WDATE1                                   * V90200
        GOTO            CONT10
.
.        PATIENT IS CURRENTLY ADMITTED
.
CONT31  MOVE            TODATE,WDATE1
.
CONT10  MOVE            STDATE,WDATE2
        CALL            CALC
        RETURN
+
.
.              CALCULATE DATES DIFFERENCE
.
CALC      PACK      CDYSFDTE,WDATE2
          PACK      CDYSTDTE,WDATE1
          CALL      CALCDAYS
          MOVE      CDYSDAYS,CALDAYS
          SUB       ONE,CALDAYS        * Only want the diff between the 2 dates
          ADD       CALDAYS,NBRDAYS
          RETURN
