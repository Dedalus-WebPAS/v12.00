.******************************************************************************
.INVPRT
.
.         PATANVS.PBL  -  INClude to RE-PRINT INVOICES
.
.         Prints : Line Up, Tail and Heading for Normal Invoices and Receipts
.
.         Used By: IBABIL09; IBABIL21; PATWEB76
.
.         VERSION: 
.
.         V11.00.01 02/04/2020  J.Tan             TSK 0868813
.                   Changed PKNAME to DIM80
.         V10.09.01 19/01/2017 J.Tan          TSK 0829932
.                   Fixed PRFA address4 PKADD4 defaulting from master details
.         V10.02.01 04/04/2011 Jill Parkinson CAR 239574
.                   Removed open of ibaprntf
.         V10.01.01 06/01/2011 Jill Parkinson CAR 233088
.                   Changed to use pmvxfdnm
.         V10.00.02 31/09/2010 J.Tan      CAR 230664
.                   Mods for HEC invoice layout
.         V10.00.01 29/03/2010 J.Tan      CAR 174079
.                   Removed closing patre1af file
.         V9.12.02  08/01/2010 J.Tan      CAR 202277
.                   Fixed printing tail of SJOG invoice
.         V9.12.01  14/07/2009 J.Tan      CAR 199760
.                   Mods to check for Events visit
.         V9.11.03  20/01/2009 J.Tan      CAR 187842
.                   Fixed printing tail for NZ Private
.         V9.11.02  02/12/2008 J.Tan      CAR 160496
.                   Mods RCH Emergency reprint invoice
.         V9.11.01  30/10/2008 J.Tan      CAR 181263
.                   Mods for printing Discharge Billing Statement
.         V9.10.03  12/08/2008 J.Tan      CAR 175967
.                   Fixed Doctor provider number
.         V9.10.02  22/07/2008 J.Tan      CAR 173734
.                   Fixed doctor name for Emergency invoice header
.         V9.10.01  23/05/2008 J.Tan      CAR 167702
.                   Mods for Cabrini invoice layout
.         V9.09.01  05/07/2007  Peter Vela     CAR 144548
.                   Initialised PRA variables to PMI values
.         V9.08.04  08/06/2007  Mike Laming    CAR 125736
.                   Changes to PATDO1af Address lines - change DSADD3 to DSADD4
.                   and DSADD2 to DSADD3 (new DSADD2 still to go in)
.         V9.08.03  14/03/2007  J.Tan          CAR 135853
.                   Added comments for Mike's changes for JH PRFA format
.         V9.08.02  08/02/2006 J.Tan      CAR 120001
.                   Mods for NZ Billing
.         V9.08.01  14/11/2006 Peter Vela CAR 124547
.                   Added functionality for PRFA of Deceased Person
.         V9.07.03  25/09/2006 Peter Vela CAR 118940
.                   Removed Lines for PTCNINVL=12
.         V9.07.02  31/07/2006 J.Tan      CAR 103365
.                   Mods for John hopkins invoice
.         V9.07.01  30/08/2006 J.Tan      CAR 115853
.                   Added a line after printing tail for PTCNINVL=16
.         V9.06.01  22/05/2006 Peter Vela CAR 104103
.                   Added Lines for PTCNINVL=12
.         V9.04.08  28/06/2005 Peter Vela CAR 65165
.                   Added open for CONTROLF
.         V9.04.07  27/05/2005 J.Tan   CAR 61488
.                   Added field for page number
.         V9.04.06  23/05/2005 J.Tan   CAR 61488
.                   Mods to replace @ with 'Parent of'
.         V9.04.05  01/12/2004 J.Tan   CAR 55599
.                   Add an extra line after the heading invoice      
.         V9.04.04  15/11/2004 J.Tan   CAR 54768
.                   Mods for Pendlebury invoice layout (PTCNINVL=16)
.         V9.04.03  11/11/2004 J.Tan   CAR 54665
.                   Fixed Holy spirit invoice layout      
.         V9.04.02  15/10/2004 J.Tan   CAR 54312
.                   Fixed reprint SVHM outpatient invoice (2nd page)
.         V9.04.01  03/09/2004 Lina Vo  CAR 52998
.                   Initialise Page Number
.         V9.03.01  27/06/2003 J.Tan
.                   Mods for WEB Billing
.         V9.02.07  26/10/2002 Dean Cramer Defect
.                   Make so Outpatient body reprint same as original    
.         V9.02.06  27/09/2002 Dean Cramer 
.                   Print one more space line before tail in STVM format
.         V9.02.05  08/08/2002 Dean Cramer 
.                   Include IF for Calvary Cairns within RCH format
.         V9.02.04  28/05/2002 J.Tan
.                   Mods for SVH layout
.         V9.02.03  09/05/2002 J.Tan
.                   Ported from r6.09
.         V6.07.05  13/10/2000  Steve Downing
.                   Fixed printing of admission/discharge diagnosis 1 & 2
.         V6.07.04  24/07/2000  Davin
.                   Mods for templating - call templating when over 2 pages
.         V6.07.03  19/06/2000  Davin  SRF 2684
.                   Made linefeed after header layout specific
.         V6.07.02  31/05/2000  Davin
.                   Set line counter after printing header (depends on layout)
.         V6.07.01  29/05/2000  Davin
.                   Reset line counter to zero after printing invoice header
.         V6.07.B02 11/04/2000  Greg Horvat
.                   Removed any reference to matadmbf
.         V6.07.B01 10/04/2000  Davin
.                   Mods for templating - use PATINVHL for headers
.         V6.06.02  06/01/2000  J.Tan
.                   Fixed displaying current dates
.         V6.06.01  01/09/1999  Davin  SRF 633607             
.                   Added linefeed to header of AAE reprint 7           
.         V6.06.00  03/05/1999  Katie Nelson                  
.                   Added web flags                                     
.         V6.05.03  09/02/1999  Steve Armstrong   srf 630128
.                   Mods to remove century from printed dates for WCH
.         V6.05.02  15/10/1998  Nicole Harrington
.                   Changed to display century using PACDATE
.         V6.05.01  06/08/1998  Davin  SRF 627245
.                   Use AFUNDH instead of PFUNDH - layout 20
.         V6.04.07  08/03/98 J.Tan  SRF 619732
.                   Mods to write to a text file
.         V6.04.06  22/10/1997  Davin
.                   More tail changes for Peninsula - layout 22
.         V6.04.05  20/10/1997  Davin
.                   Changes to tail for Peninsula - layout 22
.         V6.04.04  14/08/1997  Davin
.                   Added new invoice layout for Peninsula - layout 22
.         V6.04.03  23/07/97 J.Tan     SRF 620354
.                   Mods Invoice layout 20 - to print claim details
.         V6.04.02  23/06/1997  Greg Horvat
.                   Added new invoice layout for Greenslopes - layout 21
.         V6.04.02  12/12/1996  Greg Horvat      Holy Spirit Mods
.                   Changed layout 20 to print the health fund description
.         V6.04.01  03/12/1996  Greg Horvat      Holy Spirit Mods
.                   Added new invoice layout for Holy Spirit - layout 20
.
.         V6.03.12  05/09/1996  Greg Horvat      SRF 616714
.                   Changed layout 5 to print the reference number in invoice
.                   & receipt headers
.         V6.03.11  27/05/1996  Greg Horvat      Quote 8623
.                   Added new invoice layout for Adelaide Clinic - layout 19
.         V6.03.10  19/03/1996  Greg Horvat      SRF 614578
.                   Fixed invoice layout 10 to print the header correctly when
.                   printing with the laser printer parameter is turned on
.         V6.03.09  27/12/1995  Greg Horvat      Quote 8604
.                   Changed invoice layout 10 to print the attending doctor
.                   name & provider number for A&E patients
.         V6.03.08  16/11/1995  Greg Horvat      SRF 611839 (Rebound)
.                   Changed layout 18 tail again
.         V6.03.07  03/11/1995  Greg Horvat      SRF 611839
.                   Added new invoice layout for Mater - layout 18
.         V6.03.06  27/10/1995  Greg Horvat      SRF 612379
.                   Changed St Andrews layout with HCA tail (good stuff!) to
.                   print 4 lines extra at the end
.         V6.03.05  01/09/1995  Greg Horvat      SRF 611483
.                   Fixed St Andrews header when printing an inpatient invoice
.                   and printing 2 diagnosis lines
.         V6.03.04  11/08/1995  Greg Horvat      SRF 611173
.                   Fixed Epworths tail reprinting over 2 pages to print 2
.                   lines less
.         V6.03.03  10/08/1995  Matt             SRF 611126
.                   Fixed satement no. 2.  
.         V6.03.02  03/08/1995  Matt             SRF 610903
.                   Fixed invoice header 7
.         V6.03.B1  07/02/1995  Gabrielle        SRF 134908
.                   Fixed invoice layout No 6
.******************************************************************************
.         V6.02.01  29/09/1994  Greg Horvat      SRF 127490
.                   Mods printing patient details to its previous position
.                   before the envelope window, layout 2
.         V6.02.02  29/09/1994  Greg Horvat      SRF 132488
.                   Moved the amounts printed to the right by 1 character
.
.******************************************************************************
.
.         JULCON
.         DMYCON
.
.         MATIOMAB                     * Maternity
.
.         PATCALFN
.         PATCATAI
.         PATIOCOD
.         PATIODSC
.         PATIODTR
.         PATIOHFI                     * Health Fund to be Re-Invoiced
.         PATIOINV
.         PATIOMAS
.         PATIOMIS
.         PATIOOVR
.         PATIOPRN
.         PATIORES
.         PATPRINT
+
.         ===========================================
.         Re-print an invoice or print a cash receipt
.         ===========================================
INVPRT    MOVE      ZERO,GROSSTOT
          MOVE      ZERO,NETTOT
          MOVE      ZERO,GOVTOTAL
          MOVE      ZERO,TOTDAYS
          MOVE      ZERO,NODAYS
          MOVE      ZERO,PAGENO
.
          MOVE      SP10,DINVTP
          LOAD      DINVTP USING PINVTYP OF DINSUR
.
.         Open a spool file for this reprint
.
          MOVE      PINVADM,ADANUMB
          MOVE      ADANUMB,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,INVPRT10
.
          IF        PVITYPE=9
            MATCH     " 1",PVISYST
            GOTO      INVPRT10 IF EQUAL    * Events
          ENDIF
.
          IF        PVITYPE=2
            MOVE      PFUNDH,AFUNDH
            MOVE      PFNDTB,AFNDTB
            MOVE      PFNDMM,AFNDMM
            MOVE      PFNDMM,PMVXFNDM
          ENDIF
.          
          COMPARE   ONE,PVITYPE
          GOTO      INVPRT30 IF NOT EQUAL
.
INVPRT10  MOVE      SP70,DPROV
          OPEN      PATDO1A1,"patdo1af"
          MOVE      ADADOCT,KEY6
          CALL      RDDOCT1
.
          MATCH     "1",EMCNGINV            * Group invoice by Service Doctor
          IF        @EQUAL
            OPEN      PMSHCPA1,"pmshcpaf"
            PACK      KEY10,SERVDOCT,SP70
            CALL      RDPMHCP1
            IF        OVRCD<>1
              MOVE      PMHCTITL,DTITL
              MOVE      PMHCGNAM,DGNAME
              MOVE      PMHCSNAM,DSNAME
              MOVE      PMHCPRV1,DPROV
            ENDIF
          ENDIF
.
          UNPACK    ADADATE WITH CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,A10DATE
.
          MOVE      PFUNDH,AFUNDH
          MOVE      PFNDTB,AFNDTB
          MOVE      PFNDMM,AFNDMM
          MOVE      PFNDMM,PMVXFNDM
.
          COMPARE   ZERO,HSTANDM
          GOTO      INVPRT20 IF NOT EQUAL
.
          MOVE      ADANUMB,KEY8
          CALL      RDDISA1
          BRANCH    OVRCD OF INVPRT20
.
          UNPACK    ADSDATE WITH CCENT1,CYEAR1,CMON1,CDAY1
          CALL      PACDATE
          MOVE      CPCDATE,D10DATE 
          GOTO      INVPRT22
.
INVPRT20  MOVE      SP10,D10DATE
          GOTO      INVPRT22
.
INVPRT30  UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,A10DATE
.
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,D10DATE
.
.         Get the discharge diagnosis if available
.
          MATCH     SP8,DDATE
          GOTO      INVPRT22 IF EQUAL
.
          MATCH     SP70,DDIAG         * Does discharge diagnosis exist?
          GOTO      INVPRT22 IF EQUAL  * No, don't replace admission diagnosis
          MOVE      DDIAG,ADIAG1
          MOVE      DDIAG2,ADIAG2
.
          MATCH     SP9,DFMBS
          GOTO      INVPRT22 IF EQUAL
          MOVE      DFMBS,AMBS
.
INVPRT22  CALL      GPRAC000                 * Get PRFA details
.
.         Print the heading
.
          BRANCH    DBSFLAG,INVPRT99         * printing Disc.billing statement
.
          CALL      HEAD
.
.         Print the body of the invoice
.
          MOVE      ONE,PRINTFLG
          CALL      CALCTAI
.
INVPRT99  RETURN
.
.******************************************************************************
.         Get the person responsible for the account
.******************************************************************************
GPRAC000  COMPARE   ONE,ISBLFLAG
          GOTO      GPRAC080 IF NOT EQUAL       * Not Independence Serv.billing
.
          CALL      IPYR0000                    * Get payer details
          BRANCH    EXIT,GPRAC999
          GOTO      GPRAC300
.
GPRAC080  COMPARE   "22",PTCNINVL
          GOTO      GPRAC100 IF NOT EQUAL       * John Hopkins invoice
.
          CALL      JHPFA000                    * Set default PRFA name
          MOVE      PFNAME,PKNAME
          GOTO      GPRAC200
.
GPRAC100  COMPARE   "23",PTCNINVL
          GOTO      GPRAC300 IF NOT EQUAL   * Not Southern Cross invoice
.
GPRAC200  MATCH     "PRFA",EXPPAYER
          GOTO      GPRAC300 IF EQUAL
          MATCH     SP70,EXPPAYER
          GOTO      GPRAC300 IF EQUAL
.
          OPEN      PATFN1A1,"patfn1af"
          PACK      KEY14,EXPPAYER,ZERO8,SP70
          CALL      RDFUND1
          IF        OVRCD=0
            PACK      PKNAME,HFNAME,SP70
            PACK      PKADD1,HFADD1,SP70
            PACK      PKADD2,HFADD2,SP70
            PACK      PKSUBR,HFADD3,SP70
            PACK      PKADD4,HFADD4,SP70
            PACK      PKPOST,HFPCODE,SP70
            GOTO      GPRAC400 
          ENDIF
.
GPRAC300  MOVE      PINVADM,KEY8
          OPEN      PATRE1A1,"patre1af"
          CALL      RDRESP1
.
          COMPARE   "22",PTCNINVL
          GOTO      GPRAC400 IF NOT EQUAL
          MATCH     "SELF",PKRELP
          GOTO      GPRAC900 IF EQUAL
.
GPRAC400  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*237,PTCNPAOF
          READ      CONTROLF,HUND03;*220,PTCNDEES,*221,PTCNDDES
.
          MATCH     "1",PTCNDEES             * using deceased PRFA indicator
          GOTO      GPRAC500 IF NOT EQUAL     * no
.
          CMATCH    "$",PKNAME
          IF        @EQUAL
            STRIP     PTCNDDES
            MOVE      PTCNDDES,PFNAME
            UNPACK    PKNAME,DIM2,KEY78      * ignore "$ "
            PACK      PFNAME,PFNAME,SP1,KEY78,SP30
            GOTO      GPRAC700
          ENDIF
.
GPRAC500  COMPARE   ONE,PTCNPAOF             * using 'parent of'?
          GOTO      GPRAC600 IF NOT EQUAL      * no
.
.---- If first character of PKNAME is "@" then replace with "Parent of"
.
          CMATCH    "@",PKNAME
          IF        @EQUAL
            MOVE      "Parent of ",PFNAME
            UNPACK    PKNAME,KEY2,KEY78      * ignore "@ "
            PACK      PFNAME,PFNAME,KEY78,SP30
            GOTO      GPRAC700
          ENDIF
.
GPRAC600  MATCH     SP70,PKNAME
          GOTO      GPRAC720 IF EQUAL
          GOTO      GPRAC720 IF EOS
          PACK      PFNAME WITH PKNAME,SP30
.
GPRAC700  COMPARE   ZERO,OVRCD
          GOTO      GPRAC999 IF EQUAL
.
GPRAC720  RESET     PGNAME
          MOVE      PGNAME,ANS
          PACK      DIM8 WITH SP1,PTITL,SP1,ANS,DOT
GPRAC780  CMATCH    SP1,DIM8
          GOTO      GPRAC800 IF NOT EQUAL
          BUMP      DIM8
          GOTO      GPRAC780  IF NOT EOS
.
GPRAC800  PACK      PFNAME WITH DIM8,PTMASNAM,SP70
          REP       ", ",PFNAME
.
GPRAC900  MOVE      PSUBRB,PKSUBR
          MOVE      PADD1,PKADD1
          MOVE      PADD2,PKADD2
          MOVE      PADD4,PKADD4
          MOVE      PPOST,PKPOST
          MOVE      SP10,PKRELP
GPRAC999  RETURN
+
.*******************************************************************************
.         Get payer name as Person responsible for Account
.*******************************************************************************
IPYR0000  MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PMSPYRA1,"pmspyraf"
          TRAPCLR   IO
          BRANCH    OVRCD,IPYR8000
.
          PACK      KEY19,SPVIURNO,PINVCLM,PINVDTE,SP70
          CALL      RSPMPYR1
IPYR1000  CALL      RPPMPYR1
          BRANCH    OVRCD,IPYR8000
.
          MATCH     SPVIURNO,PMPYURNO
          GOTO      IPYR8000 IF NOT EQUAL
          MATCH     PINVCLM,PMPYCLTY
          GOTO      IPYR8000 IF NOT EQUAL
.
          RESET     PMPYGNAM
          MOVE      PMPYGNAM,ANS
          MATCH     SP70,ANS
          IF        @EQUAL
            PACK      DIM8 WITH SP1,PMPYTITL,SP70
          ELSE
            PACK      DIM8 WITH SP1,PMPYTITL,SP1,ANS,DOT
          ENDIF
.
IPYR2200  CMATCH    SP1,DIM8
          GOTO      IPYR3000 IF NOT EQUAL
          BUMP      DIM8
          GOTO      IPYR2200 IF NOT EOS
.
IPYR3000  PACK      PFNAME WITH DIM8,PMPYSNAM
          REP       ", ",PFNAME
          MOVE      PFNAME,PKNAME
          MOVE      PMPYADD1,PKADD1
          MOVE      PMPYADD2,PKADD2
          MOVE      PMPYADD3,PKSUBR
          MOVE      PMPYADD4,PKADD4
          MOVE      PMPYPOST,PKPOST
          MOVE      PMPYRELP,PKRELP
          MOVE      ONE,EXIT
          GOTO      IPYR9999
.
IPYR8000  MOVE      ZERO,EXIT
IPYR9999  RETURN
+
.**************************************************************************
.*                              FDOC0000                                  *
.*             Format doctors name into the variable DOCTNAM              *
.*             Requires the relevant doctor record read in                *
.**************************************************************************
FDOC0000  CLEAR     DOCTNAM
.
          MATCH     SP9,DTITL               * test doctors title for spaces
          GOTO      FDOC2050 IF EQUAL
.
          MOVE      DTITL,DOCTNAM           * move doctors title to DOCTNAM
          ENDSET    DOCTNAM
.
.------ skip over extra blanks at the end of the doctors title ------
.
FDOC1000  CMATCH    SP1,DOCTNAM             * test if current character
          GOTO      FDOC2000 IF NOT EQUAL     is a space
.
          BUMP      DOCTNAM,-1
          GOTO      FDOC1000
.
.------ Append a space and then the doctors initial to DOCTNAM ------
.
FDOC2000  APPEND    SP1,DOCTNAM
.
.------ just append the doctors initial if no title exists ------
.
FDOC2050  MATCH     SP20,DGNAME
          GOTO      FDOC3050 IF EQUAL
.
          MOVE      DGNAME,ANS
          APPEND    ANS,DOCTNAM
          ENDSET    DOCTNAM
          APPEND    DOT,DOCTNAM
.
.------ just append the doctors surname if no given name exists ------
.
FDOC3050  APPEND    DSNAME,DOCTNAM
          ENDSET    DOCTNAM
          APPEND    SP30,DOCTNAM
          RESET     DOCTNAM
.
FDOC9999  RETURN
+
.**********************************************************************
.*                              FDAD0000                              *
.*       Routine to format the doctors address into DOCLINE2          *
.*       taking into account that we want the address right           *
.*       justified                                                    *
.**********************************************************************
FDAD0000  SETLPTR   DOCLINE2
          ENDSET    DOCLINE2
.
          MATCH     SP4,DSPOST              * see if the postcode is blank
          GOTO      FDAD1000 IF EQUAL
          ENDSET    DSPOST
.
.------ match sp1 to current character in DSPOST ------
.------ if not equal then append this character to the current position ------
.------ in DOCLINE2 ------
.
FDAD0100  CMATCH    SP1,DSPOST
          GOTO      FDAD0500 IF EQUAL
          CMOVE     DSPOST,DOCLINE2
.
          BUMP      DOCLINE2,-1
          GOTO      FDAD9000 IF EOS
.
.------ get next character from DSPOST ------
.
FDAD0500  BUMP      DSPOST,-1
          GOTO      FDAD0900 IF EOS
          GOTO      FDAD0100
.
.------ finished processing DSPOST ------
.
FDAD0900  CMOVE     SP1,DOCLINE2
          BUMP      DOCLINE2,-1
          GOTO      FDAD9000 IF EOS
.
.------ process address line 4 ------
.
FDAD1000  MATCH     SP15,DSADD4             * see if address line 4 is blank
          GOTO      FDAD2000 IF EQUAL
          ENDSET    DSADD4
.
.------ remove trailing blanks in DSADD4 first ------
.
FDAD1100  CMATCH    SP1,DSADD4
          GOTO      FDAD1500 IF EQUAL
          CMOVE     DSADD4,DOCLINE2
.
          BUMP      DOCLINE2,-1
          GOTO      FDAD9000 IF EOS
.
          BUMP      DSADD4,-1
          GOTO      FDAD1900 IF EOS
          GOTO      FDAD1600
.
.------ get next character from DSADD4 ------
.
FDAD1500  BUMP      DSADD4,-1
          GOTO      FDAD1900 IF EOS
          GOTO      FDAD1100
.
.------ now process the rest of the address line leaving single blanks ------
.------ between words but removing multiple blanks between words ------
.
FDAD1600  CMATCH    SP1,DSADD4             * test current character for a space
          GOTO      FDAD1650 IF NOT EQUAL
          CMOVE     DSADD4,DOCLINE2        * append the space to DOCLINE2
.
          BUMP      DOCLINE2,-1
          GOTO      FDAD9000 IF EOS
.
          BUMP      DSADD4,-1
          GOTO      FDAD1900 IF EOS
.
.------ skip over multiple blanks between words ------
.
FDAD1630  CMATCH    SP1,DSADD4             * test current character for a space
          GOTO      FDAD1700 IF EQUAL
.
.------ current character is not a space ------
.
FDAD1650  CMOVE     DSADD4,DOCLINE2
          BUMP      DOCLINE2,-1
          GOTO      FDAD9000 IF EOS
.
          BUMP      DSADD4,-1
          GOTO      FDAD1900 IF EOS
          GOTO      FDAD1600
.
.------ get next character from DSADD4 ------
.
FDAD1700  BUMP      DSADD4,-1
          GOTO      FDAD1900 IF EOS
          GOTO      FDAD1630
.
.------ finished processing DSADD4 ------
.
FDAD1900  CMOVE     SP1,DOCLINE2
          BUMP      DOCLINE2,-1
          GOTO      FDAD9000 IF EOS
.
.------ process address line 3 - "Suburb" ------
.
FDAD2000  MATCH     SP15,DSADD3             * see if address line 3 is blank
          GOTO      FDAD9000 IF EQUAL
          ENDSET    DSADD3
.
.------ remove trailing blanks in DSADD3 first ------
.
FDAD2100  CMATCH    SP1,DSADD3
          GOTO      FDAD2500 IF EQUAL
          CMOVE     DSADD3,DOCLINE2
.
          BUMP      DOCLINE2,-1
          GOTO      FDAD9000 IF EOS
.
          BUMP      DSADD3,-1
          GOTO      FDAD9000 IF EOS
          GOTO      FDAD2600
.
.------ get next character from DSADD3 ------
.
FDAD2500  BUMP      DSADD3,-1
          GOTO      FDAD9000 IF EOS
          GOTO      FDAD2100
.
.------ now process the rest of the address line leaving single blanks ------
.------ between words but removing multiple blanks between words ------
.
FDAD2600  CMATCH    SP1,DSADD3             * test current character for a space
          GOTO      FDAD2650 IF NOT EQUAL
          CMOVE     DSADD3,DOCLINE2        * append the space to DOCLINE2
.
          BUMP      DOCLINE2,-1
          GOTO      FDAD9000 IF EOS
.
          BUMP      DSADD3,-1
          GOTO      FDAD9000 IF EOS
.
.------ skip over multiple blanks between words ------
.
FDAD2630  CMATCH    SP1,DSADD3             * test current character for a space
          GOTO      FDAD2700 IF EQUAL
.
.------ current character is not a space ------
.
FDAD2650  CMOVE     DSADD3,DOCLINE2
          BUMP      DOCLINE2,-1
          GOTO      FDAD9000 IF EOS
.
          BUMP      DSADD3,-1
          GOTO      FDAD9000 IF EOS
          GOTO      FDAD2600
.
.------ get next character from DSADD3 ------
.
FDAD2700  BUMP      DSADD3,-1
          GOTO      FDAD9000 IF EOS
          GOTO      FDAD2630
.
.------ finished processing the doctors address ------
.
FDAD9000  RESET     DSPOST
          RESET     DSADD2
          RESET     DSADD3
          RESET     DSADD4
          RESET     DOCLINE2
.
FDAD9999  RETURN
+
.****************************************************************************
.  SINVH000 - Set up invoice header for stationery templating
.****************************************************************************
SINVH000  OPEN      IBATMHA1,"ibatmhaf"
          OPEN      IBATDET1,"ibatmdaf"
.
          MOVE      SP6,KEY6
          LOAD      KEY6,PINVSYS,AECNAEIH,OTCNOTIH,PTCNPAIH,PTCNPAIH
.                                  aae      out      inp      oth
          CALL      RDIBTH1                   * read header detail file
          BRANCH    OVRCD,SINVH900
.
          PACK      KEY12,KEY6,SP6        * position on details file
          CALL      RSIBDET1
          CALL      RKIBDET1
          BRANCH    OVRCD,SINVH900
.
          MATCH     KEY6,IBDTSCOD         * match stationery codes
          GOTO      SINVH950 IF EQUAL
.
.------ stationery code not on file ------
.
SINVH900  IF        WEBFLAG=1
            MOVE      "User Defined Stationery Code not on File. ",WEBTITLE
            CALL      WEBERR00
          ELSE
            DISPLAY   *P1:24,*EL,*B:
                      "User Defined Stationery Code not on File. ";
            CALL      HITENTER
          ENDIF
          MOVE      ONE,EXIT
          GOTO      SINVH999
.
SINVH950  MOVE      ZERO,EXIT
SINVH999  RETURN
+
.****************************************************************************
.  SINVT000 - Set up invoice tail for stationery templating
.****************************************************************************
SINVT000  OPEN      IBATMHA1,"ibatmhaf"
          OPEN      IBATDET1,"ibatmdaf"
.
          MOVE      SP6,KEY6
          LOAD      KEY6,PINVSYS,AECNAEIT,OTCNOTIT,PTCNPAIT,PTCNPAIT
.                                  aae      out      inp      oth
          CALL      RDIBTH1                   * read header detail file
          BRANCH    OVRCD,SINVT800
.
          PACK      KEY12,KEY6,SP6        * position on details file
          CALL      RSIBDET1
          CALL      RKIBDET1
          BRANCH    OVRCD,SINVT800
.
          MATCH     KEY6,IBDTSCOD         * match stationery codes
          GOTO      SINVT950 IF EQUAL
          GOTO      SINVT900
.
SINVT800  COMPARE   "22",PTCNINVL
          GOTO      SINVT950 IF EQUAL     * John Hopkins has not invoice tail
.
.------ stationery code not on file ------
.
SINVT900  IF        WEBFLAG=1  
            MOVE      "User Defined Stationery Code not on File. ",WEBTITLE
            CALL      WEBERR00
          ELSE
            DISPLAY   *P1:24,*EL,*B:
                      "User Defined Stationery Code not on File. ";
            CALL      HITENTER
          ENDIF
          MOVE      ONE,EXIT
          GOTO      SINVT999
.
SINVT950  MOVE      ZERO,EXIT
SINVT999  RETURN
+
.------------------------------------------------------------
.         Set up JH PRFA name
.------------------------------------------------------------
JHPFA000  SQUEEZE   PTITL
          SQUEEZE   PGNAME
.
          MOVE      SP70,PFNAME
          CLEAR     PFNAME
          APPEND    PTITL,PFNAME
          APPEND    SP1,PFNAME
          APPEND    PGNAME,PFNAME
          APPEND    SP1,PFNAME
          APPEND    PTMASNAM,PFNAME
          APPEND    SP70,PFNAME
          RESET     PFNAME
JHPFA999  RETURN
.
.

