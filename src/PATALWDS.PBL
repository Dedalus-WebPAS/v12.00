.
.       PATALWDS : Display Allowance File Entries
.
.       This include is used to display various options available, using
.       whatever file it needs, along with any special parameter supplied
.       by the calling program.
.
.     Modifications: 28/11/89 Graeme Williams
.                    Added the parameter CNEWDS to determine if the include
.                    is to use the new standards for "?" routines
.
.        Subroutine to display the available allowances
.
PATALWDS  MOVE       FIVE,CVERT
          MOVE       ONE,CCOL
          DISPLAY    *P1:3,*EF,*P1:4:
                     *V2LON,*ULON,*P20:4,"ALLOWANCE CODES";
.
          MOVE       SP2,KEY2
          CALL       RDSALLW1
.
.         Loop over file
.
ALLWLP1   CALL       RDKALLW1
          BRANCH     OVRCD OF ENDALLW1
.
          DISPLAY    *PCCOL:CVERT,*V2LON,ALWCODE,SP2,SP2,*HOFF,ALWDESC
          ADD        ONE,CVERT
          COMPARE    TWENTY3,CVERT
          GOTO       ALLWLP1 IF LESS
.
          MOVE       FIVE,CVERT
          ADD        "40",CCOL
          COMPARE    "70",CCOL
          GOTO       ALLWLP1 IF LESS
.
.         Check for new standards question, or old question
.
          BRANCH    CNEWDS,DSALW200
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSE,ANS
          GOTO      ENDALLW2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATALWDS IF EQUAL
.
          GOTO      DSALW400
.
.         Use the new standards question
.
DSALW200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSC,ANS
          GOTO      ENDALLW2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATALWDS IF EQUAL
.
DSALW400  MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
          GOTO      ALLWLP1
.
.      End of allowances
.
ENDALLW1  BRANCH    CNEWDS,ENDALLW2
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
ENDALLW2  RETURN
