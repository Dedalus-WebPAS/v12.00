. *----------------------------------------------------------------------
. * Include Name  :   TRKALTCD.PBL
. *
. * Function      :   Check for MRT tracking alerts
. *                   Check for any current ED or IP visits  or any PRE, BOOK,
. *                   OUT, REQ within x days
. *
. * V10.02.01 08/06/2011  Mike Laming    CAR 240724 
. *           Added "Hospital" to MRT Tables MRTLOCFD,MRTRQHFD,MRTMASFD + Keys
. * V10.00.01 30/08/2010  Ebon Clements  CAR 229027 
. *           Exclude departed OP visits
. * V10.00.00 19/05/2010  Ebon Clements  CAR 59880  
. *           Created include
. *----------------------------------------------------------------------
.------------------------------------------------------------
          DEFPROC   TRKALT00 
.
          INC       MRTLOCFD/INC
          INC       MRTRQHFD/INC
          INC       MRTRQDFD/INC
          INC       MRTMASFD/INC
.
          OPEN      MRTLOCA1,"mrtlocaf"
          OPEN      MRTRQHR1,"mrtrqhaf"
          OPEN      MRTRQDR2,"mrtrqdaf"
          OPEN      MRTMASA1,"mrtmasaf"
.
          PACK      TRAKHOSP,TRAKHOSP,SP70                            *I-240724
          PACK      TRAKLOCN,TRAKLOCN,SP70
          PACK      URNUMBER,URNUMBER,SP70
          PACK      ALRTMESS,SP70,SP70
          PACK      ALRTCLAS,SP70
.
          MATCH     SP70,TRAKLOCN      * Exit if no mrt location
          GOTO      TRKALT90 IF EQUAL
.
          MATCH     SP70,URNUMBER      * Exit if no ur
          GOTO      TRKALT90 IF EQUAL
.
          MATCH     "00",MRCNTDAY      * Max days to search for tracking alerts
          GOTO      TRKALT90 IF EQUAL
.
          MATCH     " 0",MRCNTDAY
          GOTO      TRKALT90 IF EQUAL
.
          MATCH     "  ",MRCNTDAY
          GOTO      TRKALT90 IF EQUAL
.
          PACK      CURRDATE,CCC,CYY,CMM,CDD
          REP       " 0",CURRDATE      * Set Current Date
          MOVE      CURRDATE,WORKDATE
.
          MOVE      ZERO,F2
          SQUEEZE   MRCNTDAY
          MOVE      MRCNTDAY,F2
          RESET     MRCNTDAY
          MOVE      CURRDATE,ENDDDATE      * Calculate search end date
          ADD       F2,ENDDDATE
.
          PACK      KEY7,TRAKHOSP,TRAKLOCN,SP70                       *C-240724
          CALL      RDMRLOC1
          BRANCH    OVRCD,TRKALT90
.
          MATCH     SP70,MRLOTYPE
          GOTO      TRKALT90 IF EQUAL
.
          PACK      KEY5,ANSL,ANST,MRLOTYPE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,TRKALT90
.
          MATCH     "H",TCINDC1             * Health information services
          GOTO      TRKALT90 IF NOT EQUAL
.
          PACK      KEY8,URNUMBER,SP70
          CALL      RDMAST1                * Read Pateint Master File
          BRANCH    OVRCD,TRKALT90
.
.  Current ED
.
          PACK      KEY16,URNUMBER,Z70
          CALL      RSEMVIS3
          CALL      RPEMVIS3
          BRANCH    OVRCD,TRKALT02
.
          MATCH     EMVIURNO,URNUMBER
          GOTO      TRKALT02 IF NOT EQUAL
.
          COMPARE   ONE,EMVISTAT            * Current ED
          GOTO      TRKALT02 IF NOT EQUAL
.
          UNPACK    EMVIDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          CLEAR     ALRTMESS
          APPEND    "EMG - ",ALRTMESS
          APPEND    DISPDATE,ALRTMESS
          APPEND    SP1,ALRTMESS
          APPEND    EMVITIME,ALRTMESS
          RESET     ALRTMESS
.
          MOVE      "GreenText",ALRTCLAS
          GOTO      TRKALT90
.
. Current IP
.
TRKALT02  PACK      KEY17,URNUMBER,TWO,SP20
          CALL      RSPTMI18
          CALL      RKPTMI18
          BRANCH    OVRCD,TRKALT04
.
          MATCH     URNUMBER,AURNO
          GOTO      TRKALT04 IF NOT EQUAL
.
          COMPARE   TWO,ASTAT               * Current IP
          GOTO      TRKALT04 IF NOT EQUAL
.
          MOVE      SP70,TDESC
          PACK      KEY8,AADMNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,TRKALT03
.
          MATCH     SP70,PMVXAPWD
          GOTO      TRKALT03 IF EQUAL
.
          PACK      KEY5,CATAP,PMVXAPWD,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE     PMVXAPWD,TDESC
          ENDIF
.
TRKALT03  UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          CLEAR     ALRTMESS
          APPEND    "IP - ",ALRTMESS
.
          MATCH     SP70,TDESC
          IF        !@EQUAL
            APPEND    TDESC,ALRTMESS
            APPEND    SP1,ALRTMESS
          ENDIF
.
          APPEND    DISPDATE,ALRTMESS
          APPEND    SP1,ALRTMESS
          APPEND    ATIME,ALRTMESS
          RESET     ALRTMESS
.
          MOVE      "GreenText",ALRTCLAS
          GOTO      TRKALT90
.
.  Pre admissions
.
TRKALT04  PACK      KEY17,URNUMBER,ONE,SP20
          CALL      RSPTMI18
CHKMRT05  CALL      RKPTMI18
          BRANCH    OVRCD,TRKALT07
.
          MATCH     URNUMBER,AURNO
          GOTO      TRKALT07 IF NOT EQUAL
.
          COMPARE   ONE,ASTAT               * Pre admisson
          GOTO      TRKALT07 IF NOT EQUAL
.
          MATCH     ADATE,WORKDATE          * Today or within x days
          GOTO      CHKMRT05 IF NOT EQUAL
.
          MOVE      SP70,TDESC
          PACK      KEY8,AADMNO,SP70
          CALL      RDPMVX11
          BRANCH    OVRCD,TRKALT06
.
          MATCH     SP70,PMVXAPWD
          GOTO      TRKALT06 IF EQUAL
.
          PACK      KEY5,CATAP,PMVXAPWD,SP70
          CALL      RDCODE1
          IF        OVRCD=1
            MOVE     PMVXAPWD,TDESC
          ENDIF
.
TRKALT06  UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          CLEAR     ALRTMESS
          APPEND    "IP - ",ALRTMESS
.
          MATCH     SP70,TDESC
          IF        !@EQUAL
            APPEND    TDESC,ALRTMESS
            APPEND    SP1,ALRTMESS
          ENDIF
.
          APPEND    DISPDATE,ALRTMESS
          APPEND    SP1,ALRTMESS
          APPEND    ATIME,ALRTMESS
          RESET     ALRTMESS
.
          MATCH     WORKDATE,CURRDATE
          IF        @EQUAL
            MOVE      "GreenText",ALRTCLAS
          ELSE
            MOVE      "OrangeText",ALRTCLAS
          ENDIF
          GOTO      TRKALT90
.
. Bookings
.
TRKALT07  PACK      KEY16,URNUMBER,Z70
          CALL      RSBKREC6
TRKALT08  CALL      RPBKREC6
          BRANCH    OVRCD,TRKALT09
.
          MATCH     URNUMBER,BKREURNO
          GOTO      TRKALT09 IF NOT EQUAL
.
          COMPARE   ZERO,BKRESTAT           * Booked only
          GOTO      TRKALT08 IF NOT EQUAL
.
          MATCH     WORKDATE,BKREEDAT       * Today or within x days
          GOTO      TRKALT08 IF NOT EQUAL
.
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          CLEAR     ALRTMESS
          APPEND    "BOOK - ",ALRTMESS
          APPEND    BKREWARD,ALRTMESS
.
          MATCH     SP70,BKREEBED
          IF        !@EQUAL
            APPEND     "/",ALRTMESS
            APPEND    BKREEBED,ALRTMESS
          ENDIF
.
          APPEND    SP1,ALRTMESS
          APPEND    DISPDATE,ALRTMESS
          APPEND    SP1,ALRTMESS
          APPEND    BKREATIM,ALRTMESS
          RESET     ALRTMESS
.
          MATCH     WORKDATE,CURRDATE
          IF        @EQUAL
            MOVE      "GreenText",ALRTCLAS
          ELSE
            MOVE      "OrangeText",ALRTCLAS
          ENDIF
          GOTO      TRKALT90
.
. OP bookings or Attendance
.
TRKALT09  PACK      KEY36,URNUMBER,CURRDATE,SP70
          CALL      RDSBOKA3
TRKALT10  CALL      RDKBOKA3             * Reads Visit File
          BRANCH    OVRCD,TRKALT12
.
          MATCH     URNUMBER,OBAURNO     * U/R
          GOTO      TRKALT12 IF NOT EQUAL
.
          MATCH     WORKDATE,OBADATE     * Today or within x days
          GOTO      TRKALT12 IF NOT EQUAL
.
          MATCH     WBSESIT,OBASITE      * Site
          GOTO      TRKALT10 IF NOT EQUAL
.
          IF        OBASTAT<>1 & OBASTAT<>4
            GOTO      TRKALT10                   * Bookings & Attended only
          ENDIF
.
          PACK      KEY8,OBAOUTNO,SP70
          CALL      RDBOKB1
          BRANCH    OVRCD,TRKALT10
.
          IF        OBASTAT=4
            MATCH     SP70,OTBBDPTM              * Skip if departed
            GOTO      TRKALT10 IF NOT EQUAL
          ENDIF
.
          PACK      KEY25,OBASITE,OBACLIN,OBADATE,OBASTRT,SP70
          CALL      RDSESA1
          BRANCH    OVRCD,TRKALT10
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDAY,OSESTRT,OSESSHNO,OSESSDAT,SP70
          CALL      RDOTHED1
          BRANCH    OVRCD,TRKALT10
.
          MATCH     CURRDATE,WORKDATE
          IF        @EQUAL
            CLOCK     TIME,DIM5
            MATCH     DIM5,OTHESEND         * Session ended
            GOTO      TRKALT10 IF LESS
          ENDIF
.
          PACK      KEY12,OBASITE,OBACTYP,SP70
          CALL      RDCTYA1
          BRANCH    OVRCD,TRKALT10
.
          MATCH     SP70,OCTGRP
          GOTO      TRKALT10 IF EQUAL
.
          PACK      KEY5,ANSC,ANSG,OCTGRP,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,TRKALT10
.
          MATCH     "S",TCINDC7                  * Show tracking alerts
          GOTO      TRKALT10 IF NOT EQUAL
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          CLEAR     ALRTMESS
          APPEND    "OP - ",ALRTMESS
          APPEND    OCTDESC,ALRTMESS
          APPEND    SP1,ALRTMESS
          APPEND    DISPDATE,ALRTMESS
          APPEND    SP1,ALRTMESS
          APPEND    OBATIME,ALRTMESS
          RESET     ALRTMESS
.
          MATCH     WORKDATE,CURRDATE
          IF        @EQUAL
            MOVE      "GreenText",ALRTCLAS
          ELSE
            MOVE      "OrangeText",ALRTCLAS
          ENDIF
          GOTO      TRKALT90
.
. Medical record requests
.
TRKALT12  PACK      KEY20,URNUMBER,SP70                               *C-240724
          CALL      RSMRMAS1
TRKALT13  CALL      RKMRMAS1
          BRANCH    OVRCD,TRKALT20
.
          MATCH     URNUMBER,MRMAURNO
          GOTO      TRKALT20 IF NOT EQUAL
.
          PACK      KEY20,MRMAKEY,SP70
          CALL      RSMRRQD2
TRKALT14  CALL      RKMRRQD2
          BRANCH    OVRCD,TRKALT13
.
          MATCH     MRMAKEY,MRRDRKEY
          GOTO      TRKALT13 IF NOT EQUAL
.
          MATCH     ANSN,MRRDFILL                * New
          GOTO      TRKALT14 IF NOT EQUAL
.
          PACK      KEY10,MRRDRQNO
          CALL      RDMRRQH1
          BRANCH    OVRCD,TRKALT14
.
          MATCH     WORKDATE,MRRQDTRQ            * Today or within x days
          GOTO      TRKALT14 IF NOT EQUAL
.
          PACK      KEY7,MRRQHOSC,MRRQLOCR,SP70                       *C-240724
          CALL      RDMRLOC1
          IF         OVRCD=1
            MOVE       MRRQLOCR,MRLODESC
          ENDIF
.
          UNPACK    MRRQDTRQ,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      DISPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          CLEAR     ALRTMESS
          APPEND    "REQ - ",ALRTMESS
          APPEND    MRLODESC,ALRTMESS
          APPEND    SP1,ALRTMESS
          APPEND    DISPDATE,ALRTMESS
          APPEND    SP1,ALRTMESS
          APPEND    MRRQTMRQ,ALRTMESS
          RESET     ALRTMESS
.
          MATCH     WORKDATE,CURRDATE
          IF        @EQUAL
            MOVE      "GreenText",ALRTCLAS
          ELSE
            MOVE      "OrangeText",ALRTCLAS
          ENDIF
          GOTO      TRKALT90
.
TRKALT20  MATCH     WORKDATE,ENDDDATE             * Exit if end date reached
          GOTO      TRKALT90 IF EQUAL
.
          ADD       ONE,WORKDATE
          GOTO      TRKALT04
.
TRKALT90  CLOSE     MRTLOCA1
          CLOSE     MRTRQHR1
          CLOSE     MRTRQDR2
          CLOSE     MRTMASA1
          GOTO      TRKALT99
.
          INC       IBAOVRIO/INC
          INC       MRTLOCIO/INC
          INC       MRTRQHIO/INC
          INC       MRTRQDIO/INC
          INC       MRTMASIO/INC
.
TRKALT99  ENDPROC
