.******************************************************************************
.* System    :   Accounting System                                            *
.* Procedure :   PRCFMSUA                                                     *
.* Desc      :   Total Account Update for a single total account              *
.******************************************************************************
.* Date      :   03.12.90                                                     *
.* Author    :   B.G.Ackland (IBA)                                            *
.*                                                                            *
.*               26.08.91  Neeriem Dye (IBA)                                  *
.*                         Modified to move zero to a var divided by zero     *
.*                                                                            *
.*               19.09.91  Neeriem Dye (IBA)                                  *
.*                         Modified to write to FMSTUPAF instead of performing*
.*                         update    SRF 110636                               *
.*                                                                            *
.*                     21.10.93 Neeriem Dye (I.B.A.)                          *
.*                     Modified for new committed expenditure field           *
.*                     srf HWK                                                *
.*                                                                            *
.******************************************************************************
.$$$$$$
.    1. Open Files Required 
.          OPEN     FMSAMAA1,"fmsamaaf"
.          OPEN     FMSTCFA2,"fmstcfaf"
.          OPEN     FMSTUPA1,"fmstupaf"
.
.    2. Global Variables Used - LEDGER ACCOUNT UTYPE (year or budget to update)
.
.    3. For the LEDGER ACCOUNT Passed to this procedure the program will
.       write to FMSTUPAF all accounts added to
.
.$$$$$$
.
.==============================================================================
.FILE DESCRIPTION INCLUDES
.-------------------------
          DEFPROC   FMSUA000
.
.==============================================================================
.LOCAL VARIABLE DEFINITION
.-------------------------
.**********************************************************************
. Routine to loop through the total calculation tree
.**********************************************************************
TOTL0000  PACK     UTYPE,UTYPE,SP70
          PACK     KEY33,LEDGER,ACCOUNT,SP70
          CALL     RSFMTC2
.
TOTL1000  CALL     RKFMTC2
          BRANCH   OVRCD,TOTL9999             * Exit Routine when the account
          MATCH    LEDGER,FMTCSLED            * Does Not Add to Any More 
          GOTO     TOTL9999 IF NOT EQUAL      * Account codes.
          MATCH    ACCOUNT,FMTCSACC
          GOTO     TOTL9999 IF NOT EQUAL
.
          MOVE     FMTCTLED,TOTLEDG
          MOVE     FMTCTACC,TOTACCNT
.
          CALL     SETU0000      * write FMSTUPAF record
          GOTO     TOTL1000
.
TOTL9999  
          GOTO     FMSUA999
.******************************************************************************
.* SETU - write record for total account update                               *
.******************************************************************************
SETU0000  COMPARE   ONE,FMCOCALC
          GOTO      SETU9999 IF NOT EQUAL
.
          PACK      KEY14,TOTLEDG,TOTACCNT,SP70
          CALL      RDFMAM1            * account exists ?
          BRANCH    OVRCD,SETU9999
.
          PACK      KEY20,PORT,UTYPE,TOTLEDG,TOTACCNT,SP70
          CALL      RDFMTU1            * record exists ?
          COMPARE   ZERO,OVRCD
          GOTO      SETU9999 IF EQUAL
.
          UNPACK    KEY20,FMTUPORT,FMTUSPEC,FMTUTLED,FMTUTACC
          MOVE      FMAMCLEV,FMTULEV
          MOVE      ZERO,FMTUACT
          MOVE      ZERO,FMTUBUD
          MOVE      SP70,FMTUSPAR
          CALL      WRFMTU1            * Flag For Total Account Update
.
SETU9999  RETURN
.******************************************************************************
.* INCLUDES                                                                   *
.******************************************************************************
.
FMSUA999  ENDPROC
