.------------------------------------------------------------
. Include:       WEBDATCD
.
. Modifications:
.     V10.06.02 25/09/2015 Don Nguyen    CAR 321237      
.                          Modified CURVYM00 to not set VIEWTYPE
.                          Added id=lastdate to <select> outputs
.                V10.06.01 B.G.Ackland   CAR 318467
.                          Fix Month Options SELV4000 to work correctly for June
.                          Check for start month less than 1 (replace less than 0)
.                V10.03.04 Ebon Clements CAR 287112
.                          Changed CURPER20 so LSTWKDAY can't be after the end
.                          of the month if VIEWTYPE=4
.                V10.03.02 Saroeun Soeur CAR 258960
.                          VIEWTYPE = 4 to display week view 1 - 7, 8 - 15, 17 - 24, 25 end of month
.                V10.03.01 Saroeun Soeur CAR 246840
.                          VIEWTYPE = 4 to display week view 1 - 8, 15 - 21, 22 - end of month
.                          WEEK0000
.                V9.12.01  Ebon Clements CAR 187136
.                          Added viewtype=3 - Mid week Monday to Friday
.                V9.08.01  Ebon Clements CAR 119815
.                          Changed SELV0000 to display +/- 1 week for day view
.                05/01/2000  Nicole Harrington
.                            Replaced spaces with zeroes when packing KEY4 with 
.                            CCENT and CYEAR. This fixed problem of 0000 being 
.                            left in FSTWKYER.
.------------------------------------------------------------
.------------------------------------------------------------
. Current Period Details
.------------------------------------------------------------
CURPER00  MATCH     SP70,FRSTDATE
          GOTO      CURPER99 IF NOT EQUAL
.
          MATCH     SP70,VYEARMTH
          IF        !@EQUAL
            CALL      CURVYM00           * Use Month / Year 
            GOTO      CURPER99
          ENDIF
.
          MATCH     SP70,VIEWTYPE
          IF        @EQUAL
            MOVE    "1",VIEWTYPE
          ENDIF 
.
          READ      CONTROLF,ZERO;*2,CDD,CMM,CYY,CCC
          MOVE      CDD,CDAY
          MOVE      CMM,CMON
          MOVE      CYY,CYEAR
          MOVE      CCC,CCENT
          CALL      IBACLOCK
          MOVE      CDD,CDAY
          MOVE      CMM,CMON
          MOVE      CYY,CYEAR
          MOVE      CCC,CCENT
          PACK      TODAYDAT,CCENT,CYEAR,CMON,CDAY
          REP       " 0",TODAYDAT
.
          MATCH     SP70,LASTDATE
          IF        !@EQUAL
            UNPACK    LASTDATE,CCENT,CYEAR,CMON,CDAY
          ENDIF
.
          MATCH     "1",VIEWTYPE         * Weekly
          GOTO      CURPER20 IF EQUAL
          MATCH     "2",VIEWTYPE         * Monthly
          GOTO      CURPER40 IF EQUAL
          MATCH     "3",VIEWTYPE         * Weekly Mon - Fri
          GOTO      CURPER20 IF EQUAL
          MATCH     "4",VIEWTYPE
          GOTO      CURPER20 IF EQUAL
.
.
. Single Day Only
.----------------
          PACK      FRSTDATE,CCENT,CYEAR,CMON,CDAY
          PACK      LASTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FRSTDATE
          REP       " 0",LASTDATE
          GOTO      CURPER99
.
. Current Week
.----------------
CURPER20  CALL      DAYOFWEK     * Determine Day of Week 1-7 Mon-Sun
          MOVE      CDAY,FSTWKDAY
          MOVE      CDAY,LSTWKDAY
          ASSIGN    FSTWKDAY-WEEKDAY+1,FSTWKDAY
          ASSIGN    LSTWKDAY+7-WEEKDAY,LSTWKDAY
          MOVE      CMON,FSTWKMON
          MOVE      CMON,LSTWKMON
          PACK      KEY4,CCENT,CYEAR
          REP       " 0",KEY4
          MOVE      KEY4,FSTWKYER
          MOVE      KEY4,LSTWKYER
.
          CALL      MTHEND00
          IF        LSTWKDAY>MTHENDDY
            MATCH     "4",VIEWTYPE
            IF        @EQUAL
              MOVE      MTHENDDY,LSTWKDAY
            ELSE
              ASSIGN    LSTWKDAY-MTHENDDY,LSTWKDAY
              ADD       ONE,LSTWKMON
              IF        LSTWKMON>12
                MOVE      ONE,LSTWKMON
                ADD       ONE,LSTWKYER
              ENDIF
            ENDIF
          ENDIF
.
          IF        FSTWKDAY<1
            SUB       ONE,FSTWKMON
            IF        FSTWKMON<1
              MOVE      "12",FSTWKMON
              SUB       ONE,FSTWKYER
            ENDIF
            MOVE      FSTWKMON,CMON
            CALL      MTHEND00
            ASSIGN    FSTWKDAY+MTHENDDY,FSTWKDAY
          ENDIF
          PACK      FRSTDATE,FSTWKYER,FSTWKMON,FSTWKDAY
          PACK      LASTDATE,LSTWKYER,LSTWKMON,LSTWKDAY
          REP       " 0",FRSTDATE
          REP       " 0",LASTDATE
          GOTO      CURPER99
.
. Current Month
.---------------
CURPER40  PACK      FRSTDATE,CCENT,CYEAR,CMON,ZERO,ONE
          REP       " 0",FRSTDATE
          CALL      MTHEND00
          PACK      LASTDATE,CCENT,CYEAR,CMON,MTHENDDY
          REP       " 0",LASTDATE
.
CURPER99  RETURN
.------------------------------------------------------------
. Calculate the End of Month  Input CMON  Output MTHENDDY
.------------------------------------------------------------
MTHEND00  MOVE      CMON,F2
          LOAD      KEY2,F2,D31,D28,D31,D30,D31,D30,D31,D31,D30,D31,D30,D31
          MOVE      KEY2,MTHENDDY
          IF        F2=2
            PACK      KEY4,CCENT,CYEAR
            REP       " 0",KEY4
            MOVE      KEY4,F4
            IF        F4%4=0
              MOVE      D29,MTHENDDY
            ENDIF
          ENDIF
          RETURN
.------------------------------------------------------------
. Calculate Dates for Next and Last Period
.------------------------------------------------------------
CALCDT00  MATCH     "1",VIEWTYPE             * Week
          GOTO      CALCDT20 IF EQUAL
          MATCH     "2",VIEWTYPE             * Month
          GOTO      CALCDT40 IF EQUAL
          MATCH     "3",VIEWTYPE             * Week Mon-Fri
          GOTO      CALCDT20 IF EQUAL
          MATCH     "4",VIEWTYPE             * Week Mon-Fri
          GOTO      CALCDT20 IF EQUAL
.
          UNPACK    FRSTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      "-1",ADJDAYS
          CALL      ADJDAT00
          PACK      PRELDATE,CCENT,CYEAR,CMON,CDAY
          PACK      PREFDATE,CCENT,CYEAR,CMON,CDAY
.
          UNPACK    LASTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      "1",ADJDAYS
          CALL      ADJDAT00
          PACK      NXTFDATE,CCENT,CYEAR,CMON,CDAY
          PACK      NXTLDATE,CCENT,CYEAR,CMON,CDAY
.
          GOTO      CALCDT99
.
CALCDT20  UNPACK    FRSTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      "-1",ADJDAYS
          CALL      ADJDAT00
          PACK      PRELDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      "-6",ADJDAYS
          CALL      ADJDAT00
          PACK      PREFDATE,CCENT,CYEAR,CMON,CDAY
.
          UNPACK    LASTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      "1",ADJDAYS
          CALL      ADJDAT00
          PACK      NXTFDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      "6",ADJDAYS
          CALL      ADJDAT00
          PACK      NXTLDATE,CCENT,CYEAR,CMON,CDAY
.
          MATCH     "3",VIEWTYPE                 * This week Mon - Fri
          IF        @EQUAL
            UNPACK    LASTDATE,CCENT,CYEAR,CMON,CDAY  * Remove Sat and Sun
            MOVE      "-2",ADJDAYS
            CALL      ADJDAT00
            PACK      LASTDATE,CCENT,CYEAR,CMON,CDAY
          ENDIF
.
          GOTO      CALCDT99
.
CALCDT40  UNPACK    FRSTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          ADD       ONE,F2
          PACK      KEY4,CCENT,CYEAR
          REP       " 0",KEY4
          IF        F2>12
            MOVE      "01",F2
            MOVE      KEY4,F4
            ADD       ONE,F4
            MOVE      F4,KEY4
          ENDIF
          PACK      NXTFDATE,KEY4,F2,ZERO,ONE
          REP       " 0",NXTFDATE
          MOVE      F2,CMON
          CALL      MTHEND00
          PACK      NXTLDATE,KEY4,CMON,MTHENDDY
          REP       " 0",NXTLDATE
.
          UNPACK    FRSTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          SUB       ONE,F2
          PACK      KEY4,CCENT,CYEAR
          REP       " 0",KEY4
          IF        F2<1
            MOVE      "12",F2
            MOVE      KEY4,F4
            SUB       ONE,F4
            MOVE      F4,KEY4
          ENDIF
          PACK      PREFDATE,KEY4,F2,ZERO,ONE
          REP       " 0",PREFDATE
.
          MOVE      F2,CMON
          CALL      MTHEND00
          PACK      PRELDATE,KEY4,CMON,MTHENDDY
          REP       " 0",PRELDATE
.
CALCDT99  RETURN
.----------------------------------------------------------------------
. Adjust Date in CDAY,CMON,CCENT,CYEAR by ADJDAYS number of Days
.    ADJDAYS must be < Number of Days in Month
.----------------------------------------------------------------------
ADJDAT00  PACK      KEY4,CCENT,CYEAR
          REP       " 0",KEY4
          MOVE      KEY4,CURYER
          MOVE      CMON,CURMON
          MOVE      CDAY,CURDAY
          MOVE      CURDAY,NUMBDAYS
          ADD       ADJDAYS,NUMBDAYS
.
ADJDAT10  IF        NUMBDAYS<1
            SUB       ONE,CURMON
            IF        CURMON=0
              ASSIGN    31+NUMBDAYS,NUMBDAYS
              MOVE      "12",CURMON
              SUB       ONE,CURYER
              PACK      KEY8,CURYER,CURMON,CURDAY
              REP       " 0",KEY8
              UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
              GOTO      ADJDAT10
            ENDIF
            MOVE      CURMON,CMON
            CALL      MTHEND00
            ASSIGN    MTHENDDY+NUMBDAYS,NUMBDAYS
            PACK      KEY8,CURYER,CURMON,CURDAY
            REP       " 0",KEY8
            UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
            GOTO      ADJDAT10
          ENDIF
.
ADJDAT20  CALL      MTHEND00
          IF        NUMBDAYS>MTHENDDY
            ADD       ONE,CURMON
            IF        CURMON=13
              ASSIGN    NUMBDAYS-31,NUMBDAYS
              MOVE      "1",CURMON
              ADD       ONE,CURYER
              PACK      KEY8,CURYER,CURMON,CURDAY
              REP       " 0",KEY8
              UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
              GOTO      ADJDAT20
            ENDIF
            ASSIGN    NUMBDAYS-MTHENDDY,NUMBDAYS
            PACK      KEY8,CURYER,CURMON,CURDAY
            REP       " 0",KEY8
            UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
            GOTO      ADJDAT20
          ENDIF
.
ADJDAT90  MOVE      NUMBDAYS,CURDAY
          PACK      KEY8,CURYER,CURMON,CURDAY
          REP       " 0",KEY8
          UNPACK    KEY8,CCENT,CYEAR,CMON,CDAY
.
          RETURN
.-----------------------------------------------------------
.Calculate Current VYEARMTH
.-----------------------------------------------------------
CURVYM00  MOVE      "01",CDAY
          PACK      FRSTDATE,VYEARMTH,CDAY
          UNPACK    FRSTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      MTHEND00
          PACK      LASTDATE,VYEARMTH,MTHENDDY
.         MOVE      "2",VIEWTYPE  
.
          RETURN
.------------------------------------------------------------
. Selects Day, Week or Month from Viewtype
.------------------------------------------------------------
SELV0000  MATCH     "1",VIEWTYPE            * Week
          GOTO      SELV2000 IF EQUAL
          MATCH     "2",VIEWTYPE            * Month
          GOTO      SELV4000 IF EQUAL
          MATCH     "3",VIEWTYPE            * Weekly Mon - Fri
          GOTO      SELV2000 IF EQUAL
          MATCH     "4",VIEWTYPE
          IF        @EQUAL
            CALL      WEEK0000
            GOTO      SELV9999
          ENDIF
.
.Defaults to Daily Viewtype
.--------------------------
.
          WRITE     HTMLFILE;"<select name=lastdate id=lastdate":
                             " class=DaySelect onchange='this.form.submit()'>";
          UNPACK    FRSTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      "8",ADJDAYS
          CALL      ADJDAT00
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
.
          MOVE      "-7",ADJDAYS
          UNPACK    FRSTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      ADJDAT00
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
.
SELV1000  UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      DAYOFWEK
          LOAD      DAYNAM3,WEEKDAY,MON,TUE,WED,THU,FRI,SAT,SUN
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          MATCH     STRTDATE,FRSTDATE
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",STRTDATE," selected>":
                               DAYNAM3,SP1,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR:
                               "</option>";
          ELSE
            WRITE     HTMLFILE;"<option value=",STRTDATE,">":
                               DAYNAM3,SP1,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR:
                               "</option>";
          ENDIF
          MOVE      "1",ADJDAYS
          CALL      ADJDAT00
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          MATCH     ENDDATE,STRTDATE
          GOTO      SELV1000 IF NOT EQUAL
          GOTO      SELV9000
.
.Select Weekly Viewtype
.--------------------------
SELV2000  WRITE     HTMLFILE;"<select name=lastdate id=lastdate":
                             " class=WeekSelect onchange='this.form.submit()'>";
          UNPACK    FRSTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      "42",ADJDAYS
          CALL      ADJDAT00
          PACK      ENDDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDATE
.
          UNPACK    FRSTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      "-42",ADJDAYS
          CALL      ADJDAT00
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
          REP       " 0",FRSTDATE
.
SELV3000  UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          MATCH     STRTDATE,FRSTDATE
          IF        @EQUAL
            MATCH     "3",VIEWTYPE
            IF        @EQUAL
              WRITE     HTMLFILE;"<option value=",STRTDATE," selected>":
                                 "Mid Week of ":
                                 CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</option>";
            ELSE
              WRITE     HTMLFILE;"<option value=",STRTDATE," selected>Week of ":
                                 CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</option>";
            ENDIF
          ELSE
            MATCH     "3",VIEWTYPE
            IF        @EQUAL
              WRITE     HTMLFILE;"<option value=",STRTDATE,">Mid Week of ":
                                 CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</option>";
            ELSE
              WRITE     HTMLFILE;"<option value=",STRTDATE,">Week of ":
                                 CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</option>";
            ENDIF
          ENDIF
          MOVE      "7",ADJDAYS
          CALL      ADJDAT00
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
          MATCH     ENDDATE,STRTDATE
          GOTO      SELV3000 IF NOT EQUAL
          GOTO      SELV9000
.
.
.Select Monthly Viewtype
.------------------------
.
SELV4000  WRITE     HTMLFILE;"<select name=vyearmth id=vyearmth":
                            " class=MonthSelect onchange='this.form.submit()'>";
          UNPACK    FRSTDATE,KEY4,CMON,CDAY
          MOVE      KEY4,F4
          MOVE      CMON,F2
          MOVE      CMON,ENDMTH
          ADD       SIX,ENDMTH
          IF        ENDMTH>12
            SUB       "12",ENDMTH
          ENDIF
          SUB       SIX,F2
          IF        F2<1
            SUB       ONE,F4
            ADD       "12",F2
          ENDIF
.
SELV5000  LOAD      MTHNAM,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY6,F4,F2
          REP       " 0",KEY6
          MATCH     KEY6,FRSTDATE
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY6," selected>":
                               MTHNAM,SP1,F4,"</option>";
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY6,">":
                               MTHNAM,SP1,F4,"</option>";
          ENDIF
          ADD       ONE,F2
          IF        F2>12
            ADD      ONE,F4
            SUB      "12",F2
          ENDIF
          COMPARE   ENDMTH,F2
          GOTO      SELV5000 IF NOT EQUAL
.
SELV9000  WRITE     HTMLFILE;"</select>"
SELV9999  RETURN
.------------------------------------------------------------
. Next Day, Week, Month
.------------------------------------------------------------
NXTPER00  MOVE      VIEWTYPE,F1
          BRANCH    F1,NXTPER10,NXTPER20,NXTPER30
          WRITE     HTMLFILE;"SelectNextDay();";
          GOTO      NXTPER99
NXTPER10  WRITE     HTMLFILE;"SelectNextWeek();";
          GOTO      NXTPER99
NXTPER20  WRITE     HTMLFILE;"SelectNextMonth();";
          GOTO      NXTPER99
NXTPER30  WRITE     HTMLFILE;"SelectNextWeek5();";
          GOTO      NXTPER99
NXTPER99  RETURN
.------------------------------------------------------------
. Last Day, Week, Month
.------------------------------------------------------------
PREPER00  MOVE      VIEWTYPE,F1
          BRANCH    F1,PREPER10,PREPER20,PREPER30
          WRITE     HTMLFILE;"SelectLastDay();";
          GOTO      PREPER99
PREPER10  WRITE     HTMLFILE;"SelectLastWeek();";
          GOTO      PREPER99
PREPER20  WRITE     HTMLFILE;"SelectLastMonth();";
          GOTO      PREPER99
PREPER30  WRITE     HTMLFILE;"SelectLastWeek5();";
          GOTO      PREPER99
PREPER99  RETURN
.-----------------------------------------------------------
. Current Start Date
.-----------------------------------------------------------
CURSTD00  UNPACK    FRSTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      DAYOFWEK
          LOAD      DAYNAM,WEEKDAY,MON,TUE,WED,THU,FRI,SAT,SUN
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY," ",MTHNAM3," ",CCENT,CYEAR;
.
CURSTD99  RETURN
.------------------------------------------------------------
. Current End Date
.------------------------------------------------------------
CUREND00  UNPACK    LASTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      DAYOFWEK
          LOAD      DAYNAM,WEEKDAY,MON,TUE,WED,THU,FRI,SAT,SUN
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY," ",MTHNAM3," ",CCENT,CYEAR;
.
CUREND99  RETURN
.------------------------------------------------------------
. Current Year
.------------------------------------------------------------
CURYR000  UNPACK    FRSTDATE,CCENT,CYEAR,CMON,CDAY
          WRITE     HTMLFILE;CCENT,CYEAR;
.
CURYR999  RETURN
.------------------------------------------------------------
. Current Month Name
.------------------------------------------------------------
CURMON00  UNPACK    FRSTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;MTHNAM3;
.
CURMON99  RETURN
.
.------------------------------------------------------------
. WEEK0000 - VIEWTYPE = 4 display one months worth
.            of weeks
.------------------------------------------------------------
WEEK0000  MOVE      "25",ENDMON2
          UNPACK    LASTDATE,CCENT,CYEAR,CMON,CDAY
.
          MATCH     "08",CDAY
          IF        @LESS
            MOVE      "01",CDAY
            GOTO      WEEK1000
          ENDIF
.
          MATCH     "16",CDAY
          IF        @LESS
            MOVE      "08",CDAY
            GOTO      WEEK1200
          ENDIF
.
          MATCH     "24",CDAY
          IF        @LESS
            MOVE      "16",CDAY
            GOTO      WEEK1200
          ENDIF
.
          MATCH     "24",CDAY
          IF        !@LESS
            MOVE      "24",CDAY
            GOTO      WEEK1200
          ENDIF
.
WEEK1000  MOVE      CDAY,FORM2     
          ASSIGN    (FORM2/7),FORM2
          GOTO      WEEK1300
.
WEEK1200  MOVE      CDAY,FORM2
          ASSIGN    (FORM2/8),FORM2
.
WEEK1300  BRANCH    FORM2,WEEK2010,WEEK2020,WEEK2030,WEEK2030
          MOVE      "01",CDAY
          GOTO      WEEK3000
.
WEEK2010  MOVE      "08",CDAY
          GOTO      WEEK3000
.
WEEK2020  MOVE      "16",CDAY
          GOTO      WEEK3000
.
WEEK2030  MOVE      "24",CDAY
          GOTO      WEEK3000
.
WEEK3000  PACK      PREFDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      "01",DIM2
          PACK      STRTDATE,CCENT,CYEAR,CMON,DIM2
          PACK      ENDDATE,CCENT,CYEAR,CMON,ENDMON2
          REP       " 0",ENDDATE
          REP       " 0",STRTDATE
          REP       " 0",PREFDATE
          WRITE     HTMLFILE;"<select name=lastdate id=lastdate":
                              " class=WeekSelect onchange='this.form.submit()'>"
WEEK4000  MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          MATCH     PREFDATE,STRTDATE
          IF        @EQUAL
           WRITE     HTMLFILE;"<option value=",STRTDATE," selected>Week of ":
                      CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</option>";
          ELSE
            WRITE     HTMLFILE;"<option value=",STRTDATE,">Week of ":
                      CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,"</option>";
          ENDIF
          MOVE      CDAY,FORM2
.
          COMPARE   "01",FORM2
          IF        @EQUAL
            ADD       "7",FORM2
          ELSE
            ADD       "8",FORM2
          ENDIF
.
          MOVE      FORM2,CDAY
          PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDATE
          MATCH     ENDDATE,STRTDATE
          GOTO      WEEK4000 IF LESS
          GOTO      WEEK4000 IF EQUAL
.
          WRITE     HTMLFILE;"</select>"
.
WEEK9999  RETURN
+
