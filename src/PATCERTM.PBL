.--------------------------------------------------------------------------
. Include Name  :   PATCERTM.PBL        -  Patient Certificates
.
. Function      :   CGI variables for PATCERTM - patceraf.dat
. 
. Includes Needed :
.
.          Inputs  -  Valid record from the patceraf
.          Outputs -  Field specified by FLDITMNO & DIM127 for any parameters
.
. Modifications : 
.         V11.03.01 13/09/2023  Bella Turco    TSK 0936614
.                   Commented out "U" TCINDC1 check in LSTCR000 which caused
.                   functionality issues for 'Current Visit Only' cert checkbox
.         V10.03.01 25/09/2013  Patrick Adair  CAR 284545
.                   Added LSTCR000 - Display Certificate List Table Sort
.                   Added showcurr to allow display of certificates by Admissno
.                   Added ADMCH000 - Check certificate is for the current Admission
.         V10.02.01 14/06/2011  Jill Parkinson CAR 240715
.                   Added PTCECSTA - Certificate Status
.         V9.06.01  08/05/2006 Peter Vela    CAR 104252
.                   Removed redefine field PTCEURNO
.                   Renamed DPTCEURN to PTCEURNO
.         V9.05.01  14/03/2006 Mike Laming   CAR 79281
.                   Change LSTCER00 to add Admission No. to list item
.         V9.05.B01 14/12/2005 Mike Laming   CAR 86040
.                   Change LSTCER00 to handle by UR or by Admission Tags
.         V9.03.00  15/11/2005 Mike Laming   CAR 86040
.                   Create module
.
.------------------------------------------------------------------------
.
.------------------------------------------------------------------------
.   Clear Parameters for Patient Certificates Table
.------------------------------------------------------------------------
CLPTCE00  MOVE      Z70,PTCER001
          MOVE      Z70,PTCER002
          MOVE      Z70,PTCER003
          MOVE      Z70,PTCER004
          MOVE      Z70,PTCER005
          MOVE      Z70,PTCER006
          MOVE      Z70,PTCER007
          MOVE      Z70,PTCER008
.
CLPTCE09  RETURN
.
.------------------------------------------------------------------------
.   Clear Parameters for Patient Certificates Table
.------------------------------------------------------------------------
CLRPTCE0  UNPACK    SP70,PTCEURNO,PTCETYPE,PTCEFDAT,PTCETDAT,PTCESDAT
          UNPACK    SP70,PTCEDCOD,PTCESPAR
          MOVE      ZERO,PTCESENT
          UNPACK    SP70,PTCECSTA
.
CLRPTCE9  RETURN
.
.--------------------------------------------------------------------------
. Display Fields
.--------------------------------------------------------------------------
DPTCE000  BRANCH    FLDITMNO,DPTCE100,DPTCE200,DPTCE300,DPTCE400,DPTCE500:
                             DPTCE600,DPTCE700,DPTCE800,DPTCE900,DPTCEA00:
                             DPTCEB00
.
          WRITE     HTMLFILE;"Invalid IBA Tag (PATCERTM)";
          GOTO      DPTCE999
.
.
DPTCE100  CALL      LSTCER00                * List Certificates
          GOTO      DPTCE999
.
DPTCE200  MOVE      CATcr,CATEGORY          * Certificate Type (Cat."cr")
          MOVE      PTCETYPE,CODE
          CALL      CODITM00
          GOTO      DPTCE999
.
DPTCE300  MATCH     SP70,PTCEFDAT          * Date From
          GOTO      DPTCE999 IF EQUAL
          UNPACK    PTCEFDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      DPTCE999
.
DPTCE400  MATCH     SP70,PTCETDAT           * Date To
          GOTO      DPTCE999 IF EQUAL
          UNPACK    PTCETDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      DPTCE999
.
DPTCE500  MATCH     SP70,PTCESDAT           * Date signed/issued
          GOTO      DPTCE999 IF EQUAL
          UNPACK    PTCESDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      DPTCE999
.
.
DPTCE600  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,DPTCE605
          PACK      KEY10,PTCEDCOD
          CALL      RDPMHCP1
          BRANCH    OVRCD,DPTCE999
          MOVE      PMHCTITL,FMTTITLE
          MOVE      PMHCGNAM,FMTGNAME
          MOVE      PMHCSNAM,FMTSNAME
          CALL      DOCNM000
          WRITE     HTMLFILE;DOCFNAME;
          GOTO      DPTCE999
.
DPTCE605  WRITE     HTMLFILE;PTCEDCOD;
          GOTO      DPTCE999
.
DPTCE700  WRITE     HTMLFILE;PTCESENT;
          GOTO      DPTCE999
.
DPTCE800  CALL      DPTCL000                * read Comments records (patclcaf)
          GOTO      DPTCE999
.
DPTCE900  PACK      CATEGORY,ANSF,ANSG       * Certificate Status (Cat."FD")
          MOVE      PTCECSTA,CODE
          CALL      CODITM00
          GOTO      DPTCE999
.
DPTCEA00  CALL      LSTCR000                * List Certificates - Table Sort
          GOTO      DPTCE999
.
DPTCEB00  WRITE     HTMLFILE;SHOWCURR;
          GOTO      DPTCE999
.
DPTCE999  RETURN
.
.------------------------------------------------------------
. Dispay Comments lines  
.------------------------------------------------------------
DPTCL000  PACK      KEY22,PTCEURNO,PTCETYPE,PTCEFDAT,SP20
          MOVE      KEY22,SAVKEY19
          CALL      RSPTCLC1
.
DPTCL100  CALL      RKPTCLC1
          BRANCH    OVRCD,DPTCL999
.
          PACK      KEY19,PTCLURNO,PTCLTYPE,PTCLFDAT,SP20
          MATCH     SAVKEY19,KEY19
          GOTO      DPTCL999 IF NOT EQUAL
.
          WRITE     HTMLFILE;PTCLTEXT
          GOTO      DPTCL100
.
DPTCL999  RETURN
.
.------------------------------------------------------------
. List Certificates      
.------------------------------------------------------------
LSTCER00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
          UNPACK    DIM127,D1,DIM1,DIM127         * "select by Admission" flag
          MOVE      SP1,ANS
.
          PACK      KEY19,URNUMBER,SP20
          CALL      RSPTCER1
LSTCER10  CALL      RKPTCER1
          BRANCH    OVRCD,LSTCER99
.
          MATCH     URNUMBER,PTCEURNO
          GOTO      LSTCER99 IF NOT EQUAL
.
          MOVE      SP70,CTYPDESC
          MOVE      SP70,CSTADESC
          MATCH     "1",DIM1
          GOTO      LSTCER20 IF NOT EQUAL
          CALL      ADMCHK00                * check if for current Admission
          BRANCH    EXIT,LSTCER10
          GOTO      LSTCER30
.
LSTCER20  PACK      KEY5,CATcr,PTCETYPE,SP70
          CALL      RDCODE1
          IF        OVRCD<>1
            PACK      CTYPDESC,TDESC
          ENDIF
.
          MATCH     SP70,PTCECSTA
          IF        !@EQUAL
            PACK      KEY5,ANSF,ANSG,PTCECSTA,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      CSTADESC,TDESC
            ENDIF
          ENDIF
.
LSTCER30  UNPACK    PTCEFDAT,CCENT,CYEAR,CMON,CDAY
          PACK      DATEFR,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",DATEFR
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY11,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          UNPACK    PTCETDAT,CCENT,CYEAR,CMON,CDAY
          PACK      DATETO,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",DATETO
.
          UNPACK    PTCESDAT,CCENT,CYEAR,CMON,CDAY
          PACK      DATESN,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",DATESN
.
          PACK      DOCFNAME,SP70
          MATCH     SP6,PTCEDCOD
          GOTO      LSTCER60 IF EQUAL
.
          MOVE      "Unknown Doctor",DOCFNAME
          PACK      KEY6,PTCEDCOD,SP20
          CALL      RDDOCT1
          BRANCH    OVRCD,LSTCER50
.
          MOVE      DTITL,FMTTITLE
          MOVE      DGNAME,FMTGNAME
          MOVE      DSNAME,FMTSNAME
          CALL      DOCNM000                * output is DOCFNAME
          GOTO      LSTCER60
.
LSTCER50  PACK      KEY10,PTCEDCOD,SP20
          CALL      RDPMHCP1
          BRANCH    OVRCD,LSTCER60
          MOVE      PMHCTITL,FMTTITLE
          MOVE      PMHCGNAM,FMTGNAME
          MOVE      PMHCSNAM,FMTSNAME
          CALL      DOCNM000                * output is DOCFNAME
.
LSTCER60  MOVE      SP4,DIM4
          MATCH     "1",PTCESENT
          IF        @EQUAL
            MOVE      "Yes",DIM4
          ENDIF
.
LSTCER70  PACK      KEY50,SP70              * any comments ?
          MOVE      "  1",DIM3
          PACK      KEY22,PTCEURNO,PTCETYPE,PTCEFDAT,DIM3
          CALL      RDPTCLC1
          BRANCH    OVRCD,LSTCER80
          MOVE      PTCLTEXT,KEY50
.
LSTCER80  MOVE      ZERO,ANS
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:":
                             "UpdateCertificate":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&urnumber=",URNUMBER:
                             "&ptcer002=",PTCETYPE:
                             "&ptcer003=",KEY11:
                             "&admissno=",ADMISSNO,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             CTYPDESC,"</td>":
                             "<td>",DATEFR,"</td>":
                             "<td>",DATETO,"</td>":
                             "<td>",DATESN,"</td>":
                             "<td>",DOCFNAME,"</td>":
                             "<td>",DIM4,"</td>":
                             "<td>",CSTADESC,"</td>":
                             "<td>",KEY50,"</td>":
                             "</tr>";
          GOTO      LSTCER10
.
LSTCER99  RETURN
+
.--------------------------------------------------------------------------
. Check if "patceraf" record is for the current Admission
.--------------------------------------------------------------------------
ADMCHK00  MOVE      ZERO,EXIT
          MATCH     "Y",ANS
          GOTO      ADMCHK10 IF EQUAL       * only do the next but once
          MOVE      "Y",ANS
.                                           * Get Admission Date & Disch. Date
          MATCH     ADMISSNO,DAADMNO
          IF        !@EQUAL
            PACK      KEY8,ADMISSNO,SP10
            CALL      RDMISS1
          ENDIF
.
          MOVE      "99991231",DIM8
          COMPARE   THREE,ASTAT
          GOTO      ADMCHK10 IF NOT EQUAL   * not discharged
.
          MATCH     ADMISSNO,DDADMNO
          IF        !@EQUAL
            PACK      DDATE,SP20
            PACK      KEY8,ADMISSNO,SP10
            CALL      RDDSCH1
          ENDIF
.
          MATCH     SP8,DDATE
          GOTO      ADMCHK10 IF EQUAL
          MOVE      DDATE,DIM8
.
.
.                                           * check From & To dates for extremes
ADMCHK10  MATCH     ADATE,PTCETDAT
          GOTO      ADMCHK90 IF LESS        * bypass this one
          MATCH     PTCEFDAT,DIM8
          GOTO      ADMCHK90 IF LESS        * bypass this one
.
.                                           * now check Certificate type
          MOVE      SP1,TCINDC1
          PACK      KEY5,CATcr,PTCETYPE,SP10
          CALL      RDCODE1
.
          MATCH     "U",TCINDC1
          GOTO      ADMCHK99 IF EQUAL       * Accept any UR based Certificates
.
.                                           * now check if dates in Admiss range
          MATCH     ADATE,PTCEFDAT
          GOTO      ADMCHK90 IF LESS       * bypass this one
          MATCH     PTCETDAT,DIM8
          GOTO      ADMCHK90 IF LESS       * bypass this one
          GOTO      ADMCHK99
.
ADMCHK90  MOVE      ONE,EXIT                * set EXIT to bypass the Certificate
.
ADMCHK99  RETURN
+
.--------------------------------------------------------------------------
. Move Field
.--------------------------------------------------------------------------
UPTCE000  MATCH     PTCER001,Z70
          IF        !@EQUAL
            MOVE      PTCER001,PTCEURNO
          ENDIF
.
          MATCH     PTCER002,Z70
          IF        !@EQUAL
            MOVE      PTCER002,PTCETYPE
          ENDIF
.
          MATCH     PTCER003,Z70
          IF        !@EQUAL
            MOVE      PTCER003,PTCEFDAT
          ENDIF
.
          MATCH     PTCER004,Z70
          IF        !@EQUAL
            MOVE      PTCER004,PTCETDAT
          ENDIF
.
          MATCH     PTCER005,Z70
          IF        !@EQUAL
            MOVE      PTCER005,PTCESDAT
          ENDIF
.
          MATCH     PTCER006,Z70
          IF        !@EQUAL
            MOVE      PTCER006,PTCEDCOD
          ENDIF
.
          MATCH     PTCER007,Z70
          IF        !@EQUAL
            MOVE      PTCER007,PTCESENT
          ENDIF
.
          MATCH     PTCER008,Z70
          IF        !@EQUAL
            MOVE      PTCER008,PTCECSTA
          ENDIF
.
UPTCE999  RETURN
.
.------------------------------------------------------------
.   Set Parameters for Patient Certificates Table
.------------------------------------------------------------
SPPTCE00  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPPTCE01,SPPTCE02,SPPTCE03,SPPTCE04,SPPTCE05:
                       SPPTCE06,SPPTCE07,SPPTCE08
.
          MOVE      ONE,EXIT
          GOTO      SPPTCE99
.
SPPTCE01  MOVE      QRYDATA,PTCER001
          GOTO      SPPTCE99
.
SPPTCE02  MOVE      QRYDATA,PTCER002
          GOTO      SPPTCE99
.
SPPTCE03  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      PTCER003,CCENT,CYEAR,CMON,CDAY
          GOTO      SPPTCE99
.
SPPTCE04  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      PTCER004,CCENT,CYEAR,CMON,CDAY
          GOTO      SPPTCE99
.
SPPTCE05  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      PTCER005,CCENT,CYEAR,CMON,CDAY
          GOTO      SPPTCE99
.
SPPTCE06  MOVE      QRYDATA,PTCER006
          GOTO      SPPTCE99
.
SPPTCE07  MOVE      QRYDATA,PTCER007
          GOTO      SPPTCE99
.
SPPTCE08  MOVE      QRYDATA,PTCER008
          GOTO      SPPTCE99
.
SPPTCE99  RETURN
.
.-------------------------------------------------------------------------------
. List Certificates - New for Table Sort
.   Clone of LSTCER00 rehashed so as not to break existing functionality
.-------------------------------------------------------------------------------
LSTCR000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          MOVE      SP1,ANS
.
          PACK      KEY19,URNUMBER,SP20
          CALL      RSPTCER1
LSTCR100  CALL      RKPTCER1
          BRANCH    OVRCD,LSTCR999
.
          MATCH     URNUMBER,PTCEURNO
          GOTO      LSTCR999 IF NOT EQUAL
.
          MOVE      SP70,CTYPDESC
          MOVE      SP70,CSTADESC
.
          MOVE      SP1,TCINDC1             * Get Certificate Cat Code
          PACK      KEY5,CATcr,PTCETYPE,SP10
          CALL      RDCODE1
          IF        OVRCD<>1
            PACK      CTYPDESC,TDESC
          ENDIF
.
.          MATCH     "U",TCINDC1             * Accept any UR based Certificates
.          GOTO      LSTCR200 IF EQUAL
.
          MATCH     "1",SHOWCURR            * only show current Admiss certificates
          GOTO      LSTCR200 IF NOT EQUAL
.
          CALL      ADMCH000                * check if cert is for current Admiss
          BRANCH    EXIT,LSTCR100
.
LSTCR200  MATCH     SP70,PTCECSTA
          IF        !@EQUAL
            PACK      KEY5,ANSF,ANSG,PTCECSTA,SP70
            CALL      RDCODE1
            IF        OVRCD<>1
              PACK      CSTADESC,TDESC
            ENDIF
          ENDIF
.
LSTCR300  UNPACK    PTCEFDAT,CCENT,CYEAR,CMON,CDAY
          PACK      DATEFR,CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP       " 0",DATEFR
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          PACK      KEY11,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
.
          PACK      DOCFNAME,SP70
          MATCH     SP6,PTCEDCOD
          GOTO      LSTCR600 IF EQUAL
.
          MOVE      "Unknown Doctor",DOCFNAME
          PACK      KEY6,PTCEDCOD,SP20
          CALL      RDDOCT1
          BRANCH    OVRCD,LSTCR500
.
          MOVE      DTITL,FMTTITLE
          MOVE      DGNAME,FMTGNAME
          MOVE      DSNAME,FMTSNAME
          CALL      DOCNM000                * output is DOCFNAME
          GOTO      LSTCR600
.
LSTCR500  PACK      KEY10,PTCEDCOD,SP20
          CALL      RDPMHCP1
          BRANCH    OVRCD,LSTCR600
          MOVE      PMHCTITL,FMTTITLE
          MOVE      PMHCGNAM,FMTGNAME
          MOVE      PMHCSNAM,FMTSNAME
          CALL      DOCNM000                * output is DOCFNAME
.
LSTCR600  MOVE      SP4,DIM4
          MATCH     "1",PTCESENT
          IF        @EQUAL
            MOVE      "Yes",DIM4
          ENDIF
.
LSTCR700  PACK      KEY50,SP70              * any comments ?
          MOVE      "  1",DIM3
          PACK      KEY22,PTCEURNO,PTCETYPE,PTCEFDAT,DIM3
          CALL      RDPTCLC1
          BRANCH    OVRCD,LSTCR800
          MOVE      PTCLTEXT,KEY50
.
LSTCR800  MOVE      ZERO,ANSA
          CLEAR     HTMLLINK
          APPEND    "javascript:UCrt('",HTMLLINK
          APPEND    LINKPRG,HTMLLINK
          APPEND    "#.pbl?reportno=",HTMLLINK
          APPEND    LINKREP,HTMLLINK
          APPEND    "&template=",HTMLLINK
          APPEND    LINKTEM,HTMLLINK
          APPEND    "&urnumber=",HTMLLINK
          APPEND    URNUMBER,HTMLLINK
          APPEND    "&ptcer002=",HTMLLINK
          APPEND    PTCETYPE,HTMLLINK
          APPEND    "&ptcer003=",HTMLLINK
          APPEND    KEY11,HTMLLINK
          APPEND    "&admissno=",HTMLLINK
          APPEND    ADMISSNO,HTMLLINK
          APPEND    "')",HTMLLINK
          RESET     HTMLLINK

          WRITE     HTMLFILE;"t.addRow(#"",HTMLLINK,"#",":      * 0 Link
                                      "#"",CTYPDESC,"#",":      * 1 Cert Type
                                      "#"",PTCEFDAT,"#",":      * 2 Strt date
                                      "#"",PTCETDAT,"#",":      * 3 End date
                                      "#"",PTCESDAT,"#",":      * 4 Issue Date
                                      "#"",DOCFNAME,"#",":      * 5 Doctor
                                      "#"",DIM4,"#",":          * 6 Sent
                                      "#"",CSTADESC,"#",":      * 7 Status 
                                      "#"",KEY50,"#");"         * 8 Comments
          GOTO      LSTCR100
.
LSTCR999  RETURN
+
.-------------------------------------------------------------------------------
. Check certificate is for the current Admission 
.    Clone of ADMCHK00 rehashed so as not to break existing functionality
.-------------------------------------------------------------------------------
ADMCH000  MOVE      ZERO,EXIT
          MATCH     "Y",ANS
          GOTO      ADMCH100 IF EQUAL       * only read Admiss/Disch once
          MOVE      "Y",ANS
.
          PACK      KEY8,ADMISSNO,SP10
          CALL      RDMISS1
          BRANCH    OVRCD,ADMCH900
.
          IF        ASTAT = 1 | ASTAT = 5
            GOTO      ADMCH900              * Ignore cancelled and pre-admissions
          ENDIF
.
          MOVE      "99991231",DIM8
          COMPARE   THREE,ASTAT
          GOTO      ADMCH100 IF NOT EQUAL   * not discharged
.
          PACK      DDATE,SP20
          PACK      KEY8,ADMISSNO,SP10
          CALL      RDDSCH1
.
          MATCH     SP8,DDATE
          GOTO      ADMCH100 IF EQUAL
          MOVE      DDATE,DIM8
.
ADMCH100  MATCH     ADATE,PTCETDAT          * check From & To dates for extremes
          GOTO      ADMCH900 IF LESS        * bypass this one
          MATCH     PTCEFDAT,DIM8
          GOTO      ADMCH900 IF LESS        * bypass this one
.
          GOTO      ADMCH999
.
ADMCH900  MOVE      ONE,EXIT                * set EXIT to bypass the Certificate
.
ADMCH999  RETURN
+
