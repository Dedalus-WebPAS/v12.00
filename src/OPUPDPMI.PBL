.***************************************************************************
.*   System          : Theatre System                                      *
.*   Include Function: OPUPDPMI.PBL                                        *
.*   Description     : Update PMI details                                  *
.***************************************************************************
.*   Date            : 08/11/1990                                          *
.*   Author          : Paul Marshall                                       *
.*   Modifications   :                                                     *
.*        V12.00.01 23/05/2025  Ebon Clements TSK 0955096                  *
.*                  Alphanueric visit number changes                       *
.*        V10.07.01 04/01/2016 Ebon Clements  TSK 0294154                  *
.*                  Added SVOSMS                                           *
.*        V10.06.01 12/03/2015  Steve Armstrong   CAR 314108               *
.*                  Removed reference to PATCGPFD (No Longer in use)       *
.***************************************************************************
.*        V10.04.01 22/01/2014  Don Nguyen        CAR 295972               *
.*                  Changed KEY16 to KEY24 for IO calls on patpa1af        *
.***************************************************************************
.*        V10.00.01 19/03/2010  Steve Armstrong   CAR 135505               *
.*                  Added HL7TRGID & HL7INCLD setting for all broadcast    *
.*                  messages                                               *
.***************************************************************************
.*        V9.02.01  08/11/2001  Mike Laming                                *
.*                  Rename DGCLIUPM to DGCLICUP                            *
.*        V5.10.B01 11/04/2001  Ebon Clements                              *
.*                  Made changes for new PATPA1FD layout. Moved spaces to  *
.*                  new fields that are not used                           *
.*        V5.09.00  04/01/2001  Pat Dirito                                 *
.*                  Added Mobile Phone & Business Phone variables          *
.*                  SVTELEB,SVCELL                                         * 
.*        V5.07.00  14/10/1998  Jim Stathopoulos                           *
.*                  507 Changes                                            *
.*        V5.04.01  14/01/1997  Steve Armstrong                            *
.*                  Mods for Datagate API's                                *
.***************************************************************************
.*   Parameters      : PMI details                                         .
.*                     Booking number                                      .
.*                     Original surname record (if it exists).             .
.*                     Original address/telephone data.                    .
.*                     Flag to say if basic PMI details have been changed. .
.*                     Flag to say if address data has been changed.       .
.*                                                                         .
.***************************************************************************
.*   Returns         : None                                                .
.***************************************************************************
.*   Function        : Post the PMI details to the appropriate files.      .
.***************************************************************************
.*   Screen          : None                                                .
.***************************************************************************
OPUPDPMI  
          MATCH     PURNO,ZEROUR
          GOTO      OUPMI050 IF EQUAL       * on pre-admission file
.
.         Update the PMI audit file if necessary
.
          CALL      PURAD000
.
.         update master file
.
          CALL      UPMAST1
.
          CALL      PMIGTNID                * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      ONE,HL7TRGID
          MOVE      "OPUPDPMI",HL7INCLD
          PROC      DGCLICUP                * write PMI changes to DG  *C-90201
.
          GOTO      OUPMI100
.
.         update pre-admission file and set up surname file details
.
OUPMI050  MOVE      OPDAADMN,PURNO
          CALL      UPPRAM1
          MOVE      ZEROUR,PURNO
.
.         update surname file
.
OUPMI100  CALL      UPDSUR
.
          MATCH     ZEROUR,PURNO
          GOTO      OUPMI999 IF EQUAL
.
.         check if a change in address/suburb/telephone
.
          MATCH     PADD1,SVADD1            *
          GOTO      OUPMI600 IF NOT EQUAL   *  All of these test are
          MATCH     PADD2,SVADD2            *  to see if the Address data
          GOTO      OUPMI600 IF NOT EQUAL   *  has been changed.
          MATCH     PSUBRB,SVSUBRB          *  If it has, the previous 
          GOTO      OUPMI600 IF NOT EQUAL   *  address data must be posted
          MATCH     PADD4,SVADD4            *  to see if the Address data
          GOTO      OUPMI600 IF NOT EQUAL   *  has been changed.
          MATCH     PPOST,SVPOST            *
          GOTO      OUPMI600 IF NOT EQUAL   *
          MATCH     PTELEP,SVTELEP          *
          GOTO      OUPMI600 IF NOT EQUAL   *
          MATCH     PTELEB,SVTELEB          *
          GOTO      OUPMI600 IF NOT EQUAL   *
          MATCH     PTMXCELL,SVCELL         *
          GOTO      OUPMI600 IF NOT EQUAL   *
          MATCH     PTMXOSMS,SVOSMS         *
          GOTO      OUPMI600 IF NOT EQUAL   *
          IF        PTCNDORP=1
            MATCH     PTMXDOFR,SVDOR
            GOTO      OUPMI600 IF NOT EQUAL
          ENDIF
.
          GOTO      OUPMI999                * no changes
.
.         write to previous address file
.         check if there has been a change for the current day, if there has
.         delete this record and write our one
.
OUPMI600  PACK      PADATE,CCC,CYY,CMM,CDD
          REP       " 0",PADATE
          CLOCK     TIME,PATIME
.
          PACK      KEY24,PURNO,PADATE,PATIME
          CALL      RDAPADD1
          BRANCH    OVRCD,OUPMI700          * no record so write our one
.
          GOTO      OUPMI999                * Only 1st change per day is posted
.
OUPMI700  MOVE      PURNO,PAURNO            * UR number
          MOVE      SVADD1,PAADD1
          MOVE      SVADD2,PAADD2
          MOVE      SVSUBRB,PASUBR
          MOVE      SVADD4,PAADD4
          MOVE      SVPOST,PAPOST
          MOVE      SVTELEP,PATELEP
          MOVE      SVTELEB,PATELEB
          MOVE      SVDOR,PTPADOR
          MOVE      SVCELL,PTPAMOBL 
          PACK      PTPAEMAL,SP70,SP70
          MOVE      SP70,PTPACDTE
          MOVE      SP70,PTPACTIM
          MOVE      SP70,PTPAWEBC
          MOVE      SP70,PTPALUPD
          MOVE      SP70,PTPALUPT
          MOVE      SP70,PTPAWEBU
          MOVE      SVOSMS,PTPAOSMS
          MOVE      SP70,PTPASPAR
.
          CALL      WRPADD1                 * write to file
.
OUPMI999  RETURN
+
. ****************************************************************************
. * PURAD000 : Post changes to U/R audit file if needed.  Called by OPUPDPMI *
. ****************************************************************************
PURAD000  MATCH     ZEROUR,PURNO
          GOTO      PURAD999 IF EQUAL       * zero UR so exit
.
          MATCH     PTITL,SVTITL
          GOTO      PURAD170 IF NOT EQUAL
          MATCH     PSNAME,SVSNAME
          GOTO      PURAD170 IF NOT EQUAL
          MATCH     PGNAME,SVGNAME
          GOTO      PURAD170 IF NOT EQUAL
          MATCH     PADD1,SVADD1
          GOTO      PURAD170 IF NOT EQUAL
          MATCH     PADD2,SVADD2
          GOTO      PURAD170 IF NOT EQUAL
          MATCH     PSUBRB,SVSUBRB
          GOTO      PURAD170 IF NOT EQUAL
          MATCH     PADD4,SVADD4
          GOTO      PURAD170 IF NOT EQUAL
          MATCH     PPOST,SVPOST
          GOTO      PURAD170 IF NOT EQUAL
          MATCH     PTELEP,SVTELEP
          GOTO      PURAD170 IF NOT EQUAL
          MATCH     PTELEB,SVTELEB
          GOTO      PURAD170 IF NOT EQUAL
          MATCH     PTMXCELL,SVCELL
          GOTO      PURAD170 IF NOT EQUAL
          MATCH     PSEX,SVSEX
          GOTO      PURAD170 IF NOT EQUAL
          MATCH     PMSTAT,SVMSTAT
          GOTO      PURAD170 IF NOT EQUAL
          MATCH     PBDATE,SVBDATE
          GOTO      PURAD170 IF NOT EQUAL
          MATCH     PTYPE,SVTYPE
          GOTO      PURAD170 IF NOT EQUAL
.
          GOTO      PURAD999                * no changes so Exit
.
.         have changes in PMI details so update necessary audit files
.         check to see if have to do PMI audits                               
.
PURAD170  CALL      OPUPDURA
.
PURAD999  RETURN
