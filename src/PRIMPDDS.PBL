.  ****************************************************************************
.  *                                                                          *
.  *  Program Name     :  PRIMPDDS.PBL                                        *
.  *                                                                          *
.  *  Function         :  Displays the doctors belonging to a given medical   *
.  *                      practice                                            *
.  *                                                                          *
.  *  Author           :  Stephen Armstrong      28/12/90                     *
.  *                                                                          *
.  *  Keys             :  KEY10, KEY18                                        *
.  *                                                                          *
.  *  Requirements     :  MPRAC - (DIM 6)                                     * 
.  *                      MPDESC - (DIM 30)                                   *
.  *                                                                          *
.  *  Returns          :  OVRCD = 1 if nothing on file                        *
.  *                                                                          *
.  *  Modifications    :                                                      *
.  *     V10.13.01  06/08/2018  J.Tan       TSK 0848506                       *
.  *                Changed PP Doctor from DIM6 to DIM10                      *
.  *      V9.02.01  02/10/2001  J.Tan                                         *
.  *                Mods to use PMSHCPFD instead of PATDO1FD                  *
.  *      V5.07.01  23/06/1999  Greg Horvat                                   *
.  *                Changed to only display valid doctors                     *
.  *                                                                          *
.  ****************************************************************************
.
PRIMPDDS  DISPLAY   *P1:4,*EF,*ULON,"Doctors in Medical Practice - ":
                    *V2LON,MPRAC,SP2,MPDESC;
          MATCH     SP6,MPRAC
          IF        @EQUAL
            MOVE      SIX,CVERT
            DISPLAY   *P1:6,*ULON,"Practice",*P11:6,"Doctor",*P19:6,"Name":
                      SP30,SP1;
          ELSE
            MOVE      FOUR,CVERT
          ENDIF
.
          PACK      KEY22,SP70
          CALL      RSPRDO1                     * anything on file ?
          CALL      RKPRDO1
          BRANCH    OVRCD,MPDDS950              * no
.
          MOVE      SP70,DCODE
          PACK      KEY22,MPRAC,SP70
          CALL      RSPRDO1                     * position 
.
.         read the next record
.
MPDDS100  CALL      RKPRDO1
          BRANCH    OVRCD,MPDDS400
.
          MATCH     SP6,MPRAC
          GOTO      MPDDS150 IF EQUAL       * Show all doctors for practices
.
          MATCH     PRDOPRAC,MPRAC              * same M.P. ?
          GOTO      MPDDS400 IF NOT EQUAL       * no
.
MPDDS150  MATCH     PRDODOCT,DCODE              * same doctor ?
          GOTO      MPDDS100 IF EQUAL           * yes
.
          MOVE      PRDODOCT,DCODE
          PACK      KEY10,PRDODOCT,SP10
          CALL      RDPMHCP1
          BRANCH    OVRCD,MPDDS100
.
          MOVE      PMHCTITL,PACTITLE
          MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          CALL      PACNAME
          MOVE      PACFNAME,KEY35
.
          ADD       ONE,CVERT
          COMPARE   TWENTY4,CVERT               * page full ?
          GOTO      MPDDS200 IF LESS            * no
          GOTO      MPDDS300                    * yes
.
MPDDS200  MATCH     SP6,MPRAC
          IF        @EQUAL
            DISPLAY   *P1:CVERT,*V2LON,PRDOPRAC,SP4,PRDODOCT,*HOFF,SP2,KEY35;
          ELSE
            DISPLAY   *P1:CVERT,*V2LON,PRDODOCT,*HOFF,SP4,KEY35;
          ENDIF
          GOTO      MPDDS100
.
.         New standards question
.
MPDDS300  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, ":
                    "(",*V2LON,"F",*HOFF,")irst Screen, ":
                    "(",*V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSC,ANS                     * continue ?
          GOTO      MPDDS400 IF EQUAL
.
          CMATCH    ANSF,ANS                     * first screen ?
          GOTO      PRIMPDDS IF EQUAL
.
          CMATCH    ANSN,ANS                     * next screen ?
          GOTO      MPDDS500 IF EQUAL
.
          BEEP                                   * invalid
          GOTO      MPDDS300
.
MPDDS400  MOVE      ZERO,OVRCD
          GOTO      MPDDS999
.
.         Clear the screen
.
MPDDS500  MATCH     SP6,MPRAC
          IF        @EQUAL
            MOVE      SEVEN,CVERT
          ELSE
            MOVE      FIVE,CVERT
          ENDIF
          DISPLAY   *P1:CVERT,*EF
          GOTO      MPDDS200
.
MPDDS950  DISPLAY   *P1:24,*EL,*B,"No doctors on file. ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL
.
MPDDS999  RETURN
+
.

