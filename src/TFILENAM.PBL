.------------------------------------------------------------------------------
. Include File   : TFILENAM.PBL
.
. Function       : This function will get the next sequence number from IBASEQFD
.                  to use for a temp file name. It will then return an 8 
.                  character unique file name which a T prefix and 7 digit
.                  zero filled number
.
.                  eg T0000001
.
. Requires       : IBASEQFD to be included
.                  WEBERRFD to be included
.                  TFILEVAR to be included          
.
. Usage          : CALL TFILENAM    * Get temp file name
.
. Returns        : TFILNAME DIM 8 - TXXXXXXX
.                  
.
. Author         : Ebon Clements 
.
. Modifications  :
.
. V10.03.02  11/04/2013  Ebon Clements     CAR 261630
.            Changed temp file name to T and a 7 digit zero filled number
. V10.03.01  25/10/2012  Jill Parkinson    CAR 275362
.            Added open of weberraf
. V10.03.00  19/07/2012  Ebon Clements     CAR 261630
.            Created include
.------------------------------------------------------------------------------
.
TFILENAM  OPEN      IBASEQA1,"ibaseqaf"
.
          MOVE      ZERO,TFILEUNQ            * Clear 7 digit unique number
          MOVE      SP70,TFILNAME            * Clear temp file name
          MOVE      ZERO,F2
.
TEMPFN10  COMPARE   TWENTY,F2                * loop 20 times if ibaseqaf is
          GOTO      TEMPFN95 IF NOT LESS     * locked - then error
.
          ADD       ONE,F2
.
          MOVE      "TFILE",KEY8             * read ibaseqaf for the next
          PACK      KEY8,KEY8,SP70           * TFILE sequential number
          CALL      RLIBSEQ1
          BRANCH    OVRCD,TEMPFN90,TEMPFN10
.
          ADD       ONE,IBSQNEXT
          CALL      UPIBSEQ1
          CALL      UUIBSEQ1
.
          IF        (IBSQNEXT>IBSQMAXM) | (IBSQMAXM > 9999999)
            MOVE      ZERO,IBSQNEXT            * go back to zero if max reached
            MOVE      "   9999999",IBSQMAXM    * or max was > 9999999
            CALL      UPIBSEQ1
            CALL      UUIBSEQ1
            GOTO      TEMPFN10
          ENDIF
.
          MOVE      IBSQNEXT,TFILEUNQ
          PACK      TFILNAME,TFILPRFX,TFILEUNQ,SP70
          REP       " 0",TFILNAME
          GOTO      TEMPFN99
.
TEMPFN90  MOVE      "TFILE   ",IBSQTYPE     * If no record on ibaseqaf for TFILE
          PACK      KEY8,IBSQTYPE,SP70      * create one with a max of 9999999
          MOVE      ZERO,IBSQNEXT
          MOVE      "   9999999",IBSQMAXM
          CALL      RAIBSEQ1
          IF        OVRCD=1
            CALL      WRIBSEQ1
          ENDIF
          GOTO      TFILENAM
.
TEMPFN95  DISPLAY   *W1,"ibaseqaf temp file record locked."
          MOVE      "ibaseqaf temp file record locked.",WBERERR
          CALL      TFLERR00
          MOVE      ZERO,F2
          GOTO      TFILENAM
.
TEMPFN99  RETURN
.
.------------------------------------------------------------------------------
. Log Error to Web Error Log
.------------------------------------------------------------------------------
TFLERR00  OPEN      WEBERRA1,"weberraf"
          CLOCK     TIME,CTIMEIS
          PACK      KEY24,PRGID,CCC,CYY,CMM,CDD,CTIMEIS,SP70
          REP       " 0",KEY24
          CALL      RDWBER1
          IF        OVRCD=0
            GOTO      TFLERR99
          ENDIF
.
          UNPACK    KEY24,WBERPID,WBERDAT,WBERTIM
          PACK      WBERUID,SP70
          PACK      WBERREP,SP70
          PACK      WBERTMP,SP70
          PACK      WBERURN,SP70
          PACK      WBERVIS,SP70
          MOVE      "1",WBERCNT
          MOVE      SP70,WBERSPA
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          CALL      WRWBER1
          TRAPCLR   IO
.
TFLERR99  RETURN
.
