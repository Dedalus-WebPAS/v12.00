.******************************************************************************
.        PATADTYP : Calculate the number of bed days for a admission type
.
.        Parameter: F1 - Selected admission type indicator
.                        1 = HCP Field   - PTCDNHCP
.                        2 - Indicator 3 - TCINDC3
.                   D1 - Indicator value
.                   TODATE  - ending date
.                   Valid admission file read
.
.        Return   : NBRDAYS - Number of bed days
.
.        Modifications :
.        V12.00.01  26/05/2025 PJ Le Febour      TSK 0955096 AlphaNum VisitNum
.                   amend  COMPARE  TADMN,AADMNO  to  MATCH  TADMN,AADMNO
.        V11.01.00  18/06/2021 Ebon Clements     TSK 0902615
.                   Created include from PATSBTYP
.
.*******************************************************************************
          DEFPROC   PATADTDY
.
          INC       PATTRNFD/INC
.
PTAT0000  OPEN      PATTRAN2,"pattranf"
          MOVE      SP10,WDATE1             * initialise the start date
          MOVE      SP10,WDATE2             * initialise the end date
          MOVE      ZERO,NBRDAYS
.
          COMPARE   THREE,ASTAT             * discharged ?
          GOTO      PTAT1000 IF NOT EQUAL   * no.
.
          MATCH     ADATE,DDATE             * day case patient ?
          GOTO      PTAT1000 IF NOT EQUAL   * no.
.
          PACK      KEY30,AADMNO,Z70
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,PTAT9000
.
          MATCH     TADMN,AADMNO            * same admission ?
          GOTO      PTAT9000 IF NOT EQUAL   * No.
.
          PACK      KEY5,ANSA,SP1,TATYPE,SP70
          CALL      RDCODE1                 * read admission type
          BRANCH    OVRCD,PTAT9000
.
          IF        F1=1
            MATCH     D1,PTCDNHCP           * same admission type HCP value ?
            GOTO      PTAT9000 IF NOT EQUAL
          ENDIF
.
          IF        F1=2
            MATCH     D1,TCINDC3           * same admission type ind 3 value ?
            GOTO      PTAT9000 IF NOT EQUAL
          ENDIF
.
          MATCH     ADATE,TODATE
          GOTO      PTAT9000 IF LESS
.
          MOVE      ONE,NBRDAYS             * no of bed days is one for daycase
          GOTO      PTAT9000
.
PTAT1000  PACK      KEY30,AADMNO,SP20,SP10
          CALL      RDSTRAN2
PTAT1200  CALL      RDKTRAN2
          BRANCH    OVRCD,PTAT4000
.
          MATCH     TADMN,AADMNO            * same admission ?
          GOTO      PTAT4000 IF NOT EQUAL
.
          REP       " 0",TDATE
          MOVE      TDATE,STDATE
.
          MATCH     ANSA,TMOVE              * Admission record ?
          GOTO      PTAT2000 IF EQUAL
.
          MATCH     ANSR,TMOVE              * Return record ?
          GOTO      PTAT2000 IF EQUAL
.
          CMATCH    ANSC,TMOVE              * Change record ?
          GOTO      PTAT2200 IF EQUAL
.
          GOTO      PTAT3000
.
.        Admission/Return record
.
PTAT2000  MATCH     TDATE,TODATE
          GOTO      PTAT9000 IF LESS
.
          PACK      KEY5,ANSA,SP1,TATYPE,SP70
          CALL      RDCODE1                 * read admission type
          BRANCH    OVRCD,PTAT1200
.
          IF        F1=1
            MATCH     D1,PTCDNHCP           * same admission type HCP value ?
            GOTO      PTAT1200 IF NOT EQUAL
          ENDIF
.
          IF        F1=2
            MATCH     D1,TCINDC3           * same admission type ind 3 value ?
            GOTO      PTAT1200 IF NOT EQUAL
          ENDIF
.
          MOVE      TDATE,WDATE1            * set from date
          GOTO      PTAT1200                * get next trans record
.
.        Change
.
PTAT2200  MATCH     TDATE,TODATE
          IF        @LESS
            MATCH     SP6,WDATE1              * Check with the fromdate
            GOTO      PTAT9000 IF EQUAL
            MOVE      TODATE,WDATE2           * set to date
            CALL      SADY0000                * Calculate bed days
            GOTO      PTAT9000
          ENDIF
.
          PACK      KEY5,ANSA,SP1,TATYPE,SP70
          CALL      RDCODE1                * read admission type
          BRANCH    OVRCD,PTAT2400
.
          IF        F1=1
            MATCH     D1,PTCDNHCP           * same admission type HCP value ?
            GOTO      PTAT2400 IF NOT EQUAL
          ENDIF
.
          IF        F1=2
            MATCH     D1,TCINDC3           * same admission type ind 3 value ?
            GOTO      PTAT2400 IF NOT EQUAL
          ENDIF
.
.        Change to the selected admisson type
.
          MATCH     SP8,WDATE1            * check if it was a real change of
          GOTO      PTAT1200 IF NOT EQUAL   * adm type
.
          MOVE      TDATE,WDATE1          * set fromdate of the change adm type
          GOTO      PTAT1200
.
.        Change to a different adm type, check if it was from selected adm type
.
PTAT2400  MATCH     SP8,WDATE1           
          GOTO      PTAT1200 IF EQUAL
.
          MOVE      TDATE,WDATE2
          CALL      SADY0000                * Calculate bed days
.
          GOTO      PTAT1200                * get next trans record
.
.        Discharge/Leave record always on the adm type as previous record
.
PTAT3000  MATCH     SP8,WDATE1
          GOTO      PTAT3200 IF EQUAL       * Was not on selected adm type
.
          MATCH     TDATE,TODATE
          IF        @LESS
            MOVE      TODATE,WDATE2         * set to end date
          ELSE
            MOVE      TDATE,WDATE2          * Set to date
          ENDIF
          REP       " 0",WDATE2
          CALL      SADY0000                * Calculate bed days
.
PTAT3200  CMATCH    "D",TMOVE
          GOTO      PTAT1200 IF NOT EQUAL   * get next record
.
          GOTO      PTAT9000
.
.        check the last record
.
PTAT4000  MATCH     SP8,WDATE1
          GOTO      PTAT9000 IF EQUAL       * Was not on selected adm type
.
          MOVE      TODATE,WDATE2
          REP       " 0",WDATE2
          CALL      SADY0000                * Calculate bed days
          GOTO      PTAT9000
.
PTAT9000  CLOSE     PATTRAN2
          GOTO      PTAT9999
.
          INC       IBAOVRIO/INC
          INC       PATTRNIO/INC
.
PTAT9999  ENDPROC
+
.******************************************************************************
.        SADY0000 - Calculate dates difference
.******************************************************************************
SADY0000  PACK      CDYSFDTE,WDATE1
          PACK      CDYSTDTE,WDATE2
          CALL      CALCDAYS
          MOVE      CDYSDAYS,CALDAYS
          SUB       ONE,CALDAYS
          ADD       CALDAYS,NBRDAYS
          MOVE      SP10,WDATE1
          MOVE      SP10,WDATE2
.
SADY9999  RETURN
+
