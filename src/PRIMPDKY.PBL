.******************************************************************************
.*  Include Name  : PRIMPDKY.PBL                                              *
.*  Description   : Keyin of a Service doctor for Private Practice            *
.*  Author        : Barney Rubble                                             *
.*  Modification  :                                                           *
.*       V10.13.01  06/08/2018  J.Tan       TSK 0848506                       *
.*                  Changed PP Doctor from DIM6 to DIM10                      *
.*        V9.02.01  02/10/2001  J.Tan                                         *
.*                  Mods to use PMSHCPFD instead of PATDO1FD                  *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF6 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMODE - is keyin insert mode (0=No  1=Yes)             *
.*                  CKYINACT - check if variable is active (0=No  1=Yes)      *
.*                  MPRAC    - Medical practice                               *
.*                                                                            *
.*  Returns       : DCODE  - Keyed in code                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.******************************************************************************
PRIMPDKY  
MPDKY010  
.         MATCH     SP6,CKYIDEF6                  * using a default ?
.         GOTO      MPDKY050 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN6:      * no default used
                    *PECOL:EVERT,*V2LON,DCODE:
                    *PECOL:EVERT,*DV,DCODE
          GOTO      MPDKY080
.
MPDKY050  MOVE      CKYIDEF6,DCODE
          KEYIN     *PECOL:EVERT,*DV,DCODE:     * default used
                    *PECOL:EVERT,*V2LON,*RV,DCODE:
                    *PECOL:EVERT,*DV,DCODE
.
.         check what was entered
.
MPDKY080  CALL      MPDCK000
          BRANCH    EXIT,MPDKY100,MPDKY705,MPDKY805,MPDKY905
.                         "?"      valid    nothing exitchar
          GOTO      MPDKY010
.
.         a "?" was input
.
MPDKY100  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
          MOVE      SP30,MPDESC
          MATCH     SP6,MPRAC
          IF        !@EQUAL
            PACK      KEY6,MPRAC
            CALL      RDPRPR1                 * Read a medical practice file rec
            IF        OVRCD<>1
              MOVE      PRPRDESC,MPDESC
            ENDIF
          ENDIF
.
MPDKY105  CALL      PRIMPDDS
          BRANCH    CNEWDS,MPDKY150
.
          CALL      PUTSCR00
          GOTO      MPDKY010
.
.         now ask for code while leaving displayed codes on screen
.
MPDKY150  MATCH     SP6,CKYIDEF6                  * using a default
          GOTO      MPDKY200 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Doctor Code : ":
                    *P15:24,*DV,UNDLN6:      * no default used
                    *P15:24,*V2LON,DCODE:
                    *P15:24,*DV,DCODE
          GOTO      MPDKY300
.
MPDKY200  MOVE      CKYIDEF6,DCODE
          KEYIN     *P1:24,*EL,*DV,"Doctor Code : ":
                    *P15:24,*DV,DCODE:     * default used
                    *P15:24,*V2LON,*RV,DCODE:
                    *P15:24,*DV,DCODE
.
.         repeat the validation of the code
.         check what was entered
.
MPDKY300  CALL      MPDCK000
          BRANCH    EXIT,MPDKY105,MPDKY700,MPDKY800,MPDKY900
.                         "?"      valid    nothing exitchar
.
          GOTO      MPDKY150
.
.         a valid code was entered 
. 
MPDKY700  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,DCODE
.
MPDKY705  MOVE      FALSE,EXIT                    * valid input
          GOTO      MPDKY999 
.
.         nothing was input
.
MPDKY800  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,DCODE
.
MPDKY805  MOVE      TRUE,EXIT                     * Nothing or Spaces entered
          GOTO      MPDKY999 
.
.         EXITCHAR input
.
MPDKY900  CALL      FRESCR00                      * free the screen saved
.
MPDKY905  MOVE      TWO,EXIT                      * EXITCHAR
.
MPDKY999  RETURN
+
.***************************************************************************
.*                              MPDCK000             Called by: PRIMPDKY   *
.*                        check what was entered                           *
.*          Returns:  EXIT  = 0   invalid                                  *
.*                          = 1   "?"                                      *
.*                          = 2   valid                                    *
.*                          = 3   nothing                                  *
.*                          = 4   exitchar                                 *
.***************************************************************************
MPDCK000  ENDSET    DCODE
          APPEND    SP70,DCODE
          RESET     DCODE
.
          MATCH     EXITCHAR,DCODE
          GOTO      MPDCK600 IF EQUAL
.
          MATCH     SP6,DCODE
          GOTO      MPDCK700 IF EQUAL
.
.         check for a "?"
.
          MATCH     QUEST,DCODE
          GOTO      MPDCK800 IF EQUAL
.
. a code was entered so validate
. 
          PACK      KEY10,DCODE,SP10
          CALL      RDPMHCP1
          IF        CKYIMODE=0
            BRANCH    OVRCD,MPDCK900
          ELSE
            COMPARE   ZERO,OVRCD
            GOTO      MPDCK900 IF EQUAL
          ENDIF
.
          IF        CKYINACT=1
            MATCH     "1",PMHCSTTS
            IF        @EQUAL
              DISPLAY   *P1:24,*EL,*B,"Doctor is Inactive.  ";
              CALL      HITENTER
              GOTO      MPDCK910
            ENDIF
          ENDIF
.
          MATCH     SP6,MPRAC
          GOTO      MPDCK200 IF EQUAL       * Blank medical practice
.
          PACK      KEY22,MPRAC,DCODE,SP20
          CALL      RSPRDO1                 * Position on & read a med prac doc
          CALL      RKPRDO1                   file record
          BRANCH    OVRCD,MPDCK100
.
          MATCH     MPRAC,PRDOPRAC
          GOTO      MPDCK100 IF NOT EQUAL   * Different medical practice
.
          MATCH     DCODE,PRDODOCT
          GOTO      MPDCK100 IF NOT EQUAL   * Different service doctor
.
          COMPARE   ONE,PRDOSTAT
          GOTO      MPDCK200 IF NOT EQUAL   * Doctor active
.
          DISPLAY   *P1:24,*EL,*B,"Doctor is Inactive for Medical ":
                    "Practice.  ";
          CALL      HITENTER
          GOTO      MPDCK910
.
MPDCK100  COMPARE   ONE,CKYIMODE
          GOTO      MPDCK200 IF EQUAL       * Insert mode, ok to continue
.
          DISPLAY   *P1:24,*EL,*B,"Doctor Not Setup for Medical Practice.  ";
          CALL      HITENTER
          GOTO      MPDCK910
.
MPDCK200  MOVE      TWO,EXIT
          GOTO      MPDCK999
.
. exitchar entered
.
MPDCK600  MOVE      FOUR,EXIT
          UNPACK    SP70,PMHCSNAM,PMHCTITL
          MOVE      SP70,PMHCGNAM
          MOVE      SP6,DCODE                   * clear code
          GOTO      MPDCK999
.
. nothing entered
.
MPDCK700  BRANCH    CKYIMAND,MPDCK950             * mandatory
.
          UNPACK    SP70,PMHCSNAM,PMHCTITL
          MOVE      SP70,PMHCGNAM
          MOVE      THREE,EXIT
          GOTO      MPDCK999
.
. "?" entered
.
MPDCK800  MOVE      ONE,EXIT
          GOTO      MPDCK999
.
. invalid code entered
.
MPDCK900  IF        CKYIMODE=0
            DISPLAY   *P1:24,*EL,*B,"Invalid Doctor Code.  ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
MPDCK910  MOVE      ZERO,EXIT
          GOTO      MPDCK999
.
MPDCK950  BEEP
          MOVE      ZERO,EXIT
.
MPDCK999  RETURN
.
          INC       PRIMPDDS
+
.

