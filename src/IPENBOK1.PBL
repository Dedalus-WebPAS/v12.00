.
.        Modification :
.
.        V11.00.01 07/05/2020 J.Tan   TSK 0890827
.                  Mod to loop site file
.        V10.14.01 30/03/2020 J.Tan   TSK 0889749
.                  Removed FSTSYS
.
.        ======================================================================
.        Invoice Pending for ABF 'Booked' outpatients IBABIL19
.        ======================================================================
.
BOUT0000  READ      CONTROLF,HUND28;*105,PTCNUABF
          MATCH     "1",PTCNUABF
          GOTO      BOUT9999 IF NOT EQUAL
.
          MOVE      ZERO,DISPROG                 * Flag required by CALCTBI
          MOVE      ONE,BOUTFLAG                 * flag for 'Booked' outpatient
.
          DISPLAY   *P1:24,*EL:
                    "Calculating Total Amount Still to be Invoiced - Booked.":
                    *P48:24,*EL,"Billing No :";
.
          PACK      KEY6,SP70
          CALL      RDSSITA1
BOUT0500  CALL      RDKSITA1
          BRANCH    OVRCD,BOUT9999
.
          MATCH     SP70,FSTSITE
          IF        !@EQUAL
            MATCH     FSTSITE,OSTSITE
            GOTO      BOUT0500 IF LESS
          ENDIF
.
          MATCH     OSTSITE,FEDSITE
          GOTO      BOUT9999 IF LESS
.
          CLOSE     OUTSESA6
          PACK      CFNAME,OSTFILE,FILSESA6,SP70
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OUTSESA6,CFNAME
          TRAPCLR   IO
          BRANCH    OVRCD,BOUT0500
.
          PACK      CFNAME,OSTFILE,FILBOKA1
          CLOSE     OUTBOKA1
          OPEN      OUTBOKA1,CFNAME           * Open the file
.
          PACK      CFNAME,OSTFILE,FILBB1A1
          CLOSE     OUTBB1A1
          CALL      OPOTBB11
.
.         Loop through Session from today date

          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
.
          PACK      KEY25,OSTSITE,KEY8,SP70
          CALL      RDSSESA6
BOUT1000  CALL      RDKSESA6
          BRANCH    OVRCD,BOUT0500
.
          MATCH     OSESITE,OSTSITE
          GOTO      BOUT0500 IF NOT EQUAL
.
          PACK      KEY25,OSESITE,OSECLIN,OSEDATE,OSESTRT,SP70
          PACK      KEY28,KEY25,SP70
          CALL      RDSBOKA1
BOUT1200  CALL      RDKBOKA1
          BRANCH    OVRCD,BOUT1000
.
          PACK      DIM25,OBASITE,OBACLIN,OBADATE,OBASTRT,SP70
          MATCH     DIM25,KEY25
          GOTO      BOUT1000 IF NOT EQUAL
.
          COMPARE   ONE,OBASTAT
          GOTO      BOUT1200 IF NOT EQUAL      * not booked
.
          DISPLAY   *P61:24,*EL,*V2LON,OBAOUTNO;  * Display number on screen
.
.         Check for ABF
.
          PACK      KEY8,OBAOUTNO
          CALL      RDBOKB1
          BRANCH    OVRCD,BOUT1200
.
          PACK      KEY5,ANSC,ANSL,OBACOMP
          CALL      RDCODE1
          BRANCH    OVRCD,BOUT1200
.
          MATCH     "A",TCINDC2          * ABF Funding ?
          GOTO      BOUT1200 IF NOT EQUAL
.
          MATCH     SP70,OTBBTCOD
          GOTO      BOUT1200 IF EQUAL         * Blank Tier 2
.
          MOVE      OBAURNO,AURNO
          MOVE      AURNO,KEY8
          CALL      RDMAST1                      * Read in the PMI record
          BRANCH    OVRCD,BOUT1200
.
          PACK      KEY8,OBAOUTNO,SP8
          CALL      RDPMVX11
          BRANCH    OVRCD,BOUT1200
.
          IF        IBCNMHOS =1
            MATCH     SP10,HOSPID
            IF        !@EQUAL
              MATCH     PMVXMHOS,HOSPID
              GOTO      BOUT1200 IF NOT EQUAL
            ENDIF
          ENDIF
.
          CLOSE     OUTDTRO1
          PACK      CFNAME,OSTFILE,FILDTRO1
          OPEN      OUTDTRO1,CFNAME
.
          PACK      PINVDTE,CCC,CYY,CMM,CDD
          REP       " 0",PINVDTE
.
          MOVE       OBAOUTNO,OTNUMB
.
          IF         IBCNUGST=2 | IBCNUGST=3
            MOVE       ZERO,IBCNUGST          * Set to No GST for to be invoiced
            CALL       OUTTBI                 * Calculate the amt to invoice
            READ       CONTROLF,ZERO;*85,IBCNUGST
          ELSE
            CALL       OUTTBI                 * Calculate the amt to invoice
          ENDIF
.
          GOTO      BOUT1200
.
BOUT9999  RETURN
+
