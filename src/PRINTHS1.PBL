+
.******************************************************************************
.*                                                                            *
.* Include Name     : PRINTHS1.PBL                                            *
.*                                                                            *
.* Description      : Print cash receipts for Holy Spirit                     *
.*                                                                            *
.* Files Required   : PATCODFD.INC  1st index     PATDOCFD.INC  1st index     *
.*                    PATGO1FD.INC  1st index     PATFN1FD.INC  1st index     *
.*                    PATIN1FD.INC  1st index                                 *
.*                    PATINVFD.INC  1st index     PATMASFD.INC  1st index     *
.*                    DEBDBTFD.INC  1st index     DEBFTHFD.INC  1st index     *
.*                    PATMISFD.INC  1st index     PATRESFD.INC  1st index     *
.*                    PATVISFD.INC  1st index     PRIHREFD.INC  1st index     *
.*                    PRIHDBFD.INC  1st index     RCPREGFD.INC                *
.*                    PRIINVFD.INC  1st index     RCPDTEFD.INC  1st index     *
.*                    RCPBNKFD.INC  1st index     RCPBDTFD.INC  2nd index     *
.*                    RCPCONFD.INC                                            *
.*                                                                            *
.* Subroutines Req  : STD001FD      RCPPRTVR.INC                              *
.*                                                                            *
.* Parameters Req   :                                                         *
.*                    RECEIPTN - Receipt number                               *
.*                                                                            *
.* Modifications    :                                                         *
.*        V10.14.01 20/03/2019  J.Tan            TSK 0857392                  *
.*                  Changed RCP Transaction count from DIM3 to DIM5           *
.*        V10.13.01 31/07/2018  J.Tan            TSK 0848506                  *
.*                  Changed PP Doctor from DIM6 to DIM10                      *
.*        V10.08.01 18/04/2016  Peter Vela     CAR 0294177                    *
.*                  Changed read to prihdb to KEY27                           *
.*        V10.04.01 24/01/2013  Patrick Adair  CAR 265844                     *
.*                  Add new fields for PRA State                              *
.*        V10.02.01 28/07/2010  J.Tan    CAR 244159                           *
.*                  Mods for Credit Card Surchage (register 89)               *
.*        V9.10.01  08/09/2008  J.Tan             CAR 170368                  *
.*                  Mods checking for zero MAXCLNO to avoid looping           *
.*        V9.09.03  26/03/2008  Peter Vela        CAR 163975                  *
.*                  More modifications for templated header and tail          *
.*        V9.09.02  19/03/2008  Peter Vela        CAR 163975                  *
.*                  Modified to print templated Header and Tail               *
.*        V9.09.01  21/02/2008  Mike Laming       CAR 161012                  *
.*                  Added printing "Cancelled" receipts to Header routines    *
.*        V9.04.03  17/09/2004 Lina Vo                                        *
.*                  Put in extra checks for HSYS3 and HSYS6 and to not read   *
.*                  files                                                     *
.*        V9.04.02  30/08/2004 Lina Vo          CAR 53012                     *
.*                  Initialised Debtor Number                                 *
.*        V9.04.01  16/08/2004 Lina Vo          CAR 53012                     *
.*                  Ported from V6.12. Changed to use new receipting tables   *
.*        V6.06.02  16/03/1999 Jill Habasque                                  *
.*                  Mods to printing of receipt                               *
.*        V6.06.01  15/03/1999 Jill Habasque SRF 628558                       *
.*                  Fixed printing of receipt                                 * 
.*                                                                            *
.*                                                                            *
.******************************************************************************
PRINTHS1  COMPARE   ONE,HFORMAT
          GOTO      HS1LD999 IF NOT EQUAL   * Not Holy Spirit's receipt layout
.
          MOVE      ZERO,CLNO
          MOVE      ZERO,MPGEFLAG
          MOVE      ZERO,CPAGENO
.
          UNPACK    SP70,PATNAME,PRNTPNUM
          UNPACK    SP70,PRNTPNAM,PRNTNAME
          UNPACK    SP70,PRNTADD1,PRNTADD2
          UNPACK    SP70,PRNTSUBR,PRNTPOST
          UNPACK    SP70,PPRAADD1,PPRAADD2
          UNPACK    SP70,PPRASUBR,PPRAPOST
          UNPACK    SP70,CRCPDATE
.
          MOVE      ZERO,SPRAFLAG           * Set Person Resp for Acct Flag
.
          PACK      KEY12,RECEIPTN,SP20     * Check if any records for the pat
          CALL      RDRCBNK1
          BRANCH    OVRCD,HS1LD999
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
.
          COMPARE   ONE,IBCNMHOS
          GOTO      PRINTHS3 IF NOT EQUAL
.
          UNPACK    SP70,PTHSHOSP,PTHSNAME
          UNPACK    SP70,PTHSADD1,PTHSADD2
          UNPACK    SP70,PTHSADD3,PTHSADD4
          UNPACK    SP70,PTHSPCOD,PTHSTELE,PTHSFAXN
.
          MATCH     SP70,RCBNCDRW
          IF        !@EQUAL & !@EOS
            PACK      KEY6,RCBNCDRW,SP70
            CALL      RDCIDA1
            BRANCH    OVRCD,PRINTHS2
.
            OPEN      PATHSPA1,"pathspaf"
            PACK      KEY3,RCCIHOSP,SP70
            CALL      RDPTHSP1
            BRANCH    OVRCD,PRINTHS2
.
          ELSE
PRINTHS2    MATCH     SP70,RCBNCUID
            IF        !@EQUAL & !@EOS
              PACK      KEY10,RCBNCUID,SP70
              CALL      RDWBSE1
              BRANCH    OVRCD,PRINTHS3
.
              OPEN      PATHSPA1,"pathspaf"
              PACK      KEY3,WBSEHOSP,SP70
              CALL      RDPTHSP1
.
            ENDIF
          ENDIF
.
PRINTHS3  MOVE      RCBNTDAT,CRCPDATE
.
          PACK      CANCLMSG,SP70                                     *I-161012
          MATCH     "1",RCBNSTAT
          IF        @EQUAL                                                    
            MOVE      "THIS  RECEIPT  HAS  BEEN  CANCELLED",CANCLMSG          
          ENDIF
.
          MATCH     "0",RCBNTTYP           * Received From Patient 
          GOTO      HS1LD100 IF EQUAL
.
          MATCH     "1",RCBNTTYP           * Received From Health Fund
          IF        @EQUAL
            PACK      KEY14,RCBNRCOD,HFHEADER,SP70
            CALL      RDFUND1
            IF        OVRCD=0
              MOVE      HFNAME,PRNTRNAM
              MOVE      HFADD1,PRNTADD1
              MOVE      HFADD2,PRNTADD2
              MOVE      HFADD3,PRNTSUBR
              MOVE      HFADD4,PRNTADD4
              SQUEEZE   HFPCODE
              MOVE      HFPCODE,PRNTPOST
              MOVE      ONE,SPRAFLAG
            ENDIF
          ENDIF
.
          MATCH     "2",RCBNTTYP           * Received From Insurance Co
          IF        @EQUAL
            PACK      KEY6,RCBNRCOD,SP70
            CALL      RDINSR1
            IF        OVRCD=0
              MOVE      INAME,PRNTRNAM
              MOVE      IADD1,PRNTADD1
              MOVE      IADD2,PPRAADD2
              MOVE      IADD3,PRNTSUBR
              MOVE      IADD4,PRNTADD4
              SQUEEZE   IPOST
              MOVE      IPOST,PRNTPOST
              MOVE      ONE,SPRAFLAG
            ENDIF
          ENDIF
.
          MATCH     "3",RCBNTTYP           * Received From Govt Dept
          IF        @EQUAL
            PACK      KEY6,RCBNRCOD,SP70
            CALL      RDGOVR1
            IF        OVRCD=0
              MOVE      GNAME,PRNTRNAM
              MOVE      GADD1,PRNTADD1
              MOVE      GADD2,PRNTADD2
              MOVE      GADD3,PRNTSUBR
              MOVE      GADD4,PRNTADD4
              SQUEEZE   GPOST
              MOVE      GPOST,PRNTPOST
              MOVE      ONE,SPRAFLAG
            ENDIF
          ENDIF
.
. ----- Record found, start printing -----
.
HS1LD100  CALL      OPNPRINT                * Prepare the spoolfile
          MOVE      ZERO,CLNO               * Reset the line counter
          MOVE      ZERO,CPAGENO
.
          PACK      KEY15,RECEIPTN,SP20      * For this receipt ...
          CALL      RSRCBDT1
          CALL      RKRCBDT1
          BRANCH    OVRCD,HS1LD999
.
          MATCH     RCBDRECN,RECEIPTN        * Same receipt still ?
          GOTO      HS1LD999 IF NOT EQUAL    * No 
.
          PACK      KEY17,RECEIPTN,SP20
          CALL      RSRCDTE1                 * Go through all transactions
HS1LD200  CALL      RKRCDTE1
          BRANCH    OVRCD,HS1LD900
.
          MATCH     RCDTRECN,RECEIPTN        * Same receipt still ?
          GOTO      HS1LD900 IF NOT EQUAL    * No - pad out receipt
.
          MOVE      RCDTREGI,KEY3
          CALL      RDREGA1
          BRANCH    OVRCD,HS1LD200
.
          CALL      HS1RG000                * Get the register details
          BRANCH    EXIT,HS1LD200
.
HS1LD300  IF        CLNO=0
            CALL      RHED0000              * Print receipt header
          ENDIF
.
.         IF        CLNO>=24
.           CALL      HS1TT000              * Print receipt tail over 2 pages
.           CALL      HS1HD000              * Print receipt header
.         ENDIF
.
          COMPARE   ZERO,MAXCLNO
          GOTO      HS1LD900 IF EQUAL
.
          MOVE      MAXCLNO,FORM2
          COMPARE   FORM2,CLNO
          IF        !@LESS
            CALL      RTAL0000
            MOVE      ONE,MPGEFLAG
            ADD       ONE,CPAGENO
            MOVE      ONE,HEADTAIL
            CALL      XRCP0000
            PRINT     " "
            MOVE      ZERO,MPGEFLAG
            MOVE      ZERO,CLNO
            GOTO      HS1LD300
          ENDIF
.
          CALL      HS1LN000                * Print a line of the receipt
          GOTO      HS1LD200
.
HS1LD900  CALL      REND0000
          ADD       ONE,CPAGENO
          MOVE      ONE,HEADTAIL
          CALL      XRCP0000
          PRINT     " "  
.
.         IF        CLNO<>0
.           CALL      HS1TL000              * Print receipt tail
.         ENDIF
          CALL      CLSPRINT                * Print the spoolfile

HS1LD999  RETURN
+
.******************************************************************************
.*                                  HS1RG000              Called by: HS1LD000 *
.*                          Get The Register Details                          *
.******************************************************************************
HS1RG000  MOVE      ZERO,EXIT
          UNPACK    SP70,PKADD1
          UNPACK    SP70,PKADD2,PKSUBR,PKPOST 
          UNPACK    SP70,PADD1
          UNPACK    SP70,PADD2,PSUBRB,PPOST 
          UNPACK    SP70,PATPROVD 
.
. ----- Patient billing register -----
.
          IF        REGIBACD=1
            UNPACK    RCDTINVN,KEY4,KEY8
            CALL      RDINV1                * Read the patient invoice file
            BRANCH    OVRCD,HS1RG950
.
            CALL      HS1LP000              * Load patient admission details
            BRANCH    EXIT,HS1RG950
          ENDIF
.
. ----- Private Practice register -----
.
          IF        REGIBACD=2
            COMPARE   ONE,HSYS3
            GOTO      HS1RG950 IF NOT EQUAL
            UNPACK    RCDTINVN,KEY4,KEY8
            CALL      RDPRIN1               * Read the PP invoice file
            BRANCH    OVRCD,HS1RG950
.
            MOVE      PRINNUMB,PINVNO       * Invoice number
            IF        PRINSCAN=0
              MOVE      SP70,PATPROVD
              IF        SPRAFLAG=0
                MOVE      PRINDEBT,PRNTPNUM             * Debtor number
                MOVE      "Unknown patient",PRNTNAME         * Patient name
                MOVE      SP70,PRNTADD1          * Patient address line 1
                MOVE      SP70,PRNTADD2          * Patient address line 2
                MOVE      SP70,PRNTSUBR          * Patient address line 3
                MOVE      SP70,PRNTPOST          * Patient postcode
                MOVE      SP70,PRNTADD4
                PACK      PRNTRNAM,SP70
                PACK      PPRAADD1,SP70
                PACK      PPRAADD2,SP70
                PACK      PPRASUBR,SP70
                PACK      PPRAPOST,SP70
                MOVE      ONE,SPRAFLAG
              ENDIF
            ELSE
              CALL      HS1LP000            * Load patient admission details
              BRANCH    EXIT,HS1RG950
            ENDIF
.
            PACK      KEY10,PRINDOCT
            CALL      RDPMHCP1              * Read the hcp file
            IF        OVRCD<>1
              STRIP     PMHCSNAM
              STRIP     PMHCGNAM
              CLEAR     PATPROVD
              APPEND    "DR ",PATPROVD
              APPEND    PMHCSNAM,PATPROVD
              APPEND    ", ",PATPROVD
              APPEND    PMHCGNAM,PATPROVD
              RESET     PATPROVD            * Service doctor name
            ENDIF
          ENDIF
.
. ----- Sundry Debtors register -----
.
          IF        REGIBACD=5
            COMPARE   ONE,HSYS6
            GOTO      HS1RG950 IF NOT EQUAL
            CALL      HS1LS000
          ENDIF
.
. ----- Private practice deposits register -----
.
          IF        REGIBACD=96
            COMPARE   ONE,HSYS3
            GOTO      HS1RG950 IF NOT EQUAL
            MATCH     "0",RCDTSFLG     * Debtor
            IF        @EQUAL
              MOVE      SP70,PATPROVD
              IF        SPRAFLAG=0
                MOVE      RCDTURNO,PRNTPNUM             * Debtor number
                MOVE      "Unknown patient",PRNTNAME         * Patient name
                MOVE      SP70,PRNTADD1            * Patient address line 1
                MOVE      SP70,PRNTADD2            * Patient address line 2
                MOVE      SP70,PRNTSUBR            * Patient address line 3
                MOVE      SP70,PRNTADD4
                MOVE      SP70,PRNTPOST            * Patient postcode
                PACK      PRNTRNAM,SP70         * PRA name
                PACK      PPRAADD1,SP70         * PRA address line 1
                PACK      PPRAADD2,SP70         * PRA address line 2
                PACK      PPRASUBR,SP70         * PRA address line 3
                PACK      PPRAPOST,SP70         * PRA postcode
                MOVE      ONE,SPRAFLAG
              ENDIF
            ELSE
              CALL      HS1LP000            * Load patient admission details
              BRANCH    EXIT,HS1RG950
            ENDIF
.
            MOVE    RCDTPRAC,KEY6
            CALL    RDPRPR1
            IF      OVRCD=0
              MOVE    PRPRDESC,PATPROVD
            ENDIF
          ENDIF
.
. ----- Patient billing deposits register -----
.
          IF        REGIBACD=90 | REGIBACD=91 | REGIBACD=97
            CALL      HS1LP000              * Load patient admission details
            BRANCH    EXIT,HS1RG950
          ENDIF
.
. ----- Internal receipts & suspense registers -----
.
          IF        REGIBACD=98|REGIBACD=99|REGIBACD=94|REGIBACD=95|REGIBACD=92|REGIBACD=89
            IF        SPRAFLAG=1
              MOVE      RCDTNAME,PATPROVD  
            ELSE 
              PACK      PRNTRNAM,RCDTNAME,SP70
              PACK      PRNTADD1,RCDTADD1,SP70
              PACK      PRNTADD2,RCDTADD2,SP70
              PACK      PRNTSUBR,RCDTADD3,SP70
              PACK      PRNTPOST,RCDTPOST,SP70
              PACK      PRNTADD4,SP70
              MOVE      RCDTNAME,PATPROVD
              MOVE      ONE,SPRAFLAG
            ENDIF
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      HS1RG999
.
HS1RG950  MOVE      ONE,EXIT
HS1RG999  RETURN
+
.******************************************************************************
.*                                  HS1LP000              Called by: HS1LD000 *
.*                        Load Patient Details                                *
.******************************************************************************
HS1LP000  PACK      KEY8,RCDTURNO
          CALL      RDMAST1                 * Read the patient master file
          BRANCH    OVRCD,HS1LP950
.
          BRANCH    SPRAFLAG,HS1LP500
          MOVE      ONE,SPRAFLAG
.
          IF        REGIBACD=2
            PACK      PRNTRNAM,PRINNAMR,SP70
            PACK      PRNTADD1,PRINPRA1,SP70
            PACK      PRNTADD2,PRINPRA2,SP70
            PACK      PRNTSUBR,PRINPRA3,SP70
            PACK      PRNTPOST,PRINPOST,SP70
            PACK      PRNTADD4,PRINPRA4,SP70
            GOTO      HS1LP100
          ENDIF
.
          IF        REGIBACD=96
            PACK      KEY27,RCDTURNO,Z70
            CALL      RSPRHD1
HS1LP040    CALL      RPPRHD1
            BRANCH    OVRCD,HS1LP100
.
            MATCH     RCDTURNO,PRHDDEBT
            GOTO      HS1LP100 IF NOT EQUAL
.
            PACK      KEY27,PRHDUNIQ,RCDTPRAC,Z70
            CALL      RSPRHR1
            CALL      RPPRHR1
            BRANCH    OVRCD,HS1LP040
.
            COMPARE   PRHDUNIQ,PRHRUNIQ
            GOTO      HS1LP040 IF NOT EQUAL
            MATCH     RCDTPRAC,PRHRPRAC
            GOTO      HS1LP040 IF NOT EQUAL
.
            PACK      PRNTRNAM,PRHRNAME,SP70
            PACK      PRNTADD1,PRHRADD1,SP70
            PACK      PRNTADD2,PRHRADD2,SP70
            PACK      PRNTSUBR,PRHRADD3,SP70
            PACK      PRNTPOST,PRHRPOST,SP70
            PACK      PRNTADD4,PRHRADD4,SP70
            GOTO      HS1LP100
          ENDIF
.
          PACK      KEY8,RCDTVIST
          CALL      RDRESP1                 * Read the patient PRA file
          IF        OVRCD=0          
            MOVE      PKNAME,PRNTRNAM
            PACK      PRNTADD1,PKADD1,SP70
            PACK      PRNTADD2,PKADD2,SP70
            PACK      PRNTSUBR,PKSUBR,SP70
            PACK      PRNTPOST,PKPOST,SP70
            PACK      PRNTADD4,SP70
          ENDIF
.
HS1LP100  MOVE      PADD1,PPRAADD1
          MOVE      PADD2,PPRAADD2
          MOVE      PSUBRB,PPRASUBR
          MOVE      PPOST,PPRAPOST
          MOVE      RCDTURNO,PRNTPNUM
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME                 * Format the patient name
          MOVE      PACFNAME,PRNTNAME        * Patient name
.
HS1LP500  MOVE      PGNAME,DIM1
          PACK      PATPROVD,DIM1,DOT,PSNAME
.
          MOVE      ZERO,EXIT
          GOTO      HS1LP999
.
HS1LP950  MOVE      ONE,EXIT
HS1LP999  RETURN
+
.******************************************************************************
.*                                  HS1LS000              Called by: HS1LD000 *
.*                    Load Sundry Debtor  Details                             *
.******************************************************************************
.
HS1LS000  MOVE      RCDTINVN,INVOICEN
          PACK      KEY12,INVOICEN
          REP       " 0",KEY12
          PACK      KEY21,ONE,KEY12,SP70
          CALL      RSDBFH6
          CALL      RKDBFH6
          BRANCH    OVRCD,HS1LS950
          MATCH     "1",DBFHDTY             * Check Document Type 1=Invoice
          GOTO      HS1LS950 IF NOT EQUAL
          MATCH     KEY12,DBFHDOC           * Check Document Ref.
          GOTO      HS1LS950 IF NOT EQUAL
.
          PACK      KEY8,DBFHDEB,SP70
          CALL      RDDBDB1
          BRANCH    OVRCD,HS1LS950
.
          BRANCH    SPRAFLAG,HS1LS500
          MOVE      ONE,SPRAFLAG
.
          MOVE      RCDTURNO,PRNTPNUM
          MOVE      DBDBNA1,PRNTRNAM
          MOVE      DBDBAD1,PRNTADD1
          MOVE      DBDBAD2,PRNTADD2
          MOVE      DBDBAD3,PRNTSUBR
          MOVE      SP70,PRNTADD4
          MOVE      DBDBPC,PRNTPOST
          MOVE      SP70,PRNTNAME        * Patient name
          MOVE      SP70,PPRAADD1          * Patient address line 1
          MOVE      SP70,PPRAADD2          * Patient address line 2
          MOVE      SP70,PPRASUBR          * Patient address line 3
          MOVE      SP70,PPRAPOST          * Patient postcode
.
HS1LS500  MOVE      DBDBNA1,PATPROVD
.
          MOVE      ZERO,EXIT
          GOTO      HS1LS999
.
HS1LS950  MOVE      ONE,EXIT
HS1LS999  RETURN
+
.******************************************************************************
.*                                  HS1HD000              Called by: HS1LD000 *
.*                            Print Receipt Header                            *
.******************************************************************************
HS1HD000  ADD       ONE,CPAGENO
.
          PRINT     *N,*N,*N,*N,*N,*54,"Debtor No. : ",PRNTPNUM,*N,*N:
                    *N,*12,PRNTPNAM,*43,PATNAME:
                    *N,*12,PRNTNAME,*43,PRNTADD1:
                    *N,*12,PPRAADD1,*43,PRNTADD2:
                    *N,*12,PPRAADD2,*43,PRNTSUBR:
                    *N,*12,PPRASUBR,*74,PRNTPOST:
                    *N,*38,PPRAPOST:
                    *N,*N,*30,"R E C E I P T",*N,*N,*N,*N,*15,CANCLMSG,*N,*N
          UNPACK    RCBNTDAT,CCENT,CYEAR,CMON,CDAY
          PRINT     *1,"Page ",CPAGENO,*12,RECEIPTN:
                    *65,CDAY,"/",CMON,"/",CCENT,CYEAR:
                    *N,*N,*N
          MOVE      ZERO,CLNO               * Reset transaction counter
HS1HD999  RETURN
+
.******************************************************************************
.*                                  HS1LN000              Called by: HS1LD000 *
.*                         Print A Line Of The Receipt                        *
.******************************************************************************
.D        P                  P                       T    I         A
HS1LN000  UNPACK    RCBNTDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      RCDTAMNT,FORM8P2
          UNPACK    RCDTINVN,D4,DIM8
          MOVE      RCDTREFR,REFERENC         * Default refer no to the provider
          IF        REGIBACD=5|REGIBACD=95|REGIBACD=89
            PRINT     *1,CDAY,"/",CMON,"/",CYEAR,*10,PATPROVD:
                      *31,REFERENC,*54,RCDTREGI,*58,DIM8,*68,FORM8P2
          ELSE
            MOVE      PATPROVD,DIM11
            PRINT     *1,CDAY,"/",CMON,"/",CYEAR,*10,RCDTVIST,*19,DIM11:
                      *31,REFERENC,*54,RCDTREGI,*58,DIM8,*68,FORM8P2
          ENDIF
          ADD       ONE,CLNO
HS1LN999  RETURN
+
.******************************************************************************
.*                                  HS1TT000              Called by: HS1LD000 *
.*                       Print Receipt Tail Over 2 Pages                      *
.******************************************************************************
HS1TT000  PRINT     *N,*N:
                    *N,*69,"CARRIED FWD":
                    *N,*N,*N,*N,*N,*N,*N,*N,*N
HS1TT999  RETURN
+
.******************************************************************************
.*                                  HS1TL000              Called by: HS1LD000 *
.*                             Print Receipt Tail                             *
.******************************************************************************
HS1TL000  REPEAT
            PRINT     " "
            ADD       ONE,CLNO
          UNTIL     CLNO=24
          MOVE      RCBNTOTP,FORM8P2
          PRINT     *N,*N:
                    *N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*67,RCBNTOTP
          MOVE      ZERO,CPAGENO
HS1TL999  RETURN
+
.******************************************************************************
.*                                  PRTHS1LN                                  *
.*                             Print Holy Spirit Line Up                      *
.******************************************************************************
PRTHS1LN  
         PRINT      *N,*N,*N,*N,*N,*N,*N,*54,"Debtor No. : ","XXXXXXXXXX",*N,*N:
                    *N,*12,"XXXXXXXXXXXXXXXXXXXXXXXXX":
                       *50,"XXXXXXXXXXXXXXXXXXXXXXXXX":
                    *N,*12,"XXXXXXXXXXXXXXXXXXXXXXXXX":
                       *50,"XXXXXXXXXXXXXXXXXXXXXXXXX":
                    *N,*12,"XXXXXXXXXXXXXXXXXXXXXXXXX":
                       *50,"XXXXXXXXXXXXXXXXXXXXXXXXX":
                    *N,*12,"XXXXXXXXXXXXXXXXXXXXXXXXX":
                       *50,"XXXXXXXXXXXXXXXXXXXXXXXXX":
                    *N,*12,"XXXXXXXXXXXXXXXXXXXXXXXXX":
                       *50,"XXXXXXXXXXXXXXXXXXXXXXXXX":
                    *N,*34,"XXXX":
                       *70,"XXXX":
                    *N,*N,*N,*N,*N,*N,*N:
                    *1,"PAGE ","XXX",*12,"XXXXXXXXXX",*65,"99/99/9999":
                    *N,*N,*N,*N:
                    *1,"99/99/99",*22,"XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX":
                    *67,"9999999999.99":
                    *N,*N,*N,*N,*N,*N,*N,*N,*N:
                    *N,*N,*N,*N,*N,*N,*N,*N,*N:
                    *N,*N,*N,*N,*N,*N,*N,*N,*N:
                    *N,*67,"9999999999.99"
PRTHS199  RETURN
