.****************************************************************************
.*                                                                          *
.*        Program Name: PRIITMDS.PBL                                        *
.*                                                                          *
.*            Function: Lists items on file according to item description   *
.*                                                                          *
.*              Author: Steve Armstrong 10/01/91                            *
.*                                                                          *
.*        Requirements: SHTDESC DIM23 - short item description              *
.*                      IFLAG   FORM2 - Item Flag                           *
.*                      ITMFLAG   FORM 1 - date flag                        *
.*                                         0 = display codes for given date *
.*                                         1 = display codes regardless     *
.*                      SERDATE   DIM 8 - service date                      *
.*                      ITMDESC  DIM  30 - item description                 *
.*                      NINE8   FORM "99999999"                             *
.*                      PATHFLAG FORM 1:  1 = display all items             *
.*                                        0 = display only pathology items  *
.*                                                                          *
.*       Returns      : OVRCD = 1 if nothing on file                        *
.*                                                                          *
.*       Modifications: 26/03/92 Steve Armstrong                            *
.*                      Added  PATHFLAG parameter to indicate only          *
.*                      to list items which are pathology related           *
.*                      V6.01  02/06/92  DIG                                *
.*                             Fixed so that if no description is keyed in  *
.*                             all items are displayed.                     *
.****************************************************************************
.
PRIITMDS  
.         
ITMDS000  MOVE      ONE,CCOL
          DISPLAY   *P1:4,*EF,*P31:4,*V2LON,*ULON," ITEM NUMBERS "
.
          BRANCH    IFLAG,ITMDS050               * AMA item
.
          DISPLAY   *P45:4," - MBS Items"
          GOTO      ITMDS100
.
ITMDS050  DISPLAY   *P45:4," - AMA Items"
.
ITMDS100  KEYIN     *P1:6,"Description : ",*DV,UNDLN30:
                    *P15:6,*V2LON,ITMDESC
.
          RESET     ITMDESC
          IF        @EOS
             PACK   KEY52,IFLAG,SP30,SP30
             MOVE   SP30,ITMDESC
          ELSE
             PACK   KEY52,IFLAG,ITMDESC,SP30,SP30
          ENDIF
.
          DISPLAY   *P1:5,*EF,*V2LON,*ULON,"Description",*P25:5,"Item No.":
                    *P40:5,"Description",*P64:5,"Item No."
          MOVE      FIVE,CVERT
.
          CALL      RSPRIT2                      * position in file
          CALL      RKPRIT2                      * read next record
          BRANCH    OVRCD,ITMDS950               * end of file
.
          GOTO      ITMDS120
.
ITMDS110  CALL      RKPRIT2                      * read next record
          BRANCH    OVRCD,ITMDS300               * end of file
.
ITMDS120  COMPARE   IFLAG,PRITFLAG               * same item type ?
          GOTO      ITMDS300 IF NOT EQUAL        * no
.
          BRANCH    ITMFLAG,ITMDS130             * ignore service date
.
          MATCH     PRITDAT1,SERDATE             * service date < eff. date ?
          GOTO      ITMDS110 IF LESS             * yes - get next record
.
.         See if we are displaying only pathology items
.
          BRANCH    PATHFLAG,ITMDS130            * display all items
.
          BRANCH    PRITPATH,ITMDS110            * not pathology item
.
.         Valid record found
.
ITMDS130  MATCH     SP30,ITMDESC                 * see if we want all items
          GOTO      ITMDS140 IF EQUAL
.
          MATCH     ITMDESC,PRITDESC             * see if item has the same
          GOTO      ITMDS300 IF NOT EQUAL          description
.
ITMDS140  ADD       ONE,CVERT                    * increment line count
          COMPARE   TWENTY3,CVERT                * page full ?
          GOTO      ITMDS150 IF LESS             * no
.
          COMPARE   ONE,CCOL                     * second half of screen full ?
          GOTO      ITMDS200 IF NOT EQUAL        * yes
.
          MOVE      FORTY,CCOL                   * set up for second half of
.                                                  screen display
          MOVE      SIX,CVERT
.
ITMDS150  MOVE      PRITDESC,SHTDESC
          DISPLAY   *PCCOL:CVERT,SHTDESC,SP1,PRITITMN
.      
          MATCH     SP3,PRITSUBN                 * any subitem # ?
          GOTO      ITMDS170 IF EQUAL            * no
.
          ADD       THIRTY,CCOL
          ADD       THREE,CCOL
          DISPLAY   *PCCOL:CVERT,"(",PRITSUBN,")"
          SUB       THIRTY,CCOL
          SUB       THREE,CCOL
.
.         Position at end of this description/item number, ready to read the
.         next entry
.
ITMDS170  PACK      KEY52,PRITFLAG,PRITDESC,PRITITMN,PRITSUBN,NINE8
          CALL      RSPRIT2
          GOTO      ITMDS110
.
.         Page full
.
ITMDS200  KEYIN     *P1:24,*EL,"(",*V2LON,*DV,ANSC,*HOFF,")ontinue, ":
                    "(",*V2LON,*DV,ANSF,*HOFF,")irst Screen, ":
                    "(",*V2LON,*DV,ANSN,*HOFF,")ext Screen : ",*V2LON,ANS;
.
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
          REP       UPPLOW,ANS
.
          CMATCH    ANSC,ANS                     * C entered ?
          GOTO      ITMDS300 IF EQUAL            * yes
.
          CMATCH    ANSF,ANS                     * F entered ?
          GOTO      PRIITMDS IF EQUAL            * yes
.
          CMATCH    ANSN,ANS                     * N entered ?
          GOTO      ITMDS400 IF EQUAL            * yes
.
          BEEP
          GOTO      ITMDS200                     * invalid entry
.
ITMDS300  MOVE      ZERO,OVRCD                   * set flag for records found
          GOTO      ITMDS9999
.
.         Clear the screen
.
ITMDS400  MOVE      SIX,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:CVERT,*EF
.
          GOTO      ITMDS150
.
ITMDS950  DISPLAY   *P1:24,*EL,*B,"No items on file. ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL;
.
ITMDS999  RETURN
