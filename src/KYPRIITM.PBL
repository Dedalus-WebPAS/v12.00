.******************************************************************************
.*   Include name  :  KYPRIITM.PBL                                            *
.*   Author        :  Stephen Armstrong                                       *
.*   Date          :  16/01/92                                                *
.*                                                                            *
.*   Parameters passed : CCOL : entry column                                  *
.*                       CVERT: entry row                                     *
.*                       IFLAG:  0 = MBS item  (Form 2)                       *
.*                               1 = AMA item                                 *
.*                                                                            *
.*   Returns           : EXIT  0 = code input                                 *
.*                             1 = exitchar input                             *
.*                             2 = ? input                                    *
.*                             3 = nothing input                              *
.*                       ITEMNUMB  (DIM 9 Item code)                          *
.*                       SUBITEM (DIM 3 Subitem code)                         *
.*                                                                            *
.*   Modifications :                                                          *
.*        V6.03.01  20/05/1996  Greg Horvat      SRF 615223                   *
.*                  Changed to space fill the sub item code before the keyin  *
.*                                                                            *
.******************************************************************************
.
KYPRIITM  KEYIN     *PCCOL:CVERT,*DV,UNDLN9,*DV,SP4:  * input item code
                    *PCCOL:CVERT,*V2LON,ITEMNUMB:
                    *PCCOL:CVERT,*DV,ITEMNUMB
.
          ENDSET    ITEMNUMB                     * pad with blanks
          APPEND    SP9,ITEMNUMB
          RESET     ITEMNUMB
.
          MATCH     SP3,ITEMNUMB                 * anything entered ?
          GOTO      KYITM950 IF EQUAL            * no
.
          MATCH     EXITCHAR,ITEMNUMB            * exitchar input ?
          GOTO      KYITM850 IF EQUAL            * yes
.
          MATCH     QUEST,ITEMNUMB               * ? input ?
          GOTO      KYITM900 IF EQUAL            * yes
.
          MOVE      SP3,SUBITEM                  * clear the sub item
.
.         A code was entered, so if this is an MBS code, get the subitem
.
          BRANCH    IFLAG,KYITM800               * AMA code
.
          ADD       NINE,CCOL
          DISPLAY   *PCCOL:CVERT,SLASH
          ADD       ONE,CCOL
          KEYIN     *PCCOL:CVERT,*DV,UNDLN3:     * keyin subitem number
                    *PCCOL:CVERT,*V2LON,SUBITEM:
                    *PCCOL:CVERT,*DV,SUBITEM
.
          ENDSET    SUBITEM                      * pad with blanks
          APPEND    SP3,SUBITEM
          RESET     SUBITEM
.
          MATCH     SP3,SUBITEM                  * anything entered ?
          GOTO      KYITM700 IF EQUAL            * no
.
          SUB       TEN,CCOL
          MATCH     QUEST,SUBITEM                * ? entered ?
          GOTO      KYITM900 IF EQUAL            * yes
.
          MATCH     EXITCHAR,SUBITEM             * exitchar entered ?
          GOTO      KYITM850 IF EQUAL            * yes
.
          GOTO      KYITM800
.
KYITM700  SUB       ONE,CCOL
          DISPLAY   *PCCOL:CVERT,SP4
          SUB       NINE,CCOL
.
KYITM800  MOVE      ZERO,EXIT
          GOTO      KYITM9999
.
KYITM850  MOVE      ONE,EXIT
          GOTO      KYITM9999
.
KYITM900  MOVE      TWO,EXIT
          GOTO      KYITM9999
.
KYITM950  MOVE      THREE,EXIT
.
KYITM999  RETURN
