.******************************************************************************
.* System         : Patient Management                                        *
.* Include        : PATWIS16.PBL                                              *
.* Description    : Calculate WIES16 value for an individual admission        *
.******************************************************************************
.* Date           : 10/06/2009                                                *
.* Author         : Davin Sloan                                               *
.*                                                                            *
.* Vars Required  : VARWIS16 - Variables required for WIES16                  *
.*                                                                            *
.* Files Required Open :                                                      *
.*        PATASFFD  PATASFA1 - Admission special funding arrangement file     *
.*        PATCODFD  PATCODE1 - Codes file                                     *
.*        PATECDFD  PATECDA2 - Episode disease coding file                    *
.*        PATECOFD  PATECOA2 - Episode operation coding file                  *
.*        PAT10OFD  PATI10O1 - ICD10 operations file                          *
.*        PATICUFD  PATICUA1 - ICU file                                       *
.*        PATTRNFD  PATTRAN2 - Transfer file                                  *
.*        PATW16FD  PATW16A1 - Victorian cost weights (for WIES16) file       *
.*                                                                            *
.* Parameters Req : Successful read on admission file                         *
.*                  Successful read on discharge file                         *
.*                  HIHLOS   - for Hopsipta in Home LOS (from RHIHDAYS)       *
.*                  NBRDAYS  - for LOS calculation (from NHOSPDAY)            *
.*                  PNSWRACE - User defined field for race                    *
.*                  WIS16DRG - DRG to calculate WIES16 for                    *
.*                                                                            *
.* Returns        : TOTWIS16 - Total WIES16 value                             *
.*                  WIS16DRG - DRG used to calculate WIES16                   *
.*                  WIS16LOS - LOS used to calculate WIES16                   *
.*                  WIS16WGT - Weight used to calculate WIES16                *
.*                  WIS16DSC - Description of DRG weight used                 *
.*                                                                            *
.* Modifications  :                                                           *
.*        V12.00.01 20/05/2025  Ebon Clements    TSK 0955096                  *
.*                  Alphanueric visit number changes                          *
.*        V10.02.01 28/03/2011  Mike Laming   CAR 240107                      *
.*                  Mods to PATECDaf & PATECOaf variables & Keys - file change*
.*        V9.12.01  04/09/2009  Davin            CAR 204230                   *
.*                  Changed to only modify Radiotherapy DRGs                  *
.*        V9.12.00  10/06/2009  Davin            CAR 199724                   *
.*                  Created for 2009/2010 WIES16 changes:                     *
.******************************************************************************
+
PATWIS16  CALL      IN16W000                * Initialise variables
          BRANCH    EXIT,WIS16950
.
          CALL      C16TY000                * Determine and Check Care Type
          BRANCH    EXIT,WIS16950
.
          CALL      C16TR000                * Check for Contracted Patient
          BRANCH    EXIT,WIS16950
.
          CALL      M16RG000                * Modify DRG if necessary
.
          CALL      C16RG000                * Check for problem DRG
          BRANCH    EXIT,WIS16950
.
          CALL      M16CH000                * Check for Mechanical Ventilation
.
          CALL      T16AL000                * Check for Thalessaemia
.
          CALL      A16AA000                * Check for AAA
.
          CALL      A16SD000                * Check for ASD
.
          CALL      B16SW000                * Calculate Base WIES16
.
          CALL      C16AB000                * Check for aboriginality
.
. ---- Calculate WIES16 value ----
.
          MOVE      TOTBWIES,TOTWIS16       * base WIES
          ADD       MVCOPAY,TOTWIS16        * mechv copayment
          ADD       THCOPAY,TOTWIS16        * thal  copayment
          ADD       AAACOPAY,TOTWIS16       * AAA   copayment
          ADD       ASDCOPAY,TOTWIS16       * ASD   copayment
          ADD       ATSIWIES,TOTWIS16       * ATSI  copayment
.
          MOVE      ZERO,EXIT
          GOTO      WIS16999
.
WIS16950  MOVE      ONE,EXIT
WIS16999  RETURN
+
.******************************************************************************
.*                                  IN16W000              Called by: PATWIS16 *
.*                         Initialise WIES16 variables                        *
.******************************************************************************
IN16W000  MOVE      ZERO,TOTWIS16
          MOVE      ZERO,WIS16LOS
          MOVE      ZERO,WIS16WGT
          MOVE      SP20,WIS16DSC
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATW16A1,"patw16af"
          TRAPCLR   IO
          BRANCH    OVRCD,IN16W950
.
IN16W900  MOVE      ZERO,EXIT
          GOTO      IN16W999
.
IN16W950  MOVE      ONE,EXIT
IN16W999  RETURN
+
.******************************************************************************
.*                                  C16TY000              Called by: PATWIS16 *
.*                 Check for valid Care Type & Incomplete Coding              *
.******************************************************************************
C16TY000  PACK     KEY5,ANSC,ANSC,ACARE     * use admission care type
          CALL     RDCODE1
          BRANCH   OVRCD,C16TY900
.
          MATCH    "6",TCINDC4              * Check Program Funding Source
          GOTO     C16TY900 IF NOT EQUAL
.
          MATCH    "0   ",THCSCOD           * check care types
          GOTO     C16TY900 IF EQUAL
          MATCH    "1   ",THCSCOD
          GOTO     C16TY900 IF EQUAL
          MATCH    "2   ",THCSCOD
          GOTO     C16TY900 IF EQUAL
          MATCH    "6   ",THCSCOD
          GOTO     C16TY900 IF EQUAL
          MATCH    "7   ",THCSCOD
          GOTO     C16TY900 IF EQUAL
          MATCH    "8   ",THCSCOD
          GOTO     C16TY900 IF EQUAL
          MATCH    "9   ",THCSCOD
          GOTO     C16TY900 IF EQUAL
          MATCH    "5   ",THCSCOD
          GOTO     C16TY900 IF EQUAL
          MATCH    "3   ",THCSCOD
          GOTO     C16TY900 IF EQUAL
          MATCH    "E   ",THCSCOD
          GOTO     C16TY900 IF EQUAL
          MATCH    "F   ",THCSCOD
          GOTO     C16TY900 IF EQUAL
          MATCH    "J   ",THCSCOD
          GOTO     C16TY900 IF EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     C16TY999
.
C16TY900  MOVE     ONE,EXIT                 * Not valid
C16TY999  RETURN
+
.******************************************************************************
.*                                  C16TR000              Called by: PATWIS16 *
.*                        Check for Contracted Patient                        *
.******************************************************************************
C16TR000  PACK     KEY25,AADMNO,ANSA,SP30
          CALL     RSPTASF1
C16TR100  CALL     RKPTASF1                 * Read adm. special funding file
          BRANCH   OVRCD,C16TR800           * still process
.
          MATCH    AADMNO,PTAFADMN
          GOTO     C16TR800 IF NOT EQUAL    * still process
.
          MATCH    ANSA,PTAFTYPE            * special fund. arrang. type = adm
          GOTO     C16TR100 IF NOT EQUAL
.
          MATCH    ANSB,PTAFCROL            * Ignore if contract role = 'B'
          GOTO     C16TR900 IF EQUAL
.
C16TR800  MOVE     ZERO,EXIT                * valid
          GOTO     C16TR999
.
C16TR900  MOVE     ONE,EXIT                 * invalid
C16TR999  RETURN
+
.******************************************************************************
.*                                  M16RG000              Called by: PATWIS16 *
.*                      Modify DRG if necessary for WIES16                    *
.******************************************************************************
M16RG000  CALL      R16IO000           * Radiotherapy
M16RG999  RETURN
+
.******************************************************************************
.*                                  R16IO000              Called by: M16RG000 *
.*                         Modify DRG if Radiotherapy                         *
.******************************************************************************
.>>>>     Don't modify these DRGs:
.
R16IO000  MATCH     "B61A",WIS16DRG
          GOTO      R16IO999 IF EQUAL
.
          MATCH     "B61B",WIS16DRG
          GOTO      R16IO999 IF EQUAL
.
          MATCH     "A40Z",WIS16DRG
          GOTO      R16IO999 IF EQUAL
.
          MATCH     "W60Z",WIS16DRG
          GOTO      R16IO999 IF EQUAL
.
          MATCH     "W61A",WIS16DRG
          GOTO      R16IO999 IF EQUAL
.
          MATCH     "W61B",WIS16DRG
          GOTO      R16IO999 IF EQUAL
.
          MATCH     "S65A",WIS16DRG
          GOTO      R16IO999 IF EQUAL
.
          MATCH     "S65B",WIS16DRG
          GOTO      R16IO999 IF EQUAL
.
          MATCH     "S65C",WIS16DRG
          GOTO      R16IO999 IF EQUAL
.
          MATCH     "B60A",WIS16DRG
          GOTO      R16IO999 IF EQUAL
.
          MATCH     "B60B",WIS16DRG
          GOTO      R16IO999 IF EQUAL
.
          MATCH     "B82A",WIS16DRG
          GOTO      R16IO999 IF EQUAL
.
          MATCH     "B82B",WIS16DRG
          GOTO      R16IO999 IF EQUAL
.
          MATCH     "B82C",WIS16DRG
          GOTO      R16IO999 IF EQUAL
.
.>>>>     Check if operation code exists:
.
          PACK      ICDRDDTE,DDATE,SP10
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
R16IO050  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,R16IO999
.
          MATCH     AADMNO,PTEOADMN         * same admission?
          GOTO      R16IO999 IF NOT EQUAL
.
.>>>>     Check if block number is in range 1786-1792, 1794 or 1795:
.
          PACK      KEY9,PTEOCODE,SP9
          CALL      RDPTICO1
          BRANCH    OVRCD,R16IO050
.
          MATCH     "1786",PT0OBLKN         * block code >= 1786?
          GOTO      R16IO050 IF LESS
.
          MATCH     "1796",PT0OBLKN         * block code <=1795?
          GOTO      R16IO050 IF NOT LESS
.
          MATCH     "1793",PT0OBLKN         * block code = 1793?
          GOTO      R16IO050 IF EQUAL
.
.>>>>     Check if disease code exists in range C00-D48:
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
R16IO100  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,R16IO999
.
          MATCH     AADMNO,PTEDADMN         * same admission?
          GOTO      R16IO999 IF NOT EQUAL
.
          MATCH     "C00",PTEDCODE          * neoplasm code >= C00?
          GOTO      R16IO100 IF LESS
.
          MATCH     "D49",PTEDCODE          * neoplasm code <= D48.9?
          GOTO      R16IO100 IF NOT LESS
.
          MOVE      "R64Z",WIS16DRG         * modified for WIES16
R16IO999  RETURN
+
.******************************************************************************
.*                                  C16RG000              Called by: PATWIS16 *
.*                            Check for Problem DRG                           *
.******************************************************************************
C16RG000  PACK     KEY4,WIS16DRG
          CALL     RDPT161                  * check WIES16 weights file
          BRANCH   OVRCD,C16RG900
.
          MATCH    "960Z",PT16DRGC
          GOTO     C16RG900 IF EQUAL
.
          MATCH    "961Z",PT16DRGC
          GOTO     C16RG900 IF EQUAL
.
          MATCH    "962Z",PT16DRGC
          GOTO     C16RG900 IF EQUAL
.
          MATCH    "963Z",PT16DRGC
          GOTO     C16RG900 IF EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     C16RG999
.
C16RG900  MOVE     ONE,EXIT
C16RG999  RETURN
+
.******************************************************************************
.*                                  M16CH000              Called by: PATWIS16 *
.*                Calculate Mechanical Ventialtion Co-payment                 *
.******************************************************************************
M16CH000  MOVE      ZERO,ADJMVDAY
          MOVE      ZERO,MVCOPAY
.
          PACK      KEY8,AADMNO
          CALL      RDPTICU1                * get mech. vent. hours
          BRANCH    OVRCD,M16CH999
.
          MATCH     "D",PT16MVEL            * check mech. vent. eligibility
          IF        @EQUAL
            IF        PTICUMEC > 6
              ASSIGN    ((PTICUMEC+12)/24),FORM12P4    * days receiving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7729)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
            GOTO      M16CH999
          ENDIF
.
          MATCH     "4",PT16MVEL
          IF        @EQUAL
            IF        PTICUMEC > 96
              ASSIGN    (((PTICUMEC+12)/24)-4),FORM12P4   * days recving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7729)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
          ENDIF
.
M16CH999  RETURN
+
.******************************************************************************
.*                                  T16AL000              Called by: PATWIS16 *
.*                      Calculate Thalessaemia Co-payment                     *
.******************************************************************************
T16AL000  MOVE      ZERO,THCOPAY
          MATCH     "T",PT16CPAY            * check WIES16 weights file
          IF        @EQUAL
            PACK      KEY16,AADMNO,SP20
            CALL      RSPTECD2              * Position on & read a patient
T16AL100    CALL      RKPTECD2                episode disease file record
            BRANCH    OVRCD,T16AL999
.
            MATCH     AADMNO,PTEDADMN
            GOTO      T16AL999 IF NOT EQUAL
.
            MATCH     "D56",PTEDCODE        * check ICD-10 code
            IF        !@EQUAL
              MATCH     "D57.2",PTEDCODE    * check ICD-10 code
              GOTO      T16AL100 IF NOT EQUAL
            ENDIF
            MOVE      "0.2648",THCOPAY      * eligible for thal. copayment
          ENDIF
T16AL999  RETURN
+
.******************************************************************************
.*                                  A16AA000              Called by: PATWIS16 *
.*                          Calculate AAA Co-payment                          *
.******************************************************************************
A16AA000  MOVE      ZERO,AAACOPAY
          MATCH     "AAA",PT16CPAY          * check WIES16 weights file
          GOTO      A16AA999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
A16AA100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,A16AA999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      A16AA999 IF NOT EQUAL
.
          MATCH     "33116-00",PTEOCODE     * check ICD-10 code
          GOTO      A16AA100 IF NOT EQUAL
.
          MOVE      "3.1421",AAACOPAY       * eligible for AAA copayment
A16AA999  RETURN
+
.******************************************************************************
.*                                  A16SD000              Called by: PATWIS16 *
.*                          Calculate ASD Co-payment                          *
.******************************************************************************
A16SD000  MOVE      ZERO,ASDCOPAY
          MATCH     "ASD",PT16CPAY          * check WIES16 weights file
          GOTO      A16SD999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
A16SD100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,A16SD999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      A16SD999 IF NOT EQUAL
.
          MATCH     "38742-00",PTEOCODE     * check ICD-10 code
          GOTO      A16SD100 IF NOT EQUAL
.
          MOVE      "2.4713",ASDCOPAY       * eligible for ASD copayment
A16SD999  RETURN
+
.******************************************************************************
.*                                  B16SW000              Called by: PATWIS16 *
.*                            Calculate Base WIES16                           *
.******************************************************************************
B16SW000  CALL      D16SC000                * length of stay category
          CALL      C16ST000                * calculate inlier status
.
          MATCH     "L",W16NLIER            * low outlier
          CALL      BW16L000 IF EQUAL       * calculate base WIES16
.
          MATCH     "I",W16NLIER            * inlier
          CALL      BW16N000 IF EQUAL       * calculate base WIES16
.
          MATCH     "H",W16NLIER            * high outlier
          CALL      BW16H000 IF EQUAL       * calculate base WIES16
.
B16SW999  RETURN
+
.******************************************************************************
.*                                  D16SC000              Called by: B16SW000 *
.*                      Determine Length Of Stay Category                     *
.******************************************************************************
D16SC000  MOVE      NBRDAYS,WIS16LOS        * length of stay from NHOSPDAY
          MOVE      "M",LOSCAT              * set LOS category to multiday
          MATCH     ADATE,DDATE             * check for sameday
          IF        @EQUAL
            MOVE      ONE,SAMEDAY           * sameday
            MOVE      ONE,WIS16LOS          * LOS = 1
            MOVE      "S",LOSCAT            * set LOS category to sameday
            GOTO      D16SC999
          ELSE
            MOVE      ZERO,SAMEDAY          * not sameday
          ENDIF
.
          IF        SAMEDAY = 0 & WIS16LOS = 1
            MOVE      "O",LOSCAT            * set LOS category to oneday
            GOTO      D16SC999
          ENDIF
.
          IF        WIS16LOS > 1825
            MOVE      "1825",WIS16LOS       * maximum LOS is 5 years
          ENDIF
.
D16SC999  RETURN
+
.******************************************************************************
.*                                  C16ST000              Called by: B16SW000 *
.*                           Calculate Inlier Status                          *
.******************************************************************************
C16ST000  MOVE      "I",W16NLIER            * set status to inlier
          IF        WIS16LOS < PT16LIBP
            MOVE      "L",W16NLIER          * set status to low outlier
            GOTO      C16ST999
          ENDIF
.
          ASSIGN    (PT16HIBP+ADJMVDAY),FORM12P4   * high bound. + mech.v days
          IF        WIS16LOS > FORM12P4
            MOVE      "H",W16NLIER          * set status to high outlier
          ENDIF
C16ST999  RETURN
+
.******************************************************************************
.*                                  BW16L000              Called by: B16SW000 *
.*                     Calculate Base WIES16 for Low Outlier                  *
.******************************************************************************
BW16L000  MATCH     "S",LOSCAT            * sameday
          IF        @EQUAL
            MOVE      PT16TSDW,TOTBWIES             * base WIES16
            ASSIGN    (TOTBWIES/PT16TIMW),TOTIES16  * IES16
.
            MOVE      PT16TSDW,WIS16WGT             * for display
            MOVE      "Same Day Wgt ",WIS16DSC
            GOTO      BW16L999
          ENDIF
.
          MATCH     "O",LOSCAT            * oneday
          IF        @EQUAL
            MOVE      PT16TODW,TOTBWIES             * base WIES16
            ASSIGN    (TOTBWIES/PT16TIMW),TOTIES16  * IES16
.
            MOVE      PT16TODW,WIS16WGT             * for display
            MOVE      "One Day Wgt  ",WIS16DSC
            GOTO      BW16L999
          ENDIF
.
          MATCH     "M",LOSCAT            * multiday
          IF        @EQUAL
            ASSIGN    (PT16TODW+((WIS16LOS-1)*PT16TLOM)),TOTBWIES
.                                                    * base WIES16
            ASSIGN    (TOTBWIES/PT16TIMW),TOTIES16   * IES16
.
            MOVE      PT16TLOM,WIS16WGT             * for display
            MOVE      "Low Outlr Wgt",WIS16DSC
            GOTO      BW16L999
          ENDIF
BW16L999  RETURN
+
.******************************************************************************
.*                                  BW16N000              Called by: B16SW000 *
.*                      Calculate Base WIES16 for Inlier                      *
.******************************************************************************
BW16N000  MOVE      ONE,TOTIES16          * IES16 = 1
          MATCH     "S",LOSCAT            * sameday
          IF        @EQUAL
            MOVE      PT16TSDW,TOTBWIES   * base WIES16
.
            MOVE      PT16TSDW,WIS16WGT             * for display
            MOVE      "Same Day Wgt ",WIS16DSC
            GOTO      BW16N999
          ENDIF
.
          MATCH     "O",LOSCAT            * oneday
          IF        @EQUAL
            MOVE      PT16TODW,TOTBWIES   * base WIES16
.
            MOVE      PT16TODW,WIS16WGT             * for display
            MOVE      "One Day Wgt  ",WIS16DSC
            GOTO      BW16N999
          ENDIF
.
          MATCH     "M",LOSCAT            * multiday
          IF        @EQUAL
            MOVE      PT16TIMW,TOTBWIES   * base WIES16
.
            MOVE      PT16TIMW,WIS16WGT             * for display
            MOVE      "Inlier Wgt   ",WIS16DSC
            GOTO      BW16N999
          ENDIF
BW16N999  RETURN
+
.******************************************************************************
.*                                  BW16H000              Called by: B16SW000 *
.*                    Calculate Base WIES16 for High Outlier                  *
.******************************************************************************
BW16H000  ASSIGN    ((WIS16LOS-PT16HIBP)-ADJMVDAY),HIGHDAYS  * high outlr days
          COMPARE   ZERO,HIGHDAYS
          IF        @LESS
            MOVE      ZERO,HIGHDAYS         * highdays must be >= zero
          ENDIF
.
          COMPARE   HIGHDAYS,HIHLOS
          IF        @LESS
            MOVE      HIHLOS,HITHDAYS       * get lesser of highdays and
          ELSE                              *  HIHLOS
            MOVE      HIGHDAYS,HITHDAYS
          ENDIF
.
          ASSIGN    (HIGHDAYS-HITHDAYS)*PT16THOM,TOTBWIES    *high WIES
          ASSIGN    (HITHDAYS*PT16THIH)+TOTBWIES,TOTBWIES    *HITH WIES
          ASSIGN    (PT16TIMW+TOTBWIES),TOTBWIES             *base WIES16
          ASSIGN    (TOTBWIES/PT16TIMW),TOTIES16             * IES16
.
          MOVE      PT16THOM,WIS16WGT                        * for display
          MOVE      "High Outl Wgt",WIS16DSC
BW16H999  RETURN
+
.******************************************************************************
.*                                  C16AB000              Called by: PATWIS16 *
.*                          Check for Aboriginality                           *
.******************************************************************************
C16AB000  MOVE      ZERO,ATSIWIES                * set ATSI WIES to zero
          IF        PNSWRACE<1 | PNSWRACE>14
            GOTO      C16AB999              * Invalid parameter
          ENDIF
.
          LOAD      TCODE,PNSWRACE,CATP1,CATP2,CATP3,CATP4,CATP5:
                                   CATP6,CATA1,CATA2,CATA3,CATA4:
                                   CATA5,CATA6,CATA7,CATVA
          LOAD      ACODE,PNSWRACE,PUSR1,PUSR2,PUSR3,PUSR4,PUSR5:
                                   PUSR6,AUSR1,AUSR2,AUSR3,AUSR4:
                                   AUSR5,AUSR6,AUSR7,PMPXABRG
.
          MATCH     ACODE,SP3
          GOTO      C16AB999 IF EQUAL
.
          PACK      KEY5,TCODE,ACODE
          CALL      RDCODE1                 * Read a code file record
          BRANCH    OVRCD,C16AB999
.
          UNPACK    THCSCOD,DIM1,KEY1       * use 2nd char of HDP Equiv
.>>>>     MOVE      THCSCOD,KEY1
          TYPE      KEY1
          GOTO      C16AB999 IF NOT EQUAL   * Non numeric, error
.
          MOVE      KEY1,FORM1
.
. ---- Calculate ATSI Loading if aboriginality = 1, 2, or 3 ----
.
          IF        FORM1=1 | FORM1=2 | FORM1=3
            MOVE      TOTBWIES,FORM12P4       * base WIES
            ADD       MVCOPAY,FORM12P4        * mechv copayment
            ADD       THCOPAY,FORM12P4        * thal  copayment
            ADD       AAACOPAY,FORM12P4       * AAA   copayment
            ADD       ASDCOPAY,FORM12P4       * ASD   copayment
            ASSIGN    (0.3*FORM12P4),ATSIWIES
          ENDIF
.
C16AB999  RETURN
+
