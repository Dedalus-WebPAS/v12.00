.******************************************************************************
.*                                  EMRACCVL                                  *
.* Validate EMRACAFD effective from and to dates for overlaps                 *
.*                                                                            *
.* Requires:   KEY38 populated with -                                         *
.*                      - ECDG Subcode
.*                      - Service Date (defaults to current date)             *
.*             VALFDATE - Effictive date from date                            *
.*             VALTDATE - Effictive date to date                              *
.*                                                                            *
.* Returns:    EXIT  0 = Valid effective from and to dates                    *
.*                   1 = Invalid effective from and to dates                  *
.*             ENDKEY48 = Key to record that requires an auto end date update *
.*                                                                            *
.* Modifications:                                                             *
.* V11.01.02  31/08/2021  Thanh T         TSK 0905305                         *
.*            Changed to check for date overlapping with ECDG code and        *
.*            mininum score instead of ECDG code                              *
.* V11.01.01  20/08/2021  Thanh T         TSK 0905305                         *
.*            Initial Created. Copy from EMRICHVL.PBL to be consistent with   *
.*            table name                                                      *
.******************************************************************************
.
RRVL0000  MOVE      SP70,ENDKEY48           * Clear auto end key
          UNPACK    KEY48,SAVKEY30,SAVKEY10
.
          CALL      RDEMACC1                * Record on file so must be an
          BRANCH    OVRCD,RRVL5000          * update
.
          MATCH     VALTDATE,EMACTDAT       * No change in effective from and
          GOTO      RRVL8000 IF EQUAL       * to dates so exit
.
RRVL5000  CALL      RSEMACC1                * Check for the same ECDG sub cat
          CALL      RPEMACC1                * with an earlier effective from
          BRANCH    OVRCD,RRVL6000          * date
.
          MATCH     SAVKEY30,EMACCODE       * Same ECDG Subcategory
          GOTO      RRVL6000 IF NOT EQUAL
.
          MATCH     SAVKEY10,EMACDMIN       * Same Minimum Score
          GOTO      RRVL6000 IF NOT EQUAL
.
          MATCH     SP70,EMACTDAT           * No effective to date prev  record
          IF        @EQUAL
            PACK      ENDKEY48,EMACCODE,EMACDMIN,EMACFDAT,SP70
                                            * auto end date record key
            GOTO      RRVL8000 
          ENDIF
.
          MATCH     VALFDATE,EMACTDAT
          IF        !@LESS
            GOTO      RRVL9000              * Effective from date overlap
          ENDIF
.
          MATCH     SP70,VALTDATE           * Blank effective to date
          GOTO      RRVL6000 IF EQUAL
.
          MATCH     VALTDATE,EMACTDAT
          IF        !@LESS
            GOTO      RRVL9000              * Effective to date overlap
          ENDIF
.
RRVL6000  CALL      RSEMACC1                * Check for the same ECDG sub cat
          CALL      RKEMACC1                * with an earlier effective from
          BRANCH    OVRCD,RRVL8000          * date
.
          MATCH     SAVKEY30,EMACCODE       * Same ECDG Subcategory
          GOTO      RRVL8000 IF NOT EQUAL
.
          MATCH     SAVKEY10,EMACDMIN       * Same Minimum Score
          GOTO      RRVL8000 IF NOT EQUAL
.
          MATCH     SP70,VALTDATE           * Blank effective to date
          GOTO      RRVL9000 IF EQUAL
.
          MATCH     EMACFDAT,VALFDATE
          IF        !@LESS
            GOTO      RRVL9000              * Effective from date overlap
          ENDIF
.
          MATCH     EMACTDAT,VALTDATE
          IF        !@LESS
            GOTO      RRVL9000              * Effective to date overlap
          ENDIF
.
RRVL8000  MOVE      ZERO,EXIT               * OK
          GOTO      RRVL9999
.
RRVL9000  MOVE      SP70,ENDKEY48
          MOVE      ONE,EXIT                * Error end date overlap
          GOTO      RRVL9999
.
RRVL9999  RETURN
