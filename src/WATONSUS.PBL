.******************************************************************************
.* System         : Waiting Lists                                             *
.*                                                                            *
.* Include name   : WATONSUS.PBL                                              *
.*                                                                            *
.* Function       : To see if a procedure is on suspension at a given date    *
.*                                                                            *
.* Author         : Justin Coates           18/02/1993                        *
.*                                                                            *
.* Parameters     : WATTR1FD recored                                          *
.*                  WTSPDTCK      the date to check if on suspension for      *
.*                                                                            *
.* Returns        : WTSPSTRT      from date of suspension                     *
.*                  WTSPSTOP      to   date of suspension                     *
.*                  EXIT = 0      suspended on given date                     *
.*                  EXIT = 1      not suspended                               *
.*                                                                            *
.* Modifications  :                                                           *
.******************************************************************************
WATONSUS  PACK      WTSPDTCK,WTSPDTCK,SP8
          MATCH     SP8,WTSPDTCK            * if no date, default to current
          IF        @EQUAL
            PACK      WTSPDTCK,CCC,CYY,CMM,CDD
            REP       " 0",WTSPDTCK
          ENDIF
          MOVE      ONE,EXIT
          PACK      KEY21,WMURNO,WMCODE,WMCNT,SP2
          CALL      RSWTSUS1
.
ONSUS100  CALL      RKWTSUS1
          BRANCH    OVRCD,ONSUS999          * not on suspension
          PACK      KEY19,WTSUURNO,WTSUCODE,WTSUCNT
          MATCH     KEY19,KEY21
          GOTO      ONSUS999 IF NOT EQUAL   * not on suspension
.
. on suspension if : susp from date <= check date <= susp to date
.
          MATCH     WTSUFDAT,WTSPDTCK
          GOTO      ONSUS100 IF LESS        * check date < start date
.
          MATCH     WTSPDTCK,WTSUTDAT
          GOTO      ONSUS100 IF LESS        * to date < check date
.
          MOVE      ZERO,EXIT               * have suspension records
          MOVE      WTSUFDAT,WTSPSTRT       * from date
          MOVE      WTSUTDAT,WTSPSTOP       * to   date
.
ONSUS999  RETURN
+
