. *----------------------------------------------------------------------
. * Include Name  :   PMSAMNFD.PBL
. *
. * Function      :   CGI functions for PMSAMNFD - pmsamnaf.dat
. * 
. * Usage         :   Requires PMSAMNFD - Open on index 1
. *                            PMSAMNIO
. *                            PMSAMNTD
. *                            PMSAMNTM
. * 
. *                   CLRPAR00 CALL CPPMAM00
. *
. *                   SETPAR00 
. *
. *                   MATCH     "pmamcatg",QRYNAME
. *                   IF        @EQUAL
. *                     CALL      SPMCAT00
. *                     GOTO      SETPAR99
. *                   ENDIF
. *
. *                   MATCH     "pmamcode",QRYNAME
. *                   IF        @EQUAL
. *                     CALL      SPMCOD00
. *                     GOTO      SETPAR99
. *                   ENDIF
. *
. *                   Add, Update or delete CALL MVPMAM00
. *
. *                   PROFLD tag CALL AMAITM00
. *----------------------------------------------------------------------
.  Modifications Done :
.
.  V10.03.00  04/09/2013  Ebon Clements     CAR 275205
.             Created include
. *----------------------------------------------------------------------
.------------------------------------------------------------------------
. Clear cgi parameter array for patient needs fields
.------------------------------------------------------------------------
CPPMAM00  MOVE      ONE,PMCATCNT
          WHILE     PMCATCNT <= 20
            PACK      PMCATARR[PMCATCNT],SP70
            ADD       ONE,PMCATCNT
          DO
          MOVE      ZERO,PMCATCNT
.
          MOVE      ONE,PMCODCNT
          WHILE     PMCODCNT <= 200
            PACK      PMCODARR[PMCODCNT],SP70,SP70,SP70
            ADD       ONE,PMCODCNT
          DO
          MOVE      ZERO,PMCODCNT
.
CPPMAM99  RETURN
.
.------------------------------------------------------------
. Set cgi parameters array for patient needs category fields
.------------------------------------------------------------
SPMCAT00  ADD       ONE,PMCATCNT
          IF        PMCATCNT>20
            GOTO      SPMCAT99
          ENDIF
.
          PACK      PMCATARR[PMCATCNT],QRYDATA,SP70
.
SPMCAT99  RETURN
.
.------------------------------------------------------------
. Set cgi parameters array for patient needs category and code fields
.------------------------------------------------------------
SPMCOD00  ADD       ONE,PMCODCNT
          IF        PMCODCNT>200
            GOTO      SPMCOD99
          ENDIF
.
          PACK      PMCODARR[PMCODCNT],QRYDATA,SP70,SP70,SP70
.
SPMCOD99  RETURN
.
.------------------------------------------------------------
. Update patient needs coding fields
.------------------------------------------------------------
MVPMAM00  COMPARE   ZERO,PMCATCNT                * No records in category
          GOTO      MVPMAM99 IF EQUAL            * array
.
          MOVE      ZERO,F3
.
MVPMAM10  ADD       ONE,F3
          IF        F3>PMCATCNT 
            GOTO      MVPMAM50                   * Finished category array
          ENDIF
.
          UNPACK    PMCATARR[F3],PMSAMNCT
          MOVE      PMARREQN,PMSAMNKY
.
          PACK      KEY15,PMSAMNKY,PMSAMNCT,SP70
MVPMAM15  CALL      RSPMAMN1
          CALL      RKPMAMN1                     * Delete records before
          BRANCH    OVRCD,MVPMAM10               * update
.
          MATCH     PMSAMNKY,PMAMREQN            * Correct link key
          GOTO      MVPMAM10 IF NOT EQUAL
.
          MATCH     PMSAMNCT,PMAMCATG            * Correct link category
          GOTO      MVPMAM10 IF NOT EQUAL
.
          PACK      KEY15,PMAMREQN,PMAMCATG,PMAMCODE,SP70
          CALL      DEPMAMN1
          GOTO      MVPMAM15
.
MVPMAM50  COMPARE   ZERO,PMCODCNT                * No records in category
          GOTO      MVPMAM99 IF EQUAL            * code array
.
          MOVE      ZERO,F3
.
MVPMAM60  ADD       ONE,F3
          IF        F3>PMCODCNT
            GOTO      MVPMAM99
          ENDIF
.
          CALL      CLPMSAMN
.
          UNPACK    PMCODARR[F3],PMAMCATG,PMAMCODE,PMAMCOMM
          MOVE      PMARREQN,PMSAMNKY
          MOVE      PMSAMNKY,PMAMREQN
.
          MATCH     SP70,PMAMCODE                    * Skip blank codes
          GOTO      MVPMAM60 IF EQUAL
.
          MOVE      USERID,PMAMCUID
          PACK      PMAMCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMAMCDAT
          CLOCK     TIME,PMAMCTIM
.
          PACK      KEY15,PMAMREQN,PMAMCATG,PMAMCODE,SP70
          CALL      RAPMAMN1
          IF        OVRCD=1
            CALL      WRPMAMN1
          ENDIF       
.
          GOTO      MVPMAM60
.
MVPMAM99  RETURN
.
.------------------------------------------------------------
. Clear patient needs fields
.------------------------------------------------------------
CLPMSAMN  MOVE      SP70,PMAMREQN
          MOVE      SP70,PMAMURNO
          MOVE      SP70,PMAMVISN
          MOVE      SP70,PMAMCATG
          MOVE      SP70,PMAMCODE
          MOVE      SP70,PMAMCOMM
          MOVE      SP70,PMAMCDAT
          MOVE      SP70,PMAMCTIM
          MOVE      SP70,PMAMCUID
          MOVE      SP70,PMAMSPAR
.
          RETURN
.
.------------------------------------------------------------
. Tags to output patient needs details
. Usage - XXXWEB01.XX.XXX.AA.B
.
. Tag Parameter 
.          AA = Link Category
.          B  = 1 - Check box option list - Short Description
.               2 - Check box option list - Long Description
.               3 - Selecteion list       - Short Description
.               4 - Selecteion list       - Long Description
.------------------------------------------------------------
AMAITM00  
          UNPACK    DIM127,D1,PMSAMNCT,DIM127        * Link Category
.
          UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
.
          MOVE      PMARREQN,PMSAMNKY
.
          BRANCH    F1,AMAITM10,AMAITM20,AMAITM30,AMAITM40
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      AMAITM99
.
AMAITM10  CALL      AMABOX00              
          GOTO      AMAITM99
.
AMAITM20  CALL      AMABOX00
          GOTO      AMAITM99
.
AMAITM30  CALL      AMASLS00
          GOTO      AMAITM99
.
AMAITM40  CALL      AMASLS00
          GOTO      AMAITM99
.
AMAITM99  RETURN
.
.------------------------------------------------------------
. Add check box options
.------------------------------------------------------------
AMABOX00  WRITE     HTMLFILE;"<input type=#"hidden#" name=#"pmamcatg#"":
                             " value=#"",PMSAMNCT,"#">"
.
          PACK      PTCDKEY2,PMSAMNCT,SP70
          CALL      RDSCODE2
AMABOX10  CALL      RDKCODE2
          BRANCH    OVRCD,AMABOX99
.
          MATCH     PMSAMNCT,TCODE
          GOTO      AMABOX99 IF NOT EQUAL
.
          MATCH     SP70,ACODE
          GOTO      AMABOX10 IF EQUAL
.
          PACK      KEY7,TCODE,ACODE,SP70
.
          MOVE      PMARREQN,PMSAMNKY
.
          PACK      KEY15,PMSAMNKY,TCODE,ACODE,SP70
          CALL      RDPMAMN1
          IF        OVRCD<>1
            MATCH     ANSO,TCINDC12
            IF        @EQUAL
              WRITE     HTMLFILE;"<input type=#"checkbox#" name=#"pmamcode#" ":
                                 "id=#"",ANSC,KEY7,"#" ":
                                 "onclick=#"VisCodToggle('",KEY7,"');#" ":
                                 "value=#"",TCODE,ACODE,PMAMCOMM,"#" ":
                                 "checked>";
.
              IF        F1=1
                WRITE     HTMLFILE;TDESC;
              ENDIF
              IF        F1=2
                WRITE     HTMLFILE;PTCDLDES;
              ENDIF
              WRITE     HTMLFILE;"<br>"
.
              WRITE     HTMLFILE;"<input type=#"text#" title=#"",TDESC,"#" ":
                                 "name=#"d_pmamcode#" class=#"Mandatory#" ":
                                 "id=#"",ANST,KEY7,"#" ":
                                 "onblur=#"VisCodField('",KEY7,"');#" ":
                                 "value=#"",PMAMCOMM,"#" ":
                                 "size=80 maxlength=200><br> "
            ELSE
              WRITE     HTMLFILE;"<input type=#"checkbox#" name=#"pmamcode#" ":
                                 "value=#"",TCODE,ACODE,"#" checked>";
              IF        F1=1
                WRITE     HTMLFILE;TDESC;
              ENDIF
              IF        F1=2
                WRITE     HTMLFILE;PTCDLDES;
              ENDIF
              WRITE     HTMLFILE;"<br>"
            ENDIF
          ELSE
            MATCH     "I",PTCOACTV
            GOTO      AMABOX10 IF EQUAL
.
            IF        IBCNMHOS=1
              MATCH    "1",PTCDHOSS
              IF       @EQUAL
                PACK     KEY8,WBSEHOSP,TCODE,ACODE,SP70
                CALL     RDMLCOD1
                BRANCH   OVRCD,AMABOX10
              ENDIF
            ENDIF
.
            MATCH     ANSO,TCINDC12
            IF        @EQUAL
              WRITE     HTMLFILE;"<input type=#"checkbox#" name=#"pmamcode#" ":
                                 "id=#"",ANSC,KEY7,"#" ":
                                 "onclick=#"VisCodToggle('",KEY7,"');#" ":
                                 "value=#"",TCODE,ACODE,PMAMCOMM,"#">";
              IF        F1=1
                WRITE     HTMLFILE;TDESC;
              ENDIF
              IF        F1=2
                WRITE     HTMLFILE;PTCDLDES;
              ENDIF
              WRITE     HTMLFILE;"<br>"
.
              WRITE     HTMLFILE;"<input type=#"text#" title=#"",TDESC,"#" ":
                                 "name=#"d_pmamcode#" disabled ":
                                 "style=#"display:none#" ":
                                 "id=#"",ANST,KEY7,"#" ":
                                 "onblur=#"VisCodField('",KEY7,"');#" ":
                                 "value=#"",PMAMCOMM,"#" ":
                                 "size=80 maxlength=200><br> "
            ELSE
              WRITE     HTMLFILE;"<input type=#"checkbox#" name=#"pmamcode#"":
                                 " value=#"",TCODE,ACODE,"#">";
              IF        F1=1
                WRITE     HTMLFILE;TDESC;
              ENDIF
              IF        F1=2
                WRITE     HTMLFILE;PTCDLDES;
              ENDIF
              WRITE     HTMLFILE;"<br>"
            ENDIF
          ENDIF
          GOTO      AMABOX10
.
AMABOX99  RETURN
.
.------------------------------------------------------------
. Add Selection options to 
.------------------------------------------------------------
AMASLS00  WRITE     HTMLFILE;"<input type=#"hidden#" name=#"pmamcatg#"":
                             " value=#"",PMSAMNCT,"#">"
.
          PACK      KEY4,TCODE,SP70
          WRITE     HTMLFILE;"<select name=#"pmamcode#" ":
                             "id=#"",ANSS,KEY4,"#">"
.
          WRITE     HTMLFILE;"<option ":
                             "value=#"",PMSAMNCT,SP3,"#">":
                             "</option>";
.
          PACK      PTCDKEY2,PMSAMNCT,SP70
          CALL      RDSCODE2
AMASLS10  CALL      RDKCODE2
          BRANCH    OVRCD,AMASLS90
.
          MATCH     PMSAMNCT,TCODE
          GOTO      AMASLS90 IF NOT EQUAL
.
          MATCH     SP70,ACODE
          GOTO      AMASLS10 IF EQUAL
.
          PACK      KEY15,PMSAMNKY,TCODE,ACODE,SP70
          CALL      RDPMAMN1
          IF        OVRCD<>1
            WRITE     HTMLFILE;"<option ":
                               "value=#"",TCODE,ACODE,"#" selected>";
            IF        F1=3
              WRITE     HTMLFILE;TDESC;
            ENDIF
            IF        F1=4
              WRITE     HTMLFILE;PTCDLDES;
            ENDIF
            WRITE     HTMLFILE;"</option>"
          ELSE
            MATCH     "I",PTCOACTV
            GOTO      AMASLS10 IF EQUAL
.
            IF        IBCNMHOS=1
              MATCH    "1",PTCDHOSS
              IF       @EQUAL
                PACK     KEY8,WBSEHOSP,TCODE,ACODE,SP70
                CALL     RDMLCOD1
                BRANCH   OVRCD,AMASLS10
              ENDIF
            ENDIF
.
            WRITE     HTMLFILE;"<option ":
                               "value=#"",TCODE,ACODE,"#">";
.
            IF        F1=3
              WRITE     HTMLFILE;TDESC;
            ENDIF
            IF        F1=4
              WRITE     HTMLFILE;PTCDLDES;
            ENDIF
            WRITE     HTMLFILE;"</option>"
          ENDIF
          GOTO      AMASLS10
.
AMASLS90  WRITE     HTMLFILE;"</select>"
.
AMASLS99  RETURN
.

