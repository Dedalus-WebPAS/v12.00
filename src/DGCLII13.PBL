.************************************************************************
.*  Procedure: DGCLII13  -  Send an Allied Health Referral update       *
.*                                                                      *
.*  Requires : PATMA1FD (Patient Master & Extension)                    *
.*             PMSPX2FD (Patient Master Extn.2)                         *
.*             ALLREFFD (Allied Health Referral)                        *
.*                                                                      *
.*  Mods     :                                                          *
.*                                                                      *
.************************************************************************
.*  V11.04.01 09/01/2024 Thanh T           TSK 0936263                  *
.*            Added OPEN/CLOSE PMSQPXA1 index                           *
.************************************************************************
.*  V10.08.01 09/08/2016  Ebon Clements    TSK 0820569                  *
.*            Set CISMFLAG to 1                                         *
.*  V10.03.02 30/06/2013  Davin S          CAR 279183                   *
.*            Added h7ciutim (Allref Update Time) to spare in hl7cisaf  *
.*  V10.03.01 17/04/2013  Davin S          CAR 280425                   *
.*            Changed IBCNUCIS - value 1 or greater will write trigger  *
.*  V10.00.00 19/03/2010  Steve Armstrong   CAR 135505                  *
.*            Added setting of H7CITRID & H7CIINCL                      *
.*            01/02/2010  Steve Armstrong  CAR 135505                   *
.*            Created include                                           *
.************************************************************************
.
DGCLII13  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,HUND06;*219,ALCNI13L
.
          MATCH     "1",IBCNUCIS                 * using HL7 messaging ?
          GOTO      DGI13999 IF LESS             * no - finished
.
.         Check if we are using this feature
.
          MATCH     "1",ALCNI13L                 * sending I13 messages ?
          GOTO      DGI13999 IF NOT EQUAL        * no - finished
.
.         Open the relevant broadcast message holding tables
.
          OPEN      HL7CISA1,"hl7cisaf"
          OPEN      PMSQPTA1,"pmsqptaf"
          OPEN      PMSQPXA1,"pmsqpxaf"
          OPEN      ALLQUEA1,"allqueaf"
.
          MOVE      ONE,CISMFLAG                 * yes- set flag for CIS message
.
          CALL      DGMESSID                     * get the unique message id
.
.         Write a record to pmsqptaf (holding patient details)
.
          MOVE      MESSAGID,PMQPMESI
          MOVE      SP8,PMQPOURN
          PACK      PMQPSPAR,SP70,SP70
          CALL      WRPMQPT1
.
.         Write a record to allqueaf (holding referral details)
.
          MOVE      MESSAGID,ALQUMESI
          PACK      ALQUSPAR,SP70
          CALL      WRALQUE1
.
.         Write header record (hl7cisaf)
.
          MOVE      "I13",H7CIMETP
          MOVE      ALREVISN,H7CIVISN
          MOVE      HL7TRGID,H7CITRID
          MOVE      HL7INCLD,H7CIINCL
          MOVE      ALREUTIM,H7CIUTIM       * allref update time to spare
          CALL      WRCIS000
.
          CLOSE     HL7CISA1
          CLOSE     PMSQPTA1
          CLOSE     PMSQPXA1
          CLOSE     ALLQUEA1
.
DGI13999  RETURN
+
