.************************************************************************
.*  Procedure: DGCLICAE  -  Send Emergency Visit Details                *
.*                                                                      *
.*  Author   : Davin Sloan      (04/01/2002)                            *
.*                                                                      *
.*  Mods     :                                                          *
.*                                                                      *
.************************************************************************
.*  V11.04.01 09/01/2024 Thanh T           TSK 0936263                  *
.*            Added OPEN/CLOSE PMSQPXA1 index                           *
.************************************************************************
.*  V10.13.01 24/10/2018  Steve Armstrong   TSK 0863512                 *
.*            Mods to write to pmsqviaf.                                *
.************************************************************************
.*  V10.11.01 28/12/2017  Ania P            TSK 0848601                 *
.*            Changed IBCNUCIS comparison                               *
.************************************************************************
.*  V10.03.01 17/04/2013  Davin S           CAR 280425                  *
.*            Changed IBCNUCIS - value 1 or greater will write trigger  *
.************************************************************************
.*  V10.02.01 05/06/2011  Steve Armstrong   CAR 240692                  *
.*            Mods to populate H7CIRTAG & H7CISTAG                      *
.************************************************************************
.*  V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                  *
.*            Added setting of H7CITRID & H7CIINCL                      *
.************************************************************************
.*        V9.05.01  03/05/2002  Steve Armstrong     CAR 88829           *
.*                  Added HL7 messages                                  *
.************************************************************************
.*        V9.02.01  03/05/2002  Steve Armstrong     Defect No. 3681     *
.*                  Mods to not send zero U/R records                   *
.*                                                                      *
.************************************************************************
          DEFPROC   DGCLICAE
.
          INC       EMRQUEFD/INC
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
          INC       PMSQPTFD/INC
          INC       PMSQVIFD/INC
.           
CISMFLAG  FORM      1             * CIS message flag
.                                    0 = don't send HL7 CIS message
.                                    1 = send HL7 CIS message
IBAMFLAG  FORM      1             * IBA proprietary message flag
.                                    0 = don't send IBA proprietary message
.                                    1 = send IBA proprietary message
MESSAGID  DIM       20
.
DGCLI000  MATCH     ZEROUR,EMVIURNO              * "0" U/R ?
          GOTO      DGCLI999 IF EQUAL            * yes - don't send
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,EIGHTY9;*177,EMCNDGAE:   * sending CAE
                                     *200,EMCNA04M    * sending A04
.
          MOVE      ZERO,CISMFLAG                * set default interface flag
          MATCH     "1",IBCNUCIS                 * using CIS interface ?
          IF        !@LESS
            COMPARE   ONE,EMCNA04M               * yes - sending A04 ?
            IF        !@LESS
              MOVE      ONE,CISMFLAG             * yes- set flag for CIS message
            ENDIF
          ENDIF
.
.         Check if we are sending any messages.
.
          IF        EMCNDGAE <> 1 & CISMFLAG <> 1
            GOTO      DGCLI999
          ENDIF
.
.         We are sending IBA Proprietary and/or CIS HL7 messages, so
.         first open the relevant holding tables
.
          MOVE      ZERO,IBAMFLAG
          IF        EMCNDGAE = 1
            OPEN      HL7INB01,"hl7inbaf"
            MOVE      ONE,IBAMFLAG
          ENDIF
          IF        CISMFLAG = 1
            OPEN      HL7CISA1,"hl7cisaf"
          ENDIF
          OPEN      PMSQPTA1,"pmsqptaf"
          OPEN      PMSQPXA1,"pmsqpxaf"
          OPEN      PMSQVIA1,"pmsqviaf"
          OPEN      EMRQUEA1,"emrqueaf"
.
          CALL      DGMESSID                    * Get Message ID, Date & Time
.
.         Write a record to pmsqptaf (holding patient details)
.
          MOVE      MESSAGID,PMQPMESI
          MOVE      SP8,PMQPOURN
          PACK      PMQPSPAR,SP30,SP30,SP20
          CALL      WRPMQPT1
.
.         Write a record to pmsqviaf (holding visit details)
.
          MOVE      MESSAGID,PMQVMESI
          MOVE      ONE,PMQVTRAN
          PACK      PMQVSPAR,SP70,SP30
          CALL      WRPMQVI1
.
.         Write a record to emrqueaf (holding visit details)
.
          MOVE      MESSAGID,EMQUMESI
          PACK      EMQUSPAR,SP30,SP30,SP30
          CALL      WREMQUE1
.
          COMPARE   ONE,CISMFLAG                 * sending CIS HL7 A04 message?
          IF        @EQUAL
            MOVE      "A04",H7CIMETP
            MOVE      HL7TRGID,H7CITRID
            MOVE      HL7INCLD,H7CIINCL
            MATCH     "HL7REGEN",PRGID
            IF        @EQUAL
              MOVE      HL7RCTAG,H7CIRTAG
              MOVE      HL7SFTAG,H7CISTAG
            ELSE
              MOVE      SP20,H7CIRTAG
              MOVE      SP3,H7CISTAG
            ENDIF
            CALL      WRCIS000                   * yes - write hl7cisaf record
          ENDIF
.
          COMPARE   ONE,EMCNDGAE                 * sending proprietary message?
          IF        @EQUAL
            MOVE      "CAE",H7INMETP
            CALL      WRINB000                   * yes - write hl7inbaf record
          ENDIF
.
          CLOSE     HL7INB01
          CLOSE     HL7CISA1
          CLOSE     PMSQPTA1
          CLOSE     PMSQPXA1
          CLOSE     PMSQVIA1
          CLOSE     EMRQUEA1
.
DGCLI999  GOTO      DGCLIEND                    * end procedure
.
.         IO's go in here
.
          INC       BRHLCOMN
          INC       CLPMSQPX
.
          INC       EMRQUEIO/INC
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       IBAOVRIO/INC
          INC       PMSQPTIO/INC
          INC       PMSQVIIO/INC
.
DGCLIEND  ENDPROC
