.**************************************************************************
.*  DGADTDIS  :  Discharge a patient  from a DataGate request             *
.*               ~~~~~~~~~~~~~~~~~~~                                      *
.*                                                                        *
.*  Author    :  ROD   (19/10/95)                                         *
.*                                                                        *
.*  Mods      :                                                           *
.*        V12.00.01 14/05/2025  J.Tan          TSK 0955096                *
.*                  Added alpanumeric visit number generation             *
.*        V10.02.01 25/06/2011 Steve Armstrong    CAR 240722              *
.*                  Mods to cater for changes to PATLOCFD.                *
.*                       - LSNAME & LGNAME extended to 40 chars.          *
.*                       - PTLCGNM2 added.                                *
.**************************************************************************
.*        V10.01.02 02/02/2011  Steve Armstrong   CAR 237520              *
.*                  Mods to remove Detente interface (PATDETNT)           *
.*        V10.01.01 01/12/2010  Peter Vela        CAR 233148              *
.*                  Initialised pattranf variables to spaces before write *
.**************************************************************************
.*        V9.08.01  06/10/2006 J.Tan  CAR 119972                          *
.*                  Mods CFEETYP=5 for NZ Private                         *
.*        V9.04.01  04/08/2005  J.Tan        CAR 62750                    *
.*                  Mods not to use the redefined amount                  *
.*        V9.02.06  02/02/2002 Ebon Clements                              *     
.*                  Added WATHI002 cgi for WLHISSUB effective date set    *
.*        V9.02.05  24/01/2002 Ebon Clements                              *     
.*                  Added ADDWLFLG for waiting list effective date set    *
.*        V9.02.04  16/01/2002 Sai SRF 14169                              *     
.*                  Removed PATMDIFD/IO & PATMOPFD/IO                     *
.*        V9.02.03  20.11.2001  B.G.Ackland                               *
.*                  Remove pi's from Program                              *
.*        V9.02.02  24.10.2001  B.G.Ackland                               *
.*                  Remove pi from CFRDTE & CMMHDT Control File Update    *
.*        V5.08.01  21/08/2000  Jill Habasque                             *
.*                  Added variables for patdetnt                          *
.*        V5.08.B02 25/02/2000  Greg Horvat                               *
.*                  Removed PATHCSX1 PATHX1IO & HCSDIAG                   *
.*        V5.08.B01 25/02/2000  Greg Horvat                               *
.*                  Removed PATADJFD PATADJIO PATMMRFD PATMMRIO & PATPRADJ*
.*                  25/02/2000  Greg Horvat      SRF 637565               *
.*                  Recompiled for PATDYCAS - Changes for 4 character DRGs*
.*            V5.07.04 29/11/1999  Davin                                  *
.*                     Changed for 4 character drgs                       *
.*            V5.07.03 21/07/1999  Steve Downing                          *
.*                     Included century in date calculations              *
.*            V5.07.02 22/12/1998  Davin                                  *
.*                     Y2K mods                                           *
.*            V5.07.01 17/11/1998  Jim Stathopoulos                       *
.*                     Removed the HCS Discharge                          *
.*                     Data Audit Files                                   *
.*            V5.07.00 21/10/1998  Davin                                  *
.*                     Mods for 507 changes                               *
.*            V5.05.01 16/01/1998  Steve Armstrong   SRF 642548 (AUS)     *
.*                     Mods for new discharge variables                   *
.**************************************************************************
.*            V5.04.02 07/07/1997  Steve Armstrong                        *
.*                     Mods to use DDCENT instead of CCC.                 *
.*            V5.04.01 17/04/1997  Steve Armstrong                        *
.*                     Mods to use inbound & outbound comms client        *
.*                     (DGATEIN & DGATEOUT).                              *
.*                                                                        *
.**************************************************************************
          DEFPROC   DGADTDIS
 IFGE     $$VER,7
.
          INC       PATDAYFD/INC
          INC       WATTR1FD/INC
          INC       WATHISFD/INC
          INC       PATSTAFD/INC
          INC       MEHDS1FD/INC
          INC       MEHVI1FD/INC
          INC       PATIN1FD/INC
          INC       OUTCLIFD/INC
          INC       PATADBFD/INC
          INC       PATNUMFD/INC
          INC       PATDADFD/INC
          INC       PATAXEFD/INC
          INC       PATNOBFD/INC
          INC       PATALRFD/INC
          INC       PATMRGFD/INC
          INC       PATLOCFD/INC
          INC       PATONLFD/INC
          INC       PATDO1FD/INC
.
.
ADDWLFLG  FORM      1
WATHI002  DIM       8      * Date of change
ACTION    DIM       1
ADD       DIM       2
ADMISNO   DIM       8
AMM       DIM       2
AUDIFLAG  FORM      1
AUTOPSYI  FORM      1          * autopsy indicator passed in message 0=No,1=Yes
AYY       DIM       2
CALDAYS   FORM      4
CMDLINE   DIM       50
CODE      DIM       2
CODEBC    INIT      "BC"
CODECL    INIT      "CL"
CODED     INIT      "D"
CODEUNK   DIM       3
COUNT     FORM      3
CPTDATE   DIM       8
DCEASE    DIM       3
DETCADES  DIM       20
DETCODES  DIM       20
DETNTREC  FORM      2
DIM14     DIM       14
DIM14A    DIM       14
DIM1A     DIM       1
DIM20     DIM       20
DIM5      DIM       5
DJULCCYR  DIM       4
DSECONDS  DIM       2
ENDDATE   DIM       8
EOF       FORM      "-3"
FORM3     FORM      3
FORM4     FORM      4
FORM6     FORM      6
FORM62    FORM      6.2
FORM6P2   FORM      6.2
FROMDATE  DIM       8
HINVDES   DIM       30
HOURS     FORM      2
HSTANDM   FORM      1
JULCCYR   FORM      4
JYEAR     FORM      2
MINUTES   FORM      2
NBRDAYS   FORM      4
NEWBED    DIM       3
NEWWARD   DIM       3
NOFEE     FORM      "0"
NUMBER    FORM      3
OLDBED    DIM       3
OLDCEASE  FORM      1
OLDWARD   DIM       3
ONAME     DIM       30
ONLEAVE   FORM      1
OPERCODE  DIM       4
PATJAUD   SFILE    ASCII, FIXED=300
PIPE      INIT      "|"
PRTITCOD  DIM       9
RECTYPEA  INIT      "A"
SAVDECDT  DIM       8
SAVPAUTO  FORM      1
SAVPCEAS  FORM      1
SAVTDATE  DIM       8
SAVTMOVE  DIM       1
SAVTTIME  DIM       6
SBBED     DIM       3
SBWARD    DIM       3
SDCEASE   DIM       3
SEC1      FORM      "00001"
SECONDS   FORM      2
SMONDAY   DIM       4
STBYPAT   DIM       8
STRTDATE  DIM       8
SUPRSQST  FORM      1
SYEAR     DIM       4
TFRSRCE   DIM       5
THCFLG    FORM      2
THISYEAR  DIM       4
TIMEIS    DIM       8
TMPDTCAT  DIM       2
TMPDTCOD  DIM       3
TMPDTACT  DIM       1                    * detente temporary action
TMPDVIST  DIM       1                    * detente temporary visit type
TMPDVISN  DIM       8                    * detente temporary visit number
TMPDDATE  DIM       8                    * detente temporary visit date
TODATE    DIM       8
TYEARR    DIM       4
WDATE     DIM       8
WDATE1    DIM       8
WDATE2    DIM       8
WORKDATE  DIM       8
+
.*************************************************************************
.*  DGDIS000  :  Process datagate Discharge message                      *
.*************************************************************************
DGADTDIS  MOVE      "DSC",RPLYHEAD
          CALL      DSOPN000                      * open required files
.
          READ      DGATEOUT;AADMNO,DDATE,DTIME,DSTAT,DDEST,DDIAG:
                          DDIAG2,DUSD1,DUSD2,DUSD3,DUSD4,DUSD5,DFMBS,PTDSDMDC:
                          PTDSDDRG,DWLREASN,PTDSSIDX,PTDSVOGU,PTDSOPER,PTDADCTS:
                          AUTOPSYI
.
          CALL      VALID000                      * read pat. details & validate
          BRANCH    EXIT,DGADI900                 * Error!  Patient not valid
.
          CALL      CDEAD000                      * check if discharged dead
.
. we have a valid patient so do the discharge
.
          CALL      DIMEH000                      * update Mental Health stuff
          CALL      DIHMS000                      * subtract from MHMS adjustmen
          CALL      DIWRD000                      * update ward stuff
          CALL      DIDIS000                      * write discharge record
          CALL      DIAXE000                      * update admission extension
          CALL      DIDAD000                      * upd adm/disch transfer destn
          CALL      PATD0000                      * update the patdayaf files
          CALL      DIADS000                      * write admiss/disch stats
          CALL      DIDAU000                      * write discharge audit
          CALL      DITRN000                      * write transfer file record
          CALL      DINUM000                      * update ward usage file
          CALL      DIMIS000                      * update admission record
          CALL      DIBOK000                      * update bookings/waiting list
          CALL      PATWLDSC                      * Put patient back on W/L?
          CALL      DIMAS000                      * update master file
.
          SUSPEND
          PROC      PATDEATH                      * update files if dead
          FLUSH     1,24,80,24
.
          CALL      DICON000                      * update control file
          CALL      DILOC000                      * delete patient location rec
          CALL      DIHMA000                      * add pat to MHMS adjustments
          CALL      DIRAT000                      * change rates if day case
          CALL      DIUNL000                      * unlock admission & master
          CALL      DISBY000                      * check for a stand-by patient
.
. send reply message as successful
.
          MOVE      "00000",RPLYCODE              * successful message
          MOVE      SP50,RPLYDESC                 * clear error description
          PACK      CCENCODE,CCDATE,CCDST,CCSRC,CCNAME,REPLYRPL
          WRITE     DGATEIN;CCENCODE:
                            RPLYHEAD,RPLYCODE,RPLYDESC:
                            DGMISC1,DGMISC2,DGMISC3
.
          ADD       ONE,SUCCPROC                  * increm # success transactns
          GOTO      DGADI999
.
. --- send back an Error response ---
.
DGADI900  PACK      CCENCODE,CCDATE,CCDST,CCSRC,CCNAME,REPLYRPL
          WRITE     DGATEIN;CCENCODE:
                            RPLYHEAD,RPLYCODE,RPLYDESC:
                            DGMISC1,DGMISC2,DGMISC3
          ADD       ONE,ERRRPROC                  * increm # error transactions
.
DGADI999  GOTO      ENDPROC1
+
.*************************************************************************
.*  DSOPN000  :  open required files                                     *
.*************************************************************************
DSOPN000  OPEN      PATONLV1,"patonlvf"
          OPEN      PATNOBE1,"patnobef"
          IF        PTCNAXEU=1
            OPEN      PATAXEA1,"pataxeaf"
          ENDIF
          IF        CAUDB<>1
            OPEN      PATBAUD,"patbaud"
          ENDIF
.
DSOPN999  RETURN
+
.***************************************************************************
.*  VALID000  :  read patients details & make sure we have a valid patient *
.*               Returns:  EXIT=0 --> Valid patient,   EXIT=1 --> ERROR    *
.***************************************************************************
VALID000  MOVE      AADMNO,DADMNO
          MOVE      AADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,VALID800                * no admission details
.
          COMPARE   TWO,ASTAT                     * make sure pat is admitted
          GOTO      VALID830 IF NOT EQUAL
.
          MOVE      AADMNO,KEY8
          CALL      RDVISA1                       * no visit details
          BRANCH    OVRCD,VALID820
.
          MOVE      "0",PCEASE
          MOVE      AURNO,DURNO
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,VALID850                * no master details
          CALL      RDPMPX21
          MOVE      PCEASE,OLDCEASE
.
          MOVE      ZERO,EXIT                     * we have a valid patient
          GOTO      VALID999
.
VALID800  MOVE      "Missing Admission record",RPLYDESC
          MOVE      "02100",RPLYCODE
          MOVE      ONE,EXIT
          GOTO      VALID999
.
VALID820  MOVE      "Missing Visit record",RPLYDESC
          MOVE      "02101",RPLYCODE
          MOVE      ONE,EXIT
          GOTO      VALID999
.
VALID830  MOVE      "Patient is not currently admitted",RPLYDESC
          MOVE      "02113",RPLYCODE
          MOVE      ONE,EXIT
          GOTO      VALID999
.
VALID850  MOVE      "Patient Master details not found",RPLYDESC
          MOVE      "01100",RPLYCODE
          MOVE      ONE,EXIT
          GOTO      VALID999
.
VALID999  RETURN
+
.**************************************************************************
.*  CDEAD000  :  set fields if patient is discharged dead                 *
.**************************************************************************
CDEAD000  PACK      KEY5,ANSD,SP1,DSTAT
          CALL      RDCODE1
          BRANCH    OVRCD,CDEAD999
.
          MATCH     ANSD,TCINDC1                   * 1st Indic 'D' = Dead
          GOTO      CDEAD999 IF NOT EQUAL
.
          MOVE      ONE,PCEASE                     * deceased indicator
          PACK      PDECDTE,DDATE                  * deceased date
          MOVE      AUTOPSYI,PAUTOPY               * autopsy indic in message
.
CDEAD999  RETURN
+
.**************************************************************************
.*  DIMEH000  :  update Mental Health stuff                               *
.**************************************************************************
DIMEH000  COMPARE   ONE,MHCNUSE
          GOTO      DIMEH999 IF NOT EQUAL         * using Mental Health system?
.
          OPEN      MEHVI1A1,"mehvi1af"                      * open MH visit file
          OPEN      MEHDS1A1,"mehds1af"                      * open MH discharge file
.
          MOVE      AADMNO,KEY8
          CALL      RDMHVIS1
          BRANCH    OVRCD,DIMEH900                * not a mental health patient
.  
.         write to meh discharge file
.
          MOVE      DADMNO,KEY8
          CALL      DEMHDSC1
          MOVE      DADMNO,MHDSADMN
          CALL      WRMHDSC1
          MOVE      ONE,AUDIFLAG                  * add audit
          CALL      MHAUDDSC                      * write audit
.
.         update status on visit file
.
          MOVE      SIX,MHVISTAT                  * set as discharged
          CALL      UPMHVIS1                      * update visit file
          MOVE      THREE,AUDIFLAG                * after change audit
          CALL      MHAUDVIS                      * write audit
.
          CALL      UPMAST1                       * update master file
.....     COMPARE   OLDCEASE,PCEASE               * deceased indicator changed?
.....     IF        !@EQUAL & (PCEASE=1 | OLDCEASE=1)
.....       PROC      PRADEATH                    * Option to enter PRA if 
.....                                             * deceased indicator changed
.....     ENDIF
.
          PROC      PATDEATH                      * update files if dead
.
DIMEH900  CLOSE     MEHDS1A1                      * close discharge file
          CLOSE     MEHVI1A1                      * close visit file
.
DIMEH999  RETURN
+
.*************************************************************************
.*  DIHMS000  :  Subtract off the old data for pat from MHMS adjustments *
.*************************************************************************
DIHMS000  MOVE      AADMNO,PVIBILL
          MOVE      PVIBILL,KEY8
          CALL      RDVISA1               * Make sure visit file record is read
.
DIHMS999  RETURN
+
.*************************************************************************
.*  DIWRD000  :  update Ward stuff                                       *
.*************************************************************************
DIWRD000  MOVE      AADMNO,DADMNO
          MOVE      AURNO,DURNO
          MOVE      ZEROVISN,STBYPAT              * Init adm. no. of standby pat
          MOVE      SP3,SBWARD                    * Init standby ward
          MOVE      SP3,SBBED                     * Init standby bed
.
          MATCH     SP3,AWARD
          GOTO      DIWRD400 IF EQUAL
.
.         Clear bed that patient was in
.
          MATCH     SP3,ABED
          GOTO      DIWRD300 IF EQUAL
.
          PACK      KEY6,AWARD,ABED
          CALL      RDWARD1
.
          MOVE      WSTBY,WADMNO
          MOVE      ZEROVISN,WSTBY
          CALL      UPWARD1
          MOVE      WADMNO,STBYPAT                * Save adm.# of new pat in bed
          MOVE      WWARD,SBWARD                  * Save standby ward
          MOVE      WBED,SBBED                    * Save standby bed
          GOTO      DIWRD999
.
.         delete patient out of no-bed ward
.
DIWRD300  MOVE      APLUR,NBPLUR
          PACK      KEY13,AWARD,AADMNO,NBPLUR
          OPEN      PATNOBE1,"patnobef"
          CALL      DENOBE1
          CLOSE     PATNOBE1
          GOTO      DIWRD999
.
.         Discharging a standby patient
.
DIWRD400  MOVE      AADMNO,WSTBY
          PACK      KEY14,WSTBY,SP6
          OPEN      PATWR1A4,"patwr1af"
          CALL      RDSWARD4
          CALL      RDKWARD4
          CLOSE     PATWR1A4
          BRANCH    OVRCD,DIWRD999
.
          MATCH     AADMNO,WSTBY
          GOTO      DIWRD999 IF NOT EQUAL
.
          PACK      KEY6,WWARD,WBED
          CALL      RDWARD1
          MOVE      ZEROVISN,WSTBY
          CALL      UPWARD1
          GOTO      DIWRD999
.
DIWRD999  RETURN
+
.*************************************************************************
.*  DIDIS000  :  write discharge record                                  *
.*************************************************************************
DIDIS000  MOVE      DADMNO,KEY8
          CALL      WRDSCH1
.
DIDIS999  RETURN
+
.*************************************************************************
.*  DIAXE000  :  update admission extension record                       *
.*************************************************************************
DIAXE000  IF        PTCNAXEU=1
            PACK      KEY8,DADMNO
            CALL      RDPTAXE1
            IF        OVRCD=1
........      DISPLAY   *P1:24,*EL,*B,"Adm. Extension File Missing for ":
........      "Admission [",DADMNO,"]";
........      CALL      HITENTER
            ELSE
              MOVE      TWO,PTAXDAMI 
              MATCH     SP8,PTAXSDPM
              IF        !@EQUAL
                PACK      PTAXENPM,DDATE
              ENDIF
              CALL      UPPTAXE1
            ENDIF
          ENDIF
DIAXE999  RETURN
+
.*************************************************************************
.*  DIDAD000  :  update admission/discharge transfer destination file    *
.*************************************************************************
DIDAD000  COMPARE   ONE,PTCNUADT
          GOTO      DIDAD999 IF NOT EQUAL
.
          MATCH     SP5,PTDADCTS                  * Transfer Source = spaces ?
          GOTO      DIDAD999 IF EQUAL             * yes
.
          OPEN      PATDADA1,"patdadaf"
          MOVE      PTDADCTS,TFRSRCE              * save Transfer Source
          PACK      KEY8,AADMNO
          CALL      RDPTDAD1                      * read PTDADFD record
          BRANCH    OVRCD,DIDAD300                * record not found
.
          MOVE      TFRSRCE,PTDADCTS              * set Trans Source=saved temp
          CALL      UPPTDAD1                      * update PTDADFD record
          CLOSE     PATDADA1
          GOTO      DIDAD999
.
DIDAD300  MOVE      SP5,PTDAADTS
          MOVE      TFRSRCE,PTDADCTS
          CALL      WRPTDAD1                      * write a PTDADFD record
          CLOSE     PATDADA1
.
DIDAD999  RETURN
+
.*************************************************************************
.*                               PATD0000                                *
.*              Update the patdayaf files if neccessary                  *
.*************************************************************************
PATD0000  COMPARE   ZERO,PTCNDAYF           * skip if not using patdayaf 
          GOTO      PATD9999 IF EQUAL         file for statistics
.
          MATCH     SP8,PTCNNDAT            * Don't update patdayaf files if
          GOTO      PATD9999 IF EQUAL         the update has not been run
.                                             before
          MATCH     "00000000",PTCNNDAT
          GOTO      PATD9999 IF EQUAL
.
.------ Update the patdayaf files for a discharged patient ------
.
PATD2000  PACK      STRTDATE,DDATE
          REP       " 0",STRTDATE
.
          MATCH     PTCNNDAT,STRTDATE    * don't process if discharge date is >
          GOTO      PATD9999 IF NOT LESS   or = date for next update
.
.------ we need to update from the day after the discharge date ------
.
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON              * convert to a julian date
.
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON              * convert back to CC,YY,MM,DD format
.
          PACK      STRTDATE,CC,YY,MM,DD
          REP       " 0",STRTDATE
.
.------ we need to update up until the day before the next update ------
.
          UNPACK    PTCNNDAT,CCENT,CYEAR,CMON,CDAY
          CALL      SUBD0000            * subtract a day from PTCNNDAT
.
          PACK      ENDDATE,CC,YY,MM,DD
          REP       " 0",ENDDATE
.
          MATCH     STRTDATE,ENDDATE    * for the case when there is no
          GOTO      PATD9999 IF LESS    * days between the discharge date and
.                                         the day for the next update
          MOVE      AADMNO,ADMISNO
          CALL      PATDAYDL            * delete records from patdayaf files
          GOTO      PATD9999
.
.------ Update the patdayaf files for a change in discharge date ------
.
. THis option used for modify!!!!
PATD5000  MATCH     SDDATE,DDATE        * match new date to old discharge date
          GOTO      PATD5500 IF LESS
.
          PACK      STRTDATE,SDDATE
          REP       " 0",STRTDATE
.
.------ we need to update from the day after the old discharge date ------
.
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON              * convert to a julian date
.
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON              * convert back to CC,YY,MM,DD format
.
          PACK      STRTDATE,CC,YY,MM,DD
          REP       " 0",STRTDATE
.
          PACK      ENDDATE,DDATE
          REP       " 0",ENDDATE
.
          MATCH     PTCNNDAT,ENDDATE    * match if new discharge date is less
          GOTO      PATD5100 IF LESS      than date for next update
.
.------ we need to update up until the day before the next update ------
.
          UNPACK    PTCNNDAT,CCENT,CYEAR,CMON,CDAY
.
          CALL      SUBD0000            * subtract a day from PTCNNDAT
          PACK      ENDDATE,CC,YY,MM,DD
          REP       " 0",ENDDATE
.
PATD5100  MOVE      AADMNO,ADMISNO
          CALL      PATDAYAD            * add records to the patdayaf files
          GOTO      PATD9999
.
.------ new discharge date is less than the old date ------
.
PATD5500  PACK      STRTDATE,DDATE
          REP       " 0",STRTDATE
.
.------ we need to update from the day after the new discharge date ------
.
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON              * convert to a julian date
.
          ADD       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON              * convert back to CC,YY,MM,DD format
.
          PACK      STRTDATE,CC,YY,MM,DD
          REP       " 0",STRTDATE
.
          PACK      ENDDATE,SDDATE
          REP       " 0",ENDDATE
.
          MATCH     PTCNNDAT,ENDDATE    * match if old discharge date is less
          GOTO      PATD5600 IF LESS      than date for next update
.
.------ we need to update up until the day before the next update ------
.
          UNPACK    PTCNNDAT,CCENT,CYEAR,CMON,CDAY
.
          CALL      SUBD0000            * subtract a day from PTCNNDAT
          PACK      ENDDATE,CC,YY,MM,DD
          REP       " 0",ENDDATE
.
PATD5600  MOVE      AADMNO,ADMISNO
          CALL      PATDAYDL            * delete records from patdayaf files
.
PATD9999  RETURN
+
.*************************************************************************
.*                               SUBD0000                                *
.*              Routine to subtract one from a given date                *
.* Requires : CCENT, CYEAR, CMON, CDAY                                   *
.* Returns  : CC, YY, MM, DD                                             *
.*************************************************************************
SUBD0000  MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON           * convert to julian date
.
          COMPARE   ONE,JULDAY       * see if we have the first day of the year
          GOTO      SUBD1000 IF NOT EQUAL
.
.------ if we have the first day of the year then set up the return ------
.------ date as the last day of the previous year ------
.
          MOVE      "31",DD          
          MOVE      "12",MM
          PACK      DJULCCYR,CC,YY
          MOVE      DJULCCYR,JULCCYR
          SUB       ONE,JULCCYR
          UNPACK    JULCCYR,CCENT,CYEAR
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          GOTO      SUBD9999
.
.------ the day is not the first day of the year so subract one day ------
.------ and set up the return date -------
.
SUBD1000  SUB       ONE,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON              * convert back to DD,MM,YY format
.
SUBD9999  RETURN
+
.*************************************************************************
.*  DIADS000  :  write admission/discharge stats                         *
.*************************************************************************
DIADS000  OPEN      PATSTAD1,"patstads"
          CALL      UPDSTAD
          CLOSE     PATSTAD1
.
DIADS999  RETURN
+
.*************************************************************************
.*  DIDAU000  :  write discharge audit                                   *
.*************************************************************************
DIDAU000  BRANCH    CAUDB,DIDAU999                * using discharge audit?
.
          MOVE      ANSA,ACTION
          CALL      ACTBSAV
          CALL      WRBAUD
.
DIDAU999  RETURN
+
.*************************************************************************
.*  DITRN000  :  write transfer file record                              *
.*************************************************************************
.
.        Get the last rate information from the transfer file, and
.        use this info to generate the "D" record in the transfer file
.
DITRN000  MOVE      ZERO,STDISC
          MOVE      ZERO,STALLW
          MOVE      ZERO,STRATEF
          MOVE      ZERO,STRATEG
          MOVE      ZERO,STRATEH
          MOVE      ZERO,STEXTRA
          MOVE      ZERO,STRBEND
          MOVE      SP3,STRBTYP
.
          PACK      KEY30,DADMNO,SP20
          CALL      RDSTRAN2
DITRN100  CALL      RDKTRAN2
          BRANCH    OVRCD,DITRN300
.
          MATCH     DADMNO,TADMN
          GOTO      DITRN300 IF NOT EQUAL
.
          MOVE      TWARD,STWARD                  * save all transfer vars in
          MOVE      TBED,STBED                    * case last transfer record
          MOVE      TDEPT,STDEPT
          MOVE      PTTREPNO,STEPNO
          MOVE      TDISC,STDISC
          MOVE      TALLW,STALLW
          MOVE      TRATEF,STRATEF
          MOVE      TRATEG,STRATEG
          MOVE      TRATEH,STRATEH
          MOVE      TEXTRA,STEXTRA
          MOVE      TRBEND,STRBEND
          MOVE      TRBTYP,STRBTYP
          GOTO      DITRN100
.
DITRN300  UNPACK    DDATE,XCENT,XYEAR,XMON,XDAY
          PACK      TDATE,XCENT,XYEAR,XMON,XDAY
          REP       " 0",TDATE
.
          MOVE      ANSD,TMOVE
          MOVE      DURNO,TURNO
          MOVE      DADMNO,TADMN
.         MOVE      DTIME,TTIME
          PACK      TTIME,DTIME,COLON,TWENTY
.
          MOVE      STWARD,TWARD                  * restore all of the save vars
          MOVE      STBED,TBED
          MOVE      STDEPT,TDEPT
          MOVE      STEPNO,PTTREPNO
          MOVE      STRATEF,TRATEF
          MOVE      STRATEG,TRATEG
          MOVE      STRATEH,TRATEH
          MOVE      STEXTRA,TEXTRA
          MOVE      STDISC,TDISC
          MOVE      STALLW,TALLW
          MOVE      STRBEND,TRBEND
          MOVE      STRBTYP,TRBTYP
.
          MOVE      ATYPE,TATYPE
          MOVE      ADOCTA,TADOCT
          MOVE      AUSR2,TCHSTAT
          MOVE      SP3,PTTRLTYP                  * clear type of leave 
.
          MOVE      ZERO,PTTREPSD
          MOVE      ZERO,PTTRLSPT
          MOVE      ZERO,PTTRLSRB
          MOVE      ZERO,PTTRAEND
          MOVE      ZERO,PTTRADPT
          MOVE      ZERO,PTTRADRB
          MOVE      SP70,PTTRNHAC
          MOVE      SP70,PTTROPER
          MOVE      SP70,PTTRAUAT
          MOVE      SP70,PTTRCUID
          MOVE      SP70,PTTRLTSC
          MOVE      ACLAIM,PTTRCLTY
.
          COMPARE   ONE,ONLEAVE             * discharging while on leave?
          CALL      GRET0000 IF EQUAL       * Generate a return from leave rec.
.
          PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED
          PACK      PTTRCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTTRCDAT
          CLOCK     TIME,PTTRCTIM
          MOVE      WBSEUID,PTTRUCID
          UNPACK    SP70,TRCDATE,TRCTIME,PTTRUUID
.
          CALL      WRTRAN2
.
DITRN999  RETURN
+
.**********************************************************************
. GRET0000   : Generate a return from leave record
. Called by  :
. Parameters : Transfer record set up for Discharge
.**********************************************************************
GRET0000
. ------- save discharge record transfer details
.
          MOVE      TMOVE,SAVTMOVE           * save variables
          MOVE      TTIME,SAVTTIME
          MOVE      TDATE,SAVTDATE            
.
          CALL      TSET0000                 * set TTIME & TDATE for this record
          MOVE      "R",TMOVE                * set move type as a Return
.
          MOVE      ZERO,PTTREPSD
          MOVE      ZERO,PTTRLSPT
          MOVE      ZERO,PTTRLSRB
          MOVE      ZERO,PTTRAEND
          MOVE      ZERO,PTTRADPT
          MOVE      ZERO,PTTRADRB
          MOVE      SP70,PTTRNHAC
          MOVE      SP70,PTTROPER
          MOVE      SP70,PTTRAUAT
          MOVE      SP70,PTTRCUID
          MOVE      SP70,PTTRLTSC
          MOVE      ACLAIM,PTTRCLTY
.
          PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED
          PACK      PTTRCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTTRCDAT
          CLOCK     TIME,PTTRCTIM
          MOVE      WBSEUID,PTTRUCID
          UNPACK    SP70,TRCDATE,TRCTIME,PTTRUUID
.
          CALL      WRTRAN2
.
. ------- delete leave record from patonlvf 
.
          PACK      KEY14,TWARD,TBED,TADMN,SP20
          CALL      DEONLV1
.
. ------- restore discharge record transfer details
.
          MOVE      SAVTMOVE,TMOVE          * restore variables
          MOVE      SAVTTIME,TTIME  
          MOVE      SAVTDATE,TDATE
.
GRET9999  RETURN
+
.**********************************************************************
. TSET0000   : Set Transfer time to be one second before current time
. Called By  : GRET0000
. Parameters : SAVTTIME, SAVTDATE, SAVTCENT
. Returns    : TTIME   - one second previous
.              TDATE   - date value after time change
.**********************************************************************
TSET0000
          UNPACK    SAVTTIME,CHOUR,DIM1,CMIN:
                             DIM1,DSECONDS       * unpack time, ignore ":"
          MOVE      CHOUR,HOURS
          MOVE      CMIN,MINUTES
          MOVE      DSECONDS,SECONDS
          PACK      DIM6,HOURS,MINUTES,SECONDS   * repack
          REP       " 0",DIM6                    * no spaces
          MOVE      DIM6,FORM6                   * convert to form
.
          MATCH     "000000",DIM6                * handle midnight case
          IF        @EQUAL
            MOVE     "23",HOURS
            MOVE     "59",MINUTES
            MOVE     "59",SECONDS
            CALL    DSET0000                     * set date as previous
            GOTO    TSET9000
          ENDIF
.
          SUB       "1",FORM6                    * subtract 1 from date
          MOVE      FORM6,DIM6                   * convert to dim
          REP       " 0",DIM6                    * no spaces
          UNPACK    DIM6,CHOUR,CMIN,DSECONDS
          MOVE      CHOUR,HOURS                  * convert to forms
          MOVE      CMIN,MINUTES   
          MOVE      DSECONDS,SECONDS

          COMPARE   "99",MINUTES                 * fix minutes
          IF        @EQUAL
            MOVE    "59",MINUTES
            GOTO    TSET9000
          ENDIF
.
          COMPARE   "99",SECONDS                 * fix seconds
          IF        @EQUAL
            MOVE    "59",SECONDS
            GOTO    TSET9000
          ENDIF
.
TSET9000  PACK      TTIME,HOURS,COLON,MINUTES:
                          COLON,SECONDS          * pack up time
          REP       " 0",TTIME
.
TSET9999  RETURN
+
.
.**********************************************************************
. DSET0000   : Set Transfer Date as one day before current value
. Called by  : TSET0000
. Parameters : SAVTDATE, SAVTCENT
. Returns    : TDATE   - one day previous
.**********************************************************************
DSET0000
          UNPACK    TDATE,CCENT,CYEAR,CMON,CDAY    * set up parameters
          MOVE      CDAY,DD
          MOVE      CMON,MM
          MOVE      CYEAR,YY
          MOVE      CCENT,CC               
.
          CALL      DMYCON                   * convert to Julian date
          SUB       ONE,JULDAY               * set to previous day
.
          MOVE      JULDAY,JWKDAY            * set up parameters
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON                   * convert to DDMMYYCC
.
          PACK      TDATE,CC,YY,MM,DD        * set up date
          REP       " 0",TDATE
.
DSET9999  RETURN
+
.*************************************************************************
.*  DINUM000  :  update ward usage file                                  *
.*************************************************************************
DINUM000  BRANCH    PTCNNUMM,DINUM999             * using this file?
.
          OPEN      PATNUMW1,"patnumwr"
          PACK      CPTDATE,TDATE
          REP       " 0",CPTDATE
          CALL      GPERD                 * Get the financial period for date
          BRANCH    EXIT,DINUM900         * Date not in period file
.
          PACK      PDATE,DRGYR,DRGNUM
          MOVE      TWARD,PWARD
.
.         Update number of patient transfer out
.
DINUM100  PACK      KEY9,PWARD,PDATE
          CALL      RLNUMW1
          BRANCH    OVRCD,DINUM500,DINUM190
          ADD       ONE,PDSCH
          CALL      UPNUMW1
          CALL      UUNUMW1
          GOTO      DINUM900
.
DINUM190  DISPLAY   *W,"Waiting on Record Lock PATNUMFD"
          GOTO      DINUM100
.
DINUM500  MOVE      ZERO,PTRIN
          MOVE      ZERO,PTROUT
          MOVE      ONE,PDSCH
          MOVE      ZERO,PADMN
          MOVE      ZERO,PLEAV
          MOVE      ZERO,PRETN
          CALL      WRNUMW1
.
DINUM900  CLOSE     PATNUMW1
.
DINUM999  RETURN
+
.*************************************************************************
.*  DIMIS000  :  update admission record                                 *
.*************************************************************************
DIMIS000  MOVE      AADMNO,KEY8
          CALL      RDMISS1
.
          MOVE      SP3,AWARD
          MOVE      SP3,ABED                   * Clear ward/bed
          MOVE      THREE,ASTAT                * Label patient has discharged
          CALL      UPLMISS1
.
DIMIS999  RETURN
+
.*************************************************************************
.*  DIBOK000  :  update booking & waiting list files                     *
.*************************************************************************
DIBOK000  COMPARE   ONE,CBOOK                     * using booking system?
          GOTO      DIBOK999 IF NOT EQUAL
.
          OPEN      BOKRC1A1,"bokrc1af"
          MOVE      AADMNO,KEY8
          CALL      RDBKREC1
          BRANCH    OVRCD,DIBOK900
.
          LOAD      BKRESTAT,ASTAT,ONE,THREE,FOUR,THREE
          CALL      UPBKREC1
.
.         Waiting Lists WMSTAT set to 4 as admitted
.
          COMPARE   ONE,HBWAIT                    * using Waiting Lists?
          GOTO      DIBOK900 IF NOT EQUAL
.
          OPEN      WATTR1A1,"wattr1af"
          OPEN      WATHISA1,"wathisaf"
          PACK      KEY19,BKREURNO,BKREPROC,BKRECNT
          CALL      RDTREA1
          BRANCH    OVRCD,DIBOK850
.
          CALL      SAVHIS00
.
          IF        WMSTAT <= 6
            LOAD      WMSTAT,ASTAT,THREE,FOUR,FIVE,FOUR
            IF        WMSTAT=5
              MOVE      "20",WTWMBSCD
              MOVE      ONE,NBRFLAG
              MOVE      ZERO,ADDWLFLG
              MOVE      Z70,WATHI002
              CALL      WRTHIS00
            ENDIF
            CALL      UPTREA1
          ENDIF
.
DIBOK850  CLOSE     WATTR1A1
DIBOK900  CLOSE     BOKRC1A1                     * close booking file
.
DIBOK999  RETURN
+
.*************************************************************************
.*  DIMAS000  :  update master file                                      *
.*************************************************************************
DIMAS000  MOVE      PCEASE,SAVPCEAS
          MOVE      PAUTOPY,SAVPAUTO
          MOVE      PDECDTE,SAVDECDT
.
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          CALL      RDPMPX21
          MOVE      ONE,PHOSP
          UNPACK    DDATE,XCENT,XYEAR,XMON,XDAY
          PACK      PLDDATE,DDATE
.
.         Calculate number of days last in hospital
.
          MOVE      ADATE,FROMDATE
          MOVE      DDATE,TODATE
          CALL      NHOSPDAY
          ADD       NBRDAYS,PLDAYS
.
          MOVE      SAVPCEAS,PCEASE
          MOVE      SAVPAUTO,PAUTOPY
          MOVE      SAVDECDT,PDECDTE
.
.         check if patient should no longer be classified as mental health
.
          COMPARE   ONE,MHCNUSE
          GOTO      DIMAS500 IF NOT EQUAL   * not using mental health
.
          MOVE      AADMNO,KEY8
          OPEN      MEHVI1A1,"mehvi1af"                * visit file
          CALL      RDMHVIS1
          CLOSE     MEHVI1A1
          BRANCH    OVRCD,DIMAS500          * not a mental health patient
.
          PACK      KEY5,ANSL,ANSS,PTMXLS   * legal status
          CALL      RDCODE1
          MATCH     "S",TCINDC1
          GOTO      DIMAS500 IF EQUAL       * special patient
.
          PACK      KEY5,ANSD,SP1,DSTAT     * discharge status
          CALL      RDCODE1
          MATCH     "S",TCINDC2
          GOTO      DIMAS500 IF EQUAL       * discharged to another hosp
.
          MOVE      SP3,PTMXLS              * no longer mental health patient
.   
DIMAS500  CALL      UPLMAST1
.
DIMAS999  RETURN
+
.*************************************************************************
.*  DICON000  :  update controlf file with transfer date                 *
.*************************************************************************
DICON000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,EIGHTY8;*148,CFRDTE
          MATCH     CFRDTE,TDATE
          GOTO      DICON900 IF NOT LESS
          MOVE      TDATE,CFRDTE
          WRITAB    CONTROLF,EIGHTY8;*148,CFRDTE
.
DICON900  CLOSE     CONTROLF
.
DICON999  RETURN
+
.*************************************************************************
.*  DILOC000  :  delete patient from patient location file               *
.*************************************************************************
DILOC000  PACK      KEY88,PTMASNAM,PMPXFNAM,AADMNO
          OPEN      PATLOCA1,"patlocaf"
          CALL      DELOCA1
          CLOSE     PATLOCA1
.
DILOC999  RETURN
+
.*************************************************************************
.*  DIHMA000  :  add pat to MHMS adjustments                             *
.*************************************************************************
DIHMA000  MOVE      DADMNO,PVIBILL
          MOVE      PVIBILL,KEY8
          CALL      RDVISA1             * Make sure visit file record is read.
.
DIHMA999  RETURN
+
.*************************************************************************
.*  DIRAT000  :  if day case, change rates to day case rates             *
.*************************************************************************
DIRAT000  MATCH     ADATE,DDATE
          GOTO      DIRAT999 IF NOT EQUAL
.
          COMPARE   ONE,CDYCASE
          GOTO      DIRAT999 IF NOT EQUAL
.
          IF        CFEETYP = 0 | CFEETYP = 4 
            CALL      PATDYCAS
          ENDIF
.
DIRAT999  RETURN
+
.*************************************************************************
.*  DIUNL000  :  unlock admission and master file records                *
.*************************************************************************
DIUNL000  MOVE      AADMNO,KEY8
          CALL      UUPTMIS1
.
          MOVE      AURNO,KEY8
          CALL      UUPTMAS1
.
DIUNL999  RETURN
+
.*************************************************************************
.*  DISBY000  :  check for a stand-by patient                            *
.*************************************************************************
DISBY000  MATCH     ZEROVISN,STBYPAT
          GOTO      DISBY999 IF EQUAL
.
          MOVE      STBYPAT,AADMNO
          MOVE      AADMNO,KEY8
          CALL      RLPTMIS1
          COMPARE   ZERO,OVRCD
          GOTO      DISBY999 IF NOT EQUAL
.
          MOVE      SBWARD,AWARD                  * Put in saved ward
          MOVE      SBBED,ABED                    * Put in saved bed
          CALL      UPMISS1
          CALL      UUPTMIS1
.
DISBY999  RETURN
+
.**********************************************************************
.*  ACTBSAV  :  same some discharge vars                              *
.**********************************************************************
ACTBSAV   MOVE      DURNO,SDURNO
          MOVE      DADMNO,SDADMNO
          MOVE      DDATE,SDDATE
          MOVE      DTIME,SDTIME
          MOVE      DSTAT,SDSTAT
.
          MOVE      DDEST,SDDEST
          MOVE      DDIAG,SDDIAG
          MOVE      DDIAG2,SDDIAG2
          MOVE      DUSD1,SDUSD1
          MOVE      DUSD2,SDUSD2
          MOVE      DUSD3,SDUSD3
          MOVE      DUSD4,SDUSD4
          MOVE      DUSD5,SDUSD5
          MOVE      DFMBS,SDFMBS
          MOVE      PTDSDMDC,SDMDC
          MOVE      PTDSDDRG,SDDRG
          MOVE      DWLREASN,SDWLREAS
          MOVE      PTDSSIDX,SPTDSSID
          MOVE      PTDSVOGU,SPTDSVOG
          MOVE      PTDSOPER,SPTDSOPE
          MOVE      PTDSUYN1,SPTDSUY1
          MOVE      PTDSUYN2,SPTDSUY2
          MOVE      PTDSUYN3,SPTDSUY3
          MOVE      PTDSUYN4,SPTDSUY4
          MOVE      PTDSDWRD,SPTDSDWR
          MOVE      PTDSDLOS,SPTDSDLO
          RETURN
+
.************************************************************************
.*  UPDSTAD  :  Add one for new discharge info                          *
.************************************************************************
UPDSTAD   COMPARE   ONE,PTCNSTAU         * return if using the new admission 
          RETURN    IF EQUAL               and discharges stats report
.
          PACK      CPTDATE,DDATE
          REP       " 0",CPTDATE
          CALL      GPERD
          BRANCH    EXIT,NOAPER2
.
          MOVE      DRGYR,SADYEAR
          MOVE      DRGNUM,FORM2
.
          MOVE      ANSD,ANS
          PACK      KEY8,SADYEAR,ANS,DDEST
.
          MATCH     DDEST,SP3
          CALL      TRYUNK1 IF EQUAL
.
          MOVE      "STA",PRXCODE   * System Lock Stats
          CALL      GETSLK00        * Get System Lock Stats
          CALL      RDSTAD1
          BRANCH    OVRCD,NEWCODE
          MOVE      ONE,COUNT
          CALL      ADDSTAD
          CALL      RELSLK00        * Release System Lock Stats
          RETURN
.
NOAPER2   UNPACK    CPTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
.......   DISPLAY   *P1:24,*EL,*B,CPCDATE:
.......             " not in Period File.  ";
.......   CALL      HITENTER
.
.         Subroutine to set the key when no Dest. code is given
.
TRYUNK1   MOVE      ANSD,ANS
          PACK      KEY8,SADYEAR,ANS,CODEUNK
          RETURN
.
NEWCODE   MOVE      ANSD,SADTYPE
          MOVE      DDEST,SADCODE
          MATCH     SP3,DDEST
          GOTO      NEWCOD1 IF NOT EQUAL
          MOVE      CODEUNK,SADCODE
.
NEWCOD1   CALL      NEWSTAD
          CALL      RELSLK00        * Release System Lock Stats
          RETURN
+
.**************************************************************************
.*  ADDSTAD  :  Subroutine to update the appropriate variable in the file *
.**************************************************************************
ADDSTAD   LOAD      FORM6,FORM2,SADMTH1,SADMTH2,SADMTH3,SADMTH4,SADMTH5,SADMTH6:
                                SADMTH7,SADMTH8,SADMTH9,SADMTH10,SADMTH11:
                                SADMTH12,SADMTH13
          ADD       COUNT,FORM6
          STORE     FORM6,FORM2,SADMTH1,SADMTH2,SADMTH3,SADMTH4,SADMTH5,SADMTH6:
                                SADMTH7,SADMTH8,SADMTH9,SADMTH10,SADMTH11:
                                SADMTH12,SADMTH13
          CALL      UPSTAD1
          RETURN
+
.**************************************************************************
.*  NEWSTAD  :  Subroutine to add a new record to the Statistics file     *
.**************************************************************************
NEWSTAD   MOVE      ZERO,SADMTH1
          MOVE      ZERO,SADMTH2
          MOVE      ZERO,SADMTH3
          MOVE      ZERO,SADMTH4
          MOVE      ZERO,SADMTH5
          MOVE      ZERO,SADMTH6
          MOVE      ZERO,SADMTH7
          MOVE      ZERO,SADMTH8
          MOVE      ZERO,SADMTH9
          MOVE      ZERO,SADMTH10
          MOVE      ZERO,SADMTH11
          MOVE      ZERO,SADMTH12
          MOVE      ZERO,SADMTH13
          MOVE      ONE,FORM6
          STORE     FORM6,FORM2,SADMTH1,SADMTH2,SADMTH3,SADMTH4,SADMTH5,SADMTH6:
                                SADMTH7,SADMTH8,SADMTH9,SADMTH10,SADMTH11:
                                SADMTH12,SADMTH13
          CALL      WRSTAD1
          RETURN
.
OVERCOND  MOVE      ONE,OVRCD
          RETURN
OVERLOCK  MOVE      TWO,OVRCD
          RETURN
CNOCHG    MOVE      ONE,CDCHG
          RETURN
CNOQST    MOVE      ONE,CQUEST
          GOTO      OVERCOND
STOPDATE  
STOPDATC  DISPLAY   *PCCOL:CVERT,SP10
          RETURN
.
.
          INC       PATONLIO/INC
          INC       PATNOBIO/INC
          INC       PATAXEIO/INC
          INC       PATNUMIO/INC
          INC       PATDADIO/INC
          INC       MEHDS1IO/INC
          INC       MEHVI1IO/INC
          INC       PATSTAIO/INC
          INC       WATTR1IO/INC
          INC       WATHISIO/INC
          INC       PATLOCIO/INC
          INC       PATDAYIO/INC
          INC       PATADBIO/INC
          INC       WLHISSUB
          INC       PATDAYUP
          INC       NHOSPDAY
          INC       MHAUDVIS
          INC       MHAUDDSC
          INC       PATCODKY
          INC       PATWLDSC
.
 XIF
ENDPROC1  ENDPROC
