.******************************************************************************
.* Include Name    : CHKWCCLM                                                 *
.* Function        : Check for multiple Workers Compensation claims records   *
.*                   with the same Claim Number.                              *
.* Author          : Don Nguyen                                               *
.* Date            : 02/11/2018                                               *
.*                                                                            *
.*                                                                            *
.* Requires : CHWCURNO - UR Number                                            *
.*            CHWCADMN - Admission Number                                     *
.*                                                                            *
.* Returns  : EXIT  0 = Only one WC claim exists for the Claim No.            *
.*                  1 = Multiple WC claims exist for the Claim No.            *
.*                                                                            *
.* Modifications   :                                                          *
.*       V10.13.00   02/11/2018  Don Nguyen      TSK 0853817                  *
.*                   Created include                                          *
.******************************************************************************
          DEFPROC   CHKWCCLM
.
          INC       PATWC1FD/INC
.
          OPEN      PATWC1A1,"patwc1af"
          OPEN      PATWC1A2,"patwc1af"
.
          MOVE      ZERO,EXIT
          MOVE      ZERO,FORM1              * reset counter
.
          MOVE      CHWCADMN,KEY8
          CALL      RDWCOM1
          BRANCH    OVRCD,CHKWCC99          * No workers comp claim; finish
.
          MOVE      WCCLMNO,DIM20           * Store our Claim Number
.
          PACK      KEY16,CHWCURNO,SP70
          CALL      RDSWCOM2
CHKWCC10  CALL      RDKWCOM2
          BRANCH    OVRCD,CHKWCC99
.
          MATCH     PTWCURNO,CHWCURNO
          GOTO      CHKWCC99 IF NOT EQUAL
.
          MATCH     WCCLMNO,DIM20
          GOTO      CHKWCC10 IF NOT EQUAL
.
          ADD       ONE,FORM1
.
          IF        FORM1 > 1
            MOVE      ONE,EXIT
            GOTO      CHKWCC99
          ENDIF
          GOTO      CHKWCC10
.
CHKWCC99  GOTO      ENDCHKWC
.
          INC       PATWC1IO/INC
          INC       IBAOVRIO/INC
.
ENDCHKWC  ENDPROC
+
