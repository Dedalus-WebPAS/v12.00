.****************************************************************************** .*                                                                            *
.*                                 PRICLMDS                                   *
.*                                 --------                                   *
.*                                                                            *
.*                Include to do a ? option for Claim Number.                  *
.*                                                                            *
.*    AUTHOR           : DIG                                                  *
.*                                                                            *
.*    DATE             : 31/03/92                                             *
.*                                                                            *
.*    FILES NEEDED     : PRIBULFD + IO ( index 6 and index 1 )                *
.*                       INC       PMSHCPFD + IO                              *
.*                                                                            *
.*    VARIABLES NEEDED : FROMFLAG  FORM    1                                  *
.*                       SAVCLAIM  DIM     8                                  *
.*                       FORM8     FORM    8                                  *
.*                       TOFLAG    FORM    1                                  *
.*                       ZEDS      "zzzzzzzzzzzzzzzzzzz"                      *
.*                       INDXFLAG  FORM    1                                  *
.*                                                                            *
.*                                                                            *
.*    PARAMETERS       : FROMFLAG  - Claim status to print from               *
.*                       TOFLAG    - Claim status to print to                 *
.*                       INDXFLAG  - 0 - use index 6                          *
.*                                   1 - use index 1                          *
.*                                                                            *
.*                                                                            *
.*    RETURNS          : EXIT      = 1    If no claims were displayed.        *
.*                                 = 0    If we displayed claims.             *
.*                                                                            *
.*    COMMENTS         :                                                      *
.*                                                                            *
.*    MODIFICATIONS    :                                                      *
.*       V10.13.01 26/07/2018  J.Tan            TSK 0848506                   *
.*                 Changed PRBBDOCT to DIM10                                  *
.*        V9.02.01  02/10/2001  J.Tan                                         *
.*                  Mods to use PMSHCPFD instead of PATDO1FD                  *
.*                                                                            *
.*                       V6.00.01  19/02/93  DIG                              *
.*                                 Fixed for changes to the PRIBULFD file.    *
.*                                                                            *
.******************************************************************************
PRICLMDS  MOVE      FALSE,EXIT
          MOVE      FOUR,CVERT
          DISPLAY   *P1:4,*EF,*V2LON,*ULON,"Claim No":
                    *P10:4,"Bulk Bill Code":
                    *P25:4,"Fund/Insurance/Govt Code":
                    *P50:4,"Service Doctor                ";
.
          MOVE      SP8,SAVCLAIM
.
.------ INDXFLAG = 0 then we use index 6 else we use index 1 -----
          IF        INDXFLAG = 0
            PACK      KEY38,FROMFLAG,SP20,SP20
            CALL      RSPRBB6                * position on the bulk billing file
          ELSE
            PACK      KEY28,SP30
            CALL      RSPRBB1
          ENDIF
.
.------ read through the Bulk Billing file ------
.------ On index six                       ------
.
CLMDS100  BRANCH    INDXFLAG,CLMDS150
          CALL      RKPRBB6
          BRANCH    OVRCD,CLMDS900
.
          COMPARE   PRBBSTAT,TOFLAG
          GOTO      CLMDS900 IF LESS 
.
          COMPARE   ZERO,PRBBCLAM           * see if we have a zero claim 
          GOTO      CLMDS100 IF EQUAL         number
.
          MATCH     SP8,SAVCLAIM
          IF        !@EQUAL       
            MOVE      SAVCLAIM,FORM8
.                                           * see if we have a saved claim 
            COMPARE   PRBBCLAM,FORM8          number and if we have, see if
            GOTO      CLMDS100 IF EQUAL       we have the same claim
          ENDIF
.
          GOTO      CLMDS190
.
.------ read through the Bulk Billing file ------
.------ On index one                       ------
.
CLMDS150  CALL      RKPRBB1
          BRANCH    OVRCD,CLMDS900
.
.-------  Check that the status is within the required range -----
.
          COMPARE   FROMFLAG,PRBBSTAT
          GOTO      CLMDS150 IF LESS
.
          COMPARE   PRBBSTAT,TOFLAG
          GOTO      CLMDS150 IF LESS
.
.------   We have a desired record for this claim so reposition at the end -----
.------   of that claim                                                    -----
.
          PACK      KEY28,PRBBCLAM,ZEDS
          CALL      RSPRBB1
.
CLMDS190  MOVE      SP1,DIM1
          IF        PRBBSTAT = 2 | PRBBSTAT = 3
            MOVE      "*",DIM1
          ENDIF
.
          PACK      DOCNAME,SP20,SP20,SP10
.
          MATCH     SP70,PRBBDOCT            * see if the doctor code exists
          GOTO      CLMDS200 IF EQUAL
.
          MOVE      "Unknown Doctor",DOCNAME
          PACK      KEY10,PRBBDOCT,SP10
          CALL      RDPMHCP1                * read the doctor file
          BRANCH    OVRCD,CLMDS200
.
          MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          MOVE      PMHCTITL,PACTITLE
          MOVE      ONE,PACFRMT
.
          CALL      PACNAME                 * pack the doctors name
.
          MOVE      PACFNAME,DOCNAME
.
.------ see if we need to paginate ------
.
CLMDS200  ADD       ONE,CVERT
          IF        CVERT = 23
.
.------ we have a full page so see if we want to continue or display ------
.------ the next page ------
.
CLMDS210    DISPLAY   *P1:24,*EL,"(",*V2LON,ANSC,*HOFF,")ontinue, (",*V2LON:
                             ANSN,*HOFF,")ext Page ? ";
            KEYIN     *V2LON,ANS;
.
            MATCH     ANSC,ANS              * see if we want to continue
            IF        @EQUAL 
              GOTO      CLMDS999
            ENDIF
.
            MATCH     ANSN,ANS              * see if we want to display the 
.                                             next page
            IF        @EQUAL  
              MOVE      FIVE,CVERT
              DISPLAY   *P1:4,*EF,*V2LON,*ULON,"Claim No":
                        *P10:4,"Bulk Bill Code":
                        *P25:4,"Fund/Insurance/Govt Code":
                        *P50:4,"Service Doctor                ";
            ELSE
              BEEP                          * invalid option
.
              GOTO      CLMDS210
            ENDIF
          ENDIF
.
          DISPLAY   *P1:CVERT,PRBBCLAM:
                    *P9:CVERT,*V2LON,DIM1,*HOFF:
                    *P16:CVERT,PRBBBULK:
                    *P31:CVERT,PRBBCODE:
                    *P50:CVERT,DOCNAME;
          MOVE      PRBBCLAM,SAVCLAIM
.
          GOTO      CLMDS100
.
.------ we have finished looping through the file so see if we found ------
.------ any claims ------
.
CLMDS900  MATCH     SP8,SAVCLAIM
          IF        @EQUAL
            DISPLAY   *P1:24,*EL,*B,"No claims found - Hit <":
                             *V2LON,"ENTER",*HOFF,"> to continue";
            KEYIN     ANS;
            DISPLAY   *P1:24,*EL;
            MOVE      TRUE,EXIT
          ENDIF
.
CLMDS999  RETURN
+
