.------------------------------------------------------------
.  Standard Finance Coded Field Option 
.  
.  Parameters    CODE & CATAGORY
.
.  Tag Parameter 0=Description
.                1=Code
.                2=Selection List
.
.  Modifications
.  V11.05.02 20.12.2024 DA Sarkies      TSK 0954547
.            Commented out <cat> tags to prevent browser incompatibility
.  V11.05.01 26.11.2024  Ebon Clements  Task 0954547
.            Added <cat-c> start and end tag to category=XX tag
.  V9.02.01 20.09.2001 Ebon Clements 
.           Created FMSCODTM from PATCODTM
.------------------------------------------------------------
FMSITM00  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,FMSITM10,FMSITM20
          CALL      FMSCOD00
          WRITE     HTMLFILE;FMCDDESC;
          GOTO      FMSITM99
.
FMSITM10  PACK      CODE,CODE,SP70
          MATCH     SP70,CODE
          GOTO      FMSITM99 IF EQUAL
          WRITE     HTMLFILE;CODE;
          GOTO      FMSITM99
.
FMSITM20  CALL      FMSSEL00
          GOTO      FMSITM99
.
FMSITM99  RETURN
.------------------------------------------------------------
. Get Code Description
.------------------------------------------------------------
FMSCOD00  MOVE      SP70,FMCDDESC
          MOVE      SP70,FMCDCODE
          MATCH     SP70,CODE
          GOTO      FMSCOD99 IF EQUAL
.
          PACK      KEY5,CATEGORY,CODE
          CALL      RDFMCD1
          IF        OVRCD=1
            MOVE      SP70,FMCDCODE
            MOVE      SP70,FMCDDESC
          ENDIF
FMSCOD99  RETURN
.------------------------------------------------------------
. Add Selection Options to Form (code value)
.------------------------------------------------------------
FMSSEL00  MOVE      SP70,FMCDDESC
          PACK      KEY5,CATEGORY,SP70
          CALL      RDFMCD1
          IF        OVRCD=1
            MOVE      CATEGORY,FMCDCATY
            MOVE      "Invalid Category",FMCDDESC
          ENDIF
          REP       " _",FMCDCATY
          WRITE     HTMLFILE;"<!--<cat-c category=#"",FMCDCATY,"#" name=#"":
                              FMCDDESC,"#"></cat-c>-->"
.  
          PACK      KEY5,CATEGORY,SP70
          CALL      RSFMCD1
FMSSEL10  CALL      RKFMCD1
          BRANCH    OVRCD,FMSSEL99
.
          MATCH     CATEGORY,FMCDCATY
          GOTO      FMSSEL99 IF NOT EQUAL
.
          MATCH     SP70,FMCDCODE
          GOTO      FMSSEL10 IF EQUAL
.
          PACK      KEY20,QUOTE,FMCDCODE,QUOTE
          MATCH     CODE,FMCDCODE
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY20," selected>":
                               FMCDDESC,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY20,">",FMCDDESC,"</option>"
          ENDIF
          GOTO      FMSSEL10
FMSSEL99  RETURN
.
