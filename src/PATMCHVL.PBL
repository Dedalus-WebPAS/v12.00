.******************************************************************************
.*                                  PATMCHVL                                  *
.* Validate misc charge effective from and to dates for overlaps              *
.*                                                                            *
.* Requires:   KEY29 populated with -                                         *
.*                      - Claim Code (usually from ACLAIM)                    *
.*                      - Health Fund (usually AFUNDH)                        *
.*                      - H/F Table Type (converted from "patfn1af" -  AFNDTB)*
.*                      - Miscellaneous Charge code                           *
.*                      - Service Date (defaults to current date)             *
.*             VALFDATE - Effictive date from date                            *
.*             VALTDATE - Effictive date to date                              *
.*                                                                            *
.* Returns:    EXIT  0 = Valid misc charge effective from and to dates        *
.*                   1 = Invalid misc charge effective from and to dates      *
.*                                                                            *
.* Modifications:                                                             *
.* V10.01.00  21/02/2011  Ebon Clements   CAR 233160                          *
.*            Created include                                                 *
.******************************************************************************
.
VMDT0000  MOVE      KEY29,SAVKEY21
.
          CALL      RDMCHG1                 * Record on file so must be an
          BRANCH    OVRCD,VMDT5000          * update
.
          MATCH     VALTDATE,PTMCETDT       * No change in effective from and
          GOTO      VMDT8000 IF EQUAL       * to dates so exit
.
VMDT5000  CALL      RDSMCHG1                * Check for the same misc charge
          CALL      RDPMCHG1                * with an earlier effective from
          BRANCH    OVRCD,VMDT6000          * date
.
          PACK      KEY21,MCLMCOD,MHFUND,PTMCHFTT,MCHARGE,SP20
          MATCH     SAVKEY21,KEY21
          GOTO      VMDT6000 IF NOT EQUAL
.
          MATCH     SP70,PTMCETDT           * No effective to date on previous
          GOTO      VMDT8000 IF EQUAL       * misc charge.
.
          MATCH     VALFDATE,PTMCETDT
          IF        !@LESS
            GOTO      VMDT9000              * Effective from date overlap
          ENDIF
.
          MATCH     SP70,VALTDATE           * Blank effective to date
          GOTO      VMDT6000 IF EQUAL
.
          MATCH     VALTDATE,PTMCETDT
          IF        !@LESS
            GOTO      VMDT9000              * Effective to date overlap
          ENDIF
.
VMDT6000  CALL      RDSMCHG1                * Check for the same misc charge
          CALL      RDKMCHG1                * with an earlier effective from
          BRANCH    OVRCD,VMDT8000          * date
.
          PACK      KEY21,MCLMCOD,MHFUND,PTMCHFTT,MCHARGE,SP20
          MATCH     SAVKEY21,KEY21
          GOTO      VMDT8000 IF NOT EQUAL
.
          MATCH     SP70,VALTDATE           * Blank effective to date
          GOTO      VMDT9000 IF EQUAL
.
          MATCH     PTMCEFDT,VALFDATE
          IF        !@LESS
            GOTO      VMDT9000              * Effective from date overlap
          ENDIF
.
          MATCH     PTMCEFDT,VALTDATE
          IF        !@LESS
            GOTO      VMDT9000              * Effective to date overlap
          ENDIF
.
VMDT8000  MOVE      ZERO,EXIT
          GOTO      VMDT9999
.
VMDT9000  MOVE      ONE,EXIT
          GOTO      VMDT9999
.
VMDT9999  RETURN

