.
.*************************************************************************
.* OUTHEAD.PBL   : Include to display the standard 3 line header for a   *
.*                 given outpatient visit.                               *
.*                 This include is calls PMIHEAD to print the standard   *
.*                 2 line PMI heading.                                   *
.* DATA FILES    : PATMA1FD    PMI master file                           *
.*                 PATVISFD    Visit file                                *
.*                 PATDO1FD    Patient doctors file                      *
.*                 OUTCLIFD    Outpatient clinic file                    *
.*                 OUTSITFD    Outpatient site file                      *
.*                 PATDHEAD    Special variables for this include        *
.* PARAMETERS    : CCNOTES     Case Notes Indicator (System Parameter)   *
.*                 A PATMA1FD record                                     *
.*                 A PATVISFD record                                     *
.* OPEN FILES    : OUTCLIA1    Outpatient clinic file                    *
.*                 PATDO1A1    Patient doctors file                      *
.*                                                                       *
.* MODIFICATIONS :                                                       *
.*     V10.07.01   29/10/2015  Ebon Clements  CAR 268970                 *
.*                 Change to use OUTCLIFD index 1 instead of index 2     *
.*     V5.08.00    16/03/2000  Steve Armstrong   5.08 Mods               *
.*                 Mods for changes to OUTCLIFD (effective date)         *
.*     V5.07.00    15/10/98 Jim Stathopoulos                             *
.*                 507 Changes                                           *
.*************************************************************************
.*                 24/05/90 Graeme Williams                              *
.*                 Fixed the read of the clinic id file (was CLIA1)      *
.*************************************************************************
.
. -----   Initialise display variables  -----
.
OUTHEAD   MOVE      SP30,PATHDADM
          MOVE      SP30,PATHDDOC
.
. -----  Get the clinics name -----
.
OUTHD200  PACK      KEY20,PVISITE,PVIDOCT,PVIDATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     PVISITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     PVIDOCT,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          IF        OVRCD = 1
            MOVE      "Unknown clinic",OCADESC
            PACK      OCADESC,OCADESC,SP30
          ENDIF
          BRANCH    OVRCD,OUTHD500
.
          MATCH     SP6,OCADOCT              * Do we have a doctor code ?
          GOTO      OUTHD500 IF EQUAL        * No. Use description
.
          MOVE      OCADOCT,KEY6
          CALL      RDDOCT1                  * Read in the doctors record
          BRANCH    OVRCD,OUTHD700
.
. -----  format doctors name -----
.
          MOVE      DSNAME,PATHDDOC
          ENDSET    PATHDDOC
          CALL      OUTHD250                       * get rid of spaces
          LENSET    PATHDDOC
          APPEND    ", ",PATHDDOC
          APPEND    DGNAME,PATHDDOC
          RESET     PATHDDOC
          GOTO      OUTHD700
.
OUTHD250  CMATCH    SP1,PATHDDOC                    * get rid of spaces
          RETURN    IF NOT EQUAL                   * in doctors desc
          BUMP      PATHDDOC BY -1
          RETURN    IF EOS
          GOTO      OUTHD250
.
. ----- Use the clinic description -----
.
OUTHD500  MOVE      OCADESC,PATHDDOC
.
. ----- format status description -----
.
OUTHD700  UNPACK    PVIDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          CLEAR     PATHDADM
          APPEND    "Outpatient on ",PATHDADM
          APPEND    CPCDATE,PATHDADM
          RESET     PATHDADM
.
          CALL      PMIHEAD                  * Display PMI details
          DISPLAY   *P1:5,"Outpatient No: ",*V2LON,PVIBILL:
                    *P1:6,PATHDADM,*HOFF:
                    *P26:6,"Site : ",*V2LON,PVISITE,*HOFF:
                    *P42:6,"Clin : ",*V2LON,PATHDDOC
.
OUTHD999  RETURN
