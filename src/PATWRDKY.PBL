.******************************************************************************
.*                                                                            *
.* Include Name   : PATWRDKY.PBL                                              *
.*                                                                            *
.* Description    : Keyin of a Ward code from PATWR1FD                        *
.*                                                                            *
.* Author         : Justin Coates           07/05/1992                        *
.*                                                                            *
.* Parameters     : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF3 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMODE - is keyin insert mode (0=No  1=Yes)             *
.*                                                                            *
.* Returns        : WWARD    - Keyed in Ward                                  *
.*                  WBDESC   - Description                                    *
.*                  EXIT = 0   Valid code entered                             *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.* On return      : You must display the description yourself.                *
.*                                                                            *
.******************************************************************************
PATWRDKY  MOVE      ONE,FORM1               * set ? as not entered
          MOVE      ECOL,CCOL
          MOVE      EVERT,CVERT
.
KPTWD100  MATCH     SP3,CKYIDEF3
          GOTO      KPTWD120 IF EQUAL       * no default
.
          MOVE      CKYIDEF3,WWARD
          KEYIN     *PCCOL:CVERT,*DV,WWARD:
                    *PCCOL:CVERT,*V2LON,*RV,WWARD:
                    *PCCOL:CVERT,*DV,WWARD
          GOTO      KPTWD150
.
KPTWD120  KEYIN     *PCCOL:CVERT,*DV,UNDLN3:
                    *PCCOL:CVERT,*V2LON,WWARD:
                    *PCCOL:CVERT,*DV,WWARD
.
KPTWD150  PACK      WWARD,WWARD,SP3
          MATCH     SP3,WWARD
          GOTO      KPTWD790 IF EQUAL       * nothing entered
          MATCH     EXITCHAR,WWARD
          GOTO      KPTWD820 IF EQUAL       * exitchar entered
          MATCH     QUEST,WWARD
          GOTO      KPTWD200 IF NOT EQUAL   * ? not entered
.
.         ? entered
.
          IF        FORM1=1
            MOVE      "0",HLEF                * save all of the screen
            CALL      GETSCR00                * save screen
            MOVE      ZERO,FORM1              * set as ? performed
          ENDIF
.
          CALL      PATWRDDS                * display valid entries
.
          IF        CNEWDS = 0
            CALL      PUTSCR00                * redisplay screen
            MOVE      ONE,FORM1               * set as ? not performed
            MOVE      ECOL,CCOL
            MOVE      EVERT,CVERT
          ELSE
            DISPLAY   *P1:24,"Enter Ward : ",*EL;
            MOVE      "24",CVERT
            MOVE      "14",CCOL
            MOVE      ZERO,FORM1              * set ? as entered
          ENDIF
          GOTO      KPTWD100
.
.         validate what was entered
.
KPTWD200  PACK      KEY6,WWARD,SP3
          CALL      RDWARD1 
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,KPTWD400          * invalid ward
          ELSE
            COMPARE   ZERO,OVRCD
            GOTO      KPTWD400 IF EQUAL
          ENDIF
.
          MOVE      ZERO,EXIT               * set as valid ward
          GOTO      KPTWD800
.
KPTWD400  CALL      GETSCR00
          IF        CKYIMODE=0
            DISPLAY   *P1:24,*B,"Invalid Ward.  ",*EL;
            CALL      HITENTER
          ELSE 
            CALL      REXISTS
          ENDIF
          CALL      PUTSCR00
.
          BRANCH    FORM1,KPTWD100
          DISPLAY   *P1:24,"Enter Ward : ",*EL;
          GOTO      KPTWD100
.
.         nothing entered
.
KPTWD790  MOVE      SP30,WBDESC            * clear desc
          MOVE      ONE,EXIT                * set exit flag
          COMPARE   ZERO,CKYIMAND
          GOTO      KPTWD800 IF EQUAL       * valid entry
.
          DISPLAY   *P1:24,*B,"Field is mandatory.  ",*EL;
          CALL      HITENTER
          BRANCH    FORM1,KPTWD100
          DISPLAY   *P1:24,"Enter Ward : ",*EL;
          GOTO      KPTWD100
.
.         valid entry
.
KPTWD800  IF        FORM1=0
            CALL      PUTSCR00                * redisplay screen
            MOVE      ONE,FORM1               * set as ? not entered
          ENDIF
          DISPLAY   *PECOL:EVERT,*V2LON,WWARD
          GOTO      KPTWD999 
.
.         exitchar entered
.
KPTWD820  MOVE      TWO,EXIT                * EXITCHAR entered
          MOVE      SP3,WWARD
          MOVE      SP30,WBDESC
          IF        FORM1=0
            CALL      FRESCR00                * free the screen
          ENDIF
.
KPTWD999  RETURN
+
          INC       PATWRDDS
.
+
