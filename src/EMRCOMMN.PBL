.****************************************************************************
.*                           EMRCOMMN.PBL                                   *
.*           This include contains code that is used by the old A&E system  *
.*           and the new EMR system.                                        *
.****************************************************************************
.
.****************************************************************************
.*                             EMRCKCMP                                     *
.*       If this patient is a claim, ask if the claim details               *
.*       are to be entered                                                  *
.****************************************************************************
.
EMRCKCMP  PACK      KEY5,CODECL,ADACOMP
          CALL      RDCODE1
          BRANCH    OVRCD,EMRCK999
.
          MOVE      ZERO,FORM2
EMRCK100  ADD       ONE,FORM2
          COMPARE   SIX,FORM2
          GOTO      EMRCK999 IF NOT LESS
.
          LOAD      ANS,FORM2,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
.
          MATCH     ANSW,ANS
          GOTO      EMRCK200 IF EQUAL
          MATCH     ANSM,ANS
          GOTO      EMRCK300 IF EQUAL
          MATCH     ANSV,ANS
          GOTO      EMRCK400 IF EQUAL
          GOTO      EMRCK100
.
.         Workers Compensation Claim
.
EMRCK200  MOVE      ONE,INSTYPE
          MOVE      SP6,DIM6
          GOTO      EMRCK500
.
.         M.A.B. Claim
.
EMRCK300  MOVE      TWO,INSTYPE
          MOVE      CMABINS,DIM6
          GOTO      EMRCK500
.
.         Veteran Affairs Claim
.
EMRCK400  MOVE      THREE,INSTYPE
          MOVE      CVETINS,DIM6
.
.         Set up parameter file for claims program
.
EMRCK500  COMPARE   ONE,HINPCLM
          GOTO      EMRCK999 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,"Do you want to enter Claim details (":
                    *V2LON,*DV,ANSY,*HOFF,*DV,SLASH:
                    *V2LON,*DV,ANSN,*HOFF,") ? ",*V2LON,ANS;
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          GOTO      EMRCK999 IF NOT EQUAL
.
.         Input Claim details
.
          MOVE      ONE,CLMFLG                    reopened in this include again
          MOVE      ADANUMB,AADMNO
.         CALL      PATCLAIM
          MOVE      ZERO,CLMFLG
          OPEN      PATRE1A1,"patre1af"          * Open person responsible file
.
EMRCK999  RETURN
+
