.------------------------------------------------------------------------
. Include        :  CALCBMIN.PBL
. Date Created   :  03/12/2013
. Author         :  Peter Vela
. Modifications  :
.
. V10.04.02 22.01.2014 Peter Vela CAR 286158
.                      Changed display variable to only have 1 decimal point
. V10.04.01 17.01.2014 Peter Vela CAR 286158
.                      Changed BODMASUW to purple
. V10.03.01 03.12.2013 Peter Vela CAR 286158
.                      Created include. Calculate BMI
.                                       Display BMI Range
.------------------------------------------------------------------------
.
. Input Parameters
. --------------------------------------------------
.
. CLCBMI00
. --------
. Valid read of patatraf
. FORM2P1                       BMI work variable
. FORM4P2                       Height work variable
. FORM6P1                       Height work variable
. FORM6P1A                      Weight work variable
.
. RNGBMI00
. --------
. controlf PTCNUNDW             Underweight
.          PTCNHEFR             Healthy from
.          PTCNHETO             Healthy to
.          PTCNOVRW             Overweight
.          PTCNOBES             Obese
. FORM2P1                       BMI work variable
. FORM2P1A                      BMI work variable
. BODMASUW                      "<font color=purple>"
. BODMASHE                      "<font color=black>"
. BODMASFT                      "<font color=red>"
. BOLD                          "<b>"
. ENDBOLD                       "</b>"
. ENDFONT                       "</font>"
.
.
. Input Files Required
. --------------------------------------------------
.
. CLCBMI00
. --------
. PATATRFD.INC
. PATATRIO.INC
.
.
. RNGBMI00
. --------
.
.
. Returns
. --------------------------------------------------
.
. CLCBMI00
. --------
. BODMASIN   DIM   5
.
.
. RNGBMI00
. --------
. BODMASRN   DIM   40
.
.
.------------------------------------------------------------------------
. Calculate BMI
.------------------------------------------------------------------------
CLCBMI00  MOVE      SP70,BODMASIN
          MOVE      ZERO,FORM6P1                    * Height work variable
          MOVE      ZERO,FORM6P1A                   * Weight work variable
          MOVE      ZERO,FORM2P1                    * BMI work variable
          MOVE      ZERO,FORM4P2                    * Height work variable
.
          MATCH     SP70,PTARVAL1                   * Height (cms)
          GOTO      CLCBMI99 IF EQUAL
          GOTO      CLCBMI99 IF EOS
.
          MATCH     SP70,PTARVAL2                   * Weight (kgs)
          GOTO      CLCBMI99 IF EQUAL
          GOTO      CLCBMI99 IF EOS
.
          SQUEEZE   PTARVAL1
          SQUEEZE   PTARVAL2
          MOVE      PTARVAL1,FORM6P1                * Move height into work
          MOVE      PTARVAL2,FORM6P1A               * Move weight into work
.
          COMPARE   ZERO,FORM6P1
          GOTO      CLCBMI99 IF EQUAL
.
          COMPARE   ZERO,FORM6P1A
          GOTO      CLCBMI99 IF EQUAL
.
          ASSIGN    (FORM6P1 / 100),FORM4P2         * Convert height cm to m
          ASSIGN    (FORM4P2 * FORM4P2),FORM4P2     * Square of height m
.
          ASSIGN    (FORM6P1A / FORM4P2),FORM2P1    * BMI Calculation
.
          MOVE      FORM2P1,BODMASIN
          PACK      BODMASIN,BODMASIN,SP70
          LJUSTIFY  BODMASIN
.
CLCBMI99  RETURN
+
.------------------------------------------------------------------------
. Get BMI Range
.------------------------------------------------------------------------
RNGBMI00  MOVE      SP70,BODMASRN
          MATCH     SP70,BODMASIN
          GOTO      RNGBMI99 IF EQUAL
          GOTO      RNGBMI99 IF EOS
.
          MOVE      SP70,BODMASRN                   * Underweight
          PACK      BODMASRN,BODMASUW,BOLD,BODMASIN,ENDBOLD,ENDFONT
          MOVE      ZERO,FORM2P1
          MOVE      ZERO,FORM2P1A
          SQUEEZE   PTCNUNDW
          SQUEEZE   BODMASIN
          MOVE      PTCNUNDW,FORM2P1
          MOVE      BODMASIN,FORM2P1A
.
          COMPARE   FORM2P1A,FORM2P1
          GOTO      RNGBMI99 IF NOT LESS
.
          MOVE      SP70,BODMASRN                   * Health Range
          PACK      BODMASRN,BODMASHE,BODMASIN,ENDFONT
          MOVE      ZERO,FORM2P1
          MOVE      ZERO,FORM2P1A
          SQUEEZE   PTCNOVRW
          SQUEEZE   BODMASIN
          MOVE      PTCNOVRW,FORM2P1
          MOVE      BODMASIN,FORM2P1A
.
          COMPARE   FORM2P1,FORM2P1A
          GOTO      RNGBMI99 IF LESS 
.
          MOVE      SP70,BODMASRN                   * Overweight
          PACK      BODMASRN,BODMASFT,BOLD,BODMASIN,ENDBOLD,ENDFONT
.
RNGBMI99  RETURN
