. **********************************************************
. *              I N C L U D E  S U M M A R Y              *
. *              -------------  -------------              *
. *                                                        *
. *     INCLUDE NAME:     NHOSPDAY                         *
. *                                                        *
. *     FUNCTION:         CALCULATE DAYS IN HOSPITAL       *
. *                                                        *
. *     AUTHOR:           PAUL LO                          *
. *     DATE WRITTEN:     24/06/86                         *
. *                                                        *
. *     MODIFICATIONS:                                     *
. *       V12.00.01 23/05/2025  Ebon Clements   TSK 0955096*
. *                 Alphanueric visit number changes       *
. *       V9.03.00  15/07/2004  Ebon Clements  CAR 51768   *
. *                 Added NHOSDAYC - Return sameday patient*
. *                 LOS = 1 day . 1 = Yes 0 = No           *
. *       V5.09.B01 12/02/2001  Greg Horvat                *
. *                 Changed to not call the CALC routine   *
. *                 but rather use DAYSEP directly         *
. *             V5.08.02  07/12/2000  Steve Downing        *
. *                       Mods to comparing of TDATE with  *
. *                       FROMDATE                         *
. *             V5.08.01  21/09/2000  Caleb Sharman  SRF 647246  
. *                       Mods to use TDATE when changing adm.
. *                       date to an earlier date          *
. *             V5.07.02  13/09/1999  Andrew Peel          *
. *                       Changed sameday patient LOS = 0  *
. *             V5.07.01  03/08/1999  Steve Downing        *
. *                       Mods to use DAYSEP               *
. *             V5.07.B02 29/01/1999  Nicole Harrington    *
. *                       Mods to use KEY30 for RDSTRAN2   *
. *                 08/10/1998  Glenn Berry      5.07 changes
. **********************************************************
. *                       M.HAYSE DOES HANDLE MULTPLY DAYS *
. *                       NRBDAYS GETS WAPPED IN MULTY     *
. *                       TRANSFERS AND DOESN'T ADD UP     *
. *                       CURRENT PATIENTS DOESNT INCLUDE  * 
. *                       TODAY AS NUMBER OF DAYS          *
. *                       G.Williams 02/11/87              *
. *                       New Transfer File format         *
. *              V5.01.01 11/06/92 J.Tan   SRF 114981      *
. *                       Fixed if the 2 dates have  more  *
. *                       than one year difference         *
. *                                                        *
. *                                                        *
. **********************************************************
.
.                READ TRANSFER FILE
.
NHOSPDAY MOVE           SP8,STDATE
        MOVE            SP1,STMOVE
        MOVE            ZERO,NBRDAYS
        MOVE            ZERO,CALDAYS
.
        COMPARE         THREE,ASTAT
        GOTO            NOSAME IF NOT EQUAL
.
        MATCH           ADATE,DDATE
        GOTO            NOSAME IF NOT EQUAL
.
        MATCH           FROMDATE,ADATE
        RETURN          IF LESS
.
        MATCH           ADATE,TODATE
        RETURN          IF LESS
.
        IF               NHOSDAYC=1
          MOVE            ONE,NBRDAYS         * Sameday patient LOS = 1 day
        ELSE
          MOVE            ZERO,NBRDAYS        * Sameday patient LOS = 0 days
        ENDIF
        RETURN
.
NOSAME  PACK            KEY30 WITH AADMNO,SP20
        CALL            RDSTRAN2
NEXTTN  CALL            RDKTRAN2
        BRANCH          OVRCD OF XXXX
.
        MATCH           TADMN,AADMNO
        GOTO            XXXX IF NOT EQUAL
.
        MOVE            TMOVE,STMOVE
        REP             " 0",TDATE
.
        MATCH           "A",TMOVE
        GOTO            ARITYPE IF EQUAL
        MATCH           "R",TMOVE
        GOTO            ARITYPE IF EQUAL
        MATCH           "L",TMOVE
        GOTO            LOTYPE IF EQUAL
        MATCH           "D",TMOVE
        GOTO            NEXTTN IF NOT EQUAL
.
        MATCH           FROMDATE,TDATE
        RETURN          IF LESS
        MATCH           TODATE,TDATE
        GOTO            XXXX IF LESS
        MOVE            TODATE,TDATE  
        GOTO            XXXX
.
.
.
ARITYPE MATCH           "A",TMOVE
        GOTO            CONARI IF NOT EQUAL
.
        MATCH           TDATE,ADATE
        GOTO            CONARI IF EQUAL
.
.       PRINT           *N,"*** Admission : ",AADMNO," Date is ",ADATE:
.                       "   Transfer File Admit Date is ",TDATE:
.                       "Contact I.B.A *** NOW ***":
.                       " (NHOSPDAY)"
.
        DISPLAY         *P1:23,*EL,*V2LON,*B:
                        "*** Admission : ",AADMNO:
                        "   Transfer and Admission date DIFFERENT ***",*W
.
CONARI  MATCH           TODATE,TDATE
        RETURN          IF NOT LESS
        MATCH           FROMDATE,TDATE
        GOTO            CONT30 IF NOT LESS
        MOVE            FROMDATE,STDATE
        GOTO            NEXTTN 
.
CONT30   MOVE            TDATE,STDATE
        GOTO            NEXTTN
.
LOTYPE  MATCH           FROMDATE,TDATE
        GOTO            NEXTTN IF LESS
.
        MATCH           TODATE,TDATE
        GOTO            CONT8 IF LESS
.
          DAYSEP    STDATE,TODATE,CALDAYS
          ADD       CALDAYS,NBRDAYS
        RETURN
.
CONT8     DAYSEP    STDATE,TDATE,CALDAYS
          ADD       CALDAYS,NBRDAYS
        GOTO            NEXTTN
.
XXXX    MATCH           "L",STMOVE
        RETURN          IF EQUAL
        MATCH           " ",STMOVE
        RETURN          IF EQUAL
.
.         this is the last record
.
        MATCH           "D",STMOVE
        GOTO            CONT31 IF NOT EQUAL
.
.       PATIENT IS DISCHARGED
.
          DAYSEP    STDATE,TDATE,CALDAYS
          ADD       CALDAYS,NBRDAYS
          RETURN
.
.        PATIENT IS CURRENTLY ADMITTED
.
CONT31    DAYSEP    STDATE,TODATE,CALDAYS
          ADD       CALDAYS,NBRDAYS
        RETURN
