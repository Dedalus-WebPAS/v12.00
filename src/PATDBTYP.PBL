.PATDBTYP.PBL
.
         DEFPROC    PATDBTYP
.
         INC        PATAFEFD/INC
         INC        PATTRNFD/INC
.
.******************************************************************************
.        PATDBTYP : Calculate the number of bed days for a given bed type
.
.        Parameter: SAVBTYP - selected bed type
.                   TODATE  - ending date
.        Return   : NBRDAYS - Number of bed days
.
.        Modifications :
.       V12.00.01 22/05/2025  J.Tan          TSK 0955096
.                 Added Alphanumeric visit number changes
.       V10.13.02 28/07/2019  J.Tan             TSK 0875639
.                 Fixed to read health fund for the HF Table type
.       V10.13.01 18/09/2018  J.Tan             TSK 0961312
.                 Fixed No pay day to calculate bed days from PCENDDTE
.       V10.11.01 21/08/2017  J.Tan             TSK 0836172
.                 Mod to No pay day (PCENDDTE)
.       V10.10.03 13/06/2017  J.Tan             TSK 0839991
.                 Fixed checking for blank PTFEACIN - Non Accum Inlier
.       V10.10.02 17/05/2017  J.Tan             TSK 0297904
.                 Mod for Non-Accumulative inlier ICU/CCU rule
.       V10.10.01 04/04/2017 J.Tan  TSK 0297904
.                 Mod for Non-Accumulative inlier ICU/CCU rule
.       V10.03.01 21/11/12 J.Tan   CAR  262673
.                 Mods checking for Qualified days (Catg A - TCINDC12 spaces)
.        V9.12.01 08/09/09 J.Tan   CAR  205303
.                 Added Casemix Contract ID
.        V9.09.01 20/08/07 J.Tan   CAR  144656
.                 Mods for Non-Accumulative Inliers
.        V9.02.01 30/10/02 J.Tan
.                 Fixed NoPayDay called from PATCATBI
.
.*******************************************************************************
DBTP0000 OPEN       PATTRAN2,"pattranf"
         MOVE       SP10,WDATE1             * initialise the start date
         MOVE       SP10,WDATE2             * initialise the end date
         MOVE       ZERO,NBRDAYS
         CALL       CINL0000                * Check Non-Accumulative Inliers
.
         COMPARE    THREE,ASTAT             * discharged ?
         GOTO       DBTP1000 IF NOT EQUAL   * no.
.
         MATCH      ADATE,DDATE             * day case patient ?
         GOTO       DBTP1000 IF NOT EQUAL   * no.
.
         PACK       KEY30,AADMNO,Z70
         CALL       RDSTRAN2
         CALL       RDPTRAN2
         BRANCH     OVRCD,DBTP9000
         MATCH      TADMN,AADMNO            * same admission ?
         GOTO       DBTP9000 IF NOT EQUAL   * No.
.
         MATCH      "1",PTFEACIN
         GOTO       DBTP0800 IF EQUAL        * Non-Accum.Inlier is set to Yes
.
         MATCH      "0",PTFEACIN
         GOTO       DBTP0600 IF EQUAL        * Non-Accum.Inlier is set to No
         MATCH      SP1,PTFEACIN
         GOTO       DBTP0600 IF EQUAL        * Non-Accum.Inlier is set to No
.
         CALL       CCIC0000                 * No reset - Calc. ICU/CCUdays
         BRANCH     EXIT,DBTP9000
         GOTO       DBTP0800
.
.   Non Accum. field is not ticked ie. Reset
.
DBTP0600 MATCH      SAVBTYP,TRBTYP          * same bed type ?
         GOTO       DBTP9000 IF NOT EQUAL
.
.   Non Accum. field is ticked ie. No Reset
.
DBTP0800 PACK       KEY5,ANSA,SP1,TATYPE
         CALL       RDCODE1
         IF         OVRCD=0
           MATCH      SP70,TCINDC12         * Unqualified days?
           GOTO       DBTP9000 IF NOT EQUAL
         ENDIF
.
         MATCH      ADATE,TODATE
         GOTO       DBTP9000 IF LESS
.
         MOVE       ONE,NBRDAYS             * no of bed days is one for daycase
         GOTO       DBTP9000
.
DBTP1000 IF         NPDAYFLG<>0
           PACK       KEY30,AADMNO,PCENDDTE,SP20,SP10
           MOVE       PCENDDTE,WDATE1            * set from date
         ELSE
           PACK       KEY30,AADMNO,SP20,SP10
         ENDIF
         CALL       RDSTRAN2
DBTP1200 CALL       RDKTRAN2
         BRANCH     OVRCD,DBTP4000
.
         MATCH      TADMN,AADMNO            * same admission ?
         GOTO       DBTP4000 IF NOT EQUAL
.
         REP        " 0",TDATE
         MOVE       TDATE,STDATE
         MATCH      ANSA,TMOVE              * Admission record ?
         GOTO       DBTP2000 IF EQUAL
.
         MATCH      ANSR,TMOVE              * Return record ?
         GOTO       DBTP2000 IF EQUAL
.
         CMATCH     ANSC,TMOVE              * Change record ?
         GOTO       DBTP2200 IF EQUAL
         GOTO       DBTP3000
.
.        Admission/Return record
.
DBTP2000 MATCH      TDATE,TODATE
         GOTO       DBTP9000 IF LESS
.
         MATCH      "1",PTFEACIN
         GOTO       DBTP2140 IF EQUAL        * Non-Accum.Inlier is set to Yes
.
         MATCH      "0",PTFEACIN
         GOTO       DBTP2120 IF EQUAL        * Non-Accum.Inlier is set to No
         MATCH      SP1,PTFEACIN
         GOTO       DBTP2120 IF EQUAL        * Non-Accum.Inlier is set to No
.
         CALL       CCIC0000                 * No reset - Calc. ICU/CCUdays
         BRANCH     EXIT,DBTP1200
         GOTO       DBTP2140
.
.        Reset
.
DBTP2120 MATCH      SAVBTYP,TRBTYP          * same bed type ?
         GOTO       DBTP1200 IF NOT EQUAL
.
.        No reset
.
DBTP2140 PACK       KEY5,ANSA,SP1,TATYPE
         CALL       RDCODE1
         IF         OVRCD=0
           MATCH      SP70,TCINDC12          * Unqualified days ?
           GOTO       DBTP1200 IF NOT EQUAL
         ENDIF
.
         MOVE       TDATE,WDATE1            * set from date
         GOTO       DBTP1200                * get next trans record
.
.        Change
.
DBTP2200 MATCH      TDATE,TODATE
         IF         @LESS
           MATCH      SP6,WDATE1              * Check with the fromdate
           GOTO       DBTP9000 IF EQUAL
           MOVE       TODATE,WDATE2           * set to date
           CALL       CBDY0000                * Calculate bed days
           GOTO       DBTP9000
         ENDIF
.
         MATCH      "1",PTFEACIN
         GOTO       DBTP2240 IF EQUAL        * Non-Accum.Inlier is set to Yes
.
         MATCH      "0",PTFEACIN
         GOTO       DBTP2220 IF EQUAL        * Non-Accum.Inlier is set to No
         MATCH      SP1,PTFEACIN
         GOTO       DBTP2220 IF EQUAL        * Non-Accum.Inlier is set to No
.
         CALL       CCIC0000                 * No reset - Calc. ICU/CCUdays
         BRANCH     EXIT,DBTP2400
         GOTO       DBTP2240
.
DBTP2220 MATCH      SAVBTYP,TRBTYP          * same bed type ?
         GOTO       DBTP2400 IF NOT EQUAL
.
DBTP2240 PACK       KEY5,ANSA,SP1,TATYPE
         CALL       RDCODE1
         IF         OVRCD=0
           MATCH      SP70,TCINDC12        * Unqualified days
           GOTO       DBTP2400 IF NOT EQUAL
         ENDIF
.
.        Change to the selected bed type
.
         MATCH      SP8,WDATE1            * check if it was a real change of
         GOTO       DBTP1200 IF NOT EQUAL   * bed type
.
         MOVE       TDATE,WDATE1          * set fromdate of the change bedtype
         GOTO       DBTP1200
.
.        Change to a different bed type, check if it was from selected bedtype
.
DBTP2400 MATCH      SP8,WDATE1           
         GOTO       DBTP1200 IF EQUAL
.
         MOVE       TDATE,WDATE2
         CALL       CBDY0000                * Calculate bed days
         GOTO       DBTP1200                * get next trans record
.
.        Discharge/Leave record always on the same bed type as previous record
.
DBTP3000 MATCH      SP8,WDATE1
         GOTO       DBTP3200 IF EQUAL       * Was not on selected bed type
.
         MATCH      TDATE,TODATE
         IF         @LESS
           MOVE       TODATE,WDATE2         * set to end date
         ELSE
           MOVE       TDATE,WDATE2          * Set to date
         ENDIF
         REP        " 0",WDATE2
         CALL       CBDY0000                * Calculate bed days
.
DBTP3200 CMATCH     "D",TMOVE
         GOTO       DBTP1200 IF NOT EQUAL   * get next record
         GOTO       DBTP9000
.
.        check the last record
.
DBTP4000 MATCH      SP8,WDATE1
         GOTO       DBTP9000 IF EQUAL       * Was not on selected bed type
.
         MOVE       TODATE,WDATE2
         REP        " 0",WDATE2
         CALL       CBDY0000                * Calculate bed days
.
DBTP9000 CLOSE      PATTRAN2
DBTP9999 GOTO       ENDP9999
+
.******************************************************************************
.        CBDY0000 - Calculate dates difference
.******************************************************************************
CBDY0000 PACK       CDYSFDTE,WDATE1
         PACK       CDYSTDTE,WDATE2
         MOVE       ZERO,CDYSDAYS
         CALL       CALCDAYS
         MOVE       CDYSDAYS,CALDAYS
         SUB        ONE,CALDAYS
         ADD        CALDAYS,NBRDAYS
         MOVE       SP10,WDATE1
         MOVE       SP10,WDATE2
CBDY9999 RETURN
+
.******************************************************************************
.        CINL0000 - Check Non-Accumulative Inliers
.******************************************************************************
CINL0000  OPEN      PATAFEA1,"patafeaf"
.
          MOVE      SP10,PTHFBCAT           * Initialise table type
          MATCH     SP70,AFUNDH
          IF        !@EQUAL
            OPEN      PATFN1A1,"patfn1af"
            PACK      KEY14,AFUNDH,AFNDTB,SP20
            CALL      RDFUND1                 * read health fund file
          ENDIF
.
          MOVE      ZERO,FORM1
          PACK      SDIM30,CONTRCID,ACLAIM,AFUNDH,PTHFBCAT,CMCODE,SAVBTYP,SP70
CINL1000  PACK      KEY34,SDIM30
          CALL      RSPTAFEA1
          CALL      RKPTAFEA1
          BRANCH    OVRCD,CINL9000
.
          PACK      DIM30,PTFECNID,PTFECLMN,PTFEHFND,PTFETABT,PTFECASM:
                          PTFEBTYP,SP70
          MATCH     DIM30,SDIM30
          GOTO      CINL4000 IF EQUAL
.
          ADD       ONE,FORM1
          BRANCH    FORM1,CINL2000,CINL3000
          GOTO      CINL9000
.
.         Try with no health fund
.
CINL2000  PACK      SDIM30,CONTRCID,ACLAIM,SP6,SP3,CMCODE,SAVBTYP,SP70
          GOTO      CINL1000
.
.         Try with default claim code
.
CINL3000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY;*193,PTCNDCLM
          CALL      DCLM0000
          PACK      SDIM30,CONTRCID,PTCNDCLM,SP6,SP3,CMCODE,SAVBTYP,SP70
          GOTO      CINL1000
.
CINL4000  MOVE      PTFEEDAY,SAVFEDAY     * Save End day
          GOTO      CINL9999
.
CINL9000  MOVE      SP70,PTFEACIN         * Accumulative Inliers
CINL9999  RETURN
+
.*******************************************************************************
.        Check for ICU/CCU bed type
.*******************************************************************************
CCIC0000 PACK       KEY5,ANSB,ANST,TRBTYP
         CALL       RDCODE1
         BRANCH     OVRCD,CCIC9000
.
         MATCH      "1",TCINDC9
         GOTO       CCIC8000 IF EQUAL          * ICU/CCU
         MATCH      "3",TCINDC9
         GOTO       CCIC9000 IF NOT EQUAL      * ICU/CCU
.
CCIC8000 MOVE       ZERO,EXIT
         GOTO       CCIC9999
.
CCIC9000 MOVE       ONE,EXIT
CCIC9999 RETURN
+
         INC       IBAOVRIO/INC
         INC       PATAFEIO/INC
         INC       PATTRNIO/INC
.
ENDP9999 ENDPROC
