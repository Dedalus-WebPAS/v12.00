.------------------------------------------------------------
.V12.00.02 11/09/2025 Nikitha B      TSK 0965462
.          Modified at MYEMR100 for mode of arrival not to be mandatory
.          for telehealth CAT YM Ind - 2/6.
.V12.00.01 15.05.2025  DA SArkies    TSK 0955096         
.          Updated for Alpha-Numeric Visit Number             
.V10.14.01 28/08/2019 Ebon Clements  TSK 0880096
.          Changed mandatory field 178 to check the new indicator 9 = "N"
.          to indicate the diagnosis is mandatory for the nurse. NOTE
.          indicator 3 = N - No diagnosis will take priority
.V10.13.01 08/01/2018 Ebon Clements  TSK 0866749
.          Set MANDINUR for ambulance handover date/time 176 and 177
.          so discharges without a doctor still report as mandatory
.V10.10.02 25/05/2017 Peter Vela     TSK 0833345
.          Modified Alcohol Data Capture business rules in MYEMR200 
.V10.10.01 09/05/2017 Peter Vela     TSK 0833345
.          Added Alcohol Data visxdcaf - 179
.V10.08.01 27/05/2016 Jill Parkinson TSK 0814124
.          Changed emrmtxaf to use emic9cod instead of emicvemd
.V10.07.01 09/10/2015 Peter Vela    CAR 317170
.          Added EMVIPURC - HP Purchaser - 134
.V10.04.02 22/05/2014 Peter Vela    CAR 292610
.          Added Free text field (Diagnosis on clinical screen) emrdcmaf
.V10.04.01 12/03/2014 Ebon Clements CAR 294181
.          Added ambulance arrival date and time
.V10.03.01 14/03/2013 J.Tan         CAR 281349
.          Added Clinical Notes from Comment type 001 in vismdtaf (173)
. V9.12.01 03/03/2010 Ebon Clements CAR 215823
.          Added arrived by ambulance test to ambulance case number (71)
.          Added off stretcher time (172)
. V9.09.01 29/10/2007 Peter Vela    CAR 151159
.          Added new Emergency Cancellation status
. V9.04.00 10/03/2005 Ebon Clements CAR 58443
.          Changed item 23 EMVIPLIN (Place when injury occured) to use MCHINJ00
. V9.02.02 11/12/2002 Ebon Clements SRF 23449
.          Changed MICD0000 to use EMVIADMN not ADMISSNO    
. V9.02.01 25/11/2002 Ebon Clements SRF 23449
.          Recompiled for EMRCHMAN - Z099 primary diagnosis
.------------------------------------------------------------
.         MYEMR000  Mandatory field checking
.------------------------------------------------------------
MYEMR000  LOAD      DIM80,FORM5,EMVIDATE,EMVITIME,EMVITRIG,EMVICOM1,EMVICOM2:
                                EMVILOCN,EMVIDOCT,EMVICOMP,EMVICLAS,EMVIINSR: 10
                                EMVISRCE,EMVISITN,EMVIMODE,EMVIADMT,EMVIDURN:
                                EMVIEMPL,EMVIWAIT,EMVIPREV,EMVIFUND,EMVITABL: 20
                                EMVIREFG,EMVIREFD,EMVIPLIN,EMVIINIT,EMVIRTAS:
                                EMVIUC01,EMVIUC02,EMVIUC03,EMVIUC04,EMVIUC05: 30
                                EMVIUC06,EMVIUC07,EMVIUC08,EMVIUC09,EMVIUC10:
                                EMVIUC11,EMVIUC12,EMVIUC13,EMVIUC14,EMVIUC15: 40
                                EMVIUC16,EMVIUC17,EMVIUC18,EMVIUC19,EMVIUC20:
                                EMVIUC21,EMVIUC22,EMVIUC23,EMVIUC24,EMVIUC25: 50
                                EMVIUC26,EMVIUC27,EMVIUC28,EMVIUC29,EMVIUC30:
                                EMVIUD01,EMVIUD02,EMVIUD03,EMVIUD04,EMVIUD05: 60
                                EMVIUD06,EMVIUD07,EMVIUD08,EMVIUD09,EMVIUD10:
                                EMVIUT01,EMVIUT02,EMVIUT03,EMVIUT04,EMVIUT05: 70
                                EMVIUT06,EMVIUT07,EMVIUT08,EMVIUT09,EMVIUT10:
                                EMVIYN01,EMVIYN02,EMVIYN03,EMVIYN04,EMVIYN05: 80
                                EMVIYN06,EMVIYN07,EMVIYN08,EMVIYN09,EMVIYN10:
                                EMVITXT1,EMVITXT2,EMVITXT3,EMVITXT4,EMVITXT5: 90
                                EMVIUN01,EMVIUN02,EMVIUN03,EMVIUN04,EMVIUN05:
                                EMVIDDAT,EMVIDTIM,EMVIDSTA,EMVIOPR1,EMVIOPR2:100
                                EMVIELAP,EMVINSDT,EMVINSTM,EMVIDRDT,EMVIDRTM:
                                EMVIPATT,EMVIAMBL,EMVIREGN,EMVIUR01,EMVIUR02:110
                                EMVIUR03,EMVIUR04,EMVIUR05,EMVIPADM,EMVIEWRD:
                                EMVIEBED,EMVIHFMN,EMVIACLS,EMVICCLS,EMVITRNS:120
                                EMVITRDT,EMVITRTM,EMVIATNS,EMVIEXLC,EMVISITE:
                                EMVITSRC,EMVIDEST,EMVISENT,EMVIINJ1,EMVINATI:130
                                EMVIINJC,EMVIHUMI,EMVIACTI,EMVIPURC
.
          COMPARE   SEVENTY7,FORM5
          GOTO      MYEMR100 IF NOT EQUAL
.
          IF        EMVISTAT<>1 & EMVISTAT<>4
            MATCH     SP70,EMVIPADM
            IF        @EQUAL
              COMPARE   ZERO,EMVIYN02
              GOTO      MYEMR900 IF NOT EQUAL
            ENDIF
          ENDIF
.
MYEMR100  IF        FORM5=13 & PTCNHDPS=3
          PACK      KEY5,ANSY,ANSW,EMVIVSTY,SP70
            CALL      RDCODE1
            BRANCH    OVRCD,MYEMR950
.
            UNPACK    THCSCOD,D1
            MOVE      ZERO,F1
            MOVE      D1,F1
            IF        F1 = 2 | F1 = 6
              GOTO      MYEMR950         * Not Mandatory for telehealth
            ENDIF
          ENDIF
.
          IF        FORM5=166
            MOVE      ZERO,PRDGFLAG      * check for procedure and diagnosis
            CALL      VICD0000           * ICD Validation
            BRANCH    EXIT,MYEMR900
            GOTO      MYEMR950
          ENDIF
.
          IF        FORM5=167
            CALL      ZERUR000           * Check for Zero UR
            BRANCH    EXIT,MYEMR900
            GOTO      MYEMR950
          ENDIF
.
          IF        FORM5=168
            CALL      ADUNIT00           * Check for admission unit
            BRANCH    EXIT,MYEMR900
            GOTO      MYEMR950
          ENDIF
.
          IF        FORM5=169
            MOVE      ONE,PRDGFLAG
            CALL      VICD0000           * Check for diagnosis only
            BRANCH    EXIT,MYEMR900
            GOTO      MYEMR950
          ENDIF
.
          IF        FORM5=170
            MOVE      TWO,PRDGFLAG
            CALL      VICD0000           * Check for procedure unit
            BRANCH    EXIT,MYEMR900
            GOTO      MYEMR950
          ENDIF
.
          IF        FORM5=171
            CALL      MICD0000        * Check for Mandatory Additional Diagnosis
            BRANCH    EXIT,MYEMR900
            GOTO      MYEMR950
          ENDIF
.
          IF        (FORM5 > 128) & (FORM5 < 134) | (FORM5 = 23)
            CALL      MCHINJ00
            BRANCH    EXIT,MYEMR950
            GOTO      MYEMR999
          ENDIF
.
          IF        FORM5=162
            PACK      KEY17,EMVIADMN,SP20      * procedures
            CALL      RSEMVPR1                 * position in file ?
            CALL      RKEMVPR1                 * read next record
            BRANCH    OVRCD,MYEMR900           * eof - field blank
            MATCH     EMVIADMN,EMVPADMN        * same visit still ?
            GOTO      MYEMR900 IF NOT EQUAL    * no - field blank
            GOTO      MYEMR950
          ENDIF
.
          IF        FORM5 = 163
            PACK      KEY14,EMVIADMN,SP70    * clinical notes
            CALL      RSVSCMT1               * position in file ?
            CALL      RKVSCMT1               * read next record
            BRANCH    OVRCD,MYEMR900         * eof - field blank
            MATCH     DEMVIADM,VSCTVIST      * same visit still ?
            GOTO      MYEMR900 IF NOT EQUAL  * no - field blank
            GOTO      MYEMR950
          ENDIF
.
          IF  FORM5 = 96
            IF        EMVISTAT<>1 & EMVISTAT<>4
              MATCH     SP70,EMVIDDAT
              GOTO      MYEMR900 IF EQUAL  * no - field blank
              GOTO      MYEMR950
            ELSE
              GOTO      MYEMR950
            ENDIF
          ENDIF
.
          IF  FORM5 = 97
            IF        EMVISTAT<>1 & EMVISTAT<>4
              MATCH     SP70,EMVIDTIM
              GOTO      MYEMR900 IF EQUAL  * no - field blank
              GOTO      MYEMR950
            ELSE
              GOTO      MYEMR950
            ENDIF
          ENDIF
.
          IF  FORM5 = 98
            IF        EMVISTAT<>1 & EMVISTAT<>4
              MATCH     SP70,EMVIDSTA
              GOTO      MYEMR900 IF EQUAL  * no - field blank
              GOTO      MYEMR950
            ELSE
              GOTO      MYEMR950
            ENDIF
          ENDIF
.
          IF        FORM5 = 107
            CALL      MAMB0000                 * Check mandatory amb case number
            BRANCH    EXIT,MYEMR900
            GOTO      MYEMR950
          ENDIF
.
          IF        FORM5 = 172
            CALL      MSTR0000                 * Check mandatory off stretcher  
            BRANCH    EXIT,MYEMR900            * time
            GOTO      MYEMR950
          ENDIF
.
          IF        FORM5 = 173
            PACK      KEY17,EMVIADMN,SP70    * clinical notes
            CALL      RSVSMDT1               * position in file ?
            CALL      RKVSMDT1               * read next record
            BRANCH    OVRCD,MYEMR900         * eof - field blank
            MATCH     DEMVIADM,VSMDVISN      * same visit still ?
            GOTO      MYEMR900 IF NOT EQUAL  * no - field blank
            MATCH     "001",VSMDTYPE         * Clinical Notes
            GOTO      MYEMR900 IF NOT EQUAL
            GOTO      MYEMR950
          ENDIF
.
          IF        FORM5 = 174
            CALL      MAAD0000                 * Ambulance Arrival Date
            BRANCH    EXIT,MYEMR900
            GOTO      MYEMR950
          ENDIF
.
          IF        FORM5 = 175
            CALL      MAAT0000                 * Ambulance Arrival Time
            BRANCH    EXIT,MYEMR900
            GOTO      MYEMR950
          ENDIF
.
          IF        FORM5 = 176
            CALL      MAHD0000                 * Ambulance Handover Date
            BRANCH    EXIT,MYEMR900
            GOTO      MYEMR950
          ENDIF
.
          IF        FORM5 = 177
            CALL      MAHT0000                 * Ambulance Handover Time
            BRANCH    EXIT,MYEMR900
            GOTO      MYEMR950
          ENDIF
.
          IF         FORM5 = 178
.           PACK       KEY8,EMVIADMN,SP70
.           CALL       RDEMDCM1
.           BRANCH     OVRCD,MYEMR200
.
.           MATCH      SP70,EMDCTXT1
.           GOTO       MYEMR950 IF NOT EQUAL
.
MYEMR200    MATCH      TCINDC3,ANSN         * No diagnosis Required
            GOTO       MYEMR950 IF EQUAL
.
            MATCH      SP70,EMVIDOCT
            GOTO       MYEMR950 IF NOT EQUAL
.
            MATCH      SP70,EMVIDSTA
            GOTO       MYEMR950 IF EQUAL
.
            PACK       KEY5,ANSE,ANSD,EMVIDSTA,SP70
            CALL       RDCODE1
            BRANCH     OVRCD,MYEMR950
.
            MATCH      TCINDC9,ANSN          * No doctor allocated diagnosis is
            GOTO       MYEMR950 IF NOT EQUAL * Mandatory for the Nurse
.
            MOVE      ONE,PRDGFLAG
            CALL      VICD0000           * Check for diagnosis only
            IF        ONE=EXIT
              MOVE      ONE,MANDINUR
              GOTO      MYEMR900
            ENDIF
            GOTO      MYEMR950
.
          ENDIF
.
          IF         FORM5 = 179
            COMPARE    ONE,VISXDCIN
            GOTO       MYEMR950 IF EQUAL
.
            MATCH      SP70,EMVIUC27
            GOTO       MYEMR950 IF EQUAL
            GOTO       MYEMR950 IF EOS
.
            PACK       KEY5,CATES,EMVIUC27,SP70
            CALL       RDCODE1
            BRANCH     OVRCD,MYEMR950
.
            MATCH      "Y",TCINDC1
            GOTO       MYEMR950 IF NOT EQUAL
.
            PACK       KEY11,EMVIADMN,ZERO,ZERO,ONE,SP70
            CALL       RDVSXDC1
            BRANCH     OVRCD,MYEMR900
.
            MATCH      "1",VSXDDELT
            GOTO       MYEMR900 IF EQUAL
.
            MATCH      SP70,VSXDVAL1
            GOTO       MYEMR950 IF NOT EQUAL
.
            MATCH      SP70,VSXDCOD1
            GOTO       MYEMR950 IF NOT EQUAL
.
            PACK       DIM100,SP70,SP70
            MATCH      DIM100,VSXDTXT1
            GOTO       MYEMR950 IF NOT EQUAL
.
            GOTO       MYEMR900 
.
          ENDIF
.
          GOTO       MYEMR999                  * field has data
.
MYEMR900  PACK       DIM80,SP70,SP70           * feild has no data
          GOTO       MYEMR999
.
MYEMR950  MOVE       "VALID DATA AVAILABLE",DIM80   * Injury data mandatory
.
MYEMR999  RETURN
+
.------------------------------------------------------------
. Check for Injury Survellence
.------------------------------------------------------------
MCHINJ00  PACK      KEY14,EMVIADMN,TYPE5,SP20    * diagnoses
          CALL      RSEMVCD1               * position in file ?
          CALL      RKEMVCD1               * read next record
          BRANCH    OVRCD,MCHINJ90         * eof - field blank
          MOVE      EMVIADMN,KEY8
          MATCH     KEY8,EMVCVIST      * same visit still ?
          GOTO      MCHINJ90 IF NOT EQUAL  * no - field blank
          MATCH     TYPE5,EMVCTYPE
          GOTO      MCHINJ90 IF NOT EQUAL  * no - field blank
.
          PACK      KEY9,EMVCMNCD,SP70
          CALL      RDEMICD1
          BRANCH    OVRCD,MCHINJ90         * eof - field blank
.
          PACK      KEY10,EMIC9COD,SP70
          CALL      RSEMMX3
          CALL      RKEMMX3
          BRANCH    OVRCD,MCHINJ90         * eof - field blank
          MATCH     EMMXPDG,EMIC9COD
          GOTO      MCHINJ90 IF NOT EQUAL  * no - field blank
.
          MOVE      ZERO,EXIT
          GOTO      MCHINJ99
.
MCHINJ90  MOVE      ONE,EXIT
.
MCHINJ99  RETURN
.
.------------------------------------------------------------
. Check mandatory procedure details
.------------------------------------------------------------
MICD0000  MOVE      ZERO,EXIT
          MOVE      SP70,XPRIDIAG
          MOVE      SP70,XADDIAG1
.
          PACK      KEY14,EMVIADMN,TYPE5,SP70
          CALL      RSEMVCD1
MICD1000  CALL      RKEMVCD1
          BRANCH    OVRCD,MICD3000
.
          MATCH     DEMVIADM,EMVCVIST
          GOTO      MICD3000 IF NOT EQUAL
.
          MATCH     TYPE5,EMVCTYPE          * Emergency diagnosis ?
          GOTO      MICD3000 IF NOT EQUAL
.
          MATCH     "000",EMVCSEQU
          GOTO      MICD1000 IF NOT EQUAL
.
          MOVE      EMVCMNCD,KEY9
          CALL      RDEMICD1
          BRANCH    OVRCD,MICD3000
.
          PACK      XPRIDIAG,EMICVEMD,SP70
.
MICD3000  PACK      KEY14,EMVIADMN,TYPE5,SP70
          CALL      RSEMVCD1
MICD3500  CALL      RKEMVCD1
          BRANCH    OVRCD,MICD4000
.
          MATCH     DEMVIADM,EMVCVIST
          GOTO      MICD4000 IF NOT EQUAL
.
          MATCH     TYPE5,EMVCTYPE          * Emergency diagnosis ?
          GOTO      MICD4000 IF NOT EQUAL
.
          MATCH     "000",EMVCSEQU          * Skip primary diagnosis
          GOTO      MICD3500 IF EQUAL
.
          MATCH     "1",EMVCEFLG            * Send in extract
          GOTO      MICD3500 IF NOT EQUAL
.
          MOVE      EMVCMNCD,KEY9
          CALL      RDEMICD1
          BRANCH    OVRCD,MICD4000
.
          PACK      XADDIAG1,EMICVEMD,SP70
.
MICD4000  MATCH     "Z099",XPRIDIAG
          IF        @EQUAL
            MATCH     SP70,XADDIAG1
            GOTO      MICD9900 IF EQUAL
          ENDIF
.
          GOTO      MICD9999
.
MICD9900  MOVE      ONE,EXIT
.
MICD9999  RETURN
.
.------------------------------------------------------------
. Check to see if the ambulance case number is required
.------------------------------------------------------------
MAMB0000  MOVE      ZERO,EXIT
.
          MATCH     SP70,EMVIMODE          
          GOTO      MAMB9999 IF EQUAL
.
          PACK      KEY5,ANSE,ANSA,EMVIMODE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,MAMB9999
.
          MATCH     ANSA,TCINDC1            * Arrived by ambulance
          GOTO      MAMB9999 IF NOT EQUAL
.
          MATCH     SP70,EMVIAMBL           * Amb case number populated
          GOTO      MAMB9999 IF NOT EQUAL
.
          MOVE      ONE,EXIT                * Amb case number is outstanding
.
MAMB9999  RETURN
.
.------------------------------------------------------------
. Check to see if the ambulance arrival date is required
.------------------------------------------------------------
MAAD0000  MOVE      ZERO,EXIT
.
          MATCH     SP70,EMVIMODE
          GOTO      MAAD9999 IF EQUAL
.
          PACK      KEY5,ANSE,ANSA,EMVIMODE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,MAAD9999
.
          MATCH     ANSA,TCINDC1            * Arrived by ambulance
          GOTO      MAAD9999 IF NOT EQUAL
.
          MATCH     SP70,EMVIAADT           * Ambulance arrival date
          GOTO      MAAD9999 IF NOT EQUAL
.
          MOVE      ONE,EXIT                * Amb arrival date is outstanding
.
MAAD9999  RETURN
.
.------------------------------------------------------------
. Check to see if the ambulance arrival time is required
.------------------------------------------------------------
MAAT0000  MOVE      ZERO,EXIT
.
          MATCH     SP70,EMVIMODE
          GOTO      MAAT9999 IF EQUAL
.
          PACK      KEY5,ANSE,ANSA,EMVIMODE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,MAAT9999
.
          MATCH     ANSA,TCINDC1            * Arrived by ambulance
          GOTO      MAAT9999 IF NOT EQUAL
.
          MATCH     SP70,EMVIAATM           * Ambulance arrival time
          GOTO      MAAT9999 IF NOT EQUAL
.
          MOVE      ONE,EXIT                * Amb arrival time is outstanding
.
MAAT9999  RETURN
.
.------------------------------------------------------------
. Check to see if the ambulance handover date is required
.------------------------------------------------------------
MAHD0000  MOVE      ZERO,EXIT
.
          MATCH     SP70,EMVIMODE
          GOTO      MAHD9999 IF EQUAL
.
          PACK      KEY5,ANSE,ANSA,EMVIMODE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,MAHD9999
.
          MATCH     ANSA,TCINDC1            * Arrived by ambulance
          GOTO      MAHD9999 IF NOT EQUAL
.
          MATCH     SP70,EMVIAHDT           * Ambulance handover date
          GOTO      MAHD9999 IF NOT EQUAL
.
          MOVE      ONE,EXIT                * Amb handover date is outstanding
          MOVE      ONE,MANDINUR            * Set mandatory input
.
MAHD9999  RETURN
.
.------------------------------------------------------------
. Check to see if the ambulance handover time is required
.------------------------------------------------------------
MAHT0000  MOVE      ZERO,EXIT
.
          MATCH     SP70,EMVIMODE
          GOTO      MAHT9999 IF EQUAL
.
          PACK      KEY5,ANSE,ANSA,EMVIMODE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,MAHT9999
.
          MATCH     ANSA,TCINDC1            * Arrived by ambulance
          GOTO      MAHT9999 IF NOT EQUAL
.
          MATCH     SP70,EMVIAHTM           * Ambulance handover time
          GOTO      MAHT9999 IF NOT EQUAL
.
          MOVE      ONE,EXIT                * Amb handover time is outstanding
          MOVE      ONE,MANDINUR            * Set mandatory input
.
MAHT9999  RETURN
.
.------------------------------------------------------------
. Check to see if the off stretcher time is required. 
.------------------------------------------------------------
MSTR0000  MOVE      ZERO,EXIT
.
          MATCH     SP70,EMVIMODE
          GOTO      MSTR9999 IF EQUAL
.
          PACK      KEY5,ANSE,ANSA,EMVIMODE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,MSTR9999
.
          MATCH     ANSA,TCINDC1            * Arrived by ambulance
          GOTO      MSTR9999 IF NOT EQUAL
.
          MATCH     SP70,EMVIUT06           * Off stretcher time populated
          GOTO      MSTR9999 IF NOT EQUAL
.
          MOVE      ONE,EXIT                * Off stretcher time is outstanding
.
MSTR9999  RETURN
