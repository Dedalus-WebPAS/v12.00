.**************************************************************************
.*  DGPMIENQ  :  PMI Enquiry for a Datagate request                       *
.*               ~~~~~~~~~~~                                              *
.*                                                                        *
.*  Author    :  ROD   (19/10/95)                                         *
.*  Mods      :                                                           *
.*        V9.02.03  14/11/2002  Davin                                     *
.*                  Removed reference to $$DBASE WEB compile option       *
.*        V9.02.02  22/04/2002  Mike Laming                               *
.*                  Replace PMEDI with Composite variable PMEDIXX (which  *
.*                  contains PMEDI+PTMXMCCD) Add call (SPLTPMED)to split  *
.*                  these (WEB based Server only                          *
.*        V9.02.01  26/09/2001 (18/06/2001)  Mike Laming                  *
.*                  Add fields from PMI Master Extension 2 - PMSPX2       *
.*                  data fields PMPXINTR -> PMPXFREL (for RCH & SVHM)     *
.*                  for WEB based versions only                           *
.*        V5.08.01  27/01/2000  Steve Armstrong                           *
.*                  Removed check on hospital number when looping through *
.*                  patnidaf as PATNIDIO always writes/updates with       *
.*                  hospital id set to "1".                               *
.**************************************************************************
.*            V5.07.01 22/12/1998  Davin                                  *
.*                     Y2K mods                                           *
.*            V5.07.00 26/10/1998  Davin                                  *
.*                     Mods for 507                                       *
.**************************************************************************
.*            V5.05.01 13/10/1997  Steve Armstrong     WHL                *
.*                     Added NHI Master fields.                           *
.*                     Mods for new Master extension file variables       *
.*                     (PTMXOPER - PTMXPOST)                              *
.**************************************************************************
.*            V5.04.02 12/05/1997  Steve Armstrong                        *
.*                     Fixed read of nhimasaf if using NHI                *
.*            V5.04.01 16/04/1997  Steve Armstrong                        *
.*                     Mods to use separate inbound & outbound comms      *
.*                     clients for PMI requests (DGATEIN/DGATEOUT)        *
.*                                                                        *
.**************************************************************************
DGPMIENQ  
 IFGE     $$VER,7
.
          MOVE      "ENQ",RPLYHEAD
          READ      DGATEOUT;PURNO,PTNINMPI
.
          MATCH     SP8,PURNO                     * do we have a U/R Number?
          GOTO      DGPEN100 IF EQUAL
.
. we have a U/R Number so get National Number
.
          CALL      PMIGTNID                      * get national number
          MOVE      NMPNUMB,PTNINMPI
          GOTO      DGPEN500
.
. no U/R number passed so make sure we are given a national number
.
DGPEN100  COMPARE   ZERO,PTCNNMPI                 * using link?
          GOTO      DGPEN780 IF EQUAL             * no
.
          MATCH     SP20,PTNINMPI                 * no UR No. so try for a Nat #
          GOTO      DGPEN850 IF EQUAL             * UR/Nat# missing, so Error
.
          BRANCH    PTCNNHII,DGPEN200             * using NHI link
.
. get U/R Number using National Number where NHI link is not used
.
          MOVE      PTNINMPI,KEY20                * save nmpi number
          PACK      KEY30,PTNINMPI,CHOSPNUM,SP30
          CALL      RSPTNID2
          CALL      RKPTNID2
          BRANCH    OVRCD,DGPEN750
.
          MATCH     KEY20,PTNINMPI                * still same National No.?
          GOTO      DGPEN750 IF NOT EQUAL
.
          MOVE      PTNILNUM,PURNO                * set up U/R Number
          GOTO      DGPEN500
.
.         Get U/R Number using National Number where NHI link is used.
.         The national number is only 7 characters in length and is returned
.         right justified.
.
DGPEN200  BUMP      PTNINMPI,TEN3                * get last 7 characters
          PACK      KEY7,PTNINMPI,SP7            * load national number
          RESET     PTNINMPI
          CALL      RDNHMAS1                     * NHI record on file ?
          BRANCH    OVRCD,DGPEN750               * no - display error
.
          MOVE      NHMAURNO,PURNO               * set up U/R Number
.
. now get details for this patient
.
DGPEN500  MOVE      PURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,DGPEN800                * missing U/R Number
.
          MOVE      "ENQ",RPLYHEAD
          MOVE      "00000",RPLYCODE              * successful message
          MOVE      SP50,RPLYDESC                 * clear error description
.
.     NB.  SP1 at end of reply has been added because the interpreter does
.     not place a "|" after the last field, and Datagate requires this.  By
.     putting an extra field on the end (which is blank), a "|" will then
.     be added after the last field.
.        
          PACK      CCENCODE,CCDATE,CCDST,CCSRC,CCNAME,REPLYRPL
.
          WRITE     DGATEIN;CCENCODE:
                          RPLYHEAD,RPLYCODE,RPLYDESC,DGMISC1,DGMISC2:
                          DGMISC3,PSTAT,PURNO,PTITL,PHOSP,PLDAYS,PBDEBT:
                          PSNAME,PGNAME,PPSNAME,PADD1,PADD2,PSUBRB,PADD4,PPOST:
                          PTELEP,PTELEB,PSEX,PMSTAT,PBDATE,PCONT,PREG,PTYPE:
                          POCCUP,PLDDATE,PMEDIXX,PPENNO,PPNDTE,PREPAT,PSMOK:
                          PMICRO,PCEASE,PCASE,PAUTOPY,PHIGH1,PHIGH2,PHIGH3:
                          PLOCDOC,PFUNDH,PFNDTB,PFNDMM,PUSR1,PUSR2,PUSR3:
                          PUSR4,PUSR5,PUSR6,PUYN1,PUYN2,PUYN3,PYRRES:
                          PNKNAME,PNKADD1,PNKADD2,PNKSUBR,PNKADD4,PNKPOST:
                          PNKTELP,PNKTELB,PNKRELP,PFNDYY,PFNDCG,PDECDTE:
                          PDIET,PINITHOS,PHKADUNI,PTMAEMPC,PTMANOKO:
                          PTMANOKE,PTMADCNO,PTMXRDAT,PTMXMEAL,PTMXLGA:
                          PTMXLS,PTMXLSDT,PTMXFHMD,PTMXMHU1,PTMXMHU2:
                          PTMXMHU3,PTMXECNM,PTMXECA1,PTMXECA2,PTMXECA3:
                          PTMXECA4,PTMXECPC,PTMXECPP,PTMXECBP,PTMXECRE:
                          PTMXNRNM,PTMXNRA1,PTMXNRA2,PTMXNRA3,PTMXNRA4:
                          PTMXNRPC,PTMXNRPP,PTMXNRBP,PTMXNRRE,PTMX3BEX:
                          PTMXCARD,PTMXCEXP,PTMXCXMP,PTMXFMLY,PTMXCHNM:
                          PTMXDOFR,PMPXRHC1,PMPXRH1G,PTMXSPID,PMPXR1GC:
                          PTMXOSDT,PTNINMPI:
                          NHMASURN,NHMAGIV1,NHMAGIV2,NHMAGIV3,NHMAPREI:
                          NHMAADD1,NHMAADD2,NHMAADD3,NHMAADD4,NHMAADD5:
                          NHMADOB,NHMADDTH,NHMADOMC,NHMARES,NHMAETH,NHMASEX:
                          NHMADAT,NHMATIM,NHMAETH2,NHMAETH3:
                          PTMXOPER,PTMXUKDO,PTMXCELL,PTMXADD1,PTMXADD2:
                          PTMXADD3,PTMXADD4,PTMXPOST:
                          PMPXINTR,PMPXLNG1,PMPXLNG2,PMPXLNG3,PMPXPEML:
                          PMPXEDOB,PMPXCMOB,PMPXCEML,PMPXCCRP,PMPXKMOB:
                          PMPXKEML,PMPXKCRP,PMPXRMOB,PMPXREML,PMPXRCRP:
                          PMPXMEDC,PMPXDETY,PMPXPRIS,PMPXCSER,PMPXPHEI:
                          PMPXPWEI,PMPXDREC,PMPXDVAC,PMPXPNSD,PMPXUYN4:
                          PMPXUYN5,PMPXUYN6,PMPXABRG,PMPXPRVI,PMPXCRIN:
                          PMPXHOML,PMPXUDC1,PMPXUDC2,PMPXUDC3,PMPXUSR7:
                          PMPXUSR8,PMPXUSR9,PMPXMSNA,PMPXMGNA,PMPXMTLE:
                          PMPXMAD1,PMPXMAD2,PMPXMAD3,PMPXMAD4,PMPXMPOS:
                          PMPXMPNO,PMPXMBNO,PMPXMMNO,PMPXMEML,PMPXMCNT:
                          PMPXMCRP,PMPXMLAB,PMPXMREL,PMPXFSNA,PMPXFGNA:
                          PMPXFTLE,PMPXFAD1,PMPXFAD2,PMPXFAD3,PMPXFAD4:
                          PMPXFPOS,PMPXFPNO,PMPXFBNO,PMPXFMNO,PMPXFEML:
                          PMPXFCNT,PMPXFCRP,PMPXFLAB,PMPXFREL:
                          SP1
.
          CALL      SPLTPMED                * Split PMEDIXX into PMEDI, PTMXMCCD
.
          ADD       ONE,SUCCPROC               * increment # successful messages
          GOTO      DGPEN999
.
. now set up reply error messages
.
DGPEN750  MOVE      "01110",RPLYCODE
          MOVE      "National Id record not found",RPLYDESC
          GOTO      DGPEN900
.
DGPEN780  MOVE      "01115",RPLYCODE
          MOVE      "U/R required since not using National Link",RPLYDESC
          GOTO      DGPEN900
.
DGPEN800  MOVE      "01100",RPLYCODE
          MOVE      "Master file record not found",RPLYDESC
          GOTO      DGPEN900
.
DGPEN850  MOVE      "01114",RPLYCODE
          MOVE      "Missing U/R and National No. in message",RPLYDESC
          GOTO      DGPEN900
.
. write reply error message
.
DGPEN900  MOVE      "ENQ",RPLYHEAD
          PACK      CCENCODE,CCDATE,CCDST,CCSRC,CCNAME,REPLYRPL
          WRITE     DGATEIN;CCENCODE:
                          RPLYHEAD,RPLYCODE,RPLYDESC,DGMISC1,DGMISC2:
                          DGMISC3,SP1
          ADD       ONE,ERRRPROC               * increment # error messages
.
 XIF
DGPEN999  RETURN
