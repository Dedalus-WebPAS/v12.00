. *****************************************************************************
. *                                                                           *
. * Include Name  : PATCOMN1.PBL                                              *
. *                                                                           *
. * Function      : Subroutine Include for                                    *
. *                 Patient standard library                                  *
. *                                                                           *
. * Subroutines   : KURN     : Keyin a U/R number                             *
. *                 MURN     : Keyin a tracking U/R No.                       *
. *                                                                           *
. * Files needed  : IBACOMM.INC    IBA Special Variables                      *
. *                 PATCOMM.INC    Special Variables                          *
. *                 PATGSRFD.INC   Surname/Given names file                   *
. *                 PATMA1FD.INC   Master File                                *
. *                 PATPR1FD.INC   0 U/R Master File                          *
. *  (redundant r5.10)             PATSURFD.INC   Surname Index File          *
. *                 PATMI1FD.INC   Admissions File                            *
. *                 PMSVX1FD.INC   Visit Extension File                       *
. *                 OUTPREFD.INC   Outpatient Pre-att file                    *
. *                                                                           *
. * Modifications :                                                           *
. *                                                                           *
. *      V10.10.01  15/06/2017  Steve Armstrong                               *
. *                 Removed reference to ACTCOMN1                             *
. *****************************************************************************
. *       V9.03.01  10/08/2004  Lina Vo     CAR 51011                         *
. *                 Changed KURN to skip UR merged message if used the web.   *
. *       V9.02.01  01/11/2001  Greg Horvat                                   *
. *                 Recompiled for ACTCOMN1 - Added xtra option to the        *
. *                 parameter PNSWRACE                                        *
. *       V5.10.B01 04/05/2001  Glenn Saunders                                *
. *                 Change comments ONLY re: decommission of patsurnf         *
. *       V5.09.B02 29/04/2001  Steve Armstrong                               *
. *                 Removed ACT routines to ACTCOMN1                          *
. *       V5.09.B01 09/01/2001  Greg Horvat                                   *
. *                 Changed the ACT PMI search routine to be a fork rather    *
. *                 than a procedure                                          *
. *       V5.08.03  17/11/2000  Davin                                         *
. *                 Added routine to perform search on ACT PMI if required    *
. *       V5.08.02  19/07/2000    Steve Armstrong                             *
. *                 Write Last U/R Entered (patlueaf) in MURN                 *
. *       V5.08.01  27/01/2000  Steve Armstrong                               *
. *                 Removed check on hospital id when validating if U/R exists*
. *                 on patnidaf (as PATNIDIO is hardcoded to always use       *
. *                 hospital id "1").                                         *
. *****************************************************************************
. *       V5.03.03  25/10/1995    Justin Coates  (SUHT mods)                  *
. *                 Allow for Last U/R Entered (patlueaf)                     *
. *       V5.03.02  05/10/1995  MATT                                          *
. *                 Changed CONTROLF Def to SFILE                             *
. *       V5.03.01  11/06/1995  Graeme Williams                               *
. *                 Changed MURN to allow for a local U/R without a nat. no.  *
. *                                                                           *
. *****************************************************************************
. *                                                                           *
. *                 10/04/89 Graeme Williams                                  *
. *                 Include Written                                           *
. *                 05/06/89 Graeme Williams         SRF 4705                 *
. *                 Treat Exit option from surname searchs as Continue        * 
. *                 Allow for new surname searchs                             *
. *                 14/07/89 Darren Jones                                     *
. *                 Added MURNO to allow Med records tracking to use barcode  *
. *                 for entire record id, not just the U/R.                   *
. *                 15/08/89 Monika Ohleiter                                  *
. *                 Added new surname enquiry for SRCHOPT=3                   *
. *       V5.01.02  30/08/89 Graeme Williams                                  *
. *                 Allow for PSTAT = 1 (Merged UR)                           *
. *       V5.01.03  14/10/89 Graeme Williams                                  *
. *                 Fixed the right justification of U/R in MURN              *
. *       V5.01.04  09/01/90 Graeme Williams     SRF 103238                   *
. *                 Added retaining of surname search parameters.             *
. *       V5.01.05  24/01/90 Graeme Williams                                  *
. *                 Fix bug with (C)ontinue, then hitting <ENTER> for U/R.    *
. *       V5.01.06  12/03/1992    Justin Coates  (Bay of Plenty changes)      *
. *                 Add check on PATNMPFD for UR number if not on master file *
. *       V5.01.07  16/07/1992    LOFTY and DOG                               *
. *                 When read in from BAR CODE, right justify the U/R number  *
. *       V5.01.08  04/08/1992    Justin Coates  (HONG KONG changes)          *
. *                 Changes for PATNMPFD National number being 20 chars.      *
. *                 16/10/1992    Graeme Williams                             *
. *                 Use the PTCNNMPI parameter to decide which file to access *
. *       V5.01.09  08/12/1992    Steve O'Gorman (Dudley changes)             *
. *                 Added the DOB Search                                      *
. *       V5.01.10  19/02/1993    Graeme Williams                             *
. *                 Fix reading of the New Zealand NMPI file (was checking    *
. *                 CMPURNO, should be CPPURNO)                               *
. *       V5.01.11  26/04/1993    ROD                                         *
. *                 Re-wrote PATSRCH                                          *
. *       V5.01.12  09/06/1993    ROD                                         *
. *                 Save WHOLE screen when doing a GETSCR                     *
. *       V5.01.13  24/11/1993    Matt Surridge    MTA                        *
. *                 Modified KURN to accept 17 char input for Singapore I/C   *
. *                 numbers.                                                  *
. *       V5.01.14  11/05/1994    Matt Surridge                               *
. *                 Cleared NMPNUMB before keying in a U/R number in KURN.    *
. *       V5.01.15  16/09/1994    ROD                                         *
. *                 load ERROR (in common) with either U/R or NMPI Number.    *
. *       V5.02.01  05/12/1994  Graeme Williams  SRF 133649                   *
. *                 Changed MURN to check for national numbers                *
. *       V5.02.02  21/02/1995  Graeme Williams  SRF 135204                   *
. *                 Changed MURN to check for a barcode if NHI link is on     *
. *****************************************************************************
.
. *****************************************************************************
. * KURN       : Key in a U/R number                                          *
. *                                                                           *
. * Parameters : CCOL     Column for keyin                                    *
. *              CVERT    Line for keyin                                      *
. *              SRCHOPT  0 = Deceased Indicator in surname search            *
. *                       1 = Programmer determined last column in surname    *
. *                       2 = First line of address in surname search         *
. *                       3 = Two line surname enquiry                        *
. *              SRCHSYS  System for surname search.                          *
. *                       0 = All systems, 1 = A + E, 2 = Outpatients         *
. *                       3 = Inpatients                                      *
. * External   : REDISPS  Redisplay the screen                                *
. *              GETSVAR  Programmer defined last column of search routine    *
. * Returns    : EXIT     0 = U/R entered                                     *
. *                       1 = No U/R entered                                  *
. *              PURNO    U/R number entered (if EXIT=0) as a DIM 8           *
. *              CPPURNO  U/R number entered (if EXIT=0) as a DIM 8           *
. *              OVRCD    0 = U/R entered exists, and PATMAST record read in  *
. *                       1 = U/R entered does not exist.                     *
. *              SRCHSUR  Surname entered in surname search (if any)          *
. *              SRCHGIV  Given name entered in surname search (if any)       *
. *              SRCHSEX  Sex entered in surname search (if any)              *
. *              SRCHDOB  Date of birth entered in surname search (if any)    *
. *              SRCHAGE  Age entered in surname search (if any)              *
. * Open files : PATMA1A1 Master file.                                        *
. *              PATPR1A1 0 U/R Master file.                                  *
. * (redundant r5.10)     PATSURN1 Surname index, Sound-ex order              *
. * (redundant r5.10)     PATSURN2 Surname index, Strict surname order        *
. *              PATGSRN1 Surname index, ur order                             *
. *              PATGSRN2 Surname index, Strict surname order                 *
. *              PATGSRN3 Surname index, soundex surname and given name order *
. *              PATGSRN4 Surname index, soundex surname and reverse given nam*
. *              PATMI1A1 Admissions file.                                    *
. *                                                                           *
. *****************************************************************************
.
KURN      CALL      SRCHCLR             * Clear the surname search parameters
          OPEN      CONTROLF,"controlf"
.
KURN0000  MOVE      SP8,CPPURNO         * Initialise U/R Number ( 8 chars)
          MOVE      SP8,PURNO           * Initialise U/R Number ( 8 chars)
          MOVE      SP20,C12URNO        * Initialise U/R Number (12 chars)
          MOVE      SP20,C17URNO        * Initialise U/R Number (17 chars)
          MOVE      SP20,CMPURNO        * Initialise U/R Number (20 chars)
          MOVE      SP20,NMPNUMB
          MOVE      SP8,ACTURNO
          MOVE      SP20,ERROR          * clear for NZHIS interface UR/NMPI #
          MOVE      ONE,NZHISMOD        * clear for NZHIS Change Indicator
          READ      CONTROLF,FIFTY9;*234,CPNMPICK
          READ      CONTROLF,TWENTY1;*55,SRCHDOBS,*138,PTCNNHII
          READ      CONTROLF,FIFTY9;*234,PTCNNMPI
.
KURN0100  PROC      KNDLUE00                * display last U/R entered
          COMPARE   ZERO,CPNMPICK
          CALL      URNOA000 IF EQUAL
          PERFORM   CPNMPICK,URNOA000,URNOB000,URNOC000,URNOC000
          DISPLAY   *P1:3,*EL;
          BRANCH    EXIT,KURN9000
          IF        EXIT=2
            CALL      KURSCH00                * ? Selected
            BRANCH    EXIT,KURN0000,KURN9999  * EXIT 0=Selected,1=UR,2=Exit
          ENDIF
.
. Validate the U/R number Keyed or Selected from ?
.
          MOVE      ZERO,EXIT               * U/R number entered
          MOVE      CPPURNO,PURNO           * Move to the master file variable
          IF        PTCNNHII=1
            CALL      KURNHI00 
            BRANCH    EXIT,KURN0000,KURN9999   * re-enter,doesnt exist
            GOTO      KURN0400
          ENDIF
.
          MOVE      PURNO,KEY8
          CALL      RDMAST1                 * Validate U/R number against PMI
          COMPARE   ZERO,OVRCD
          GOTO      KURN0400 IF EQUAL       * Valid UR so Continue
.
          MOVE      ZERO,OVRCD
          COMPARE   ZERO,PTCNNMPI
          PROC      VALURNMP IF NOT EQUAL   * check if on patnmpaf
          BRANCH    OVRCD,KURN9999,KURN0000
.
. Check if this is an associated U/R number
.-------------------------------------------
KURN0400  DISPLAY   *PCCOL:CVERT,*V2LON,PURNO,*EL;
          COMPARE   ONE,PSTAT               * Is this an associated U/R number ?
          GOTO      KURN9500 IF NOT EQUAL   * No
.         
          CALL      ASSURN00                * Yes, Get Associated UR Details
          BRANCH    EXIT,KURN0000           * Abort Selected
.
KURN0500  DISPLAY   *PCCOL:CVERT,*V2LON,CPPURNO
          GOTO      KURN9500
.
KURN9000  MOVE      ONE,EXIT    * Nothing Entered
          GOTO      KURN9999
.
KURN9500  PROC      KNWLUE00                * write last U/R entered
.
KURN9999  RETURN
.
. *****************************************************************************
. * MURN       : Key in a U/R number or bar code input                        *
. *                                                                           *
. *              This routine allows for an invisable interface of the use of *
. *              bar codes throughout the system. If a less than eight digits *
. *              ar input it is assumed that keyboard input has occurred and  *
. *              the normal method of processing is used. If more than eight  *
. *              characters is input it is assumed that full identification   *
. *              has occurred thus allowing the system to proceed accordingly *
. *                                                                           *
. * Parameters : CCOL     Column for keyin                                    *
. *              CVERT    Line for keyin                                      *
. *              SRCHOPT  0 = Deceased Indicator in surname search            *
. *                       1 = Programmer determined last column in surname    *
. *                       2 = First line of address in surname search         *
. *              SRCHSYS  System for surname search.                          *
. *                       0 = All systems, 1 = A + E, 2 = Outpatients         *
. *                       3 = Inpatients                                      *
. * External   : REDISPS  Redisplay the screen                                *
. *              GETSVAR  Programmer defined last column of search routine    *
. * Returns    : EXIT     0 = U/R entered                                     *
. *                       1 = No U/R entered                                  *
. *                       2 = Full record identified                          *
. *              PURNO    U/R number entered (if EXIT=0) as a FORM 6          *
. *              CMPURNO  Full record identifier                              *
. *              CPPURNO  U/R number entered (if EXIT=0) as a DIM 8           *
. *              OVRCD    0 = U/R entered exists, and PATMAST record read in  *
. *                       1 = U/R entered does not exist.                     *
. *              SRCHSUR  Surname entered in surname search (if any)          *
. *              SRCHGIV  Given name entered in surname search (if any)       *
. *              SRCHSEX  Sex entered in surname search (if any)              *
. *              SRCHDOB  Date of birth entered in surname search (if any)    *
. *              SRCHAGE  Age entered in surname search (if any)              *
. * Open files : PATMA1A1 Master file.                                        *
. *              PATPR1A1 0 U/R Master file.                                  *
. * (redundant r5.10)     PATSURN1 Surname index, Sound-ex order              *
. * (redundant r5.10)     PATSURN2 Surname index, Strict surname order        *
. *              PATMI1A1 Admissions file.                                    *
. *                                                                           *
. *****************************************************************************
MURN      CALL      SRCHCLR             * Clear the surname search parameters
          OPEN      CONTROLF,"controlf"
.
MURN0000  MOVE      SP8,CPPURNO         * Initialise U/R Number ( 8 chars)
          MOVE      SP8,PURNO           * Initialise U/R Number ( 8 chars)
          MOVE      SP20,C12URNO        * Initialise U/R Number (12 chars)
          MOVE      SP20,C17URNO        * Initialise U/R Number (17 chars)
          MOVE      SP20,CMPURNO        * Initialise U/R Number (20 chars)
          MOVE      SP20,NMPNUMB
          MOVE      SP20,ACTURNO
          MOVE      SP20,ERROR          * clear for NZHIS interface UR/NMPI #
          MOVE      ONE,NZHISMOD        * clear for NZHIS Change Indicator
          READ      CONTROLF,TWENTY1;*55,SRCHDOBS,*138,PTCNNHII
          READ      CONTROLF,FIFTY9;*234,PTCNNMPI
          READ      CONTROLF,EIGHTY;*138,PTCNNZUR
.
MURN0100  PROC      KNDLUE00         * display last UR entered
          CALL      URNOC000
          BRANCH    EXIT,MURN9000
          IF        EXIT=2
            CALL      KURSCH00                * ? Selected
            BRANCH    EXIT,MURN0000,MURN9100  * EXIT 0=Selected,1=UR,2=Exit
. Please note, EXIT=2 only happens in IBANHI01, which doesn't use MURN
          ENDIF
.
. Validate the U/R number Keyed or Selected from ?
.
          MOVE      ZERO,EXIT               * U/R number entered
          MOVE      CPPURNO,PURNO           * Move to the master file variable
          IF        PTCNNHII=1
            CALL      KURNHI00 
            IF        PTCNNZUR <> 1 & OVRCD = 0 & EXIT = 2
              GOTO      MURN0400            * local number without a nat. no.
            ENDIF
            BRANCH    EXIT,MURN0000,MURN0600
            GOTO      MURN0400
          ENDIF
.
          MOVE      PURNO,KEY8
          CALL      RDMAST1                 * Validate U/R number against PMI
          COMPARE   ZERO,OVRCD
          GOTO      MURN0400 IF EQUAL       * Valid UR so Continue
.
          MOVE      ZERO,OVRCD
          COMPARE   ZERO,PTCNNMPI
          PROC      VALURNMP IF NOT EQUAL   * check if on patnmpaf
          BRANCH    OVRCD,MURN0600,MURN0000 
.
. Check if this is an associated U/R number
.-------------------------------------------
MURN0400  DISPLAY   *PCCOL:CVERT,*V2LON,PURNO,*EL;
          MOVE      ZERO,EXIT               * U/R number being returned
          COMPARE   ONE,PSTAT               * Is this an associated U/R number ?
          GOTO      MURN9500 IF NOT EQUAL   * No
.         
          CALL      ASSURN00                * Yes, Get Associated UR Details
          BRANCH    EXIT,MURN0000           * Abort Selected
.
MURN0500  DISPLAY   *PCCOL:CVERT,*V2LON,CPPURNO
          MOVE      ZERO,EXIT               * U/R number being returned
          MOVE      ZERO,OVRCD
          GOTO      MURN9500
.
. Check if this could be a barcoded number
.-----------------------------------------
MURN0600  UNPACK    CMPURNO,KEY3,CPPURNO    * get first eight characters
          MOVE      TWO,EXIT                * possible barcode being returned
          MOVE      CPPURNO,KEY8
          CALL      RDMAST1                 * check if it is a U/R number
          BRANCH    OVRCD,MURN9999          * no, not a valid numebr
.
. Check if this is an associated U/R number
.-------------------------------------------
          DISPLAY   *PCCOL:CVERT,*V2LON,PURNO,*EL;
          COMPARE   ONE,PSTAT               * Is this an associated U/R number ?
          GOTO      MURN9500 IF NOT EQUAL   * No
.         
          CALL      ASSURN00                * Yes, Get Associated UR Details
          BRANCH    EXIT,MURN0000           * Abort Selected
.
          DISPLAY   *PCCOL:CVERT,*V2LON,CPPURNO
          MOVE      TWO,EXIT                * possible barcode being returned
          MOVE      ZERO,OVRCD
          GOTO      MURN9500
.
MURN9000  MOVE      ONE,EXIT    * Nothing Entered
          MOVE      ZERO,OVRCD  * Nothing Entered
          GOTO      MURN9999
.
MURN9100  MOVE      ZERO,EXIT   * new U/R selected
          MOVE      ONE,OVRCD   * not currently on database
          GOTO      MURN9999
.
MURN9500  PROC      KNWLUE00                * write last U/R entered
.
MURN9999  RETURN
................................................................................
.*********************************************************************
.*                  VALURNMP                    Called by : KURN0100 *
.*        Check if entered UR number is on National Master PMI       *
.*        when it isnt on normal master file                         *
.*        Para's  : CMPURNO       keyed in UR number (20 chars)      *
.*                  CPPURNO       keyed in UR number (8 chars)       *
.*        Returns : OVRCD = 0     have a NMPMI UR number sets PURNO  *
.*                  OVRCD = 1     invalid UR number                  *
.*                  OVRCD = 2     Abort the UR number                *
.*********************************************************************
          DEFPROC   VALURNMP
.
          INC       PATNIDFD/INC            * national master patient index file
.
SP12      INIT      "            "
PTCNNMDX  DIM       12
PTCNNULU  FORM      1
PTCNNMPI  FORM      1
 IFGE     $$VER,7
CONTROLF  SFILE    ASCII, FIXED=256
 XIF
 IFEQ     $$VER,6
CONTROLF  FILE     ASCII, FIXED=256
 XIF
.
VLNMP000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY;*225,PTCNNMDX,PTCNNULU
          READ      CONTROLF,FIFTY9;*234,PTCNNMPI
.
          IF        PTCNACTI=1
            MATCH     SP8,ACTURNO
            GOTO      VLNMP900 IF NOT EQUAL * Using ACT interface
          ENDIF
          COMPARE   ZERO,PTCNNMPI
          GOTO      VLNMP910 IF EQUAL       * not using NMPI so invalid UR
          COMPARE   ONE,PTCNNHII            * New Zealand Not Using This
          GOTO      VLNMP910 IF EQUAL       * not using NMPI so invalid UR
.
.         Hong Kong/England National ID file
.
          OPEN      PATNIDA2,"patnidaf"
          PACK      KEY30,CMPURNO,CHOSINDX,SP20
          CALL      RSPTNID2
          CALL      RKPTNID2
          CLOSE     PATNIDA2
          BRANCH    OVRCD,VLNMP910          * no record on file
          MATCH     CMPURNO,PTNINMPI
          GOTO      VLNMP910 IF NOT EQUAL   * different National UR
.
.         The following lines of code which check the hospital id have been
.         removed as the PATNIDIO always sets hospital id to "1" (hardcoded)
.         in all the IO routines.  This meant that in a multiple database
.         situation, only hospital "1" can ever keyin an alternate id.
.
.>>>>     COMPARE   CHOSINDX,PTNIHNUM
.>>>>     GOTO      VLNMP910 IF NOT EQUAL   * different Hospital Number
.
.         the entered UR number is a National one, so return the Local number
.
          COMPARE   ONE,PTCNNULU
          GOTO      VLNMP800 IF NOT EQUAL   * dont tell user about link
.
VLNMP700  DISPLAY   *P1:24,*EL,*B,*+,PTCNNMDX,*-," has a Local U/R No. of ":
                    *V2LON,PTNILNUM,*HOFF:
                    ". Hit <",*V2LON,"ENTER",*HOFF,"> to continue or ":
                    *V2LON,"A",*HOFF,"bort:";
          KEYIN     *P1:24,*EOFF,*+,ANS
          DISPLAY   *P1:24,*EL;
.
          RESET     ANS
          GOTO      VLNMP800 IF EOS         * enter hit
.
          REP       UPPLOW,ANS
          CMATCH    ANSA,ANS
          GOTO      VLNMP920 IF EQUAL       * abort this UR
.
          BEEP
          GOTO      VLNMP700
.
.         obtain the new PMI details
.
VLNMP800  MOVE      PTNILNUM,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,VLNMP910          * new UR number is invalid
          MOVE      PURNO,CPPURNO           * set UR number
          DISPLAY   *PCCOL:CVERT,*V2LON,CPPURNO,*EL;
.
VLNMP900  MOVE      ZERO,OVRCD              * set OVRCD to say valid UR number
          GOTO      VLNMP990
.
.         the entered UR number is invalid
.
VLNMP910  MOVE      ONE,OVRCD               * set OVRCD to say invalid UR number
          GOTO      VLNMP990
.
.         abort the entered UR number
.
VLNMP920  MOVE      TWO,OVRCD
.
VLNMP990  GOTO      VLNMP999
.
.         I/O's for procedure
.
          INC       PATNIDIO/INC            * national master patient index
          INC       IBAOVRIO/INC            * IO labels
.
VLNMP999  ENDPROC
.
.*********************************************************************
.*                  RJURN000                    Called by : MURN9900 *
.*        Prepend the required number of spaces to the value read in *
.*        by the barcode.                                            *
.*********************************************************************
          DEFPROC   RJURN000
.
DIM17     DIM       17
.
.         string is U/R|HOME|TYPE|VOL
.                    8   4    3   2   = 17 characters
.         so if LL is 10, the U/R is 1 character  so have to add 7 spaces
.         so if LL is 11, the U/R is 2 characters so have to add 6 spaces
.
RJURN000  MOVELPTR  CMPURNO,FORM2           * get the logical length of string
          PACK      DIM17,CMPURNO,SP20      * get copy of string
          SUB       NINE,FORM2              * adjust the length value
          BRANCH    FORM2,RJURN100,RJURN200,RJURN300,RJURN400,RJURN500:
                          RJURN600,RJURN700
          GOTO      RJURN999
.
RJURN100  PACK      CMPURNO,SP7,DIM17
          GOTO      RJURN999
.
RJURN200  PACK      CMPURNO,SP6,DIM17
          GOTO      RJURN999
.
RJURN300  PACK      CMPURNO,SP5,DIM17
          GOTO      RJURN999
.
RJURN400  PACK      CMPURNO,SP4,DIM17
          GOTO      RJURN999
.
RJURN500  PACK      CMPURNO,SP3,DIM17
          GOTO      RJURN999
.
RJURN600  PACK      CMPURNO,SP2,DIM17,SP1
          GOTO      RJURN999
.
RJURN700  PACK      CMPURNO,SP1,DIM17,SP2
          GOTO      RJURN999
.
RJURN999  ENDPROC
+
.*********************************************************************
.                   URNOA000                    Called by : xxxx0000 
.         keyin the U/R number - max 8 chars
.*********************************************************************
URNOA000  KEYIN     *PCCOL:CVERT,*V2LON,*JR,CPPURNO
.
          MOVE      ONE,EXIT
          RESET     CPPURNO
          GOTO      URNOA999 IF EOS         * nothing entered
.
          MOVE      TWO,EXIT
          MATCH     "       ?",CPPURNO
          GOTO      URNOA999 IF EQUAL       * ? entered
.
          MATCH     "       ##",CPPURNO
          IF        @EQUAL
            PROC      KNSLUE00              * '#' entered - see if last U/R
          ENDIF
.
          PACK      CMPURNO,SP10,SP2,CPPURNO
          DISPLAY   *PCCOL:CVERT,*V2LON,CPPURNO
          MOVE      ZERO,EXIT
.
URNOA999  RETURN
+
.*********************************************************************
.                   URNOB000                    Called by : xxxx0000 
.         keyin the U/R number - max 12 chars
.*********************************************************************
URNOB000  KEYIN     *PCCOL:CVERT,*V2LON,*JR,C12URNO
.
          MOVE      ONE,EXIT
          RESET     C12URNO
          GOTO      URNOB999 IF EOS         * nothing entered
.
          MOVE      TWO,EXIT
          MATCH     "           ?",C12URNO
          GOTO      URNOB999 IF EQUAL       * ? entered
.
          MATCH     "           ##",C12URNO
          IF        @EQUAL
            PROC      KNSLUE00              * '#' entered - see if last U/R
          ENDIF
.
          PACK      CMPURNO,SP8,C12URNO
          UNPACK    CMPURNO,C12DIM,CPPURNO   * set normal U/R
          MATCH     C12DIM,SP20
          IF        !@EQUAL
            MOVE      "********",CPPURNO      * set so read on master file fails
          ENDIF
          MOVE      ZERO,EXIT
.
URNOB999  RETURN
+
.*********************************************************************
.                   URNOC000                    Called by : xxxx0000 
.         keyin the U/R number - max 17 chars
.*********************************************************************
URNOC000  KEYIN     *PCCOL:CVERT,*V2LON,*JR,C17URNO
.
          MOVE      ONE,EXIT
          RESET     C17URNO
          GOTO      URNOC999 IF EOS         * nothing entered
.
          MOVE      TWO,EXIT
          MATCH     "                ?",C17URNO
          GOTO      URNOC999 IF EQUAL       * ? entered
.
          MATCH     "                ##",C17URNO
          IF        @EQUAL
            PROC      KNSLUE00              * '#' entered - see if last U/R
          ENDIF
.
          PACK      CMPURNO,SP3,C17URNO
          UNPACK    CMPURNO,C12DIM,CPPURNO   * set normal U/R
          MATCH     C12DIM,SP20
          IF        !@EQUAL
            MOVE      "********",CPPURNO      * set so read on master file fails
          ENDIF
          MOVE      ZERO,EXIT
.
URNOC999  RETURN
.----------------------------------------------------------------------
. Help (?) enter for Keyin of U/R No.
.----------------------------------------------------------------------
KURSCH00  MOVE      ZERO,HLEF           * save whole screen
          MOVE      ZERO,NZHISNEW       * Check for Register Option
          CALL      GETSCR00            * save screen
          MOVE      CVERT,CSCVERT       * Save the CVERT value
          MOVE      CCOL,CSCCOL         * Save the CCOL  value
.
          IF        ACTSRFLG=1 & PTCNACTI=1
            CALL      ACTSRC00          * search ACT PMI or Local PMI
            BRANCH    EXIT,KURSCH95,KURSCH05
            GOTO      KURSCH40          * Patient selected in ACT search
          ENDIF
.
KURSCH05  CALL      KSCOPT00            * Enter Search Option
          COMPARE   ZERO,FORM1          * Surname search not really wanted ?
          GOTO      KURSCH95 IF EQUAL   * No, just redisplay the screen
          BRANCH    FORM1,KURSCH10,KURSCH20,KURSCH30
.
.         Surname Search
.
KURSCH10  MOVE      FOUR,CVERT          * Full screen surname search
          MOVE      ONE,SRCHCLR         * dont clear search parameters
          CALL      STRCT000            * Strict surname search
          MOVE      ZERO,SRCHCLR        * clear search parameters
          BRANCH    SRCHFLAG,KURSCH40   * Continue
          GOTO      KURSCH95            * Don't want to enter a U/R number
.
.         Sound-ex Search
.
KURSCH20  MOVE      FOUR,CVERT          * Full screen surname search
          MOVE      ONE,SRCHCLR         * dont clear search parameters
          CALL      SOUND000            * Strict surname search
          MOVE      ZERO,SRCHCLR        * clear search parameters
          BRANCH    SRCHFLAG,KURSCH40   * Continue
          GOTO      KURSCH95            * Don't want to enter a U/R number
.
.         Date of Birth search
.
KURSCH30  CALL      DBRTH000            * DOB search
          BRANCH    SRCHFLAG,KURSCH40,KURSCH30   * Continue,Next
          GOTO      KURSCH95            * Don't want to enter a U/R number
.
.         Key in the U/R number after the surname search
.
KURSCH40  CALL      PUTSCR00            * restore the screen
          MOVE      CSCCOL,CCOL         * restore the col. to display on
          MOVE      CSCVERT,CVERT       * Restore the line to display on
          IF        PTCNNMPI = 1 | PTCNNMPI = 0
            DISPLAY   *PCCOL:CVERT,*V2LON,CPPURNO
          ENDIF
          IF        PTCNNMPI = 2
            DISPLAY   *PCCOL:CVERT,*V2LON,C12URNO
          ENDIF
          IF        PTCNNMPI = 3
            DISPLAY   *PCCOL:CVERT,*V2LON,C17URNO
          ENDIF
.
KURSCH90  MOVE      ZERO,EXIT            * U/R Selected
          GOTO      KURSCH99 
.
. They didn't really want to do a surname search Redisplay the screen
.
KURSCH95  CALL      PUTSCR00            * restore the screen
          MOVE      CSCVERT,CVERT       * Restore the line to display on
          MOVE      CSCCOL,CCOL         * Restore the col. to display on
          MOVE      ONE,EXIT            * Return to U/R Keyin
          GOTO      KURSCH99 
.
KURSCH99  IF        NZHISNEW<>0
            MOVE      TWO,EXIT
          ENDIF
          RETURN   
.----------------------------------------------------------------------
. Perform ACT PMI search if requested
.----------------------------------------------------------------------
ACTSRC00  PACK      ERROR,ZERO,SP20         * go to search option selection
          FORK      "IBAACT01",TASKID       * ACT search
          MOVE      SP20,ERROR
.
. ----- Check the status from IBAACT01 -----
.
          CLEAR     KEY8
          APPEND    "act01a",KEY8
          APPEND    PORT,KEY8
          RESET     KEY8
          REP       " 0",KEY8
          RESET     KEY8
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      ACTTMP1,KEY8
          TRAPCLR   IO
          BRANCH    OVRCD,ACTSRC50
.
          READ      ACTTMP1,SEQ;KEY1,ACTLOCAL,KEY8;
          GOTO      ACTSRC50 IF OVER
.
          MOVE      ZERO,FORM1
          MOVE      KEY1,FORM1
          BRANCH    FORM1,ACTSRC90,ACTSRC95
.                         Exit     Local
.                                  Search
          COMPARE   ZERO,FORM1
          GOTO      ACTSRC90 IF NOT EQUAL   * Not zero, search again
.
          MOVE      ACTLOCAL,ACTURNO        * save ACT number
          MATCH     SP8,KEY8
          IF        @EQUAL
            MOVE      ZEROUR,PURNO
          ELSE
            MOVE      KEY8,PURNO            * save local u/r
          ENDIF
.
          MOVE      PURNO,CPPURNO
          PACK      C12URNO,SP4,CPPURNO
          PACK      C17URNO,SP9,CPPURNO
          PACK      CMPURNO,SP10,SP2,CPPURNO
          PACK      NMPNUMB,SP10,SP2,ACTURNO
          MOVE      ZERO,EXIT
          GOTO      ACTSRC99
.
ACTSRC50  DISPLAY   *P1:24,*EL,*B,"Error defaulting ACT number details.  ";
          CALL      HITENTER
          GOTO      ACTSRC95
.
ACTSRC90  MOVE      ONE,EXIT                * Exit
          GOTO      ACTSRC99
.
ACTSRC95  DISPLAY   *P1:3,*EF;
          MOVE      TWO,EXIT                * Do local search
.
ACTSRC99  CLOSE     ACTTMP1,DELETE
          RETURN
+
.------------------------------------------------------------
. Enter Surname Search Option  Returns FORM1 of 0,1,2 or 3
.------------------------------------------------------------
KSCOPT00  DISPLAY   *P1:4,*EF:
                    *P1:4,*V2LON," 0",*HOFF," = Exit":
                    *P1:5,*V2LON," 1",*HOFF," = Strict Surname Search":
                    *P1:6,*V2LON," 2",*HOFF," = Sound-ex Search"
.
KSCOPT10  BRANCH    SRCHDOBS,KSCOPT20
          MOVE      ZERO,FORM1
          KEYIN     *P1:8,*EF,"Select Option : ",*RV,*V2LON,FORM1
          COMPARE   ZERO,FORM1          * Surname search not really wanted ?
          GOTO      KSCOPT99 IF EQUAL   * No, just redisplay the screen
          BRANCH    FORM1,KSCOPT99,KSCOPT99
          BEEP
          GOTO      KSCOPT10
.
. Date of Birth Option Available
.
KSCOPT20  MOVE      ZERO,FORM1
          DISPLAY   *P1:7,*V2LON," 3",*HOFF," = Date of Birth Search"
          KEYIN     *P1:9,*EF,"Select Option : ",*RV,*V2LON,FORM1
          COMPARE   ZERO,FORM1          * Surname search not really wanted ?
          GOTO      KSCOPT99 IF EQUAL   * No, just redisplay the screen
          BRANCH    FORM1,KSCOPT99,KSCOPT99,KSCOPT99
          BEEP
          GOTO      KSCOPT20
.
KSCOPT99  RETURN
.----------------------------------------------------------------------
. ASSURN00 : We have an associated U/R number. 
.            Now find the correct U/R number.
.
. Called by KURN0000
.----------------------------------------------------------------------
ASSURN00  MOVE      PPSNAME,CPPURNO     * This is where the new U/R is hidden
.
.         We have to check this new U/R number, just in case it is also
.         associated to another U/R number. eg. A->B->C->D->...->H
.
          MOVE      CPPURNO,KEY8        * Key for new U/R number
          CALL      RDMAST1             * Read in the new PMI record
          BRANCH    OVRCD,ASSURN80      * New U/R number missing
          BRANCH    PSTAT,ASSURN00      * Another associated U/R number
.
.         We have the correct U/R number
.
          MATCH     "IBARSH",PGM
          GOTO      ASSURN60 IF EQUAL
.
          SCAN      "WEB",PRGID
          RESET     PRGID
          GOTO      ASSURN60 IF EQUAL
.
          KEYIN     *P1:24,*EL,*B:
                    "This U/R is merged with ",*V2LON,*DV,PURNO,*HOFF:
                    ". Hit ",*V2LON,"<ENTER>",*HOFF," to continue, or #"":
                    *V2LON,"A",*HOFF:
                    "#" to abort : ",*V2LON,ANS,*P1:24,*EL;
          CMATCH    ANSA,ANS            * Check for the abort condition
          GOTO      ASSURN95 IF EQUAL   * Yes. Try another U/R number
.
ASSURN60  MOVE      ZERO,EXIT
          GOTO      ASSURN99            * Return the U/R
.
.         Error in the data files. Tell the user, and ask for another U/R
.
ASSURN80  DISPLAY   *P1:24,*EL,*B:
                    "Error: U/R merged with ",*V2LON,CPPURNO,*HOFF:
                    " which is missing. ";
          CALL      HITENTER
ASSURN95  MOVE      ONE,EXIT
ASSURN99  RETURN
.
          INC       PATSRCH             * Surname search routines
.
.*********************************************************************
.         display the last U/R entered for this operator
.*********************************************************************
          DEFPROC   KNDLUE00
.
          INC       PATLUEFD/INC
          INC       PATMA1FD/INC
.
PTCNULUE  FORM      1
.
DLUE0000  READ      CONTROLF,SEVENTY7;*10,PTCNULUE
          COMPARE   ONE,PTCNULUE
          GOTO      DLUE9999 IF NOT EQUAL   * not using this feature
.
          OPEN      PATLUEA1,"patlueaf"
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          MOVE      PASSCODE,KEY4
          CALL      RDPTLUE1
          BRANCH    OVRCD,DLUE9999
.
          MOVE      PTLUURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,DLUE9999
.
          MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      PTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
.
          DISPLAY   *P1:3,*EL,"Enter '",*V2LON,"##",*HOFF,"' to select U/R : ":
                    *V2LON,PTLUURNO,*HOFF,SP1,PACFNAME
          GOTO      DLUE9999
.
          INC       IBAOVRIO/INC
          INC       PATLUEIO/INC
          INC       PATMA1IO/INC
.
DLUE9999  MOVE      ZERO,OVRCD
          ENDPROC
+
.*********************************************************************
.         write the last U/R entered for this operator
.*********************************************************************
          DEFPROC   KNWLUE00
.
          INC       PATLUEFD/INC
.
PTCNULUE  FORM      1
.
WLUE0000  READ      CONTROLF,SEVENTY7;*10,PTCNULUE
          COMPARE   ONE,PTCNULUE
          GOTO      WLUE9999 IF NOT EQUAL   * not using this feature
.
          OPEN      PATLUEA1,"patlueaf"
          MOVE      PASSCODE,KEY4
          CALL      RDPTLUE1
.
          MOVE      PASSCODE,PTLUOPER
          MOVE      PURNO,PTLUURNO
.
          IF        OVRCD = ZERO
            CALL      UPPTLUE1
          ELSE
            CALL      WRPTLUE1
          ENDIF
          MOVE      ZERO,OVRCD
          GOTO      WLUE9999
.
          INC       IBAOVRIO/INC
          INC       PATLUEIO/INC
.
WLUE9999  ENDPROC
+
.*********************************************************************
.         select the last U/R entered for this operator
.*********************************************************************
          DEFPROC   KNSLUE00
.
          INC       PATLUEFD/INC
.
PTCNULUE  FORM      1
.
SLUE0000  READ      CONTROLF,SEVENTY7;*10,PTCNULUE
          COMPARE   ONE,PTCNULUE
          GOTO      SLUE9999 IF NOT EQUAL   * not using this feature
.
          OPEN      PATLUEA1,"patlueaf"
          MOVE      PASSCODE,KEY4
          CALL      RDPTLUE1
          BRANCH    OVRCD,SLUE9999
.
          MOVE      PTLUURNO,CPPURNO
          PACK      C12URNO,SP4,CPPURNO
          PACK      C17URNO,SP9,CPPURNO
          GOTO      SLUE9999
.
          INC       IBAOVRIO/INC
          INC       PATLUEIO/INC
.
SLUE9999  MOVE      ZERO,OVRCD 
          ENDPROC
+
