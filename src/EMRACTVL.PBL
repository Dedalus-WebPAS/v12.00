.******************************************************************************
.*                                  EMRACTVL                                  *
.* Validate EMRACTFD effective from and to dates for overlaps                 *
.*                                                                            *
.* Requires:   KEY38 populated with -                                         *
.*                      - ECDG Code                                           *
.*                      - Service Date (defaults to current date)             *
.*             VALFDATE - Effictive date from date                            *
.*             VALTDATE - Effictive date to date                              *
.*                                                                            *
.* Returns:    EXIT  0 = Valid effective from and to dates                    *
.*                   1 = Invalid effective from and to dates                  *
.*             ENDKEY38 = Key to record that requires an auto end date update *
.*                                                                            *
.* Modifications:                                                             *
.* V11.01.01  20/08/2021  Thanh T         TSK 0905305                         *
.*            Renamed EMRECHVL.PBL to EMRACTVL.PBL to be consistent with      *
.*            table name                                                      *
.* V11.01.00  02/08/2021  DA Sarkies      TSK 0905305                         *
.*            Created include                                                 *
.******************************************************************************
.
REVL0000  MOVE      SP70,ENDKEY38           * Clear auto end key
          MOVE      KEY38,SAVKEY30          * Save ECDG Subcategory
.
          CALL      RDEMACT1                * Record on file so must be an
          BRANCH    OVRCD,REVL5000          * update
.
          MATCH     VALTDATE,EMATTDAT       * No change in effective from and
          GOTO      REVL8000 IF EQUAL       * to dates so exit
.
REVL5000  CALL      RSEMACT1                * Check for the same ECDG sub cat
          CALL      RPEMACT1                * with an earlier effective from
          BRANCH    OVRCD,REVL6000          * date
.
          MATCH     SAVKEY30,EMATCODE       * Same ECDG Subcategory
          GOTO      REVL6000 IF NOT EQUAL
.
          MATCH     SP70,EMATTDAT           * No effective to date prev  record
          IF        @EQUAL
            PACK      ENDKEY38,EMATCODE,EMATFDAT,SP70 * auto end date record key
            GOTO      REVL8000 
          ENDIF
.
          MATCH     VALFDATE,EMATTDAT
          IF        !@LESS
            GOTO      REVL9000              * Effective from date overlap
          ENDIF
.
          MATCH     SP70,VALTDATE           * Blank effective to date
          GOTO      REVL6000 IF EQUAL
.
          MATCH     VALTDATE,EMATTDAT
          IF        !@LESS
            GOTO      REVL9000              * Effective to date overlap
          ENDIF
.
REVL6000  CALL      RSEMACT1                * Check for the same ECDG sub cat
          CALL      RKEMACT1                * with an earlier effective from
          BRANCH    OVRCD,REVL8000          * date
.
          MATCH     SAVKEY30,EMATCODE       * Same ECDG Subcategory
          GOTO      REVL8000 IF NOT EQUAL
.
          MATCH     SP70,VALTDATE           * Blank effective to date
          GOTO      REVL9000 IF EQUAL
.
          MATCH     EMATFDAT,VALFDATE
          IF        !@LESS
            GOTO      REVL9000              * Effective from date overlap
          ENDIF
.
          MATCH     EMATTDAT,VALTDATE
          IF        !@LESS
            GOTO      REVL9000              * Effective to date overlap
          ENDIF
.
REVL8000  MOVE      ZERO,EXIT               * OK
          GOTO      REVL9999
.
REVL9000  MOVE      SP70,ENDKEY38
          MOVE      ONE,EXIT                * Error end date overlap
          GOTO      REVL9999
.
REVL9999  RETURN
