.***************************************************************************
.* System    :   Inpatients                                                *
.* Program   :   IBAPMS59                                                  *
.* Desc      :   Check for missing patdadaf records                        *
.***************************************************************************
.* Date      :   06/08/2002                                                *
.* Author    :   Jill Habasque                                             *
.* Function  :   This program will loop the admission file and check       *
.*           :   for a record on patdadaf if required                      *
.* Mods      :                                                             *
.*        V10.01.01 03/02/2011 Jill Parkinson CAR 233088                      *
.*                  Added pmsvx1af                                            *
.*        V9.08.01  01/06/2007  Mike Laming  CAR 140164                    *
.*                  Correct DDEST/DSTAT Indicator selection at PROD1000    *
.*        V9.03.00  17/03/2005  Mike Laming   CAR 58923                    *
.*                  Was CHECKASR - ported to 9.03 at RCH request           *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       PATDADFD/INC
          INC       PATTRNFD/INC
          INC       PATMI1FD/INC
          INC       PMSVX1FD/INC
          INC       PATDSCFD/INC
          INC       PATCODFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
ERRMSG    DIM       20
STATUS    DIM       14
PREAD     INIT      "Preadmitted   "
ADMISS    INIT      "Admitted      "
DISCH     INIT      "Discharged    "
LEAVE     INIT      "On Leave      "
CANC      INIT      "Cancelled     "
STRTDATE  INIT      "20020201"
PTCNDRSM  FORM      1
.
PRGID     INIT      "IBAPMS59"
PRGNAM    INIT      "Print missing patdadaf records"
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process admission source
          CALL      PROD0000               * process discharge source
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PATDADA1,"patdadaf"
          OPEN      PATTRAN2,"pattranf"
          OPEN      PATMI1A3,"patmi1af"
          OPEN      PMSVX1A1,"pmsvx1af"
          OPEN      PATDSCH2,"patdschf"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
          CALL      IBACLOCK
.
          READ      CONTROLF,TWENTY1;*45,PTCNDRSM                     *I-140164
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run"
.
OPTN0500  KEYIN     *P1:7,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:7,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000             * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  CALL     HEAD0000                      * Print Heading
          PRINT    *2,"Admission Source"
.
          PACK     KEY16,STRTDATE,SP70
          CALL     RDSMISS3
PROC1000  CALL     RDKMISS3
          BRANCH   OVRCD,PROC9000
.
          MOVE     SP70,STATUS
.
          MATCH    SP70,ASRCE
          GOTO     PROC1000 IF EQUAL
.
          PACK     KEY5,ANSS,SP1,ASRCE,SP70
          CALL     RDCODE1                  * Cat."S "
          BRANCH   OVRCD,PROC1000
.
          MATCH    "1",TCINDC2
          GOTO     PROC1000 IF NOT EQUAL
.
          LOAD     STATUS,ASTAT,PREAD,ADMISS,DISCH,LEAVE,CANC
.
          PACK     KEY8,DAADMNO,SP70
          CALL     RDPTDAD1
          IF       OVRCD=1
            MOVE     "Missing Record          ",ERRMSG
            CALL     PRIN0000
            GOTO     PROC1000
          ENDIF
.
          MATCH    SP70,PTDAADTS
          IF       @EQUAL
            MOVE     "Blank Record            ",ERRMSG
            CALL     PRIN0000                    
          ENDIF
.
          GOTO     PROC1000
.
PROC9000  CALL     UND80
.
PROC9999  RETURN
+
.**************************************************************************
.*                               PROD0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROD0000  MOVE     "Discharged",STATUS
          PRINT    *2,"Discharge Source"
.
          PACK     KEY16,STRTDATE,SP70
          CALL     RDSDSCH2
PROD1000  CALL     RDKDSCH2
          BRANCH   OVRCD,PROD9000
.                                                                     *C-140164
          IF        PTCNDRSM=1
            PACK      KEY3,DSTAT
            PACK      KEY5,ANSD,SP1,DSTAT,SP5
          ELSE
            PACK      KEY3,DDEST
            PACK      KEY5,ANSD,ANSD,DDEST,SP5
          ENDIF
.
          MATCH    SP70,KEY3 
          GOTO     PROD1000 IF EQUAL
.
          CALL     RDCODE1                  * Cat."DD"
          BRANCH   OVRCD,PROD1000
.
          MATCH    "1",TCINDC2
          GOTO     PROD1000 IF NOT EQUAL
.
          PACK     KEY8,DDADMNO,SP70
          CALL     RDPTDAD1
          IF       OVRCD=1
            MOVE     "Missing Record          ",ERRMSG
            CALL     PRIN0000
            GOTO     PROD1000
          ENDIF
.
          MATCH    SP70,PTDADCTS
          IF       @EQUAL
            MOVE     "Blank Record            ",ERRMSG
            CALL     PRIN0000
          ENDIF
.
          GOTO     PROD1000
.
PROD9000  CALL     ENDP0000                      * Print End of Report
.
PROD9999  RETURN
+
.*********************************************************************
.         print a new page
.*********************************************************************
HEAD0000  CALL      HEAD80   
.
          CALL      UND80  
          PRINT     *1,"| Admission ",*20,"| Error ",*50,"| Status",*80,"|"
.
          CALL      UND80 
.
HEAD9999  RETURN
+
.*********************************************************************
.         print 
.*********************************************************************
PRIN0000 
.
.
          PRINT     *1,"|",*3,KEY8,*20,"|",ERRMSG,*50,"|":
                    STATUS,*80,"|"
.
PRIN9999  RETURN
+
.**********************************************************************
.*                           ENDP0000                                 *
.*                      Print the end of the report                   *
.**********************************************************************
ENDP0000  CALL      UND80                    * finish off report
.
          PRINT     *N,*30,"*** End of Report ***"
ENDP9999  RETURN
+
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
          INC       PATTRNIO/INC
          INC       PATMI1IO/INC
          INC       PMSVX1IO/INC
          INC       PATDSCIO/INC
          INC       PATCODIO/INC
          INC       PATDADIO/INC
