.******************************************************************************
.
.         OUTLETDS.PBL
.         ------------
.         This include displays Letters from Outpatient letter file
.
.     Modifications: 
.          V11.00.01 23/03/2020 Peter Vela Task 0889143
.                    Fixed OUTLET IO calls
.                    10/03/93  ROD            
.                    Adapted from PATILTDS
.
.------------------------------------------------------------------------------
.         Display Heading, and then loop over entries
.------------------------------------------------------------------------------
.
OUTLETDS  DISPLAY   *P1:3,*EF,*P20:4,*V2LON,*ULON,"OUTPATIENT LETTERS"
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
.
          MOVE      ZERO,OTLTLINO
          PACK      KEY6,OTLTLINO,SP70
          CALL      RSOTLET2
OLDS1000  CALL      RKOTLET2
          BRANCH    OVRCD,OLDS5000
.
          COMPARE   ZERO,OTLTLINO                * Still only letter desc's
          GOTO      OLDS5000 IF NOT EQUAL        * No - exit
.
          MOVE      OTLTTEXT,DIM30
          DISPLAY   *PCCOL:CVERT,*V2LON,OTLTLTNO,SP1,*HOFF,DIM30
.
          ADD       ONE,CVERT
          COMPARE   TWENTY4,CVERT
          GOTO      OLDS1000 IF LESS
.
          MOVE      FIVE,CVERT
          ADD       "40",CCOL
          COMPARE   "70",CCOL
          GOTO      OLDS1000 IF LESS
.
.         Check for new standards question, or old question
.
          BRANCH    CNEWDS,OLDS2000
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSE,ANS
          GOTO      OLDS6000 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      OUTLETDS IF EQUAL
.
          GOTO      OLDS4000
.
.         Use the new standards question
.
OLDS2000  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          CMATCH    ANSC,ANS
          GOTO      OLDS6000 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      OUTLETDS IF EQUAL
.
OLDS4000  MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
          GOTO      OLDS1000
.
.         End of codes
.
OLDS5000  BRANCH    CNEWDS,OLDS6000
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
.
OLDS6000  MOVE      ZERO,OVRCD
          RETURN
