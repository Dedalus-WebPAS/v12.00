.------------------------------------------------------------------------------
. Include Name   : MODTIMFN.PBL
.
. Function       : Include with date/time utility routines
.
. Author         : Don Nguyen
.
. Parameters     : D8        Date (ccyymmdd)
.                  HOURTME   Hour (hh) 
.                  MINTIME   Minutes (mm) 
.                  SECTIME   Seconds (ss)
.
. Modifications  :
.
. V10.05.00  11/12/2014  Don Nguyen         CAR 301494
.            Created New
.------------------------------------------------------------------------------
+
.******************************************************************************
.*                            ASEC0000                                        *
.*               Add one second to date/time                                  *
.* Requires: D8-Date & HOURTME/MINTIME/SECTIME-time value (ccyymmddhhmmss)    *
.* Returns : D8 & HOURTME/MINTIME/SECTIME - incremented by 1 second           *
.******************************************************************************
ASEC0000  MATCH     "59",SECTIME
          IF        !@EQUAL
            MOVE      SECTIME,FORM2              * just increment seconds
            ADD       ONE,FORM2
            MOVE      FORM2,SECTIME
            REP       " 0",SECTIME
            GOTO      ASEC9999
          ENDIF
.
.         The seconds were "59", so reset these to "00" and increment the
.         minutes
.
          MOVE      "00",SECTIME
          MATCH     "59",MINTIME
          IF        !@EQUAL
            MOVE      MINTIME,FORM2              * just increment minutes
            ADD       ONE,FORM2
            MOVE      FORM2,MINTIME
            REP       " 0",MINTIME
            GOTO      ASEC9999
          ENDIF
.
.         The minutes were "59", so reset these to "00" and increment the
.         hours
.
          MOVE      "00",SECTIME
          MOVE      "00",MINTIME
          MATCH     "23",HOURTME
          IF        !@EQUAL
            MOVE      HOURTME,FORM2              * just increment hours
            ADD       ONE,FORM2
            MOVE      FORM2,HOURTME
            REP       " 0",HOURTME
            GOTO      ASEC9999
          ENDIF
.
.         The hours were "59", so reset these to "00" and increment the
.         date
.
          MOVE      "00",SECTIME
          MOVE      "00",MINTIME
          MOVE      "00",HOURTME
.
.         We need to increment the date by 1 day
.
          UNPACK    D8,CCENT,CYEAR,CMON,CDAY
          MOVE      CCENT,CC
          MOVE      CYEAR,YY
          MOVE      CMON,MM
          MOVE      CDAY,DD
          CALL      DMYCON                       * get julian day
          ADD       ONE,JULDAY                   * increment julian day
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON                       * get new date
          PACK      D8,CC,YY,MM,DD,SP70
          REP       " 0",D8
.
ASEC9999  RETURN
+
