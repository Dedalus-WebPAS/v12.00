.******************************************************************************
.* System         : Outpatients                                               *
.* Program        : IBAOUT57.PBL                                              *
.* Name           : Clinic Appointment Listing                                *
.******************************************************************************
.* Date           : 20/02/1997                                                *
.* Author         : Graham Sims                                               *
.* Function       : Program to print clinic appointment details for WGH       *
.*                  Consultancies.                                            *
.* Modifications  :                                                           *
.*        V10.07.01 30/10/2015  Ebon Clements CAR 268970                      *
.*                  Change to use OUTCLIFD index 1 instead of index 2         *
.*        V10.05.01 15/07/2014  Ebon Clements CAR 261630                      *
.*                  Removed port number from temp file name                   *
.*        V9.10.01  18/08/2008  Peter Vela    CAR 173240                      *
.*                  Added Mobile Phone to display                             *
.*        V9.09.02  04/03/2008  Peter Vela    CAR 162914                      *
.*                  Recompiled for KYOUTCLI - readk of outcliaf               *
.*        V9.09.01  28/07/2007  Jill Habasque CAR 103645                      * 
.*                  Recompiled for KYOUTCLI - read of pmshcpaf                *
.*        V9.02.01  28/11/2002  Lina Vo                                       * 
.*                  Remove site specific open of outsitaf                     *
.*        V9.00.B01 13/07/2001  Sandra Barcham                                *
.*                  Replace GP with HCP                                       *
.*        V5.08.02  19/09/2000  Steve Armstrong   srf 647263                  *
.*                  Recompiled for changes to OUTDSCLN                        *
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*                  V5.08.B01  14/03/2000  Steve Armstrong  5.08 Mods         *
.*                             Mods for changes to OUTCLIFD (effective date)  *
.******************************************************************************
.*                  V5.07.B01  02/11/1998 Jim Stathopoulos                    *
.*                             507 Changes                                    *
.*                                                                            *
.******************************************************************************
+
          INC       STD001FD
.
          INC       IBASEQFD/INC
          INC       OUTBOAFD/INC            * Clinic session booking file A
          INC       OUTBB1FD/INC            * Clinic session booking file B
          INC       OUTCLIFD/INC            * Clinic file
          INC       OUTCONFD/INC            * Control file
          INC       OUTSITFD/INC            * Site file
          INC       PATDO1FD/INC            * Doctors file
          INC       PMSHCPFD/INC            * National HCP file
          INC       PATMA1FD/INC            * Master file
          INC       PATPR1FD/INC            * Pre admission file
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
. ----- Tempfile definitions -----
.
TEMP1     IFILE     SQL, FIXED=236, KEY="U1-8,9-14,15-19,20-22"
FILELIN1  INIT      " 236 U1-8,9-14,15-19,20-22"
.
.Name     Type   Length   Physical   Start   Description
.----     ----   ------   --------   -----   -----------
CLINDTE8  DIM       8         8        1     Clinic date (ccyymmdd)
CLINCODE  DIM       6         6        9     Clinic code
CLINSTTM  DIM       5         5        15    Clinic start time (hh:mm)
CLINSLOT  DIM       3         3        20    Clinic slot no
CLINDESC  DIM       30        30       23    Clinic description
SITEDESC  DIM       30        30       53    Site description
CLINDAY   FORM      1         2        83    Clinic day
SLOTTIME  DIM       5         5        85    Slot time (hh:mm)
VISITYPE  DIM       3         3        90    Visit type (Cat CV)
SLOTSTAT  FORM      1         2        93    Slot status
OUTPATNO  DIM       8         8        95    Outpatient no
URNO      DIM       8         8       103    U/R no
PATNAME   DIM       25        25      111    Patient name
ADDRESS   DIM       35        35      136    Address
DOB       DIM       10        10      171    Date of birth (dd/mm/ccyy)
BUSTELE   DIM       10        10      181    Business telephone no
PRITELE   DIM       10        10      191    Private telephone no
REFDOCDS  DIM       15        15      201    Referral doctor description
MOBTELE   DIM       20        20      216    Mobile telephone no
.
.End of Record                        236
.
. ----- Program Variables -----
.
CLINCNT   FORM      6                       * Clinic counter
CLINICID  DIM       6                       * Clinic id
CURDTE8   DIM       8                       * Current date (ccyymmdd)
CMDLINE   DIM       127
.
DIM40     DIM       40                      * For OUTDSCLN
.
ENDDTE8   DIM       8                       * End date (ccyymmdd)
FNAMEA    DIM       8                       * Tempfile name
.
OUTRDCNT  FORM      6                       * Outpat read counter
OUTWRCNT  FORM      6                       * Outpat write counter
.
SAVCCODE  DIM       6                       * Save the clinic code
SAVCDTE8  DIM       8                       * Save the clinic date (ccyymmdd)
SAVCSTTM  DIM       5                       * Save the clinic start time
SITEID    DIM       6                       * Site id
STRTDTE8  DIM       8                       * Start date (ccyymmdd)
.
TOTALCNT  FORM      6                       * Total counter
.
. ----- Program Constants -----
.
SP100     INIT      "                                                  ":
                    "                                                  "
Z20       INIT      "zzzzzzzzzzzzzzzzzzzz"
.
PRGID     INIT      "IBAOUT57"
PRGNAM    INIT      "Clinic Appointment Listing"
VERSION   INIT      "V12.00.00"
+
.******************************************************************************
.*                                   ML0000                                   *
.*                                 Main Module                                *
.******************************************************************************
ML0000    CALL      INIT0000                * Initialise variables & open files
ML1000    CALL      OPTN0000                * Choose main option
          BRANCH    OPTION,ML2000
          BRANCH    EXIT,ML9000
.
ML2000    CALL      CLIN0000                * Keyin the clinic
          BRANCH    EXIT,ML1000
.
ML3000    CALL      DATE0000                * Keyin the date range
          BRANCH    EXIT,ML2000
.
          CALL      CONTQST                 * Ok to continue (Y/N/C) ?
          BRANCH    CEXIT,ML4000,ML3000,ML1000
          GOTO      ML1000
.
ML4000    CALL      LOAD0000                * Load the tempfile
          CALL      PRNT0000                * Print the tempfile
          GOTO      ML1000
.
ML9000    CALL      KILL0000                * Delete the tempfile
ML9999    CHAIN     PGM
+
.******************************************************************************
.*                                   INIT0000             Called by: ML0000   *
.*                        Initialise Variable & Open Files                    *
.******************************************************************************
INIT0000  CALL      DISPHEAD                * Display screen heading
.
          DISPLAY   *P56:24,"Opening controlf";
          OPEN      CONTROLF,"controlf"
.
          PACK      CFNAME,UID,FILBOKA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA1,CFNAME
.
          PACK      CFNAME,UID,FILBB1A1
          DISPLAY   *P64:24,CFNAME;
          CALL      OPOTBB11
.
          PACK      CFNAME,UID,FILCLIA1
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTCLIA1,CFNAME
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
.
          DISPLAY   *P64:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
.
          DISPLAY   *P64:24,"pmshcpaf";
          OPEN      PMSHCPA1,"pmshcpaf"
.
          DISPLAY   *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P64:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P64:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMEA
.
          MOVE      ONE,CNEWDS              * ? keyin on display
          MOVE      ONE,CNOUNDLN            * Dont print report header
          CLOCK     TIME,CTIMEIS
          PACK      CURDTE8,CCC,CYY,CMM,CDD
          REP       " 0",CURDTE8
          PACK      SITEID,IDDD,SP6
.
INIT9000  MOVE      ZERO,EXIT
INIT9999  RETURN
+
.******************************************************************************
.*                                  OPTN0000              Called by: ML0000   *
.*                                Choose Option                               *
.******************************************************************************
OPTN0000  MOVE      ZERO,OPTION
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = By Clinic Id & Date Range":
                    *P1:7,"Select Option : ";
OPTN1000  KEYIN     *P17:7,*V2LON,*DV,UNDLN2,*P17:7,OPTION;
.
          COMPARE   ZERO,OPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    OPTION,OPTN9000
.
          BEEP
          GOTO      OPTN1000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
.******************************************************************************
.*                                   CLIN0000             Called by: ML0000   *
.*                             Keyin The Clinic Code                          *
.******************************************************************************
CLIN0000  DISPLAY   *P1:9,*EF,"Clinic Id     : ";
          MOVE      SP6,CLINICID
          MOVE      "17",ECOL
          MOVE      "9",EVERT
          MOVE      SP6,CKYIDEF6
          MOVE      ZERO,CKYIMAND
          CALL      KYOUTCLI                * Keyin the clinic id
          BRANCH    EXIT,CLIN1000,CLIN9500,CLIN9500
.
          MOVE      OCACLIN,CLINICID
          DISPLAY   *P17:9,*EL,*V2LON,CLINICID,*P25:9,OCADESC;
          GOTO      CLIN9000
.
CLIN1000  MOVE      SP6,CLINICID
          DISPLAY   *P17:9,*EL,*V2LON,"All Patients";
.
CLIN9000  MOVE      ZERO,EXIT
          GOTO      CLIN9999
.
CLIN9500  MOVE      ONE,EXIT
CLIN9999  RETURN
+
.******************************************************************************
.*                                  DATE0000              Called by: ML0000   *
.*                            Keyin The Date Range                            *
.******************************************************************************
DATE0000  MOVE      "17",CCOL
          DISPLAY   *P1:11,"Starting Date : ",*EF:
                    *P1:12,"Ending Date   : ";
.
          MOVE      ONE,CCANLDTE            * Blank cancel default
.
. ----- Keyin start date -----
.
DATE1000  MOVE      "11",CVERT
          MOVE      CCC,CCENT
          UNPACK    SP6,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE9500          * Nothing entered
          BRANCH    CQUEST,DATE1000
.
          PACK      STRTDTE8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",STRTDTE8
.
. ----- Keyin end date -----
.
DATE2000  MOVE      "12",CVERT
          UNPACK    STRTDTE8,CCENT,CYEAR,CMON,CDAY
          CALL      KEYDATE                 * Keyin date
          BRANCH    OVRCD,DATE1000          * Nothing entered
          BRANCH    CQUEST,DATE2000
.
          PACK      ENDDTE8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",ENDDTE8
.
          MATCH     STRTDTE8,ENDDTE8
          IF        @LESS
            DISPLAY   *P1:24,*B,*EL,"End date must after the start date.  ";
            CALL      HITENTER 
            GOTO      DATE2000
          ENDIF
.
DATE9000  MOVE      ZERO,EXIT
          GOTO      DATE9999
.
DATE9500  MOVE      ONE,EXIT
DATE9999  RETURN
+
.******************************************************************************
.*                                  LOAD0000              Called by: ML0000   *
.*                              Load The Tempfile                             *
.******************************************************************************
LOAD0000  CALL      OPEN0000                * Open the tempfile
          MOVE      ZERO,OUTRDCNT           * Clear the outpat read counter
          MOVE      ZERO,OUTWRCNT           * Clear the outpat write counter
          DISPLAY   *P1:24,*EL,"Scanning : ",*P35:24,"Loading : ";
          CALL      CLER0000                * CLear the tempfile variables
.
          PACK      KEY6,SITEID
          CALL      RDSITA1                 * Read the site file
          IF        OVRCD<>1
            MOVE      OSTDESC,SITEDESC      * Site description
          ENDIF
.
          PACK      KEY28,SITEID,CLINICID,SP30
          CALL      RDSBOKA1                * Position on & read a clinic
LOAD1000  CALL      RDKBOKA1                  session booking file record
          BRANCH    OVRCD,LOAD9000
.
          MATCH     SITEID,OBASITE
          GOTO      LOAD9000 IF NOT EQUAL   * Different site id
.
          ADD       ONE,OUTRDCNT            * Increment the outpat read counter
          IF        OUTRDCNT%20=1
            DISPLAY   *P12:24,*V2LON,OBACLIN,SP2,OBASLOT;
          ENDIF
.
          MATCH     SP6,CLINICID
          GOTO      LOAD2000 IF EQUAL       * No clinic specified, get all
.
          MATCH     CLINICID,OBACLIN
          GOTO      LOAD9000 IF NOT EQUAL   * Different clinic, exit
.
LOAD2000  MATCH     STRTDTE8,OBADATE
          GOTO      LOAD1000 IF LESS        * Clinic date < start date, get next
.
          MATCH     OBADATE,ENDDTE8
          GOTO      LOAD1000 IF LESS        * End date < clinic date, get next
.
          COMPARE   TWO,OBASTAT
          GOTO      LOAD1000 IF EQUAL       * Ignore unused slots
.
          COMPARE   THREE,OBASTAT
          GOTO      LOAD1000 IF EQUAL       * Ignore extended slots
.
          PACK      KEY20,OBASITE,OBACLIN,OBADATE
          CALL      RDCLIA1
          IF        OVRCD = 1
            CALL      RDPCLIA1
            IF        OVRCD = 0
              MATCH     OBASITE,OCASITE
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ELSE
                MATCH     OBACLIN,OCACLIN
                IF        !@EQUAL
                  MOVE      ONE,OVRCD
                ENDIF
              ENDIF
            ENDIF
          ENDIF
          BRANCH    OVRCD,LOAD1000
.
          CALL      CLER0000                * CLear the tempfile variables
          MOVE      OBADATE,CLINDTE8        * Clinic date
          MOVE      OBACLIN,CLINCODE        * Clinic code
          MOVE      OBASTRT,CLINSTTM        * Clinic start time
          MOVE      OBASLOT,CLINSLOT        * Clinic slot no
.
          PACK      CLINDESC,OCADESC,SP30   * Clinic description
          MATCH     SP6,OCADOCT
          IF        !@EQUAL
            PACK      KEY6,OCADOCT
            CALL      RDDOCT1               * Read a doctors file record
            IF        OVRCD<>1
              PACK      CLINDESC,SP30
              CLEAR     CLINDESC
              STRIP     DTITL
              APPEND    DTITL,CLINDESC
              APPEND    ". ",CLINDESC
              APPEND    DSNAME,CLINDESC
              APPEND    SP30,CLINDESC
              RESET     CLINDESC            * Clinic description
            ENDIF
          ENDIF
.
          MOVE      OBADAY,CLINDAY          * Clinic day
          MOVE      OBATIME,SLOTTIME        * Slot start time
          MOVE      OBAVISIT,VISITYPE       * Visit type
          MOVE      OBASTAT,SLOTSTAT        * Slot status
.
          COMPARE   ZERO,OBASTAT
          GOTO      LOAD4000 IF EQUAL       * Slot not booked, dont print
.                                             patient details
.
          PACK      KEY8,OBAURNO
          CALL      RDMAST1                 * Read a master file record
          COMPARE   ONE,OVRCD
          GOTO      LOAD3000 IF NOT EQUAL   * Record found continue
.
          PACK      KEY8,OBAOUTNO
          CALL      RDPRAM1                 * Read a pre admission file record
          BRANCH    OVRCD,LOAD4000
.
LOAD3000  MOVE      OBAOUTNO,OUTPATNO       * Outpatient no
          MOVE      OBAURNO,URNO            * U/R no
.
          CLEAR     PATNAME
          STRIP     PSNAME
          APPEND    PSNAME,PATNAME
          APPEND    ", ",PATNAME
          APPEND    PGNAME,PATNAME
          APPEND    SP30,PATNAME
          RESET     PATNAME                 * Patient name
.
          CLEAR     ADDRESS
          STRIP     PADD1
          APPEND    PADD1,ADDRESS
          APPEND    SP1,ADDRESS
          STRIP     PADD2
          APPEND    PADD2,ADDRESS
          APPEND    SP1,ADDRESS
          STRIP     PSUBRB
          APPEND    PSUBRB,ADDRESS
          APPEND    SP1,ADDRESS
          APPEND    PPOST,ADDRESS
          APPEND    SP30,ADDRESS
          RESET     ADDRESS                 * Address
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the DOB
          REP       " 0",CPCDATE
          MOVE      CPCDATE,DOB             * Date of birth
.
          MOVE      PTELEB,BUSTELE          * Business telephone no
          MOVE      PTELEP,PRITELE          * Private telephone no
          MOVE      PTMXCELL,MOBTELE        * Mobile telephone no
.
LOAD4000  PACK      KEY8,OBAOUTNO
          CALL      RDBOKB1                 * Read a clinic ses book file record
          IF        OVRCD<>1
            PACK      KEY10,OTBBRFGP,SP70
            CALL      RDPMHCP1              * Read a national HCP file record
            IF        OVRCD<>1
              MOVE      PMHCSNAM,REFDOCDS   * Referral doctor description
            ENDIF
          ENDIF
.
          PACK      KEY22,CLINDTE8,CLINCODE,CLINSTTM,CLINSLOT
          CALL      RATEMP1                 * Position read a tempfile record
          IF        OVRCD=1
            CALL      WRTEMP1               * Write a tempfile record
.
            ADD       ONE,OUTWRCNT          * Increment the outpat write counter
            IF        OUTWRCNT%20=1
              DISPLAY   *P45:24,*V2LON,CLINCODE,SP2,CLINSLOT;
            ENDIF
          ENDIF
          GOTO      LOAD1000
.
LOAD9000  MOVE      ZERO,EXIT
LOAD9999  RETURN
+
.******************************************************************************
.*                                  CLER0000              Called by: LOAD0000 *
.*                        Clear The Tempfile Variables                        *
.******************************************************************************
CLER0000  UNPACK    SP100,CLINDTE8,CLINCODE,CLINSTTM,CLINSLOT,CLINDESC
          UNPACK    SP100,SITEDESC,SLOTTIME,VISITYPE,OUTPATNO,URNO
          UNPACK    SP100,PATNAME,ADDRESS,DOB,BUSTELE,PRITELE,MOBTELE
          UNPACK    SP100,REFDOCDS
          MOVE      ZERO,CLINDAY
          MOVE      ZERO,SLOTSTAT
.
CLER9000  MOVE      ZERO,EXIT
CLER9999  RETURN
+
.******************************************************************************
.*                                  PRNT0000              Called by: ML0000   *
.*                             Print The Tempfile                             *
.******************************************************************************
PRNT0000  MOVE      ZERO,CLINCNT            * Reset the clinic counter
          MOVE      ZERO,TOTALCNT           * Reset the total counter
          MOVE      ZERO,CPAGENO            * Reset the page no
          DISPLAY   *P1:24,*EL,"Printing : ";
.
          PACK      KEY22,SP30
          CALL      RSTEMP1                 * Position on & read a tempfile
          CALL      RKTEMP1                   record
          CALL      HEAD0000                * Print report header
.
          PACK      KEY22,SP30
          CALL      RSTEMP1                 * Position on & read a tempfile
PRNT1000  CALL      RKTEMP1                   record
          BRANCH    OVRCD,PRNT8000
.
          IF        TOTALCNT%10=0
            DISPLAY   *P12:24,*V2LON,CLINCODE,SP2,CLINSLOT;
          ENDIF
.
          COMPARE   CLNO,FIFTY9
          CALL      HEAD0000 IF LESS        * Print report header
.
          CALL      CHCK0000                * Check for change in header
.
          PRINT     *1,"|",SLOTTIME,*7,"|",URNO,*16,"|",VISITYPE:
                    *21,"|",PATNAME,*47,"|",ADDRESS,*83,"|",DOB:
.                   *94,"|",BUSTELE,SP1,PRITELE,*116,"|",REFDOCDS,*132,"|"
                    *94,"|",PRITELE,SP1,MOBTELE,*116,"|",REFDOCDS,*132,"|"
          ADD       "1",CLNO
.
          IF        SLOTSTAT<>0
            ADD       ONE,CLINCNT           * Increment the clinic counter
            ADD       ONE,TOTALCNT          * Increment the total counter
          ENDIF
          GOTO      PRNT1000
.
PRNT8000  CALL      UND132                  * Print underline
          PRINT     *1,"Total Clinic Visits : ",CLINCNT,*N,*N:
                    *1,"Total Visits        : ",TOTALCNT,*N,*N:
                    *1,"*** End of Report ***"
PRNT9000  MOVE      ZERO,EXIT
PRNT9999  RETURN
+
.******************************************************************************
.*                                  HEAD0000              Called by: PRNT0000 *
.*                           Print The Report Header                          *
.******************************************************************************
HEAD0000  COMPARE   ZERO,CPAGENO
          GOTO      HEAD3000 IF EQUAL       * 1st page just print header
.
          MATCH     SAVCCODE,CLINCODE
          GOTO      HEAD1000 IF NOT EQUAL   * Diff clinic code
.
          MATCH     SAVCDTE8,CLINDTE8
          GOTO      HEAD1000 IF NOT EQUAL   * Diff clinic date
.
          MATCH     SAVCSTTM,CLINSTTM
          GOTO      HEAD2000 IF EQUAL       * Same clinic start time
.
HEAD1000  CALL      UND132                  * Print underline
          PRINT     *1,"Total Clinic Visits : ",CLINCNT
          MOVE      ZERO,CLINCNT            * Reset the clinic counter
          GOTO      HEAD3000
.
HEAD2000  CALL      UND132                  * Print underline
HEAD3000  CALL      HEAD132                 * Print report header
          CALL      SHED0000                * Print report sub header
.
HEAD9000  MOVE      ZERO,EXIT
HEAD9999  RETURN
+
.******************************************************************************
.*                                  CHCK0000              Called by: PRNT0000 *
.*                         Check For Change In Header                         *
.******************************************************************************
CHCK0000  MATCH     SAVCCODE,CLINCODE
          GOTO      CHCK1000 IF NOT EQUAL   * Diff clinic code
.
          MATCH     SAVCDTE8,CLINDTE8
          GOTO      CHCK1000 IF NOT EQUAL   * Diff clinic date
.
          MATCH     SAVCSTTM,CLINSTTM
          GOTO      CHCK9000 IF EQUAL       * Same clinic start time
.
CHCK1000  CALL      UND132                  * Print underline
          PRINT     *1,"Total Clinic Visits : ",CLINCNT
          ADD       "1",CLNO
          MOVE      ZERO,CLINCNT            * Reset the clinic counter
.
          MATCH     SAVCCODE,CLINCODE
          IF        !@EQUAL | CLNO>=53
            CALL      HEAD132               * Print report header
          ENDIF
.
          CALL      SHED0000                * Print report sub header
.
CHCK9000  MOVE      ZERO,EXIT
CHCK9999  RETURN
+
.******************************************************************************
.*                                  SHED0000              Called by: HEAD0000 *
.*                         Print The Report Sub Header             & CHCK0000 *
.******************************************************************************
SHED0000  PACK      KEY73,SP100
          CLEAR     KEY73
          STRIP     CLINDESC
          APPEND    CLINDESC,KEY73
          APPEND    " / ",KEY73
          APPEND    SITEDESC,KEY73
          APPEND    SP100,KEY73
          RESET     KEY73
.
          STRIP     KEY73
          ENDSET    KEY73
          MOVELPTR  KEY73,FORM2
          RESET     KEY73
          ASSIGN    (132-FORM2)/2,FORM2
          PRINT     *FORM2,*+,KEY73
.
          PACK      KEY99,SP100
          CLEAR     KEY99
          MOVE      SP10,KEY10
          LOAD      KEY10,CLINDAY,DMON,DTUE,DWED,DTHU,DFRI,DSAT,DSUN
          STRIP     KEY10
          APPEND    KEY10,KEY99
          APPEND    ", ",KEY99
          MATCH     SP8,CLINDTE8
          IF        !@EQUAL
            UNPACK    CLINDTE8,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE               * Format the clinic date
            REP       " 0",CPCDATE
            APPEND    CPCDATE,KEY99
            APPEND    ", ",KEY99
          ENDIF
          APPEND    CLINSTTM,KEY99
          APPEND    SP100,KEY99
          RESET     KEY99
.
          STRIP     KEY99
          ENDSET    KEY99
          MOVELPTR  KEY99,FORM2
          RESET     KEY99
          ASSIGN    (132-FORM2)/2,FORM2
          PRINT     *FORM2,*+,KEY99
.
          CALL      UND132                  * Print underline
          PRINT     *1,"|Time",*7,"| Pat Id",*16,"|Type":
                    *21,"|",*28,"Patient Name",*47,"|",*61,"Address":
.                   *83,"|  D.O.B.",*94,"|",*103,"Phone",*116,"|    Ref. Dr":
                *83,"|  D.O.B.",*94,"| Home Ph   Mobile Ph",*116,"|    Ref. Dr":
                    *132,"|"
          CALL      UND132                  * Print underline
          ADD       "3",CLNO
.
          MOVE      CLINCODE,SAVCCODE       * Save the clinic code
          MOVE      CLINDTE8,SAVCDTE8       * Save the clinic date
          MOVE      CLINSTTM,SAVCSTTM       * Save the clinic start time
.
SHED9000  MOVE      ZERO,EXIT
SHED9999  RETURN
+
.******************************************************************************
.*                                  OPEN0000              Called by: LOAD0000 *
.*                              Open The Tempfile                             *
.******************************************************************************
OPEN0000  CALL      KILL0000                * Delete the 1st tempfile
.
          CLEAR     CMDLINE
          APPEND    "isbuild ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          APPEND    FILELIN1,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMP1,FNAMEA
OPEN9999  RETURN
+
.******************************************************************************
.*                                  KILL0000              Called by: ML0000   *
.*                             Delete The Tempfile                 & OPEN0000 *
.******************************************************************************
KILL0000  CLOSE     TEMP1
.
          CLEAR     CMDLINE
          APPEND    "iserase ",CMDLINE
          APPEND    FNAMEA,CMDLINE
          RESET     CMDLINE
.
          EXECUTE   CMDLINE,TASKID
KILL9999  RETURN
+
.******************************************************************************
.*                        I/O Routines For The Tempfile                       *
.******************************************************************************
RSTEMP1   RESET     KEY22
          READ      TEMP1,KEY22;;
          RETURN
.
RATEMP1   MOVE      ZERO,OVRCD
          RESET     KEY22
          READ      TEMP1,KEY22;ANS
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMP1;CLINDTE8,CLINCODE,CLINSTTM,CLINSLOT,CLINDESC:
                          SITEDESC,CLINDAY,SLOTTIME,VISITYPE,SLOTSTAT:
                          OUTPATNO,URNO,PATNAME,ADDRESS,DOB:
                          BUSTELE,PRITELE,REFDOCDS,MOBTELE
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   RESET     KEY22
          WRITE     TEMP1,KEY22;CLINDTE8,CLINCODE,CLINSTTM,CLINSLOT,CLINDESC:
                                SITEDESC,CLINDAY,SLOTTIME,VISITYPE,SLOTSTAT:
                                OUTPATNO,URNO,PATNAME,ADDRESS,DOB:
                                BUSTELE,PRITELE,REFDOCDS,MOBTELE
          RETURN
+
.==============================================================================
.
          INC       STD001IO
          INC       KYOUTCLI                * Keyin the clinic id
          INC       OUTDSCLN                * Display the clinic ids
          INC       TFILENAM
.
          INC       IBASEQIO/INC
          INC       OUTBOAIO/INC            * Clinic session booking file A
          INC       OUTBB1IO/INC            * Clinic session booking file B
          INC       OUTCLIIO/INC            * Clinic file
          INC       OUTSITIO/INC            * Site file
          INC       PATDO1IO/INC            * Doctors file
          INC       PMSHCPIO/INC            * National HCP file
          INC       PATMA1IO/INC            * Master file
          INC       PATPR1IO/INC            * Pre admission file
          INC       WEBERRIO/INC
.
