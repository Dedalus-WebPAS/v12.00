.******************************************************************************
.*        Validate if all theatre charges are complete for a visit.           *
.*                                                                            *
.*                                                                            *
.*        PARAMETERS:                                                         *
.*          OPTHVISN   Visit number                                           *
.*                                                                            *
.*        RETURN:                                                             *
.*              EXIT   1 - Yes; all theatre charges complete                  *
.*                     0 - No; theatre incomplete                             *
.*          OPTHRICM   Reason Incomplete (Category Rt)                        *
.*                                                                            *
.*        MODIFICATION :                                                      *
.*                     V11.03.01 14/11/2022 J.Tan  Task 0926814               *
.*                     Mod checking for Abandoned case                        *
.*                                                                            *
.******************************************************************************
          DEFPROC   OPTHCMPL
.
          INC       OPRDEAFD/INC
          INC       OPRSRGFD/INC
.
OPTHCM00  MOVE      ONE,EXIT      * default to complete (all charges posted)
          MOVE      SP70,OPTHRICM
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      OPRDETA2,"oprdetaf"
          TRAPCLR   IO
          BRANCH    OVRCD,OPTHCM99
.
          OPEN      OPRSRGA1,"oprsrgaf"
.
          MATCH     SP70,OPTHVISN
          GOTO      OPTHCM99 IF EQUAL
.
          PACK      KEY31,OPTHVISN,SP70
          CALL      RSOPDEA2
OPTHCM10  CALL      RKOPDEA2
          BRANCH    OVRCD,OPTHCM99
.
          MATCH     OPTHVISN,OPDAADMN
          GOTO      OPTHCM99 IF NOT EQUAL
.
          COMPARE   THREE,OPDASTAT     * Cancelled?
          GOTO      OPTHCM10 IF EQUAL
.
.         Checking for Abandoned case
.
          MOVE      ZERO,OVRCD
          PACK      KEY11,OPDAUNIQ,ONE,SP70   * reading surgical table
          CALL      RDOPSRG1
          IF        OVRCD=0
            MATCH     "1",OPSGUY05
            GOTO      OPTHCM10 IF EQUAL       * Abandoned case
          ENDIF
.
          MATCH     "1",OPDAACPD       * All charges posted for theatre record?
          GOTO      OPTHCM90 IF NOT EQUAL
.
          GOTO      OPTHCM10           * Get next theatre record
.
OPTHCM90  MOVE      OPDARICM,OPTHRICM
          MOVE      ZERO,EXIT          * All charges NOT posted (Incomplete)
          GOTO      OPTHCM99
.
.
          INC       IBAOVRIO/INC
          INC       OPRDEAIO/INC
          INC       OPRSRGIO/INC
.
OPTHCM99  ENDPROC
+
