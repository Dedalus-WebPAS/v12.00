.****************************************************************************** .*  Include Name  : PATFNDKY.PBL                                              *
.*  Description   : Keyin of a Health fund                                    *
.*  Author        : Wilma Flintstone                                          *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF6 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory   (0=No  1=Yes)             *
.*                  CKYIMODE - is keyin insert mode (0=No  1=Yes)             *
.*                  CKYINACT - display inactive codes (0=No  1=Yes)           *
.*                                                                            *
.*  Returns       : HCODE   - Keyed in code                                   *
.*                  HFNAME   - Description                                    *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing Input                                  *
.*                       = 2   EXITCHAR input                                 *
.*                       = 3   Spaces input                                   *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.* Mods:                                                                      *
.*        V5.08.03  25/10/2000  Dean Cramer   SRF 6710                        *
.*                  Recomplied for PATFNDDS                                   *
.*                  Changed for health fund table code size increase to       *
.*                  8 characters                                              *
.*        V5.08.02  19/07/2000  Jill Habasque                                 *
.*                  Added check of CKYINACT to not allow inactive HF          *
.*        V5.08.01  06/06/2000  Jill Habasque                                 *
.*                  Changed from key10 to key14 for the health fund file      *
.*        V5.02.01  23/03/1995  Graeme Williams  SRF 135688                   *
.*                  Recompiled for changes to PATFNDKY - blank HF's are now   *
.*                  distinguished from no HF entered.                         *
.******************************************************************************
PATFNDKY
KFUND010  MATCH     SP6,CKYIDEF6                  * using a default ?
          GOTO      KFUND050 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN6:      * no default used
                    *PECOL:EVERT,*V2LON,*NOSTRIP,HCODE:
                    *PECOL:EVERT,*DV,HCODE
          GOTO      KFUND080
.
KFUND050  MOVE      CKYIDEF6,HCODE
          KEYIN     *PECOL:EVERT,*DV,CKYIDEF6:     * default used
                    *PECOL:EVERT,*V2LON,*RV,*NOSTRIP,HCODE:
                    *PECOL:EVERT,*DV,HCODE
.
.         check what was entered
.
KFUND080  CALL      FNDCK000
          BRANCH    EXIT,KFUND100,KFUND705,KFUND805,KFUND905,KFUND855
.                         "?"      valid    nothing exitchar spaces
          GOTO      KFUND010
.
.         a "?" was input
.
KFUND100  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
KFUND105  MOVE      ONE,CNEWDS
          PACK      KEY6,HCODE,SP6
          CALL      PATFNDDS
.
.         now ask for code while leaving displayed codes on screen
.
KFUND150  MATCH     SP6,CKYIDEF6                  * using a default
          GOTO      KFUND200 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Code    : ":
                    *P11:24,*DV,UNDLN6:      * no default used
                    *P11:24,*V2LON,*NOSTRIP,HCODE:
                    *P11:24,*DV,HCODE
          GOTO      KFUND300
.
KFUND200  MOVE      CKYIDEF6,HCODE
          KEYIN     *P1:24,*EL,*DV,"Code    : ":
                    *P11:24,*DV,HCODE:     * default used
                    *P11:24,*V2LON,*RV,*NOSTRIP,HCODE:
                    *P11:24,*DV,HCODE
.
.         repeat the validation of the code
.         check what was entered
.
KFUND300  CALL      FNDCK000
          BRANCH    EXIT,KFUND105,KFUND700,KFUND800,KFUND900,KFUND850
.                         "?"      valid    nothing exitchar spaces
.
          GOTO      KFUND150
.
.         a valid code was entered 
. 
KFUND700  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,HCODE
.
KFUND705  MOVE      FALSE,EXIT                    * valid input
          GOTO      KFUND999 
.
.         nothing was input
.
KFUND800  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,HCODE
.
KFUND805  MOVE      SP20,HFNAME                    * blank out description
          MOVE      ONE,EXIT                       * Nothing entered
          GOTO      KFUND999 
.
.         spaces were input
.
KFUND850  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,HCODE
.
KFUND855  MOVE      SP20,HFNAME                    * blank out description
          MOVE      THREE,EXIT                     * Spaces entered
          GOTO      KFUND999 
.
.         EXITCHAR input
.
KFUND900  CALL      FRESCR00                      * free the screen saved
.
KFUND905  MOVE      TWO,EXIT                      * EXITCHAR
.
KFUND999  RETURN
+
.***************************************************************************
.*                              FNDCK000             Called by: PATFNDKY   *
.*                        check what was entered                           *
.*          Returns:  EXIT  = 0   invalid                                  *
.*                          = 1   "?"                                      *
.*                          = 2   valid                                    *
.*                          = 3   nothing                                  *
.*                          = 4   exitchar                                 *
.*                          = 5   spaces                                   *
.***************************************************************************
FNDCK000  RESET     HCODE
          GOTO      FNDCK700 IF EOS
.
          ENDSET    HCODE
          APPEND    SP6,HCODE
          RESET     HCODE
.
          MATCH     EXITCHAR,HCODE
          GOTO      FNDCK600 IF EQUAL
.
          MATCH     SP6,HCODE
          GOTO      FNDCK750 IF EQUAL
.
. check for a "?"
.
          MATCH     QUEST,HCODE
          GOTO      FNDCK800 IF EQUAL
.
. a code was entered so validate
. 
FNDCK500  MOVE      ZERO,PHFTACT
          PACK      KEY14,HCODE,ZERO,ZERO,ZERO,ZERO,SP10
          CALL      RDFUND1
          IF        CKYIMODE=ZERO
            BRANCH    OVRCD,FNDCK900
          ELSE
            COMPARE   ZERO,OVRCD
            GOTO      FNDCK900 IF EQUAL
          ENDIF
.
.    ----- dont display inactive codes if CKYINACT=0
.
          IF        CKYINACT=0
            IF        PHFTACT=1
              DISPLAY   *P1:24,*EL,*V2LON,"Inactive Code. ";
              CALL      HITENTER
              MOVE      SIX,EXIT
              GOTO      FNDCK999
            ENDIF
          ENDIF
.
          MOVE      TWO,EXIT
          GOTO      FNDCK999
.
. exitchar entered
.
FNDCK600  MOVE      FOUR,EXIT
          GOTO      FNDCK999
.
. nothing entered
.
FNDCK700  BRANCH    CKYIMAND,FNDCK950             * mandatory
          MOVE      THREE,EXIT
          MOVE      SP20,HCODE                     * clear code field
          MOVE      SP20,HFNAME                    * clear desc field
          GOTO      FNDCK999
.
. spaces entered
.
FNDCK750  BRANCH    CKYIMAND,FNDCK950             * mandatory
          MOVE      FIVE,EXIT
          MOVE      SP20,HCODE                     * clear code field
          MOVE      SP20,HFNAME                    * clear desc field
          GOTO      FNDCK999
.
. "?" entered
.
FNDCK800  MOVE      ONE,EXIT
          GOTO      FNDCK999
.
. invalid code entered
.
FNDCK900  
          IF        CKYIMODE=0
            DISPLAY   *P1:24,*EL,*B,"Invalid Code. ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      FNDCK999
.
FNDCK950  BEEP
          MOVE      ZERO,EXIT
.
FNDCK999  RETURN
.
          INC       PATFNDDS
.
+
