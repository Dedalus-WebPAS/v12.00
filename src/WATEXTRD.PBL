
.******************************************************************************
.*                                WATEXTRD                                    *
.*  Caleb Sharman  6/12/1999                                                  *
.*         Extracts the required information from various files               *
.*              for general letters                                           *
.******************************************************************************
.
.
.         V5.08.01  21/07/2000 Caleb Sharman  SRF 646913
.                   Mods to speed up the search process depending on the
.                   criteria entered
.
.******************************************************************************
.*                                EXT20000                                    *
.*                 extract records from Procedure file                        *
.*                 check if valid ( check parameters)                         *
.******************************************************************************
EXT20000  MOVE      ZERO,PROPTION      * signal SVRC to display data to screen
          CALL      CRET0000
          MOVE      ONE,COUNT2
          MOVE      ONE,SCREEN
.
EXT20200  DISPLAY   *P1:3,*EF,*V2LON:
                    *ULON,*P1:4,"Cnt",*P5:4,"  U/R No",*P14:4,"Code     ":
                    *P24:4,"Description         ",*P45:4,"On List   ":
                    *P56:4,"Review    ",*P67:4,"Status      ";
.
          BRANCH    EXTRFLAG,DSPP0000
EXT20300
          MOVE      ZERO,EXTRFLAG      * set search thru wattr1af
          DISPLAY   *P1:5,*EF,*P1:24,*BLINKON,"Searching...";
          MOVE      ZERO,COUNT         * zero the number of records counter
          MOVE      FIVE,YPOS
.
          IF        FLGUR = 0
            IF        FLGD1 = 1
              PACK      KEY27,KSTDATE1,SP30   * date on list range found
              CALL      RDSTREA3              * position in file
              CALL      RDKTREA3              * read next record
              BRANCH    OVRCD,EXT28700        * eof - finished
.
              MATCH     WMDATE1,KTODATE1      * past end date ?
              GOTO      EXT28700 IF LESS      * yes - finished
              GOTO      EXT23000              * no - valid record
            ELSE
              IF        FLGST = 1
                PACK      KEY20,KSTSTAT,SP20  * proc. status range found
                CALL      RDSTREA2            * position in file
                CALL      RDKTREA2            * read next record
                BRANCH    OVRCD,EXT28700      * eof - finished
.
                COMPARE   WMSTAT,KSTSTAT         * same status still ?
                GOTO      EXT28700 IF NOT EQUAL  * no - finished
                GOTO      EXT23000               * yes - valid record
              ENDIF
            ENDIF
          ENDIF
.
          PACK      KEY19,KSTUR,SP20        * start UR on list
          CALL      RDSTREA1
	  CALL      RDKTREA1
          BRANCH    OVRCD,EXT28700
          CALL      MACH0000
.
EXT22222  IF        FLGUR = 0
            IF        FLGD1 = 1
              CALL      RDKTREA3            * using date on list - read next rec
              BRANCH    OVRCD,EXT28700      * eof - finished
.
              MATCH     WMDATE1,KTODATE1    * past end date ?
              GOTO      EXT28700 IF LESS    * yes - finished
              GOTO      EXT23000            * no - valid record
            ELSE
              IF        FLGST = 1
                CALL      RDKTREA2          * using proc. status, read next rec
                BRANCH    OVRCD,EXT28700    * eof - finished
.
                COMPARE   WMSTAT,KSTSTAT         * same status still ?
                GOTO      EXT28700 IF NOT EQUAL  * no - finished
                GOTO      EXT23000               * yes - valid record
              ENDIF
            ENDIF
          ENDIF
.
          CALL      RDKTREA1
          BRANCH    OVRCD,EXT28700       
          MATCH     KTOUR,SP8
          GOTO      EXT23000 IF EQUAL
          MATCH     WMURNO,KTOUR
          GOTO      EXT28700 IF LESS
.
EXT23000  DISPLAY   *P55:24,*EL,*V2LON,WMURNO
          CALL      MACH0000         * match record against search criteria
.
          BRANCH    EXIT,EXT22222      * exit=1 if match failed
.
          CALL      SISP0000                * see if on suspension
          BRANCH    EXIT,EXT22222           * code is suspeneded
.
EXT23333  RESET     KEY27
          PACK      KEY27,WMDATE1,WMURNO,WMCODE,DWMCNT
          UNPACK    KEY27,YYDATE1,YYURNO,YYCODE,YYMCNT
.
          PACK      KEY27,YYDATE1,YYURNO,YYCODE,YYMCNT,SP30
.
EXT23500  WRITE     TEMPF2,KEY27;YYDATE1,YYURNO,YYCODE,YYMCNT
          ADD       ONE,COUNT
          GOTO      EXT22222
.
EXT24444  CALL      DSPP0000           * show screen a single record
.
          MATCH     ANSX,ANS
          IF        @EQUAL
            MOVE      ONE,EXIT
          ENDIF
.
          GOTO      EXT29999
.
EXT28700  COMPARE   ZERO,COUNT
          GOTO      EXT24444 IF NOT EQUAL
          DISPLAY   *P1:24,*B,"No records found.  ";
          CALL      HITENTER
          GOTO      EXT29000
.
EXT29000  MOVE      ONE,EXIT           * go back to previous screen
          GOTO      EXT29999
.
EXT29500  MOVE      ZERO,EXIT           
          GOTO      EXT29999
.
EXT29999  RETURN
+
.*********************************************************************
.         see if this procedure is suspended
.         Returns : EXIT = 1      procedure is suspended
.*********************************************************************
SISP0000  MOVE      ZERO,EXIT
          PACK      KEY21,WMURNO,WMCODE,WMCNT,SP2
          CALL      RSWTSUS1
          CALL      RKWTSUS1
          BRANCH    OVRCD,SISP9999          * not suspened
          PACK      KEY18,WTSUURNO,WTSUCODE,WTSUCNT
          MATCH     KEY18,KEY21
          GOTO      SISP9999 IF NOT EQUAL   * not suspended
          MOVE      ONE,EXIT
.
SISP9999  RETURN
+
.******************************************************************************
.*                                MACH0000                                    *
.*           MATCH records against the specified search fields                *
.******************************************************************************
MACH0000  BRANCH    FLAGUR TO MACH0100             * UR number
          MATCH     KSTUR,WMURNO
          GOTO      MACH9990 IF LESS
          MATCH     WMURNO,KTOUR
          GOTO      MACH9990 IF LESS
.
MACH0100  BRANCH    FLAGCO TO MACH0200             * procedure code
          MATCH     KSTCODE,WMCODE
          GOTO      MACH9990 IF LESS
          MATCH     WMCODE,KTOCODE
          GOTO      MACH9990 IF LESS
.
MACH0200  BRANCH    FLAGST TO MACH0300             * procedure status
          COMPARE   KSTSTAT,WMSTAT
          GOTO      MACH9990 IF LESS
          COMPARE   WMSTAT,KSTSTAT
          GOTO      MACH9990 IF LESS
.
MACH0300  BRANCH    FLAGD1,MACH0400                * date on list
          MATCH     KSTDATE1,WMDATE1
          GOTO      MACH9990 IF LESS
          MATCH     WMDATE1,KTODATE1
          GOTO      MACH9990 IF LESS
.
MACH0400  BRANCH    FLAGD2 TO MACH0500             * date last review
          MATCH     KSTDATE2,WMDATE2
          GOTO      MACH9990 IF LESS
          MATCH     WMDATE2,KTODATE2
          GOTO      MACH9990 IF LESS
.
MACH0500  BRANCH    FLAGD3 TO MACH0600             * scheduled admission
          MATCH     KSTDATE3,WMDATE3
          GOTO      MACH9990 IF LESS
          MATCH     WMDATE3,KTODATE3
          GOTO      MACH9990 IF LESS
.
MACH0600  BRANCH    FLAGUN TO MACH0700             * unit
          MATCH     KSTUNIT,WMUNIT
          GOTO      MACH9990 IF LESS
          MATCH     WMUNIT,KTOUNIT
          GOTO      MACH9990 IF LESS
.
MACH0700  BRANCH    FLAGDO TO MACH0800             * doctor
          MATCH     KSTDOC,WMDOCTOR
          GOTO      MACH9990 IF LESS
          MATCH     WMDOCTOR,KTODOC
          GOTO      MACH9990 IF LESS
.
MACH0800  BRANCH    FLAGES TO MACH0900             * estimated stay
          COMPARE   KSTSTAY,WMSTAY
          GOTO      MACH9990 IF LESS
          COMPARE   WMSTAY,KTOSTAY
          GOTO      MACH9990 IF LESS
.
MACH0900  BRANCH    FLAGPT TO MACH1000             * priority
          MATCH     KSTPTY,WMPTY
          GOTO      MACH9990 IF NOT EQUAL
.
MACH1000  BRANCH    FLAGSN TO MACH2200             * short notice
          MATCH     KSTNOT,WMNOTICE
          GOTO      MACH9990 IF NOT EQUAL
.
MACH2200
          MOVE      ZERO,EXIT
          GOTO      MACH9999
MACH9990
          MOVE      ONE,EXIT
.
MACH9999  RETURN
+
.******************************************************************************
.*                                SVRC0000                                    *
.*                           Show valid record                                *
.******************************************************************************
SVRC0000  MOVE      SP10,DATE10
          MOVE      SP10,DATE20
          MOVE      SP10,DATE30
.
          MATCH     SP8,WMDATE1                  * date on list
          GOTO      SVRC3333 IF EQUAL
          UNPACK    WMDATE1,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      DATE10,CPCDATE,SP10
          REP       " 0",DATE10
SVRC3333
          MATCH     SP8,WMDATE2                  * last review date
          GOTO      SVRC4444 IF EQUAL
          UNPACK    WMDATE2,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      DATE20,CPCDATE,SP10
          REP       " 0",DATE20
SVRC4444
          MATCH     SP8,WMDATE3
          GOTO      SVRC5555 IF EQUAL
          UNPACK    WMDATE3,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PACK      DATE30,CPCDATE,SP10
          REP       " 0",DATE30
SVRC5555
          BRANCH    PROPTION,SVRC9999       * test if printing record
.
          MOVE      WMDESC,TDESC
          LOAD      DESCSTAT,WMSTAT,STAT1,STAT2,STAT3,STAT4:
                                    STAT5,STAT6,STAT7,STAT8
.
          DISPLAY   *P1:YPOS,*V2LON,COUNT,*HOFF,*P5:YPOS,WMURNO:
                    *P14:YPOS,WMCODE,*P24:YPOS,TDESC,*P45:YPOS,DATE10:
                    *P56:YPOS,DATE20,*P67:YPOS,DESCSTAT;
SVRC9999  RETURN
+
.******************************************************************************
.*                                DSPP0000                                    *
.*                 extract records from Procedure file                        *
.*                 check if valid ( check parameters)                         *
.******************************************************************************
DSPP0000  MOVE      ZERO,FIELD
          MOVE      ZERO,COUNT
          MOVE      ONE,CPAGENO
          PACK      KEY27,SP30
          RESET     KEY27
          READ      TEMPF2,KEY27;;
DSPP1000  READKS    TEMPF2;YYDATE1,YYURNO,YYCODE,YYMCNT
          GOTO      DSPP5000 IF OVER
.
          PACK      KEY27,YYDATE1,YYURNO,YYCODE,YYMCNT
          CALL      RDTREA3
          ADD       ONE,COUNT
          ADD       ONE,YPOS 
          COMPARE   TEN7,COUNT 
          GOTO      DSPP4000 IF NOT LESS
.
DSPP3000  ADD       ONE,FIELD
          CALL      SVRC0000
          PACK      TMPKEY[FIELD],KEY27
          GOTO      DSPP1000
.
.         new page needed
.
DSPP4000  BRANCH    CPAGENO,DSPP4500
.
.         in middle pages : Next & First options
.
DSPP4100  MOVE      "1",CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,DSPP9999:
                         DSPP7000:
                         DSPP8000:
                         EXT29999
          GOTO      DSPP6000
.
.         on first page : Next option
.
DSPP4500  MOVE      "2",CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,DSPP9999:
                         DSPP7000:
                         DSPP5900:
                         EXT29999
          GOTO      DSPP6000
.
.         end of data
.
DSPP5000  BRANCH    CPAGENO,DSPP5500
.
.         in middle pages : First option
.
DSPP5100  MOVE      "3",CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,DSPP9999:
                         DSPP5900:
                         DSPP8000:
                         EXT29999
          GOTO      DSPP6000
.
.         on first page : No options
.
DSPP5500  MOVE      "4",CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,DSPP9999:
                         DSPP5900:
                         DSPP5900:
                         EXT29999
          GOTO      DSPP6000
.
DSPP5900  BEEP
DSPP5910  BRANCH    CALLPOSN,DSPP4100,DSPP4500,DSPP5100,DSPP5500
.
.         delete entered
.
DSPP6000  MOVE      FIVE,YPOS
          DISPLAY   *P1:YPOS,*EF
          MOVE      ZERO,COUNT
DSPP6500  IF        COUNT < TEN7
            ADD       ONE,COUNT
            CLEAR     TMPKEY[COUNT]
            GOTO      DSPP6500
          ENDIF
.
          GOTO      DSPP0000  
.
.         next screen selected
.
DSPP7000  MOVE      SIX,YPOS 
          ADD       ONE,CPAGENO
          DISPLAY   *P1:YPOS,*EF
          MOVE      ZERO,FIELD
          MOVE      ONE,COUNT
          GOTO      DSPP3000
.
.         previous screen selected
.
DSPP8000  MOVE      TMPKEY[1],KEY27
          READ      TEMPF2,KEY27;YYDATE1,YYURNO,YYCODE,YYMCNT
          MOVE      ZERO,FORM2
.         ADD       TEN6,COUNT
DSPP8500  IF        FORM2 <= TEN6   
            READKP   TEMPF2;YYDATE1,YYURNO,YYCODE,YYMCNT
.
            ADD       ONE,FORM2
            GOTO      DSPP8500
          ENDIF
.
          MOVE      FIVE,YPOS 
          SUB       ONE,CPAGENO
          MOVE      ZERO,FIELD
          MOVE      ZERO,COUNT
          DISPLAY   *P1:YPOS,*EF
          GOTO      DSPP1000
.
DSPP9999  RETURN
+
.******************************************************************************
.*                                KEYA0000                Called by: DSPP0000 *
.*                 Keyin response to question                                 *
.*                 Returns : EXIT = 0		number entered                *
.*                           EXIT = 1		Exit entered                  *
.*                           EXIT = 2		Next entered                  *
.*                           EXIT = 3           Prev entered                  *
.******************************************************************************
KEYA0000  DISPLAY   *P1:24,*EL,"e(",*V2LON,ANSX,*HOFF,")it":
                    ", (",*V2LON,ANSD,*HOFF,")elete":
                    ", (",*V2LON,ANSP,*HOFF,")rint";
                    
          MOVE      TWENTY5,CCOL
          IF        CALLPOSN = 1 | CALLPOSN = 2
            DISPLAY   ", (",*V2LON,ANSN,*HOFF,")ext";
            ADD       EIGHT,CCOL
          ENDIF
          IF        CALLPOSN = 1 | CALLPOSN = 3
            DISPLAY   ", p(",*V2LON,ANSR,*HOFF,")evious";
            ADD       TEN2,CCOL
          ENDIF
.
          DISPLAY   " : ";
          ADD       FOUR,CCOL
.
KEYA1000  KEYIN     *PCCOL:24,*DV,UNDLN2:
                    *PCCOL:24,*V2LON,*JR,DIM2:
                    *PCCOL:24,*DV,DIM2
          PACK      DIM2,DIM2,SP2
          UNPACK    DIM2,ANS,ANS
          REP       UPPLOW,ANS
.
          MATCH     ANSX,ANS
          GOTO      KEYA9100 IF EQUAL
.
          MATCH     ANSD,ANS
          GOTO      KEYA9400 IF EQUAL
.
          MATCH     ANSN,ANS
          GOTO      KEYA9200 IF EQUAL
.
          MATCH     ANSR,ANS
          GOTO      KEYA9300 IF EQUAL
.
          MATCH     ANSP,ANS
          GOTO      KEYA9500 IF EQUAL
.
          TYPE      DIM2
          GOTO      KEYA5000 IF EQUAL
.
KEYA1500  BEEP
          GOTO      KEYA1000   
.
.         number entered directly
.
KEYA5000  MOVE      DIM2,FORM2
.
          COMPARE   FORM2,ZERO
          GOTO      KEYA1500 IF NOT LESS
.
          COMPARE   FORM2,FIELD
          GOTO      KEYA1500 IF LESS
.
.         set the exit flag
.
          MOVE      ZERO,EXIT
          GOTO      KEYA9999
.
KEYA9100  MOVE      ONE,EXIT
          GOTO      KEYA9999
.
KEYA9200  MOVE      TWO,EXIT
          GOTO      KEYA9999
.
KEYA9300  MOVE      THREE,EXIT
          GOTO      KEYA9999
.
KEYA9400  MOVE      ZERO,EXIT
          CALL      DELR0000
          GOTO      KEYA9999
.
KEYA9500  MOVE      FOUR,EXIT
          GOTO      KEYA9999
.
KEYA9999  RETURN
+
.*************************************************************************
.*                             DELR0000            Called by:            *
.*                      delete a selected record from temp               *
.*************************************************************************
DELR0000  KEYIN     *P1:24,*EL,"Enter a Line to Delete : ",*V2LON,CCITEM
          IF        COUNT < CCITEM| CCITEM = 0
            GOTO      DELR0000
          ENDIF
.
          MOVE      CCITEM,FORM2
          MOVE      TMPKEY[FORM2],KEY27
          CALL      RDTREA3
          MOVE      CCITEM,YPOS
          ADD       FIVE,YPOS
          DISPLAY   *P1:YPOS,*V2LON,FORM2,*HOFF,*ULON,*HON,*P5:YPOS,WMURNO:
                    *P14:YPOS,WMCODE,*P24:YPOS,TDESC,*P45:YPOS,DATE10:
                    *P56:YPOS,DATE20,*P67:YPOS,DESCSTAT,*HOFF;
.
DELR1000  KEYIN     *P1:24," Are you sure you want to delete this":
                    " Item (Y/N) ? ",*V2LON,DIM1
          REP       UPPLOW,DIM1
          REP       "Y1N2",DIM1
          MOVE      DIM1,FORM1
.
          BRANCH    FORM1,DELR5000,DELR9999
          GOTO      DELR1000          
.
DELR5000  MOVE      CCITEM,FORM2
          MOVE      TMPKEY[FORM2],KEY27
          DELETE    TEMPF2,KEY27
.
          SUB       ONE,COUNTER
.
DELR9999  RETURN 
+
.*************************************************************************
.*                             CRET0000            Called by:            *
.*                      create temporary file                            *
.*************************************************************************
CRET0000  CALL      KILL0000
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,FNAMEX,TMKEY
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPF2,FNAMEX
.
CRET9999  RETURN
+
.*************************************************************************
.*                             KILL0000             Called by:           *
.*                   erase temporary file                                *
.*************************************************************************
KILL0000  CLOSE     TEMPF2
          CLEAR     CMDLINE  
          PACK      CMDLINE,ISERASE,FNAMEX
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID 
KILL9999  RETURN
+
