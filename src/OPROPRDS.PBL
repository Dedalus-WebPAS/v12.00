.******************************************************************************
.                                 OPROPRDS
.                       Operating Clinics code D.S.
.
. Description : Display Operating Room Codes in description order from the
.               opropraf file using the 2nd index.
.
. Requirements: This routine requires OPROPRFD and OPROPRIO.INC and KEY46
.             OPROPRDS was programed by Bill Dew on the 28/12/1989
.                        I.B.A. Development Department
.                             D. Jones supervisor
.
.   Modifications : 25/02/1999  Jill Habasque
.                   Changed display so that inactive codes would not be shown
.
.******************************************************************************
OPROPRDS 
.
          DISPLAY    *P1:4,*EF,*P33:4,*V2LON,*ULON,"OPERATING ROOMS"
          MOVE       FIVE,CVERT
          MOVE       ONE,CCOL
.
.>>>>>>>  TRAP       OPRDS900 IF F1
.
          PACK       KEY46,SP30,SP20
          CALL       RSOPOPR2
OPRDS222  
          CALL       RKOPOPR2
          BRANCH     OVRCD,OPRDS990
.
          RESET      OPRMDESC,30            * set to 30 characters
          LENSET     OPRMDESC               * set LL to display 30 chars
          RESET      OPRMDESC
          COMPARE    ONE,OPRMSTAT           * inactive code, don't display
          GOTO       OPRDS222 IF EQUAL
          DISPLAY    *PCCOL:CVERT,*V2LON,OPRMROOM,SP2,*HOFF,*+,OPRMDESC
          ADD        ONE,CVERT
          COMPARE    TWENTY3,CVERT
          GOTO       OPRDS222 IF LESS
          MOVE       FIVE,CVERT
          ADD        "40",CCOL
          COMPARE    "70",CCOL
          GOTO       OPRDS222 IF LESS
.
.>>>>>>>  TRAPCLR   F1
.
.         Check for old or new standards question
.
          BRANCH    CNEWDS,OPRDS200
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
          CMATCH    ANSF,ANS
          GOTO      OPROPRDS IF EQUAL
          CMATCH    ANSE,ANS
          GOTO      OPRDS999 IF EQUAL
          GOTO      OPRDS400
.
.         New standards question
.
OPRDS200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, ":
                    "(",*V2LON,"F",*HOFF,")irst Screen, ":
                    "(",*V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
          CMATCH    ANSC,ANS
          GOTO      OPRDS999 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      OPROPRDS IF EQUAL
.
.         Clear the screen
.
OPRDS400  MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:CVERT,*EF
.
.>>>>>>>  TRAP      OPRDS900 IF F1
          GOTO      OPRDS222
.
OPRDS900  NORETURN
.>>>>>>>  TRAPCLR   F1
OPRDS990  
          BRANCH    CNEWDS,OPRDS999
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
OPRDS999  RETURN
