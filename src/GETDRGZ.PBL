.******************************************************************************
.* Include Name   : GETDRGZ.PBL    (call GETDRGZ or GTDVER00)                 *
.* Description    : This routine differs from GETDRG.PBL in that it will      *
.*                  always return a DRG Version in DRGVERZ (Dim 3) and does   *
.*                  not read PATMI1af or PATDSCaf.                            *
.*                  The routine is used for validating DRG Codes against      *
.*                  PATDGWaf and getting DRG data where weightings and the    *
.*                  wording of descriptions differ between Versions           *
.*                                                                            *
.* Author         : Mike Laming  04/05/2007                                   *
.* Parameters Req : ACLAIM (may be blank)                                     *
.*                  AFUND  (may be blank)                                     *
.*                  ASTAT  (may be blank)                                     *
.*                  PTDSVOGU  (may be blank)                                  *
.* Returns        : DRGVERZ  - DRG version                                    *
.*                                                                            *
.* Modifications  :                                                           *
.*       V11.05.01  26/03/2025  Davin         TSK 0956210                     *
.*                  Added PTCNE120 for DRG Version 12.0                       *
.*       V11.02.01  20/04/2022  Davin         TSK 0917793                     *
.*                  Added PTCNE110 for DRG Version 11.0                       *
.*       V10.14.01  27/02/2019  Richa Phogat  TSK 0870439                     *
.*                  Added PTCNE100 for DRG Version 10.0                       *
.*       V10.12.01  23/03/2018  Tracey Nguyen TSK 0852793                     *
.*                  Added PTCNED90 for DRG Version 9.0                        *
.*       V10.08.01  26/08/2016  Don Nguyen    TSK 0312570                     *
.*                  Added call to GTEDRG00 when PTCNDGED=1                    *
.*                  Added missing GOTO GTDVER99 at GTDVER10 & GTDVER20        *
.*       V10.06.01  29/05/2015  Ebon Clements CAR 317094                      *
.*                  Added DRG Version 8.0                                     *
.*       V10.05.01  17/09/2014  Davin         CAR 304204                      *
.*                  Mods to get drg based on discharge date (gdrgdz00)        *
.*                                                                            *
.******************************************************************************
GETDRGZ
GTDVER00  MOVE      ZERO,EXIT
          READ      CONTROLF,HUND05;*158,PTCNDGPV
          READ      CONTROLF,HUND19;*183,PTCNED41,*191,PTCNED42,*199,PTCNED50:
                                    *207,PTCNED51,*215,PTCNED52,*223,PTCNED60:
                                    *231,PTCNED62,*239,PTCNED70
          READ      CONTROLF,HUND20;*232,PTCNED80
          READ      CONTROLF,HUND23;*232,PTCNED90
          READ      CONTROLF,HUND24;*121,PTCNDGED,*224,PTCNE100
          READ      CONTROLF,HUND28;*239,PTCNE110
          READ      CONTROLF,HUND30;*94,PTCNE120
.
          MATCH     "1",PTCNDGED
          IF        @EQUAL
            OPEN      PMSEDGA1,"pmsedgaf"     * Effective DRG
            OPEN      PMSEDGA2,"pmsedgaf"     * Effective DRG
          ENDIF
.
          MOVE      PTCNDGPV,DRGVERZ        * use Hospital default Grouper Ver.
.
          MATCH     "1",PTCNDGED
          GOTO      GTDVER30 IF EQUAL
.
          COMPARE   THREE,ASTAT
          GOTO      GTDVER10 IF NOT EQUAL
.
          MATCH     SP3,PTDSVOGU            * use Discharge DRG Version
          GOTO      GTDVER10 IF EQUAL
          MOVE      PTDSVOGU,DRGVERZ
.
          MATCH     SP8,DDATE
          GOTO      GTDVER10 IF EQUAL       * Patient not discharged
.
          CALL      GDRGDZ00                * get vers based on discharge date
.
GTDVER10  MATCH     SP6,AFUNDH
          GOTO      GTDVER20 IF EQUAL
          MOVE      "0000",DIM4
          PACK      KEY14,AFUNDH,DIM4,SP10
          CALL      RDFUND1
          BRANCH    OVRCD,GTDVER20
          MATCH     SP3,PTHFGRPV
          GOTO      GTDVER20 IF EQUAL
          MOVE      PTHFGRPV,DRGVERZ        * use Health Fund DRG Ver.if exists
          GOTO      GTDVER99
.
GTDVER20  MATCH     SP3,ACLAIM
          GOTO      GTDVER99 IF EQUAL
          PACK      KEY5,CODECL,ACLAIM
          CALL      RDCODE1
          BRANCH    OVRCD,GTDVER99
          MATCH     SP3,PTCDGRPV
          GOTO      GTDVER99 IF EQUAL
          MOVE      PTCDGRPV,DRGVERZ        * use Claim Type DRG Ver. if exists
          GOTO      GTDVER99
.
GTDVER30  CALL      GTEDRG00                * Get Effective DRG Ver
          BRANCH    EXIT,GTDVER99
.
          MOVE      KEY3,DRGVERZ
          GOTO      GTDVER99
.
GTDVER99  RETURN
+
.******************************************************************************
.*                                  GDRGDZ00                                  *
.*                      Get drg based on discharge date                       *
.******************************************************************************
GDRGDZ00  MATCH     PTCNE120,DDATE          * disc.date > DRG 12.0 effect.date?
          IF        !@LESS
            MOVE      "120",DRGVERZ         * Use 12.0 DRG version
            GOTO      GDRGDZ99
          ENDIF
.
          MATCH     PTCNE110,DDATE          * disc.date > DRG 11.0 effect.date?
          IF        !@LESS
            MOVE      "110",DRGVERZ         * Use 11.0 DRG version
            GOTO      GDRGDZ99
          ENDIF
.
          MATCH     PTCNE100,DDATE          * disc.date > DRG 10.0 effect.date?
          IF        !@LESS
            MOVE      "100",DRGVERZ         * Use 10.0 DRG version
            GOTO      GDRGDZ99
          ENDIF
.
          MATCH     PTCNED90,DDATE          * disc.date > DRG 9.0 effect.date?
          IF        !@LESS
            MOVE      "090",DRGVERZ         * Use 9.0 DRG version
            GOTO      GDRGDZ99
          ENDIF
.
          MATCH     PTCNED80,DDATE          * disc.date > DRG 8.0 effect.date?
          IF        !@LESS
            MOVE      "080",DRGVERZ         * Use 8.0 DRG version
            GOTO      GDRGDZ99
          ENDIF
.
          MATCH     PTCNED70,DDATE          * disc.date > DRG 7.0 effect.date?
          IF        !@LESS
            MOVE      "070",DRGVERZ         * Use 7.0 DRG version
            GOTO      GDRGDZ99
          ENDIF
.
          MATCH     PTCNED62,DDATE          * disc.date > DRG 6.2 effect.date?
          IF        !@LESS
            MOVE      "062",DRGVERZ         * Using 6.2 DRG version
            GOTO      GDRGDZ99
          ENDIF
.
          MATCH     PTCNED60,DDATE          * disc.date > DRG 6.0 effect.date?
          IF        !@LESS
            MOVE      "060",DRGVERZ         * Using 6.0 DRG version
            GOTO      GDRGDZ99
          ENDIF
.
          MATCH     PTCNED52,DDATE          * disc.date > DRG 5.2 effect.date?
          IF        !@LESS
            MOVE      "052",DRGVERZ         * Using 5.2 DRG version
            GOTO      GDRGDZ99
          ENDIF
.
          MATCH     PTCNED51,DDATE          * disc.date > DRG 5.1 effect.date?
          IF        !@LESS
            MOVE      "051",DRGVERZ         * Using 5.1 DRG version
            GOTO      GDRGDZ99
          ENDIF
.
          MATCH     PTCNED50,DDATE          * disc.date > DRG 5.0 effect.date?
          IF        !@LESS
            MOVE      "050",DRGVERZ         * Using 5.0 DRG version
            GOTO      GDRGDZ99
          ENDIF
.
          MATCH     PTCNED42,DDATE          * disc.date > DRG 4.2 effect.date?
          IF        !@LESS
            MOVE      "042",DRGVERZ         * Using 4.2 DRG version
            GOTO      GDRGDZ99
          ENDIF
.
          MATCH     PTCNED41,DDATE          * disc.date > DRG 4.1 effect.date?
          IF        !@LESS
            MOVE      "041",DRGVERZ         * Using 4.1 DRG version
            GOTO      GDRGDZ99
          ENDIF
.
GDRGDZ99  RETURN
+
