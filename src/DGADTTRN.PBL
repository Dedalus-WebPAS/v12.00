.**************************************************************************
.*  DGADTTRN  :  Transfer a patient  from a DataGate request              *
.*               ~~~~~~~~~~~~~~~~~~                                       *
.*                                                                        *
.*  Author    :  ROD   (19/10/95)                                         *
.*                                                                        *
.*  Mods      :                                                           *
.*        V12.00.01 14/05/2025  Davin          TSK 0955096                *
.*                  Changed for Alphanumeric Visit Numbers                *
.*        V10.01.02 02/02/2011  Steve Armstrong   CAR 237520              *
.*                  Mods to remove Detente interface (PATDETNT)           *
.*        V10.01.01 01/12/2010  Peter Vela        CAR 233148              *
.*                  Initialised pattranf variables to spaces before write *
.**************************************************************************
.*        V9.02.01  20.11.2001  B.G.Ackland                               *
.*                  Remove pi's from Program
.*        V5.08.02  21/08/2000  Jill Habasque                             *
.*                  Added variables for patdetnt                          *
.*            V5.08.01 06/04/2000  Steve Armstrong                        *
.*                     Removed code for PATHCSWT                          *
.**************************************************************************
.*            V5.07.00 22/10/1998  Davin                                  *
.*                     Mods for 507                                       *
.*            V5.04.02 07/07/1997  Steve Armstrong                        *
.*                     Fixed program to read in century & date of transfer*
.*                     instead of just date.                              *
.*            V5.04.01 17/04/1997  Steve Armstrong                        *
.*                     Mods to use inbound & outbound comms client        *
.*                     (DGATEIN & DGATEOUT).                              *
.*                                                                        *
.**************************************************************************
          DEFPROC   DGADTTRN
 IFGE     $$VER,7
.
          INC       IBACODFD/INC      
          INC       OUTCLIFD/INC
          INC       PATALWFD/INC           * Patient Allowance file
          INC       PATAXEFD/INC
.         INC       PATHCSWT/INC           * HCS Ward Transfer file
          INC       PATHSPFD/INC
          INC       PATMRGFD/INC
          INC       PATNOBFD/INC
          INC       PATNUMFD/INC           * Ward Usage file
          INC       PATONLFD/INC
          INC       PATRCAUD/INC           * Change Rate Audit file
          INC       PATSTAFD/INC           * Admission/Discharge Statistic file 
.                 
ADDESC    DIM       17
ADIAG     DIM       50
ADLABEL   FORM      2
ADMFLG    FORM      1           * Admission flag
ARCHIVE   INIT      "Hospital Archives"
BBDESC    DIM       20     
BBED      DIM       3
BEDESC    DIM       14          * Bed description
BFAMT     FORM      4.2
BFCOL     FORM      1
BFEND1    FORM      3
BFEND2    FORM      3
BFEND3    FORM      3
BFEND4    FORM      3
BFEND5    FORM      3
BFEND6    FORM      3
BJDAY     FORM      3
BOOKDATE  DIM       10
BRMRHIDATE  DIM       10
BWARD     DIM       3
BWDESC    DIM       20
CALDAYS   FORM      3
CALLPOSN  FORM      1
CDEFPRT1  DIM       3
CHG       FORM      1
CHG1      FORM      1
CHG2      FORM      1
CHGDAT    FORM      1
CHGDOC    FORM      1
CHGTYP    FORM      1
CHGWRD    FORM      1
CJDAY     FORM      3
CMDLINE   DIM       35
CODE      DIM       2
CODEA     INIT      "A "
CODEBT    INIT      "BT"
CODECC    INIT      "CC"
CODECL    INIT      "CL"
CODELT    INIT      "LT"
CODERS    INIT      "RS"
CODET     INIT      "T "
CODETY    INIT      "TY"
CODEU1    INIT      "U1"
CODEU2    INIT      "U2"
CODEUNK   INIT      "UNK"
COMMA     INIT      ","
COND      FORM      1
COUNT     FORM      2
COUNTER   FORM      2
CPPAPER1  FORM      2
CSEC      DIM       2
CUDATE    DIM       6
CURRDATE  DIM       10
CURRLOCN  DIM       16
CURRLOCT  DIM       30
CURRTIME  DIM       5
DANS1     DIM       20
DANS2     DIM       20
DANS3     DIM       20
DANS4     DIM       20
DANS5     DIM       4
DANS6     DIM       11
DATE8     DIM       8
DATIMEND  DIM       13
DATIMSTR  DIM       13
DEFLAG    FORM      1
DESCLOCT  DIM       30
DESCOPER  DIM       45
DESCREAS  DIM       30
DESCRQBY  DIM       45
DESTDATE  DIM       8
DESTFORM  FORM      8
DESTIN    FORM      1           * 1 = Normal Bed, 2 = Standby, 3 = Ward Only Bed
DESTTIME  DIM       8
DETCADES  DIM       20
DETCODES  DIM       20
DETNTREC  FORM      2
DIM10     DIM       10
DIM14     DIM       14
DIM15     DIM       15
DIM17     DIM       17
DIM19A    DIM       19
DIM20     DIM       20
DIM20A    DIM       20
DIM23     DIM       23
DIM25     DIM       25
DIM26     DIM       26
DIM3      DIM       3
DIM30     DIM       30
DIM31     DIM       31
DIM4      DIM       4
DIM47     DIM       47
DIM5      DIM       5           * HR:MM 
DIM55     DIM       55
DIM5A     DIM       5
DIM6      DIM       6
DIM7      DIM       7
DIM8      DIM       8
DIMDD     DIM       2
DIMMM     DIM       2
DIMYY     DIM       2
DISPARRY  DIM       4[36]
DISPDATE  DIM       10
DNAME     DIM       20
DOCCODE   DIM       6
DOCDISP   DIM       8
DOCNAME   DIM       12
DSEX      DIM       6
DSMOK3    DIM       3
DSTFARRY  DIM       6[36]
DUEDATE   DIM       10
DUMMY     DIM       1
DVA      INIT      "DVA"
ENDDATE   DIM       8
ENDFLG    FORM      1
EOF       FORM      "-3"
FIELD     FORM      2
FLAG      FORM      1
FLAGPRNT  FORM      1
FORM10    FORM      10
FORM3     FORM      3
FORM6     FORM      6
FORM7     FORM      7
FORM8     FORM      8
FORMCENT  FORM      2
FORMDAY   FORM      4
FORMMON   FORM      2
FORMYEAR  FORM      2
FROMDATE  DIM       8
FTUREDAT  DIM       8
FUTRFORM  FORM      8
HOMEDESC  DIM       30
HOMEONLY  FORM      "1"
IBCNMHOS  FORM      1
INDEX     FORM      2
INWARD    DIM       3
JYEAR     FORM      2
KEYCOL    DIM       1
LABBDTE   DIM       8
LABCOS    FORM      3
LABNAME   DIM       40
LABSEX    DIM       6
LALLW     FORM      6.2
LATYPE    DIM       3
LBED      DIM       3
LCHSTAT   DIM       3
LDATE     DIM       6           * Last Transfer Date
LDISC     FORM      6.2
LEXTRA    FORM      6.2
LINE      DIM       55
LMOVE     DIM       1           * Last Transfer Move
LOCADESC  DIM       30
LOCATION  DIM       4
LOCATNS   FORM      1
LONGTIME  DIM       8
LOOPTRN   FORM      1           * 0 = Enquiry, 1 = Print
LTAMTF    FORM      6.2
LTAMTH    FORM      6.2
LTIME     DIM       8           * Last Transfer time
LWARD     DIM       3
MAXNUM    FORM      1
MISSING  INIT      "Missing"
MODE      DIM       1
MODE1     DIM       1
MOVEDAT   DIM       8
MOVEINDC  DIM       1
MOVETIM   DIM       8
MTINDC    FORM      1           * MTINDC=1  ... empty beds ONLY
NALLOW    DIM       2
NAMEDIM   DIM       44
NAMELINE  DIM       45
NBED      DIM       3
NBRDAYS   FORM      4
NDATE     DIM       8
NDISC     FORM      3.2
NDOCTA    DIM       6
NEGONE    FORM      "-1"
NETTOT    FORM      6.2
NEWBED    DIM       3
NEWWARD   DIM       3
NLWAMT    FORM      6.2
NMPNUMB   DIM       20
NOFEE     FORM      "0"
NOLABEL   FORM      3
NTAMTF    FORM      6.2
NTAMTG    FORM      6.2
NTAMTH    FORM      6.2
NUMBER    FORM      3
NWARD     DIM       3
NZGNAME   DIM       20
NZHOSP    DIM       1
NZSNAME   DIM       18
OLDBED    DIM       3
OLDWARD   DIM       3
OPERATOR  DIM       6
PATNAME   DIM       53
PIPE      INIT      "|"
PNAME     DIM       20
PROGFLAG  FORM      "1"         * 0 = IBAPAT35, 1 = IBAPAT31,IBAPAT36
PUTONSBY  DIM       1
REASDESC  DIM       30
REASON    DIM       4
RECDCNT   FORM      10
RECSTAT   DIM       20
RECTYPEA  INIT      "A"
REQDATE   DIM       8
REQTIME   DIM       5
REQUESTR  DIM       6
RESSFLAG  DIM       1
ROOF     INIT      "Roof"
SAVADMNO  DIM       8           * save Admission key before SCAN
SAVALLW   FORM      6.2
SAVCOL    FORM      1
SAVDISC   FORM      6.2
SAVDOCT   DIM       6
SAVEND    FORM      3
SAVENUMB  FORM      2
SAVEXTR   FORM      6.2
SAVHF     FORM      4.2
SAVHOME   DIM       4
SAVKEY26  DIM       26
SAVPAT    FORM      4.2
SAVPURNO  DIM       8
SAVTOT    FORM      4.2
SAVTYPE   DIM       3
SAVURNO   DIM       8
SAVWRD    DIM       3
SEC1      FORM      "00001" 
SECINT    FORM      1
SKEY6     DIM       6
SORL      DIM       1
SOURCE    FORM      1           * 1 = Normal Bed, 2 = Standby, 3 = Ward Only Bed
SP12      INIT      "            "
SP14      INIT      "              "
SP15      INIT      "               "
SPFLAG    FORM      1
SPLFILE1  DIM       8
STAFF     DIM       6
STAFFREC INIT      "Staff"
STATDESC  DIM       30
STBYPAT   DIM       8
STRTDATE  DIM       8
SVDOCTA   DIM       6
SVMRLOCN  DIM       4
TEMP55    DIM       55
TEMPKEY   DIM       17
TEMPTYPE  FORM      1
TIMEIS    DIM       8
TMPDTCAT  DIM       2
TMPDTCOD  DIM       3
TMPDTACT  DIM       1                    * detente temporary action
TMPDVIST  DIM       1                    * detente temporary visit type
TMPDVISN  DIM       8                    * detente temporary visit number
TMPDDATE  DIM       8                    * detente temporary visit date
TMPKEY    DIM       8[16]
TODATE    DIM       8
TODAY     DIM       8
TOTAL     FORM      4.2
TOTDISC   FORM      6.2
TRANDEPT  DIM       3           * entered department code
TTIME2    DIM       8           * Out Time
TYPEDESC  DIM       30
VALOPTN   FORM      2
VERTPOS   FORM      2
VISITTYP  FORM      1
WARDESC   DIM       18          * Ward description
WDATE     DIM       8
WDATE1    DIM       8
WDATE2    DIM       8
WGLINE    DIM       35
WRDESC    DIM       30
WREXTN    DIM       4
WSOPT     FORM      1
WSWARD    DIM       3
Z15       INIT      "zzzzzzzzzzzzzzz"
Z16       INIT      "zzzzzzzzzzzzzzzz"
Z2        INIT      "zz"
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
          MOVE      "TRN",RPLYHEAD
.
          OPEN      PATNOBE1,"patnobef"
          OPEN      PATVISA1,"patvisaf"    * this shoold alredy by open DGSRVADT
.
.
. read the rest of the incoming message
.
DGTRN000  READ      DGATEOUT;AADMNO,NDATE,TTIME2,NWARD,NBED,NDOCTA:
                    TRANDEPT,PUTONSBY,NEWBFTYP,NEWBFAMT,NEWBFDAM,NEWBFAAM
.
          MOVE      NDATE,DATE8
.
. --->
. ---> NB: bed fee stuff still needs to be done <---
. --->
.
          CALL      VALID000                      * read pat. details & validate
          BRANCH    EXIT,DGTRN900                 * Error!  Patient not valid
.
. we have a valid patient so do the transfer
.
          CALL      CLOBD000                      * clear bed patient was in
          BRANCH    EXIT,DGTRN900                 * Error. War/Bed inconsistency
.
          CALL      WRTRN000                      * write transfer file record
          CALL      WRDUS000                      * write/upd ward usage file
          CALL      RCHGA000                      * update rate change audit fil
          CALL      INBED000                      * put patient in their new bed
          CALL      UPVIS000                      * update visit file
          CALL      UPMIS000                      * update admission file
          CALL      UPCON000                      * update controlf file if ness
          CALL      UNLCK000                      * unlock admission & master
          CALL      CKSBY000                      * put stand-by patient in bed
.
. send reply message as successful
.     NB.  SP1 at end of reply has been added because the interpreter does
.     not place a "|" after the last field, and Datagate requires this.  By
.     putting an extra field on the end (which is blank), a "|" will then
.     be added after the last field.
.
          MOVE      "00000",RPLYCODE              * successful message
          MOVE      SP50,RPLYDESC                 * clear error description
          PACK      CCENCODE,CCDATE,CCDST,CCSRC,CCNAME,REPLYRPL
          WRITE     DGATEIN;CCENCODE:
                            RPLYHEAD,RPLYCODE,RPLYDESC:
                            DGMISC1,DGMISC2,DGMISC3,SP1
.
          ADD       ONE,SUCCPROC                  * increm # success transactns
          GOTO      DGTRN999
.
. --- send back an Error response ---
.
DGTRN900  PACK      CCENCODE,CCDATE,CCDST,CCSRC,CCNAME,REPLYRPL
          WRITE     DGATEIN;CCENCODE:
                            RPLYHEAD,RPLYCODE,RPLYDESC:
                            DGMISC1,DGMISC2,DGMISC3,SP1
          ADD       ONE,ERRRPROC                  * increm # error transactions
.
DGTRN999  GOTO      ENDPROC1
+
.***************************************************************************
.*  VALID000  :  read patients details & make sure we have a valid patient *
.*               Returns:  EXIT=0 --> Valid patient,   EXIT=1 --> ERROR    *
.***************************************************************************
VALID000  MOVE      AADMNO,DADMNO
          MOVE      AADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,VALID800                * no admission details
.
.    must be a current admission
.
          BRANCH    ASTAT,VALID830,VALID100,VALID830,VALID100,VALID830
.
VALID100  MOVE      AADMNO,KEY8
          CALL      RDVISA1                       * no visit details
          BRANCH    OVRCD,VALID820
.
          MOVE      AURNO,DURNO
          MOVE      AURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,VALID850                * no master details
          CALL      RDPMPX21
.
          PACK      KEY30,AADMNO,Z30
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,VALID810
.
          MATCH     AADMNO,TADMN
          GOTO      VALID810 IF NOT EQUAL
.
. make sure the date/time of the new transfer record will be after the last one
.
          MATCH     TDATE,DATE8
          GOTO      VALID880 IF LESS         * new date before last date
          IF        @EQUAL
            MATCH     TTIME,TTIME2           * no - error
            GOTO      VALID880 IF EQUAL      * new date/time = last date/time
            GOTO      VALID880 IF LESS       * new time < last time (dates =)
          ENDIF
.
. save heaps of vars
. 
          MOVE      TWARD,LWARD                   * save original ward
          MOVE      TBED,LBED                     * save original bed
          MOVE      TADOCT,STADOCT                * save original doctor
          MOVE      TDATE,LDATE
          MOVE      TTIME,LTIME
          MOVE      TMOVE,LMOVE
          MOVE      TATYPE,LATYPE
          MOVE      TRBTYP,NEWBFTYP
          MOVE      TCHSTAT,LCHSTAT
          MOVE      TDISC,SAVDISC
          MOVE      TALLW,SAVALLW
          MOVE      PTTREPNO,STEPNO
.
          MOVE      TALLW,LALLW
          MOVE      TDISC,LDISC
          MOVE      TEXTRA,LEXTRA
          MOVE      TRATEF,LTAMTF
          MOVE      TRATEH,LTAMTH
.
. set up the type of bed the pat WAS in (SOURCE 1=Normal,2=Standby,3=Ward only)
.
          MOVE      TWO,SOURCE                    * assume pat was on standby
          MATCH     SP3,AWARD
          GOTO      VALID200 IF EQUAL
.
          MOVE      THREE,SOURCE                  * assume Ward only bed
          MATCH     SP3,TBED
          GOTO      VALID200 IF EQUAL
.
          MOVE      ONE,SOURCE                    * Normal Bed
.
.
. now check the bed the patient is going into
.
VALID200  MOVE      THREE,DESTIN                  * assume going to Ward only
          MATCH     SP3,NBED
          GOTO      VALID700 IF EQUAL
.
          PACK      KEY6,NWARD,NBED
          CALL      RDWARD1
          BRANCH    OVRCD,VALID840
.
          MOVE      ONE,DESTIN                    * assume normal
          MATCH     ZEROVISN,WADMNO
          GOTO      VALID700 IF EQUAL
.
          MATCH     "N",PUTONSBY                  * put the patient on standby?
          GOTO      VALID860 IF EQUAL             * No
.
.         Check if there is already some one on standby for this bed
.
          MOVE      TWO,DESTIN                    * assume standby
          MATCH     ZEROVISN,WSTBY
          GOTO      VALID870 IF NOT EQUAL
.
VALID700  MOVE      ZERO,CHGWRD
. --->  all this crap should be done using read *SB (suppress blanks in read)
. set vars to what they were in previous transfer record if blank (except NBED)
          MATCH     SP3,NWARD
          IF        @EQUAL
            MOVE      LWARD,NWARD
          ENDIF
          MATCH     SP6,NDOCTA
          IF        @EQUAL
            MOVE      STADOCT,NDOCTA
          ENDIF
          MATCH     SP3,TRANDEPT
          IF        @EQUAL
            MOVE      LATYPE,TRANDEPT
          ENDIF
.
          CALL      ANYCH000                      * check if anything has change
          BRANCH    EXIT,VALID890                 * nothing has changed
.
          MATCH     LWARD,NWARD                   * has ward changed?
          IF        !@EQUAL
            MOVE      ONE,CHGWRD
          ENDIF
.
          MOVE      ZERO,EXIT                     * we have a valid patient
          GOTO      VALID999
.
VALID800  MOVE      "Missing Admission record",RPLYDESC
          MOVE      "02100",RPLYCODE
          GOTO      VALID990
.
VALID810  MOVE      "Missing Transfer record",RPLYDESC
          MOVE      "02102",RPLYCODE
          GOTO      VALID990
.
VALID820  MOVE      "Missing Visit record",RPLYDESC
          MOVE      "02101",RPLYCODE
          GOTO      VALID990
.
VALID830  MOVE      "Patient is not currently admitted",RPLYDESC
          MOVE      "02113",RPLYCODE
          GOTO      VALID990
.
VALID840  MOVE      "Invalid Ward/Bed",RPLYDESC
          MOVE      "02130",RPLYCODE
          GOTO      VALID990
.
VALID850  MOVE      "Patient Master details not found",RPLYDESC
          MOVE      "01100",RPLYCODE
          GOTO      VALID990
.
VALID860  MOVE      "Ward/Bed already occupied",RPLYDESC
          MOVE      "02131",RPLYCODE
          GOTO      VALID990
.
VALID870  MOVE    "There is a patient already on standby for this bed.",RPLYDESC
          MOVE      "02132",RPLYCODE
          GOTO      VALID990
.
VALID880  MOVE      "Transfer date/time must be after last transfer",RPLYDESC
          MOVE      "02180",RPLYCODE
          GOTO      VALID990
.
VALID890  MOVE      "Transfer details contains no changes",RPLYDESC
          MOVE      "02181",RPLYCODE
          GOTO      VALID990
.
VALID990  MOVE      ONE,EXIT
.
VALID999  RETURN
+
.************************************************************************
.*  ANYCH000  :  check for changes in transfer details                  *
.************************************************************************
ANYCH000  MOVE      ZERO,EXIT                    * assume something has changed
.
          MATCH     LWARD,NWARD
          GOTO      ANYCH999 IF NOT EQUAL  
          MATCH     LBED,NBED
          GOTO      ANYCH999 IF NOT EQUAL  
          MATCH     STADOCT,NDOCTA
          GOTO      ANYCH999 IF NOT EQUAL  
          MATCH     LATYPE,TRANDEPT
          GOTO      ANYCH999 IF NOT EQUAL  
.
          MOVE      ONE,EXIT                     * nothing has changed. Error!
.
ANYCH999  RETURN
.************************************************************************
.*  CLOBD000  :  Clear the patient from the bed they were in originally *
.************************************************************************
CLOBD000  MOVE      ZEROVISN,STBYPAT        * Assume no standby patient for bed
          BRANCH    SOURCE,CLOBD100,CLOBD300,CLOBD500
.
. Clear the patient from the bed they were in
.
CLOBD100  PACK      KEY6,LWARD,LBED
          CALL      RDWARD1
          BRANCH    OVRCD,CLOBD900
.
          MATCH     AADMNO,WADMNO
          GOTO      CLOBD900 IF NOT EQUAL
.
          MATCH     SP3,AWARD
          GOTO      CLOBD900 IF EQUAL
.
          MOVE      WSTBY,WADMNO
          MOVE      ZERO,WPLUR
          MOVE      ZEROVISN,WSTBY
          CALL      UPWARD1
          MOVE      WADMNO,STBYPAT           * Save Adm no of patient on standby
          MOVE      ZERO,EXIT
          GOTO      CLOBD999
.
. Take the patient from the standby bed they were in
.
CLOBD300  PACK      KEY6,LWARD,LBED
          CALL      RDWARD1
          BRANCH    OVRCD,CLOBD900
.
          MATCH     AADMNO,WSTBY
          GOTO      CLOBD900 IF NOT EQUAL
          MOVE      ZEROVISN,WSTBY
          MOVE      ZEROVISN,STBYPAT
          CALL      UPWARD1
          MOVE      ZERO,EXIT
          GOTO      CLOBD999
.
. Remove the patient from the Ward Only bed they are in
.
CLOBD500  MOVE      APLUR,NBPLUR
          PACK      KEY13,LWARD,AADMNO,NBPLUR
          CALL      RDNOBE1
          BRANCH    OVRCD,CLOBD900
.
          CALL      DENOBE1
          MOVE      ZEROVISN,STBYPAT
          MOVE      ZERO,EXIT
          GOTO      CLOBD999
.
CLOBD900  MOVE      "Ward/Bed Inconsistency",RPLYDESC
          MOVE      "01133",RPLYCODE
          MOVE      ONE,EXIT
.
CLOBD999  RETURN
+
.*************************************************************************
.*  WRTRN000  :  write transfer file record                              *
.*************************************************************************
WRTRN000  MOVE      NWARD,TWARD      * SAVE THE TWARD AND BED
          MOVE      NBED,TBED
          MOVE      DATE8,TDATE
          MOVE      NEWBFTYP,TRBTYP
.
          MOVE      AADMNO,TADMN
          MOVE      AURNO,TURNO
          MOVE      TTIME2,TTIME
          MOVE      "C",TMOVE
.
. set up new bed fees if new bed fee info was sent in the message
.
          MATCH     SP3,NEWBFTYP
          IF        !@EQUAL
            MOVE      NEWBFTYP,TRBTYP                * new bed type
          ENDIF
          IF        NEWBFAMT<>0
. ?????     MOVE                                     * new bed fee amount
          ENDIF
          IF        NEWBFDAM<>0
            MOVE      NEWBFDAM,TDISC                 * new discount
          ENDIF
          IF        NEWBFAAM<>0
            MOVE      NEWBFAAM,TALLW                 * new allowance
          ENDIF
.
. ?????   MOVE      NTAMTH,TRATEH
. ?????   MOVE      NTAMTG,TRATEG
. ?????   MOVE      NTAMTF,TRATEF
. ?????   MOVE      NDISC,TDISC
. ?????   MOVE      NLWAMT,TALLW
.
. ???     MOVE      SAVEXTR,TEXTRA
. ???     MOVE      SAVEND,TRBEND
          MOVE      TRANDEPT,TATYPE
          MOVE      NDOCTA,TADOCT
          MOVE      TRANDEPT,TDEPT   make sure dept has changed before set it up
          MOVE      SP3,PTTRLTYP            * clear type of leave value
          MOVE      STEPNO,PTTREPNO         * episode number
          MOVE      AUSR2,TCHSTAT
.
.         Private Hospital does not use Chargeable Status 
.
          IF        CFEETYP = 0
            MOVE      SP3,TCHSTAT
          ENDIF
.
.         Recalculate the rate to be charged, just in case the department or
.         doctor was changed
.
          IF        CFEETYP <> 0
            PACK      NEWTDATE,TDATE
            MOVE      TWARD,NEWTWARD
            MOVE      TATYPE,NEWTATYP
            MOVE      TRBTYP,NEWTBTYP
            MOVE      TCHSTAT,NEWTCHST
            MOVE      TDEPT,NEWTDEPT
.
            PROC      PATTRRAT                 * calculate new rate
            BRANCH    EXIT,WRTRN500            * couldn't find rate
.
            MOVE      NEWTPPOR,TRATEF
            MOVE      NEWTRPOR,TRATEH
            MOVE      ZERO,TRATEG
            MOVE      NEWTXTRA,TEXTRA
            MOVE      NEWTBEND,TRBEND
          ENDIF
.
WRTRN500  MOVE      ZERO,PTTREPSD
          MOVE      ZERO,PTTRLSPT
          MOVE      ZERO,PTTRLSRB
          MOVE      ZERO,PTTRAEND
          MOVE      ZERO,PTTRADPT
          MOVE      ZERO,PTTRADRB
          MOVE      SP70,PTTRNHAC
          MOVE      SP70,PTTROPER
          MOVE      SP70,PTTRAUAT
          MOVE      SP70,PTTRCUID
          MOVE      SP70,PTTRLTSC
          MOVE      ACLAIM,PTTRCLTY
.
          PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED
          PACK      PTTRCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTTRCDAT
          CLOCK     TIME,PTTRCTIM
          MOVE      WBSEUID,PTTRUCID
          UNPACK    SP70,TRCDATE,TRCTIME,PTTRUUID
.
          CALL      WRTRAN2
.
WRTRN999  RETURN
+
.**********************************************************************
.*  WRDUS000  :  update the ward usage file                           *
.**********************************************************************
WRDUS000  OPEN      PATNUMW1,"patnumwr"
.
          BRANCH    PTCNNUMM,WRDUS900     * Skip if doing the monthly update
.                                                of the PATNUM file
          PACK      CPTDATE,NDATE
          REP       " 0",CPTDATE
          CALL      GPERD                 * Get the financial period for date
          BRANCH    EXIT,WRDUS400         * Date not in period file
.
          PACK      PDATE,DRGYR,DRGNUM
          MOVE      LWARD,PWARD
.
.         Update number of patient transfer out
.
WRDUS100  PACK      KEY9,PWARD,PDATE
          CALL      RLNUMW1
          BRANCH    OVRCD,WRDUS200,WRDUS190
          ADD       ONE,PTROUT
          CALL      UPNUMW1
          CALL      UUNUMW1
          GOTO      WRDUS400
.
WRDUS190  DISPLAY   *W,"Waiting on Record Lock PATNUMFD"
          GOTO      WRDUS100
.
WRDUS200  MOVE      ZERO,PTRIN
          MOVE      ONE,PTROUT
          MOVE      ZERO,PDSCH
          MOVE      ZERO,PADMN
          MOVE      ZERO,PLEAV
          MOVE      ZERO,PRETN
          CALL      WRNUMW1
.
WRDUS400  MOVE      NWARD,PWARD
.
.         Update number of patient transfer in
.
          PACK      KEY9,PWARD,PDATE
          CALL      RDNUMW1
          BRANCH    OVRCD,WRDUS500
          ADD       ONE,PTRIN
          CALL      UPNUMW1
          GOTO      WRDUS900
.
WRDUS500  MOVE      ZERO,PTROUT
          MOVE      ONE,PTRIN
          MOVE      ZERO,PDSCH
          MOVE      ZERO,PADMN
          MOVE      ZERO,PLEAV
          MOVE      ZERO,PRETN
          CALL      WRNUMW1
.
WRDUS900  CLOSE     PATNUMW1
.
WRDUS999  RETURN
+
.********************************************************************
.*  RCHGA000  :  Update the Rate Change Audit file                  *
.********************************************************************
RCHGA000  COMPARE   ONE,CHG1
. ??????  GOTO      RCHGA999 IF NOT EQUAL
.
          COMPARE   ZERO,CFEETYP             * Check if using bed fee
          GOTO      RCHGA999 IF NOT EQUAL    * No.
.
. check if any bed fee info has changed
.
          ASSIGN    (NEWBFAMT+NEWBFDAM+NEWBFAAM),FORM7
          COMPARE   ZERO,FORM7               * do we have any new bed fee figure
          GOTO      RCHGA999 IF EQUAL
.
          MATCH     SP3,NEWBFTYP             * do we have a new bed type?
          GOTO      RCHGA999 IF EQUAL
.
. Only update the Rate Change Audit file if system par is switched on
.
          BRANCH    CAUDQ,RCHGA999           * added for V5.01.27 - SRF 105195
.
          OPEN      PATRCAUD,"patrcaud"
          MOVE      "PUR",PRXCODE   * System Lock Audits
          CALL      GETSLK00        * Get System Lock Audits
          CALL      RDSRCA
          IF        RSTATUS<>0      
            CALL      RELSLK00        * Release System Lock Audits
            GOTO      RCHGA999
          ENDIF
.
          CALL      WRIRCA
          CALL      RELSLK00        * Release System Lock Audits
.
          MOVE      AADMNO,RADMNO
          MOVE      PGNAME,ANS
          PACK      RNAME,ANS,DOT,PSNAME
          MOVE      ACLAIM,RCLAIM
          MOVE      AFUNDH,RFUND
          MOVE      AFNDTB,RHFTAB
          MOVE      NEWBFTYP,RBTYPE
.
          MOVE      ZERO,RNPAT              * New rate = rate - disc - allow
. ?????   MOVE      NTAMTF,RNPAT
. ????    SUB       NDISC,RNPAT
          SUB       NLWAMT,RNPAT
          MOVE      NTAMTH,RNFUND
.
          MOVE      DATE8,RDATE
          MOVE      TRANDEPT,RATYPE
          MOVE      SP3,RCLSS
          MOVE      SP3,RCHST
          MOVE      ZERO,ROPAT
          MOVE      PASSCODE,ROPER          * Operator code
.
.         DISPBF sets the computer generate rate and the day rate effective
.
          CALL      WRRCA
          CLOSE     PATRCAUD
.
RCHGA999  RETURN
+
.***********************************************************************
.*  INBED000  :  put patient in their new bed                          *
.***********************************************************************
INBED000  BRANCH    DESTIN,INBED100,INBED300,INBED500
.
.         Put the patient into the ward/bed file
.
INBED100  PACK      KEY6,NWARD,NBED
          CALL      RDWARD1
.
          MOVE      NWARD,AWARD
          MOVE      NBED,ABED
          MOVE      AADMNO,WADMNO
          MOVE      ZERO,WPLUR
          CALL      UPWARD1
          GOTO      INBED999
.
.         Put the patient on standby
.
INBED300  PACK      KEY6,NWARD,NBED
          CALL      RDWARD1
.
          MOVE      AADMNO,WSTBY
          CALL      UPWARD1
.
          MOVE      SP3,AWARD
          MOVE      SP3,ABED
          GOTO      INBED999
.
.         Put the patient into the Ward Only bed
.
INBED500  MOVE      NWARD,NBWARD
          MOVE      AADMNO,NBADMNO
          MOVE      ZERO,NBPLUR
          MOVE      SP3,NBROOM
          PACK      KEY13,NBWARD,NBADMNO,NBPLUR
          CALL      WRNOBE1
.
          MOVE      NWARD,AWARD
          MOVE      NBED,ABED
.
INBED999  RETURN
+
.***********************************************************************
.*  UPVIS000  :  update visit file                                     *
.***********************************************************************
UPVIS000  MOVE      AADMNO,KEY8
          CALL      RDVISA1
.
          MOVE      NDOCTA,PVIDOCT
          CALL      UPVISA1
.
UPVIS999  RETURN
+
.***********************************************************************
.*  UPMIS000  :  update admission file                                 *
.***********************************************************************
UPMIS000  
. ??????   MOVE      NDISC,ADISC
. ??????  MOVE      NALLOW,AALLOW
.
          MOVE      TRANDEPT,ATYPE
          MOVE      NDOCTA,ADOCTA
          MOVE      ASTAT,SASTAT
          CALL      UPLMISS1
.
UPMIS999  RETURN
.**************************************************************************
.*  UPCON000  : Update the control file with the transfer date if neccess *
.**************************************************************************
UPCON000  OPEN      CONTROLF,"controlf"           * CORUD000 closes this file
          READ      CONTROLF,EIGHTY8;*148,CFRDTE
          MATCH     CFRDTE,DATE8
          GOTO      UPCON100 IF NOT LESS
          MOVE      DATE8,CFRDTE
          WRITAB    CONTROLF,EIGHTY8;*148,CFRDTE
.
UPCON100  READ      CONTROLF,EIGHTY8;*154,CMMHDT
          MATCH     CMMHDT,DATE8
          GOTO      UPCON999 IF NOT LESS
          MOVE      DATE8,CMMHDT
          WRITAB    CONTROLF,EIGHTY8;*154,CMMHDT
.
UPCON999  RETURN
.***********************************************************************
.*  UNLCK000  :  unlock admission and master records                   *
.***********************************************************************
UNLCK000  MOVE      AADMNO,KEY8
          CALL      UUPTMIS1                * release Admission record
.
          MOVE      AURNO,KEY8
          CALL      UUPTMAS1                * release Master record
.
UNLCK999  RETURN
+
.**************************************************************************
.*  CKSBY000  :  check for standby patient to go into bed just freed up   *
.**************************************************************************
CKSBY000  MATCH     ZEROVISN,STBYPAT
          GOTO      CKSBY999 IF EQUAL
.
          MOVE      STBYPAT,AADMNO
          MOVE      AADMNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,CKSBY999
.
          MATCH     SP3,AWARD
          GOTO      CKSBY999 IF NOT EQUAL
.
          BRANCH    ASTAT,CKSBY999,CKSBY100
          GOTO      CKSBY999
.
CKSBY100  PACK      KEY6,LWARD,LBED
          CALL      RDWARD1
          BRANCH    OVRCD,CKSBY999
.
CHKPORT3  MOVE      AADMNO,KEY8
          CALL      RLPTMIS1
          COMPARE   ZERO,OVRCD
          GOTO      CKSBY999 IF NOT EQUAL
.
          MOVE      LWARD,AWARD
          MOVE      LBED,ABED
          CALL      UPMISS1
          CALL      UUPTMIS1
.
CKSBY999  RETURN
.
.
OVERCOND  MOVE      ONE,OVRCD
          RETURN
OVERLOCK  MOVE      TWO,OVRCD
          RETURN
.
.
.
          INC       OUTCLIIO/INC
          INC       PATAXEIO/INC
          INC       PATHSPIO/INC
          INC       PATALWIO/INC
.         INC       PATHWTIO/INC
          INC       PATNOBIO/INC
          INC       PATNUMIO/INC
          INC       PATONLIO/INC
          INC       PATRCAIO/INC
          INC       PATSTAIO/INC
          INC       PATNIDIO/INC
+
.
.
 XIF
ENDPROC1  ENDPROC
