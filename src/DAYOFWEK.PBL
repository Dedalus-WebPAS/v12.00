.------------------------------------------------------------
. DOFWKONV  : Convert a date to a day number : 1 Mon to 7 Sun
. Declare   : FSTDAY    FORM      1
.             FSTJULDY  FORM      3
.             WEEKDAY   FORM      1
. Variables : CDAY,CMON,CYEAR,CCENT
. Returns   : WEEKDAY the day number of the week (1-7) 
.------------------------------------------------------------
. V10.03.01 06/02/2012  Peter McMullen  CAR 259587
.           Change DOFYR000 algorithm to calculate for 1900 to 9999
. V10.02.01 03/02/2012  Ebon Clements   CAR 259451
.           Added years 2014 to 2028 to DOFYR000
.------------------------------------------------------------
DAYOFWEK  PACK      KEY4,CCENT,CYEAR
          REP       " 0",KEY4
          CALL      DOFYR000                    * get first day of given year
          BRANCH    EXIT,DOFWK999
.
.------ First day of year exists ------
.
          MOVE      CDAY,DD
          MOVE      CMON,MM
          MOVE      CYEAR,YY
          MOVE      CCENT,CC
          CALL      DMYCON                      * convert to a julian date
.
.------ Calculate the day of the week ------
.
          MOVE      JULDAY,FSTJULDY
          DIV       SEVEN,FSTJULDY
          MULT      SEVEN,FSTJULDY
.
          SUB       FSTJULDY,JULDAY
.
          MOVE      JULDAY,FSTJULDY
          ADD       FSTDAY,FSTJULDY
          SUB       ONE,FSTJULDY
          MOVE      FSTJULDY,WEEKDAY
.
.------ Change to a number from 1 to 7 ------
.
          COMPARE   ZERO,FSTJULDY
          GOTO      DOFWK900 IF EQUAL
.
          COMPARE   EIGHT,FSTJULDY
          GOTO      DOFWK999 IF LESS
.
          SUB       SEVEN,FSTJULDY
          MOVE      FSTJULDY,WEEKDAY
          GOTO      DOFWK999
.
DOFWK900  MOVE      SEVEN,WEEKDAY
.
DOFWK999  RETURN
