. *----------------------------------------------------------------------
. * Include Name  :   PATFHITM.INC
. *
. * Function      :   CGI variables for PATFHIFD - patfhaf.dat
. *
. * V11.01.00  15/10/2021 J.Tan          TSK 0880410
. *                       Created TM
. *----------------------------------------------------------------------
.
.------------------------------------------------------------------------
.   Clear Parameters 
.------------------------------------------------------------------------
.
CPTFHI00  MOVE      SP70,PTFHI001
          MOVE      SP70,PTFHI002
          MOVE      SP70,PTFHI003
          MOVE      SP70,PTFHI004
          MOVE      SP70,PTFHI005
          MOVE      SP70,PTFHI006
CPTFHI99  RETURN
.
.------------------------------------------------------------
.   Set Parameters 
.------------------------------------------------------------
SPTFHI00  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPTFHI01,SPTFHI02,SPTFHI03,SPTFHI04,SPTFHI05:
                       SPTFHI06
.
          MOVE      ONE,EXIT
          GOTO      SPTFHI99
.
SPTFHI01  MOVE      QRYDATA,PTFHI001
          GOTO      SPTFHI99
.
SPTFHI02  MOVE      QRYDATA,PTFHI002
          PACK      PTFHI002,PTFHI002,SP70
          GOTO      SPTFHI99
.
SPTFHI03  SQUEEZE   QRYDATA
          MOVE      ZERO,FORM2
          MOVE      QRYDATA,FORM2
          MOVE      FORM2,PTFHI003
          GOTO      SPTFHI99
.
SPTFHI04  PACK      QRYDATA,QRYDATA,SP70
          MATCH     SP70,QRYDATA
          GOTO      SPTFHI99 IF EQUAL
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      PTFHI004,CCENT,CYEAR,CMON,CDAY
          REP       " 0",PTFHI004
          GOTO      SPTFHI99
.
SPTFHI05  MOVE      QRYDATA,PTFHI005
          GOTO      SPTFHI99
.
SPTFHI06  MOVE      QRYDATA,PTFHI006
          GOTO      SPTFHI99
.
SPTFHI99  RETURN
.
.------------------------------------------------------------
. Display fields
.------------------------------------------------------------
DFHI0000  BRANCH    FLDITMNO,DFHI0100,DFHI0200,DFHI0300,DFHI0400,DFHI0500:
                             DFHI0600,DFHI0700
          GOTO      DFHI9999
.
DFHI0100  COMPARE   ONE,IBCNMHOS
          GOTO      DFHI9999 IF NOT EQUAL
.
          UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,DFHI0110,DFHI0120
.
          WRITE     HTMLFILE;PTFHHOSP;
          GOTO      DFHI9999
.
DFHI0110  MATCH     SP70,PTFHHOSP
          IF        @EQUAL
            WRITE     HTMLFILE;"All";
          ELSE
            PACK      KEY3,PTFHHOSP,SP70
            CALL      RDPTHSP1
            BRANCH    OVRCD,DFHI9999
            WRITE     HTMLFILE;PTHSNAME;           * hospital name
          ENDIF
          GOTO      DFHI9999
.
DFHI0120  MOVE      ONE,F1
          CALL      HSPSEL00           * Hospital Selection List
          GOTO      DFHI9999
.
DFHI0200  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,DFHI0210
.
          WRITE     HTMLFILE;PTFHHFND;
          GOTO      DFHI9999
.
DFHI0210  PACK      HFNAME,SP70
          PACK      KEY14,PTFHHFND,ZERO8,SP70
          CALL      RDFUND1

          WRITE     HTMLFILE;HFNAME;
          GOTO      DFHI9999
.
DFHI0300  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
.
          MOVE      ZERO,F2
          MOVE      PTFHSYST,F2
          MOVE      SP70,VISITDSC            * System indicator
          LOAD      VISITDSC,F2,VISTYEMR,VISTYOUT,VISTYINP
          IF        F2=4
            MOVE      VISTYPPR,VISITDSC
          ENDIF
.
          BRANCH    F1,DFHI0310,DFHI0320
.
          WRITE     HTMLFILE;PTFHSYST;
          GOTO      DFHI9999
.
DFHI0310  WRITE     HTMLFILE;VISITDSC;
          GOTO      DFHI9999
.
DFHI0320  CALL      SSYS0000                * selection of system indicator
          GOTO      DFHI9999
.
DFHI0400  MATCH     PTFHHDAT,SP70
          GOTO      DFHI9999 IF EQUAL
.
          UNPACK    PTFHHDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      DFHI9999
.
DFHI0500  MOVE      "rh",CATEGORY
          MOVE      PTFHHREA,CODE
          CALL      CODITM00
          GOTO      DFHI9999
.
DFHI0600  WRITE     HTMLFILE;PTFHDESC;
          GOTO      DFHI9999
.
DFHI0700  WRITE     HTMLFILE;USEFHINV;
          GOTO      DFHI9999
.
DFHI9999  RETURN
+
.------------------------------------------------------------
.  Selection of system indicator
.------------------------------------------------------------
SSYS0000  MOVE      THREE,F2
          WRITE     HTMLFILE;"<option value=#"",F2,"#">":
                             "Inpatients</option>";
          GOTO      SSYS9999
.
SSYS9999  RETURN
+
.------------------------------------------------------------
.  Update file
.------------------------------------------------------------
MVPFHI00  MATCH     PTFHI001,SP70
          IF        !@EQUAL
            MOVE      PTFHI001,PTFHHOSP
          ENDIF
.
          MATCH     PTFHI002,SP70
          IF        !@EQUAL
            MOVE      PTFHI002,PTFHHFND
          ENDIF
.
          MATCH     PTFHI003,SP70
          IF        !@EQUAL
            SQUEEZE   PTFHI003
            MOVE      PTFHI003,FORM2
            MOVE      FORM2,PTFHSYST
            PACK      PTFHI003,PTFHSYST,SP70
          ENDIF
.
          MOVE      PTFHI004,PTFHHDAT
          MOVE      PTFHI005,PTFHHREA
          MOVE      PTFHI006,PTFHDESC
.
MVPFHI99  RETURN
