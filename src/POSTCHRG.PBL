.*****************************************************************************
.* V11.05.01 29/11/2024  Thanh T        TSK 0939466                          *
.*           Added Distance in kms field                                     *
.*****************************************************************************
.*        V11.01.12 08/11/2021 J.Tan           TSK 0880410                   *
.*                  Added Hospital Id and System to Health Fund Invoice hold *
.*        V11.01.01 18/02/2021  J.Tan          TSK 0888639                   *
.*                  Added PMMITMNO - Theatre Team no                         *
.*        V10.15.01 30/10/2019  J.Tan            TSK 0857392                 *
.*                  Mod to set PMMISERV                                      *
.*        V10.14.01 25/09/2019  J.Tan            TSK 0857392                 *
.*                  Mod for ABF invoice                                      *
.*        V10.13.03 23/10/2018  Tracey Nguyen    TSK 0859743                 *
.*                  Added PMMIHOSI - Hopsital Indicator on PMSMTIFD          *
.*        V10.13.02 19/09/2018  J.Tan            TSK 0863727                 *
.*                  Added Restrictive Override code                          *
.*        V10.13.01 21/08/2017  J.Tan        TSK 0859742                     *
.*                  Added ECLIPSE new fields from pmsmtiaf                   *
.*        V10.11.01 31/08/2017  J.Tan            TSK 0837479                 *
.*                  Added PMMICTYP - Consumption Type                        *
.*****************************************************************************
.*                              POSTCHRG                                     *
.*            Post charge to the outpatient transaction file                 *
.*        Note : Variables already set up in XCHG0000                        *
.*****************************************************************************
.
POSTCHRG  BRANCH    ABFFLAG,PSTCHG30         * ABF Funding
.
          BRANCH    XCHGFLAG,PSTCHG99        * no charge applicable - exit
.
          COMPARE   ZERO,PMMIAMTT            * charge to be posted ?
          GOTO      PSTCHG99 IF EQUAL        * no - exit
.
. ------- Write a transaction record -------
.
          PACK      KEY8,OBAOUTNO
PSTCHG10  CALL      TRVISA1                  * get next transaction number
          MOVE      PVITRAN,PMMITRAN
.
          PACK      KEY14,OBAOUTNO,PMMITRAN
          CALL      RAPMMTI1
          COMPARE   ZERO,OVRCD               * record exists already ?
          GOTO      PSTCHG10 IF EQUAL        * yes - try next trx number
.
          MOVE      "0",PMMIMVBR             * mechanical ventilation billing
          MOVE      ONE,PMMISERV
          MOVE      SP70,PMMICNTR
          MOVE      SP70,PMMISUNQ
          MOVE      SP70,PMMIINCT
          MOVE      SP70,PMMISUBN
          MOVE      SP70,PMMIEDAD
          MOVE      SP70,PMMISVTM
          UNPACK    SP70,PMMITMNO,PMMIPMBS,PMMIRBRS,PMMICTYP
          PACK      PMMIDUPD,CCC,CYY,CMM,CDD
          REP       " 0",PMMIDUPD
          CALL      IBACLOCK
          MOVE      CTIMEIS,PMMITUPD
          MOVE      WBSEUID,PMMIWUSR
          PACK      PMMIACOI,SP70       * After Care Override Indicator
          PACK      PMMIDSOV,SP70       * Duplicate Service Override
          PACK      PMMISTXT,SP70       * Service Text
          PACK      PMMILSPN,SP70       * Location Specific Practice No(LSPN)
          PACK      PMMIMPOV,SP70       * Multi Procedure Override
          PACK      PMMINMPT,SP70       * Number of Patients Seen
          PACK      PMMISDCD,SP70       * Self Deem Code (Cat Sd)
          PACK      PMMITDUR,SP70       * Time Duration (Mins)
          PACK      PMMIROVR,SP70       * Restricted Override Code (Cat Ro)
          PACK      PMMIHOSI,SP70       * Hospital Indicator         *T0859743
          MOVE      SP70,PMMIDKSM       * Distance in kms
          PACK      PMMISPAR,SP70,SP70
          CALL      WRPMMTI1                 * write new pmsmti record
.
. ------- Write an invoice pending record -------
.
PSTCHG30  COMPARE   ZERO,PTCNIPEN
          GOTO      PSTCHG99 IF NOT EQUAL
.
          MOVE      OBAOUTNO,IPADMNO         * Outpatient/Id Number
          MOVE      TWO,IPSYSTM              * Outpatients system
          MOVE      OBASITE,IPSITE           * Site code
.
          MOVE      SP70,PENDHOSP            * Hospital ID
          IF        IBCNMHOS=1
            MOVE      IPADMNO,KEY8
            CALL      RDPMVX11
            IF        OVRCD=0
              MOVE      PMVXMHOS,PENDHOSP
            ENDIF
          ENDIF
          MOVE      " 2",PENDSYST            * Outpatient system
.
          MOVE      OBADATE,PENDSDAT         * as at date
          MOVE      OBADATE,PENDADAT         * Visit date
          MOVE      OBADATE,PENDDDAT         * discharged date
          MOVE      OTBBFUND,PENDFUND        * Health fund
          MOVE      OBACOMP,PENDCLAM         * Claim code
          CALL      IPRH0000                 * Invoicing on hold
          MOVE      ONE,PTIPRSTA             * Record status
          MOVE      SP70,PTIPSVAR            * Clear spare
.
          PACK      KEY8,IPADMNO,SP70
          CALL      RAIPEN1
          IF        OVRCD=1
            CALL      WRIPEN1               * Write new record
          ELSE
            CALL      UPIPEN1               * Update record
          ENDIF
.
PSTCHG99  RETURN
+
