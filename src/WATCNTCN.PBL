.******************************************************************************
.*                                                                            *
.*    Include    :  WATCNTCN.PBL                                              *
.*                                                                            *
.*    Function   :  Count Waiting List Cancellations                          *
.*                                                                            *
.*    Author     :  Graeme Williams                                           *
.*                                                                            *
.*    Date       :  10/12/1996                                                *
.*                                                                            *
.*    Parameters :  WATTR1FD record                                           *
.*                                                                            *
.*    Returns    :  XWTWMHOS - Number of hospital cancellations               *
.*                  XWTWMPAT - Number of patient  cancellations               *
.*                  XWTWMOTH - Number of other    cancellations               *
.*                                                                            *
.* Modifications :                                                            *
.*                                                                            *
.*                                                                            *
.******************************************************************************
WATCNTCN  MOVE      ZERO,XWTWMHOS           * initialise hospital cancellations
          MOVE      ZERO,XWTWMPAT           * initialise patient cancellations
          MOVE      ZERO,XWTWMOTH           * initialise other cancellations
.
          PACK      KEY29,WMURNO,WMCODE,WMCNT,CATBC,Z8 * key for previous read
          CALL      RSWTCAT2                * positional read
WCTCNL10  CALL      RPWTCAT2                * read prev record
          BRANCH    OVRCD,WCTCNL99          * not on file
.
. ------ set up the values just read in, if they match original values ------
. ------ then we have a cancelled booking.  So store the date and code ------
.
          PACK      KEY21,WTCAURNO,WTCAPROC,DWTCAPCN,WTCACATF
.
. ------ check if the read in values match the original values ------
. ------ ie. the latest cat. change is a booking cancellation  ------
.
          MATCH     KEY21,KEY29             * is it the same data ??
          GOTO      WCTCNL99 IF NOT EQUAL   * yes, same values so use them
.
          PACK      KEY5,CATBC,WTCACODT
          CALL      RDCODE1
          IF        OVRCD = 1
            ADD       ONE,XWTWMOTH          * assume other cancellation
          ELSE
            CMATCH    ANSP,TCINDC2
            IF        @EQUAL
              ADD       ONE,XWTWMPAT        * patient cancellation
            ELSE
              CMATCH    ANSH,TCINDC2
              IF        @EQUAL
                ADD       ONE,XWTWMHOS      * hospital cancellation
              ELSE
                ADD       ONE,XWTWMOTH      * other cancellation
              ENDIF
            ENDIF
          ENDIF
          GOTO      WCTCNL10
.
WCTCNL99  RETURN
