.******************************************************************************
.* System         : Patient Billing                                           *
.* Program        : PRTMITEM.PBL                                              *
.* Name           : Print Misc Items                                          *
.******************************************************************************
.* Date           : 17/09/2015                                                *
.* Author         : J.Tan                                                     *
.* Function       : This program will print the 'To be Invoiced' items        *
.*                  applicable for the date range entered                     *
.* Modifications  :                                                           *
.*                  V11.02.01  14.09.2022 DA Sarkies TSK 0909756              *
.*                             Increased the size of the description field    *
.*                             and included PMMIDES2 to allow 65 characters   *
.*                  V10.07.01  04/11/2015 Peter Vela CAR 323303               *
.*                             Changed print format from html to text only    *
.******************************************************************************
.
          DEFPROC   PRTMITEM
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       WEBSECFD/INC
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
.
          INC       PATCODFD/INC       * codes file
          INC       PATCONFD/INC       * patient control file
          INC       PATMA1FD/INC       * patient master file
          INC       PATMI1FD/INC       * admissions file
          INC       PMSVX1FD/INC       * visit ext file
          INC       PMSMTIFD/INC       * Misc.item charges file
+
.*****************************************************************************
.                           VARIABLE DECLARATIONS
.*****************************************************************************
TABLE     INIT      "<table width=100%>"
BOLD      INIT      "<b>"
EBOLD     INIT      "</b>"
ETABLE    INIT      "</table>"
NBSP      INIT      "&nbsp;"
TD        INIT      "<td>"
TDHD      INIT      "<td class=HeadingCell><bgcolor=#cccccc>"
ETD       INIT      "</td>"
TR        INIT      "<tr>"
ETR       INIT      "</tr>"
UNDLN40   INIT      "-----------------------------------------"
.	
CMDLINE   DIM       127
CNTVAR    FORM      2
CODE      DIM       2
.
DIM5      DIM       5
.
SAVEMGRP  DIM       3
SAVEIDAT  DIM       8
FNAMEC    DIM       8
.
ITEMTYP1  INIT      "Included in Contract"
ITEMTYP2  INIT      "Bill to Patient     "
ITEMTYP3  INIT      "Extra to Contract   "
.
ERASE     INIT      "iserase "
ISBUILD   INIT      "isbuild "
FINISH    INIT      "Finish"
.
START     INIT      "Start"
ZZZZ      INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
TEMPFIL1  IFILE     SQL, FIXED=130, KEY="U1-3,5-12,4-4,13-21,22-29,124-129"
UKEY1     INIT      " 130 u1-3,5-12,4-4,13-21,22-29,124-129"
.
.Data File Definition
.--------------------
.
. NAME    TYPE   LENGTH  PHYSICAL START LOC.   DESCRIPTION
. ----    ----   ------  -------- ----------   -----------
XMMIMGRP  DIM       3      3        1          Misc.Group
XMMIINCT  DIM       1      1        4          Inclusion Type 0=Inc in contract
.                                                     0=Inc in contract
.                                                     1=Bill to Patient
.                                                     2=Extra to contract
XMMITDAT  DIM       8      8        5          Transaction date
XMMIITEM  DIM       9      9        13         Item Number
XMMISVTM  DIM       8      8        22         Time
XMMISERV  FORM      5      4        30         Service Number
XMMIDESC  DIM       90     90       34         Description
XMMITRAN  DIM       6      6        124         Transaction number
.End of Record                      130
.
.         Print Misc.items
.
PRTM0000  COMPARE   ZERO,NOCOPIES
          GOTO      PRTM9999 IF EQUAL
.
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      PATMI1A1,"patmi1af"
          OPEN      PATCODE1,"patcodes"
          OPEN      PMSMTIA1,"pmsmtiaf"
          OPEN      PMSVX1A1,"pmsvx1af"
.
.  ------ set up temp file filename ------
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMEC
.
          CALL      CREA0000                * Create tempfile
          CALL      PROC0000                * Process the files
.
          CALL      PRIT0000                * Print report
.
          CALL      UPDEFP00                * Update Default Printer
.
PRTM9999  GOTO      ENDPRTMI
+
.**********************************************************************
.         Update Default Printer File for User
.**********************************************************************
UPDEFP00  OPEN      WEBSECA1,"websecaf"
          PACK      KEY10,USERID,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,UPDEFP99
.
          MOVE      "PATWEB05",IBPDPID
          MOVE      "03",IBPDREP
          MOVE      WBSEUID,IBPDUID
          MOVE      SELPRINT,IBPDPRT
          MOVE      NOCOPIES,IBPDCOP        * no of copies
          MOVE      SP70,IBPDDOC
          MOVE      SP70,IBPDNUM
          MOVE      SP70,IBPDSPA
          PACK      KEY20,IBPDPID,IBPDREP,IBPDUID,SP70
          CALL      RAIBPD1
          IF        OVRCD=0
            CALL      UPIBPD1
          ELSE
            CALL      WRIBPD1
          ENDIF
UPDEFP99  RETURN
+
.******************************************************************************
.*                                  CREA0000                                  *
.*                  Create a tempfile                                         *
.******************************************************************************
CREA0000  CALL      KILL0000                * deleting temporary file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,FNAMEC,UKEY1
          EXECUTE   CMDLINE,TASKID
          OPEN      TEMPFIL1,FNAMEC
          CALL      CLET0000              * Clear tempfile
.
CREA9999  RETURN
+
.******************************************************************************
.*                       Deleting an indexed file                             *
.******************************************************************************
KILL0000  CLOSE     TEMPFIL1
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,FNAMEC         * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999  RETURN
+
.*******************************************************************************
.         Clear tempfile
.*******************************************************************************
CLET0000  MOVE      SP70,KEY35
          CALL      RDSTEMP1
          CALL      RDKTEMP1
          BRANCH    OVRCD,CLET9999
.
          PACK      KEY35,XMMIMGRP,XMMITDAT,XMMIINCT,XMMIITEM,XMMISVTM:
                          XMMITRAN,SP70
          CALL      DELTEMP1
          GOTO      CLET0000
.
CLET9999  RETURN
.
.****************************************************************************
.*                            PROC0000
.****************************************************************************
PROC0000  DISPLAY   *P1:24,*EL,"Scanning : ";
.
          PACK      KEY14,ADMISSNO,SP20
          CALL      RSPMMTI1                * position pointer
PROC1000  CALL      RKPMMTI1                * read next record
          BRANCH    OVRCD,PROC9999          * last record read
.
          MATCH     PMMIVISN,ADMISSNO       * same admission no?
          GOTO      PROC9999 IF NOT EQUAL   * no
.
          DISPLAY   *P12:24,PMMITDAT,PMMISVTM;
.
          MATCH     "3",PMMIRTYP
          GOTO      PROC1000 IF NOT EQUAL   * Not a misc.item
.
          MATCH     FILTFDAT,PMMITDAT
          GOTO      PROC1000 IF LESS        * Not in the range
.
          MATCH     PMMITDAT,FILTTDAT
          GOTO      PROC1000 IF LESS
.
          MATCH     "1",MENUFLAG
          IF        @EQUAL
            MATCH     USERID,PMMIWUSR
            GOTO      PROC1000 IF NOT EQUAL  * different user id
          ENDIF
.
          MOVE      PMMIMGRP,XMMIMGRP
          MOVE      PMMIINCT,XMMIINCT
          MOVE      PMMIITEM,XMMIITEM
          MOVE      PMMITDAT,XMMITDAT
          MOVE      PMMISVTM,XMMISVTM
          MOVE      PMMISERV,XMMISERV
          PACK      XMMIDESC,PMMIDESC,PMMIDES2
          MOVE      PMMITRAN,XMMITRAN
.
          PACK      KEY35,XMMIMGRP,XMMITDAT,XMMIINCT,XMMIITEM,XMMISVTM:
                          XMMITRAN,SP70
          CALL      WRTTEMP1
.
          GOTO      PROC1000
.
PROC9999  RETURN
+
************************************************************************
.*                    PRIT0000                      Called by: ML1000  *
.*         loop through the temporary index file                       *
************************************************************************
PRIT0000  DISPLAY   *P1:24,*EL,"Printing : ";
          MOVE      ZERO,CLNO
          MOVE      SP70,SAVEMGRP
          MOVE      SP70,SAVEIDAT
.
          MOVE      ADMISSNO,KEY8
          CALL      RDMISS1
          BRANCH    OVRCD,PRIT9990
.
          PACK      KEY8,AURNO
          CALL      RDMAST1              * read master file
          BRANCH    OVRCD,PRIT9990
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*138,PTCNNHII
          MOVE      SP70,NHMANMPI        * initialise NHI no
          IF        PTCNNHII=1
            OPEN     NHIMASA2,"nhimasaf"
            MOVE     PURNO,KEY8
            CALL     RDNHMAS2
          ENDIF
.
          CALL      HEAD0000             * display heading
.
          PRINT     *N
.
          MOVE      SP70,KEY35
          CALL      RDSTEMP1
PRIT1000  CALL      RDKTEMP1
          BRANCH    OVRCD,PRIT8000        * last record read
.
          DISPLAY   *P12:24,*V2LON,XMMITDAT,SP1,XMMISVTM;
.
          MATCH     SAVEMGRP,XMMIMGRP        * Same item group?
          IF        !@EQUAL
.           PRINT     *N
            MOVE      XMMIMGRP,SAVEMGRP
            MOVE      SP70,TDESC
            MATCH     SP70,XMMIMGRP
            IF        !@EQUAL
              PACK      KEY5,ANSF,ANSI,XMMIMGRP
              CALL      RDCODE1
              IF        OVRCD=0
                PRINT     *1,TDESC;
                GOTO      PRIT2000
              ENDIF
            ENDIF
            PRINT     *1,"<BLANK GROUP CODE>";
          ELSE
            PRINT     *1,"                    ";
          ENDIF
.
PRIT2000  MOVE      ITEMTYP1,KEY20             * Inc in Contract
          MATCH     "1",XMMIINCT
          IF        @EQUAL
            MOVE      ITEMTYP2,KEY20           * Bill to Patient
          ELSE
            MATCH     "2",XMMIINCT
            IF        @EQUAL
              MOVE      ITEMTYP3,KEY20         * Extra to Contract
            ENDIF
          ENDIF
          PRINT     *30,KEY20,*60,XMMIITEM;
.
          MATCH     SAVEIDAT,XMMITDAT
          IF        !@EQUAL
            UNPACK    XMMITDAT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE                 * Format the start date
            PRINT     *82,CPDATE;
            MOVE      XMMITDAT,SAVEIDAT
          ELSE
            PRINT     *82,"        ";
          ENDIF
.
          MOVE      XMMISERV,DIM5
          LJUSTIFY  DIM5
          PRINT     *92,DIM5:
                    *98,XMMIDESC
.
          ADD       ONE,CLNO
.
.         IF        CLNO>48
.           PRINT    "</tr></table><p class=#"break#">"
.           PRINT    TABLE
.           MOVE     ZERO,CLNO
.           CALL     RHED0000
.         ENDIF
.
          PRINT     *N
.
          GOTO      PRIT1000
.
.         Print End of report
.
PRIT8000  PRINT     *N,*N,*N
.
          PRINT     *1,"Name: ";
          IF        PUSRNAME=1
            PRINT     USERID;
          ELSE
            PRINT     "________________________";
          ENDIF
          PRINT     *44,"Signature:_____________________________________":
                    *110,"Date: ",CURRDATE
.
PRIT9990  CALL      KILL0000               * delete temp. file
.
PRIT9999  RETURN
+
***************************************************************************
.*                           HEAD0000               Called by : MANY      *
.*           print the heading                                            *
***************************************************************************
HEAD0000  CLOCK     TIME,CTIMEIS
          PRINT     *1,"Printed on ",CURRDATE," at ",CTIMEIS
.
          UNPACK    FILTFDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *N,*N,*1,"Items for Dates :   From: ",CPCDATE:
                    " To ";
.
          UNPACK    FILTTDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     CPCDATE
.
          CALL      PATN0000             * format patient's name
.
          PRINT     *N,*N,*1,"NHI : ",NHMANMPI,*60,"Name: ",PACFNAME
.
          UNPACK    ADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *1,"Visit Number : ",ADMISSNO,*60,"Date :",CPCDATE
.
          PRINT     *N,*N,*1,"FI Group":
                          *30,"Inc Type":
                          *60,"Procedure":
                          *82,"Date":
                          *92,"Qty":
                          *98,"Item"
.
          PRINT     *1,"--------":
                    *30,"--------":
                    *60,"---------":
                    *82,"----":
                    *92,"---":
                    *98,"----"
.
          ADD       TEN2,CLNO
. 
HEAD9999  RETURN
+
***************************************************************************
.*                           PATN0000                                     *
.*        get patients name                                               *
***************************************************************************
PATN0000  MOVE      TWO,PACFRMT
          MOVE      PTITL,PACTITLE
          MOVE      PGNAME,PACGNAME
          MOVE      PSNAME,PACSNAME
          CALL      PACNAME
.
PATN9999  RETURN
+
.********************************************************************
.         I/O
.********************************************************************
RDSTEMP1  RESET     KEY35
          READ      TEMPFIL1,KEY35;;
          RETURN
.
RDKTEMP1  MOVE      ZERO,OVRCD
          READKS    TEMPFIL1;XMMIMGRP,XMMIINCT,XMMITDAT,XMMIITEM:
                             XMMISVTM,XMMISERV,XMMIDESC,XMMITRAN
          GOTO      OVERCOND IF OVER
          RETURN
.
DELTEMP1  RESET     KEY35
          DELETE    TEMPFIL1,KEY35
          RETURN
.
WRTTEMP1  MOVE      ZERO,OVRCD
          WRITE     TEMPFIL1,KEY35;XMMIMGRP,XMMIINCT,XMMITDAT,XMMIITEM:
                                   XMMISVTM,XMMISERV,XMMIDESC,XMMITRAN
          GOTO      OVERCOND IF OVER
          RETURN
.
.********************************************************************
.
          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       PATCODIO/INC       * codes file
          INC       PATMA1IO/INC       * patient master file
          INC       PATMI1IO/INC       * admissions file
          INC       PMSVX1IO/INC       * visit ext file
          INC       PMSMTIIO/INC       * Misc.item charge file
          INC       WEBSECIO/INC
.
ENDPRTMI  ENDPROC
+
