.-------------------------------------------------------------------------------
.   System    :   FMS Accounting System
.
.   Program   :   FMSSBAFM
.
.   Requires  :   FMLALEDG Ledger Number
.                 FMLADESC Ledger Number Description
.                 FMSUUD3A Subjective Code Length (Sector 51 controlf)
.                 CSUBJIND 0=all subj, 1=total, 2=non total, 3=non head
.                          4=stat-tot subj, 5=posting subj, 6=norm post subj
.                          7=Stats Posting
.                 file FMSAMAA1 (account master) to be open
.
.   Routines  :   KSBAFM00 Keyin Subjective   - Returns a valid record
.                 ASBAFM00 Accept Subjective  - Returns a key NOT on file
.
.   Return    :   EXIT = 1 null entry EXIT = 2 EXITCHAR entered
.
.   Internal  :   HSBAFM00 Help Display for Subjective
.
.-------------------------------------------------------------------------------
.   Mods      :   07/02/97  ROD          
.                           Added csubjind type 7                    
.                 24/07/91  Neeriem Dye
.                           Fixed infinite loop on previous page
.                           Only occurred if first item on file is invalid
.-------------------------------------------------------------------------------
. Normal Code Keyin - Code Must Exist Exit = 1 if Null Entry
.-------------------------------------------------------------------------------
KSBAFM00  MOVE      "0",CKEYTYP
          MOVE      FMLALEDG,FMSALEDG
          CALL      KFMSAA00
          RETURN
.-------------------------------------------------------------------------------
. Accept Code Keyin - Code Must Not Exist
.-------------------------------------------------------------------------------
ASBAFM00  MOVE      "1",CKEYTYP
          MOVE      FMLALEDG,FMSALEDG
          CALL      KFMSAA00
          RETURN
.---------------------------------------------------------------------------
.   System    :   Financial Management System
.   Include   :   FMSSBAKY
.   Routine   :   HFMSAA00 Subjective
.   Return    :   FMCAACC
.-------------------------------------------------------------------------------
HFMSAA00
          MOVE      ZERO,CSCRSIZ
          IF        HTOP<3
            MOVE      "10",HTOP
            MOVE      "24",HBOT
            MOVE      "30",HLEF
            MOVE      "80",HRIG
          ENDIF
HFMSAA04
          CALL      GSCRN000
          BRANCH    EXIT,HFMSAA99
          MOVE      STDCOL00,STDCOL01
          ADD       " 5",STDCOL01
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON,"Subjective"
.
          MOVE      STDCOL01,STDCOL02
          ADD       "13",STDCOL02
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"Description"
.
          MOVE      STDCOL02,STDCOL03
          ADD       "40",STDCOL03
HFMSAA08
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          MOVE      SP70,FMSASUBJ
          PACK      KEY14,FMSALEDG,FMSASUBJ,SP70
          PACK      SAVKEY80,FMSALEDG,FMSASUBJ,SP70
          SETLPTR   SAVKEY80,2
          CALL      RSFMSA1
HFMSAA10
          CALL      RKFMSA1
          BRANCH    OVRCD,HFMSAA15
          CALL      CKSBA000
          BRANCH    OVRCD,HFMSAA10
.
          PACK      KEY14,FMSALEDG,FMSASUBJ,SP70
          MATCH     SAVKEY80,KEY14
          GOTO      HFMSAA15 IF NOT EQUAL
          BRANCH    CSEARCH,HFMSAA12
          CALL      HFMSAA60
          BRANCH    OVRCD,HFMSAA10
HFMSAA12
          ADD       ONE,XITEMNO
          ADD       ONE,STDROW
          STORE     KEY14,XITEMNO,ST1,ST2,ST3,ST4,ST5:
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,XITEMNO,DOT
          DISPLAY   *PSTDCOL01:STDROW,FMSASUBJ
          DISPLAY   *PSTDCOL02:STDROW,FMSADESC
          COMPARE   MAXITEM,XITEMNO
          GOTO      HFMSAA10 IF NOT EQUAL
HFMSAA15
          UNPACK    SAVKEY80,FMSALEDG,FMSASUBJ
          MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
HFMSAA20
          CALL      HSCRI000
          BRANCH    EXIT,HFMSAA30,HFMSAA35,HFMSAA40,HFMSAA45,HFMSAA04:
                              HFMSAA91,HFMSAA50
          GOTO      HFMSAA92
HFMSAA30
          CALL      RKFMSA1
          BRANCH    OVRCD,HFMSAA85
          CALL      CKSBA000
          BRANCH    OVRCD,HFMSAA30
.
          PACK      KEY14,FMSALEDG,FMSASUBJ,SP70
          MATCH     SAVKEY80,KEY14
          GOTO      HFMSAA85 IF NOT EQUAL
          CALL      RPFMSA1
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HFMSAA10
HFMSAA35
          MOVE      ST1,KEY14
          CALL      RDFMSA1
HFMSSA3A
          CALL      RPFMSA1
          BRANCH    OVRCD,HFMSAA85
          CALL      CKSBA000
          BRANCH    OVRCD,HFMSSA3A
.
          PACK      KEY14,FMSALEDG,FMSASUBJ,SP70
          MATCH     SAVKEY80,KEY14
          GOTO      HFMSAA85 IF NOT EQUAL
          MATCH     SP70,FMSASUBJ
          GOTO      HFMSAA85 IF EQUAL
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
HFMSAA36
          CALL      RPFMSA1
          BRANCH    OVRCD,HFMSAA37
          CALL      CKSBA000
          BRANCH    OVRCD,HFMSAA36
.
          PACK      KEY14,FMSALEDG,FMSASUBJ,SP70
          MATCH     SAVKEY80,KEY14
          GOTO      HFMSAA37 IF NOT EQUAL
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HFMSAA36 IF NOT EQUAL
HFMSAA37
          MOVE      ZERO,XITEMNO
          GOTO      HFMSAA10
HFMSAA40
          MOVE      ST1,KEY14
          CALL      RDFMSA1
HFMSAA41
          CALL      RPFMSA1
          BRANCH    OVRCD,HFMSAA85
          CALL      CKSBA000
          BRANCH    OVRCD,HFMSAA41
.
          PACK      KEY14,FMSALEDG,FMSASUBJ,SP70
          MATCH     SAVKEY80,KEY14
          GOTO      HFMSAA85 IF NOT EQUAL
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HFMSAA08
HFMSAA45
          CALL      RKFMSA1
          BRANCH    OVRCD,HFMSAA85
          CALL      CKSBA000
          BRANCH    OVRCD,HFMSAA45
.
          PACK      KEY14,FMSALEDG,FMSASUBJ,SP70
          MATCH     SAVKEY80,KEY14
          GOTO      HFMSAA85 IF NOT EQUAL
          SUSPEND
          CLIP      1,1,CRIG,24
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,XITEMNO
          PACK      KEY14,SAVKEY80,Z70
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
          CALL      RSFMSA1
HFMSAA46
          CALL      RPFMSA1
          BRANCH    OVRCD,HFMSAA47
          CALL      CKSBA000
          BRANCH    OVRCD,HFMSAA46
.
          PACK      KEY14,FMSALEDG,FMSASUBJ,SP70
          MATCH     SAVKEY80,KEY14
          GOTO      HFMSAA47 IF NOT EQUAL
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HFMSAA46 IF NOT EQUAL
HFMSAA47
          MOVE      ZERO,XITEMNO
          GOTO      HFMSAA10
HFMSAA50  COMPARE   ZERO,CSEARCH
          GOTO      HFMSAA55 IF EQUAL
          MOVE      ZERO,CSEARCH
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          UNPACK    SP70,SCHFLD01,SCHFLD02,SCHFLD03,SCHFLD04,SCHFLD05
          UNPACK    SP70,SCHFLD06,SCHFLD07,SCHFLD08,SCHFLD09,SCHFLD10
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          DISPLAY   *PSTDCOL00:STDROW,*V2LON,*HON,"Sch"
          KEYIN     *PSTDCOL01:STDROW,*V2LON,SCHFLD01
          RESET     SCHFLD01
          KEYIN     *PSTDCOL02:STDROW,*V2LON,SCHFLD02
          RESET     SCHFLD02
          PACK      KEY8,SP70
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HFMSAA08
HFMSAA55  MOVE      ONE,CSEARCH
          PACK      KEY8,SP70
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HFMSAA08
HFMSAA60  MOVE      ZERO,OVRCD
          MATCH     SP70,SCHFLD01
          GOTO      HFMSAA61 IF EQUAL
          MATCH     SCHFLD01,FMSASUBJ
          GOTO      HFMSAA72 IF NOT EQUAL
HFMSAA61
          MATCH     SP70,SCHFLD02
          GOTO      HFMSAA62 IF EQUAL
          MATCH     SCHFLD02,FMSADESC
          GOTO      HFMSAA72 IF NOT EQUAL
HFMSAA62
HFMSAA63
HFMSAA64
HFMSAA65
HFMSAA66
HFMSAA67
HFMSAA68
HFMSAA69
HFMSAA70
          GOTO      HFMSAA75
HFMSAA72  MOVE      ONE,OVRCD
HFMSAA75  RETURN
HFMSAA85
          LOAD      KEY14,XITEMNO,ST1,ST2,ST3,ST4,ST5:
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY14,FMSALEDG,FMSASUBJ
          CALL      RDFMSA1
HFMSAA90
          UNPACK    SAVKEY80,FMSALEDG,FMSASUBJ
          BEEP
          GOTO      HFMSAA20
HFMSAA91
          MOVE      SP70,FMSADESC
          MOVE      SP70,FMSAADES
          MOVE      SP70,FMSAPPOS
          MOVE      SP70,FMSASPAR
          MOVE      SP70,KEY14
          MOVE      SP70,FMSASUBJ
          GOTO      HFMSAA96
HFMSAA92
          MOVE      SP70,KEY14
          MOVE      SP70,FMSASUBJ
          LOAD      KEY14,CCITEM,ST1,ST2,ST3,ST4,ST5:
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY14,FMSALEDG,FMSASUBJ
HFMSAA96
          PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
HFMSAA99
          RETURN
.---------------------------------------------------------------------------
.   System    :   Financial Management System
.   Include   :   FMSSBAKY
.   Routine   :   KFMSAA00 Keyin Subjective
.   Parameter :   CKEYTYP 0 = Keyin , 1 = Accept, 2 = Either
.   Return    :   EXIT = 1 null entry, 2 EXITCHAR entered
.-------------------------------------------------------------------------------
KFMSAA00 TOPIC     "FMSASUBJ"
          MOVE      FMSUUD3A,CSTRLEN         * set up string length
          CALL      GSTR0000                 * get code
          PACK      FMSASUBJ,CSTRING,SP70
.
          ENDSET    FMSASUBJ
          APPEND    SP70,FMSASUBJ
          RESET     FMSASUBJ
.
          MATCH     SP70,FMSASUBJ
          GOTO      KFMSAA90 IF EQUAL
.
          CMATCH    EXITCHAR,FMSASUBJ
          GOTO      KFMSAA95 IF EQUAL
.
          CMATCH    QUEST,FMSASUBJ
          GOTO      KFMSAA10 IF NOT EQUAL
.
          MOVE      CCITEM,SCCITEM
          CALL      HFMSAA00
          MOVE      SCCITEM,CCITEM
.
          ENDSET    FMSASUBJ
          APPEND    SP70,FMSASUBJ
          RESET     FMSASUBJ
.
          MATCH     SP70,FMSASUBJ
          GOTO      KFMSAA00 IF EQUAL
.
          DISPLAY   *PCCOL:CVERT,*V2LON,FMSASUBJ
KFMSAA10
          PACK      KEY14,FMSALEDG,FMSASUBJ
          CALL      RDFMSA1
          BRANCH    OVRCD,KFMSAA80
          CALL      CKSBA000
          BRANCH    OVRCD,KSBAFM85
          BRANCH    CKEYTYP,KFMSAA75
KFMSAA70
          MOVE      ZERO,EXIT
          GOTO      KFMSAA99
KFMSAA75
          MOVE      "Subjective Already Exists - ",DISPMSG
          CALL      MESSAGE1
          GOTO      KFMSAA00
KSBAFM85  
          MOVE      ZERO,F1
          MOVE      FMSASTYP,F1
          MOVE      SASTYPE0,SASTYPE
          LOAD      SASTYPE,F1,SASTYPE1,SASTYPE2,SASTYPE3,SASTYPE4
          STRIP     SASTYPE
          CLEAR     DISPMSG
          APPEND    SASTYPE,DISPMSG
          APPEND    " Subjective Types Invalid - ",DISPMSG
          BEEP
          CALL      MESSAGE1
          GOTO      KSBAFM00
KFMSAA80
          MOVE      ZERO,EXIT
          BRANCH    CKEYTYP,KFMSAA99,KFMSAA99
          MOVE      "Subjective Does Not Exist - ",DISPMSG
          CALL      MESSAGE1
          GOTO      KFMSAA00
KFMSAA90
          MOVE      ONE,EXIT
          GOTO      KFMSAA99
KFMSAA95
          MOVE      TWO,EXIT
KFMSAA99 TOPIC    PRGID
          RETURN
.**********************************************************************
. CKSBA - Check if correct subjective indicator
.        Requires - CSUBJIND 0=all, 1=total, 2=non total, 3=non head
.                            4=stat-tot subj, 5=posting subj, 6=norm post subj
.                            7=Stats Posting   
.                   FMSASTYP (account type of record to be tested)
.        Returns  - OVRCD    (1=invalid record)
.**********************************************************************
CKSBA000  BRANCH    CSUBJIND,CKSBA100,CKSBA200,CKSBA300,CKSBA400:
                             CKSBA500,CKSBA600,CKSBA700
          GOTO      CKSBA9000                    * all
.
CKSBA100  MATCH     "1",FMSASTYP
          GOTO      CKSBA900 IF EQUAL            * total type
          GOTO      CKSBA950
.
CKSBA200  MATCH     "1",FMSASTYP
          GOTO      CKSBA900 IF NOT EQUAL        * non total type
          GOTO      CKSBA950
.
CKSBA300  MATCH     "2",FMSASTYP
          GOTO      CKSBA900 IF NOT EQUAL        * non head type
          GOTO      CKSBA950
.
CKSBA400  MATCH     "4",FMSASTYP
          GOTO      CKSBA900 IF EQUAL            * stat-tot type
          GOTO      CKSBA950
.
CKSBA500  MATCH     "0",FMSASTYP
          GOTO      CKSBA900 IF EQUAL            * normal type
          MATCH     "3",FMSASTYP
          GOTO      CKSBA900 IF EQUAL            * stat-post type
          GOTO      CKSBA950
.
CKSBA600  MATCH     "0",FMSASTYP
          GOTO      CKSBA900 IF EQUAL            * normal type
          GOTO      CKSBA950
.
CKSBA700  MATCH     "3",FMSASTYP
          GOTO      CKSBA900 IF EQUAL            * stats posting type
          GOTO      CKSBA950
.
.
CKSBA900  MOVE      ZERO,OVRCD                   * valid record
          GOTO      CKSBA999
.
CKSBA950  MOVE      ONE,OVRCD                    * invalid record
.
CKSBA999  RETURN
