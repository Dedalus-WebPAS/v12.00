.
.         PATSHEET
.         --------
.
.         THIS INCLUDE PRINTS THE PATIENT SUMMARY (IDENTIFICATION) DETAILS
.
.        IDENTIFICATION VARIABLES
.******************************************************************************
. MODS
.*        V11.00.01 02/04/2020  J.Tan             TSK 0868813                 *
.*                  Changed PKNAME to DIM80                                   *
.*        V9.03.01  11/06/2004  Mike Laming   CAR 49016  July 2004 PRS/2      *
.*                  - Add Sex Description routine SEXDSC00 with Code "R" -    *
.*                  "Intersex" added                                          *
.*        V9.02.01  19/10/2001  Mike Laming  SRF 10614                        *
.*                  Correct clearing of Fund Table field AFNDTB (4 to 8 bytes)*
.*        V5.08.02  30/08/2000  Tonii                                         *
.*                  Mods to accept Unknown and Indeterminate as valid patient *
.*                  sex description, Indeterminate = 3, Unknown =9            *
.*        V5.08.01  06/06/2000  Jill Habasque                                 *
.*                  Changed from key10 to key14 for the health fund file      *
.                   08/10/1998  Glenn Berry      5.07 changes
.******************************************************************************
.
. REGDESC  DIM      20
. INSIND   DIM      1
. SEXIND   DIM      1
. ADOCDESC DIM      22
. FUNDESC  DIM      30
. TABDESC  DIM      30
. NOKADDR  DIM      57
. RESADDR  DIM      57
. ADDRLN   DIM      57
. DCATE    DIM      1
. DCLASS   DIM      1
. DDAY1    DIM      1
. DDAY2    DIM      1
. DMON1    DIM      1
. DMON2    DIM      1
. DYEAR1   DIM      1
. DYEAR2   DIM      1
. U1QATB   DIM      3
. U2DESC   DIM      20
.
. DIM3     DIM      2
. DIM4     DIM      4
. DIM5     DIM      5
. DIM7     DIM      7
. DIM8     DIM      8
. DIM10    DIM      10
. DIM25    DIM      25
. XAGE     DIM      3
.
LINEUPID 
        CALL      OPNPRINT
        PRINT     *N,*N,*66,X5,X1,*N,*N,*29,X5,X1,*N,*29,X10:
                  *N,*N,*14,X20:
                  *44,X5,X3,*57,X1,*61,"X X X X",*73,"X X",*N,*N:
                  *14,X20,X5,*N,*N:
                  *11,X20,X5,*N:
                  *11,X20,X5,*N:
                  *4,X15,*25,X10,X2,*N,*N:
                  *10,X4,*N,*38,X1,*N,*10,X3,*31,"XX/XX/XX",*N:
                  *36,X3,*N,*N,*49,"X X X",*57,X3,*61,"X X X X",*73,"X X":
                  *N,*36,X3,*N,*N:
                  *14,X20,*N,*14,X10,X3:
                  *N,*38,X1,*N,*N,*N,*38,X1,*N,*N:
                  *11,X30:
                  *46,X30,*N,*N:
                  *28,X10,*N,*N,*13,X20,X2:
                  *53,X20,X2,*N,*N:
                  *10,X20,*48,X10,*N:
               *7,X30,X20,X5,X2:
                  *N,*10,X10,X2,*48,X10,X2,*N,*N:
                  *18,X20,*48,X10,*N:
               *7,X30,X20,X5,X2:
                  *N,*10,X10,X2,*48,X10,X2:
                  *N,*N,*N,*N,*N,*N,*N,*N,*N,*N:
                  *N,*N,*N,*N,*N,*N,*N,*N,*N,*N:
                  *N,*N,*N,*N,*N,*N,*N
.
        COMPARE   TWO,CIDLAY
        GOTO      ENDLUP IF EQUAL
.
        PRINT     *N,*N,*N,*N,*N,*N,*N
.
ENDLUP  CALL      CLSPRINT
        RETURN
.
SETLINID
        CALL        LABELAGE                * set up age
.
        MOVE            ATYPE,DCATE
        MOVE            ACLSS,DCLASS
        UNPACK    PBDATE,XCENT,XYEAR,XMON,XDAY
.
        UNPACK    ADATE,CCENT,DYEAR1,DYEAR2,DMON1,DMON2,DDAY1,DDAY2
        MOVE            SP20,REGDESC
        MATCH           SP3,PREG
        GOTO            INSURE IF EQUAL
        PACK            KEY5 WITH CODER,PREG 
        CALL            RDCODE1
        BRANCH          OVRCD OF INSURE
        MOVE            TDESC,REGDESC
.
.       ISIND = 1 IF INSURED BY MAB,W/C,..ETC
.             = 2 NOT INSURED (IE 0=ACLAIM=NO CLAIM=PRIVATE INSURANCE
.                                ....IE....HAS H.B.A OR SOMETHING )
.
INSURE  MOVE            TWO,INSIND
        CMATCH          "0",ACLAIM
        GOTO            SETSEX IF EQUAL
        MOVE            ONE,INSIND
.
SETSEX  MOVE            ZERO,SEXIND
.
.* ****************************************** Start of Changes         *C-49016
          MOVE      PSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
.                                           *       returns DSEXDESC & DSEXNO
          MOVE      DSEXNO,SEXIND
.* ******************************************   End of Changes         *C-49016
.
ATDOCT  MOVE            SP25,ADOCDESC
        MATCH           SP6,ADOCTA
        GOTO            REDOCT IF EQUAL
        MOVE            ADOCTA,KEY6
        CALL            RDDOCT1
        BRANCH          OVRCD OF REDOCT
        MOVE            DGNAME,ANS
        PACK            ADOCDESC WITH ANS,DOT,DSNAME
.
REDOCT  BRANCH          CRDRTYP OF GETFUN
.
        MOVE            SP25,ARDRNAM
        MATCH           SP6,ADOCTR
        GOTO            GETFUN IF EQUAL
        MOVE            ADOCTR,KEY6
        CALL            RDDOCT1
        BRANCH          OVRCD OF GETFUN
        MOVE            DGNAME,ANS
        PACK            ARDRNAM WITH ANS,DOT,DSNAME
.
GETFUN  MOVE            SP30,FUNDESC
        MOVE            SP30,TABDESC
        MATCH           SP6,AFUNDH
        GOTO            GETNOK IF EQUAL
        PACK            KEY14 WITH AFUNDH,ZERO4,SP10
        CALL            RDFUND1
        BRANCH          OVRCD OF GETABLE
        MOVE            HFNAME,FUNDESC
.
GETABLE MATCH           SP8,AFNDTB                                     *C-90201
        GOTO            GETNOK IF EQUAL
        PACK            KEY14 WITH AFUNDH,AFNDTB,SP10
        CALL            RDFUND1
        BRANCH          OVRCD OF GETNOK
        MOVE            HFNAME,TABDESC
.
GETNOK  MOVE            SP30,U2DESC
        MOVE            SP3,U1QATB
.
        COMPARE         TWO,CIDLAY
        GOTO            NOTROCK IF NOT EQUAL
.
        RESET           TABDESC,10
        APPEND          SP30,TABDESC
        RESET           TABDESC,17
        APPEND          AFNDMM,TABDESC
        RESET           TABDESC
.
        MOVE            AUSR1,U1QATB
        PACK            KEY5 WITH CODEU2,AUSR2 
        CALL            RDCODE1
        BRANCH          OVRCD OF NOTROCK
        MOVE            TDESC,U2DESC
.
NOTROCK PACK            NOKADDR WITH SP30,SP30
        MOVE            PNKADD1,PKADD1
        MOVE            PNKADD2,PKADD2
        MOVE            PNKPOST,PKPOST
        MOVE            PNKSUBR,PKSUBR
.
        CALL            PACADD
        MOVE            ADDRLN,NOKADDR
.
        PACK            RESADDR WITH SP30,SP30
        MOVE            PGNAME TO PKNAME
        RESET           PKNAME TO 1
        APPEND          " " TO PKNAME
        APPEND          PTMASNAM TO PKNAME
        RESET           PKNAME
        MOVE            PADD1 TO PKADD1
        MOVE            PADD2 TO PKADD2
        CALL            PACADD
.
        MOVE            "SELF",PKRELP
        MOVE            PTELEP,PKTELEP
        MOVE            PTELEB,PKTELEB
        MOVE            AADMNO,KEY8
        CALL            RDRESP1
        BRANCH          OVRCD OF SAVPK2
.
        CALL            PACADD
        MOVE            ADDRLN,RESADDR
.
SAVPK2  RETURN
.
PACADD PACK        ADDRLN WITH SP30,SP30
        MOVE        PKADD1,ADDRLN
        CALL        FENDSHT
.
        APPEND      COMMA,ADDRLN
        APPEND      PKADD2,ADDRLN
        CALL        FENDSHT
.
        APPEND      COMMA,ADDRLN
        APPEND      PKSUBR,ADDRLN
        CALL        FENDSHT
.
        APPEND      SP1,ADDRLN
        APPEND      PKPOST,ADDRLN
        RESET       ADDRLN
        RETURN
.
.
FENDSHT RESET       ADDRLN,57
FSHTLP1 CMATCH      SP1,ADDRLN
        RETURN      IF NOT EQUAL
        BUMP        ADDRLN,-1
        GOTO        FSHTLP1 IF NOT EOS
.
        RESET       ADDRLN
        RETURN
.
PRTSHEET 
        CALL      OPNPRINT
        MOVE      AURNO,DIM8
        REP       " 0",DIM8
.
        UNPACK    AWARD INTO DIM1A,DIM1B,DIM1C
        COMPARE   THREE,ASTAT
        GOTO      SCOPY1 IF NOT EQUAL
.
        UNPACK    SP3 INTO DIM1A,DIM1B,DIM1C
        MOVE      SP3,ABED
.
.        TWO COPIES... EASIER TO PRINT IT TWICE
.
SCOPY1  UNPACK    PKNAME,KEY29
        PRINT     *N,*N,*66,AADMNO,*N,*N,*29,DIM8,*N,*29,PMEDI:
                  *N,*N,*14,PSNAME:
                  *44,ATIME,*57,DCLASS,*61,DDAY1,SP1,DDAY2,SP1,DMON1,SP1:
                      DMON2,SP1,*73,DYEAR1,SP1,DYEAR2,*N,*N:
                  *14,PGNAME,*N,*N,*11,PADD1,*N,*11,PADD2,*N:
                  *4,PSUBRB,*25,PTELEP,*N,*N:
                  *10,PPOST,*N,*38,SEXIND,*N,*10,XAGE,*31,XDAY,SLASH:
                      XMON,SLASH,XYEAR,*N:
                  *36,PCONT,*N,*49,DIM1A,SP1,DIM1B,SP1,DIM1C,*57,ABED:
                  *61,DDAY1,SP1,DDAY2,SP1,DMON1,SP1:
                      DMON2,SP1,*73,DYEAR1,SP1,DYEAR2:
                  *N,*N,*36,PMSTAT,*N,*N:
                  *14,REGDESC,*N,*14,POCCUP:
                  *N,*38,PTYPE,*N,*N,*N,*38,INSIND,*N,*N:
                  *11,FUNDESC,*46,TABDESC,*N,*N:
                  *21,PPENNO,*44,U1QATB,*54,U2DESC,*N,*N:
                  *13,ARDRNAM,*53,ADOCDESC,*N,*N:
                  *10,PNKNAME,*48,PNKRELP,*N:
                   *7,NOKADDR,*N,*10,PNKTELP,*48,PNKTELB,*N,*N:
                  *18,KEY29,*48,PKRELP,*N:
                   *7,RESADDR,*N,*10,PKTELEP,*48,PKTELEB:
                  *N,*N,*N,*N,*N,*N,*N,*N,*N,*N:
                  *N,*N,*N,*N,*N,*N,*N,*N,*N,*N:
                  *N,*N,*N,*N,*N,*N,*N
.
        COMPARE   TWO,CIDLAY
        GOTO      ENDIDP IF EQUAL
.
        PRINT     *N,*N,*N,*N,*N,*N,*N
.
ENDIDP  CALL      CLSPRINT
        RETURN
.
