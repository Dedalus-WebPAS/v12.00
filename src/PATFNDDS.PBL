.
.       PATFNDDS : Display Health Fund File Entries
.
.       This include is used to display various options available, using
.       whatever file it needs, along with any special parameter supplied
.       by the calling program.
.
.     Modifications: 
.         V5.08.03  19/10/2000  Dean Cramer     SRF 6710
.                   Alter key variable sizes for moves to health fund table
.         V5.08.02  19/07/2000  Jill Habasque                                
.                   Added check of CKYINACT to not display inactive HF        
.         V5.08.01  06/06/2000  Jill Habasque                                
.                   Changed from key10 to key14 for the health fund file      
.                    28/11/89 Graeme Williams
.                    Added the parameter CNEWDS to determine if the include
.                    is to use the new standards for "?" routines
.                    10/11/1995  MATT
.                    Removed traps on F1
.
.       This subroutine displays the list of available health funds.
.
PATFNDDS
PATDSFN1  COMPARE    SIX,CFEETYP
          GOTO       PATDSFN2 IF EQUAL
          DISPLAY    *P1:3,*EF,*P20:4,*V2LON,*ULON,"HEALTH FUNDS"
          GOTO       PATDSFN3
.
PATDSFN2  DISPLAY    *P1:3,*EF,*P20:4,*V2LON,*ULON,"REGISTERED EMPLOYER CODES"
.
PATDSFN3  MOVE       FIVE,CVERT
          MOVE       ONE,CCOL
.
.         TRAP       ENDFUND0 IF F1
.
          MOVE       SP70,KEY14
          CALL       RDSFUND1
.
.         Loop over file
.
FUNDLP1   MOVE       ZERO,PHFTACT
          CALL       RDKFUND1
          BRANCH     OVRCD OF ENDFUND1
.
          MATCH      SP8,HFTABL
          GOTO       FUNDISP1 IF EQUAL
.
          MATCH      "0000    ",HFTABL
          GOTO       FUNDLP1 IF NOT EQUAL
.
.         ----   don't display inactive codes if CKYINACT=0
.
          IF         CKYINACT=0
            IF         PHFTACT=1
              GOTO       FUNDLP1          * dont display inactive HF
            ENDIF
          ENDIF
.
FUNDISP1  DISPLAY    *PCCOL:CVERT,*V2LON,HCODE,SP2,*HOFF,HFNAME
          ADD        ONE,CVERT
          COMPARE    TWENTY4,CVERT
          GOTO       FUNDLP1 IF LESS
.
          MOVE       FIVE,CVERT
          ADD        "40",CCOL
          COMPARE    "70",CCOL
          GOTO       FUNDLP1 IF LESS
.
.         TRAPCLR    F1
.
.         Check for new standards question, or old question
.
          BRANCH    CNEWDS,DSFND200
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, ":
                    "(",*V2LON,"F",*HOFF,")irst Screen, ":
                    "(",*V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSE,ANS
          GOTO      ENDFUND2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATDSFN1 IF EQUAL
.
          GOTO      DSFND400
.
.         Use the new standards question
.
DSFND200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, ":
                    "(",*V2LON,"F",*HOFF,")irst Screen, ":
                    "(",*V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSC,ANS
          GOTO      ENDFUND2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATDSFN1 IF EQUAL
.
DSFND400  MOVE       FIVE,CVERT
          MOVE       ONE,CCOL
          DISPLAY    *P1:5,*EF
.
.         TRAP       ENDFUND0 IF F1
.
          GOTO       FUNDLP1
.
ENDFUND0  NORETURN
.         TRAPCLR    F1
.
.      End of health funds
.
ENDFUND1  BRANCH     CNEWDS,ENDFUND2
.
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
          MOVE      ZERO,OVRCD
.
ENDFUND2  RETURN
.
