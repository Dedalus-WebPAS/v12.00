.------------------------------------------------------------
. SCTT External Referral Comment Processing
.  Requires Includes ALLCSTDF.PBL
.                    ALLCSTFD & IO 
.
. V9.06.00 16/05/2006  Davin         CAR 76341
.          Created include (based on viscmtcd)
.------------------------------------------------------------
.  CLRACM00 - Call from CLRPAR00 Routine to Initialise Comments Input File
.  SETACM00 - Call from SETPAR00 Routine when input parameter matches "alcst"
.  UPACM000 - Call to Update Comments for Add or Update
.  ACMHTM00 - Call to Output Comments to HTML
.------------------------------------------------------------
. Clear Inputs
.------------------------------------------------------------
CLRACM00  CLEAR     ALCSTFNM
          APPEND    "ALLCST",ALCSTFNM
          APPEND    PORT,ALCSTFNM
          RESET     ALCSTFNM
          REP       " 0",ALCSTFNM
          PREP      ALCSTFIL,ALCSTFNM
          RETURN
.------------------------------------------------------------
. Set Inputs
.------------------------------------------------------------
SETACM00  MOVE      ZERO,BLNKFLAG
          UNPACK    QRYNAME,KEY5,KEY3
          WRITE     ALCSTFIL,SEQ;"%START:",KEY3
          PACK      QRYDATA,QRYDATA,SP70,SP70
          WRITE     ALCSTFIL,SEQ;QRYDATA
.
SETACM10  READ      CONFFILE,SEQ;QRYNAME,QRYDATA * Read CONFFILE sequentially
          GOTO      SETACM99 IF OVER             * If end of file
          MATCH     SP70,QRYNAME                 * If next CGI parameter
          GOTO      SETACM80 IF NOT EQUAL
.
          PACK      QRYDATA,QRYDATA,SP70,SP70
.
          MATCH     SP70,QRYDATA
          IF        @EQUAL
            BRANCH    BLNKFLAG,SETACM10
            MOVE      ONE,BLNKFLAG               * Blank line yes
            SQUEEZE   QRYDATA
            GOTO      SETACM70                   * don't write second blank line
          ENDIF
          IF        @EOS
            BRANCH    BLNKFLAG,SETACM10          * don't write second blank line
            MOVE      ONE,BLNKFLAG               * Blank line yes
            SQUEEZE   QRYDATA
            GOTO      SETACM70
          ENDIF
.
          MOVE      ZERO,BLNKFLAG                * Blank line no
.
SETACM70  WRITE     ALCSTFIL,SEQ;QRYDATA
          GOTO      SETACM10
.
SETACM80  MOVE      ONE,TEXTAREA                 * Flag for Next CGI Parameter
SETACM99  RETURN
.------------------------------------------------------------
. Add/Update Comments
.------------------------------------------------------------
UPACM000  OPEN      ALCSTFIL,ALCSTFNM
UPACM100  READ      ALCSTFIL,SEQ;ALCSTLIN
          GOTO      UPACM999 IF OVER
          MATCH     "%START:",ALCSTLIN
          IF        @EQUAL
            UNPACK    ALCSTLIN,KEY7,ALCSTTYP
            CALL      CLACM000
            MOVE      ZERO,ALCSTCNT
            GOTO      UPACM100
          ENDIF
UPACM110  ADD       ONE,ALCSTCNT
          PACK      KEY22,URNUMBER,ALSCCNTR,ALCSTTYP,ALCSTCNT,SP70
          UNPACK    KEY22,ALCSURNO,ALCSCNTR,ALCSTYPE,ALCSLINE
          CALL      RDALCST1
          COMPARE   ZERO,OVRCD
          GOTO      UPACM110 IF EQUAL
          MOVE      ALCSTLIN,ALCSDATA
          CALL      IBACLOCK                     * get current date/time
          UNPACK    CTIMEIS,CHOUR,ANS,CMIN,ANS,CSEC
          PACK      DIM14,CCC,CYY,CMM,CDD,CHOUR,CMIN,CSEC
          REP       " 0",DIM14
          PACK      ALCSUKEY,DIM14,USERID,SP70
          CALL      WRALCST1
          GOTO      UPACM100
.
UPACM999  RETURN
.------------------------------------------------------------
. Clear/Delete Comments of a Particular Type
.------------------------------------------------------------
CLACM000  PACK      KEY22,URNUMBER,ALSCCNTR,ALCSTTYP,SP70
          CALL      RSALCST1
CLACM100  CALL      RKALCST1
          BRANCH    OVRCD,CLACM999
          MATCH     ALCSURNO,URNUMBER
          GOTO      CLACM999 IF NOT EQUAL
          MATCH     ALCSCNTR,ALSCCNTR
          GOTO      CLACM999 IF NOT EQUAL
          MATCH     ALCSTTYP,ALCSTYPE
          GOTO      CLACM999 IF NOT EQUAL
          PACK      KEY22,ALCSURNO,ALCSCNTR,ALCSTYPE,ALCSLINE,SP70
          CALL      DEALCST1
          CALL      RSALCST1
          GOTO      CLACM100
CLACM999  RETURN
.------------------------------------------------------------
. Output Comments to HTMLFILE
.------------------------------------------------------------
ACMHTM00  UNPACK    DIM127,ANS,ALCSTTYP,DIM127
          PACK      KEY22,URNUMBER,ALSCCNTR,ALCSTTYP,SP70
          CALL      RSALCST1
ACMHTM10  CALL      RKALCST1
          BRANCH    OVRCD,ACMHTM99
          MATCH     URNUMBER,ALCSURNO
          GOTO      ACMHTM99 IF NOT EQUAL
          MATCH     ALSCCNTR,ALCSCNTR
          GOTO      ACMHTM99 IF NOT EQUAL
          MATCH     ALCSTTYP,ALCSTYPE
          GOTO      ACMHTM99 IF NOT EQUAL
          STRIP     ALCSDATA
          MOVELPTR  ALCSDATA,LENGTH
.
          COMPARE   LENGTH,ZERO 
          IF        @EQUAL
            MOVE      ONE,LENGTH
          ENDIF
.
          SFORMAT   VAR,LENGTH
          MOVE      ALCSDATA,VAR
          WRITE     HTMLFILE;VAR
          SFORMAT   VAR,127
          GOTO      ACMHTM10
.
ACMHTM99  RETURN
.
