.******************************************************************************
.* System         : Theatre System                                            *
.* Include Name   : OPWEBSTT.PBL                                              *
.* Description    : Alter booking start time for all subsequent bookings      *
.*                  Copy of OPCHGSTT but for the web                          *
.******************************************************************************
.* Date           : 05/11/1990                                                *
.* Author         : Paul Marshall                                             *
.******************************************************************************
.* Parameters     : OPRSESFD - Current session record                         *
.*                  CURSESS - Current session                                *
.*                  CURCASE - Case no. of current booking                    *
.*                  ADDDURA - Adjustment time (minutes)                      *
.*                  OPCNASTD - Update session start time and duration if first*
.*                             case time is changed (Sector 42 Pos.90)        *
.*                  OPCNASLN - Update session start time and end time if first*
.*                             case time is changed (Sector 42 Pos.119)       *
.******************************************************************************
.* Returns        : EXIT = 0      Changes done                                *
.*                  EXIT = 1      Change not done                             *
.******************************************************************************
.* Function       : Will do start time adjustment for the bookings.           *
.*                  This will be validated against the start and end times of *
.*                  the session and the end time of the previous booking. Once*
.*                  it has been validated (the validations will be warnings   *
.*                  only, not errors), the program will update all start times*
.*                  from the current booking by the requested amount (may be  *
.*                  positive or negative ).                                   *
.******************************************************************************
.* Modifications  :                                                           *
.*        V11.03.01 03/03/2023  Ebon Clements     TSK 0909393                 *
.*                  Added hospital to oprsesaf(1-6) and oprdetfa(1&5) indexes *
.*        V10.11.01 09/08/2017  Ebon Clements     TSK 0843373                 *
.*                  Check record exists prior to update of oprsesaf           *
.*        V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                  *
.*                  Added HL7TRGID & HL7INCLD setting for all broadcast       *
.*                  messages                                                  *
.******************************************************************************
.*        V9.10.01  03/09/2008  Davin         CAR 147323                      *
.*                  Send hl7 message for upd bookings (added call DGCLIS14)   *
.*        V9.09.01  28/09/2006  Ebon Clements CAR 151291                      *
.*                  Fixed OPINCTIM - Do not subtract 1 from CASEFROM          *
.*        V9.04.01  22/11/2004  Mike Laming  CAR 55002                        *
.*                  Add a check to both AJSST000 & ASTEN000 to make sure the  *
.*                  Start Time does not cause a duplicate entry               *
.*        V9.04.00  09/11/2004 Lina Vo   CAR 54900                            *
.*                  Copied from OPCHGSTT for use in the web. Changed oprdetaf *
.*                  to use index 5.                                           *
.******************************************************************************
OPCHGSTT  UNPACK    CURSESS,SESNHOSP,SESNDATE,SESNTIME,SESNCODE
          COMPARE   ADDDURA,ZERO
          GOTO      OPCGS900 IF EQUAL       * Zero entered or Enter hit
          GOTO      OPCGS500 IF NOT LESS    * Negative adjustment entered
.
.         A Positive adjustment time has been entered
.
.         check that all adjustments wont go over end of session
.         by calculating the end time of the last booking, KEY5
.
          PACK      KEY26,CURSESS,Z70
          CALL      RSOPDEA5
OPCGS005  CALL      RPOPDEA5                * get the last case 
          BRANCH    OVRCD,OPCGS010          * different session
.
          COMPARE   THREE,OPDASTAT
          GOTO      OPCGS005 IF EQUAL
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN
          MATCH     KEY22,CURSESS
          GOTO      OPCGS010 IF NOT EQUAL   * different session
.
          MOVE      ZERO,EXPDURA            * clear duration to add
          ADD       OPDAEXPD,EXPDURA        * add exp duration
          ADD       OPSEPREP,EXPDURA        * add pat prep time
          ADD       ADDDURA,EXPDURA        * add adjustment
          CALL      CLDUR000                * get KEY5, end time of last book.
.
          MATCH     KEY5,OPSEENDT
          GOTO      OPCGS010 IF NOT LESS    * end time is >= calc time
.
          GOTO      OPCGS015               * recalc times
.
.         calculate adjusted time for the current time
.
OPCGS010  MOVE      ADDDURA,EXPDURA         * time to add
          PACK      KEY26,CURSESS,CURCASE,SP70
          CALL      RSOPDEA5                * get the valid patient details
OPCGS012  CALL      RKOPDEA5                * get the valid patient details
          BRANCH    OVRCD,OPCGS900          * invalid details
.
          COMPARE   THREE,OPDASTAT
          GOTO      OPCGS012 IF EQUAL
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN
          MATCH     KEY22,CURSESS
          GOTO      OPCGS900 IF NOT EQUAL   
.
          CALL      CLDUR000                * get adjusted time, KEY5
.
          MATCH     KEY5,OPSEENDT
          GOTO      OPCGS015 IF NOT LESS    * before end time so OK
.
          GOTO      OPCGS015 
.
.         update all the following bookings for positive adjustment
.
OPCGS015  MOVE      CURCASE,CASEFROM
          MOVE      ADDDURA,EXPDURA
          PACK      KEY22,SESNHOSP,SESNDATE,SESNTIME,SESNCODE
          PROC      CHKST000
          BRANCH    EXIT,OPCGS999
          CALL      OPINCTIM
          CALL      AJSST000                * adjust start times ??
          CALL      ASTEN000                * adjust start times and end time?
          MOVE      ZERO,EXIT
          GOTO      OPCGS999
.
.         negative adjustment
.
OPCGS500  MOVE      ADDDURA,EXPDURA         * the adjustment
          PACK      KEY26,CURSESS,CURCASE,SP10
          CALL      RSOPDEA5                * get the valid patient details
OPCGS550  CALL      RKOPDEA5                * get the valid patient details
          BRANCH    OVRCD,OPCGS900          * invalid details
.
          COMPARE   THREE,OPDASTAT
          GOTO      OPCGS550 IF EQUAL
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN
          MATCH     KEY22,CURSESS
          GOTO      OPCGS900 IF NOT EQUAL   
.
          CALL      CLDUR000                * get adjusted start time
          MOVE      KEY5,EXPSTART           * the work variable
.
          MATCH     "  1",OPDACASE
          GOTO      OPCGS600 IF NOT EQUAL   * not the first case
.
          MATCH     OPSETIME,EXPSTART
          GOTO      OPCGS800 IF NOT LESS    * not before start time
.
          GOTO      OPCGS800 
.
.         in the middle check booking wont overlap with previous booking
.
OPCGS600  CALL      RPOPDEA1
          BRANCH    OVRCD,OPCGS800          * the only booking in the session
.
          COMPARE   THREE,OPDASTAT
          GOTO      OPCGS600 IF EQUAL
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN
          MATCH     KEY22,CURSESS
          GOTO      OPCGS800 IF NOT EQUAL   * different session
.
          MOVE      ZERO,EXPDURA
          ADD       OPDAEXPD,EXPDURA
          ADD       OPSEPREP,EXPDURA
          CALL      CLDUR000                * get KEY5, end time of prev book.
.
          MATCH     KEY5,EXPSTART
          GOTO      OPCGS800 IF NOT LESS    * alterd time >= end time of prev
.
          DISPLAY   *P1:24,*EL,*B,"This will overlap the previous booking.  ";
          CALL      HITENTER
          GOTO      OPCHGSTT
.
.         update all the following bookings for negative adjustment
.
OPCGS800  MOVE      CURCASE,CASEFROM
          MOVE      ADDDURA,EXPDURA
          PACK      KEY22,SESNHOSP,SESNDATE,SESNTIME,SESNCODE
          PROC      CHKST000
          BRANCH    EXIT,OPCGS999
          CALL      OPINCTIM
          CALL      AJSST000                * adjust start times ??
          CALL      ASTEN000                * adjust start times and end time?
          MOVE      ZERO,EXIT
          GOTO      OPCGS999
.
OPCGS900  MOVE      ONE,EXIT
.
OPCGS999  RETURN

.*********************************************************************
.*                  CLDUR000                                         *
.*        Get an end time, given start time and duration             *
.*        Para's  : EXPDURA       the total duration                 *
.*                  OPDAEXPS      the start time                     *
.*        Returns : KEY5          the end time                       *
.*********************************************************************
.
CLDUR000  UNPACK    OPDAEXPS,CHOUR,ANS,CMIN * unpack the start time
          MOVE      CMIN,FORM2              * minutes as a form2
          MOVE      FORM2,FORM4A            * set up work variable
          ADD       EXPDURA,FORM4A          * the total minutes to add
.
CLDUR200  COMPARE   ZERO,FORM4A
          GOTO      CLDUR300 IF NOT LESS    * positve amount
.
          ADD       "60",FORM4A             * make it positive
          MOVE      CHOUR,OPTION
          SUB       ONE,OPTION
          MOVE      OPTION,CHOUR            * sub 1 hour off time
          GOTO      CLDUR200
.
CLDUR300  MOVE      FORM4A,FORM4B           * save the minutes
.
          DIV       "60",FORM4A             * the total hours to add
          MOVE      CHOUR,FORM2             * hours as a form
          ADD       FORM4A,FORM2            * add the hours
.
CLDUR400  COMPARE   "24",FORM2
          GOTO      CLDUR500 IF LESS        * below 24 hours
.
          SUB       "24",FORM2              * get back to 24 hour time
          GOTO      CLDUR400
.
CLDUR500  MOVE      FORM2,CHOUR             * set up the hours
.
          MULT      "60",FORM4A             * the total minutes added
          SUB       FORM4A,FORM4B           * the minutes to add
          MOVE      FORM4B,FORM2            * get minutes as form2
.
          COMPARE   ZERO,FORM2
          GOTO      CLDUR800 IF NOT LESS    * not negative
.
          ADD       "60",FORM2              * make it positive
          MOVE      CHOUR,OPTION
          SUB       ONE,OPTION
          MOVE      OPTION,CHOUR            * sub 1 hour off time
.
CLDUR800  MOVE      FORM2,CMIN              * the minutes
.
          PACK      KEY5,CHOUR,COLON,CMIN * the end time
          REP       " 0",KEY5
.
CLDUR999  RETURN
+
.*********************************************************************
.*                  CHKST000                    Called by : OPCGS000 *
.*        check  the session start time to make sure session does    *
.*        not already exist.                                         *
.*********************************************************************
          DEFPROC   CHKST000
.
          INC       OPRSESFD/INC
.
          MOVE      ZERO,EXIT
          COMPARE   ONE,OPCNASTD
          GOTO      CHKST100 IF EQUAL   * dont do this change
.
          COMPARE   ONE,OPCNASLN
          GOTO      CHKST100 IF EQUAL   * dont do this change
.
          COMPARE   ONE,CURCASE
          GOTO      CHKST100 IF EQUAL   * not the first case
.
          GOTO      CHKST999
.
CHKST100  OPEN      OPRSESA1,"oprsesaf" 
          PACK      KEY22,SESNHOSP,SESNDATE,SESNTIME,SESNCODE,SP20
          CALL      RDOPSES1                * update session file
          BRANCH    OVRCD,CHKST9999
.
          MOVE      OPSETIME,OPDAEXPS       * set original start time
          MOVE      ADDDURA,EXPDURA         * the adjustment time
          CALL      CLDUR000                * get new start time
.          
          PACK      KEY22,SESNHOSP,SESNDATE,KEY5,SESNCODE,SP20
          CALL      RDOPSES1                * update session file
          BRANCH    OVRCD,CHKST900
.
          DISPLAY   *P1:24,*EL,*B,"Session Exists. Patient must be ":
                    "Transferred. ";
          CALL      HITENTER
.
          MOVE      ONE,EXIT
          GOTO      CHKST999
.
CHKST900  PACK      KEY22,SESNHOSP,SESNDATE,SESNTIME,SESNCODE,SP20
          CALL      RDOPSES1                * update session file
          GOTO      CHKST999
.
          INC       OPRSESIO/INC
          INC       IBAOVRIO/INC
.
CHKST999  ENDPROC
.*********************************************************************
.*                  AJSST000                    Called by : OPCGS000 *
.*        Adjust the session start time and duration by the required *
.*        amount.                                                    *
.*********************************************************************
AJSST000  COMPARE   ONE,OPCNASTD
          GOTO      AJSST999 IF NOT EQUAL   * dont do this change
.
          COMPARE   ONE,CURCASE
          GOTO      AJSST999 IF NOT EQUAL   * not the first case
.
.******************************************** Start of Addition        *I-55002
.         check that new start time does not cause duplicate session
          MOVE      SESNTIME,OPDAEXPS       * set original start time
          MOVE      ADDDURA,EXPDURA         * the adjustment time
          CALL      CLDUR000                * get new start time - returns KEY5
.                                                  * check if session exists
          PACK      KEY22,SESNHOSP,SESNDATE,KEY5,SESNCODE  
          CALL      RDOPSES1
          IF        OVRCD = 0
            PACK      KEY22,SESNHOSP,SESNDATE,SESNTIME,SESNCODE,SP20
            CALL      RDOPSES1              * restore session record
            GOTO      AJSST999
          ENDIF
.********************************************   End of Addition        *I-55002
.
.         want to adjust start time and duration by the required amount so that
.         the end time is unchanged - if +ve adjustment sub this from duration
.
          PACK      KEY22,SESNHOSP,SESNDATE,SESNTIME,SESNCODE,SP20
          CALL      RDOPSES1                * update session file
          IF        OVRCD=0
            MOVE      "2",AUDIFLAG            * before change audit
            CALL      AUSES000                * write audit record
            MOVE      OPSETIME,OPDAEXPS       * set original start time
            MOVE      ADDDURA,EXPDURA         * the adjustment time
            CALL      CLDUR000                * get new start time
            MOVE      KEY5,OPSETIME           * set new session start time
            SUB       ADDDURA,OPSEDURA        * set new session duration
            CALL      UPOPSES1                * update session file
            MOVE      "3",AUDIFLAG            * after change audit
            CALL      AUSES000                * write audit record
          ENDIF
.
.         now have to update all the oprdeafd records for that session with
.         the new start time
.
AJSST100  PACK      KEY26,CURSESS,ZERO,SP10
          CALL      RSOPDEA1
          CALL      RKOPDEA1
          BRANCH    OVRCD,AJSST800          * finished for that session
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN
          MATCH     KEY22,CURSESS
          GOTO      AJSST800 IF NOT EQUAL   * different session
.
          MOVE      "2",AUDIFLAG            * before change audit
          CALL      AUDEA000                * write audit record
          MOVE      OPSETIME,OPDATIME       * set new start time
          CALL      IBACLOCK
          PACK      OPDAUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OPDAUDAT
.
          CLOCK     TIME,OPDAUTIM
          MOVE      WBSEUID,OPDAWEBU
.
          CALL      UPOPDEA1                * update file
          MOVE      "3",AUDIFLAG            * before change audit
          CALL      AUDEA000                * write audit record
          MOVE      ONE,HL7TRGID
          MOVE      "OPWEBSTT",HL7INCLD
          PROC      DGCLIS14                * send update booking message
          GOTO      AJSST100
.
.         now update the work variables to contain the new start time
.
AJSST800  PACK      CURSESS,OPSEHOSP,OPSEDATE,OPSETIME,OPSECLIN
          MOVE      OPSETIME,SESNTIME       * set session time
          DISPLAY   *P65:6,*V2LON,SESNTIME
.
AJSST999  RETURN
+
.************************************************************************
.*  ASTEN0000 :  Adjust Start time & End time of session                *
.*               so that the duration remains the same                  *
.************************************************************************
ASTEN000  COMPARE   ONE,OPCNASLN
          GOTO      ASTEN999 IF NOT EQUAL   * dont do this change
.
          COMPARE   ONE,CURCASE
          GOTO      ASTEN999 IF NOT EQUAL   * not the first case
.
.******************************************** Start of Addition        *I-55002
.         check that new start time does not cause duplicate session
          MOVE      SESNTIME,OPDAEXPS       * set original start time
          MOVE      ADDDURA,EXPDURA         * the adjustment time
          CALL      CLDUR000                * get new start time - returns KEY5
                                            * check if session exists
          PACK      KEY22,SESNHOSP,SESNDATE,KEY5,SESNCODE     
          CALL      RDOPSES1
          IF        OVRCD = 0
            PACK      KEY22,SESNHOSP,SESNDATE,SESNTIME,SESNCODE,SP20
            CALL      RDOPSES1              * restore session record
            GOTO      ASTEN999
          ENDIF
.********************************************   End of Addition        *I-55002
.
.         want to adjust start time and end time so the duration remains same  
.
          PACK      KEY22,SESNHOSP,SESNDATE,SESNTIME,SESNCODE,SP20
          CALL      RDOPSES1                * update session file
          IF        OVRCD=0
            MOVE      "2",AUDIFLAG            * before change audit
            CALL      AUSES000                * write audit record
            MOVE      OPSETIME,OPDAEXPS       * set original start time
            MOVE      ADDDURA,EXPDURA         * the adjustment time
            CALL      CLDUR000                * get new start time
            MOVE      KEY5,OPSETIME           * set new session start time
.
            MOVE      OPSEENDT,OPDAEXPS       * set up end time
            MOVE      ADDDURA,EXPDURA         * the adjustment time
            CALL      CLDUR000                * get new end time
            MOVE      KEY5,OPSEENDT           * set new end time
            CALL      UPOPSES1                * update session file
            MOVE      "3",AUDIFLAG            * after change audit
            CALL      AUSES000                * write audit record
          ENDIF
.
.         now have to update all the oprdeafd records for that session with
.         the new start time
.
ASTEN100  PACK      KEY26,CURSESS,ZERO,SP10
          CALL      RSOPDEA1
          CALL      RKOPDEA1
          BRANCH    OVRCD,ASTEN800          * finished for that session
.
          PACK      KEY22,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN
          MATCH     KEY22,CURSESS
          GOTO      ASTEN800 IF NOT EQUAL   * different session
.
          MOVE      "2",AUDIFLAG            * before change audit
          CALL      AUDEA000                * write audit record
          MOVE      OPSETIME,OPDATIME       * set new start time
          CALL      IBACLOCK
          PACK      OPDAUDAT,CCC,CYY,CMM,CDD
          REP       " 0",OPDAUDAT
.
          CLOCK     TIME,OPDAUTIM
          MOVE      WBSEUID,OPDAWEBU
.
          CALL      UPOPDEA1                * update file
          MOVE      "3",AUDIFLAG            * before change audit
          CALL      AUDEA000                * write audit record
          MOVE      TWO,HL7TRGID
          MOVE      "OPWEBSTT",HL7INCLD
          PROC      DGCLIS14                * send update booking message
          GOTO      ASTEN100
.
.         now update the work variables to contain the new start time
.
ASTEN800  PACK      CURSESS,OPSEDATE,OPSETIME,OPSECLIN
          MOVE      OPSETIME,SESNTIME       * set session time
          DISPLAY   *P65:6,*V2LON,SESNTIME,*P73:6,OPSEENDT
.
ASTEN999  RETURN
...............................................................................
.*******************************************************************************
.                                 OPINCTIM
.    Increment start times in a given session starting and case number.
.
.    Function : Loop through all bookings from the current booking to end of
.               the session, and add the increment amount to start times of
.               each booking.
. 
.    Parameters:
.    KEY19    - Must be set to session date, time and clinic/doctor
.    CASEFROM - Starting case number
.    EXPDURA  - Duration to be added
.
.    Return Parameters:
.    None
.*******************************************************************************
OPINCTIM  MOVE      CASEFROM,FORM3          * must decrement case because full
......    SUB       ONE,FORM3                 key
          PACK      KEY26,KEY22,FORM3,SP30
          CALL      RSOPDEA5                * position fptr oprdetaf
.
INCTIM10  CALL      RKOPDEA5
          BRANCH    OVRCD,INCTIM99
.
          PACK      KEY26,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,SP30
          MATCH     KEY22,KEY26           * test record is same session
          GOTO      INCTIM99 IF NOT EQUAL
.
          COMPARE   THREE,OPDASTAT        * test for booked patients only
          GOTO      INCTIM10 IF EQUAL
.
          MOVE      TWO,AUDIFLAG          * before change audit
          CALL      AUDEA000              * write audit
          MOVE      OPDAEXPS,EXPSTART     * set the start time, Duration is set
          CALL      OPADDTIM              * Calculate new start time in EXPSTART
          MOVE      EXPSTART,OPDAEXPS     * move new startime to operation rec.
          PACK      KEY26,OPDAHOSP,OPDADATE,OPDATIME,OPDACLIN,OPDASTAT:
                          OPDACASE,SP30
          CALL      RAOPDEA1
          IF        OVRCD=0
            CALL      IBACLOCK
            PACK      OPDAUDAT,CCC,CYY,CMM,CDD
            REP       " 0",OPDAUDAT
.
            CLOCK     TIME,OPDAUTIM
            MOVE      WBSEUID,OPDAWEBU
.
            CALL      UPOPDEA5            * update operation rec. with new start
            MOVE      THREE,AUDIFLAG      * after change audit
            CALL      AUDEA000            * write audit
            MOVE      THREE,HL7TRGID
            MOVE      "OPWEBSTT",HL7INCLD
            PROC      DGCLIS14            * send update booking message
          ENDIF
          GOTO      INCTIM10
.
INCTIM99  RETURN
+
