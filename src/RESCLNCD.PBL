.------------------------------------------------------------------
. Create Result Message Linked to Correspondence Document 
.  Inputs
.      RESLNK01 - Result Date
.      RESLNK02 - Result Time
.      RESLNK03 - Result Diagnostic Service 
.      RESLNK04 - Result Status Code         (Final/Premlin)
.      RESLNK05 - Result Abnormal Code
.      URNUMBER - U/R
.      ADMISSNO - Visit No
.
.  01/04/2022  Jill Parkinson Task 0918603
.              Changed reha length from 387 to 418 and rehd from 155 to 398
.  11/03/2022  Alina Bhari  TSK 0913264
.              Laboratory record field (RESLNK08) written to clinical Results Header
.              and Clinical Results Linking Table while adding result -HEAVAR00
.  10/02/2004  Peter Vela CAR 38290
.              Added functionality for opening yearly RESLNKFD files
.              (reln1999, reln2000, reln2001, reln2002, etc...)
.  18/12/2002  Jill Habasque SRF 34657
.              Added new link types 07 and 08
.------------------------------------------------------------------
NEWRES00  CALL      IBACLOCK
          MOVE      RESLNK01,RESULTYR
          CALL      RESOPN00                     * Open result tables
          CALL      WRHEAD00
          CALL      WRLNKS00
          CALL      UUREHA1
.
NEWRES99  RETURN
.------------------------------------------------------------
. Write header record
.----------------------------------------------------------------------
WRHEAD00  PACK      KEY17,RESLNK01,RESLNK02,Z70
          MOVE      ZERO,UNIQUEID
          CALL      RSREHA1
          CALL      RPREHA1
          BRANCH    OVRCD,WRHEAD10
          MATCH     REHARDT,RESLNK01
          GOTO      WRHEAD10 IF NOT EQUAL
          MATCH     REHARTM,RESLNK02
          GOTO      WRHEAD10 IF NOT EQUAL
          MOVE      REHARUN,UNIQUEID
.
WRHEAD10  MOVE      RESLNK01,REHARDT
          MOVE      RESLNK02,REHARTM
          MOVE      ONE,REHAFUR
          MOVE      URNUMBER,REHAPID
          MOVE      ADMISSNO,REHAVIS
.
          CALL      HEAVAR00                * Set up header values
.
WRHEAD70  ADD       ONE,UNIQUEID
          MOVE      UNIQUEID,REHARUN
          PACK      KEY17,REHARDT,REHARTM,REHARUN
          CALL      RAREHA1
          BRANCH    OVRCD,WRHEAD80
          GOTO      WRHEAD70
.
WRHEAD80  CALL      WRREHA1
          CALL      RLREHA1
WRHEAD99  RETURN
.------------------------------------------------------------
. Set up header variable values
.------------------------------------------------------------
HEAVAR00  MOVE      "CLIWEB08",REHASID
          MOVE      "UNICARE",REHAPAP
          MOVE      RESLNK01,REHAMDT
          MOVE      RESLNK02,REHAMTM
          MOVE      RESLNK03,REHADSS
          MOVE      RESLNK04,REHARST
          MOVE      RESLNK05,REHANOR  
          MOVE      RESLNK06,REHAOSN
          MOVE      RESLNK07,REHAFOR
          MOVE      SP70,REHAMID
          MOVE      SP70,REHAPOR
          MOVE      SP70,REHAFAP
          MOVE      SP70,REHAUSC
          MOVE      SP70,REHAUCS  * Coding System (Local for Demo)
          MOVE      SP8,REHACDT
          MOVE      SP5,REHACTM
          MOVE      SP70,REHACID
          MOVE      SP70,REHASDT
          MOVE      SP70,REHASTM
          MOVE      SP70,REHAOID
          MOVE      SP70,REHAOGN
          MOVE      SP70,REHARRD
          MOVE      SP70,REHARRT
          MOVE      UNIQUEID,REHARUN
          MOVE      RESLNK01,REHARED
          MOVE      RESLNK02,REHARET
          MOVE      SP70,REHAIBY
          MOVE      SP70,REHAADC
          MOVE      RESLNK08,REHALAB
          MOVE      SP70,REHALOC
          MOVE      SP70,REHARDE
.
HEAVAR99  RETURN
.----------------------------------------------------------------------
.  Write results link record
.----------------------------------------------------------------------
WRLNKS00  MOVE      "01",LINKTYPE
          PACK      LINKKEYS,REHAPID,SP70
          CALL      NEWLNK00
.
          MOVE      "05",LINKTYPE
          PACK      LINKKEYS,PMPXRHC1,SP70
          CALL      NEWLNK00
.
          MATCH     SP70,ADMISSNO
          GOTO      WRLNKS99 IF EQUAL
.
          MOVE      ADMISSNO,KEY8
          CALL      RDVISA1            * Check for Visit No 
          BRANCH    OVRCD,WRLNKS99     * Not Valid
.
          MOVE      "02",LINKTYPE
          PACK      LINKKEYS,PVIBILL,SP70
          CALL      NEWLNK00
.
          BRANCH    PVITYPE,WRLNKS10,WRLNKS20,WRLNKS30
.
WRLNKS10  PACK      KEY8,PVIBILL
.          CALL      RDDETA1
.          BRANCH    OVRCD,WRLNKS96
..
.          MOVE      "03",LINKTYPE
.          PACK      LINKKEYS,ADADOCT,SP70
.          CALL      UPDADC00
.          CALL      NEWLNK00
..
.          MOVE      "05",LINKTYPE
.          PACK      LINKKEYS,AEDARFGP,SP70
.          CALL      NEWLNK00
..
.          MOVE      "03",LINKTYPE
.          PACK      LINKKEYS,AEDARDOC,SP70
.          CALL      NEWLNK00
          GOTO      WRLNKS99
.
WRLNKS20  MOVE      PVISITE,KEY6
.          CALL      RDSITA1
.          BRANCH    OVRCD,WRLNKS95
..
.          MOVE      OSTFILE,CFILEPRE
.          PACK      CFNAME,CFILEPRE,FILBB1A1
.          CALL      OPOTBB11
..
.          PACK      KEY8,PVIBILL
.          CALL      RDBOKB1
.          BRANCH    OVRCD,WRLNKS95
..
.          MOVE      "03",LINKTYPE
.          PACK      LINKKEYS,OBADOCT,SP70
.          CALL      UPDADC00
.          CALL      NEWLNK00
..
.          MOVE      "05",LINKTYPE
.          PACK      LINKKEYS,OTBBRFGP,SP70
.          CALL      NEWLNK00
..
.          MOVE      "06",LINKTYPE
.          PACK      LINKKEYS,PVIDOCT,SP70   * Clinic / doctor
.          CALL      NEWLNK00
.
          GOTO      WRLNKS99
.
WRLNKS30  PACK      KEY8,PVIBILL
          CALL      RDMISS1
          BRANCH    OVRCD,WRLNKS99          * No record
.
          MOVE      "04",LINKTYPE           * Ward link
          PACK      LINKKEYS,AWARD,SP70
          CALL      NEWLNK00
.
          MOVE      "03",LINKTYPE           * Attending doctor link
          PACK      LINKKEYS,ADOCTA,SP70
          CALL      UPDADC00
          CALL      NEWLNK00
.
          MOVE      "07",LINKTYPE           * Attending doctor only link
          PACK      LINKKEYS,ADOCTA,SP70
          CALL      NEWLNK00
.
          MOVE      "03",LINKTYPE           * Treating doctor link
          PACK      LINKKEYS,ADOCTT,SP70
          CALL      NEWLNK00
.
          MOVE      "08",LINKTYPE           * Referring Doctor only link
          PACK      LINKKEYS,ADOCTR,SP70
          CALL      NEWLNK00
.
          MOVE      "03",LINKTYPE           * Referring Doctor link
          PACK      LINKKEYS,ADOCTR,SP70
          CALL      NEWLNK00
          GOTO      WRLNKS99
.
WRLNKS99  RETURN
.------------------------------------------------------------
. Update Attending Doctor in Header Record
.------------------------------------------------------------
UPDADC00  PACK      KEY17,REHARDT,REHARTM,REHARUN
          CALL      RDREHA1
          BRANCH    OVRCD,UPDADC99
          MOVE      LINKKEYS,REHAADC
          CALL      UPREHA1
UPDADC99  RETURN
.------------------------------------------------------------
. Write New link Record
.----------------------------------------------------------------------
NEWLNK00  PACK      KEY29,LINKTYPE,LINKKEYS,REHARDT,REHARTM,REHARUN,SP70
          UNPACK    KEY29,RELNLTY,RELNLKY,RELNRDT,RELNRTM,RELNRUN
.
          PACK      RESLNKFL,PRELEN,RELNRDT           * I CAR 38290
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESLNKA1,RESLNKFL
          TRAPCLR   IO
.
          COMPARE   ONE,OVRCD
          GOTO      NEWLNK99 IF EQUAL                 * end I CAR 38290
.
          CALL      RDRELN1
          IF        OVRCD=1
            MOVE      ZERO,RELNMSN            * Marked as seen, 0=no.
            MOVE      SP70,RELNSDT
            MOVE      SP70,RELNSTM
            MOVE      SP70,RELNSPC
            MOVE      REHAFUR,RELNFUR
            MOVE      REHAPID,RELNPID
            MOVE      REHAUCS,RELNUCS         * coding scheme
            MOVE      REHAUSC,RELNUSC         * code
            MOVE      REHADSS,RELNDSS
            MOVE      REHANOR,RELNNOR
            MOVE      REHARST,RELNRST
            MOVE      REHALAB,RELNLAB
            MOVE      TWO,RELNRTY    * 2=Correspondance Document
            CALL      WRRELN1
          ENDIF
.
          MATCH     "01",LINKTYPE
          GOTO      NEWLNK99 IF NOT EQUAL
.
          MOVE      LINKKEYS,KEY8
          PACK      KEY25,KEY8,REHARDT,REHARTM,REHARUN,SP70
          UNPACK    KEY25,RELUURN,RELURDT,RELURTM,RELURUN
          CALL      RDRELU1
          COMPARE   ONE,OVRCD
          GOTO      NEWLNK99 IF NOT EQUAL
          MOVE      ZERO,RELUMSN            * Marked as seen, 0=no.
          MOVE      SP70,RELUSDT
          MOVE      SP70,RELUSTM
          MOVE      SP70,RELUSPC
          MOVE      REHAFUR,RELUFUR
          MOVE      REHAPID,RELUPID
          MOVE      REHAUCS,RELUUCS         * coding scheme
          MOVE      REHAUSC,RELUUSC         * code
          MOVE      REHADSS,RELUDSS
          MOVE      REHANOR,RELUNOR
          MOVE      REHARST,RELURST
          MOVE      REHALAB,RELULAB
          MOVE      TWO,RELURTY            * 2=Correspondance Document
          CALL      WRRELU1
          MOVE      RELUURN,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,NEWLNK99
          MATCH     "1",PTMXSIN2
          GOTO      NEWLNK99 IF EQUAL
.
          MOVE      RELUURN,KEY8
          CALL      RLPTMAS1
          BRANCH    OVRCD,NEWLNK99,NEWLNK99
          MATCH     "1",PTMXSIN2
          IF        !@EQUAL
            MOVE      "1",PTMXSIN2
            CALL      UPMAST1
            CALL      UUPTMAS1
          ENDIF
.
NEWLNK99  RETURN
.------------------------------------------------------------
. Open result tables
.------------------------------------------------------------
RESOPN00  CLOSE     RESHEAA1
          CLOSE     RESHEAA2
.
          PACK      FILENAME,PREHEA,RESULTYR
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      RESHEAA1,FILENAME
          TRAPCLR   IO
          BRANCH    OVRCD,RESOPN90
.
          PACK      FILENAME,PREHEA,RESULTYR
          OPEN      RESHEAA2,FILENAME
.
          MOVE      RESULTYR,YEAROPEN
          MOVE      ZERO,EXIT
          GOTO      RESOPN99
.
RESOPN90  CALL      MAKRES00
          GOTO      RESOPN00
.
RESOPN99  RETURN
.
.------------------------------------------------------------
.  Create new files
.------------------------------------------------------------------------------
MAKRES00  MOVE      ZERO,TASKID
          CLEAR     CMNDLINE
          PACK      FILENAME,PREHEA,RESULTYR
          APPEND    "isbuild ",CMNDLINE
          APPEND    FILENAME,CMNDLINE
          APPEND    " 418 U1-8,9-13,14-17 ",CMNDLINE
          APPEND    "U112-117,118-137,1-8,9-13,14-17 ",CMNDLINE
          APPEND    "U19-34,150-161,138-149,1-8,9-13,14-17",CMNDLINE
          RESET     CMNDLINE
          EXECUTE   CMNDLINE,TASKID
          MATCH     "0",TASKID
          GOTO      MAKRES90 IF NOT EQUAL
.
          CLEAR     CMNDLINE
          PACK      FILENAME,PREHEB,RESULTYR
          APPEND    "isbuild ",CMNDLINE
          APPEND    FILENAME,CMNDLINE
          APPEND    " 158 U1-8,9-13,14-17,18-20",CMNDLINE
          RESET     CMNDLINE
          EXECUTE   CMNDLINE,TASKID
          MATCH     "0",TASKID
          GOTO      MAKRES90 IF NOT EQUAL
.
          CLEAR     CMNDLINE
          PACK      FILENAME,PREHEC,RESULTYR
          APPEND    "isbuild ",CMNDLINE
          APPEND    FILENAME,CMNDLINE
          APPEND    " 157 U1-8,9-13,14-17,18-19",CMNDLINE
          RESET     CMNDLINE
          EXECUTE   CMNDLINE,TASKID
          MATCH     "0",TASKID
          GOTO      MAKRES90 IF NOT EQUAL
.
          CLEAR     CMNDLINE
          PACK      FILENAME,PREDEA,RESULTYR
          APPEND    "isbuild ",CMNDLINE
          APPEND    FILENAME,CMNDLINE
          APPEND    " 365 U1-8,9-13,14-17,18-21 ",CMNDLINE
          APPEND    " U22-37,52-63,40-51,1-8,9-13,14-17,18-21",CMNDLINE
          RESET     CMNDLINE
          EXECUTE   CMNDLINE,TASKID
          MATCH     "0",TASKID
          GOTO      MAKRES90 IF NOT EQUAL
.
          CLEAR     CMNDLINE
          PACK      FILENAME,PREDEB,RESULTYR
          APPEND    "isbuild ",CMNDLINE
          APPEND    FILENAME,CMNDLINE
          APPEND    " 162 U1-8,9-13,14-17,18-21,22-24",CMNDLINE
          RESET     CMNDLINE
          EXECUTE   CMNDLINE,TASKID
          MATCH     "0",TASKID
          GOTO      MAKRES90 IF NOT EQUAL
.
          CLEAR     CMNDLINE
          PACK      FILENAME,PREDEC,RESULTYR
          APPEND    "isbuild ",CMNDLINE
          APPEND    FILENAME,CMNDLINE
          APPEND    " 162 U1-8,9-13,14-17,18-21,22-24",CMNDLINE
          RESET     CMNDLINE
          EXECUTE   CMNDLINE,TASKID
          MATCH     "0",TASKID
          GOTO      MAKRES90 IF NOT EQUAL
.
          CLEAR     CMNDLINE
          PACK      FILENAME,PREHED,RESULTYR
          APPEND    "isbuild ",CMNDLINE
          APPEND    FILENAME,CMNDLINE
          APPEND    " 398 U1-8,9-13,14-17",CMNDLINE
          RESET     CMNDLINE
          EXECUTE   CMNDLINE,TASKID
          MATCH     "0",TASKID
          GOTO      MAKRES90 IF NOT EQUAL
          GOTO      MAKRES99
.
MAKRES90  DISPLAY   *P5:6,"Unix Error - ",TASKID
          MOVE      ONE,EXIT
.
MAKRES99  RETURN
.
