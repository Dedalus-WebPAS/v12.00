.------------------------------------------------------------
. Include   : STDHLPCD.PBL
.
. Routine   : GSCRN000
.
. Function  : Initialisation for Help Screen Display.
.              - Save Screen
.              - Display Box Details
.
. Called by : All Computer Generated Help Screens
. 24.07.96 B.G.Ackland
.          Shift Suspend for Verion 7 of TBL
.------------------------------------------------------------
GSCRN000  
          MOVE      ZERO,EXIT
          MOVE      ONE,CSEARCH
.
          CALL      GETSCR00
          BRANCH    EXIT,GSCRN999
.
          MOVE      HTOP,CTOP
          MOVE      HLEF,CLEF
          MOVE      HBOT,CBOT
          MOVE      HRIG,CRIG
.
          ADD       TWO,CTOP       * Cxxx Clip Box Size
          ADD       ONE,CLEF
          SUB       THREE,CBOT
          SUB       ONE,CRIG
          MOVE      HBOT,MAXITEM
          SUB       HTOP,MAXITEM
          SUB       "4",MAXITEM
.
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          SUSPEND
.
          ADD       ONE,CBOT
          DISPLAY   *PHRIG:CBOT,*G34,*PHLEF:CBOT,*G33;
          HLINE     *G37,CBOT,CLEF,CRIG
          ADD       ONE,CBOT
.
          CLIP      1,1,CRIG,24    * Start Inside of Box Displays
.
          DISPLAY   *PCLEF:CBOT,"e",*V2LON,"X",*HOFF,"it, ex":
                                    *V2LON,"P",*HOFF,"and, ":
                                    *V2LON,"U",*HOFF,"p, ":
                                    *V2LON,"D",*HOFF,"own, ":
                                    *V2LON,"S",*HOFF,"earch, ":
                                    *V2LON,"H",*HOFF,"ome, ":
                                    *V2LON,"E",*HOFF,"nd, ":
                                    *V2LON,"R",*HOFF,"educe";
.exit, expand, up, down, search, home, end, reduce, view, more
          SUB       TWO,CBOT
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
          SUSPEND
.
          MOVE      HTOP,MINITEM
          ADD       ONE,MINITEM
          MOVE      MINITEM,STDROW
.
          MOVE      HLEF,STDCOL00
          ADD       ONE,STDCOL00
          DISPLAY   *PSTDCOL00:STDROW,*V2LON,*ULON,"Item"
.
GSCRN999  RETURN
.
.------------------------------------------------------------
. Routine     : HSCRI000
.
. Function    : Select Option Function for Help Screens
.
. Called by   : All Computer Generated Help Screens
.
. Exit Values : 0 = Item Selected      
.               1 = Page Up
.               2 = Page Down            
.               3 = Home
.               4 = End      
.               5 = Display Screen from start
.               6 = Exit
.               7 = Search
.------------------------------------------------------------
HSCRI000  MOVE      ZERO,CCITEM
          MOVE      SP2,CCITEMD
.
          KEYIN     *PCLEF:CBOT,*NOEDIT,*+,*EOFF,ANS
          GOTO      HSCRI300 IF PAGEDOWN
          GOTO      HSCRI350 IF PAGEUP
          GOTO      HSCRI400 IF HOME
          GOTO      HSCRI450 IF END
          GOTO      HSCRI500 IF RIGHT
          GOTO      HSCRI550 IF LEFT 
          GOTO      HSCRI600 IF UP
          GOTO      HSCRI650 IF DOWN
          RESET     ANS
          GOTO      HSCRI250 IF EOS
          TYPE      ANS
          GOTO      HSCRI220 IF NOT EQUAL
          MOVE      ANS,CCITEM
.
          MOVE      XITEMNO,F3
          DIV       TEN,F3
          COMPARE   CCITEM,F3          * Less than ten items on screen 
          GOTO      HSCRI210 IF LESS   * so no need to enter second character
.
          KEYIN     *PCLEF:CBOT,*NOEDIT,*+,*EOFF,ANS
          RESET     ANS
          GOTO      HSCRI210 IF EOS
          MULT      TEN,CCITEM
          MOVE      ANS,F1
          ADD       F1,CCITEM
.
HSCRI210  
          COMPARE   ONE,CCITEM
          GOTO      HSCRI900 IF LESS       * Invalid Entry
          COMPARE   CCITEM,XITEMNO
          GOTO      HSCRI900 IF LESS       * Invalid Entry
          MOVE      ZERO,EXIT
          GOTO      HSCRI999               * Item Selected
.
HSCRI220 
          REP       UPPLOW,ANS
          MATCH     SP70,ANS           * Next Item
          GOTO      HSCRI650 IF EQUAL  
          MATCH     "X",ANS             * Exit
          GOTO      HSCRI910 IF EQUAL
          MATCH     "D",ANS             * Page Down
          GOTO      HSCRI300 IF EQUAL
          MATCH     "U",ANS             * Page Up
          GOTO      HSCRI350 IF EQUAL
          MATCH     "H",ANS             * Home
          GOTO      HSCRI400 IF EQUAL
          MATCH     "E",ANS             * End
          GOTO      HSCRI450 IF EQUAL
          MATCH     "S",ANS             * Search
          GOTO      HSCRI480 IF EQUAL
          MATCH     "P",ANS             * Left  (Expand Display)
          GOTO      HSCRI500 IF EQUAL
          MATCH     "R",ANS             * Right (Reduce Display
          GOTO      HSCRI550 IF EQUAL
          GOTO      HSCRI900               * Invalid Entry
.
HSCRI250  
          MOVE      DITEM,CCITEM
          COMPARE   CCITEM,XITEMNO
          GOTO      HSCRI900 IF LESS       * Invalid Entry
          MOVE      ZERO,EXIT
          GOTO      HSCRI999               * Item Selected
.         
. Page Down
.
HSCRI300  MOVE      ONE,EXIT
          GOTO      HSCRI999
.
. Page Up
.
HSCRI350  MOVE      TWO,EXIT
          GOTO      HSCRI999
.
. Home
.
HSCRI400  MOVE      THREE,EXIT
          GOTO      HSCRI999
.
. End
.
HSCRI450  MOVE      FOUR,EXIT
          GOTO      HSCRI999
.
. Search
.
HSCRI480  MOVE      SEVEN,EXIT
          GOTO      HSCRI999
.
. Expand Display
.
HSCRI500  BRANCH    CSCRSIZ,HSCRI900
          MOVE      ONE,CSCRSIZ
          SUSPEND
          CALL      PUTSCR00
          MOVE      HTOP,STOP     * Save Screen Size
          MOVE      HLEF,SLEF
          MOVE      HBOT,SBOT
          MOVE      HRIG,SRIG
          MOVE      "3",HTOP      * Full Screen Display Size
          MOVE      "1",HLEF
          MOVE      "24",HBOT
          MOVE      "80",HRIG
          MOVE      FIVE,EXIT     * Display Help Screen from Start
          GOTO      HSCRI999
.
.
. Reduce Display
.
HSCRI550  COMPARE   ZERO,CSCRSIZ
          GOTO      HSCRI900 IF EQUAL
          MOVE      ZERO,CSCRSIZ
          CALL      PUTSCR00
          MOVE      STOP,HTOP     * Restore Saved Screen Size
          MOVE      SLEF,HLEF
          MOVE      SBOT,HBOT
          MOVE      SRIG,HRIG
          MOVE      FIVE,EXIT     * Display Help Screen from Start
          GOTO      HSCRI999
.
. Up Arrow. Highlight Last Item
.
HSCRI600  DISPLAY   *PSTDCOL00:DROW,*V2LON,SP1,DITEM,DOT
          COMPARE   DITEM,ONE
          GOTO      HSCRI610 IF NOT EQUAL
          COMPARE   XITEMNO,ZERO
          GOTO      HSCRI620 IF EQUAL
          MOVE      XITEMNO,DITEM
          MOVE      CTOP,DROW
          ADD       XITEMNO,DROW  
          ADD       ONE,DITEM
.
HSCRI610  SUB       ONE,DITEM
          SUB       ONE,DROW
HSCRI620  DISPLAY   *PSTDCOL00:DROW,*HON,*V2LON,SP1,DITEM,DOT
          GOTO      HSCRI000
.
. Down Arrow. Highlight Next Item
.
HSCRI650  DISPLAY   *PSTDCOL00:DROW,*V2LON,SP1,DITEM,DOT
          COMPARE   DITEM,XITEMNO
          GOTO      HSCRI660 IF NOT EQUAL
          MOVE      ZERO,DITEM
          MOVE      CTOP,DROW
          SUB       ONE,DROW
.
HSCRI660  COMPARE   XITEMNO,ZERO
          GOTO      HSCRI670 IF EQUAL
          ADD       ONE,DITEM
          ADD       ONE,DROW
HSCRI670  DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
          GOTO      HSCRI000
.
HSCRI900  BEEP
          GOTO      HSCRI000
.
HSCRI910  MOVE      SIX,EXIT
.
HSCRI999  RETURN
+
.

