.******************************************************************************
.*  Include   :  DGADTRET.PBL                                                 *
.*                                                                            *
.*  Function  :  This routine will RETURN A PATIENT FROM LEAVE due to         *
.*               a DataGate request.                                          *
.*                                                                            *
.*  Author    :  ROD   (19/10/95)                                             *
.*                                                                            *
.*  Mods      :                                                               *
.*        V12.00.01 15.05.2025  DA SArkies        TSK 0955096                 *
.*                  Updated for Alpha-Numeric Visit Number                    *
.******************************************************************************
.*        V10.04.01 28/04/2014  Steve Armstrong   CAR 261630                  *
.*                  Removed references to PATAULFD (No longer used)           *
.******************************************************************************
.*        V10.01.02 02/02/2011  Steve Armstrong   CAR 237520                  *
.*                  Mods to remove Detente interface (PATDETNT)               *
.*        V10.01.01 01/12/2010  Peter Vela        CAR 233148                  *
.*                  Initialised pattranf variables to spaces before write     *
.******************************************************************************
.*        V9.04.01  05/10/2004 J.Tan  CAR 53798                               *
.*                  Removed PATCHBF                                           *
.*        V9.02.01  20.11.2001  B.G.Ackland                                   *
.*                  Remove pi's from Program                                  *
.*        V5.08.01  21/08/2000  Jill Habasque                                 *
.*                  Added variables for patdetnt                              *
.*        V5.08.B02 10/04/2000  Greg Horvat                                   *
.*                  Removed MATADAFD MATADAIO MATADBFD & MATADBIO             *
.*        V5.08.B01 25/02/2000  Greg Horvat                                   *
.*                  Removed PATADJFD PATADJIO PATMMRFD PATMMRIO & PATPRADJ    *
.******************************************************************************
.*           V5.07.B02 17/11/1998  Jim Stathopoulos                       *
.*                     Removed the HCS Admission and Discharge            *
.*                     Data Audit Files                                   *
.*            V5.07.00 22/10/1998  Davin                                  *
.*                     Mods for 507                                       *
.*            V5.04.01 17/04/1997  Steve Armstrong                        *
.*                     Mods to use inbound & outbound comms client        *
.*                     (DGATEIN & DGATEOUT).                              *
.*                                                                        *
.**************************************************************************
          DEFPROC   DGADTRET
 IFGE     $$VER,7
.
          INC       IBAPASFD/INC
          INC       MEHDL1FD/INC            * Discharge to leave file
          INC       MEHLEGFD/INC
          INC       MEHLERFD/INC
          INC       MEHREVFD/INC
          INC       OUTCLIFD/INC
          INC       PATALWFD/INC
          INC       PATHSPFD/INC
          INC       PATMRGFD/INC
          INC       PATNOBFD/INC
          INC       PATNUMFD/INC
          INC       PATONLFD/INC
          INC       PATRCAUD/INC
.
ADDESC    DIM       17
ADIAG     DIM       50
AFTDECP   FORM      2  * After Decimal Place
AFTDECPF  FORM      2  * After Decimal Place Format String
ATT       DIM       80        * attribute string
AUDIFLAG  FORM      1                       * audits
BEFDECP   FORM      2  * Before Decimal Place
BEFDECPF  FORM      2  * Before Decimal Place Format String
BFAMT     FORM      4.2
BFCOL     FORM      1
BFEND1    FORM      3
BFEND2    FORM      3
BFEND3    FORM      3
BFEND4    FORM      3
BFEND5    FORM      3
BFEND6    FORM      3
BJDAY     FORM      3
CALDAYS   FORM      3
CALLPOSN  FORM      1
CDYSDAYS  FORM      6                       * number of days calculated
CDYSFDTE  DIM       8                       * From Date
CDYSTDTE  DIM       8                       * To Date
CDYSYEAR  FORM      2                       * Work Year
CHG       FORM      1
CHG1      FORM      1
CJDAY     FORM      3
CMDLINE   DIM       35
CODE      DIM       2
CODEA     INIT      "A "
CODEBT    INIT      "BT"
CODECC    INIT      "CC"
CODECL    INIT      "CL"
CODED     INIT      "D "
CODELE    INIT      "LE"                    * Leave end code category (	LE)
CODELTYP  DIM       3                       * Type of Leave code
CODEU2    INIT      "U2"
COL1      FORM      2
COL2      FORM      2
COL3      FORM      2
COUNTER   FORM      2
CUDATE    DIM       6                       * current date yymmdd
CURRDATE  DIM       8                       * current date ccyymmdd
DANS1     DIM       20
DANS2     DIM       20
DANS3     DIM       20
DANS4     DIM       20
DANS5     DIM       4
DANS6     DIM       11
DECIMALP  INIT      "."
DEFLAG    FORM      1
DETCADES  DIM       20
DETCODES  DIM       20
DETNTREC  FORM      2
DIM14     DIM       14
DIM14A    DIM       14
DIM15     DIM       15
DIM19A    DIM       19
DIM3      DIM       3
DIM4      DIM       4
DIM5      DIM       5                       * HR:MM 
DIM6      DIM       6
DIM8      DIM       8
DIM80     DIM       80
DISPPEND  FORM      1
DOCCODE   DIM       6
DOCDISP   DIM       8
DOCNAME   DIM       12
DPSTAT    DIM       31
DSEX      DIM       6
DSMOK3    DIM       3
EOF       FORM      "-3"
FIELD     FORM      2
FNAMEB    DIM       8
FNAMEM    DIM       8
FORM21    FORM      21
FORM3     FORM      3
FORM5     FORM      5
FORM62    FORM      6.2
FORMAT21  FORM      21
FROMDATE  DIM       8
INWARD    DIM       3
JYEAR     FORM      2
KEYCOL    DIM       1
LALLW     FORM      6.2
LATYPE    DIM       3
LBED      DIM       3
LCHSTAT   DIM       3
LDATE     DIM       8                       * Last Transfer Date
LDISC     FORM      6.2
LEAVTYPE  FORM      1               * leave type indicator T=1, H=2, A=3, " "=4
LEFTBRAK  INIT      "("
LLCNT     FORM      5
LMOVE     DIM       1                       * Last Transfer Move
LOOPTRN   FORM      1                       * 0 = Enquiry 1=Print
LRBEND    FORM      3
LRBTYP    DIM       3
LTAMTF    FORM      6.2
LTAMTH    FORM      6.2
LTIME     DIM       8                       * Last Transfer Time
LWARD     DIM       3
MAXNUM    FORM      1
MODE      DIM       1
MTINDC    FORM      1                       * MTINDC=1  ... Empty Beds Only
NADMNO    DIM       8
NALLOW    DIM       2
NBED      DIM       3
NBRDAYS   FORM      4
NDATE     DIM       8
NDISC     FORM      3.2
NETTOT    FORM      6.2
NEWBED    DIM       3
NEWWARD   DIM       3
NLWAMT    FORM      6.2
NMPNUMB   DIM       20
NOFEE     FORM      "0"
NTAMTF    FORM      6.2
NTAMTG    FORM      6.2
NTAMTH    FORM      6.2
NTIME     DIM       8
NUMBER    FORM      3
NWARD     DIM       3
OLDBED    DIM       3
OLDWARD   DIM       3
ON        INIT      " on "
ONAME     DIM       30
OPERCODE  DIM       4
PIPE      INIT      "|"
PNAME     DIM       20
PRINTVAR  DIM       80
PRTITCOD  DIM       9
PTPSEDAT  DIM       8
RANGE     FORM      1
RECTYPEA  INIT      "A"
RETDATE8  DIM       8                       * return date formatted
SAVADMNO  DIM       8                       * save Admission Key before scan
SAVBTYP   DIM       3
SAVCOL    FORM      1
SAVEND    FORM      3
SAVHF     FORM      4.2
SAVKEY13  DIM       13
SAVKEY28  DIM       28
SAVPAT    FORM      4.2
SAVREVW   DIM       3
SAVTOT    FORM      4.2
SAVURNO   DIM       8                       * save Master Key before scan
SEC1      FORM      "00001"
SKEY28    DIM       28
SKEY6     DIM       6
SORL      DIM       1
SP70      INIT      "                                               ":
                    "                      " 
SVDATE    DIM       8
SVTIME    DIM       5
THCFLG    FORM      2
TIMEIS    DIM       8
TMPDTCAT  DIM       2
TMPDTCOD  DIM       3
TMPDTACT  DIM       1                    * detente temporary action
TMPDVIST  DIM       1                    * detente temporary visit type
TMPDVISN  DIM       8                    * detente temporary visit number
TMPDDATE  DIM       8                    * detente temporary visit date
TMPKEY    DIM       8[16]
TODATE    DIM       8
TOTAL     FORM      4.2
TOTDISC   FORM      6.2
TTIME1    DIM       8                       * Transfer In Time
TTIME2    DIM       8                       *          Out Time
VAR       DIM       80
VERT1     FORM      2
VERT2     FORM      2
VERT3     FORM      2
WARDESC   DIM       30                      * Ward description
WDATE     DIM       8
WDATE1    DIM       8
WDATE2    DIM       8
WGLINE    DIM       35
WRDESC    DIM       30
WREXTN    DIM       4
WSOPT     FORM      1
WSWARD    DIM       3
Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"
.
+
.***********************************************************************
.*  DGART000  :  process a Return from Leave datagate message          *
.************************************************************************
DGART000  CALL      RTINI000                      * initialisation/open files
.
          READ      DGATEOUT;NADMNO,NWARD,NBED,NDATE,TTIME2,NEWATDOC,NEWDPART:
                          MHLVEEND,NEWBFTYP,NEWBFAMT,NEWBFDAM,NEWBFAAM
.
          CALL      VALID000                      * validate & get extra info
          BRANCH    EXIT,DGART900                 * error
.
          CALL      RTMAN000                      * if MH pat. MHLVEEND <> blank
          BRANCH    EXIT,DGART900                 * error, MHLVEEND is blank
.
          CALL      RTHMS000                      * subtract data for MHMS adjus
          CALL      RTTRN000                      * write transfer record
          CALL      RTATD000                      * Write att. doc/dept changes
          CALL      RTNUM000                      * update Ward Usage file
          CALL      RTRCA000                      * write rate change audit
          CALL      RTWRD000                      * fix up ward/bed files
          CALL      RTOLV000                      * delete on leave record
          CALL      RTMEH000                      * fix Mental Health stuff
          CALL      RTMIS000                      * update admission file
          CALL      RTCON000                      * update control file
.
. send reply message as successful
.     NB.  SP1 at end of reply has been added because the interpreter does
.     not place a "|" after the last field, and Datagate requires this.  By
.     putting an extra field on the end (which is blank), a "|" will then
.     be added after the last field.
.
          MOVE      "00000",RPLYCODE
          MOVE      SP50,RPLYDESC
          PACK      CCENCODE,CCDATE,CCDST,CCSRC,CCNAME,REPLYRPL
          WRITE     DGATEIN;CCENCODE:
                            RPLYHEAD,RPLYCODE,RPLYDESC:
                            DGMISC1,DGMISC2,DGMISC3,SP1
          ADD       ONE,SUCCPROC                  * increment success message
          GOTO      DGART999
.
. send reply message as Error
.
DGART900  PACK      CCENCODE,CCDATE,CCDST,CCSRC,CCNAME,REPLYRPL
          WRITE     DGATEIN;CCENCODE:
                            RPLYHEAD,RPLYCODE,RPLYDESC:
                            DGMISC1,DGMISC2,DGMISC3,SP1
          ADD       ONE,ERRRPROC                  * increment # error messages
          GOTO      DGART999
.
DGART999  GOTO      ENDPRRET
+
.***********************************************************************
.*  RTINI000  :  initialisation                                        *
.***********************************************************************
RTINI000  MOVE      "RET",RPLYHEAD
.
          OPEN      PATNOBE1,"patnobef"
          OPEN      PATONLV1,"patonlvf"
.
RTINI999  RETURN
+
.*************************************************************************
.*  VALID000  :  validate patient info & get some info                   *
.*************************************************************************
VALID000  PACK      KEY8,NADMNO
          CALL      RDMISS1
          BRANCH    OVRCD,VALID800
.
          PACK      KEY8,AURNO
          CALL      RDMAST1
          BRANCH    OVRCD,VALID810
          CALL      RDPMPX21
.
          PACK      KEY30,NADMNO,Z30
          CALL      RDSTRAN2
          CALL      RDPTRAN2
          BRANCH    OVRCD,VALID820
.
          MATCH     NADMNO,TADMN
          GOTO      VALID820 IF NOT EQUAL
.
          MATCH     "L",TMOVE                       * must be on leave?
          GOTO      VALID830 IF NOT EQUAL
.
. make sure the date/time of the new transfer record will be after the last one
.
          MATCH     TDATE,NDATE
          GOTO      VALID850 IF LESS           * new date before last date
          IF        @EQUAL
            MATCH     TTIME,TTIME2
            GOTO      VALID850 IF EQUAL        * new date/time = last date/time
            GOTO      VALID850 IF LESS         * new time < last time (dates =)
          ENDIF
.
. save transfer file vars
.
          MOVE      TRATEF,LTAMTF
          MOVE      TRATEH,LTAMTH
          MOVE      TDISC,LDISC
          MOVE      TALLW,LALLW
          MOVE      TRBTYP,LRBTYP
          MOVE      TRBEND,LRBEND
          MOVE      TDATE,LDATE
          MOVE      TTIME,LTIME
          MOVE      TMOVE,LMOVE
.
          MOVE      TWARD,LWARD
          MOVE      TBED,LBED
          MOVE      TADOCT,STADOCT
          MOVE      TATYPE,STATYPE
          MOVE      TCHSTAT,STCHSTAT
          MOVE      TEXTRA,STEXTRA
          MOVE      TDEPT,STDEPT            * save department
          MOVE      PTTRLTYP,CODELTYP       * save type of leave
          MOVE      PTTREPNO,STEPNO         * save episode number
.
          MOVE      ZERO,EXIT
          GOTO      VALID999
.
. set up error reply messages
.
VALID800  MOVE      "Missing admission record",RPLYDESC
          MOVE      "02100",RPLYCODE
          GOTO      VALID900
.
VALID810  MOVE      "U/R Number does not exist",RPLYDESC
          MOVE      "01100",RPLYCODE
          GOTO      VALID900
.
VALID820  MOVE      "Missing transfer record",RPLYDESC
          MOVE      "02102",RPLYCODE
          GOTO      VALID900
.
VALID830  MOVE      "Patient is not on leave",RPLYDESC
          MOVE      "02111",RPLYCODE
          GOTO      VALID900
.
VALID850  MOVE      "Transfer date/time must be after last transfer",RPLYDESC
          MOVE      "02180",RPLYCODE
          GOTO      VALID900
.
VALID900  MOVE      ONE,EXIT                     * set error flag
.
VALID999  RETURN
+
.*************************************************************************
.*  RTMAN000  :  if MH patient must have a leave end code passed         *
.*************************************************************************
RTMAN000  COMPARE   ONE,MHCNUSE                  * using MH system?
          GOTO      RTMAN800 IF NOT EQUAL
.
          PACK      KEY8,NADMNO                  * check if MH patient?
          CALL      RDMHVIS1
          BRANCH    OVRCD,RTMAN800
.
. a MH leave end code must have been passed
. 
          MATCH     SP3,MHLVEEND
          GOTO      RTMAN900 IF EQUAL
.
RTMAN800  MOVE      ZERO,EXIT                    * everything is OK!
          GOTO      RTMAN999
.
. error
.
RTMAN900  MOVE      "02120",RPLYCODE             * set up error code
          MOVE      "Missing field - Mental Health Leave End code",RPLYDESC
          MOVE      ONE,EXIT
.
RTMAN999  RETURN
+
.************************************************************************
.*  RTRCA000  :  Write rate change audit record if necessary            *
.************************************************************************
RTRCA000  COMPARE   ONE,CHG1
          GOTO      RTRCA999 IF NOT EQUAL
.
          COMPARE   ZERO,CFEETYP
          GOTO      RTRCA999 IF NOT EQUAL
.
.    Only write to Rate Change Audit file if system parameter is switched on (0)
.
          BRANCH    CAUDQ,RTRCA999                  * parameter ON ?
.
          OPEN      PATRCAUD,"patrcaud"
          MOVE      "PUR",PRXCODE   * System Lock Audits
          CALL      GETSLK00        * Get System Lock Audits
          CALL      RDSRCA
.
          IF        RSTATUS<>0
            CALL      RELSLK00        * Release System Lock Audits
            GOTO      RTRCA999 
          ENDIF
.
          CALL      WRIRCA
          CALL      RELSLK00        * Release System Lock Audits
.
          MOVE      NADMNO,RADMNO
          MOVE      PGNAME,ANS
          PACK      DOT,PSNAME
          MOVE      ACLAIM,RCLAIM
          MOVE      AFUNDH,RFUND
          MOVE      AFNDTB,RHFTAB
          MOVE      SAVBTYP,RBTYPE
.
          MOVE      ZERO,RNPAT        * New rate = rate - disc - allow
          MOVE      NTAMTF,RNPAT
          SUB       NDISC,RNPAT
          SUB       NLWAMT,RNPAT
          MOVE      NTAMTH,RNFUND
.
          MOVE      TDATE,RDATE
          MOVE      ATYPE,RATYPE
.
          MOVE      ZERO,ROPAT
          MOVE      SP3,RCLSS
          MOVE      SP3,RCHST
          MOVE      PASSCODE,ROPER       * Operator code
.
          CALL      WRRCA
          CLOSE     PATRCAUD
.
RTRCA999  RETURN
+
.************************************************************************
.*  RTWRD000  :  fix up ward bed files                                  *
.************************************************************************
RTWRD000  MOVE      NWARD,TWARD
          MOVE      NBED,TBED
.
          MATCH     SP3,TBED
          GOTO      RTWRD100 IF EQUAL
.
          PACK      KEY6,TWARD,TBED
          CALL      RDWARD1
.
          MOVE      NADMNO,WADMNO
          MOVE      ZERO,WPLUR
          CALL      UPWARD1
          GOTO      RTWRD999
.
RTWRD100  MOVE      ZERO,NBPLUR
          PACK      KEY13,TWARD,NADMNO,NBPLUR
          MOVE      SP3,NBROOM
          CALL      WRNOBE1
.
RTWRD999  RETURN
+
.************************************************************************
.*  RTOLV000  :  delete on leave record                                 *
.************************************************************************
RTOLV000  PACK      KEY14,LWARD,LBED,NADMNO
          CALL      DEONLV1
.
RTOLV999  RETURN
+
.************************************************************************
.*  RTMEH000  :  update mental health if required                       *
.************************************************************************
RTMEH000  COMPARE   ONE,MHCNUSE
          GOTO      RTMEH999 IF NOT EQUAL   * not using mental health
.
          MOVE      NADMNO,KEY8
          CALL      RDMHVIS1                * MH visit file
          BRANCH    OVRCD,RTMEH999          * not a mental patient
.
          MOVE      TWO,AUDIFLAG            * before change audit
          CALL      MHAUDVIS                * write audit
          MOVE      ONE,MHVISTAT            * set status  
          CALL      UPMHVIS1                * set to back in hospital
          MOVE      THREE,AUDIFLAG          * after change audit
          CALL      MHAUDVIS                * write audit
.
          MOVE      NADMNO,AADMNO
          CALL      ULSD0000                * Update other mh details
          CALL      WLER0000                * Keyin leave end codes
.
RTMEH999  RETURN
+
.**************************************************************************
.*  RTNUM000  :  Update Ward Usage file                                   *
.**************************************************************************
RTNUM000  BRANCH    PTCNNUMM,RTNUM999       * Skip if doing the monthly update
.
          OPEN      PATNUMW1,"patnumwr"
.                                         of the PATNUM file
          PACK      CPTDATE,TDATE
          REP       " 0",CPTDATE
          CALL      GPERD                 * Get the financial period for date
          BRANCH    EXIT,RTNUM900         * Date not in period file
.
          PACK      PDATE,DRGYR,DRGNUM
          MOVE      TWARD,PWARD
.
.       Update number of patient transfer out
.
RTNUM100  PACK      KEY9,PWARD,PDATE
          CALL      RLNUMW1
          BRANCH    OVRCD,RTNUM300,RTNUM190
          ADD       ONE,PRETN
          CALL      UPNUMW1
          CALL      UUNUMW1
          GOTO      RTNUM900
.
RTNUM190  DISPLAY   *W,"Waiting on Record Lock PATNUMFD"
          GOTO      RTNUM100
.
RTNUM300  MOVE      ZERO,PTRIN
          MOVE      ZERO,PTROUT
          MOVE      ZERO,PDSCH
          MOVE      ZERO,PADMN
          MOVE      ZERO,PLEAV
          MOVE      ONE,PRETN
          CALL      WRNUMW1
.
RTNUM900  CLOSE     PATNUMW1
.
RTNUM999  RETURN
+
.************************************************************************
.*  RTMIS000  :  update admission data                                  *
.************************************************************************
RTMIS000  MOVE      TWARD,AWARD
          MOVE      TBED,ABED
.
          MOVE      TWO,ASTAT
          MOVE      NADMNO,KEY8
          CALL      UPLMISS1
.
RTMIS999  RETURN
+
.************************************************************************
.*  RTCON000  :  update the control file with the transfer date if reqd *
.************************************************************************
RTCON000  READ      CONTROLF,EIGHTY8;*148,CFRDTE
          MATCH     CFRDTE,TDATE
          GOTO      RTCON999 IF NOT LESS
          MOVE      TDATE,CFRDTE
          WRITAB    CONTROLF,EIGHTY8;*148,CFRDTE
.
RTCON999  RETURN
+
.*********************************************************************
.*                  RTATD000                    Called by :          *
.*     Write the Changes to attending doc/department                 *
.*********************************************************************
RTATD000  COMPARE   ONE,PTCNCADD               * using this facility?
          GOTO      RTATD999 IF NOT EQUAL
.
          MATCH     SP6,NEWATDOC
          IF        !@EQUAL
            MATCH     STADOCT,NEWATDOC         * Change attending doc/dept
            GOTO      RTATD100 IF NOT EQUAL
          ENDIF
.
          MATCH     SP3,NEWDPART               * if spaces, no new department
          GOTO      RTATD999 IF EQUAL
          MATCH     STATYPE,NEWDPART           * changed dept?
          GOTO      RTATD999 IF EQUAL
.
RTATD100  MOVE      "C",TMOVE 
          MATCH     SP6,NEWATDOC
          IF        @EQUAL
            MOVE      STADOCT,TADOCT           * no new 1 sent so use old
          ELSE
            MOVE      NEWATDOC,TADOCT          * use new 1 sent
          ENDIF
.
          MATCH     SP3,NEWDPART
          IF        @EQUAL
            MOVE      STATYPE,TATYPE           * no new 1 sent so use old
          ELSE
            MOVE      NEWDPART,TATYPE          * use new 1 sent
          ENDIF
          UNPACK    TTIME,DIM6,DIM2  
          MOVE      DIM2,FORM2
          ADD       ONE,FORM2
          PACK      TTIME,DIM6,FORM2
.
          MOVE      ZERO,PTTREPSD
          MOVE      ZERO,PTTRLSPT
          MOVE      ZERO,PTTRLSRB
          MOVE      ZERO,PTTRAEND
          MOVE      ZERO,PTTRADPT
          MOVE      ZERO,PTTRADRB
          MOVE      SP70,PTTRNHAC
          MOVE      SP70,PTTROPER
          MOVE      SP70,PTTRAUAT
          MOVE      SP70,PTTRCUID
          MOVE      SP70,PTTRLTSC
          MOVE      ACLAIM,PTTRCLTY
.
          PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED
          PACK      PTTRCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTTRCDAT
          CLOCK     TIME,PTTRCTIM
          MOVE      WBSEUID,PTTRUCID
          UNPACK    SP70,TRCDATE,TRCTIME,PTTRUUID
.
          CALL      WRTRAN2
.
          MOVE      TADOCT,ADOCTA
          MOVE      TATYPE,ATYPE
          MOVE      TADMN,KEY8             * Update the admission file
          CALL      UPMISS1
.
RTATD999  RETURN
+
.***********************************************************************
.*  RTTRN000  :  write transfer record                                 *
.***********************************************************************
RTTRN000  MOVE      NDATE,TDATE
          MOVE      NWARD,TWARD
          MOVE      NBED,TBED
          MOVE      SAVBTYP,TRBTYP
          MOVE      "R",TMOVE
          MOVE      NADMNO,TADMN
          MOVE      AURNO,TURNO
          MOVE      NTAMTF,TRATEF
          MOVE      NTAMTH,TRATEH
          MOVE      NTAMTG,TRATEG
          MOVE      NDISC,TDISC
          MOVE      NLWAMT,TALLW
          MOVE      SAVEND,TRBEND
          MOVE      TTIME2,TTIME
          MOVE      NADMNO,TADMN
          MOVE      STATYPE,TATYPE
          MOVE      STADOCT,TADOCT
          MOVE      STCHSTAT,TCHSTAT
          MOVE      STEXTRA,TEXTRA
          MOVE      STDEPT,TDEPT            * department
          MOVE      STEPNO,PTTREPNO         * episode number
.
          MOVE      ZERO,PTTREPSD
          MOVE      ZERO,PTTRLSPT
          MOVE      ZERO,PTTRLSRB
          MOVE      ZERO,PTTRAEND
          MOVE      ZERO,PTTRADPT
          MOVE      ZERO,PTTRADRB
          MOVE      SP70,PTTRNHAC
          MOVE      SP70,PTTROPER
          MOVE      SP70,PTTRAUAT
          MOVE      SP70,PTTRCUID
          MOVE      SP70,PTTRLTSC
          MOVE      ACLAIM,PTTRCLTY
.
          PACK      KEY30,TADMN,TDATE,TTIME,TWARD,TBED
          PACK      PTTRCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PTTRCDAT
          CLOCK     TIME,PTTRCTIM
          MOVE      WBSEUID,PTTRUCID
          UNPACK    SP70,TRCDATE,TRCTIME,PTTRUUID
.
          CALL      WRTRAN2
.
RTTRN999  RETURN
+
.*************************************************************************
.*  RTHMS000  :  Subtract off the old data for pat from MHMS adjustments *
.*************************************************************************
RTHMS000  MOVE      NADMNO,PVIBILL
          MOVE      PVIBILL,KEY8
          CALL      RDVISA1               * Make sure visit file record is read
.
RTHMS999  RETURN
+
.*****************************************************************************
.*                              ULSD0000                                     *
.*                  Update the legal status and ls review file               *
.*****************************************************************************
.
ULSD0000  PACK      KEY21,AADMNO,Z30
          CALL      RSMHLEG1
          CALL      RPMHLEG1
          BRANCH    OVRCD,ULSD9999
.
          MATCH     AADMNO,MHLEADMN
          GOTO      ULSD9999 IF NOT EQUAL
.
          MATCH     MHLESTAT,PTMXLS
          GOTO      ULSD9999 IF EQUAL        * didn't change
.
          MOVE      MHLEREVW,SAVREVW
.
          CALL      IBACLOCK
          PACK      DIM8,CCENT,CYEAR,CMON,CDAY
          REP       " 0",DIM8
          MOVE      CTIMEIS,DIM5
.
          MOVE      AADMNO,MHLEADMN
          MOVE      DIM8,MHLEDATE
          MOVE      DIM5,MHLETIME
          MOVE      PTMXLS,MHLESTAT
          MOVE      ANSN,MHLESENT
          MOVE      SAVREVW,MHLEREVW
          MOVE      SP1,MHLEFLAG
          PACK      MHLESPAR,SP10,SP5
          CALL      WRMHLEG1
.
          MOVE      AADMNO,MHRVADMN
          MOVE      DIM8,MHRVDATE
          MOVE      DIM5,MHRVTIME
          MOVE      ONE,MHRVDECI
          MOVE      PTMXLSDT,MHRVLDAT
          MOVE      DIM5,MHRVLTIM
          MOVE      MHVILSRV,MHRVNDAT
          MOVE      SP3,MHRVREAS
          MOVE      SP20,MHRVSPAR
          CALL      WRMHREV1
.
ULSD9999  RETURN
+
.***************************************************************************
.*  WLER0000  :  write Mental Health leave end reason                      *
.***************************************************************************
WLER0000  COMPARE   ONE,MHCNKLER
          GOTO      WLER9999 IF NOT EQUAL
.
          PACK      MHLRDATE,TDATE 
          REP       " 0",MHLRDATE
          PACK      KEY24,TADMN,MHLRDATE,TTIME
          CALL      RAMHLER1
          BRANCH    OVRCD,WLER2000
          GOTO      WLER9999
.
WLER2000  MOVE      TADMN,MHLRADMN
          MOVE      TTIME,MHLRTIME
          MOVE      MHLVEEND,MHLRCODE               * leave end code (passed)
          CALL      WRMHLER1
.
WLER9999  RETURN
.
.
.
OVERCOND  MOVE      ONE,OVRCD
          RETURN
OVERLOCK  MOVE      TWO,OVRCD
          RETURN
CNOCHG    MOVE      ONE,CDCHG
          RETURN
CNOQST    MOVE      ONE,CQUEST
          GOTO      OVERCOND
STOPDATE
STOPDATC  DISPLAY   *PCCOL:CVERT,SP10
          RETURN
.
.
.
          INC       CALCDAYS                * calculate #days b/w dates
          INC       DISPITEM                * set up display string
          INC       DSPBGRND                * display background
          INC       DSPFIELD                * display fields
          INC       DXPATCOD                * display for patcodfd 
          INC       IBAPASIO/INC
          INC       MEHCLDSC                * clear discharge variables
          INC       MEHDL1IO/INC            * Discharge to leave file
          INC       MEHDS1IO/INC            * Discharge file
          INC       MEHDXDSC                * Sets up the DATA & ATTRIBUTES
          INC       MEHLEGIO/INC            * legal status file
          INC       MEHLERIO/INC
          INC       MEHREVIO/INC            * LS reveiw file
          INC       MHAUDDLV                * audits for dlv file
          INC       MHAUDVIS                * audits for vis file
          INC       NHOSPDAY    
          INC       OUTCLIIO/INC
          INC       PATALRIO/INC
          INC       PATALWDS    
          INC       PATCODKY                * keyin codes file entry
          INC       PATHEAD
          INC       PATHSPIO/INC
          INC       PATALWIO/INC
          INC       PATNOBIO/INC
          INC       PATNUMIO/INC
          INC       PATONLIO/INC
          INC       PATRCAIO/INC
          INC       PATMRGIO/INC
........  INC       PATSWARD
........  INC       PATWRDDS
          INC       SCRSBGIO/INC            * backgroun file
+
.
 XIF
ENDPRRET  ENDPROC
