.******************************************************************************
.*                                                                            *
.* Include Name     : PATASFKY.PBL                                            *
.*                                                                            *
.* Description      : Keyin the sepcial funding arrangment code for each      *
.*                    patient                                                 *
.*                                                                            *
.* Author           : Junior  08/08/1996                                      *
.*                                                                            *
.* Files Required   : PATCODFD  PATSFAFD  PATVADFD                            *
.*                                                                            *
.* Parameters Req   : A valid PATASFFD record or blank PATASFFD variables     *
.*                    ACODE    - Admission/transfer contract code             *
.*                    ADATE    - Admission date                               *
.*                    PTCNHDPS - Used to determine if using PRS2              *
.*                    TCODE    - Admission/transfer contract category (A/TL)  *
.*                                                                            *
.* Subroutines Used : GETSCR00 - Save the screen                              *
.*                    PATVADKY - Keyin the transfer source code               *
.*                    PUTSCR00 - Replace the screen                           *
.*                                                                            *
.* Extra Vars Req   : Z30       INIT      "zzzzzzzzzzzzzzzzzzzzzzzzzzzzzz"    *
.*                                                                            *
.* Modifications    :                                                         *
.*        V5.07.02  28/08/1999  J.Tan  SRF 635569                             *
.*                  Fixed checking discharged date for discharged patient only*
.*        V5.07.01  02/07/1999  Greg Horvat      99/00 PRS2 Changes           *
.*                  Changed to keyin the contract variables for 99/00         *
.*                  08/10/1998  Glenn Berry      5.07 changes                 *
.******************************************************************************
.*        V5.04.01  06/05/1997  Greg Horvat                                   *
.*                  Changed to use PTCNHDPS to work out which state for HDP   *
.*                  extracts                                                  *
.*        V6.04.02  04/02/1997  Greg Horvat                                   *
.*                  Changed to allow C to be set up in the 1st & 2nd positions*
.*                  of the HDP Eq                                             *
.*        V6.04.01  22/08/1996  Greg Horvat                                   *
.*                  Changed to use a parameter as validation for using PRS2   *
.*                                                                            *
.******************************************************************************
PATASFKY
KYASF000  COMPARE   THREE,PTCNHDPS
          GOTO      KYASF900 IF NOT EQUAL   * Not using PRS2
.
          MATCH     SP3,ACODE
          GOTO      KYASF900 IF EQUAL       * No contract code keyed in
.
          PACK      KEY5,TCODE,ACODE
          CALL      RDCODE1                 * Read the code file
          BRANCH    OVRCD,KYASF900
.
          RESET     THCSCOD
KYASF040  MOVE      THCSCOD,KEY1
          MATCH     SP1,KEY1
          GOTO      KYASF080 IF EQUAL       * Blank HDP eq
.
          TYPE      KEY1
          GOTO      KYASF080 IF EQUAL       * Numeric HDP eq
.
          REP       "C1S2H3",KEY1
          MOVE      ZERO,MOPTION
          MOVE      KEY1,MOPTION
          BRANCH    MOPTION,KYASF120,KYASF120,KYASF120
.                           Contract HubSpoke HFStream
.
KYASF080  BUMP      THCSCOD
          GOTO      KYASF900 IF EOS         * End of string, exit
          GOTO      KYASF040
.
KYASF120  RESET     THCSCOD
          MOVE      KEY1,CKYIDEF1           * Funding arrangement code
          MOVE      PTAFCODE,SPTAFCOD
          MOVE      PTAFCTYP,SPTAFCTY
          MOVE      PTAFCROL,SPTAFCRO
          MOVE      PTAFCSID,SPTAFCSI
          UNPACK    SP8,PTAFCODE,PTAFCTYP,PTAFCROL,PTAFCSID
          BRANCH    MOPTION,KYASF160,KYASF360,KYASF360
.                           Contract HubSpoke HFStream
          GOTO      KYASF900
.
KYASF160  COMPARE   THREE,ASTAT
          GOTO      KYASF400 IF NOT EQUAL     * Not discharged
.
          MATCH     "19990701",DDATE
          GOTO      KYASF400 IF NOT LESS    * Disch date >= 01/07/1999
.
. ----- Keyin the special funding arrangement for 98/99 -----
.
          OPEN      PATSFAA1,"patsfaaf"
          MOVE      ZERO,CQUEST             * ? flag - no
KYASF200  MOVE      SPTAFCOD,KEY1
          KEYIN     *P1:24,*EL,"Special funding arrangement code : ":
                    *V2LON,*RV,KEY1;
          PACK      KEY1,KEY1,SP1
.
          MATCH     EXITCHAR,KEY1
          GOTO      KYASF800 IF EQUAL       * Exitchar
.
          MATCH     SP1,KEY1
          GOTO      KYASF800 IF EQUAL       * Spaces
.
          MATCH     QUEST,KEY1
          GOTO      KYASF280 IF EQUAL       * ? mark
.
          CALL      RDPTSFA1                * Read a SFA file record
          BRANCH    OVRCD,KYASF240
.
          MOVE      PTSFCODE,PTAFCODE       * Special funding arrangment code
          GOTO      KYASF840
.
KYASF240  BEEP
          GOTO      KYASF200
.
KYASF280  IF        CQUEST<>1
            MOVE      "1",HLEF
            MOVE      "1",HTOP
            MOVE      "80",HRIG
            MOVE      "23",HBOT
            CALL      GETSCR00              * Save the current screen
          ENDIF
          MOVE      ONE,CQUEST              * ? flag - yes
          DISPLAY   *P1:3,*EF:
                    *P20:4,*V2LON,*ULON,"Special Funding Arrangement":
                    *P1:6,"Code & Description",SP30,SP30,SP2;
          MOVE      "7",CVERT
          PACK      KEY1,SP1
          CALL      RSPTSFA1                * Position on & read a SFA file
KYASF320  CALL      RKPTSFA1                  record
          BRANCH    OVRCD,KYASF200
.
          DISPLAY   *P1:CVERT,*V2LON,PTSFCODE,*HOFF,SP1,PTSFDESC;
          ADD       "1",CVERT
          GOTO      KYASF320
.
. ----- Keyin the contract info 99/00 -----
.
KYASF360  COMPARE   THREE,ASTAT
          GOTO      KYASF800 IF NOT EQUAL   * Not discharged
.
          MATCH     "19990701",DDATE
          GOTO      KYASF800 IF LESS        * Disch date < 01/07/1999
.
KYASF400  MOVE      CKYIDEF1,PTAFCODE       * Funding arrangment code
          MOVE      ZERO,CQUEST             * ? flag - no
          BRANCH    MOPTION,KYASF440,KYASF720,KYASF840
.
. ----- Keyin the contract type -----
.
KYASF440  MOVE      SPTAFCTY,KEY1
          KEYIN     *P1:24,*EL,"Contract type : ",*V2LON,*RV,KEY1;
          PACK      KEY1,KEY1,SP1
.
          MATCH     EXITCHAR,KEY1
          GOTO      KYASF800 IF EQUAL       * Exitchar
.
          MATCH     SP1,KEY1
          GOTO      KYASF800 IF EQUAL       * Spaces
.
          MATCH     QUEST,KEY1
          GOTO      KYASF520 IF EQUAL       * ? mark
.
          TYPE      KEY1
          GOTO      KYASF480 IF NOT EQUAL   * Not numeric, error
.
          MOVE      ZERO,FORM1
          MOVE      KEY1,FORM1
          BRANCH    FORM1,KYASF560,KYASF560,KYASF560,KYASF560,KYASF560
.
KYASF480  BEEP
          GOTO      KYASF440
.
KYASF520  IF        CQUEST<>1
            MOVE      "1",HLEF
            MOVE      "1",HTOP
            MOVE      "80",HRIG
            MOVE      "23",HBOT
            CALL      GETSCR00              * Save the current screen
          ENDIF
          MOVE      ONE,CQUEST              * ? flag - yes
          DISPLAY   *P1:3,*EF:
                    *P20:4,*V2LON,*ULON,"Contract Type",*HOFF:
                    *P1:6,*V2LON,"1",*HOFF,SP2:
  "B    - A (health authority/other external purchaser) contracts B (hospital)":
                    *P11:7:
         "for admitted service.":
                    *P1:8,*V2LON,"2",*HOFF,SP2:
  "ABA  - Patient admitted by Hospital A.  Hospital A contracts Hospital B for":
                    *P11:9:
         "admitted or non-admitted patient service.  Patient returns to ":
                    *P11:10:
         "Hospital A on completion of service by Hospital B.":
                    *P1:11,*V2LON,"3",*HOFF,SP2:
  "AB   - Patient admitted by Hospital A.  Hospital A contracts Hospital B for":
                    *P11:12:
         "admitted or non-admitted patient service.  Patient does not return ":
                    *P11:13:
         "to Hospital A on completion of service by Hospital B.":
                    *P1:14,*V2LON,"4",*HOFF,SP2:
  "(A)B - Patient not admitted by Hospital A.  Hospital A contracts Hospital B":
                    *P11:15:
         "for the whole admitted patient service.":
                    *P1:16,*V2LON,"5",*HOFF,SP2:
  "BA   - Hospital A, which does not initially admit the patient, contracts":
                    *P11:17:
         "Hospital B for an admitted patient service following which the":
                    *P11:18:
         "patient is transferred to and admitted by Hospital A.";
          GOTO      KYASF440
.
KYASF560  MOVE      FORM1,PTAFCTYP          * Contract type
          PERFORM   CQUEST,PUTSCR00         * Replace the previous screen
          MOVE      ZERO,CQUEST             * ? flag - no
.
. ----- Keyin the contract role -----
.
KYASF600  MOVE      SPTAFCRO,KEY1
          KEYIN     *P1:24,*EL,"Contract role : ",*V2LON,*RV,KEY1;
          REP       UPPLOW,KEY1
          PACK      KEY1,KEY1,SP1
.
          MATCH     EXITCHAR,KEY1
          GOTO      KYASF800 IF EQUAL       * Exitchar
.
          MATCH     SP1,KEY1
          GOTO      KYASF440 IF EQUAL       * Spaces
.
          MATCH     QUEST,KEY1
          GOTO      KYASF640 IF EQUAL       * ? mark
.
          MATCH     ANSA,KEY1
          GOTO      KYASF680 IF EQUAL       * Valid contract role
.
          MATCH     ANSB,KEY1
          GOTO      KYASF680 IF EQUAL       * Valid contract role
.
          BEEP
          GOTO      KYASF600
.
KYASF640  IF        CQUEST<>1
            MOVE      "1",HLEF
            MOVE      "1",HTOP
            MOVE      "80",HRIG
            MOVE      "23",HBOT
            CALL      GETSCR00              * Save the current screen
          ENDIF
          MOVE      ONE,CQUEST              * ? flag - yes
          DISPLAY   *P1:3,*EF:
                    *P20:4,*V2LON,*ULON,"Contract Role",*HOFF:
                    *P1:6,*V2LON,"A",*HOFF,SP2:
  "Hospital A - The contracting (purchasing) hospital is termed Hospital A.":
                    *P1:7,*V2LON,"B",*HOFF,SP2:
  "Hospital B - The contracted (service provider) hospital is termed ":
                    "Hospital B.";
          GOTO      KYASF600
.
KYASF680  MOVE      KEY1,PTAFCROL           * Contract type
          PERFORM   CQUEST,PUTSCR00         * Replace the previous screen
          MOVE      ZERO,CQUEST             * ? flag - no
.
. ----- Keyin the contract/spoke id -----
.
KYASF720  DISPLAY   *P1:24,*EL,"Contract/spoke id : ";
          MOVE      "21",ECOL
          MOVE      "24",EVERT
          MOVE      SPTAFCSI,CKYIDEF5
          MOVE      ZERO,CKYIMAND
          MOVE      ONE,CNEWDS
          CALL      PATVADKY                * Keyin the transfer source code
          BRANCH    EXIT,KYASF760,KYASF800
.
          MOVE      PTVACODE,PTAFCSID       * Contract/spoke id
          GOTO      KYASF840
.
KYASF760  BRANCH    MOPTION,KYASF600
.
KYASF800  MOVE      SPTAFCOD,PTAFCODE
          MOVE      SPTAFCTY,PTAFCTYP
          MOVE      SPTAFCRO,PTAFCROL
          MOVE      SPTAFCSI,PTAFCSID
          GOTO      KYASF880
.
KYASF840  MOVE      ONE,ASFFLG              * Admin SFA flag - yes
.
KYASF880  PERFORM   CQUEST,PUTSCR00         * Replace the previous screen
          DISPLAY   *P1:24,*EL;
          CLOSE     PATSFAA1
KYASF900  MOVE      ZERO,EXIT
KYASF999  RETURN
+
.

