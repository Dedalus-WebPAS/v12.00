.**********************************************************************
.*                                                                    *
.*                 MRTSTFDS : Display Staff Members                   *
.*                                                                    *
.*        This include displays all entries in the staff register     *
.*        file for Medical Records Tracking                           *
.*                                                                    *
.*   AUTHOR           :      DIG     25/05/89                         *
.*                                                                    *
.*   OPEN FILES       :      MRTSTFA3                                 *
.*                           CONTROLF                                 *
.*                                                                    *
.*    MODIFICATIONS    : 28/11/89 Graeme Williams                     *
.*                       Added the parameter CNEWDS to determine if   *
.*                       the include is to use the new standards for  *
.*                       "?" routines                                 *
.*                       07/06/94 Rob Leonard  SRF 129580             *
.*                       Fixed bug when more than one screen of data  *
.**********************************************************************
MRTSTFDS  MOVE      SP20,MRSTSNAM
          MOVE      SP20,KEY20
.
. ---- bypass surname input ???? ----
.
          READ      CONTROLF,SIXTY;*128,MRCNSTFF
          BRANCH    MRCNSTFF,QUES05ST
      
          MOVE      ONE,COUNT
          DISPLAY   *P1:3,*EF:
                    *P1:4,"Surname : ",UNDLN20;
          KEYIN     *P11:4,*V2LON,*RV,KEY20;
          DISPLAY   *P11:4,*V2LON,KEY20;
          PACK      KEY26,KEY20,SP30
          CALL      RSMRSTF3
          CALL      RKMRSTF3
          BRANCH    OVRCD,QUES2STF
          MATCH     SP20,KEY20
          GOTO      QUES05ST IF EQUAL
          MATCH     KEY20,MRSTSNAM 
          GOTO      QUES2STF IF NOT EQUAL
.
.------ Display Category Heading, and then loop over entries ------
.
QUES05ST  DISPLAY   *P1:3,*EF,*P30:4,*V2LON,*ULON,"STAFF REGISTER"
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          MOVE      ONE,COUNT
.
.>>>>>>>  TRAP      QUES3STF IF F1
.
          PACK      KEY26,KEY20,SP30
          CALL      RSMRSTF3
.
QUES1STF  CALL      RKMRSTF3
          BRANCH    OVRCD,STFDS400
.
.--------- check to see if staff code is active ----
.
          COMPARE   "0",MRSTSTAT                          * 0 = Active
          GOTO      QUES1STF IF NOT EQUAL
.
          MATCH     SP20,KEY20
          GOTO      QUES1ST22 IF EQUAL
.
          MATCH     KEY20,MRSTSNAM                 * no more codes? 
          GOTO      STFDS400 IF NOT EQUAL
.
QUES1ST2  COMPARE   TWENTY3,CVERT
          GOTO      QUES15ST IF LESS
.
          MOVE      FIVE,CVERT
          ADD       "41",CCOL
          COMPARE   "70",CCOL
          GOTO      QUES15ST IF LESS
.
STFDS050  MOVE      TWO,FORM1
          KEYIN     *P1:24,*EL,"Select Item, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"M",*HOFF,")ore, (":
                    *V2LON,"N",*HOFF,")ext, (":
                    *V2LON,"E",*HOFF,")xit ? ",*JR,*V2LON,KEY2;
          ENDSET    KEY2
          APPEND    SP2,KEY2
          RESET     KEY2
.
          TYPE      KEY2
          GOTO      STFDS500 IF EQUAL
          REP       UPPLOW,KEY2
.
          REP       "F1E2M3N4",KEY2
          TYPE      KEY2
          GOTO      STFDS050 IF NOT EQUAL
          MOVE      KEY2,FORM2
          BRANCH    FORM2,QUES05ST,STFDS800,STFDS184,MRTSTFDS
          GOTO      STFDS050
.
STFDS184  MOVE      FIVE,CVERT                    * Next screen selected
          MOVE      ONE,CCOL
          MOVE      ONE,COUNT
          DISPLAY   *P1:5,*EF
.
QUES15ST  CALL      FNAM0STF                       * Format staff members name
          DISPLAY   *PCCOL:CVERT,*V2LON,COUNT,*HOFF,". ",*V2LON,MRSTCODE:
                    SP1,*HOFF,KEY31;
          MOVE      MRSTCODE,DSTFARRY[COUNT]
          ADD       ONE,CVERT
          ADD       ONE,COUNT
          GOTO      QUES1STF
.
.
.------ If missing category show code missing ------
.
QUES2STF  DISPLAY   *P1:3,*EF,*P30:4,*V2LON,*BLINKON:
                    "No Match Found";
          GOTO      STFDS400
.
QUES3STF  NORETURN
.>>>>>>>  TRAPCLR   F1
.
.------ End of codes ------
.
STFDS400  MOVE      ONE,FORM1
          KEYIN     *P1:24,*EL,"Select Item, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext, (":
                    *V2LON,"E",*HOFF,")xit ? ",*JR,*V2LON,KEY2;
          ENDSET    KEY2
          APPEND    SP2,KEY2
          RESET     KEY2
.
          TYPE      KEY2
          GOTO      STFDS500 IF EQUAL
          REP       UPPLOW,KEY2
.
          REP       "F1E2N3",KEY2
          TYPE      KEY2
          GOTO      STFDS400 IF NOT EQUAL
          MOVE      KEY2,FORM2
          BRANCH    FORM2,QUES05ST,STFDS800,MRTSTFDS
          GOTO      STFDS400
.
STFDS500  MOVE      KEY2,FORM2
.
          IF        FORM2=0
            BRANCH    FORM1,STFDS400,STFDS050
          ENDIF
.
          IF        (FORM2<0) | (FORM2>=COUNT)
            BEEP
            BRANCH    FORM1,STFDS400,STFDS050
          ENDIF
.
          MOVE      DSTFARRY[FORM2],KEY6          * get record selected
          MOVE      ZERO,EXIT
          GOTO      STFDS900
.
STFDS800  MOVE      ONE,EXIT                      * Exit
.
STFDS900  RETURN
+
+
.**********************************************************************
.*                           FNAM0STF                                 *
.*                 Routine to format patients name                    *
.**********************************************************************
FNAM0STF    CLEAR      KEY31
            MATCH      SP20,MRSTSNAM
            GOTO       FNAM12ST IF EQUAL
.
FNAM05ST    APPEND     MRSTSNAM,KEY31
            ENDSET     KEY31
.            
FNAM08ST    CMATCH     SP1,KEY31
            GOTO       FNAM1STF IF NOT EQUAL
            BUMP       KEY31,-1
            GOTO       FNAM08ST
.
FNAM1STF    APPEND     ",",KEY31
            APPEND     SP1,KEY31
            RESET      MRSTTITL
.
FNAM12ST    MATCH      SP6,MRSTTITL                     * test title for spaces
            GOTO       FNAM2STF IF EQUAL
.
FNAM15ST    CMATCH     SP1,MRSTTITL
            GOTO       FNAM17ST IF EQUAL
            MOVE       MRSTTITL,ANS                    * append title to nameline
            APPEND     ANS,KEY31
            BUMP       MRSTTITL
            GOTO       FNAM17ST IF EOS
            GOTO       FNAM15ST
.                                                   *    nameline
FNAM17ST    APPEND     DOT,KEY31
            APPEND     SP1,KEY31
.
FNAM2STF    APPEND     MRSTGNAM,KEY31
            RESET      KEY31                     * append surname to
            MOVE       KEY31,KEY56               *    nameline
            CALL       COMP0STF                     * compress blanks
            MOVE       KEY56,KEY31
            GOTO       FNAM9STF
.
FNAM9STF    RETURN
+
.**********************************************************************
.*                              COMP0STF                              *
.*       Routine to compress excessive blanks in KEY56                *
.**********************************************************************
COMP0STF    MOVE       KEY56,KEY55
            MOVE       SP70,KEY57
            MATCH      KEY57,KEY55                 * test line for spaces
            GOTO       COMP9STF IF EQUAL
            CLEAR      KEY56
.
COMP1STF    CMATCH     SP1,KEY55
            GOTO       COMP5STF IF NOT EQUAL        * compress leading blanks
            BUMP       KEY55 
            GOTO       COMP1STF IF NOT EOS
            GOTO       COMP8STF
.
COMP5STF    MOVE       KEY55,KEY57
            MOVE       KEY57,KEY55
            ENDSET     KEY55
.
COMP51ST    CMATCH     SP1,KEY55
            GOTO       COMP7STF IF NOT EQUAL        * compress trailing blanks
            BUMP       KEY55,-1
            GOTO       COMP51ST
.
COMP7STF    LENSET     KEY55
            RESET      KEY55
.
COMP71ST    MOVE       KEY55,ANS
            APPEND     ANS,KEY56
            BUMP       KEY55                        * compress multiple blanks
            GOTO       COMP8STF IF EOS              *   from in-between words
            CMATCH     SP1,KEY55                   
            GOTO       COMP71ST IF NOT EQUAL
            MOVE       KEY55,ANS
            APPEND     ANS,KEY56
.     
COMP72ST    BUMP       KEY55
            CMATCH     SP1,KEY55
            GOTO       COMP71ST IF NOT EQUAL
            GOTO       COMP72ST
.
COMP8STF    RESET      KEY56
COMP9STF    RETURN
