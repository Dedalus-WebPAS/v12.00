.************************************************************************
.*  Procedure: DGCLIS14  -  Send a Notification of Appointment          *
.*                          Modification (S14)                          *
.*                                                                      *
.*  Author   : Steve Armstrong  (26/08/2008)                            *
.*                                                                      *
.*  Requires : OPRDEAFD (Theatre Booking) record                        *
.*                                                                      *
.*  Mods     :                                                          *
.*                                                                      *
.************************************************************************
.*  V11.04.01 09/01/2024 Thanh T           TSK 0936263                  *
.*            Added OPEN/CLOSE PMSQPXA1 index                           *
.************************************************************************
.*  V10.08.01 05/05/2016  Steve Armstrong  CAR 0818530                  *
.*            Added RESET  PRGID after SCAN                             *
.************************************************************************
.*  V10.03.01 17/04/2013  Davin S          CAR 280425                   *
.*            Changed IBCNUCIS - value 1 or greater will write trigger  *
.************************************************************************
.*  V10.00.01 19/03/2010  Steve Armstrong   CAR 135505                  *
.*            Added setting of H7CITRID & H7CIINCL                      *
.************************************************************************
          DEFPROC   DGCLIS14
.
          INC       HL7CISFD/INC
          INC       HL7INBFD/INC
          INC       OPRQUEFD/INC
          INC       PMSQPTFD/INC
.
CISMFLAG  FORM      1             * CIS message flag
.                                    0 = don't send HL7 CIS message
.                                    1 = send HL7 CIS message
IBAMFLAG  FORM      1             * IBA proprietary message flag
.                                    0 = don't send IBA proprietary message
.                                    1 = send IBA proprietary message
MESSAGID  DIM       20
.
DGCLI000  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*133,IBCNUCIS
          READ      CONTROLF,HUND13;*6,OPCNS14A
.
          MOVE      ZERO,IBAMFLAG                * not sending proprietary
          MOVE      ZERO,CISMFLAG                * default HL7 interface flag
          MATCH     "1",IBCNUCIS                 * using CIS interface ?
          IF        !@LESS
            MATCH     "1",OPCNS14A               * yes - sending S14 ?
            IF        @EQUAL
              MOVE      ONE,CISMFLAG             * yes- set flag for CIS message
            ENDIF
          ENDIF
.
          SCAN      "WEB82",PRGID
          IF        @EQUAL
            MOVE      ZERO,CISMFLAG              * don't send from death server
          ENDIF
          RESET     PRGID
.
          COMPARE   ONE,CISMFLAG                 * send message ?
          GOTO      DGCLI999 IF NOT EQUAL        * no - finished
.
.         We are sending S14 HL7 messages, so first open the relevant
.         holding tables
.
          OPEN      HL7CISA1,"hl7cisaf"
          OPEN      OPRQUEA1,"oprqueaf"
          OPEN      PMSQPTA1,"pmsqptaf"
          OPEN      PMSQPXA1,"pmsqpxaf"
.
          CALL      DGMESSID                    * Get Message ID, Date & Time
.
          PACK      KEY8,OPDAURNO
          CALL      RDMAST1
          IF        OVRCD<>1
            MOVE      PURNO,KEY8
            CALL      RDPMPX21
            IF        OVRCD=1
              CALL      CLPMSPX2
            ENDIF
          ENDIF
          CALL      PMIGTNID            * get national id for HL7
          MOVE      NMPNUMB,PTNINMPI
.
.         Write a record to pmsqptaf (holding patient details)
.
          MOVE      MESSAGID,PMQPMESI
          MOVE      SP8,PMQPOURN
          PACK      PMQPSPAR,SP30,SP30,SP20
          CALL      WRPMQPT1
.
.         Write a record to oprqueaf (holding table for opr booking details)
.
          MOVE      MESSAGID,OPQUMESI
          CALL      WROPQUE1
.
          MOVE      "S14",H7CIMETP
          MOVE      HL7TRGID,H7CITRID
          MOVE      HL7INCLD,H7CIINCL
          CALL      WRCIS000                     * write hl7cisaf record
.
          CLOSE     HL7CISA1
          CLOSE     OPRQUEA1
          CLOSE     PMSQPTA1
          CLOSE     PMSQPXA1
.
DGCLI999  GOTO      DGCLIEND                        * end procedure
.
.         I/O Routines
.
          INC       BRHLCOMN
          INC       CLPMSQPX
.
          INC       HL7CISIO/INC
          INC       HL7INBIO/INC
          INC       IBAOVRIO/INC
          INC       OPRQUEIO/INC
          INC       PMSQPTIO/INC
.
DGCLIEND  ENDPROC
+
