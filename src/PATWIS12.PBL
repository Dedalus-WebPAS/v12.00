.******************************************************************************
.* System         : Patient Management                                        *
.* Include        : PATWIS12.PBL                                              *
.* Description    : Calculate WIES12 value for an individual admission        *
.******************************************************************************
.* Date           : 23/07/2004                                                *
.* Author         : Henry Son                                                 *
.*                                                                            *
.* Vars Required  : VARWIS12 - Variables required for WIES12                  *
.*                                                                            *
.* Files Required Open :                                                      *
.*        PATASFFD  PATASFA1 - Admission special funding arrangement file     *
.*        PATCODFD  PATCODE1 - Codes file                                     *
.*        PATECDFD  PATECDA2 - Episode disease coding file                    *
.*        PATECOFD  PATECOA2 - Episode operation coding file                  *
.*        PAT10OFD  PATI10O1 - ICD10 operations file                          *
.*        PATICUFD  PATICUA1 - ICU file                                       *
.*        PATTRNFD  PATTRAN2 - Transfer file                                  *
.*        PATW12FD  PATW12A1 - Victorian cost weights (for WIES12) file       *
.*                                                                            *
.* Parameters Req : Successful read on admission file                         *
.*                  Successful read on discharge file                         *
.*                  HIHLOS   - for Hopsipta in Home LOS (from RHIHDAYS)       *
.*                  NBRDAYS  - for LOS calculation (from NHOSPDAY)            *
.*                  PNSWRACE - User defined field for race                    *
.*                  WIS12DRG - DRG to calculate WIES12 for                    *
.*                                                                            *
.* Returns        : TOTWIS12 - Total WIES12 value                             *
.*                  WIS12DRG - DRG used to calculate WIES12                   *
.*                  WIS12LOS - LOS used to calculate WIES12                   *
.*                  WIS12WGT - Weight used to calculate WIES12                *
.*                  WIS12DSC - Description of DRG weight used                 *
.*                                                                            *
.* Modifications  :                                                           *
.*        V12.00.01 22/05/2025  Alina Bhari   TSK 0955096                     *
.*                  Added alpanumeric visit number generation                 *
.*                 -P12IT100,C12TR100,R12IO100,H12TE100,E12RA100              *
.*                 -D12PS200,T12AL100,A12AA100,A12SD100,C12OL100              *
.*        V10.02.01 28/03/2011  Mike Laming   CAR 240107                      *
.*                  Mods to PATECDaf & PATECOaf variables & Keys - file change*
.*        V9.04.03  25/07/2005  Peter Vela       CAR 66960                    *
.*                  Added 2005/2006 WIES12 file (patw12bf)                    *
.*        V9.04.02  01/09/2004  Henry son        CAR 53126                    *
.*                  Further changes for WIES12.                               *
.*                  Diagnosis or procedure incompatible with SEX code.        *
.*        V9.03.01  23/07/2004  Henry son        CAR 51504                    *
.*                  Modified from PATWIS11 for 2004/2005 changes              *
.******************************************************************************
+
PATWIS12
WIS12000  CALL      IN12W000                * Initialise variables
          BRANCH    EXIT,WIS12950
.
          CALL      C12TY000                * Determine and Check Care Type
          BRANCH    EXIT,WIS12950
.
          CALL      C12TR000                * Check for Contracted Patient
          BRANCH    EXIT,WIS12950
.
          CALL      M12RG000                * Modify DRG if necessary
.
          CALL      C12RG000                * Check for problem DRG
          BRANCH    EXIT,WIS12950
.
          CALL      M12CH000                * Check for Mechanical Ventilation
.
          CALL      T12AL000                * Check for Thalessaemia
.
          CALL      A12AA000                * Check for AAA
.
          CALL      A12SD000                * Check for ASD
.
          CALL      C12OL000                * Check for Colonoscopy
.
          CALL      B12SW000                * Calculate Base WIES12
.
          CALL      C12AB000                * Check for aboriginality
.
. ---- Calculate WIES12 value ----
.
          MOVE      TOTBWIES,TOTWIS12       * base WIES
          ADD       MVCOPAY,TOTWIS12        * mechv copayment
          ADD       THCOPAY,TOTWIS12        * thal  copayment
          ADD       AAACOPAY,TOTWIS12       * AAA   copayment
          ADD       ASDCOPAY,TOTWIS12       * ASD   copayment
          ADD       COLCOPAY,TOTWIS12       * colon copayment
          ADD       ATSIWIES,TOTWIS12       * ATSI  copayment
.
          MOVE      ZERO,EXIT
          GOTO      WIS12999
.
WIS12950  MOVE      ONE,EXIT
WIS12999  RETURN
+
.******************************************************************************
.*                                  IN12W000              Called by: WIS12000 *
.*                         Initialise WIES12 variables                        *
.******************************************************************************
IN12W000  MOVE      ZERO,TOTWIS12
          MOVE      ZERO,WIS12LOS
          MOVE      ZERO,WIS12WGT
          MOVE      SP20,WIS12DSC
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATW12A1,"patw12af"
          TRAPCLR   IO
          BRANCH    OVRCD,IN12W950
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      PATW12B1,"patw12bf"
          TRAPCLR   IO
          BRANCH    OVRCD,IN12W950
.
IN12W900  MOVE      ZERO,EXIT
          GOTO      IN12W999
.
IN12W950  MOVE      ONE,EXIT
IN12W999  RETURN
+
.******************************************************************************
.*                                  C12TY000              Called by: WIS12000 *
.*                 Check for valid Care Type & Incomplete Coding              *
.******************************************************************************
C12TY000  PACK     KEY5,ANSC,ANSC,ACARE     * use admission care type
          CALL     RDCODE1
          BRANCH   OVRCD,C12TY900
.
          MATCH    "6",TCINDC4              * Check Program Funding Source
          GOTO     C12TY900 IF NOT EQUAL
.
          MATCH    "0   ",THCSCOD           * check care types
          GOTO     C12TY900 IF EQUAL
          MATCH    "1   ",THCSCOD
          GOTO     C12TY900 IF EQUAL
          MATCH    "2   ",THCSCOD
          GOTO     C12TY900 IF EQUAL
          MATCH    "6   ",THCSCOD
          GOTO     C12TY900 IF EQUAL
          MATCH    "7   ",THCSCOD
          GOTO     C12TY900 IF EQUAL
          MATCH    "8   ",THCSCOD
          GOTO     C12TY900 IF EQUAL
          MATCH    "9   ",THCSCOD
          GOTO     C12TY900 IF EQUAL
          MATCH    "5   ",THCSCOD
          GOTO     C12TY900 IF EQUAL
          MATCH    "3   ",THCSCOD
          GOTO     C12TY900 IF EQUAL
          MATCH    "E   ",THCSCOD
          GOTO     C12TY900 IF EQUAL
          MATCH    "F   ",THCSCOD
          GOTO     C12TY900 IF EQUAL
          MATCH    "J   ",THCSCOD
          GOTO     C12TY900 IF EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     C12TY999
.
C12TY900  MOVE     ONE,EXIT                 * Not valid
C12TY999  RETURN
+
.******************************************************************************
.*                                  C12TR000              Called by: WIS12000 *
.*                        Check for Contracted Patient                        *
.******************************************************************************
C12TR000  PACK     KEY25,AADMNO,ANSA,SP30
          CALL     RSPTASF1
C12TR100  CALL     RKPTASF1                 * Read adm. special funding file
          BRANCH   OVRCD,C12TR800           * still process
.
          MATCH    AADMNO,PTAFADMN
          GOTO     C12TR800 IF NOT EQUAL    * still process
.
          MATCH    ANSA,PTAFTYPE            * special fund. arrang. type = adm
          GOTO     C12TR100 IF NOT EQUAL
.
          MATCH    ANSB,PTAFCROL            * Ignore if contract role = 'B'
          GOTO     C12TR900 IF EQUAL
.
C12TR800  MOVE     ZERO,EXIT                * valid
          GOTO     C12TR999
.
C12TR900  MOVE     ONE,EXIT                 * invalid
C12TR999  RETURN
+
.******************************************************************************
.*                                  M12RG000              Called by: WIS12000 *
.*                      Modify DRG if necessary for WIES12                    *
.******************************************************************************
M12RG000  CALL      P12IT000                * Peritoneal Dialysis
          CALL      R12IO000                * Radiotherapy
          CALL      B12EM000                * Bone Marrow Transplants
          CALL      H12TE000                * Hysterscopy Sterilisation
          CALL      M12TO000                * Mastoid Procedure
          CALL      E12RA000                * Extra Corporal Life Support
          CALL      D12PS000                * Diag. Proc. Sex incompat *I-53126
M12RG999  RETURN
+
.******************************************************************************
.*                                  P12IT000              Called by: M12RG000 *
.*                      Modify DRG if Peritoneal Dialysis                     *
.******************************************************************************
P12IT000  PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
P12IT100  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,P12IT999
.
          MATCH     AADMNO,PTEDADMN
          GOTO      P12IT999 IF NOT EQUAL
.
          MATCH     CMDISP,PTEDTYPE         * want principal diagnosis
          GOTO      P12IT100 IF NOT EQUAL
.
          MATCH     "Z49.2",PTEDCODE
          GOTO      P12IT100 IF NOT EQUAL
.
          MOVE      "L61Y",WIS12DRG         * modified for WIES12
P12IT999  RETURN
+
.******************************************************************************
.*                                  R12IO000              Called by: M12RG000 *
.*                         Modify DRG if Radiotherapy                         *
.******************************************************************************
R12IO000  PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
R12IO100  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,R12IO999
.
          MATCH     AADMNO,PTEDADMN         * same admission?
          GOTO      R12IO999 IF NOT EQUAL
.
          MATCH     "Z51.0",PTEDCODE        * radiotherapy?
          GOTO      R12IO100 IF NOT EQUAL
.
          MOVE      "R64Z",WIS12DRG         * modified for WIES12
R12IO999  RETURN
+
.******************************************************************************
.*                                  B12EM000              Called by: M12RG000 *
.*                    Modify DRG if Bone Marrow Transplant                    *
.******************************************************************************
B12EM000  MATCH     "A04Z",WIS12DRG
          GOTO      B12EM999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
B12EM100  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,B12EM900
.
          MATCH     AADMNO,PTEOADMN
          GOTO      B12EM900 IF NOT EQUAL
.
          MATCH     "13706-00",PTEOCODE
          IF        !@EQUAL
            MATCH     "13706-06",PTEOCODE
            IF        !@EQUAL
              MATCH     "13706-09",PTEOCODE
              IF        !@EQUAL
                MATCH     "13706-10",PTEOCODE
                GOTO      B12EM100 IF NOT EQUAL
              ENDIF
            ENDIF
          ENDIF
.
          MOVE      "A04A",WIS12DRG         * new DRG = A04A
          GOTO      B12EM999
.
B12EM900  MOVE      "A04B",WIS12DRG         * new DRG = A04B
.
B12EM999  RETURN
+
.******************************************************************************
.*                                  H12TE000              Called by: M12RG000 *
.*                    Modify DRG if Hysterscopy Sterilisation                 *
.******************************************************************************
H12TE000  MATCH     "N09Z",WIS12DRG
          IF        !@EQUAL
            MATCH     "N10Z",WIS12DRG
            IF        !@EQUAL
              MATCH     "N11B",WIS12DRG
              IF        !@EQUAL
                MATCH     "O40Z",WIS12DRG
                IF        !@EQUAL
                  MATCH     "N08Z",WIS12DRG
                  IF        !@EQUAL
                    MATCH     "O05Z",WIS12DRG
                    GOTO      H12TE999 IF NOT EQUAL
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
H12TE100  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,H12TE999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      H12TE999 IF NOT EQUAL
.
          MATCH     "35688-01",PTEOCODE
          IF        !@EQUAL        
            GOTO      H12TE100 IF NOT EQUAL
          ENDIF
.
          MOVE      "N11C",WIS12DRG         * new DRG = N11C
          GOTO      H12TE999
.
H12TE999  RETURN
+
.******************************************************************************
.*                                  M12TO000              Called by: M12RG000 *
.*                    Modify DRG if Mastoid procedure                         *
.******************************************************************************
M12TO000  MATCH     "D06Z",WIS12DRG
          GOTO      M12TO999 IF NOT EQUAL
.
          MOVE      "D06B",WIS12DRG         * Other Sinus/mid/ear Proc.
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
M12TO100  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,M12TO999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      M12TO999 IF NOT EQUAL
.
          MATCH     "41545-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS12DRG       * Mastoid Procedure.
            GOTO      M12TO999
          ENDIF
.
          MATCH     "41551-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS12DRG       * Mastoid Procedure.
            GOTO       M12TO999
          ENDIF
.
          MATCH     "41554-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS12DRG       * Mastoid Procedure.
            GOTO      M12TO999
          ENDIF
.
          MATCH     "41557-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS12DRG       * Mastoid Procedure.
            GOTO      M12TO999
          ENDIF
.
          MATCH     "41557-03",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS12DRG       * Mastoid Procedure.
            GOTO      M12TO999
          ENDIF
.
          MATCH     "41560-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS12DRG       * Mastoid Procedure.
            GOTO      M12TO999
          ENDIF
.
          MATCH     "41563-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS12DRG       * Mastoid Procedure.
            GOTO      M12TO999
          ENDIF
.
          MATCH     "41564-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS12DRG       * Mastoid Procedure.
            GOTO      M12TO999
          ENDIF
.
          MATCH     "41566-00",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS12DRG       * Mastoid Procedure.
            GOTO      M12TO999
          ENDIF
.
          MATCH     "41566-01",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS12DRG       * Mastoid Procedure.
            GOTO      M12TO999
          ENDIF
.
          MATCH     "41566-02",PTEOCODE
          IF        @EQUAL
            MOVE      "D06A",WIS12DRG       * Mastoid Procedure.
            GOTO      M12TO999
          ENDIF
.
          GOTO      M12TO100
.
M12TO999  RETURN
+
.******************************************************************************
.*                                  E12RA000              Called by: M12RG000 *
.*                    Modify DRG if Extra Corporeal Life Support              *
.******************************************************************************
E12RA000  MATCH     "A01Z",WIS12DRG
          GOTO      E12RA999 IF EQUAL
.
          MATCH     "A03Z",WIS12DRG
          GOTO      E12RA999 IF EQUAL
.
          MATCH     "A05Z",WIS12DRG
          GOTO      E12RA999 IF EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
E12RA100  CALL      RKPTECO2                  episode operation file record
          BRANCH    OVRCD,E12RA999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      E12RA999 IF NOT EQUAL
.
          MATCH     "90225-00",PTEOCODE
          IF        @EQUAL
            MOVE      "A40Z",WIS12DRG   
            GOTO      E12RA999
          ENDIF
.
          MATCH     "38615-00",PTEOCODE
          IF        @EQUAL
            MOVE      "A40Z",WIS12DRG   
            GOTO      E12RA999
          ENDIF
.
          MATCH     "38615-01",PTEOCODE
          IF        @EQUAL
            MOVE      "A40Z",WIS12DRG   
            GOTO      E12RA999
          ENDIF
.
          MATCH     "38618-00",PTEOCODE
          IF        @EQUAL
            MOVE      "A40Z",WIS12DRG   
            GOTO      E12RA999
          ENDIF
.
          GOTO      E12RA100
.
E12RA999  RETURN
+
.******************************************************************************
.*                                  D12PS000              Called by: M12RG000 *
.*                    Modify DRG if Diagnosis or Procedure incompatible Sex   *
.******************************************************************************
D12PS000  MATCH     "901Z",WIS12DRG                                    *I-53126
          GOTO      D12PS999 IF NOT EQUAL
.
D12PS100  PACK      KEY16,AADMNO,SP20
          CALL      RSPTECD2                * Position on & read a patient
D12PS200  CALL      RKPTECD2                  episode disease file record
          BRANCH    OVRCD,D12PS999
.
          MATCH     AADMNO,PTEDADMN         * same admission?
          GOTO      D12PS999 IF NOT EQUAL
.
          MATCH     "E25.0",PTEDCODE        * is there a sex explin.
          GOTO      D12PS800 IF EQUAL       * yes
.
          MATCH     "E25.8",PTEDCODE        * is there a sex explin.
          GOTO      D12PS800 IF EQUAL       * yes
.
          MATCH     "E29.1",PTEDCODE        * is there a sex explin.
          GOTO      D12PS800 IF EQUAL       * yes
.
          MATCH     "E34.5",PTEDCODE        * is there a sex explin.
          GOTO      D12PS800 IF EQUAL       * yes
.
          MATCH     "F64.0",PTEDCODE        * is there a sex explin.
          GOTO      D12PS800 IF EQUAL       * yes
.
          MATCH     "Q56.0",PTEDCODE        * is there a sex explin.
          GOTO      D12PS800 IF EQUAL       * yes
.
          MATCH     "Q56.1",PTEDCODE        * is there a sex explin.
          GOTO      D12PS800 IF EQUAL       * yes
.
          MATCH     "Q56.2",PTEDCODE        * is there a sex explin.
          GOTO      D12PS800 IF EQUAL       * yes
.
          MATCH     "Q56.3",PTEDCODE        * is there a sex explin.
          GOTO      D12PS800 IF EQUAL       * yes
.
          MATCH     "Q56.4",PTEDCODE        * is there a sex explin.
          GOTO      D12PS800 IF EQUAL       * yes
.
          MATCH     "Q99.0",PTEDCODE        * is there a sex explin.
          GOTO      D12PS800 IF EQUAL       * yes
.
          MATCH     "Q99.1",PTEDCODE        * is there a sex explin.
          GOTO      D12PS800 IF EQUAL       * yes
.
          GOTO      D12PS200                * no
.
D12PS800  MOVE      "964Z",WIS12DRG
.
D12PS999  RETURN
+
.******************************************************************************
.*                                  C12RG000              Called by: WIS12000 *
.*                            Check for Problem DRG                           *
.******************************************************************************
C12RG000  PACK     KEY4,WIS12DRG
          MATCH    "20050701",DDATE
          IF       !@LESS
            CALL     RDPT12B1                 * check WIES12 weights file 05/06
            BRANCH   OVRCD,C12RG900
          ELSE
            CALL     RDPT121                  * check WIES12 weights file
            BRANCH   OVRCD,C12RG900
          ENDIF
.
          MATCH    "960Z",PT12DRGC
          GOTO     C12RG900 IF EQUAL
.
          MATCH    "961Z",PT12DRGC
          GOTO     C12RG900 IF EQUAL
.
          MATCH    "962Z",PT12DRGC
          GOTO     C12RG900 IF EQUAL
.
          MATCH    "963Z",PT12DRGC
          GOTO     C12RG900 IF EQUAL
.
          MOVE     ZERO,EXIT
          GOTO     C12RG999
.
C12RG900  MOVE     ONE,EXIT
C12RG999  RETURN
+
.******************************************************************************
.*                                  M12CH000              Called by: WIS12000 *
.*                Calculate Mechanical Ventialtion Co-payment                 *
.******************************************************************************
M12CH000  MOVE      ZERO,ADJMVDAY
          MOVE      ZERO,MVCOPAY
.
          PACK      KEY8,AADMNO
          CALL      RDPTICU1                * get mech. vent. hours
          BRANCH    OVRCD,M12CH999
.
          MATCH     "D",PT12MVEL            * check mech. vent. eligibility
          IF        @EQUAL
            IF        PTICUMEC > 6
              ASSIGN    ((PTICUMEC+12)/24),FORM12P4    * days receiving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7729)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
            GOTO      M12CH999
          ENDIF
.
          MATCH     "N",PT12MVEL            * check mech. vent. eligibility
          IF        @EQUAL
            IF        PTICUMEC > 6
              ASSIGN    ((PTICUMEC+12)/24),FORM12P4    * days receiving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7729)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
            GOTO      M12CH999
          ENDIF
.
          MATCH     "4",PT12MVEL
          IF        @EQUAL
            IF        PTICUMEC > 96
              ASSIGN    (((PTICUMEC+12)/24)-4),FORM12P4   * days recving copay.
              MOVE      FORM12P4,ADJMVDAY
              ASSIGN    ((ADJMVDAY*0.7729)+0.6980),MVCOPAY  * mech. vent. copay.
            ENDIF
          ENDIF
.
M12CH999  RETURN
+
.******************************************************************************
.*                                  T12AL000              Called by: WIS12000 *
.*                      Calculate Thalessaemia Co-payment                     *
.******************************************************************************
T12AL000  MOVE      ZERO,THCOPAY
          MATCH     "T",PT12CPAY            * check WIES12 weights file
          IF        @EQUAL
            PACK      KEY16,AADMNO,SP20
            CALL      RSPTECD2              * Position on & read a patient
T12AL100    CALL      RKPTECD2                episode disease file record
            BRANCH    OVRCD,T12AL999
.
            MATCH     AADMNO,PTEDADMN
            GOTO      T12AL999 IF NOT EQUAL
.
            MATCH     "D56",PTEDCODE        * check ICD-10 code
            IF        !@EQUAL
              MATCH     "D57.2",PTEDCODE    * check ICD-10 code
              GOTO      T12AL100 IF NOT EQUAL
            ENDIF
            MOVE      "0.2648",THCOPAY      * eligible for thal. copayment
          ENDIF
T12AL999  RETURN
+
.******************************************************************************
.*                                  A12AA000              Called by: WIS12000 *
.*                          Calculate AAA Co-payment                          *
.******************************************************************************
A12AA000  MOVE      ZERO,AAACOPAY
          MATCH     "AAA",PT12CPAY          * check WIES12 weights file
          GOTO      A12AA999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
A12AA100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,A12AA999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      A12AA999 IF NOT EQUAL
.
          MATCH     "33116-00",PTEOCODE     * check ICD-10 code
          GOTO      A12AA100 IF NOT EQUAL
.
          MOVE      "3.1421",AAACOPAY       * eligible for AAA copayment
A12AA999  RETURN
+
.******************************************************************************
.*                                  A12SD000              Called by: WIS12000 *
.*                          Calculate ASD Co-payment                          *
.******************************************************************************
A12SD000  MOVE      ZERO,ASDCOPAY
          MATCH     "ASD",PT12CPAY          * check WIES12 weights file
          GOTO      A12SD999 IF NOT EQUAL
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
A12SD100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,A12SD999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      A12SD999 IF NOT EQUAL
.
          MATCH     "38742-00",PTEOCODE     * check ICD-10 code
          GOTO      A12SD100 IF NOT EQUAL
.
          MOVE      "2.4713",ASDCOPAY       * eligible for ASD copayment
A12SD999  RETURN
+
.******************************************************************************
.*                                  C12OL000              Called by: WIS12000 *
.*                       Calculate Colonoscopy Co-payment                     *
.******************************************************************************
C12OL000  MOVE      ZERO,COLCOPAY
          MATCH     "Col",PT12CPAY          * check WIES12 weights file
          GOTO      C12OL999 IF NOT EQUAL
.
          CALL      LDCOB000                * load col. copayment codes
.
          PACK      KEY16,AADMNO,SP20
          CALL      RSPTECO2                * Position on & read a patient
C12OL100  CALL      RKPTECO2                * episode operation file record
          BRANCH    OVRCD,C12OL999
.
          MATCH     AADMNO,PTEOADMN
          GOTO      C12OL999 IF NOT EQUAL
.
          MOVE      ZERO,COUNT
C12OL200  ADD       ONE,COUNT
          COMPARE   TEN2,COUNT
          GOTO      C12OL100 IF EQUAL       * check next code on patecoaf
.
          MATCH     COLCODES[COUNT],PTEOCODE     * check ICD-10 code
          GOTO      C12OL200 IF NOT EQUAL
.
          MOVE      "0.1765",COLCOPAY       * eligible for Colono. copayment
C12OL999  RETURN
+
.******************************************************************************
.*                                  LDCOB000              Called by: C12OL000 *
.*                      Load Colonoscopy Copayment Codes                      *
.******************************************************************************
LDCOB000  MOVE      "30479-01",COLCODES[1]
          MOVE      "30479-02",COLCODES[2]
          MOVE      "90308-00",COLCODES[3]
          MOVE      "90312-01",COLCODES[4]
          MOVE      "32084-00",COLCODES[5]
          MOVE      "32084-01",COLCODES[6]
          MOVE      "32087-00",COLCODES[7]
          MOVE      "32090-00",COLCODES[8]
          MOVE      "32090-01",COLCODES[9]
          MOVE      "32093-00",COLCODES[10]
          MOVE      "32094-00",COLCODES[11]
LDCOB999  RETURN
+
.******************************************************************************
.*                                  B12SW000              Called by: WIS12000 *
.*                            Calculate Base WIES12                           *
.******************************************************************************
B12SW000  CALL      D12SC000                * length of stay category
          CALL      C12ST000                * calculate inlier status
.
          MATCH     "L",W12NLIER            * low outlier
          CALL      BW12L000 IF EQUAL       * calculate base WIES12
.
          MATCH     "I",W12NLIER            * inlier
          CALL      BW12N000 IF EQUAL       * calculate base WIES12
.
          MATCH     "H",W12NLIER            * high outlier
          CALL      BW12H000 IF EQUAL       * calculate base WIES12
.
B12SW999  RETURN
+
.******************************************************************************
.*                                  D12SC000              Called by: B12SW000 *
.*                      Determine Length Of Stay Category                     *
.******************************************************************************
D12SC000  MOVE      NBRDAYS,WIS12LOS        * length of stay from NHOSPDAY
          MOVE      "M",LOSCAT              * set LOS category to multiday
          MATCH     ADATE,DDATE             * check for sameday
          IF        @EQUAL
            MOVE      ONE,SAMEDAY           * sameday
            MOVE      ONE,WIS12LOS          * LOS = 1
            MOVE      "S",LOSCAT            * set LOS category to sameday
            GOTO      D12SC999
          ELSE
            MOVE      ZERO,SAMEDAY          * not sameday
          ENDIF
.
          IF        SAMEDAY = 0 & WIS12LOS = 1
            MOVE      "O",LOSCAT            * set LOS category to oneday
            GOTO      D12SC999
          ENDIF
.
          IF        WIS12LOS > 1825
            MOVE      "1825",WIS12LOS       * maximum LOS is 5 years
          ENDIF
.
D12SC999  RETURN
+
.******************************************************************************
.*                                  C12ST000              Called by: B12SW000 *
.*                           Calculate Inlier Status                          *
.******************************************************************************
C12ST000  MOVE      "I",W12NLIER            * set status to inlier
          IF        WIS12LOS < PT12LIBP
            MOVE      "L",W12NLIER          * set status to low outlier
            GOTO      C12ST999
          ENDIF
.
          ASSIGN    (PT12HIBP+ADJMVDAY),FORM12P4   * high bound. + mech.v days
          IF        WIS12LOS > FORM12P4
            MOVE      "H",W12NLIER          * set status to high outlier
          ENDIF
C12ST999  RETURN
+
.******************************************************************************
.*                                  BW12L000              Called by: B12SW000 *
.*                     Calculate Base WIES12 for Low Outlier                  *
.******************************************************************************
BW12L000  MATCH     "S",LOSCAT            * sameday
          IF        @EQUAL
            MOVE      PT12TSDW,TOTBWIES             * base WIES12
            ASSIGN    (TOTBWIES/PT12TIMW),TOTIES12  * IES12
.
            MOVE      PT12TSDW,WIS12WGT             * for display
            MOVE      "Same Day Wgt ",WIS12DSC
            GOTO      BW12L999
          ENDIF
.
          MATCH     "O",LOSCAT            * oneday
          IF        @EQUAL
            MOVE      PT12TODW,TOTBWIES             * base WIES12
            ASSIGN    (TOTBWIES/PT12TIMW),TOTIES12  * IES12
.
            MOVE      PT12TODW,WIS12WGT             * for display
            MOVE      "One Day Wgt  ",WIS12DSC
            GOTO      BW12L999
          ENDIF
.
          MATCH     "M",LOSCAT            * multiday
          IF        @EQUAL
            ASSIGN    (PT12TODW+((WIS12LOS-1)*PT12TLOM)),TOTBWIES
.                                                    * base WIES12
            ASSIGN    (TOTBWIES/PT12TIMW),TOTIES12   * IES12
.
            MOVE      PT12TLOM,WIS12WGT             * for display
            MOVE      "Low Outlr Wgt",WIS12DSC
            GOTO      BW12L999
          ENDIF
BW12L999  RETURN
+
.******************************************************************************
.*                                  BW12N000              Called by: B12SW000 *
.*                      Calculate Base WIES12 for Inlier                      *
.******************************************************************************
BW12N000  MOVE      ONE,TOTIES12          * IES12 = 1
          MATCH     "S",LOSCAT            * sameday
          IF        @EQUAL
            MOVE      PT12TSDW,TOTBWIES   * base WIES12
.
            MOVE      PT12TSDW,WIS12WGT             * for display
            MOVE      "Same Day Wgt ",WIS12DSC
            GOTO      BW12N999
          ENDIF
.
          MATCH     "O",LOSCAT            * oneday
          IF        @EQUAL
            MOVE      PT12TODW,TOTBWIES   * base WIES12
.
            MOVE      PT12TODW,WIS12WGT             * for display
            MOVE      "One Day Wgt  ",WIS12DSC
            GOTO      BW12N999
          ENDIF
.
          MATCH     "M",LOSCAT            * multiday
          IF        @EQUAL
            MOVE      PT12TIMW,TOTBWIES   * base WIES12
.
            MOVE      PT12TIMW,WIS12WGT             * for display
            MOVE      "Inlier Wgt   ",WIS12DSC
            GOTO      BW12N999
          ENDIF
BW12N999  RETURN
+
.******************************************************************************
.*                                  BW12H000              Called by: B12SW000 *
.*                    Calculate Base WIES12 for High Outlier                  *
.******************************************************************************
BW12H000  ASSIGN    ((WIS12LOS-PT12HIBP)-ADJMVDAY),HIGHDAYS  * high outlr days
          COMPARE   ZERO,HIGHDAYS
          IF        @LESS
            MOVE      ZERO,HIGHDAYS         * highdays must be >= zero
          ENDIF
.
          COMPARE   HIGHDAYS,HIHLOS
          IF        @LESS
            MOVE      HIHLOS,HITHDAYS       * get lesser of highdays and
          ELSE                              *  HIHLOS
            MOVE      HIGHDAYS,HITHDAYS
          ENDIF
.
          ASSIGN    (HIGHDAYS-HITHDAYS)*PT12THOM,TOTBWIES    *high WIES
          ASSIGN    (HITHDAYS*PT12THIH)+TOTBWIES,TOTBWIES    *HITH WEIS
          ASSIGN    (PT12TIMW+TOTBWIES),TOTBWIES             *base WEIS12
          ASSIGN    (TOTBWIES/PT12TIMW),TOTIES12             * IES12
.
          MOVE      PT12THOM,WIS12WGT                        * for display
          MOVE      "High Outl Wgt",WIS12DSC
BW12H999  RETURN
+
.******************************************************************************
.*                                  C12AB000              Called by: WIS12000 *
.*                          Check for Aboriginality                           *
.******************************************************************************
C12AB000  MOVE      ZERO,ATSIWIES                * set ATSI WIES to zero
          IF        PNSWRACE<1 | PNSWRACE>14
            GOTO      C12AB999              * Invalid parameter
          ENDIF
.
          LOAD      TCODE,PNSWRACE,CATP1,CATP2,CATP3,CATP4,CATP5:
                                   CATP6,CATA1,CATA2,CATA3,CATA4:
                                   CATA5,CATA6,CATA7,CATVA
          LOAD      ACODE,PNSWRACE,PUSR1,PUSR2,PUSR3,PUSR4,PUSR5:
                                   PUSR6,AUSR1,AUSR2,AUSR3,AUSR4:
                                   AUSR5,AUSR6,AUSR7,PMPXABRG
.
          MATCH     ACODE,SP3
          GOTO      C12AB999 IF EQUAL
.
          PACK      KEY5,TCODE,ACODE
          CALL      RDCODE1                 * Read a code file record
          BRANCH    OVRCD,C12AB999
.
          MOVE      THCSCOD,KEY1
          TYPE      KEY1
          GOTO      C12AB999 IF NOT EQUAL   * Non numeric, error
.
          MOVE      KEY1,FORM1
.
. ---- Calculate ATSI Loading if aboriginality = 5, 6, or 7 ----
.
          IF        FORM1=5 | FORM1=6 | FORM1=7
            MOVE      TOTBWIES,FORM12P4       * base WIES
            ADD       MVCOPAY,FORM12P4        * mechv copayment
            ADD       THCOPAY,FORM12P4        * thal  copayment
            ADD       AAACOPAY,FORM12P4       * AAA   copayment
            ADD       ASDCOPAY,FORM12P4       * ASD   copayment
            ADD       COLCOPAY,FORM12P4       * colon copayment
            ASSIGN    (0.3*FORM12P4),ATSIWIES
          ENDIF
.
C12AB999  RETURN
+
