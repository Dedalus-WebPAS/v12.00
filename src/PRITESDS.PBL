.  ****************************************************************************
.  *                                                                          *
.  *  Program Name     :  PRITESDS.PBL                                        *
.  *                                                                          *
.  *  Function         :  Displays the pathology tests on file.               *
.  *                                                                          *
.  *  Author           :  Stephen Armstrong      28/12/90                     *
.  *                                                                          *
.  *  Keys             :  KEY47                                               *
.  *                                                                          *
.  *  Requirements     :  SERDATE   DIM 8 - service date                      *
.  *                      ZED8      INIT "zzzzzzzz"                           *
.  *                      TESTDESC  DIM 30 - test description                 *
.  *                      DFLAG     FORM 1 - date flag                        *
.  *                                  0 = display codes for service date      *
.  *                                  1 = display codes regardless            *
.  *                      SHTDESC   DIM 25 - short test description           *
.  *                                                                          *
.  *  Returns          :  OVRCD = 1 if nothing on file                        *
.  *                                                                          *
.  *  Modifications    :                                                      *
.  *                                                                          *
.  ****************************************************************************
.
PRITESDS  
          MOVE      ONE,CCOL
          DISPLAY   *P1:4,*EF,*P29:4,*V2LON,*ULON,"PATHOLOGY TESTS"
.
          KEYIN     *P5:6,"Description : ",*DV,UNDLN30:
                    *P19:6,*V2LON,TESTDESC
.
          RESET     TESTDESC
          IF        @EOS
             PACK   KEY47,SP30,SP20              * nothing input
          ELSE
             PACK   KEY47,TESTDESC,SP30,SP20     * description input
          ENDIF
.
          DISPLAY   *P1:5,*EF,*V2LON,"Description",*P29:5,"Code":
                    *P40:5,*EF,*V2LON,"Description",*P68:5,"Code"
          MOVE      FIVE,CVERT
.
          CALL      RSPRTE3                      * position in file
          CALL      RKPRTE3                      * read next record
          BRANCH    OVRCD,TESDS950               * end of file
.
          GOTO      TESDS120
.
TESDS100  CALL      RKPRTE3                      * read next record
          BRANCH    OVRCD,TESDS300
.
TESDS120  BRANCH    DFLAG,TESDS130               * ignore test date
          MATCH     PRTEDATE,SERDATE             * service date less than 
.                                                  effective date ?
          GOTO      TESDS100 IF LESS             * yes - get next record
.
TESDS130  ADD       ONE,CVERT                    * increment line count
          COMPARE   TWENTY3,CVERT                * Page full ?
          GOTO      TESDS150 IF LESS             * no
.
          COMPARE   ONE,CCOL                     * second half of screen full ?
          GOTO      TESDS200 IF NOT EQUAL        * yes
.
          MOVE      FORTY,CCOL                   * set up for second half
.                                                  of screen display
          MOVE      SIX,CVERT
.
TESDS150  MOVE      PRTEDESC,SHTDESC
          DISPLAY   *PCCOL:CVERT,SHTDESC,SP3,PRTECODE
.
.         Position at the end of this description/test code, ready to read the
.         next entry
.
          PACK      KEY47,PRTEDESC,PRTECODE,ZED8
          CALL      RSPRTE3                      * position in file
          GOTO      TESDS100
.
.         Page full
.
TESDS200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, ":
                    "(",*V2LON,"F",*HOFF,")irst Screen, ":
                    "(",*V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS
.
          ENDSET    ANS
          APPEND    SP1,ANS
          RESET     ANS
          REP       UPPLOW,ANS
.
          CMATCH    ANSC,ANS                     * continue ?
          GOTO      TESDS300 IF EQUAL
.
          CMATCH    ANSF,ANS                     * first screen ?
          GOTO      PRITESDS IF EQUAL
.
          CMATCH    ANSN,ANS                     * next screen ?
          GOTO      TESDS400 IF EQUAL
.
          BEEP                                   * invalid
          GOTO      TESDS200 
.
TESDS300  MOVE      ZERO,OVRCD
          GOTO      TESDS999
.
.         Clear the screen
.
TESDS400  MOVE      SIX,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:CVERT,*EF
.
          GOTO      TESDS150
.
TESDS950  DISPLAY   *P1:24,*EL,*B,"No tests found. ";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL;
.
TESDS999  RETURN
