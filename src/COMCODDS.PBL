.
.     COMCODDS : Display Codes File Entries
.
.     This include displays entries in the codes file for a given
.      category.
.                   Input variable : CODE  contains category
.                                    CODEDATE  contains date for comparison
.
.     Modifications:                             
.
COMCODDS  OPEN      COMCODA2,"comcodaf"
          PACK      KEY5,CODE,SP5
          CALL      RDCMCOD1
          BRANCH    OVRCD OF DISPCCD
.
.         Display Category Heading, and then loop over entries
.
          DISPLAY   *P1:3,*EF,*P20:4,*V2LON,*ULON,CODE,": ",CMCDDESC
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
.
.         TRAP      ENDLP0 IF F1
.
          PACK      PTCDKEY2,CODE,SP30
          CALL      RSCMCOD1
DISPLPC1  CALL      RKCMCOD1
          BRANCH    OVRCD OF ENDLPC1
.
          MATCH     CMCDTCOD,CODE
          GOTO      ENDLPC1 IF NOT EQUAL
.
          MATCH     SP3,CMCDACOD
          GOTO      DISPLPC1 IF EQUAL
.
          MATCH     SP70,CMCDFMDT
          IF        !@EQUAL
            MATCH     CMCDFMDT,CODEDATE          * inactive ?
            GOTO      DISPLPC1 IF LESS
            MATCH     SP70,CMCDTODT
            IF        !@EQUAL
              MATCH     CODEDATE,CMCDTODT
              GOTO      DISPLPC1 IF LESS
            ENDIF
          ENDIF
.
          DISPLAY   *PCCOL:CVERT,*V2LON,CMCDACOD,*HOFF,SP2,CMCDDESC;
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      DISPLPC1 IF LESS
.
          MOVE      FIVE,CVERT
          ADD       TWENTY6,CCOL
          COMPARE   "70",CCOL
          GOTO      DISPLPC1 IF LESS
.
.         Check for new standards question, or old question
.
DISPLPC2  BRANCH    CNEWDS,DSCOE200
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       "eEfFnN",ANS
          CMATCH    ANSE,ANS
          GOTO      ENDLPC2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      COMCODDS IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      DSCOE400 IF EQUAL
.
          BEEP
          GOTO      DISPLPC2
.
.         Use the new standards question
.
DSCOE200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       "cCfFnN",ANS
          CMATCH    ANSC,ANS
          GOTO      ENDLPC2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      COMCODDS IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      DSCOE400 IF EQUAL
.
          BEEP
          GOTO      DSCOE200
.
.         Clear the screen
.
DSCOE400  MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
.
.         TRAP      ENDLPC0 IF F1
.
          GOTO      DISPLPC1
.
.       IF MISSING CATEGORY SHOW  CODE MISSING
.
DISPCCD   DISPLAY   *P1:3,*EF,*P30:4,*V2LON,*BLINKON:
                    "Category : ",CODE," missing in Common Codes Master File"
          GOTO      ENDLPC1
.
ENDLPC0    NORETURN
.         TRAPCLR   F1
.
.      End of codes
.
ENDLPC1   BRANCH    CNEWDS,ENDLPC2
.
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
.
ENDLPC2   MOVE      ZERO,OVRCD
          CLOSE     COMCODA2
          RETURN
