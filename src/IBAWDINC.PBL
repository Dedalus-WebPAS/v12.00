.
.   This include checks passwords for all programs
.        For programs that are not printer type 0, ask user for printer name
.
.        The following includes are needed :
.        INC        IBACODFD
.        INC        IBAPORFD
.        INC        IBAPRNFD
.        INC        IBASECUR
.
.
.
.  Modified         31/01/89    Steve O'Gorman
.                   Added Loop to Check for a Number of Illegal entries
.                   and if found chain the ANSWER
.                   Write to an Audit File if Illegal Access Attempted
.
.
.
CHECKPW   MOVE      ZERO TO CNT
.
          MOVE      SP30,PSECDESC
          MOVE      PROGNAME,KEY8
          CALL      RDSECU1
          BRANCH    OVRCD OF PW1
.
          COMPARE   PSECLEV TO SECLEV
          GOTO      CLEARPW IF NOT LESS
.
          COMPARE   ZERO,CIMENAUD
          GOTO      NOWRAUDT IF EQUAL
.
. -----   Write to Audit File when Access Denied to the Program  -----
.
          OPEN      IBAIMEN1,"ibaimenf"
.
          MOVE      PASSCODE,IBIMUSER
          PACK      IBIMDATE,CCC,CYY,CMM,CDD
          REP       " 0",IBIMDATE
          CLOCK     TIME,IBIMTIME
          MOVE      PORT,IBIMPORT
          MOVE      PROGNAME,IBIMPROG
          MOVE      SP30,IBIMSPAR
.
          PACK      KEY22,IBIMUSER,IBIMDATE,IBIMTIME,IBIMPORT
          CALL      WRIBIMN1
.
          CLOSE     IBAIMEN1
NOWRAUDT
          COMPARE   ZERO,CSECCNT                 * No Counter on Illegal
          GOTO      NOCLEAR IF EQUAL             * Security Levels in Menu's
.
          SUB       ONE,CSECCNT                  * Check Number of Illeg. Access
          COMPARE   CSECCNT,ZERO
          GOTO      NOCLEAR IF LESS
.
          CHAIN     SPNAME                       * Chain out if number of illeg.
.                                                * Accesses = the system. param.
CLEARPW
          MATCH     SP8,PSECPW
          GOTO      PW3 IF EQUAL
.
PW2       MOVE      SP8,PASSCHK
          KEYIN     *ES,*P31:24,*EOFF,*V2LON,"Enter Password : ",PASSCHK;
          RESET     PASSCHK TO 8
          RESET     PASSCHK
          MATCH     PSECPW TO PASSCHK
          GOTO      PW3 IF EQUAL
.
          BEEP
          ADD       ONE TO CNT
          COMPARE   THREE TO CNT
          GOTO      PW2 IF NOT EQUAL
          CHAIN     "ANSWER"
.
.        IF PROGRAM NOT IN SECURITY FILE ....DISPLAY WARNING
.
PW1       MOVE      ONE,PPAPER           * Default of plain paper report
.
          MATCH     "1426",PASSCODE
          GOTO      PW3 IF NOT EQUAL
.
          DISPLAY   *ES,*P10:12,*B,*V2LON:
                    "**** Program ",PROGNAME," missing from ":
                    " Security File ****",*HOFF
.
          KEYIN     *P1:24,*EL,"Hit ",*V2LON,"<RETURN>",*HOFF:
                              " to continue ",*EOFF,ANS;
.
.         Determine print type, and if not zero, print heading
.
PW3       MOVE      SP8,SPLFILE
          MOVE      PPAPER,CPPAPER
          MOVE      PSECLEV,CPROGLEV
.
.     PORT SCANNING
.
         MOVE      PROGNAME,IBAPPROG
         MOVE      PSECDESC,IBAPDESC      *** REQUIRED BY IBASYS09
.
         MOVE      IBAPPROG,KEY8
         CALL      WRPORT1             *** I AM IN A PROGRAM
.
.     SYSTEM TRACKING FLAG
.
         BRANCH    USERFLAG OF DOCHAIN
         MOVE      IBAPPROG,SYSPROG
         CALL      WRSYSA1 
.
DOCHAIN  COMPARE   ONE,PPAPER
         CALL      OPNPRINT IF EQUAL
.
         TRAP      NOCHAIN IF CFAIL
         CHAIN     PROGNAME
.
NOCHAIN  NORETURN
         TRAPCLR   CFAIL
         BEEP
         KEYIN     *P1:24,*EL:
                   "Selection not Available. Please Contact":
                   " I.B.A If Required. Tap Enter ",ANS;
.
ENDIT    CHAIN     PGM
