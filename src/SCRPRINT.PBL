.******************************************************************************
.* System         : ALL with screen painting.                                 *
.*                                                                            *
.* Include Name   : SCRPRINT.PBL                                              *
.*                                                                            *
.* Function       : Print any screen painted screen.                          *
.*                                                                            *
.* Author         : Justin Coates                                             *
.*                                                                            *
.* Date Written   : 13/09/1991                                                *
.*                                                                            *
.* Parameters     : SCRID     DIM       2   screen number                     *
.*                  PAGELIMT  FORM      2   number of lines per page          *
.*                  CLNO      FORM      2   current line number               *
.*                                                                            *
.* Ext. Routines  : DISPVARS - set the variables SCSBDAT & SCSBATT with the   *
.*                  required variable and attribute in the desired positions  *
.*                  (This is the same routine as needed in 'SCRDISPL.PBL'     *
.*                  HEAD0000 - display page header                            *
.*                                                                            *
.* Returns        : EXIT = 1      no screen set up                            *
.*                                                                            *
.* Modifications  : 15/10/1992    Justin Coates                               *
.*                  dispvars should now use dispitem so it will set up the    *
.*                  VAR variable so have to add it to scpsdat in this routine *
.*                  22/12/1994   Whirlpool                                    *
.*                  Dispitem needs to clear VAR so now sets up PRINTVAR       *
.*                  13/12/1995    Justin Coates and Matt   (SUHT mods)        *
.*                  added compiler directives for printvar command            *
.******************************************************************************
SCRPRINT  MOVE      ZERO,HLEF
          CALL      GETSCR00                * get screen
          SUSPEND
.
.         display the Screen
.
          PACK      KEY12,PRGID,SCRID,SP2  * get screen background
          CALL      RSSCSB1                 * positional read
.
.         get the background information
.
SPPRN100  CALL      RKSCSB1                 * next read
          BRANCH    OVRCD,SPPRN400          * end of file
.
          MATCH     PRGID,SCSBPID
          GOTO      SPPRN400 IF NOT EQUAL   * different program
.
          MATCH     SCRID,SCSBSID
          GOTO      SPPRN400 IF NOT EQUAL   * different screen
.
.         get the file variables and add to DATA & ATTRIBUTES string
.
          PACK      KEY15,PRGID,SCRID,SCSBLIN,SP20
          CALL      RSSCPS1                 * positional read
.
SPPRN200  CALL      RKSCPS1                 * next read
          BRANCH    OVRCD,SPPRN300          * end of file
.
          MATCH     PRGID,SCPSPID
          GOTO      SPPRN300 IF NOT EQUAL   * different program id
.
          MATCH     SCRID,SCPSSID
          GOTO      SPPRN300 IF NOT EQUAL   * different screen id
.
          MATCH     SCSBLIN,SCPSROW
          GOTO      SPPRN300 IF NOT EQUAL   * different line number
.
          CALL      DISPVARS                * set up VAR & CCOL
          SETLPTR   PRINTVAR,SCPSLEN        * set length of print variable
          MOVE      SCPSCOL,CCOL            * set the column again
          SUB       ONE,CCOL                * get one before end
          RESET     SCSBDAT,CCOL            * set FP to one before end
          RESET     SCSBATT,CCOL            * set FP to one before end
          SETLPTR   SCSBDAT                 * set LL to end of string
          SETLPTR   SCSBATT                 * set LL to end of string
          APPEND    PRINTVAR,SCSBDAT        * add details after FP
.
          MOVE      ZERO,FORM2
          WHILE     FORM2<=SCPSLEN 
            APPEND    SCPSATT,SCSBATT        * add details after FP
            ADD       ONE,FORM2
          DO
.
          GOTO      SPPRN200
.
SPPRN300  SETLPTR   SCSBDAT                 * set FP to end of string
          RESET     SCSBDAT                 * reset FP to start of string
          SETLPTR   SCSBATT                 
          RESET     SCSBATT                
.
.         print a line of the screen
.
          COMPARE   PAGELIMT,CLNO
.         CALL      HEAD0000 IF NOT LESS    * print new screen
.
          REP       "~ ",SCSBDAT            * indicates a blank line
          REP       BARCODE,SCSBATT         * change any barcoded fields
.
.         if using TBC_7, then compile with a printvar
.        
          IFGE      $$VER,7
            PRINTVAR  SCSBDAT,SCSBATT
            PRINT     *N,*1;
          XIF
.
.         if using TBC_6, then compile with a print
.        
          IFEQ      $$VER,6
            PRINT     *1,SCSBDAT
          XIF
          ADD       ONE,CLNO                * add to line number
          GOTO      SPPRN100
.
.         end of background file
.
SPPRN400  MOVE      ZERO,EXIT
          CALL      PUTSCR00                * restore original screen
          FLUSH
.
SPPRN999  RETURN
+
