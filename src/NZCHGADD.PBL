.**************************************************************************
.*  Include   :   NZCHGADD.PBL                                            *
.*  Function  :   Change address after error found in format (NZHIS/IBA)  *
.*  Author    :   ROD                                                     *
.*  Date      :   last week                                               *
.*                                                                        *
.*  Parameters:   NZIBADD1,NZIBADD2,NZIBSUBR,NZIBCITY,NZIBCTRY            *
.*                                                                        *
.*  Returns   :   EXIT :  0=Post, 1=Cancel                                *
.*                                                                        *
.*  Mod's     :                                                           *
.*                                                                        *
.**************************************************************************
NZCHGADD  CALL      GETSCR00
          BOX       16,20,8,61,16
          BOXCLR    21,9,60,15
.
          DISPLAY   *P22:9,*V2LON,"1",*HOFF,". ",NZIBADD1:
                    *P22:10,*V2LON,"2",*HOFF,". ",NZIBADD2:
                    *P22:11,*V2LON,"3",*HOFF,". ",NZIBSUBR:
                    *P22:12,*V2LON,"4",*HOFF,". ",NZIBCITY:
                    *P22:13,*V2LON,"5",*HOFF,". ",NZIBCTRY
.
NZCHA100  CALL      NZSPC000                      * Ask Select Item question
          COMPARE   ZERO,CCITEM
          GOTO      NZCHA800 IF EQUAL
          GOTO      NZCHA900 IF LESS
.
          BRANCH    CCITEM,NZCHA200,NZCHA500,NZCHA500,NZCHA500,NZCHA500
          BEEP
          GOTO      NZCHA100
.
NZCHA200  CALL      NZKA1D00                      * keyin Address line 1
          GOTO      NZCHA100
.
. keyin any line from 2-5
.
NZCHA500  LOAD      NZIBDM30,CCITEM,ANS,NZIBADD2,NZIBSUBR,NZIBCITY,NZIBCTRY
          CALL      NZKADD00                      * keyin a 30 character address
          STORE     NZIBDM30,CCITEM,ANS,NZIBADD2,NZIBSUBR,NZIBCITY,NZIBCTRY
          GOTO      NZCHA100
.
NZCHA800  MOVE      ZERO,EXIT
          GOTO      NZCHA999
.
NZCHA900  MOVE      ONE,EXIT
.
NZCHA999  CALL      PUTSCR00
          RETURN
+
.**********************************************************************
.*  NZKA1D00  :  Keyin Address line 1                                 *
.**********************************************************************
NZKA1D00  KEYIN     *P25:9,*RV,*V2LON,NZIBADD1:
                    *P25:9,*DV,NZIBADD1;
.
          PACK      NZIBADD1,NZIBADD1,SP30,SP5
.
NZKA1D99  RETURN
+
.**********************************************************************
.*  NZKADD00  :  Keyin an address line (2-5)                          *
.**********************************************************************
NZKADD00  ASSIGN    (EIGHT+CCITEM),CVERT
          KEYIN     *P25:CVERT,*RV,*V2LON,NZIBDM30:
                    *P25:CVERT,*DV,NZIBDM30;
.
          PACK      NZIBDM30,NZIBDM30,SP30,SP5
.
NZKADD99  RETURN
+
.**********************************************************************
.*  NZSPC000  :  Ask Select Item question (not on line 24)            *
.**********************************************************************
NZSPC000  MOVE     ZERO,CCITEM
          MOVE     SP10,CCITEMD
          KEYIN    *P22:15,*DV,SP30,*P22:15,"Select Item, (",*V2LON,"P",*HOFF:
                   ")ost, (",*V2LON,"C",*HOFF,")ancel ? ",*V2LON,*JR,CCITEMD;
.
          RESET     CCITEMD
          GOTO      NZSPC000 IF EOS
.
          REP       UPPLOW,CCITEMD
.
          MATCH     "  C",CCITEMD
          GOTO      NZSPC700 IF EQUAL
.
          MATCH     "  P",CCITEMD
          GOTO      NZSPC800 IF EQUAL
.
          TYPE      CCITEMD
          GOTO      NZSPC600 IF NOT EQUAL
.
          MOVE      CCITEMD,CCITEM
          COMPARE   ONE,CCITEM
          GOTO      NZSPC600 IF LESS
.
          RETURN
.
NZSPC600  BEEP
          GOTO      NZSPC000
.
NZSPC700  MOVE      "-1",CCITEM
          RETURN
.
NZSPC800  MOVE      "0",CCITEM
.
NZSPC999  RETURN
