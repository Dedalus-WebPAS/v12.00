.******************************************************************************
.* System         : ALL                     Release 5.0n                      *
.*                                                                            *
.* Include Name   : DXOPRDEA.PBL                                              *
.*                                                                            *
.* Function       : To display the values from OPRDEAFD/BOKRC1FD for Theatre  *
.*                  This uses the code in OPR013IO for the display routines   *
.*                  Note: for the descriptions, the display routines are      *
.*                        called at the D......50 label as this is the part   *
.*                        which displays the description                      *
.*                                                                            *
.* Author         : Justin Coates           05/05/1995                        *
.*                                                                            *
.* Parameters     : OPRDEAFD / BOKRC1FD records                               *
.*                  SCRSPTFD/SCRSBGFD records                                 *
.*                                                                            *
.* Modifications  :                                                           *
.*----------------------------------------------------------------------------*
.* V11.01.01 16/02/2021  Thanh T        TSK 0902138                           *
.*           Added DOPDA083 for booking admission number label 583            * 
.*----------------------------------------------------------------------------*
.* V11.00.01 04/03/2020  J.Tan          TSK 0838262                           *
.*           Added Effective from and to date to MBS Item file                *
.******************************************************************************
DXOPRDEA  
          MOVE      SCPSROW,OPSCKROW        * set code row
          MOVE      SCPSCOL,OPSCKCOL        * set code column
          MOVE      SCPSROW,CVERT           * set code row
          MOVE      SCPSCOL,CCOL            * set code column
          MOVE      ZERO,OPSCSIZE           * set so desc not disp with code
          MOVE      SCPSITM,FIELDNO
.
          CALL      DOPDA998                * set variables
.
          BRANCH    FIELDNO,DSTTIM00,DEXPDU00,DANAEC00,DOPDA004,DOPTYP00:
                            DOPDA006,DTRANS00,DOPDA008,DDA1US00,DOPDA010:
                            DDA2US00,DOPDA012,DDA3US00,DOPDA014,DDA4US00:
                            DOPDA016,DPR1TM00,DOPDA018,DPR2TM00,DOPDA020:
                            DPR3TM00,DOPDA022,DOP1DS00,DOP2DS00,DOP3DS00:
                            DOPCOM00,DDACNC00,DOPDA028,DDATHT00,DOPDA030:
                            DOP1PR00,DOP2PR00,DOP3PR00,DOP4PR00,DOP5PR00:
                            DOP1PS00,DOP2PS00,DOP3PS00,DOP4PS00,DDADUR00:
                            DDAKNW00,DOPDA042,DATTDO00,DOPDA044,DREFDO00:
                            DOPDA046,DDUEDT00,DDUETM00,DPASDT00,DPASTM00:
                            DFUNME00,DPRVIN00,DPREAC00,DOPDA054,DEXPWD00:
                            DOPDA056,DBKEBD00,DOPDA058,DBKCLS00,DOPDA060:
                            DBKATP00,DOPDA062,DBKDPT00,DOPDA064,DASSDO00:
                            DOPDA066,DBKCLT00,DOPDA068,DREFGP00,DGPNPR00:
                            DOPDA071,DGPPCD00,DOPDA073,DGPFHN00,DRESID00:
                            DOPDA076,DELADT00,DOPDA078,DECRAP00,DOPDA080:
                            DOPDA081,DOPDA082,DOPDA083
          GOTO      DOPDA999
.
.         this is used for displaying the descriptions 
.         because of the way OPR013IO displays the code and desc together
.         we have to call the display routines at the point where it
.         checks if there is a desc'n.  before this. must set up the
.         required variables
.
DSTTIM00  PACK      VAR,OPDAEXPS,SP30,SP30,SP30
          GOTO      DOPDA995
.
DEXPDU00  COMPARE   ZERO,OPDAEXPD
          GOTO      DOPDA999 IF EQUAL       * dur is zero so dont display
. 
          PACK      VAR,OPDAEXPD,SP30,SP30,SP30
          GOTO      DOPDA995
.
DANAEC00  PACK      VAR,OPDAANAE,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA004  MOVE      "OA",TCODE
          MOVE      OPDAANAE,ACODE
          CALL      DTHCD000
          GOTO      DOPDA995
.
DOPTYP00  PACK      VAR,OPDATYPE,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA006  MOVE      "OY",TCODE
          MOVE      OPDATYPE,ACODE
          CALL      DTHCD000
          GOTO      DOPDA995
.
DTRANS00  PACK      VAR,OPDATRAN,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA008  MOVE      "OR",TCODE
          MOVE      OPDATRAN,ACODE
          CALL      DTHCD000
          GOTO      DOPDA995
.
DDA1US00  PACK      VAR,OPDAUSR1,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA010  MOVE      "Y6",TCODE
          MOVE      OPDAUSR1,ACODE
          CALL      DTHCD000
          GOTO      DOPDA995
.
DDA2US00  PACK      VAR,OPDAUSR2,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA012  MOVE      "Y7",TCODE
          MOVE      OPDAUSR2,ACODE
          CALL      DTHCD000
          GOTO      DOPDA995
.
DDA3US00  PACK      VAR,OPDAUSR3,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA014  MOVE      "Y8",TCODE
          MOVE      OPDAUSR3,ACODE
          CALL      DTHCD000
          GOTO      DOPDA995
.
DDA4US00  PACK      VAR,OPDAUSR4,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA016  MOVE      "Y9",TCODE
          MOVE      OPDAUSR4,ACODE
          CALL      DTHCD000
          GOTO      DOPDA995
.
DPR1TM00  PACK      VAR,OPDAPROV,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA018  COMPARE   ZERO,OPSCSIZE
          GOTO      DOPDA999 IF EQUAL       * Dont want to display description
.
          PACK      KEY9,OPDAPROV
          IF        OPCNCODE=1
            PACK      KEY17,KEY9,OPDADATE,SP70
            CALL      PATITMRD              * Read MBS code file
            BRANCH    OVRCD,DOPDA999
          ELSE
            CALL      RDICO1                * Read ICD operation file
            BRANCH    OVRCD,DOPDA999
            MOVE      ODESC,IDESC           * Set up description
          ENDIF
.
          PACK      VAR,IDESC,SP30,SP30,SP30

..          SETLPTR   VAR,OPSCSIZE
..          RESET     VAR
..          DISPLAY   *POPSCDSCL:OPSCDSRW,*+,VAR   * Display desc
..          MOVE      VAR,PRINTVAR
.
          GOTO      DOPDA995
.
DPR2TM00  PACK      VAR,OPDAPRV2,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA020  COMPARE   ZERO,OPSCSIZE
          GOTO      DOPDA999 IF EQUAL       * Dont want to display description
.
          PACK      KEY9,OPDAPRV2
          IF        OPCNCODE=1
            PACK      KEY17,KEY9,OPDADATE,SP70
            CALL      PATITMRD              * Read MBS code file
            BRANCH    OVRCD,DOPDA999
          ELSE
            CALL      RDICO1                * Read ICD operation file
            BRANCH    OVRCD,DOPDA999
            MOVE      ODESC,IDESC           * Set up description
          ENDIF
.
          PACK      VAR,IDESC,SP30,SP30,SP30

..          SETLPTR   VAR,OPSCSIZE
..          RESET     VAR
..          DISPLAY   *POPSCDSCL:OPSCDSRW,*+,VAR   * Display desc
..          MOVE      VAR,PRINTVAR

          GOTO      DOPDA995
.
DPR3TM00  PACK      VAR,OPDAPRV3,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA022  COMPARE   ZERO,OPSCSIZE
          GOTO      DOPDA999 IF EQUAL       * Dont want to display description
.
          PACK      KEY9,OPDAPRV3
          IF        OPCNCODE=1
            PACK      KEY17,KEY9,OPDADATE,SP70
            CALL      PATITMRD              * Read MBS code file
            BRANCH    OVRCD,DOPDA999
          ELSE
            CALL      RDICO1                * Read ICD operation file
            BRANCH    OVRCD,DOPDA999
            MOVE      ODESC,IDESC           * Set up description
          ENDIF
.
          PACK      VAR,IDESC,SP30,SP30,SP30

..          SETLPTR   VAR,OPSCSIZE
..          RESET     VAR
..          DISPLAY   *POPSCDSCL:OPSCDSRW,*+,VAR    * Display Code
..          MOVE      VAR,PRINTVAR

          GOTO      DOPDA995
.
DOP1DS00  PACK      VAR,OPDADES1,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOP2DS00  PACK      VAR,OPDADES2,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOP3DS00  PACK      VAR,OPDADES3,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPCOM00  PACK      VAR,OPDACOMM,SP30,SP30,SP30
          GOTO      DOPDA995
.
DDACNC00  PACK      VAR,OPDACANC,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA028  MOVE      "SC",TCODE
          MOVE      OPDACANC,ACODE
          CALL      DTHCD000
          GOTO      DOPDA995
.
DDATHT00  PACK      VAR,OPDATHET,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA030  COMPARE   ZERO,OPSCSIZE
          GOTO      DOPDA999 IF EQUAL       * dont want to display description
.
          MATCH     SP6,OPDATHET
          GOTO      DOPDA999 IF EQUAL       * no code to display
.
          MOVE      "Invalid Code",OPRMDESC
          MOVE      OPDATHET,KEY6
          CALL      RDOPOPR1                * read operating room file
.
          PACK      VAR,OPRMDESC,SP30,SP30,SP30

..          SETLPTR   VAR,OPSCSIZE
..          RESET     VAR
..          DISPLAY   *POPSCDSCL:OPSCDSRW,*+,VAR        * operating room
..          MOVE      VAR,PRINTVAR

          GOTO      DOPDA995
.
DOP1PR00  PACK      VAR,OPDAPRE1,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOP2PR00  PACK      VAR,OPDAPRE2,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOP3PR00  PACK      VAR,OPDAPRE3,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOP4PR00  PACK      VAR,OPDAPRE4,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOP5PR00  PACK      VAR,OPDAPRE5,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOP1PS00  PACK      VAR,OPDAPOS1,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOP2PS00  PACK      VAR,OPDAPOS2,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOP3PS00  PACK      VAR,OPDAPOS3,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOP4PS00  PACK      VAR,OPDAPOS4,SP30,SP30,SP30
          GOTO      DOPDA995
.
DDADUR00  PACK      VAR,OPDATDUR,SP30,SP30,SP30
          GOTO      DOPDA995
.
DDAKNW00  PACK      VAR,OPDAKNOW,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA042  MOVE      "OK",TCODE
          MOVE      OPDAKNOW,ACODE
          CALL      DTHCD000
          GOTO      DOPDA995
.
DATTDO00  PACK      VAR,BKREADOC,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA044  MOVE      BKREADOC,KEY6
          CALL      DTHDC000
          GOTO      DOPDA995
.
DREFDO00  PACK      VAR,BKRESDOC,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA046  MOVE      BKRESDOC,KEY6
          CALL      DTHDC000
          GOTO      DOPDA995
.
DDUEDT00  UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          PACK      VAR,CPCDATE,SP30,SP30,SP30
          GOTO      DOPDA995
.
DDUETM00  PACK      VAR,BKREATIM,SP30,SP30,SP30
          GOTO      DOPDA995
.
DPASDT00  UNPACK    BKREPDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          REP       " 0",CPCDATE
          PACK      VAR,CPCDATE,SP30,SP30,SP30
          GOTO      DOPDA995
.
DPASTM00  PACK      VAR,BKREPTIM,SP30,SP30,SP30
          GOTO      DOPDA995
.
DFUNME00  PACK      VAR,BKREOMEM,SP30,SP30,SP30
          GOTO      DOPDA995
.
DPRVIN00  PACK      VAR,BKREPREV,SP30,SP30,SP30
          GOTO      DOPDA995
.
DPREAC00  PACK      VAR,BKREACCM,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA054  MOVE      "BP",TCODE
          MOVE      BKREACCM,ACODE
          CALL      DTHCD000
          GOTO      DOPDA995
.
DEXPWD00  PACK      VAR,BKREWARD,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA056  COMPARE   ZERO,OPSCSIZE
          GOTO      DOPDA999 IF EQUAL       * dont want to display description
.
          MATCH     SP3,BKREWARD
          GOTO      DOPDA999 IF EQUAL       * no code to display
.
          MOVE      "Invalid Code",WBDESC
          PACK      KEY6,BKREWARD,SP3
          CALL      RDWARD1                 * read ward file
.
          PACK      VAR,WBDESC,SP30,SP30,SP30
..          SETLPTR   VAR,OPSCSIZE
..          RESET     VAR
..          DISPLAY   *POPSCDSCL:OPSCDSRW,*+,VAR          * Ward
..          MOVE      VAR,PRINTVAR
.
          GOTO      DOPDA995
.
DBKEBD00  PACK      VAR,BKREWARD,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA058  MOVE      SP20,WBDESC             * clear description
          MATCH     SP3,BKREEBED
          GOTO      DOPDA58A IF EQUAL       * no bed to display
.
          DISPLAY   "/",*V2LON,BKREEBED
          MOVE      "/",ANS
          PACK      VAR,BKREWARD,ANS,BKREEBED,SP30,SP30,SP30
          MOVE      VAR,PRINTVAR
.
DOPDA58A  COMPARE   ZERO,OPSCSIZE
          GOTO      DOPDA999 IF EQUAL       * dont want to display description
.
          MOVE      "Invalid Code",WBDESC
          PACK      KEY6,BKREWARD,BKREEBED
          CALL      RDWARD1                 * read ward file
.
          PACK      VAR,WBDESC,SP30,SP30,SP30
          GOTO      DOPDA995
.
DBKCLS00  PACK      VAR,BKRECLSS,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA060  MOVE      "P ",TCODE
          MOVE      BKRECLSS,ACODE
          CALL      DTHCD000
          GOTO      DOPDA995
.
DBKATP00  PACK      VAR,BKREATYP,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA062  MOVE      "A ",TCODE
          MOVE      BKREATYP,ACODE
          CALL      DTHCD000
          GOTO      DOPDA995
.
DBKDPT00  PACK      VAR,BKREDEPT,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA064  MOVE      "AC",TCODE
          MOVE      BKREDEPT,ACODE
          CALL      DTHCD000
          GOTO      DOPDA995
.
DASSDO00  PACK      VAR,BKRETDOC,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA066  MOVE      BKRETDOC,KEY6
          CALL      DTHDC000
          GOTO      DOPDA995
.
DBKCLT00  PACK      VAR,BKRECLMT,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA068  MOVE      "CL",TCODE
          MOVE      BKRECLMT,ACODE
          CALL      DTHCD000
          GOTO      DOPDA995
.
DREFGP00  PACK      VAR,BKRERFGP,SP30,SP30,SP30
          GOTO      DOPDA995
.
DGPNPR00  PACK      VAR,BKRERFGP,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA071  COMPARE   ZERO,OPSCSIZE
          GOTO      DOPDA999 IF EQUAL       * dont want to display description
.
          PACK      PACFNAME,SP30,SP30,SP5  * clear description
.
          MATCH     SP8,BKRERFGP
          GOTO      DOPDA999 IF EQUAL       * no code to display
.
          PACK      KEY10,BKRERFGP,SP70
          CALL      RDPMHCP1                * read doctor file
          BRANCH    OVRCD,DOPDA999          * invalid code
.
          MOVE      PMHCSNAM,PACFNAME
          STRIP     PACFNAME
          ENDSET    PACFNAME
          APPEND    SP1,PACFNAME
          APPEND    PMHCGNAM,PACFNAME
.
          PACK      VAR,PACFNAME,SP30,SP30,SP30

..          SETLPTR   VAR,OPSCSIZE
..          RESET     VAR
..          DISPLAY   *POPSCDSCL:OPSCDSRW,*+,VAR    * Referring GP
..          MOVE      VAR,PRINTVAR
 
          GOTO      DOPDA995
.
DGPPCD00  MOVE      "-",ANS
          MATCH     SP2,BKREGPPT
          IF        @EQUAL
            MOVE      SP1,ANS
          ENDIF
          PACK      VAR,BKREGPPC,ANS,BKREGPPT,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA073  COMPARE   ZERO,OPSCSIZE
          GOTO      DOPDA999 IF EQUAL       * dont want to display description
.
          PACK      PMHGPNAM,SP30,SP30,SP5  * clear description
          MATCH     SP6,BKREGPPC
          GOTO      DOPDA999 IF EQUAL       * no code to display
.
          PACK      KEY10,BKREGPPC,SP70
          PACK      KEY12,KEY10,BKREGPPT,SP70
          CALL      RDPMHCG1                * read doctor file
          BRANCH    OVRCD,DOPDA999          * invalid code
.
          MOVE      PMHGPNAM,PACFNAME
          PACK      VAR,PACFNAME,SP30,SP30,SP30

..          SETLPTR   VAR,OPSCSIZE
..          RESET     VAR
..          DISPLAY   *POPSCDSCL:OPSCDSRW,*+,VAR    * GP practice code
..          MOVE      VAR,PRINTVAR

          GOTO      DOPDA995
.
DGPFHN00  PACK      VAR,BKREGPFA,SP30,SP30,SP30
          GOTO      DOPDA995
.
DRESID00  PACK      VAR,BKRERESI,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA076  MOVE      "T ",TCODE
          MOVE      BKRERESI,ACODE
          CALL      DTHCD000
          GOTO      DOPDA995
.
DELADT00  PACK      VAR,BKREEATY,SP30,SP30,SP30
          GOTO      DOPDA995
.
DOPDA078  MOVE      "CC",TCODE
          MOVE      BKREEATY,ACODE
          CALL      DTHCD000
          GOTO      DOPDA995
.
DECRAP00  PACK      VAR,BKREECRA,SP30,SP30,SP30
          GOTO      DOPDA995
.
. session date (LABELS)
.
DOPDA080  UNPACK    OPDADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          IF        SCPSLEN = 10
            PACK      VAR,CPCDATE,SP70,SP70
          ELSE
            PACK      VAR,CPDATE,SP70,SP70
          ENDIF
          GOTO      DOPDA995
.
. clinc/surgeon (LABELS)
.
DOPDA081  PACK      VAR,OPDACLIN,SP70,SP70
          GOTO      DOPDA995
.
DOPDA082  MOVE      OPDACLIN,KEY6
          CALL      DCLSU000
          GOTO      DOPDA995
.
DOPDA083  PACK      VAR,BKREBOOK,SP70,SP70
          GOTO      DOPDA995
.
.
DOPDA995  CALL      DISPITEM
DOPDA999  RETURN
+
.*********************************************************************
.         set up the variables for descriptions
.*********************************************************************
DOPDA998  MOVE      SCPSROW,OPSCDSRW        * set description row
          MOVE      SCPSCOL,OPSCDSCL        * set description column
          MOVE      SCPSLEN,OPSCSIZE        * set description size
          RETURN
+
.*********************************************************************
.         get the clinic description
.*********************************************************************
DCLSU000  MATCH     SP6,KEY6                * test key6 is non blank
          GOTO      DCLSU990 IF EQUAL
.
          BRANCH    OPCNCLSU,DCLSU555       * using doctor code
.
.         read clinic file
.
          CALL      RDOPCLI1                * check for valid clinic
          BRANCH    OVRCD,DCLSU990          * invalid code
.
          PACK      VAR,OPCLDESC,SP70,SP70
.
          MATCH     SP6,OPCLDOCT 
          GOTO      DCLSU999 IF EQUAL       * not using doctors code
.
          MOVE      OPCLDOCT,KEY6           * set up key
.
.         read doctors file
.
DCLSU555  CALL      RDDOCT1                 * read doctors information
          BRANCH    OVRCD,DCLSU990          * invalid code
.
          MOVE      DTITL,PACTITLE
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          PACK      VAR,PACFNAME,SP70,SP70
          GOTO      DCLSU999
.
DCLSU990  PACK      VAR,SP70,SP70
.
DCLSU999  RETURN
...............................................................................
.*********************************************************************
.         display from codes file
.*********************************************************************
DTHCD000  COMPARE   ZERO,OPSCSIZE
          GOTO      DTHCD999 IF EQUAL       * dont want to display description
.
          PACK      ACODE,ACODE,SP3
          MATCH     SP3,ACODE
          GOTO      DTHCD999 IF EQUAL       * no code to display
.
          MOVE      "Invalid Code",TDESC
          PACK      KEY5,TCODE,ACODE
          CALL      RDCODE1                 * read codes file
.
          PACK      VAR,TDESC,SP30,SP30,SP30

..          SETLPTR   VAR,OPSCSIZE
..          RESET     VAR
..          DISPLAY   *POPSCDSCL:OPSCDSRW,*+,VAR
..          MOVE      VAR,PRINTVAR
.
DTHCD999  RETURN
.*********************************************************************
.         display a doctor code
.*********************************************************************
DTHDC000  COMPARE   ZERO,OPSCSIZE
          GOTO      DTHDC999 IF EQUAL       * dont want to display description
.
          PACK      KEY6,KEY6,SP6
          MATCH     SP6,KEY6
          GOTO      DTHDC999 IF EQUAL       * no code to display
.
          MOVE      "Invalid Doctor",PACFNAME
          CALL      RDDOCT1                 * read doctor file
          BRANCH    OVRCD,DTHDC900          * invalid code
.
          MOVE      DSNAME,PACSNAME
          MOVE      DGNAME,PACGNAME
          MOVE      DTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
.
DTHDC900  PACK      VAR,PACFNAME,SP30,SP30,SP30
..          SETLPTR   VAR,OPSCSIZE
..          RESET     VAR
..          DISPLAY   *POPSCDSCL:OPSCDSRW,*+,VAR        * Attending Doctor
..          MOVE      VAR,PRINTVAR
.
DTHDC999  RETURN
.*********************************************************************

