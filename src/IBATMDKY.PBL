.******************************************************************************
.*  Include Name  : PATVARKY.PBL                                              *
.*  Description   : Keyin of a Stationary Code                                *
.*  Author        : John Smith                                                *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF6 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYIMODE - is keyin mode                                  *
.*                                                                            *
.*  Returns       : KEY6     - Keyin in Hospital code                         *
.*                  IBTHSCOD - Description   (PTHSxxxx  record)               *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*  Modifications : 12/08/93  ROD                                             *
.*                  When do a GETSCR, save whole screen (ie HLEF=0)           *
.*                  10/03/94  Sandra Barcham                                  *
.*                  Make Standard and Record Locking                          *
.*                  Changed name from KYPATHOS to PATVARKY                    *
.*                  Changed CCOL - ECOL & CVERT - EVERT                       *
.*                                                                            *
.******************************************************************************
IBATMDKY
PTMKY000  MATCH     SP3,CKYIDEF6                  * using a default?
          GOTO      PTMKY050 IF NOT EQUAL
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN6:
                    *PECOL:EVERT,*V2LON,KEY6:
                    *PECOL:EVERT,*DV,KEY6;
          GOTO      PTMKY080
.
PTMKY050  MOVE      CKYIDEF6,KEY6
          KEYIN     *PECOL:EVERT,*DV,KEY6:      * keyin with default
                    *PECOL:EVERT,*V2LON,*RV,KEY6:
                    *PECOL:EVERT,*DV,KEY6;
.
PTMKY080  PACK      KEY6,KEY6,SP3
.
PTMKY090  CALL      TMHCK000
          BRANCH    EXIT,PTMKY500,PTMKY760,PTMKY790,PTMKY950
.                        "?"      valid    nothing  exitchar
          GOTO      PTMKY000
.
PTMKY500  MOVE      ZERO,HLEF               * save whole screen
          CALL      GETSCR00 
.
PTMKY550  CALL      IBATMDDS
.
PTMKY600  MATCH     SP3,CKYIDEF6                  * using a default?
          GOTO      PTMKY650 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,"Stationery Code :":
                    *P19:24,*DV,UNDLN6:
                    *P19:24,*V2LON,KEY6:
                    *P19:24,*DV,KEY6;
          GOTO      PTMKY700
.
PTMKY650  MOVE      CKYIDEF6,KEY6
          KEYIN     *P1:24,*EL,"Stationery Code :":
                    *P19:24,*DV,KEY6:        * keyin with default
                    *P19:24,*V2LON,*RV,KEY6:
                    *P19:24,*DV,KEY6;
.
PTMKY700  PACK      KEY6,KEY6,SP3
          CALL      TMHCK000
          BRANCH    EXIT,PTMKY550,PTMKY750,PTMKY780,PTMKY900
.                        "?"      valid    nothing  exitchar
          GOTO      PTMKY600
.
. a valid code was entered
.
PTMKY750  CALL      PUTSCR00                 * Restore Screen
          DISPLAY   *PECOL:EVERT,*V2LON,KEY6
.
PTMKY760  MOVE      ZERO,EXIT                * Valid Input
          MOVE      KEY6,IBTHSCOD
          GOTO      PTMKY999
.
. nothing was input
.
PTMKY780  CALL      PUTSCR00                 * Restore Screen
          DISPLAY   *PECOL:EVERT,*V2LON,KEY6
.
PTMKY790  MOVE      ONE,EXIT                 * Nothing or Spaces Entered
          GOTO      PTMKY999
.
. EXITCHAR input
.
PTMKY900  CALL      FRESCR00                 * Free the Screen Saved
.
PTMKY950  MOVE      TWO,EXIT                 * EXITCHAR
.
PTMKY999  RETURN
.
.*********************************************************************
.*                  TMHCK000                    Called by : PATVARKY *
.*        check what was entered                                     *
.*        Returns : EXIT = 0      invalid                            *
.*                       = 1      "?"                                *
.*                       = 2      valid                              *
.*                       = 3      nothing                            *
.*                       = 4      exitchar                           *
.*********************************************************************
TMHCK000  ENDSET    KEY6
          APPEND    SP3,KEY6
          RESET     KEY6
.
          MATCH     EXITCHAR,KEY6
          GOTO      TMHCK600 IF EQUAL       * exit char
.
          MATCH     SP3,KEY6
          GOTO      TMHCK700 IF EQUAL       * nothing
.
          MATCH     QUEST,KEY6
          GOTO      TMHCK800 IF EQUAL       * ? mark
.
.         a code was entered so validate
. 
TMHCK500  CALL      RDIBTH1
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,TMHCK900          * invalid code
          ELSE
            COMPARE   ZERO,OVRCD   
            GOTO      TMHCK900 IF EQUAL
          ENDIF  
.
          MOVE      TWO,EXIT                * valid code
          GOTO      TMHCK999
.
.         exitchar entered
.
TMHCK600  MOVE      FOUR,EXIT
          MOVE      SP20,IBTHSCOD           * clear desc field
          GOTO      TMHCK999
.
.         nothing entered
.
TMHCK700  BRANCH    CKYIMAND,TMHCK950       * mandatory
.
          MOVE      THREE,EXIT              * nothing entered
          MOVE      SP20,IBTHSCOD            * clear desc field
          GOTO      TMHCK999
.
.         "?" entered
.
TMHCK800  MOVE      ONE,EXIT
          GOTO      TMHCK999
.
.         invalid code entered
.
TMHCK900  IF        CKYIMODE = 0
            DISPLAY   *P1:24,*EL,*B,"Invalid Stationary Code. ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
          MOVE      ZERO,EXIT
          GOTO      TMHCK999
.
.         field is mandatory
.
TMHCK950  DISPLAY   *P1:24,*B,*EL,"Field is mandatory. ";
          CALL      HITENTER 
          MOVE      ZERO,EXIT
.
TMHCK999  RETURN
.
          INCLUDE   IBATMDDS 

