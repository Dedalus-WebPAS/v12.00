.VF.09.00 14.11.2000 Charles Handaya
.         Add in Warehouse Security Modification
.---------------------------------------------------------------------------
.   System    :   Supply and Inventory               
.   Include   :   SINCIAKY
.   Routine   :   KSIIAA00 Keyin Catalog Code        
.   Parameter :   CKEYTYP 0 = Keyin , 1 = Accept, 2 = Either
.   Return    :   EXIT = 1 null entry, 2 EXITCHAR entered
.-------------------------------------------------------------------------------
KSIIAA00 TOPIC     "NATOCODE "
          READ      CONTROLF,SIXTY3;*67,INCOCATE,*141,INCNNATO,*153,SNCOKWD
          MOVE      SP70,NATOCODE 
          MOVE      SP70,SIIACAT 
          COMPARE   ZERO,INCOCATE
          GOTO      KSIIAA01 IF NOT EQUAL
.
          KEYIN     *PCCOL:CVERT,*V2LON,NATOCODE
.
          ENDSET    NATOCODE 
          APPEND    SP70,NATOCODE 
          RESET     NATOCODE 
          REP       UPPLOW,NATOCODE
.
          IF        ETOEXIT=0
            MATCH     SP70,NATOCODE 
            IF        @EQUAL
              MOVE      ONE,EXIT
              GOTO      KSIIAA99
            ENDIF
          ELSE
            MATCH     ANSE,NATOCODE 
            IF        @EQUAL
              MOVE      TWO,EXIT
              GOTO      KSIIAA99
            ENDIF
            MATCH     ANSX,NATOCODE 
            IF        @EQUAL
              MOVE      TWO,EXIT
              GOTO      KSIIAA99
            ENDIF
            MATCH     SP70,NATOCODE 
            IF        @EQUAL
              MOVE      ZERO,EXIT
              MOVE      ONE,SIIANON
              GOTO      KSIIAA99
            ENDIF
          ENDIF
.
          CMATCH    EXITCHAR,NATOCODE 
          IF        @EQUAL
            MOVE      TWO,EXIT
            GOTO      KSIIAA99
          ENDIF
.
          CMATCH    QUEST,NATOCODE 
          GOTO      KSIIAA10 IF NOT EQUAL
          GOTO      KSIIAA02
.
KSIIAA01 
          MOVE      SP70,NATOCODE 
          KEYIN     *PCCOL:CVERT,*V2LON,*JR,NATOCODE
.
          ENDSET    NATOCODE 
          APPEND    SP70,NATOCODE 
          RESET     NATOCODE 
          REP       UPPLOW,NATOCODE
.
          MATCH     SP70,NATOCODE 
          IF        @EQUAL
            IF        ETOEXIT=0
              MOVE      ONE,EXIT
            ELSE
              MOVE      ONE,SIIANON
              MOVE      ZERO,EXIT
            ENDIF
            GOTO      KSIIAA99
          ENDIF
.
          ENDSET    NATOCODE 
          CMATCH    EXITCHAR,NATOCODE 
          IF        @EQUAL
            MOVE      TWO,EXIT
            GOTO      KSIIAA99
          ENDIF
.
          IF        ETOEXIT=1
            MATCH     ANSE,NATOCODE 
            IF        @EQUAL
              MOVE      TWO,EXIT
              GOTO      KSIIAA99
            ENDIF
            MATCH     ANSX,NATOCODE 
            IF        @EQUAL
              MOVE      TWO,EXIT
              GOTO      KSIIAA99
            ENDIF
          ENDIF
.
          CMATCH    QUEST,NATOCODE 
          GOTO      KSIIAA02 IF EQUAL
          RESET     NATOCODE 
.
          TYPE      NATOCODE
          IF        !@EQUAL & CKEYTYP=1
            MOVE      "Entry is not numeric.  ",DISPMSG
            CALL      MESSAGE1
            GOTO      KSIIAA00
          ENDIF
          GOTO      KSIIAA10
.
KSIIAA02  MOVE      CCITEM,SCCITEM
          IF        SNCOKWD=1
            PROC      HSIKCA00               * Keyword Search (New)
          ELSE
            CALL      HSIIFA00               * Alias Search (Old)
          ENDIF
          BRANCH    EXIT,KSIIAA00,KSIIAA00
.
          IF        CKEYTYP=1 | CKEYTYP=2
            GOTO      KSIIAA00
          ENDIF
.
          MOVE      SCCITEM,CCITEM
.
          MATCH     SP70,SIIACAT 
          GOTO      KSIIAA00 IF EQUAL
.
          GOTO      KSIIAA80
KSIIAA10
          IF        INCOCATE<>0
            UNPACK    NATOCODE,KEY3,KEY7
          ELSE
            UNPACK    NATOCODE,KEY7,KEY3
          ENDIF
          MOVE      KEY7,SIIACAT
.
          MATCH     KEY3,SP3          * Check if too large for Catalog - Nato?
          IF        @EQUAL  
            CALL      RASIIA1 
          ELSE
            IF        INCNNATO<>1 | CKEYTYP=1 
              MOVE      "Entry too long for a Catalog Code - ",DISPMSG
              CALL      MESSAGE1
              GOTO      KSIIAA00
            ELSE
              MOVE      ONE,OVRCD     * Simulate for Nato Lookup
            ENDIF
          ENDIF
.
. ------- OVRCD=1 if not found on catalog file or if > 7 chars keyed in and
. ------- Nato Codes are being used.
          IF        OVRCD=1
            CALL      CHKNAT00               * Check Nato File
            BRANCH    EXIT,KSIIAA00
            BRANCH    CKEYTYP,KSIIAA99,KSIIAA99  * Insert mode - OK
          ELSE
            IF        CKEYTYP=1
              MOVE      "Catalog Code Already Exists - ",DISPMSG
              CALL      MESSAGE1
              GOTO      KSIIAA00
            ENDIF
          ENDIF
.
. ------- Sinciaaf record must exist as in modify mode
KSIIAA80  PACK      KEY7,SIIACAT
          CALL      RDSIIA1 
          MOVE      ZERO,EXIT
          GOTO      KSIIAA99
.
KSIIAA99  DISPLAY   *PCCOL:CVERT,*V2LON,SIIACAT,SP3
          MOVE      ZERO,ETOEXIT
         TOPIC    PRGID
          RETURN
.---------------------------------------------------------------------------
. Check if SIIANAT is from the Nato File.
.-------------------------------------------------------------------------------
CHKNAT00  MOVE      ZERO,EXIT
.
          MOVE      NATOCODE,KEY10
.
          SQUEEZE   KEY10
          PACK      KEY17,KEY10,SP70
          RESET     KEY10
          MOVE      KEY17,KEY10
.
          IF        INCNNATO=0
            IF        CKEYTYP<>1 & CKEYTYP<>2
              MOVE      "Catalog Does Not Exist - ",DISPMSG
              CALL      MESSAGE1
              MOVE      ONE,EXIT
            ENDIF
            GOTO      CHKNAT99 
          ENDIF
.
          MOVE      SP70,SINANAT
          CALL      RSSINA2
          CALL      RKSINA2
          MATCH     SINANAT,KEY10
          IF        OVRCD=1 | !@EQUAL
            IF CKEYTYP<>1 & CKEYTYP<>2
              MOVE      "Catalog or Nato Code Does Not Exist - ",DISPMSG
              CALL      MESSAGE1
              MOVE      ONE,EXIT
              GOTO      CHKNAT99
            ENDIF
            IF        INCOCATE<>0
              UNPACK    NATOCODE,KEY3,SIIACAT
            ELSE
              UNPACK    NATOCODE,SIIACAT,KEY3
            ENDIF
          ELSE 
            IF        @EQUAL & (CKEYTYP=1 | CKEYTYP=2)
              MOVE      "Nato Code Already Exists - ",DISPMSG
              CALL      MESSAGE1
              MOVE      ONE,EXIT
              GOTO      CHKNAT99
            ENDIF
            MOVE      SINACAT,SIIACAT
          ENDIF
.
CHKNAT99  RETURN
.---------------------------------------------------------------------------
.   System    :   Supply and Inventory               
.   Include   :   SINCIFKY
.   Routine   :   HSIIFA00 Catalog             
.   Return    :           
.-------------------------------------------------------------------------------
HSIIFA00
          MOVE      ZERO,CSCRSIZ
          IF        HTOP<3
            MOVE      "10",HTOP
            MOVE      "24",HBOT
            MOVE      "01",HLEF
            MOVE      "80",HRIG
          ENDIF
HSIIFA04
          CALL      GSIIF000
          BRANCH    EXIT,HSIIFA99
          MOVE      STDCOL00,STDCOL01
          ADD       " 7",STDCOL01
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON,"Catalog"            
.
          MOVE      STDCOL01,STDCOL02
          ADD       " 8",STDCOL02
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"Alias"              
.
          IF        INCNNATO=1
            MOVE      STDCOL02,STDCOL03
            ADD       "50",STDCOL03
            DISPLAY   *PSTDCOL03:STDROW,*V2LON,*ULON,"Nato Code"              
            MOVE      STDCOL03,STDCOL04
            ADD       "11",STDCOL04
          ELSE
            MOVE      STDCOL02,STDCOL03
            ADD       "61",STDCOL03
          ENDIF
.
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
          MOVE      "3",CSEARCH
          GOTO      HSIIFA50
.
. ------- Expand in mid flight
HSIIFA05
          CALL      GSIIF000
          BRANCH    EXIT,HSIIFA99
.
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          MOVE      STDCOL00,STDCOL01
          ADD       " 7",STDCOL01
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON,"Catalog"            
.
          MOVE      STDCOL01,STDCOL02
          ADD       " 8",STDCOL02
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"Alias"              
.
          IF        INCNNATO=1
            MOVE      STDCOL02,STDCOL03
            ADD       "50",STDCOL03
            DISPLAY   *PSTDCOL03:STDROW,*V2LON,*ULON,"Nato Code"              
            MOVE      STDCOL03,STDCOL04
            ADD       "11",STDCOL04
          ELSE
            MOVE      STDCOL02,STDCOL03
            ADD       "61",STDCOL03
          ENDIF
.
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
          PACK      KEY67,SST1,SP70
          UNPACK    KEY67,SIIFALI,SIIFCAT                                       
          PACK      KEY7,SIIFCAT,SP70
          IF        CSEARCH=0
            PACK      KEY67,SP70   * Scanning Whole File for String Search "*"
            PACK      KEY7,SP70    * Scanning Whole File for String Search "*"
          ENDIF
          CALL      RSSIIF00
          CALL      RPSIIF00
          GOTO      HSIIFA10
.
HSIIFA08  MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          MOVE      SP70,SIIFCAT 
          PACK      KEY60,SCHFLD02,SP70
          PACK      KEY67,KEY60,SCHFLD01,SP70
          PACK      KEY7,SCHFLD01,SP70
          IF        CSEARCH=0
            PACK      KEY67,SP70   * Scanning Whole File for String Search "*"
          ENDIF
          CALL      RSSIIF00
          CALL      RPSIIF00
HSIIFA10  CALL      RKSIIF00
          BRANCH    OVRCD,HSIIFA15
.
          CALL      CSIIFA00
          COMPARE   ZERO,OVRCD
          GOTO      HSIIFA12 IF EQUAL
          BRANCH    CSEARCH,HSIIFA15   * Not Wildcard Scan So Exit
          GOTO      HSIIFA10           * Wildcard "*" Scan So Keep Looping
.
HSIIFA12  ADD       ONE,XITEMNO
          ADD       ONE,STDROW
          PACK      KEY67,SIIFALI,SIIFCAT,SP70
          STORE     KEY67,XITEMNO,SST1,SST2,SST3,SST4,SST5:
                                SST6,SST7,SST8,SST9,SST10:
                                SST11,SST12,SST13,SST14,SST15:
                                SST16,SST17
          PACK      SIIANAT,SP70
          IF        CKEYTYPE=1
            PACK      KEY7,SIIFCAT,SP70    * If looping through Alias File
            CALL      RDSIIA1              * Then need to read the Master
          ENDIF
          MOVE      "*",ANS                * Flag as Suspended (YES)
          IF        SIIASUS=0
            MOVE      SP1,ANS              * Flag Suspended Catalogs (NO)
          ENDIF 
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,XITEMNO,DOT,ANS
          DISPLAY   *PSTDCOL01:STDROW,SIIFCAT 
          IF        INCNNATO=1
            MOVE      SIIFALI,KEY49
            DISPLAY   *PSTDCOL02:STDROW,KEY49 
            DISPLAY   *PSTDCOL03:STDROW,SIIANAT 
          ELSE
            DISPLAY   *PSTDCOL02:STDROW,SIIFALI 
          ENDIF
          COMPARE   MAXITEM,XITEMNO
          GOTO      HSIIFA10 IF NOT EQUAL
HSIIFA15
          UNPACK    SAVKEY80,SIIFALI,SIIFCAT                                    
          MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
HSIIFA20
          CALL      SSIIF000
          BRANCH    EXIT,HSIIFA30,HSIIFA35,HSIIFA40,HSIIFA45,HSIIFA05:
                              HSIIFA91,HSIIFA50,HSIIFA57
          GOTO      HSIIFA92
HSIIFA30
          CALL      RKSIIF00
          BRANCH    OVRCD,HSIIFA85
          CALL      RPSIIF00
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HSIIFA10
HSIIFA35
          MOVE      SST1,KEY67
          CALL      RDSIIF00
          CALL      RPSIIF00
          BRANCH    OVRCD,HSIIFA85
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
          MOVE      SP70,KEY7
HSIIFA36
          CALL      RPSIIF00
          BRANCH    OVRCD,HSIIFA38
          CALL      CSIIFA00
          BRANCH    OVRCD,HSIIFA36
.
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HSIIFA36 IF NOT EQUAL
HSIIFA38
          MOVE      ZERO,XITEMNO
          GOTO      HSIIFA10
HSIIFA40
          MOVE      SST1,KEY67
          CALL      RDSIIF00
          CALL      RPSIIF00
          BRANCH    OVRCD,HSIIFA85
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HSIIFA08
HSIIFA45
          CALL      RKSIIF00
          BRANCH    OVRCD,HSIIFA85
          SUSPEND
          CLIP      1,1,CRIG,24
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,XITEMNO
          IF        CSEARCH=1
            PACK      KEY67,SCHFLD02,Z70
          ELSE
            PACK      KEY67,Z70                                          
          ENDIF
          PACK      KEY7,Z70                                          
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
          CALL      RSSIIF00
HSIIFA46
          CALL      RPSIIF00
          BRANCH    OVRCD,HSIIFA48
          CALL      CSIIFA00
          BRANCH    OVRCD,HSIIFA46
.
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HSIIFA46 IF NOT EQUAL
HSIIFA48
          MOVE      ZERO,XITEMNO
          GOTO      HSIIFA10
.
HSIIFA50  
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          UNPACK    SP70,SCHFLD01,SCHFLD02,SCHFLD03,SCHFLD04,SCHFLD05
          UNPACK    SP70,SCHFLD06,SCHFLD07,SCHFLD08,SCHFLD09,SCHFLD10
          IF        CSEARCH=0 | CSEARCH=1
            BOXCLR    CLEF,CTOP,CRIG,CBOT
          ENDIF
          MOVE      ZERO,CSEARCH
          DISPLAY   *PSTDCOL00:STDROW,*V2LON,*HON,"Sch"
.
.---- keyin catalog code ----
.
          IF        ZERO=INCOCATE
            KEYIN     *PSTDCOL01:STDROW,*V2LON,KEY7;
            PACK      KEY7,KEY7,SP70
          ELSE
            KEYIN     *PSTDCOL01:STDROW,*V2LON,*JR,KEY7;
            PACK      KEY7,KEY7,SP70
          ENDIF
          PACK      SCHFLD01,KEY7
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,SCHFLD01;
.
.---- keyin catalog alias ----
.
          KEYIN     *PSTDCOL02:STDROW,*V2LON,SCHFLD02
.
          RESET     SCHFLD02
          IF        @EOS
            MOVE      "2",CKEYTYPE     * loop by catalog
          ELSE
            MOVE      "1",CKEYTYPE     * loop by alias
          ENDIF
.
          REP       UPPLOW,SCHFLD02
          MATCH     "*",SCHFLD02
          IF        @EQUAL
            STRIP     SCHFLD02
            REP       " %",SCHFLD02
            REP       "* ",SCHFLD02
            SQUEEZE   SCHFLD02
            REP       "% ",SCHFLD02
            MOVE      ZERO,CSEARCH
          ELSE
            MOVE      ONE,CSEARCH
          ENDIF
.
          PACK      KEY8,SP70
          SUSPEND
          CLIP      1,1,CRIG,24
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HSIIFA08
.
HSIIFA57  
          MOVE      SP70,KEY67
          MOVE      SP70,SIIFCAT 
          LOAD      KEY67,CCITEM,SST1,SST2,SST3,SST4,SST5:
                                SST6,SST7,SST8,SST9,SST10:
                                SST11,SST12,SST13,SST14,SST15:
                                SST16,SST17
          UNPACK    KEY67,SIIFALI,SIIFCAT                                       
          CALL      VSIIF000                * view catalog
          GOTO      HSIIFA20
.
HSIIFA60  MOVE      ZERO,OVRCD
          MATCH     SP70,SCHFLD01
          GOTO      HSIIFA61 IF EQUAL
          MATCH     SCHFLD01,SIIFCAT 
          GOTO      HSIIFA72 IF NOT EQUAL
HSIIFA61
....      MATCH     SP70,SCHFLD02
....      GOTO      HSIIFA62 IF EQUAL
....      MATCH     SCHFLD02,SIIFALI 
....      GOTO      HSIIFA72 IF NOT EQUAL
HSIIFA62
HSIIFA63
HSIIFA64
HSIIFA65
HSIIFA66
HSIIFA67
HSIIFA68
HSIIFA69
HSIIFA70
          GOTO      HSIIFA75
HSIIFA72  MOVE      ONE,OVRCD
HSIIFA75  RETURN
HSIIFA85
          LOAD      KEY67,XITEMNO,SST1,SST2,SST3,SST4,SST5:
                                SST6,SST7,SST8,SST9,SST10:
                                SST11,SST12,SST13,SST14,SST15:
                                SST16,SST17
          UNPACK    KEY67,SIIFALI,SIIFCAT                                       
          CALL      RDSIIF00
HSIIFA90
          UNPACK    SAVKEY80,SIIFALI,SIIFCAT                                    
          BEEP
          GOTO      HSIIFA20
HSIIFA91
          MOVE      SP70,SIIFTYP 
          MOVE      ZERO,SIIFATY 
          MOVE      SP70,SIIFSPA 
          MOVE      SP70,KEY67
          MOVE      SP70,SIIFCAT 
          MOVE      SP70,SIIACAT 
          GOTO      HSIIFA96
HSIIFA92
          MOVE      SP70,KEY67
          MOVE      SP70,SIIFCAT 
          LOAD      KEY67,CCITEM,SST1,SST2,SST3,SST4,SST5:
                                SST6,SST7,SST8,SST9,SST10:
                                SST11,SST12,SST13,SST14,SST15:
                                SST16,SST17
          UNPACK    KEY67,SIIFALI,SIIFCAT                                       
HSIIFA96
          PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
HSIIFA99
          MOVE      SIIFCAT,SIIACAT
          PACK      NATOCODE,SIIACAT,SP70
          RETURN
.------------------------------------------------------------
.
.----- Routines required for "V"iew Screen -----
.
.------------------------------------------------------------
. Routine   : VSIIF000
.
. Function  : Initialisation for Help Screen Display.
.              - Save Screen
.              - Display Box Details
.
. Called by : All Computer Generated Help Screens
.------------------------------------------------------------
VSIIF000  
          MOVE      HTOP,SHTOP         * restore screen vars
          MOVE      HLEF,SHLEF
          MOVE      HRIG,SHRIG
          MOVE      HBOT,SHBOT
.
.---- display sub screen ----
.
          ADD       "1",HTOP
          SUB       "1",HRIG
          ADD       "1",HLEF
          ASSIGN    HTOP+10,HBOT
          IF        HBOT>SHBOT
            ASSIGN    SHBOT-1,HBOT
          ENDIF
.
          CALL      GETSCR00
          BRANCH    EXIT,VSIIF999
.
          MOVE      HTOP,XTOP
          MOVE      HLEF,XLEF
          MOVE      HBOT,XBOT
          MOVE      HRIG,XRIG
.
          ADD       ONE,XTOP       * Cxxx Clip Box Size
          ADD       ONE,XLEF
          SUB       THREE,XBOT
          SUB       ONE,XRIG
.
          SUSPEND
.
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          ADD       ONE,XBOT
          DISPLAY   *PHRIG:XBOT,*G34,*PHLEF:XBOT,*G33;
          HLINE     *G37,XBOT,XLEF,XRIG
          ADD       ONE,XBOT
.
          DISPLAY   *PXLEF:XBOT," Hit Any key to continue";
          SUB       TWO,XBOT
.
....      CLIP      1,1,XRIG,XBOT  * Start Inside of Box Displays
.
          PACK      KEY7,SIIFCAT,SP70
          CALL      RDSIIA1                 * read catalog
          IF        OVRCD=0
            MOVE      XLEF,F2
            ADD       "40",F2
            MOVE      XTOP,SECTOR
            DISPLAY   *PXLEF:SECTOR," Catalog      : ",*V2LON,SIIACAT
.
            ADD       ONE,SECTOR
            DISPLAY   *PXLEF:SECTOR," Description  : ",*V2LON,SIIADES
.
            ADD       ONE,SECTOR
            IF        SIIANON=0
              MOVE      "Stock     ",KEY10
            ELSE
              MOVE      "Non-Stock ",KEY10
            ENDIF
            DISPLAY   *PXLEF:SECTOR," Catalog Type : ",*V2LON,KEY10,*HOFF:
                      *PF2:SECTOR," Subjective     : ",*V2LON,SIIASUB
.
            ADD       ONE,SECTOR
            DISPLAY   *PXLEF:SECTOR," Issue Unit   : ",*V2LON,SIIAISS,*HOFF:
                      *PF2:SECTOR," Pref Supplier  : ",*V2LON,SIIAPSC
.
            ADD       ONE,SECTOR
            DISPLAY   *PXLEF:SECTOR," NATO Number  : ",*V2LON,SIIANAT,*HOFF:
                      *PF2:SECTOR," Stock Catagory : ",*V2LON,SIIASTI
.
            ADD       ONE,SECTOR
            DISPLAY   *PXLEF:SECTOR," Average Cost : ",*V2LON,SIIAAVC
          ENDIF
.
          FLUSH     HLEF,HTOP,HRIG,HBOT
          KEYIN     *NOEDIT,*+,*EOFF,ANS
.
.---- restore help screen ----
.
          PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
.
          MOVE      SHTOP,HTOP         * restore screen vars
          MOVE      SHLEF,HLEF
          MOVE      SHRIG,HRIG
          MOVE      SHBOT,HBOT
.
VSIIF999  RETURN
.------------------------------------------------------------
. Routine   : GSIIF000
.
. Function  : Initialisation for Help Screen Display.
.              - Save Screen
.              - Display Box Details
.
. Called by : All Computer Generated Help Screens
.------------------------------------------------------------
GSIIF000  
          MOVE      ZERO,EXIT
          MOVE      ONE,CSEARCH
.
          CALL      GETSCR00
          BRANCH    EXIT,GSIIF999
.
          MOVE      HTOP,CTOP
          MOVE      HLEF,CLEF
          MOVE      HBOT,CBOT
          MOVE      HRIG,CRIG
.
          ADD       TWO,CTOP       * Cxxx Clip Box Size
          ADD       ONE,CLEF
          SUB       THREE,CBOT
          SUB       ONE,CRIG
          MOVE      HBOT,MAXITEM
          SUB       HTOP,MAXITEM
          SUB       "4",MAXITEM
.
          SUSPEND
.
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          ADD       ONE,CBOT
          DISPLAY   *PHRIG:CBOT,*G34,*PHLEF:CBOT,*G33;
          HLINE     *G37,CBOT,CLEF,CRIG
          ADD       ONE,CBOT
.
          CLIP      1,1,CRIG,24    * Start Inside of Box Displays
.
          DISPLAY   *PCLEF:CBOT,"e",*V2LON,"X",*HOFF,"it, ex":
                                    *V2LON,"P",*HOFF,"and, ":
                                    *V2LON,"U",*HOFF,"p, ":
                                    *V2LON,"D",*HOFF,"own, ":
                                    *V2LON,"S",*HOFF,"earch, ":
                                    *V2LON,"H",*HOFF,"ome, ":
                                    *V2LON,"E",*HOFF,"nd, ":
                                    *V2LON,"R",*HOFF,"educe, ":
                                    *V2LON,"V",*HOFF,"iew ";
.exit, expand, up, down, search, home, end, reduce, view, more
          SUB       TWO,CBOT
.
          MOVE      HTOP,MINITEM
          ADD       ONE,MINITEM
          MOVE      MINITEM,STDROW
.
          MOVE      HLEF,STDCOL00
          ADD       ONE,STDCOL00
          DISPLAY   *PSTDCOL00:STDROW,*V2LON,*ULON,"Item"
.
GSIIF999  RETURN
.
.------------------------------------------------------------
. Function    : Select Option Function for Help Screens
.
. Called by   : All Computer Generated Help Screens
.
. Exit Values : 0 = Item Selected      
.               1 = Page Up
.               2 = Page Down            
.               3 = Home
.               4 = End      
.               5 = Display Screen from start
.               6 = Exit
.               7 = Search
.               8 = View
.------------------------------------------------------------
SSIIF000  MOVE      ZERO,CCITEM
          MOVE      SP2,CCITEMD
.
          KEYIN     *PCLEF:CBOT,*NOEDIT,*+,*EOFF,ANS
          GOTO      SSIIF300 IF PAGEDOWN
          GOTO      SSIIF350 IF PAGEUP
          GOTO      SSIIF400 IF HOME
          GOTO      SSIIF450 IF END
          GOTO      SSIIF500 IF RIGHT
          GOTO      SSIIF550 IF LEFT 
          GOTO      SSIIF600 IF UP
          GOTO      SSIIF650 IF DOWN
          RESET     ANS
          GOTO      SSIIF250 IF EOS
          TYPE      ANS
          GOTO      SSIIF220 IF NOT EQUAL
          MOVE      ANS,CCITEM
.
          MOVE      XITEMNO,F3
          DIV       TEN,F3
          COMPARE   CCITEM,F3          * Less than ten items on screen 
          GOTO      SSIIF210 IF LESS   * so no need to enter second character
.
          KEYIN     *PCLEF:CBOT,*NOEDIT,*+,*EOFF,ANS
          RESET     ANS
          GOTO      SSIIF210 IF EOS
          MULT      TEN,CCITEM
          MOVE      ANS,F1
          ADD       F1,CCITEM
.
SSIIF210  
          COMPARE   ONE,CCITEM
          GOTO      SSIIF900 IF LESS       * Invalid Entry
          COMPARE   CCITEM,XITEMNO
          GOTO      SSIIF900 IF LESS       * Invalid Entry
          MOVE      ZERO,EXIT
          GOTO      SSIIF999               * Item Selected
.
SSIIF220 
          REP       UPPLOW,ANS
          MATCH     SP70,ANS           * Next Item
          GOTO      SSIIF650 IF EQUAL  
          MATCH     "X",ANS             * Exit
          GOTO      SSIIF910 IF EQUAL
          MATCH     "D",ANS             * Page Down
          GOTO      SSIIF300 IF EQUAL
          MATCH     "U",ANS             * Page Up
          GOTO      SSIIF350 IF EQUAL
          MATCH     "H",ANS             * Home
          GOTO      SSIIF400 IF EQUAL
          MATCH     "E",ANS             * End
          GOTO      SSIIF450 IF EQUAL
          MATCH     "S",ANS             * Search
          GOTO      SSIIF480 IF EQUAL
          MATCH     "P",ANS             * Left  (Expand Display)
          GOTO      SSIIF500 IF EQUAL
          MATCH     "R",ANS             * Right (Reduce Display)
          GOTO      SSIIF550 IF EQUAL
          MATCH     "V",ANS             * View Catalog
          GOTO      SSIIF700 IF EQUAL
          GOTO      SSIIF900               * Invalid Entry
.
SSIIF250  
          MOVE      DITEM,CCITEM
          COMPARE   CCITEM,XITEMNO
          GOTO      SSIIF900 IF LESS       * Invalid Entry
          MOVE      ZERO,EXIT
          GOTO      SSIIF999               * Item Selected
.         
. Page Down
.
SSIIF300  MOVE      ONE,EXIT
          GOTO      SSIIF999
.
. Page Up
.
SSIIF350  MOVE      TWO,EXIT
          GOTO      SSIIF999
.
. Home
.
SSIIF400  MOVE      THREE,EXIT
          GOTO      SSIIF999
.
. End
.
SSIIF450  MOVE      FOUR,EXIT
          GOTO      SSIIF999
.
. Search
.
SSIIF480  MOVE      SEVEN,EXIT
          GOTO      SSIIF999
.
. Expand Display
.
SSIIF500  BRANCH    CSCRSIZ,SSIIF900
          MOVE      ONE,CSCRSIZ
          SUSPEND
          CALL      PUTSCR00
          MOVE      HTOP,STOP     * Save Screen Size
          MOVE      HLEF,SLEF
          MOVE      HBOT,SBOT
          MOVE      HRIG,SRIG
          MOVE      "3",HTOP      * Full Screen Display Size
          MOVE      "1",HLEF
          MOVE      "24",HBOT
          MOVE      "80",HRIG
          MOVE      FIVE,EXIT     * Display Help Screen from Start
          GOTO      SSIIF999
.
.
. Reduce Display
.
SSIIF550  COMPARE   ZERO,CSCRSIZ
          GOTO      SSIIF900 IF EQUAL
          MOVE      ZERO,CSCRSIZ
          CALL      PUTSCR00
          MOVE      STOP,HTOP     * Restore Saved Screen Size
          MOVE      SLEF,HLEF
          MOVE      SBOT,HBOT
          MOVE      SRIG,HRIG
          MOVE      FIVE,EXIT     * Display Help Screen from Start
          GOTO      SSIIF999
.
. Up Arrow. Highlight Last Item
.
SSIIF600  DISPLAY   *PSTDCOL00:DROW,*V2LON,SP1,DITEM,DOT
          COMPARE   DITEM,ONE
          GOTO      SSIIF610 IF NOT EQUAL
          COMPARE   XITEMNO,ZERO
          GOTO      SSIIF620 IF EQUAL
          MOVE      XITEMNO,DITEM
          MOVE      CTOP,DROW
          ADD       XITEMNO,DROW  
          ADD       ONE,DITEM
.
SSIIF610  SUB       ONE,DITEM
          SUB       ONE,DROW
SSIIF620  DISPLAY   *PSTDCOL00:DROW,*HON,*V2LON,SP1,DITEM,DOT
          GOTO      SSIIF000
.
. Down Arrow. Highlight Next Item
.
SSIIF650  DISPLAY   *PSTDCOL00:DROW,*V2LON,SP1,DITEM,DOT
          COMPARE   DITEM,XITEMNO
          GOTO      SSIIF660 IF NOT EQUAL
          MOVE      ZERO,DITEM
          MOVE      CTOP,DROW
          SUB       ONE,DROW
.
SSIIF660  COMPARE   XITEMNO,ZERO
          GOTO      SSIIF670 IF EQUAL
          ADD       ONE,DITEM
          ADD       ONE,DROW
SSIIF670  DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
          GOTO      SSIIF000
.
SSIIF700  
          MOVE      DITEM,CCITEM
          COMPARE   CCITEM,XITEMNO
          GOTO      SSIIF900 IF LESS       * Invalid Entry
          MOVE      EIGHT,EXIT
          GOTO      SSIIF999               * Item Selected
.
SSIIF900  BEEP
          GOTO      SSIIF000
.
SSIIF910  MOVE      SIX,EXIT
.
SSIIF999  RETURN
.----------------------------------------------------------------------
. check if record valid
.----------------------------------------------------------------------
CSIIFA00  MOVE      ZERO,OVRCD
          BRANCH    CSEARCH,CSIIFA50
.
. Wild Card "*" Search of Description
.
          MOVE      SIIFALI,KEY60
          REP       UPPLOW,KEY60
          SCAN      SCHFLD02,KEY60
          IF        !@EQUAL
            MOVE      ONE,OVRCD
          ENDIF
          GOTO      CSIIFA99

.
. Normal Search by Catalog or Description
.
CSIIFA50  COMPARE   TWO,CKEYTYPE         * Catalog Search
          GOTO      CSIIFA99 IF EQUAL    * Therefore No Check 
.
          MATCH     SCHFLD02,SP70
          IF        !@EQUAL
            MATCH     SCHFLD02,SIIFALI
            IF        !@EQUAL
              MOVE      ONE,OVRCD
            ENDIF
          ENDIF
.
CSIIFA99  RETURN
.
.----- loop control routines ----
.
RDSIIF00  IF        CKEYTYPE=1
            CALL      RDSIIF1
          ELSE
            UNPACK    KEY67,SIIFALI,SIIFCAT
            PACK      KEY7,SIIFCAT,SP70
            CALL      RDSIIA1
            PACK      KEY67,SIIFALI,SIIFCAT
.
            MOVE      SIIACAT,SIIFCAT
            MOVE      SIIADES,SIIFALI
            MOVE      SIIANON,SIIFTYP
            MOVE      "1",SIIFATY
          ENDIF
          RETURN
.
RSSIIF00  IF        CKEYTYPE=1
            CALL      RSSIIF1
          ELSE
            CALL      RSSIIA1
          ENDIF
          RETURN
.
RKSIIF00  IF        CKEYTYPE=1
            CALL      RKSIIF1
            IF        CSEARCH=1
              MATCH     SIIFALI,SCHFLD02
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ENDIF
            ENDIF
          ELSE
            CALL      RKSIIA1
            IF        OVRCD=0
              MOVE      SIIACAT,SIIFCAT
              MOVE      SIIADES,SIIFALI
              MOVE      SIIANON,SIIFTYP
              MOVE      "1",SIIFATY
            ENDIF
          ENDIF
          RETURN
.
RPSIIF00  IF        CKEYTYPE=1
            CALL      RPSIIF1
            IF        CSEARCH=1
              MATCH     SCHFLD02,SIIFALI
              IF        !@EQUAL
                MOVE      ONE,OVRCD
              ENDIF
            ENDIF
          ELSE
            CALL      RPSIIA1
            IF        OVRCD=0
              MOVE      SIIACAT,SIIFCAT
              MOVE      SIIADES,SIIFALI
              MOVE      SIIANON,SIIFTYP
              MOVE      "1",SIIFATY
            ENDIF
          ENDIF
          RETURN
.
.----- loop control routines ----
.
RDSIIA00  PACK      KEY7,SIIACAT,SP70
          CALL      RDSIIA1
          RETURN
.
RSSIIA00  PACK      KEY7,SIIACAT,SP70
          CALL      RSSIIA1
          RETURN
.
RKSIIA00  CALL      RKSIIA1
          RETURN
.
RPSIIA00  CALL      RPSIIA1
          RETURN
.
.------------------------------------------------------------
.
. Function : Procedure for Keyword Seach of Catalog 
.
. Created  : 02.04.97 B.G.Ackland
.
. Parameters : CKEYSUSP    1=Show Suspended Items
.              CKEYSTCK    S=Show Stock Only      
.                          N=Show Non-Stock Only      
.
.------------------------------------------------------------
           DEFPROC   HSIKCA00
.
           INC       SINKCTFD/INC
           INC       SINCICFD/INC
           INC       SINCIEFD/INC
           INC       SINWARFD/INC
           INC       SINWSEFD/INC
           INC       APSMASFD/INC
.
DISPSOH   DIM       15
DISPSOO   DIM       15
F12       FORM      12
F2        FORM      2
STOCKFLG  FORM      1
FIRSTSEC  FORM      5
CATDES60  DIM       60
SAVKEY22  DIM       22
SIXTY     FORM      "60"
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
SAVSTCK   DIM       1
SAVSUSP   FORM      1
.---------------------------------------------------------------------------
.   System    :   Supply and Inventory               
.   Include   :   SINKCTKY
.   Routine   :   HSIKCA00 Catalog             
.   Return    :           
.-------------------------------------------------------------------------------
HSIKCA00  MOVE      CKEYSTCK,SAVSTCK
          MOVE      CKEYSUSP,SAVSUSP
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      SINKCTA1,"sinkctaf"
          TRAPCLR   IO
          IF        OVRCD=1
            BEEP
            MOVE      "ERROR: Keyword Search Table Missing (sinkctaf)- ",DISPMSG
            CALL      MESSAGE1
            GOTO      HSIKCAZZ
          ENDIF
.
          OPEN      SINKCTA2,"sinkctaf"
          MOVE      ZERO,CSCRSIZ
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      SINWSEA1,"sinwseaf"
          TRAPCLR   IO
          IF        OVRCD=1
            BEEP
            MOVE      "ERROR: Warehouse Security Table Missing (sinwseaf)- ",DISPMSG
            CALL      MESSAGE1
            GOTO      HSIKCAZZ
          ENDIF
.
          MOVE      "08",HTOP
          MOVE      "24",HBOT
          MOVE      "01",HLEF
          MOVE      "80",HRIG
.
HSIKCA04  CALL      GSIKC000
          BRANCH    EXIT,HSIKCA99
          MOVE      STDCOL00,STDCOL01
          ADD       " 5",STDCOL01
          DISPLAY   *PSTDCOL01:STDROW,*V2LON,*ULON,"Catalog"            
.
          MOVE      "All Catalogs",KEY12
          MATCH     "N",CKEYSTCK
          IF        @EQUAL
            MOVE      "Non-Stock   ",KEY12
          ENDIF
          MATCH     "S",CKEYSTCK
          IF        @EQUAL
            MOVE      "Stock       ",KEY12
          ENDIF
          MOVE      "Hide Suspended",KEY14
          IF        CKEYSUSP=1
            MOVE      "Show Suspended",KEY14
          ENDIF
          MOVE      STDCOL01,STDCOL02
          ADD       "8",STDCOL02
          DISPLAY   *PSTDCOL02:STDROW,*V2LON,*ULON,"Stk",*HOFF,SP1:
                    *V2LON,*ULON,"Description",*HOFF:
                    SP2,*V2LON,*ULON,"Flags: ",KEY12,SP1,KEY14
          MOVE      STDCOL02,STDCOL03
          ADD       "60",STDCOL03
          CLIP
          FLUSH     HLEF,HTOP,HRIG,HBOT
.
HSIKCA08  MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          ADD       ONE,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    STDROW+1,F2
          DISPLAY   *P21:F2,*V2LON,*ULON,"Enter Key Words for Catalog Search"
          ASSIGN    STDROW+3,F2
          KEYIN     *P10:F2,*V2LON,KEYWRD00
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          ENDSET    KEYWRD00
          APPEND    SP70,KEYWRD00
          RESET     KEYWRD00
          CALL      ESIKCA00     * Break Words into Seprate Fields
.
HSIKCA09  MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          MOVE      SP70,SIKCCAT 
.
          STRIP     KEYWRD01
          PACK      KEY22,KEYWRD01,SP70
          CALL      RSSIKC1 
HSIKCA10  CALL      RKSIKC1 
          BRANCH    OVRCD,HSIKCA15
          CALL      CHREC000
          BRANCH    OVRCD,HSIKCA10,HSIKCA15
.
          ADD       ONE,XITEMNO
          ADD       ONE,STDROW
          PACK      KEY22,SIKCKWD,SIKCCAT
          STORE     KEY22,XITEMNO,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          DISPLAY   *PSTDCOL00:STDROW,SP1,*V2LON,XITEMNO,DOT;
          IF        SIIASUS<>0
            DISPLAY   *V2LON,*BLINKON,"*"
          ENDIF
          DISPLAY   *PSTDCOL01:STDROW,SIKCCAT 
          IF        SIIANON=0
            MOVE      "Yes",KEY3
          ELSE
            MOVE      "No ",KEY3
          ENDIF
          DISPLAY   *PSTDCOL02:STDROW,KEY3,SP1,SIIADES 
          COMPARE   MAXITEM,XITEMNO
          GOTO      HSIKCA10 IF NOT EQUAL
.
HSIKCA15  MOVE      ONE,DITEM
          MOVE      MINITEM,DROW
          ADD       ONE,DROW
          DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
.
HSIKCA20  CALL      SSIKC000
          BRANCH    EXIT,HSIKCA30,HSIKCA35,HSIKCA40,HSIKCA45,HSIKCA04:
                              HSIKCA91,HSIKCA08,HSIKCA50,HSIKCA70
          GOTO      HSIKCA92
.
HSIKCA30  CALL      RKSIKC1 
          BRANCH    OVRCD,HSIKCA85
          CALL      CHREC000
          BRANCH    OVRCD,HSIKCA30,HSIKCA85
.
          PACK      KEY22,SIKCKWD,SIKCCAT,SP70                                  
          CALL      RPSIKC1 
          MOVE      ZERO,XITEMNO
          MOVE      MINITEM,STDROW
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HSIKCA10
.
HSIKCA35  MOVE      ST1,KEY22
          CALL      RDSIKC1 
.
HSIKCA36  CALL      RPSIKC1 
          BRANCH    OVRCD,HSIKCA85
          CALL      CHREC000
          BRANCH    OVRCD,HSIKCA36,HSIKCA85
          PACK      KEY22,SIKCKWD,SIKCCAT,SP70                                  
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
.
HSIKCA37  CALL      RPSIKC1 
          BRANCH    OVRCD,HSIKCA38
          CALL      CHREC000
          BRANCH    OVRCD,HSIKCA37,HSIKCA38
.
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HSIKCA37 IF NOT EQUAL
.
HSIKCA38  MOVE      ZERO,XITEMNO
          GOTO      HSIKCA10
.
HSIKCA40  MOVE      ST1,KEY22
          CALL      RDSIKC1 
HSIKCA41  CALL      RPSIKC1 
          BRANCH    OVRCD,HSIKCA85
          CALL      CHREC000
          BRANCH    OVRCD,HSIKCA41,HSIKCA85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HSIKCA09
.
HSIKCA45  CALL      RKSIKC1 
          BRANCH    OVRCD,HSIKCA85
          CALL      CHREC000
          BRANCH    OVRCD,HSIKCA45,HSIKCA85
.
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          MOVE      ZERO,XITEMNO
          PACK      KEY22,KEYWRD01,Z70                                          
          MOVE      MINITEM,STDROW
          MOVE      MAXITEM,XITEMNO
          CALL      RSSIKC1 
.
HSIKCA46  CALL      RPSIKC1 
          BRANCH    OVRCD,HSIKCA47
          CALL      CHREC000
          BRANCH    OVRCD,HSIKCA46,HSIKCA47
.
          SUB       ONE,XITEMNO
          COMPARE   ZERO,XITEMNO
          GOTO      HSIKCA46 IF NOT EQUAL
.
HSIKCA47  MOVE      ZERO,XITEMNO
          GOTO      HSIKCA10
.
HSIKCA50  MOVE      SP70,KEY67
          MOVE      SP70,SIIFCAT 
          LOAD      KEY22,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY22,SIKCKWD,SIKCCAT                                       
          CALL      VSIKC000                * view catalog
          GOTO      HSIKCA20
.
HSIKCA70  CALL      FLAGS000
          BOXCLR    CLEF,CTOP,CRIG,CBOT
          GOTO      HSIKCA09
.
HSIKCA85  LOAD      KEY22,XITEMNO,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY22,SIKCKWD,SIKCCAT                                       
          CALL      RDSIKC1 
.
HSIKCA90  BEEP
          GOTO      HSIKCA20
.
HSIKCA91  MOVE      SP70,SIKCSPA 
          MOVE      SP70,KEY22
          MOVE      SP70,SIKCCAT 
          MOVE      SP70,SIIACAT 
          GOTO      HSIKCA96
.
HSIKCA92  MOVE      SP70,KEY22
          MOVE      SP70,SIKCCAT 
          LOAD      KEY22,CCITEM,ST1,ST2,ST3,ST4,ST5:                           
                                ST6,ST7,ST8,ST9,ST10:
                                ST11,ST12,ST13,ST14,ST15:
                                ST16,ST17
          UNPACK    KEY22,SIKCKWD,SIKCCAT                                       
          MOVE      SIKCCAT,SIIACAT 
.
HSIKCA96  PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
          MOVE      ZERO,HTOP
          MOVE      ZERO,HLEF
          MOVE      ZERO,HBOT
          MOVE      ZERO,HRIG
.
HSIKCA99  CLOSE     SINKCTA1
          CLOSE     SINKCTA2
          MOVE      SAVSTCK,CKEYSTCK
          MOVE      SAVSUSP,CKEYSUSP
          GOTO      HSIKCAZZ     * End Procedure
.------------------------------------------------------------
. Alter Flags
.------------------------------------------------------------
FLAGS000  BOXCLR    CLEF,CTOP,CRIG,CBOT
          ASSIGN    MINITEM+2,F2
FLAGS100  MOVE      "A",CKEYSTCK
          KEYIN     *PSTDCOL02:F2,*V2LON,"A",*HOFF,"ll, ":
                                  *V2LON,"S",*HOFF,"tock or ":
                                  *V2LON,"N",*HOFF,"on Stock : ":
                    *V2LON,*RV,CKEYSTCK
          REP       UPPLOW,CKEYSTCK
          MATCH     "A",CKEYSTCK
          GOTO      FLAGS200 IF EQUAL
          MATCH     "S",CKEYSTCK
          GOTO      FLAGS200 IF EQUAL
          MATCH     "N",CKEYSTCK
          GOTO      FLAGS200 IF EQUAL
          BEEP 
          GOTO      FLAGS100
FLAGS200  ASSIGN    MINITEM+4,F2
FLAGS210  MOVE      "N",ANS
          KEYIN     *PSTDCOL02:F2,"Show Suspended (",*V2LON,"Y",*HOFF,"/":
                                  *V2LON,"N",*HOFF,")    : ",*V2LON,*RV,ANS
          REP       UPPLOW,ANS
          MATCH     "Y",ANS
          GOTO      FLAGS220 IF EQUAL
          MATCH     "N",ANS
          GOTO      FLAGS210 IF NOT EQUAL
          MOVE      ZERO,CKEYSUSP
          GOTO      FLAGS290
.
FLAGS220  MOVE      ONE,CKEYSUSP
.
FLAGS290  MOVE      "All Catalogs",KEY12
          MATCH     "N",CKEYSTCK
          IF        @EQUAL
            MOVE      "Non-Stock   ",KEY12
          ENDIF
          MATCH     "S",CKEYSTCK
          IF        @EQUAL
            MOVE      "Stock       ",KEY12
          ENDIF
          MOVE      "Hide Suspended",KEY14
          IF        CKEYSUSP=1
            MOVE      "Show Suspended",KEY14
          ENDIF
          DISPLAY   *PSTDCOL02:MINITEM,*V2LON,*ULON,"Stk",*HOFF,SP1:
                    *V2LON,*ULON,"Description",*HOFF:
                    SP2,*V2LON,*ULON,"Flags: ",KEY12,SP1,KEY14
          RETURN 
.------------------------------------------------------------
. Check Record
.------------------------------------------------------------
CHREC000  MATCH     KEYWRD01,SIKCKWD
          GOTO      CHREC920 IF NOT EQUAL                                       
          IF        KEYWRDNO>1
            PACK      SAVKEY22,SIKCKWD,SIKCCAT
            CALL      ESIKCB00
            BRANCH    EXIT,CHREC910
            MOVE      SAVKEY22,KEY22
            CALL      RDSIKC1
          ENDIF
          MOVE      SIKCCAT,KEY7
          CALL      RDSIIA1
          BRANCH    OVRCD,CHREC910
          COMPARE   ZERO,SIIASUS
          GOTO      CHREC100 IF EQUAL
          COMPARE   ONE,CKEYSUSP
          GOTO      CHREC910 IF NOT EQUAL
.
CHREC100  MATCH     "S",CKEYSTCK
          GOTO      CHREC200 IF NOT EQUAL
          COMPARE   ZERO,SIIANON
          GOTO      CHREC910 IF NOT EQUAL
          GOTO      CHREC900
.
CHREC200  MATCH     "N",CKEYSTCK
          GOTO      CHREC900 IF NOT EQUAL
          COMPARE   ONE,SIIANON
          GOTO      CHREC910 IF NOT EQUAL
          GOTO      CHREC900
.
CHREC900  MOVE      ZERO,OVRCD
          GOTO      CHREC999
CHREC910  MOVE      ONE,OVRCD
          GOTO      CHREC999
CHREC920  MOVE      TWO,OVRCD
CHREC999  RETURN
.------------------------------------------------------------
. Break Keywords into Words
.------------------------------------------------------------
ESIKCA00  MOVE      ZERO,KEYWRDNO
          MOVE      SP70,KEYWRD01
          MOVE      SP70,KEYWRD02
          MOVE      SP70,KEYWRD03
          MOVE      SP70,KEYWRD04
          MOVE      SP70,KEYWRD05
          MOVE      SP70,KEYWRD06
          MOVE      SP70,KEYWRD07
          MOVE      SP70,KEYWRD08
          MOVE      SP70,KEYWRD09
          MOVE      SP70,KEYWRD10
ESIKCA10  CMATCH    SP1,KEYWRD00             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWRD00
            GOTO      ESIKCA10 IF NOT EOS
            GOTO      ESIKCA99               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWRD00,SP30        * reset form pointer
          MOVE      KEY60,KEYWRD00
.
          SCAN      SP1,KEYWRD00               * Locate the first blank
          GOTO      ESIKCA20 IF NOT EQUAL
          BUMP      KEYWRD00,-1              * go back 1 to end of name
          MOVEFPTR  KEYWRD00,CCITEM          * another handly form field
          RESET     KEYWRD00                 * reset the form pointer
          SETLPTR   KEYWRD00,CCITEM          * set logical length to end of name
.
.         Save this Word
.
ESIKCA20  MOVE      KEYWRD00,KEY15           
          ADD       ONE,KEYWRDNO
          REP       UPPLOW,KEY15
          STORE     KEY15,KEYWRDNO,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                                   KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10 
          GOTO      ESIKCA19
.
.         Check for any more words
.
ESIKCA19  SETLPTR   KEYWRD00,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWRD00,CCITEM        * reset to this point
          PACK      KEY60,KEYWRD00,SP30    * reset form pointer
          MOVE      KEY60,KEYWRD00
.
          GOTO      ESIKCA10
.
ESIKCA99  RETURN
.------------------------------------------------------------
. Check for Matching Words
.------------------------------------------------------------
ESIKCB00  MOVE      SIKCCAT,KEY7
          MOVE      ONE,F2
          MOVE      ZERO,EXIT
.
ESIKCB10  COMPARE   KEYWRDNO,F2
          GOTO      ESIKCB99 IF EQUAL
          ADD       ONE,F2
          LOAD      KEY15,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                             KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
          PACK      KEY22,KEY7,KEY15
          CALL      RDSIKC2
          COMPARE   ZERO,OVRCD
          GOTO      ESIKCB10 IF EQUAL
          CALL      RKSIKC2
          BRANCH    OVRCD,ESIKCB90
          MATCH     SIKCCAT,KEY7
          GOTO      ESIKCB90 IF NOT EQUAL
          STRIP     KEY15
          MATCH     SIKCKWD,KEY15
          GOTO      ESIKCB90 IF NOT EQUAL
          GOTO      ESIKCB10
.
ESIKCB90  MOVE      ONE,EXIT
ESIKCB99  RETURN
.
.------------------------------------------------------------
.
.----- Routines required for "V"iew Screen -----
.
.------------------------------------------------------------
.
. Routine   : VSIKC000
.
. Function  : View Catalog Details
.
.------------------------------------------------------------
VSIKC000  MOVE      HTOP,SHTOP         * restore screen vars
          MOVE      HLEF,SHLEF
          MOVE      HRIG,SHRIG
          MOVE      HBOT,SHBOT
          OPEN      APSMASA1,"apsmasaf"
          OPEN      SINCICA1,"sincicaf"
          OPEN      SINCIEA1,"sincieaf"
          OPEN      SINWARA1,"sinwaraf"
.
.---- display sub screen ----
.
          CALL      GETSCR00
          BRANCH    EXIT,VSIKC999
.
          MOVE      HTOP,XTOP
          MOVE      HLEF,XLEF
          MOVE      HBOT,XBOT
          MOVE      HRIG,XRIG
.
          ADD       ONE,XTOP       * Cxxx Clip Box Size
          ADD       ONE,XLEF
          SUB       THREE,XBOT
          SUB       ONE,XRIG
.
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          ADD       ONE,XBOT
          DISPLAY   *PHRIG:XBOT,*G34,*PHLEF:XBOT,*G33;
          HLINE     *G37,XBOT,XLEF,XRIG
          SUB       ONE,XBOT
.
          PACK      KEY7,SIKCCAT,SP70
          CALL      RDSIIA1                 * read catalog
          BRANCH    OVRCD,VSIKC800
.
          MOVE      XLEF,F2
          ADD       "35",F2
          MOVE      XTOP,SECTOR
.
          IF        SIIANON=0
            MOVE      "Stock     ",KEY10
          ELSE
            MOVE      "Non-Stock ",KEY10
          ENDIF
          DISPLAY   *PXLEF:SECTOR," Catalog      : ",*V2LON,SIIACAT,*HOFF:
                    *PF2:SECTOR," Catalog Type   : ",*V2LON,KEY10,*HOFF
.
          ADD       ONE,SECTOR
          DISPLAY   *PXLEF:SECTOR," Description  : ",*V2LON,SIIADES
.
          ADD       ONE,SECTOR
          IF        SIIANON=0
            MOVE      "Stock     ",KEY10
          ELSE
            MOVE      "Non-Stock ",KEY10
          ENDIF
          DISPLAY   *PXLEF:SECTOR," Issue Unit   : ",*V2LON,SIIAISS,*HOFF:
                    *PF2:SECTOR," Product Group  : ",*V2LON,SIIASUB
.
          ADD       ONE,SECTOR
          MOVE      SIIAPSC,KEY5
          CALL      RDAPMA1
          IF        OVRCD=1
            CLEAR     APMASN1
            APPEND    "Code Invalid ",APMASN1
            APPEND    KEY5,APMASN1
            RESET     APMASN1
          ENDIF
          MOVE      APMASN1,KEY24
          DISPLAY   *PXLEF:SECTOR," NATO Number  : ",*V2LON,SIIANAT,*HOFF:
                    *PF2:SECTOR," Pref Supplier  : ",*V2LON,KEY24
.
          ADD       ONE,SECTOR
          DISPLAY   *PXLEF:SECTOR," Average Cost : ",*V2LON,SIIAAVC,*HOFF:
                    *PF2:SECTOR," Stock Catagory : ",*V2LON,SIIASTI
          MOVE      SECTOR,FIRSTSEC
          MOVE      ZERO,STOCKFLG
.
VSIKC090  ADD       ONE,SECTOR
          DISPLAY   *PXLEF:SECTOR,SP1:
                    *V2LON,*ULON,"Supplier                ",*HOFF,SP1:
                    *V2LON,*ULON,"Unit      ",*HOFF,SP1:
                    *V2LON,*ULON,"Part Number     ",*HOFF,SP1:
                    *V2LON,*ULON,"Lead Time",*HOFF,SP1:
                    *V2LON,*ULON,"   Curr Cost",*HOFF,SP1
.         
          PACK      KEY27,SIIACAT,SP70
          CALL      RSSIIC1
VSIKC100  CALL      RKSIIC1
          BRANCH    OVRCD,VSIKC800
          MATCH     SIICCAT,SIIACAT
          GOTO      VSIKC800 IF NOT EQUAL
.
          ADD       ONE,SECTOR
          IF        SECTOR>XBOT
            GOTO      VSIKC800 
          ENDIF
          MOVE      SIICSUP,KEY5
          CALL      RDAPMA1
          IF        OVRCD=1
            CLEAR     APMASN1
            APPEND    "Code Invalid ",APMASN1
            APPEND    KEY5,APMASN1
            RESET     APMASN1
          ENDIF
.
          MOVE      APMASN1,KEY24
          MOVE      SIICSUT,KEY10
          MOVE      SIICPNO,KEY19
          MOVE      SIICPD1,KEY30
          MOVE      "9999999.9999",KEY12
          FEDIT     SIICCCT,KEY12
          MATCH     SIIAPSC,SIICSUP
          IF        @EQUAL
            DISPLAY   *PXLEF:SECTOR,*V2LON,SP1,KEY24,SP1,KEY10,SP1,KEY19,SP1:
                                    SIICLTM,SP1,KEY12
          ELSE
            DISPLAY   *PXLEF:SECTOR,SP1,KEY24,SP1,KEY10,SP1,KEY19,SP1:
                                    SIICLTM,SP1,KEY12
          ENDIF
          GOTO      VSIKC100
.
VSIKC190  ADD       ONE,SECTOR
          DISPLAY   *PXLEF:SECTOR,SP1:
                    *V2LON,*ULON,"Warehouse                     ",*HOFF,SP1:
                    *V2LON,*ULON,"Rack & Shelf",*HOFF,SP1:
                    *V2LON,*ULON,"   Stock on Hand",*HOFF,SP1:
                    *V2LON,*ULON," Stock on Order",*HOFF,SP1
          PACK      KEY12,SIIACAT,SP70
          CALL      RSSIIE1
VSIKC200  CALL      RKSIIE1
          BRANCH    OVRCD,VSIKC800
          MATCH     SIIECAT,SIIACAT
          GOTO      VSIKC800 IF NOT EQUAL
.
          ADD       ONE,SECTOR
          IF        SECTOR>XBOT
            GOTO      VSIKC800
          ENDIF
.
. check warehouse security
.
          PACK      KEY9,PASSCODE,SIIEWAR,SP70
          CALL      RDSIWS1
          BRANCH    OVRCD,VSIKC200      * no warehouse access
.
          MOVE      SIIEWAR,KEY5        * warehouse access is granted
          CALL      RDSIWA1
          MOVE      SIWADES,KEY30
          MOVE      "999,999,999,999",DISPSOH
          MOVE      "999,999,999,999",DISPSOO
          MOVE      SIIESOH,F12
          FEDIT     F12,DISPSOH
          MOVE      SIIESOO,F12
          FEDIT     F12,DISPSOO
          DISPLAY   *PXLEF:SECTOR,SP1,KEY30,SP3,SIIERAS,SP4,DISPSOH,SP1,DISPSOO
          GOTO      VSIKC200
.
VSIKC800  ADD       TWO,XBOT
          IF        STOCKFLG=0
            DISPLAY   *PXLEF:XBOT,SP1,*V2LON,"D",*HOFF,"own, ":
                                  *V2LON,"H",*HOFF,"ome, ":
                                  *V2LON,"S",*HOFF,"tock, e":
                                  *V2LON,"X",*HOFF,"it      "
          ELSE
            DISPLAY   *PXLEF:XBOT,SP1,*V2LON,"D",*HOFF,"own, ":
                                  *V2LON,"H",*HOFF,"ome, ":
                                  *V2LON,"S",*HOFF,"uppliers, e":
                                  *V2LON,"X",*HOFF,"it       "
          ENDIF
          BRANCH    SIIASUS,VSIKC801,VSIKC802,VSIKC803
          GOTO      VSIKC804
VSIKC801  DISPLAY   *P50:XBOT,*V2LON,*BLINKON,"Suspended for Requisitions"
          GOTO      VSIKC804
VSIKC802  DISPLAY   *P50:XBOT,*V2LON,*BLINKON,"Suspended for Purchase Orders"
          GOTO      VSIKC804
VSIKC803  DISPLAY   *P50:XBOT,*V2LON,*BLINKON,"Suspended"
.
VSIKC804  SUB       TWO,XBOT
VSIKC809  KEYIN     *NOEDIT,*+,*EOFF,ANS
          GOTO      VSIKC810 IF PAGEDOWN
          GOTO      VSIKC820 IF HOME
          REP       UPPLOW,ANS
          MATCH     "X",ANS
          GOTO      VSIKC900 IF EQUAL
          MATCH     "D",ANS
          GOTO      VSIKC810 IF EQUAL
          MATCH     "H",ANS
          GOTO      VSIKC820 IF EQUAL
          MATCH     "S",ANS
          GOTO      VSIKC830 IF EQUAL
          BEEP
          GOTO      VSIKC800
.
VSIKC810  CALL      CLRLIN00
          MOVE      FIRSTSEC,SECTOR
          ADD       ONE,SECTOR
          BRANCH    STOCKFLG,VSIKC200
          GOTO      VSIKC100
.
VSIKC820  CALL      CLRLIN00
          MOVE      FIRSTSEC,SECTOR
          BRANCH    STOCKFLG,VSIKC190
          GOTO      VSIKC090
.
VSIKC830  CALL      CLRLIN00
          IF        STOCKFLG=1
            MOVE      ZERO,STOCKFLG
          ELSE
            MOVE      ONE,STOCKFLG
          ENDIF
          MOVE      FIRSTSEC,SECTOR
          BRANCH    STOCKFLG,VSIKC190
          GOTO      VSIKC090
.---- restore help screen ----
.
VSIKC900  PUTSCR    CSSCRNUM,HLEF,HTOP,HRIG,HBOT
          CALL      FRESCR00
.
          MOVE      SHTOP,HTOP         * restore screen vars
          MOVE      SHLEF,HLEF
          MOVE      SHRIG,HRIG
          MOVE      SHBOT,HBOT
.
          CLOSE     APSMASA1
          CLOSE     SINCICA1
.
VSIKC999  RETURN
.------------------------------------------------------------
. Clear Supplier Details 
.------------------------------------------------------------
CLRLIN00  MOVE      FIRSTSEC,SECTOR
          ADD       ONE,SECTOR
CLRLIN10  ADD       ONE,SECTOR
          IF        SECTOR>XBOT
            GOTO      CLRLIN99 
          ENDIF
          DISPLAY   *PXLEF:SECTOR,SP70,SP8
          GOTO      CLRLIN10
CLRLIN99  RETURN
.------------------------------------------------------------
. Routine   : GSIKC000
.
. Function  : Initialisation for Help Screen Display.
.              - Save Screen
.              - Display Box Details
.
. Called by : All Computer Generated Help Screens
.------------------------------------------------------------
GSIKC000  
          MOVE      ZERO,EXIT
          MOVE      ONE,CSEARCH
.
          CALL      GETSCR00
          BRANCH    EXIT,GSIKC999
.
          MOVE      HTOP,CTOP
          MOVE      HLEF,CLEF
          MOVE      HBOT,CBOT
          MOVE      HRIG,CRIG
.
          ADD       TWO,CTOP       * Cxxx Clip Box Size
          ADD       ONE,CLEF
          SUB       THREE,CBOT
          SUB       ONE,CRIG
          MOVE      HBOT,MAXITEM
          SUB       HTOP,MAXITEM
          SUB       "4",MAXITEM
.
          SUSPEND
.
          BOXCLR    HLEF,HTOP,HRIG,HBOT
          BOX       16,HLEF,HTOP,HRIG,HBOT
          ADD       ONE,CBOT
          DISPLAY   *PHRIG:CBOT,*G34,*PHLEF:CBOT,*G33;
          HLINE     *G37,CBOT,CLEF,CRIG
          ADD       ONE,CBOT
.
          CLIP      1,1,CRIG,24    * Start Inside of Box Displays
.
          DISPLAY   *PCLEF:CBOT,"e",*V2LON,"X",*HOFF,"it, ex":
                                    *V2LON,"P",*HOFF,"and, ":
                                    *V2LON,"U",*HOFF,"p, ":
                                    *V2LON,"D",*HOFF,"own, ":
                                    *V2LON,"S",*HOFF,"earch, ":
                                    *V2LON,"H",*HOFF,"ome, ":
                                    *V2LON,"E",*HOFF,"nd, ":
                                    *V2LON,"R",*HOFF,"educe, ":
                                    *V2LON,"V",*HOFF,"iew, ":
                                    *V2LON,"F",*HOFF,"lags ";
.exit, expand, up, down, search, home, end, reduce, view, Flags
          SUB       TWO,CBOT
.
          MOVE      HTOP,MINITEM
          ADD       ONE,MINITEM
          MOVE      MINITEM,STDROW
.
          MOVE      HLEF,STDCOL00
          ADD       ONE,STDCOL00
          DISPLAY   *PSTDCOL00:STDROW,*V2LON,*ULON,"Item"
.
GSIKC999  RETURN
.
.------------------------------------------------------------
. Function    : Select Option Function for Help Screens
.
. Called by   : All Computer Generated Help Screens
.
. Exit Values : 0 = Item Selected      
.               1 = Page Up
.               2 = Page Down            
.               3 = Home
.               4 = End      
.               5 = Display Screen from start
.               6 = Exit
.               7 = Search
.               8 = View
.------------------------------------------------------------
SSIKC000  MOVE      ZERO,CCITEM
          MOVE      SP2,CCITEMD
.
          KEYIN     *PCLEF:CBOT,*NOEDIT,*+,*EOFF,ANS
          GOTO      SSIKC300 IF PAGEDOWN
          GOTO      SSIKC350 IF PAGEUP
          GOTO      SSIKC400 IF HOME
          GOTO      SSIKC450 IF END
          GOTO      SSIKC500 IF RIGHT
          GOTO      SSIKC550 IF LEFT 
          GOTO      SSIKC600 IF UP
          GOTO      SSIKC650 IF DOWN
          RESET     ANS
          GOTO      SSIKC250 IF EOS
          TYPE      ANS
          GOTO      SSIKC220 IF NOT EQUAL
          MOVE      ANS,CCITEM
.
          MOVE      XITEMNO,F3
          DIV       TEN,F3
          COMPARE   CCITEM,F3          * Less than ten items on screen 
          GOTO      SSIKC210 IF LESS   * so no need to enter second character
.
          KEYIN     *PCLEF:CBOT,*NOEDIT,*+,*EOFF,ANS
          RESET     ANS
          GOTO      SSIKC210 IF EOS
          MULT      TEN,CCITEM
          MOVE      ANS,F1
          ADD       F1,CCITEM
.
SSIKC210  
          COMPARE   ONE,CCITEM
          GOTO      SSIKC900 IF LESS       * Invalid Entry
          COMPARE   CCITEM,XITEMNO
          GOTO      SSIKC900 IF LESS       * Invalid Entry
          MOVE      ZERO,EXIT
          GOTO      SSIKC999               * Item Selected
.
SSIKC220 
          REP       UPPLOW,ANS
          MATCH     SP70,ANS           * Next Item
          GOTO      SSIKC650 IF EQUAL  
          MATCH     "X",ANS             * Exit
          GOTO      SSIKC910 IF EQUAL
          MATCH     "D",ANS             * Page Down
          GOTO      SSIKC300 IF EQUAL
          MATCH     "U",ANS             * Page Up
          GOTO      SSIKC350 IF EQUAL
          MATCH     "H",ANS             * Home
          GOTO      SSIKC400 IF EQUAL
          MATCH     "E",ANS             * End
          GOTO      SSIKC450 IF EQUAL
          MATCH     "S",ANS             * Search
          GOTO      SSIKC480 IF EQUAL
          MATCH     "P",ANS             * Left  (Expand Display)
          GOTO      SSIKC500 IF EQUAL
          MATCH     "R",ANS             * Right (Reduce Display)
          GOTO      SSIKC550 IF EQUAL
          MATCH     "V",ANS             * View Catalog
          GOTO      SSIKC700 IF EQUAL
          MATCH     "F",ANS             * View Catalog
          GOTO      SSIKC750 IF EQUAL
          GOTO      SSIKC900               * Invalid Entry
.
SSIKC250  
          MOVE      DITEM,CCITEM
          COMPARE   CCITEM,XITEMNO
          GOTO      SSIKC900 IF LESS       * Invalid Entry
          MOVE      ZERO,EXIT
          GOTO      SSIKC999               * Item Selected
.         
. Page Down
.
SSIKC300  MOVE      ONE,EXIT
          GOTO      SSIKC999
.
. Page Up
.
SSIKC350  MOVE      TWO,EXIT
          GOTO      SSIKC999
.
. Home
.
SSIKC400  MOVE      THREE,EXIT
          GOTO      SSIKC999
.
. End
.
SSIKC450  MOVE      FOUR,EXIT
          GOTO      SSIKC999
.
. Search
.
SSIKC480  MOVE      SEVEN,EXIT
          GOTO      SSIKC999
.
. Expand Display
.
SSIKC500  BRANCH    CSCRSIZ,SSIKC900
          MOVE      ONE,CSCRSIZ
          SUSPEND
          CALL      PUTSCR00
          MOVE      HTOP,STOP     * Save Screen Size
          MOVE      HLEF,SLEF
          MOVE      HBOT,SBOT
          MOVE      HRIG,SRIG
          MOVE      "3",HTOP      * Full Screen Display Size
          MOVE      "1",HLEF
          MOVE      "24",HBOT
          MOVE      "80",HRIG
          MOVE      FIVE,EXIT     * Display Help Screen from Start
          GOTO      SSIKC999
.
.
. Reduce Display
.
SSIKC550  COMPARE   ZERO,CSCRSIZ
          GOTO      SSIKC900 IF EQUAL
          MOVE      ZERO,CSCRSIZ
          CALL      PUTSCR00
          MOVE      STOP,HTOP     * Restore Saved Screen Size
          MOVE      SLEF,HLEF
          MOVE      SBOT,HBOT
          MOVE      SRIG,HRIG
          MOVE      FIVE,EXIT     * Display Help Screen from Start
          GOTO      SSIKC999
.
. Up Arrow. Highlight Last Item
.
SSIKC600  DISPLAY   *PSTDCOL00:DROW,*V2LON,SP1,DITEM,DOT
          COMPARE   DITEM,ONE
          GOTO      SSIKC610 IF NOT EQUAL
          COMPARE   XITEMNO,ZERO
          GOTO      SSIKC620 IF EQUAL
          MOVE      XITEMNO,DITEM
          MOVE      CTOP,DROW
          ADD       XITEMNO,DROW  
          ADD       ONE,DITEM
.
SSIKC610  SUB       ONE,DITEM
          SUB       ONE,DROW
SSIKC620  DISPLAY   *PSTDCOL00:DROW,*HON,*V2LON,SP1,DITEM,DOT
          GOTO      SSIKC000
.
. Down Arrow. Highlight Next Item
.
SSIKC650  DISPLAY   *PSTDCOL00:DROW,*V2LON,SP1,DITEM,DOT
          COMPARE   DITEM,XITEMNO
          GOTO      SSIKC660 IF NOT EQUAL
          MOVE      ZERO,DITEM
          MOVE      CTOP,DROW
          SUB       ONE,DROW
.
SSIKC660  COMPARE   XITEMNO,ZERO
          GOTO      SSIKC670 IF EQUAL
          ADD       ONE,DITEM
          ADD       ONE,DROW
SSIKC670  DISPLAY   *PSTDCOL00:DROW,*V2LON,*HON,SP1,DITEM,DOT
          GOTO      SSIKC000
.
SSIKC700  MOVE      DITEM,CCITEM
          COMPARE   CCITEM,XITEMNO
          GOTO      SSIKC900 IF LESS       * Invalid Entry
          MOVE      EIGHT,EXIT
          GOTO      SSIKC999               * Item Selected
.
SSIKC750  MOVE      NINE,EXIT
          GOTO      SSIKC999               * Item Selected
.
SSIKC900  BEEP
          GOTO      SSIKC000
.
SSIKC910  MOVE      SIX,EXIT
.
SSIKC999  RETURN
.

          INC       SINKCTIO/INC
          INC       SINCICIO/INC
          INC       SINCIEIO/INC
          INC       SINWARIO/INC
          INC       SINWSEIO/INC
          INC       APSMASIO/INC
          INC       IBAOVRIO/INC
.
HSIKCAZZ  ENDPROC   
.
