.* System         : Patient Billing                                           *
.* Include Name   : GETTFEES.PBL                                              *
.* Author         : J.Tan                                                     *
.* Date           : 13/01/2011                                                *
.* Function       : Routine to get a valid Theatre fees                       *
.*                                                                            *
.* Requires       : KEY18 - key for theatre fees record                       *
.*                  CNTRCEFR - Contract Effective date details                *
.*                  CEFFDATE - Effective date calculation                     *
.*                  TSRVDATE - Service date                                   *
.*                                                                            *
.* Returns        : MBSCONTR - Contract ID                                    *
.*                  EXIT  - 0 Found valid Theatre fees                        *
.*                        - 1 No valid Theatre fees                           *
.*                        - 2 Default record found                            *
.*                                                                            *
.* Modification   :                                                           *
.*                  V11.04.01 08/03/2024  J.Tan     TSK 0919335               *
.*                  Mod checking for HF History file                          *
.*                  V10.08.01 06/07/2016 J.Tan   TSK 0822042                  *
.*                  Fixed Johns hopkins theatre fees                          *
.*                  V10.04.01 04/02/2014 J.Tan   CAR 285293                   *
.*                  Fixed Theatre Banding                                     *
.*                  V10.03.04 11/11/2011 J.Tan   CAR 255151                   *
.*                  Fixed to look for valid Contract id before using default  *
.*                  V10.03.03 24/11/2011 J.Tan   CAR 255151                   *
.*                  Fixed searching for next theatre fees after finding an    *
.*                  invalid Contract                                          *
.*                  V10.03.02 11/11/2011 J.Tan   CAR 255151                   *
.*                  Fixed to check for Exact match of banding                 *
.*                  V10.03.01 09/11/2011 J.Tan   CAR 255042                   *
.*                  Fixed to reset HF type for overnight if daycase not found *
. ******************************************************************************
GETTFEES  OPEN      PATTFEE1,"pattfeef"
          OPEN      PMSCIDA1,"pmscidaf"
          OPEN      PATFN1A1,"patfn1af"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TEN9;*212,CFEETYP
.
          MOVE      ZERO,THCFLG
          MOVE      SP70,MBSCONTR
          MOVE      ZERO,F2
          MOVE      KEY18,DIM18
          UNPACK    KEY18,STFCLM,STFHFUND,STFHFTAB,STFDAYC
.
          COMPARE   NINE,CFEETYP
          GOTO      GETT3000 IF EQUAL        * Johns Hopkins
.
          COMPARE   THREE,CNTRCEFR
          GOTO      GETT9000 IF EQUAL        * Invalid Contract Effective detail
.
          UNPACK    KEY18,STFCLM,STFHFUND,STFHFTAB,STFDAYC
          MOVE      SP70,PTHFBCAT             * HF Table type
          PACK      KEY14,STFHFUND,STFHFTAB
          CALL      RDFUND1
          MOVE      PTHFBCAT,SPTTFHTY
.
          PACK      KEY13,STFCLM,STFHFUND,SPTTFHTY,STFDAYC,SP70
          MOVE      STFDAYC,KEY1
.
GETT1000  UNPACK    KEY13,STFCLM,STFHFUND,SPTTFHTY,STFDAYC
.
          MOVE      ZERO,TCFORM6
          MOVE      ZERO,HFBAND
          PACK      KEY5 WITH ANSC,ANSL,STFCLM
          CALL      RDCODE1
          IF        OVRCD<>1
            MOVE      TCFORM6,HFBAND
          ENDIF
.
          MATCH     SP70,STFHFUND
          IF        !@EQUAL
            PACK      KEY14 WITH STFHFUND,ZERO,ZERO,ZERO,ZERO,SP10
            CALL      RDFUND1
.
.           Check HF history file
.
            MOVE      "03",KEY2        * Theatre charging indicator
            PACK      KEY17,STFHFUND,KEY2,TSRVDATE,SP70
            CALL      PATDDHRD
          ENDIF
.
          MOVE      HFBAND,THCFLG
.
          MOVE      SP70,KEY6
          MOVE      SP70,DIM5
          MOVE      IAMT,DIM5
          LOAD      DIM5,THCFLG,IBAND1,IBAND2,IBAND3,IBAND4,IBAND5:
                                IBAND6,IBAND7,IBAND8,IBAND9,IBAND10:
                                IBAND11,IBAND12,IBAND13,IBAND14,IBAND15:
                                IBAND16
          PACK      DIM5,DIM5,SP70
.
          PACK      KEY24,KEY13,DIM5,SP70
          CALL      RDSTFEE1
          CALL      RDKTFEE1
          BRANCH    OVRCD OF GETT2400
.
          PACK      DIM18A WITH TFCLM,TFHFUND,PTTFHTYP,TFDAYC,TFENDMBS,SP70
          MATCH     DIM18A,KEY24
          GOTO      GETT2800 IF EQUAL
.
.         If we are using banding, then try next option
.
GETT2400  COMPARE   ZERO,THCFLG
          GOTO      GETT4000 IF NOT EQUAL
.
          CALL      RDSTFEE1
GETT2600  CALL      RDKTFEE1
          BRANCH    OVRCD OF GETT4000
.
          IF        THCFLG=0
            PACK      DIM18A WITH TFCLM,TFHFUND,PTTFHTYP,TFDAYC,SP70
            MATCH     DIM18A,KEY18
            GOTO      GETT4000 IF NOT EQUAL
            MATCH     DIM5,TFENDMBS
            GOTO      GETT2600 IF LESS
          ELSE
            PACK      DIM18A WITH TFCLM,TFHFUND,PTTFHTYP,TFDAYC,TFENDMBS,SP70
            MATCH     DIM18A,KEY24
            GOTO      GETT4000 IF NOT EQUAL
          ENDIF
.
GETT2800  MOVE      PTTFCNID,KEY6         * Contract ID
          CALL      VALCONTR              * Validate the Contract ID
          BRANCH    EXIT,GETT2600
.
          MOVE      PTTFCNID,MBSCONTR     * Contract ID
          GOTO      GETT8000
.
.         Johns Hopkins theatre fees
.
GETT3000  OPEN      JHSTFEA1,"jhstfeaf"
          UNPACK    KEY18,STFCLM,STFHFUND,SPTTFHTY,STFDAYC
          MOVE      STFDAYC,KEY1
.
          MOVE      ZERO,HFBAND
          PACK      KEY5,ANSC,ANSL,STFCLM
          CALL      RDCODE1                 * Read a codes file record
          IF        OVRCD<>1
            MOVE      TCFORM6,HFBAND
          ENDIF
.
          PACK      KEY14,STFHFUND,ZERO8
          CALL      RDFUND1                 * Read a health fund file record
          MOVE      HFBAND,THCFLG
.
          MOVE      IAMT,DIM5
          LOAD      DIM5,THCFLG,IBAND1,IBAND2,IBAND3,IBAND4,IBAND5:
                               IBAND6,IBAND7,IBAND8,IBAND9,IBAND10:
                               IBAND11,IBAND12,IBAND13,IBAND14,IBAND15:
                               IBAND16
.
. ----- Check if default theatre fee on file -----
.
          PACK      KEY23,KEY18,DIM5,SP5
          CALL      RDJHTFE1                * Read a theatre fee file record
          COMPARE   ZERO,OVRCD
          GOTO      GETT8000 IF EQUAL       * Record found
.
          COMPARE   ZERO,THCFLG
          GOTO      GETT4000 IF NOT EQUAL   * Validate theatre fee banding no
.
          CALL      RSJHTFE1                * Position on & read a theatre fee
GETT3200  CALL      RKJHTFE1                  file record
          BRANCH    OVRCD,GETT4000
.
          PACK      DIM18A,TFCLM,TFHFUND,TFHFTAB,TFDAYC
          MATCH     DIM18A,KEY18
          GOTO      GETT4000 IF NOT EQUAL   * Different theatre fee combination
.
          MATCH     DIM5,TFENDMBS
          GOTO      GETT3200 IF LESS        * Last MBS fee < counter
          GOTO      GETT8000
.
.         Use default Theatre fees
.
GETT4000  ADD       ONE,F2
          BRANCH    F2,GETT7000,GETT7200
.
          CMATCH    "D",KEY1
          GOTO      GETT9000 IF NOT EQUAL
.
.        Try again, without daycase indicator
.
          MOVE      ZERO,F2
          MOVE      SP1,KEY1
          UNPACK    DIM18,STFCLM,STFHFUND,STFHFTAB
.
          MOVE      SP70,PTHFBCAT             * HF Table type
          PACK      KEY14,STFHFUND,STFHFTAB
          CALL      RDFUND1
          MOVE      PTHFBCAT,SPTTFHTY
.
          PACK      KEY13,STFCLM,STFHFUND,SPTTFHTY,KEY1,SP70
          PACK      KEY18,STFCLM,STFHFUND,STFHFTAB,KEY1,SP70
          COMPARE   NINE,CFEETYP
          GOTO      GETT3000 IF EQUAL        * Johns Hopkins
          GOTO      GETT1000
.
GETT7000  PACK      KEY13,STFCLM,SP6,SP3,KEY1,SP70
          PACK      KEY18,STFCLM,SP6,SP8,KEY1,SP70
          COMPARE   NINE,CFEETYP
          GOTO      GETT3000 IF EQUAL        * Johns Hopkins
          GOTO      GETT1000
.
GETT7200  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY;*193,PTCNDCLM
          CALL      TCLM0000        * Check Hospital claim code charging
          PACK      KEY13,PTCNDCLM,SP6,SP3,KEY1,SP70
          PACK      KEY18,STFCLM,SP6,SP8,KEY1,SP70
          COMPARE   NINE,CFEETYP
          GOTO      GETT3000 IF EQUAL        * Johns Hopkins
          GOTO      GETT1000
.
GETT8000  MOVE      ZERO,EXIT
          GOTO      GETT9999
.
GETT9000  MOVE      ZERO,TFHF1
          MOVE      ZERO,TFHF2
          MOVE      ZERO,TFHF3
          MOVE      ZERO,TFHF4
          MOVE      ZERO,TFHF5
          MOVE      ZERO,TFHF6
          MOVE      ZERO,TFPAT1
          MOVE      ZERO,TFPAT2
          MOVE      ZERO,TFPAT3
          MOVE      ZERO,TFPAT4
          MOVE      ZERO,TFPAT5
          MOVE      ZERO,TFPAT6
          MOVE      ONE,EXIT
GETT9999  RETURN
+
.*******************************************************************************
.         Get the default charging claim code for multi hospital
.*******************************************************************************
TCLM0000  IF        IBCNMHOS=1
            OPEN      PATHSPA1,"pathspaf"
            MOVE      PMVXMHOS,KEY3
            CALL      RDPTHSP1
            IF        OVRCD=0
              MOVE      PTHSDCLM,PTCNDCLM     * default claim code charging
            ENDIF
          ENDIF
          MATCH     SP70,PTCNDCLM
          IF        @EQUAL
            MOVE      "0  ",PTCNDCLM          * Use zero claim code
          ENDIF
TCLM9999  RETURN
+
.*******************************************************************************
+
