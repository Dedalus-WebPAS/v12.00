. *----------------------------------------------------------------------
. * Include Name  :   ALLATSTM.PBL
. *
. * Function      :   CGI variables for ALLATSFD - allatsaf.dat
. *
. * Includes Needed :
. *
. *----------------------------------------------------------------------
.  Modifications Done :
.
. V11.02.01 01/08/2022  Bella Turco    TSK 0921957
.           Replaced default 10 records (NORECORD) with system parameter 
.           PTCNNRTD & called NOMORE00 to display 'No more records' message
.  V9.09.01 06/08/2007  Davin         CAR 135538
.           Created include
.------------------------------------------------------------------------
.   Clear parameters for Assessment Type Setup
.------------------------------------------------------------------------
CPALAT00  MOVE      Z70,ALATS001
          MOVE      Z70,ALATS002
          MOVE      Z70,ALATS003
          MOVE      Z70,ALATS004
          MOVE      Z70,ALATS005
          MOVE      Z70,ALATS006
          MOVE      Z70,ALATS007
          MOVE      Z70,ALATS008
CPALAT99  RETURN
.------------------------------------------------------------
. Display Assessment Type Setup
.------------------------------------------------------------
ALAT0000  BRANCH    FLDITMNO,ALAT0010,ALAT0020,ALAT0030,ALAT0040,ALAT0050:
                             ALAT0060,ALAT0070,ALAT0080,ALAT0090,ALAT0100:
                             ALAT0110
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      ALAT9999
.
ALAT0010  WRITE     HTMLFILE;ALATTYPE;
          GOTO      ALAT9999
.
ALAT0020  WRITE     HTMLFILE;ALATDESC;
          GOTO      ALAT9999
.
ALAT0030  WRITE     HTMLFILE;ALATACTV;
          GOTO      ALAT9999
.
ALAT0040  WRITE     HTMLFILE;ALATDEPT;
          GOTO      ALAT9999
.
ALAT0050  MATCH     ALATCDTE,SP70
          GOTO      ALAT9999 IF EQUAL
          UNPACK    ALATCDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      ALAT9999
.
ALAT0060  MATCH     ALATFDTE,SP70
          GOTO      ALAT9999 IF EQUAL
          UNPACK    ALATFDTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2  
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      ALAT9999 
.         
ALAT0070  WRITE     HTMLFILE;ALATREMD;
          GOTO      ALAT9999
.
ALAT0080  WRITE     HTMLFILE;ALATNAME;
          GOTO      ALAT9999
.
ALAT0090  CALL      ASTAB000                * Table of Assessment Types
          GOTO      ALAT9999
.
ALAT0100  CALL      NEXTAS00                * Next Assessment Types
          GOTO      ALAT9999
.
ALAT0110  CALL      PREVAS00                * Previous Assessment Types
          GOTO      ALAT9999
.
ALAT9999  RETURN    
.         
.------------------------------------------------------------
. Table of Assessment Types
.------------------------------------------------------------
ASTAB000  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          IF        NORECORD=0              
            MOVE      PTCNNRTD,NORECORD      * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      KEY13,DEPTCODE,STARTKEY,SP70
          CALL      RSALATS1
          CALL      RPALATS1
.
ASTAB100  CALL      RKALATS1
          BRANCH    OVRCD,ASTAB900
.
          MATCH     DEPTCODE,ALATDEPT
          GOTO      ASTAB900 IF NOT EQUAL
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      ASTAB100
          ENDIF
.
          CALL      ASROW000                * Display Row
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      ASTAB100 IF NOT EQUAL
          GOTO      ASTAB999
.
ASTAB900  CALL      NOMORE00    * Display No More Records Message
.
ASTAB999  RETURN
.------------------------------------------------------------
. Assessment Types (TABLE ROW)
.------------------------------------------------------------
ASROW000  MATCH     "1",ALATACTV
          IF        @EQUAL
            MOVE    "Yes     ",ACTIDESC
          ELSE
            MOVE    "No      ",ACTIDESC
          ENDIF
.
          MATCH     SP70,ALATCDTE
          IF        !@EQUAL
            UNPACK    ALATCDTE,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDAT1,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ELSE
            MOVE      SP70,DISPDAT1
          ENDIF
.
          MATCH     SP70,ALATFDTE
          IF        !@EQUAL
            UNPACK    ALATFDTE,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      DISPDAT2,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ELSE
            MOVE      SP70,DISPDAT2
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td nowrap><A HREF='javascript:ShowDetail":
                             "(#"",LINKPRG,"#.pbl":
                             "?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&deptcode=",DEPTCODE:
                             "&alats001=",ALATTYPE,"#")'>":
                             "<img src=../images/MaintenanceIcon.gif hspace=4":
                             " border=0 align=absmiddle></a>":
                             ALATTYPE,"</td>":
                             "<td>",ALATDESC,"</td>":
                             "<td align=center>",ACTIDESC,"</td>":
                             "<td align=center>",DISPDAT1,"&nbsp;</td>":
                             "<td align=center>",DISPDAT2,"&nbsp;</td>":
                             "<td align=center>",ALATREMD,"&nbsp;</td>":
                             "<td align=center>",ALATNAME,"&nbsp;</td>":
                             "<td align=center>",ALATCUID,"&nbsp;</td>":
                             "<td align=center>",ALATUUID,"&nbsp;</td>":
                             "</tr>"
.
ASROW999 RETURN
.
.-----------------------------------------------------------
. Link to Next Group
.-----------------------------------------------------------
NEXTAS00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,D3,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",DEPTCODE
          REP       " +",ALATTYPE
          REP       " +",KEYWORDS
.
          WRITE     HTMLFILE;"allweb01.pbl":
                                 "?reportno=",F2:
                                 "&template=",D3:
                                 "&startnum=",KEY3:
                                 "&norecord=",KEY2:
                                 "&deptcode=",DEPTCODE:
                                 "&keywords=",KEYWORDS:
                                 "&alats001=",ALATTYPE;
.
          REP       "+ ",DEPTCODE
          REP       "+ ",ALATTYPE
          REP       "+ ",KEYWORDS
.
NEXTAS99  RETURN
.
.-------------------------------------------------------------
. Link to Prev Group
.-------------------------------------------------------------
PREVAS00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          REP       " +",ALATTYPE
          REP       " +",DEPTCODE
          REP       " +",KEYWORDS
.
          WRITE     HTMLFILE;"allweb01.pbl":
                                "?reportno=",F2:
                                "&template=",FLDPARA:
                                "&startnum=",KEY3:
                                "&norecord=",KEY2:
                                "&deptcode=",DEPTCODE:
                                "&keywords=",KEYWORDS:
                                "&alats001=",ALATTYPE;
.
          REP       "+ ",ALATTYPE
          REP       "+ ",DEPTCODE
          REP       "+ ",KEYWORDS
.
PREVAS99  RETURN
+
.------------------------------------------------------------
.   Set Parameters for Assessment Type Setup
.------------------------------------------------------------
SPALAT00  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SPALAT01,SPALAT02,SPALAT03,SPALAT04,SPALAT05:
                       SPALAT06,SPALAT07,SPALAT08
.
          MOVE      ONE,EXIT
          GOTO      SPALAT99
.
SPALAT01  SQUEEZE   QRYDATA
          MOVE      QRYDATA,ALATS001
          PACK      ALATS001,ALATS001,SP70
          GOTO      SPALAT99
.
SPALAT02  MOVE      QRYDATA,ALATS002
          GOTO      SPALAT99
.
SPALAT03  MOVE      QRYDATA,ALATS003
          GOTO      SPALAT99
.
SPALAT04  SQUEEZE   QRYDATA
          MOVE      QRYDATA,ALATS004
          PACK      ALATS004,ALATS004,SP70
          GOTO      SPALAT99
.
SPALAT05  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      ALATS005,CCENT,CYEAR,CMON,CDAY
          GOTO      SPALAT99
.
SPALAT06  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      ALATS006,CCENT,CYEAR,CMON,CDAY
          GOTO      SPALAT99
.
SPALAT07  MOVE      QRYDATA,ALATS007
          GOTO      SPALAT99
.
SPALAT08  MOVE      QRYDATA,ALATS008
          GOTO      SPALAT99
.
SPALAT99  RETURN
.
.------------------------------------------------------------
.  Update Assessment Type Setup
.------------------------------------------------------------
MVALAT00  MATCH     ALATS001,Z70
          IF        !@EQUAL
            MOVE      ALATS001,ALATTYPE
          ENDIF
.         
          MATCH     ALATS002,Z70
          IF        !@EQUAL
            MOVE      ALATS002,ALATDESC
          ENDIF
.         
          MATCH     ALATS003,Z70
          IF        !@EQUAL
            MOVE      "1",ALATACTV
          ELSE
            MOVE      "0",ALATACTV
          ENDIF
.         
          MATCH     ALATS004,Z70
          IF        !@EQUAL
            MOVE      ALATS004,ALATDEPT
          ENDIF
.         
          MATCH     ALATS005,Z70
          IF        !@EQUAL
            MOVE      ALATS005,ALATCDTE
          ENDIF
.         
          MATCH     ALATS006,Z70
          IF        !@EQUAL
            MOVE      ALATS006,ALATFDTE
          ENDIF
.         
          MATCH     ALATS007,Z70
          IF        !@EQUAL
            MOVE      ALATS007,ALATREMD
          ENDIF
.         
          MATCH     ALATS008,Z70
          IF        !@EQUAL
            MOVE      ALATS008,ALATNAME
          ENDIF
.         
MVALAT99  RETURN
.         
.------------------------------------------------------------
.  Clear Assessment Type Setup
.------------------------------------------------------------
CLALAT00  MOVE      SP70,ALATTYPE
          MOVE      SP70,ALATDESC
          MOVE      SP70,ALATACTV
          MOVE      SP70,ALATDEPT
          MOVE      SP70,ALATCDTE
          MOVE      SP70,ALATFDTE
          MOVE      SP70,ALATREMD
          MOVE      SP70,ALATNAME
.
CLALAT99  RETURN
