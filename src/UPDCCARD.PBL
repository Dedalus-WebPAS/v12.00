. *****************************************************************************
. *                                                                           *
. * Routine Name  : UPDCCARD.PBL                                              *
. *                                                                           *
. * Function      : If the pmi community card details have been changed then  *
. *                 Loop over each of the databases Future O/P Bookings and   *
. *                 Preadmissions for the UR and update the visit card details*
. *                                                                           *
. * Author        : Paul Howells       13/08/1996                             *
. *                                                                           *
. * Parameters    : Patmastf/Patma1af Record                                  *
. *                 Patmsxaf/Patmx1af Record                                  *
. *                 Patadmaf/Patam1af Record                                  *
. *                                                                           *
. * Modifications :                                                           *
. *                 20/06/2017  Ania P                CAR 261630              *
. *                             Removed use of PORT.                          *
. *                 16/02/2000  Katie Nelson                                  *
. *                             Added flag on display statements so they don't*
. *                             execute through WEB.                          *
. *****************************************************************************
.                   08/10/1998  Glenn Berry      5.07 changes                 *
. *****************************************************************************
UPCOMCRD  MATCH     SPCARD,PTMXCARD
          GOTO      UPCOM50 IF NOT EQUAL
.
          MATCH     SPCEXP,PTMXCEXP
          GOTO      UPCOM50 IF NOT EQUAL
.
          MATCH     SPCXMP,PTMXCXMP
          GOTO      UPCOM50 IF NOT EQUAL
.
          MATCH     SPFMLY,PTMXFMLY
          GOTO      UPCOM99 IF EQUAL
.
UPCOM50   PROC      UPDUCARD
.
UPCOM99   RETURN
. *                                                                           *
. *****************************************************************************
. * Routine Name  : UPDUCARD.PBL                                              *
. *                                                                           *
. * Function      : Loop over each of the databases Future O/P Bookings and   *
. *                 Preadmissions for the UR and update the visit card details*
. *                                                                           *
. * Author        : Paul Howells       13/08/1996                             *
. *                                                                           *
. * Parameters    : Patmastf/Patma1af Record                                  *
. *                 Patmsxaf/Patmx1af Record                                  *
. *                                                                           *
. * External Rout : PATDPATH - extract the DPATH for each database            *
. *                 OTRF0000 - this routine will open the required file       *
. *                                                                           *
. * Modifications :                                                           *
. *****************************************************************************
.
          DEFPROC   UPDUCARD
.
          INC       PATDPTHV/INC            * variables for patdpath
          INC       PATHOSFD/INC            * hospital file
          INC       OUTSITFD/INC            * Outpatient Site File
          INC       OUTBOAFD/INC
          INC       OUTBB1FD/INC
          INC       PATVKCFD/INC
          INC       PATKWCFD/INC
          INC       PATMI1FD/INC
          INC       PATVISFD/INC
          INC       IBASEQFD/INC 
          INC       TFILEVAR/INC
          INC       WEBERRFD/INC
.
MRCNNOHS  FORM      2
PTCNUCRD  FORM      1
PTCNVKCA  FORM      1
PTCNBCUT  DIM       4
.
HOSPCNT   FORM      2
XXDPATH   DIM       120
CMDLINE   DIM       120
CLAIMCOD  DIM       3
ORIGVISI  FORM      4
ORIGDAYS  FORM      4
CFNAME    DIM       8
KWCFIL    INIT      "patkwc"
.
.-------- Temp File Definition ------------------
.
ISBUILD   INIT      "isbuild "
ISERASE   INIT      "iserase "
CFNAMEO   DIM       8
TEMPO     IFILE     SQL, FIXED=4, KEY="U1-3"
PRFIXO    INIT      "CCARDO"
KEYO      INIT      " 4 U1-3"
.
.*****************************************************************************
.
UCARD000  OPEN      CONTROLF,"controlf" 
          READ      CONTROLF,SIXTY;*129,MRCNNOHS  * number of data directories
          READ      CONTROLF,TEN;*25,PTCNVKCA
          READ      CONTROLF,TWENTY;*196,PTCNUCRD,*199,PTCNBCUT
.
          CALL      TFILENAM
          MOVE      TFILNAME,CFNAMEO 
.
          COMPARE   ONE,PTCNUCRD
          GOTO      UCARD999 IF NOT EQUAL   * not using card information
.
          CALL      PATDPATH                * extract the dpaths
.
          MOVE      ZERO,HOSPCNT            * clear the counter
          COMPARE   ZERO,MRCNNOHS
          GOTO      UCARD080 IF EQUAL       * not using data directories
.
.         loop over each data directory
.
UCARD050  ADD       ONE,HOSPCNT             * increment hospital counter
          COMPARE   HOSPCNT,MRCNNOHS
          GOTO      UCARD999 IF LESS        * finished number of hospitals
.
UCARD080  PACK      XXDPATH,SP20,SP20,SP20
          LOAD      XXDPATH,HOSPCNT,HDPATH1,HDPATH2,HDPATH3,HDPATH4,HDPATH5
          IF        HOSPCNT <> ZERO
            MATCH     SP30,XXDPATH
            GOTO      UCARD050 IF EQUAL       * no DPATH for hospital
          ENDIF
.
          MOVE      "patvkcaf",OPFILE
          MOVE      TWENTY2,FILENUM
          CALL      OMDFN000                * open visit card file
          BRANCH    EXIT,UCARD100           * no files
.
. ------- update the Community Card details for future O/P bookings ----------- 
.
          CALL      GFOB0000                * update future O/P bookings
          CALL      GFPR0000                * update future Preadmissions
.
UCARD100  COMPARE   ZERO,MRCNNOHS
          GOTO      UCARD050 IF NOT EQUAL   * using many directories
.
UCARD999  GOTO      ENDUCARD
.
+
.*********************************************************************
.         get the future OP bookings
.*********************************************************************
GFOB0000  CALL      CTMPO000                * create temp file 
          OPEN      OUTSITA1,"outsitaf"
.
          MOVE      SP6,KEY6
          CALL      RDSSITA1
.
GFOB1000  CALL      RDKSITA1
          BRANCH    OVRCD,GFOB2000          * end if file
.
          MOVE      OSTFILE,KEY3
          READ      TEMPO,KEY3;ANS
          GOTO      GFOB1000 IF NOT OVER    * already on temp file
.
          WRITE     TEMPO,KEY3;OSTFILE
          GOTO      GFOB1000
.
.         loop over temp file to look at all bookings
.
GFOB2000  MOVE      SP3,KEY3
          READ      TEMPO,KEY3;;
.
GFOB2100  READKS    TEMPO;OSTFILE
          GOTO      GFOB9500 IF OVER        * end of file
.
. ------- open outbokaf -----------------------------------
.
          PACK      OPFILE,OSTFILE,FILBOKA3
          MOVE      SEVEN,FILENUM
          CALL      OMDFN000                * open bookings file
          BRANCH    EXIT,GFOB2100
.
. ------- open outbb1af/outbb1af --------------------------
.
          PACK      OPFILE,OSTFILE,FILBB1A1
          MOVE      EIGHT,FILENUM
          CALL      OMDFN000
          BRANCH    EXIT,GFOB2100           * no files
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY36,PURNO,KEY8,SP30,SP30
          CALL      RDSBOKA3
.
. loop over the bookings file for the patients UR number
.
GFOB3000  CALL      RDKBOKA3
          BRANCH    OVRCD,GFOB2100          * no more data
.
          MATCH     PURNO,OBAURNO
          GOTO      GFOB2100 IF NOT EQUAL   * no more data
.
          COMPARE   ONE,OBASTAT
          GOTO      GFOB3000 IF NOT EQUAL   * not booked
.
          MOVE      OBAOUTNO,KEY8
          CALL      RDBOKB1                 * read bookings file
          BRANCH    OVRCD,GFOB3000
.
GFOB3500  MOVE      OBAOUTNO,KEY8
          CALL      RLOTBOB1
          BRANCH    OVRCD,GFOB3000
.
          COMPARE   ZERO,OVRCD
          GOTO      GFOB4000 IF EQUAL
.
          SCAN      "WEB",PRGID
          IF        @EQUAL
            DISPLAY   *W2,*R,"Booking Number [",OBAOUTNO,"] Locked on Port ":
                      OBBPORT
            ADD       ONE,SECTOR
            IF        SECTOR>10
              GOTO      GFOB4000
            ENDIF
          ELSE
            DISPLAY   *P1:24,*EL,*B:
                      "Booking Number [",OBAOUTNO,"] Locked on Port ":
                      *V2LON,OBBPORT,". ";
            CALL      HITENTER
          ENDIF
          GOTO      GFOB3500
.
GFOB4000  MOVE      OBACOMP,CLAIMCOD        * claim code
          MOVE      OBAOUTNO,PTVKBILL
          CALL      UPCC0000                * update patkvcaf
.
          UNPACK    OBADATE,XDATE
          CALL      DFKW0000                * get default kiwi visits number
          MOVE      ORIGVISI,OTBBPVIS       * set the default
          MOVE      OBAOUTNO,KEY8
          CALL      UPBOKB1                 * update bookings file
          CALL      UUOTBOB1                * unlock bookings file
.
          GOTO      GFOB3000
.
GFOB9500  CALL      DTMPO000
.
GFOB9999  RETURN
+
.*********************************************************************
.         get the future preadmissions
.*********************************************************************
GFPR0000  MOVE      "patvisaf",OPFILE
          MOVE      "18",FILENUM
          CALL      OMDFN000
          BRANCH    EXIT,GFPR9999           * invalid file
.
          MOVE      ONE,FILENUM
          MOVE      "patmi1af",OPFILE
          CALL      OMDFN000                * open admission file
          BRANCH    EXIT,GFPR9999           * invalid file
.
. loop over the admission file for the patients UR number
.
          PACK      KEY8,CCC,CYY,CMM,CDD
          REP       " 0",KEY8
          PACK      KEY24,PURNO,KEY8,SP30
          CALL      RDSVISA2
GFPR1000  CALL      RDKVISA2
          BRANCH    OVRCD,GFPR9999
.
          MATCH     PVIURNO,PURNO                 * same U/R No.?
          GOTO      GFPR9999 IF NOT EQUAL
.
          COMPARE   "3",PVITYPE
          GOTO      GFPR1000 IF NOT EQUAL
.
          PACK      KEY8,PVIBILL,SP10
          CALL      RDMISS1
          BRANCH    OVRCD,GFPR1000          * no more data
.
          COMPARE   ONE,ASTAT
          GOTO      GFPR1000 IF NOT EQUAL   * not preadmitted
.
GFPR1500  PACK      KEY8,PVIBILL,SP10
          CALL      RLPTMIS1
          BRANCH    OVRCD,GFPR1000
.
          COMPARE   ZERO,OVRCD
          GOTO      GFPR2000 IF EQUAL
.
          SCAN      "WEB",PRGID
          IF        @EQUAL
            DISPLAY   *W2,*R,"Admission Number [",PVIBILL,"] Locked on Port ":
                      APORT
            ADD       ONE,SECTOR
            IF        SECTOR>10
              GOTO      GFPR2000
            ENDIF
          ELSE
            DISPLAY   *P1:24,*EL,*B:
                      "Admission Number [",PVIBILL,"] Locked on Port ":
                      *V2LON,APORT,". ";
            CALL      HITENTER
          ENDIF
          GOTO      GFPR1500
.
GFPR2000  MOVE      ACLAIM,CLAIMCOD         * claim code
          MOVE      AADMNO,PTVKBILL
          CALL      UPCC0000                * update patkvcaf
.
          MOVE      ADATE,XDATE
          CALL      DFKW0000                * get default kiwi visits number
          MOVE      PTKWVISI,PTMIPVIS       * no. of previous visits
          MOVE      PTKWDAYS,ADYHOSP        * no. of days charged
          CALL      UPMISS1                 * update bookings file
          CALL      UUPTMIS1
.
          GOTO      GFPR1000
.
GFPR9999  RETURN
.*********************************************************************
.*                  CTMPO000                    Called by : xxxx0000 *
.*        Create a temp file                                         *
.*********************************************************************
CTMPO000  CALL      DTMPO000                * delete temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISBUILD,CFNAMEO,KEYO,SP30      * set command
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * build the temp file
          OPEN      TEMPO,CFNAMEO           * open the temp file
.
CTMPO999  RETURN
+
.*********************************************************************
.*                  DTMPO000                    Called by : CTPA0000 *
.*        Delete temp file O                                         *
.*********************************************************************
DTMPO000  PACK      CFNAMEO,PRFIXO,PORT     * set the port dependant file name
          REP       " 0",CFNAMEO
.
          CLOSE     TEMPO                   * close temp file
          CLEAR     CMDLINE                 * clear command line
          PACK      CMDLINE,ISERASE,CFNAMEO * set command line for erase
          RESET     CMDLINE                 * reset FP
          EXECUTE   CMDLINE,TASKID          * delete the temp file
.
DTMPO999  RETURN
.*********************************************************************
.*                  UPCC0000                    Called by : CTPA0000 *
.*        update the community card details                          *
.*        Parameters : PTVKBILL                                      *
.*                     CLAIMCOD  Claim Code                          *
.*********************************************************************
UPCC0000  MOVE      PTVKBILL,KEY8           * key
          CALL      RDPTVKC1
          IF        OVRCD = ZERO
            MOVE      "2",FORM1             * before change audit
            CALL      AVKC0000              * write audit record
            MOVE      PTMXCARD,PTVKCARD     * card number
            MOVE      PTMXCEXP,PTVKCEXP     * card expiry date
            MOVE      PTMXCXMP,PTVKCXMP     * exemption code
            MOVE      PTMXFMLY,PTVKFMLY     * family id
            CALL      UPPTVKC1              * update record
            MOVE      THREE,FORM1           * after change audit
            CALL      AVKC0000              * write audit record
          ELSE
            MOVE      PTMXCARD,PTVKCARD     * card number
            MOVE      PTMXCEXP,PTVKCEXP     * card expiry date
            MOVE      PTMXCXMP,PTVKCXMP     * exemption code
            MOVE      PTMXFMLY,PTVKFMLY     * family id
            MOVE      CLAIMCOD,PTVKCOMP     * claim code
            CALL      WRPTVKC1              * write record
            MOVE      ONE,FORM1             * add audit
            CALL      AVKC0000              * write audit record
          ENDIF
.
UPDC9999  RETURN
+
.*********************************************************************
.*                  AVKC0000                    
.*        Write audit record for PATVKCFD      
.*********************************************************************
AVKC0000  BRANCH    PTCNVKCA,AVKC9999       * audits not on
          OPEN      PATAUDVK,"pataudvk"
          COMPARE   FIVE,FORM1
          GOTO      AVKC5000 IF EQUAL       * delete before change audit
.
          COMPARE   THREE,FORM1
          GOTO      AVKC1000 IF EQUAL       * after change audit
.
          PACK      PTVKAUDD,CCC,CYY,CMM,CDD
          REP       " 0",PTVKAUDD
          MOVE      CTIMEIS,PTVKAUDT
.
AVKC1000  MOVE      PORT,PTVKAUDP
          MOVE      FORM1,PTVKAUDR
          REP       "1A2B3C4D",PTVKAUDR
          MOVE      ONE,PTVKAUDS
          MOVE      PASSCODE,PTVKAUDO
.
          PACK      KEY19,PTVKAUDD,PTVKAUDT,PTVKAUDP,PTVKAUDR
          CALL      AWPTVKC
          CLOSE     PATAUDVK
          GOTO      AVKC9999
.
.         delete before change audit
.
AVKC5000  MOVE      ANSB,PTVKAUDR
          PACK      KEY19,PTVKAUDD,PTVKAUDT,PTVKAUDP,PTVKAUDR
          CALL      ADPTVKC
          CLOSE     PATAUDVK
.
AVKC9999  RETURN
.
.*********************************************************************
.*                  DFKW0000                    Called by : ADDS2400 *
.*        Get the # of visits on the kiwi file - ORIGVISI            *
.*        Get the # of days on the kiwi file - ORIGDAYS              *
.*        Parameters : XDATE -      - booking or preadmission date   *
.*********************************************************************
DFKW0000  MOVE      ZERO,ORIGVISI           * clear number of visits
          MOVE      ZERO,ORIGDAYS           * clear number of days
          MOVE      ZERO,PTKWDAYS           * clear amount incase read fails
          MOVE      ZERO,PTKWVISI           * clear amount incase read fails
.
          COMPARE   ONE,PTCNUCRD
          GOTO      DFKW9999 IF NOT EQUAL   * not using card details
.
          CALL      OPFI0000                * open correct file
          BRANCH    EXIT,DFKW9999           * file not there
.
          MATCH     SP20,PTVKCARD           * do we have a card number ?
          GOTO      DFKW0500 IF EQUAL       * no, special case
.
          CMATCH    ANSC,PTVKCARD           * valid card ?
          GOTO      DFKW0700 IF NOT EQUAL   * no.
.
          MOVE      PTVKCARD,PTKWCARD       * card number
          MOVE      SP8,PTKWURNO            * no U/R Number
          GOTO      DFKW0600
.
DFKW0500  MATCH     ZEROUR,PURNO            * do we have a U/R Number ?
          GOTO      DFKW0700 IF EQUAL       * no, don't do anything
.
          MOVE      SP20,PTKWCARD           * no card number
          MOVE      PURNO,PTKWURNO          * U/R Number
.
DFKW0600  PACK      KEY28,PTKWCARD,PTKWURNO
          CALL      RDPTKWC1                * read kiwicard file
          MOVE      PTKWVISI,ORIGVISI       * original number of visits
          MOVE      PTKWDAYS,ORIGDAYS       * original number of visits
.
DFKW0700  CLOSE     PATKWCA1
.
DFKW9999  RETURN
.
. *****************************************************************************
. * OPFI0000 : Open the appropriate kiwicard file                             *
. *****************************************************************************
OPFI0000  MATCH     SP8,XDATE                * check if we have a date
          IF        @EQUAL
            PACK      XDATE,CCC,CYY,CMM,CDD
          ENDIF
.
          UNPACK    XDATE,CCENT,CYEAR,KEY4
          MATCH     PTCNBCUT,KEY4
          IF        !@LESS
            MOVE      CYEAR,FORM2            * year of kiwicard
          ELSE
            MOVE      CYEAR,FORM2
            SUB       ONE,FORM2              * calculate start of kiwicard year
            IF        FORM2 < 0
              MOVE     "99",FORM2            * allow for the year 2000
            ENDIF
          ENDIF
.
          PACK      CFNAME,KWCFIL,FORM2      * name of kiwicard file
          REP       " 0",CFNAME
.
          MOVE      ZERO,OVRCD               * assume file exists
          TRAP      OVERCOND IF IO            * trap for error openning file
          OPEN      PATKWCA1,CFNAME           * open kiwicard file
          TRAPCLR   IO
          BRANCH    OVRCD,OPFI9000            * open failed
.
          MOVE      ZERO,EXIT
          GOTO      OPFI9999
.
OPFI9000  MOVE      ONE,EXIT
.
OPFI9999  RETURN
+
.*********************************************************************
.         open the required file
.*********************************************************************
OTRF0000  IF        FILENUM = 1
            CLOSE     PATMI1A1
            OPEN      PATMI1A1,PATH
          ENDIF
.
          IF        FILENUM = 6
            CLOSE     OUTBOKA1
            OPEN      OUTBOKA1,PATH
          ENDIF
          IF        FILENUM = 7
            CLOSE     OUTBOKA3
            OPEN      OUTBOKA3,PATH
          ENDIF
.
          IF        FILENUM = 8
            CLOSE     OUTBB1A1
            OPEN      OUTBB1A1,PATH
          ENDIF
.
          IF        FILENUM = 18
            CLOSE     PATVISA2
            OPEN      PATVISA2,PATH
          ENDIF
.
          IF        FILENUM = 20
            CLOSE     PATVISA1
            OPEN      PATVISA1,PATH
          ENDIF
.
          IF        FILENUM = 22
            CLOSE     PATVKCA1
            OPEN      PATVKCA1,PATH
          ENDIF
.
OTRF9999  RETURN
.
          INC       PATDPATH
          INC       IBAOVRIO/INC
          INC       PATHOSIO/INC            * hospital file
          INC       OUTSITIO/INC            * Outpatient Site File
          INC       OUTBOAIO/INC
          INC       OUTBB1IO/INC
          INC       PATVKCIO/INC
          INC       PATKWCIO/INC
          INC       PATMI1IO/INC
          INC       PATVISIO/INC
          INC       TFILENAM 
          INC       IBASEQIO/INC 
          INC       WEBERRIO/INC
.
ENDUCARD  ENDPROC
