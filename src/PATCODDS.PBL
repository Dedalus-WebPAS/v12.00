.
.     PATCODDS : Display Codes File Entries
.
.     This include displays entries in the codes file for a given
.      category.
.                   Input variable : CODE  contains category
.
.     Modifications: 14/10/89 Graeme Williams
.                    Added the parameter CNEWDS to determine if the include
.                    is to use the new standards for "?" routines
.                    02/12/89 Graeme Williams
.                    Changed the include to use the second index of the codes
.                    file to get the screen in description order.
.                    22/03/1994  Steve Armstrong
.                    Changed routine from PATCODDS to PATCODDS
.                    06/06/1994  Rob Leonard
.                    Fix display if CNEWDS=1
.                    28/09/1994  Rob Leonard
.                    Dont display inactive codes
.
PATCODDS  OPEN      PATCODE2,"patcodes"
          PACK      KEY5,CODE,SP5
          CALL      RDCODE1
          BRANCH    OVRCD OF DISPBCD
.
.         Display Category Heading, and then loop over entries
.
          DISPLAY   *P1:3,*EF,*P20:4,*V2LON,*ULON,CODE,": ",TDESC
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
.
.         TRAP      ENDLP0 IF F1
.
          PACK      PTCDKEY2,CODE,SP30
          CALL      RDSCODE2
DISPLP1   CALL      RDKCODE2
          BRANCH    OVRCD OF ENDLP1
.
          MATCH     TCODE,CODE
          GOTO      ENDLP1 IF NOT EQUAL
.
          MATCH     SP3,ACODE
          GOTO      DISPLP1 IF EQUAL
.
          MATCH     PTCOACTV,ANSI              * inactive ?
          GOTO      DISPLP1 IF EQUAL
.
          DISPLAY   *PCCOL:CVERT,*V2LON,ACODE,*HOFF,SP2,TDESC;
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      DISPLP1 IF LESS
.
          MOVE      FIVE,CVERT
          ADD       TWENTY6,CCOL
          COMPARE   "70",CCOL
          GOTO      DISPLP1 IF LESS
.
.         TRAPCLR   F1
.
.         Check for new standards question, or old question
.
DISPLP2  BRANCH    CNEWDS,DSCOD200
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       "eEfFnN",ANS
          CMATCH    ANSE,ANS
          GOTO      ENDLP2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATCODDS IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      DSCOD400 IF EQUAL
.
          BEEP
          GOTO      DISPLP2
.
.         Use the new standards question
.
DSCOD200  KEYIN     *P1:24,*EL," (",*V2LON,"C",*HOFF,")ontinue, (":
                    *V2LON,"F",*HOFF,")irst Screen, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          REP       "cCfFnN",ANS
          CMATCH    ANSC,ANS
          GOTO      ENDLP2 IF EQUAL
          CMATCH    ANSF,ANS
          GOTO      PATCODDS IF EQUAL
          CMATCH    ANSN,ANS
          GOTO      DSCOD400 IF EQUAL
.
          BEEP
          GOTO      DSCOD200
.
.         Clear the screen
.
DSCOD400  MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
.
.         TRAP      ENDLP0 IF F1
.
          GOTO      DISPLP1
.
.       IF MISSING CATEGORY SHOW  CODE MISSING
.
DISPBCD   DISPLAY   *P1:3,*EF,*P30:4,*V2LON,*BLINKON:
                    "Category : ",CODE," missing in Codes Master File"
          GOTO      ENDLP1
.
ENDLP0    NORETURN
.         TRAPCLR   F1
.
.      End of codes
.
ENDLP1    BRANCH    CNEWDS,ENDLP2
.
          DISPLAY   *P1:24,*EL;
          CALL      HITENTER
.
ENDLP2    MOVE      ZERO,OVRCD
          CLOSE     PATCODE2
          RETURN
