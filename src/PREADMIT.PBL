.*****************************************************************************
.*                             PREADMIT.PBL                                  *
.*           Common Pre-admission routine used by PATWEB89 & CONVPADM        *
.*                                                                           *
.* V11.00.01  22/04/2020  Ebon Clements    TSK 0850105                       *
.*            Clear PMCUTSTA and PMCUTLOC before write to pmscuraf           *
.* V10.15.01  13/11/2019  Peter Vela       Task 0879283                      *
.*            Added reciprocal update to PMPXFLDR in CHKF8500                *
.* V10.06.00  10/06/2015  Steve Armstrong  CAR 313856                        *
.*            Created include.                                               *
.*****************************************************************************
.
.------------------------------------------------------------
. WRCPT000 - Write to the current patient list
.------------------------------------------------------------
.
WRCPT000  COMPARE   TWO,ASTAT
          GOTO      WRCPT100 IF EQUAL
.
          CALL      CALRAN00
.
          MATCH     STRTDATE,ADATE        * before start date
          GOTO      WRCPT900 IF LESS
.
          MATCH     ADATE,LASTDATE        * after end date
          GOTO      WRCPT900 IF LESS
.
WRCPT100  MOVE      AADMNO,PMCUVISN
          MOVE      ADATE,PMCUDATE
          MOVE      PTMASNAM,PMCUSURN
          MOVE      PMPXFNAM,PMCUGNAM
          MOVE      PMPXSNAM,PMCUGNM2
          MOVE      PURNO,PMCUURNO
          MOVE      "3",PMCUSYST
          MOVE      SP70,PMCULOCN
          MOVE      SP70,PMCUOSIT
          MOVE      PMVXMHOS,PMCUHOSP
          MOVE      SP70,PMCUTSTA
          MOVE      SP70,PMCUTLOC
.
          PACK      KEY8,PMCUVISN,SP70
          CALL      RAPMCUR1
          IF        OVRCD=1
            CALL      WRPMCUR1
          ELSE
            CALL      UPPMCUR1
          ENDIF
          GOTO      WRCPT999
.
WRCPT900  PACK      KEY8,AADMNO,SP70
          CALL      RAPMCUR1
          IF        OVRCD=0
            CALL      DEPMCUR1
          ENDIF
.
WRCPT999  RETURN
+
.----------------------------------------------------------------
. CALRAN00 - Calculate the date range for current patient records
.----------------------------------------------------------------
.
CALRAN00  READ      CONTROLF,HUND01;*49,CWEBSDAY  * no of days for search
          MOVE      SP70,STRTDATE
          MOVE      SP70,LASTDATE
.
. ---  calculate starting date ---
.
          CALL      IBACLOCK
          CALL      DMYCON
          SUB       CWEBSDAY,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      STRTDATE,CC,YY,MM,DD
          REP       " 0",STRTDATE
.
. ---  calculate last date ---
.
          CALL      IBACLOCK
          CALL      DMYCON
          ADD       CWEBSDAY,JULDAY
          MOVE      JULDAY,JWKDAY
          MOVE      JULYR,JWKYER
          MOVE      JULCC,JWKCC
          CALL      JULCON
          PACK      LASTDATE,CC,YY,MM,DD
          REP       " 0",LASTDATE
.
CALRAN99  RETURN
+
.*************************************************************************
. WRINT000 - Write interpreter details
.*************************************************************************
.
WRINT000  MATCH     "1",PMPXINTR
          GOTO      WRINT999 IF NOT EQUAL
.
          CALL      CLVISINT
          MOVE      AADMNO,VSINVISN
          MOVE      ADATE,VSINDATE
          MOVE      ATIME,VSINTIME
          MOVE      "3",VSINTYPE
          MOVE      SP70,VSINSUBK
          MOVE      USERID,VSINWUID
          MOVE      ZERO,VSINNOTF           * Notified Ind.            *I-40489
          MOVE      ZERO,VSINCONF
.
          PACK      KEY8,AADMNO,SP70
          CALL      RAVSINT1
          IF        OVRCD=1
            CALL      WRVSINT1
            MOVE      "A",INTAUDTY
            CALL      INTAUD00
          ELSE
            CALL      UPVSINT1
            MOVE      "C",INTAUDTY
            CALL      INTAUD00
          ENDIF
.
WRINT999  RETURN
+
.*******************************************************************************
. INTAUD00 - Update Interpreter Audit Table
.  Input - CASEKEYS  Key To Booking
.          INTAUDTY  Audit Type
.*******************************************************************************
.
INTAUD00  CALL      CLVISIAU
          PACK      VSIACDAT,CCC,CYY,CMM,CDD
          REP       " 0",VSIACDAT
          MOVE      CTIMEIS,VSIACTIM
          MOVE      INTAUDTY,VSIARTYP
.
          MOVE      VSINVISN,VSIAVISN
          MOVE      VSINDATE,VSIADATE
          MOVE      VSINTIME,VSIATIME
.....     MOVE      "3",VSIATYPE                                        D-40489
          MOVE      VSINTYPE,VSIATYPE                                  *I-40489
          MOVE      VSINSUBK,VSIASUBK
          MOVE      VSINWUID,VSIAWUID
          MOVE      PMPXLNG1,VSIALNG1
          MOVE      PMPXLNG2,VSIALNG2
          MOVE      PMPXLNG3,VSIALNG3
          MOVE      VSINNOTF,VSIANOTF
          MOVE      VSINCONF,VSIACONF
.
          PACK      KEY25,VSIACDAT,VSIACTIM,VSIARTYP,VSIAVISN,SP70
          CALL      RAVSIAU1
          IF        OVRCD=0
            CALL      UPVSIAU1
          ELSE
            CALL      WRVSIAU1
          ENDIF
          MOVE      ZERO,EXIT
.
INTAUD99  RETURN
+
.*****************************************************************************
.*                                   BRDPRE00                                *
.*                           Broadcast the pre-admission                     *
.*****************************************************************************
.
BRDPRE00  CALL      PMIGTNID                   * get national id for dgate write
          MOVE      NMPNUMB,PTNINMPI
          MOVE      ONE,HL7TRGID
          MOVE      "PREADMIT",HL7INCLD
          PROC      DGCLICPE                    * broadcast change Pre-admission
.
          RETURN
+
.-----------------------------------------------------------------------------
. CHKF0000 - Check to see if this is a family choice care type. If it is update
.            the pmi folder to family choice if not TAC,WORK,VET
.            or OVERSEAS claim code.
.            If it isn't family choice care type then chekc if the claim code is
.            OVERSEAS/INELIGIBLE. If it is then update the pmi folder
.-----------------------------------------------------------------------------
.
CHKF0000  PACK      KEY5,ANSC,ANSC,ACARE,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CHKF8500
.
          MATCH     "3",THCSCOD                * Not family choice
          GOTO      CHKF8500 IF NOT EQUAL
.
          PACK      KEY5,ANSC,ANSL,ACLAIM,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CHKF9999
.
          PACK      KEY5,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
.
          SCAN      ANSV,KEY5
          IF        @EQUAL
            GOTO      CHKF9999                 * Skip VET
          ENDIF
.
          SCAN      ANSM,KEY5
          IF        @EQUAL
            GOTO      CHKF9999                 * Skip TAC
          ENDIF
.
          SCAN      ANSW,KEY5
          IF        @EQUAL
            GOTO      CHKF9999                 * Skip work comp
          ENDIF
.
          MATCH     "1",TCINDC1
          IF        @EQUAL
            MATCH     "1",TCINDC19
            IF        !@EQUAL
              GOTO      CHKF8550                 * Overseas/Ineligible
            ENDIF
          ENDIF
.
          MATCH     "11",PTCDNHCP
          IF        @EQUAL
            GOTO      CHKF8700
          ENDIF
.
CHKF8000  MOVE      FOUR,FOLDERTY              * Set default to family choice
          CALL      UPFOLD00                   * Update PMI folder details
          IF        UPDFOLD=0
            GOTO      CHKF9999
          ENDIF
.
          GOTO      CHKF9000
.
CHKF8500  PACK      KEY5,ANSC,ANSL,ACLAIM,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,CHKF9999
.
          PACK      KEY5,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
.
          MATCH     "1",TCINDC1
          IF        @EQUAL
            MATCH     "1",TCINDC19
            IF        !@EQUAL
CHKF8550      MOVE      SIX,FOLDERTY
              CALL      UPFOLD00
              IF        UPDFOLD=0
                GOTO      CHKF9999
              ENDIF
            ELSE
              GOTO      CHKF9999
            ENDIF
          ELSE
            MATCH     "11",PTCDNHCP
            IF        @EQUAL
CHKF8700      MOVE      EIGHT,FOLDERTY
              CALL      UPFOLD00
              IF        UPDFOLD=0
                GOTO      CHKF9999
              ENDIF
            ELSE
              GOTO      CHKF9999
            ENDIF
          ENDIF
.
CHKF9000  MATCH     "PATWEB89",PRGID
          IF        @EQUAL
            CLEAR     WEBTITLE
            APPEND    "Patient folder conflict - ",WEBTITLE
            APPEND    "Patient folder details will be updated.",WEBTITLE
            RESET     WEBTITLE
            ADD       ONE,WARCOUNT
            MOVE      WEBTITLE,WEBWARNG[WARCOUNT]
          ENDIF
.
CHKF9999  RETURN
+
.--------------------------------------------------------------------------
. UPFOLD00 - This will update the pmi field PMPXFLDR with the appropriate
.            category code (cat FS) that has a matching TCINDC1 to FOLDERTY.
.
. Parameters: FOLDERT1 values 1=DVA,2=TAC,3=Work Comp and 4 = Family choice
.                             5=Civil Tort
.--------------------------------------------------------------------------
.
UPFOLD00  MATCH     "0",PTCNCOMF            * Not using compensable folders
          GOTO      UPFOLD99 IF EQUAL
.
          MOVE      ZERO,UPDFOLD
          MOVE      "FS",CATEGORY
          PACK      KEY5,CATEGORY,SP70
          CALL      RDSCODE1
UPFOLD10  CALL      RDKCODE1
          BRANCH    OVRCD,UPFOLD99
.
          MATCH     CATEGORY,TCODE
          GOTO      UPFOLD99 IF NOT EQUAL
.
          MATCH     FOLDERTY,TCINDC1
          GOTO      UPFOLD80 IF EQUAL
.
          GOTO      UPFOLD10
.
UPFOLD80  PACK      KEY8,URNUMBER,SP70
          CALL      RDPMPX21
          BRANCH    OVRCD,UPFOLD99
.
          MATCH     ACODE,PMPXFLDR
          GOTO      UPFOLD99 IF EQUAL
.
          MOVE      ACODE,PMPXFLDR
.
          CALL      UPPMPX21
          MOVE      ONE,UPDFOLD
.
UPFOLD99  RETURN
+
