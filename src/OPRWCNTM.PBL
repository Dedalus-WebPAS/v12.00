. *----------------------------------------------------------------------
. * Include Name  :   OPRWCNTM.PBL
. *
. * Function      :   CGI variable functions for OPRWCNFD - oprwcnaf.dat
.----------------------------------------------------------------------
.  Modifications Done :
.------------------------------------------------------------------------
.  V11.01.00  02/03/2021  Ebon Clements TSK 0886820
.             Created include
.----------------------------------------------------------------------
.------------------------------------------------------------------------
.   Clear CGI Parameters for theatre week cycle number file
.------------------------------------------------------------------------
CPOPWC00  MOVE      Z70,OPWCN001
          MOVE      Z70,OPWCN002
          MOVE      Z70,OPWCN003
.
CPOPWC99  RETURN
.
.------------------------------------------------------------------------
. Display tags for theatre week cycle number file
.------------------------------------------------------------------------
OPWC0000  BRANCH    FLDITMNO,OPWC0010,OPWC0020,OPWC0030,OPWC0040,OPWC0050:
                             OPWC0060
.
          WRITE     HTMLFILE;"Invalid IBA Tag";
          GOTO      OPWC9999
.
OPWC0010  UNPACK    OPWCEFFD,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      OPWC9999
.
OPWC0020  MOVE      "ST",CATEGORY
          MOVE      OPWCUNIT,CODE
          CALL      CODITM00
          GOTO      OPWC9999
.
OPWC0030  WRITE     HTMLFILE;OPWCWEEK;
          GOTO      OPWC9999
.
OPWC0040  UNPACK    OPWCCDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      OPWC9999
.
OPWC0050  WRITE     HTMLFILE;OPWCCTIM;
          GOTO      OPWC9999
.
OPWC0060  PACK      KEY10,OPWCCUID,SP70
          CALL      RDWBSE1
          IF        OVRCD=1
            MOVE      OPWCCUID,WBSENAM
          ENDIF
          WRITE     HTMLFILE;WBSENAM;
          GOTO      OPWC9999
.
OPWC9999  RETURN
.------------------------------------------------------------------------
.   Set CGI Parameters for theatre week cycle number file
.------------------------------------------------------------------------
SOPWC000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,SOPWC001,SOPWC002,SOPWC003
.
          MOVE      ONE,EXIT
          GOTO      SOPWC999
.
SOPWC001  UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      OPWCN001,CCENT,CYEAR,CMON,CDAY,SP70
          GOTO      SOPWC999
.
SOPWC002  PACK      OPWCN002,QRYDATA,SP70
          GOTO      SOPWC999
.
SOPWC003  PACK      OPWCN003,QRYDATA,SP70
          GOTO      SOPWC999
.
SOPWC999  RETURN
.
.------------------------------------------------------------------------
.   Update theatre week cycle number file
.------------------------------------------------------------------------
MVOPWC00  MATCH     OPWCN001,Z70
          IF        !@EQUAL
            MOVE      OPWCN001,OPWCEFFD
          ENDIF
.
          MATCH     OPWCN002,Z70
          IF        !@EQUAL
            MOVE      OPWCN002,OPWCUNIT
          ENDIF
.
          MATCH     OPWCN003,Z70
          IF        !@EQUAL
            MOVE      ZERO,F2
            SQUEEZE   OPWCN003
            MOVE      OPWCN003,F2
            MOVE      F2,OPWCWEEK
          ENDIF
.
MVOPWC99  RETURN
