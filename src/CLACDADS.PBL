.******************************************************************************
.* System         : ALL                                                       *
.*                                                                            *
.* Include Name   : CLACDADS.PBL                                              *
.*                                                                            *
.* Function       : Display all the categories from the                       *
.*                    Clinical Audits Codes file                              *
.*                                                                            *
.* Author         : Justin Coates           04/05/1992                        *
.*                                                                            *
.* Modifications  :                                                           *
.******************************************************************************
CLACDADS  MOVE      FIVE,CVERT              * line counter
          MOVE      ONE,CCOL                * start on LHS of screen
          MOVE      SP5,KEY5                * key of blanks
          CALL      RSCACOD1                * positional read
.
          DISPLAY   *P1:3,*EF,*V2LON,*P30:4,"CATEGORIES":
                    *P1:5,*ULON,"Cat. ",*P8:5,"          Description         "
.
. -------------------------------------
. ------- next read on item file ------
. -------------------------------------
.
ACACD100  CALL      RKCACOD1
          BRANCH    OVRCD,ACACD999          * end of file
.
          MATCH     SP3,CACDCODE
          GOTO      ACACD100 IF NOT EQUAL   * not a category desc
.
. ----------------------------------
. ------- increment counters -------
. ----------------------------------
.
ACACD200  ADD       ONE,CVERT               * inc line counter
.
          COMPARE   "23",CVERT
          GOTO      ACACD300 IF LESS        * will fit on the page
.
          ADD       "40",CCOL               * inc column counter
          MOVE      SIX,CVERT               * reset row position
.
          COMPARE   "80",CCOL  
          GOTO      ACACD400 IF NOT LESS    * need a new page
.
          DISPLAY   *V2LON,*ULON,*P41:5," Code ":
                    *P48:5,"          Description         "
.
. -------------------------------------------
. ------- display all the information -------
. -------------------------------------------
.
ACACD300  DISPLAY   *PCCOL:CVERT,*V2LON,SP1,CACDCATG,*HOFF,SP4,CACDDESC
          GOTO      ACACD100
.
. -------------------------------
. ------- new page needed -------
. -------------------------------
.
ACACD400  DISPLAY   *P1:24,"( )ontinue, ( )irst, ( )ext ? ",*EL:
                    *V2LON,*P2:24,ANSC,*P14:24,ANSF,*P23:24,ANSN
.
          KEYIN     *P31:24,*DV,UNDLN1:
                    *P31:24,*V2LON,ANS
.
          REP       UPPLOW,ANS
.
          MATCH     ANSC,ANS
          GOTO      ACACD999 IF EQUAL       * Continue selected
.
          MATCH     ANSF,ANS
          GOTO      CLACDADS IF EQUAL       * First selected
.
. --------------------------------------
. ------- (N)ext screen selected -------
. --------------------------------------
.
ACACD700  DISPLAY   *P41:5,*EF;
          MOVE      SIX,CVERT               * first display row 
          MOVE      ONE,CCOL                * get back to LHS of screen
          GOTO      ACACD300                * display the data
.
ACACD999  RETURN
+
