.------------------------------------------------------------
. Program  : EMRMR5
.
. Function : This include prints the EMR MR-5 details
.
. Modifications: 
.          V11.03.01 09.11.2023  DA Sarkies       TSK 0939373
.                    Shifted the date processing to after the display
.                    of the header construction
.          V11.00.01 31/03/2020  J.Tan            TSK 0868813
.                    Changed PKNAME from DIM30 to DIM 80
.          V10.06.01 02/04/2015  Peter Vela    CAR 297061
.                    Added validations to Local GP HSPFP000
.          V10.04.01 28/05/2014  Don Nguyen    CAR 301096
.                    Fixed WACLN000 - Moved unpack of VSMDDATE just above print
.          V10.03.10 29.04.2013  Peter Vela    CAR 281388
.                    Changed call to read from CLINOT00 BHS layout
.                    Added check for PASSNO = 0 @ CLIDIA00
.          V10.03.09 19.04.2013  Peter Vela    CAR 281388
.                    Added BHS layout
.          V10.03.08 14/02/2013  Don Nguyen    CAR 277835
.                    Modified WANOTE00 to skip deleted notes
.          V10.03.07 11/02/2013  Don Nguyen    CAR 277835
.                    Added WACLN000, WANOTE00, PRPGHD00 and CHKWOF00
.          V10.03.06 13.07.2012  Don Nguyen    CAR 268835
.                    Changed HSPFP000 & HSPF2000 to resolve problem of page
.                    overflow calculations being incorrect.
.          V10.03.05 12.07.2012  Don Nguyen    CAR 268189
.                    Formatted Patient and Attending Doctor name for both
.                    Healthscope print forms (HSPFP000 & HSPF2000)
.          V10.03.04 19.06.2012 Peter Vela     CAR 264245
.                    Added HSPF2000 Healthscope Clinical Notes
.          V10.03.03 05.06.2012 Ebon Clements  CAR 265695
.                    Removed extra blank lines from healthscope layout
.                    on diagnosis and second page heading (HSPFP000)
.          V10.03.02 04.06.2012 Ebon Clements  CAR 265695
.                    Added local GP fax and additional diagnosis details
.                    to (HSPFP000) Healthscope Layout
.          V10.03.01 30.11.2011  Jill Parkinson CAR 249362
.                    Changed alert key from key13 to key16
.          V10.02.04 19.09.2011 B.G.Ackland    CAR 249083
.                    Fixed Procedures (HSPFP000) Healthscope Layout
.          V10.02.03 08/07/2011 B.G.Ackland    CAR 243247
.                    Fixed Procedures (HSPFP000) Healthscope Layout
.          V10.02.02 05/07/2011 B.G.Ackland    CAR 243247
.                    Fixed Procedures (HSPFP000) Healthscope Layout
.          V10.02.01 05/07/2011 B.G.Ackland    CAR 243247
.                    Added Procedures (HSPFP000) Healthscope Layout
.          V9.12.10  25/02/2010  Mike Laming   CAR 215117
.                    Added Procedures (HDHCLIN0) routines PROCS000 & LOKCOD00
.                    Changed sequence of Mang.Notes & Medical History
.          V9.12.09  04/01/2010  Ebon Clements CAR 210183
.                    Modify HDH report (HDHCLIN0) to print at positon 1 not 9 
.          V9.12.08 24/12/2009  Ebon Clements CAR 213034
.                    Print this visits medical history - HDHCLIN0
.          V9.12.07  18/11/2009  Ebon Clements CAR 210183
.                    Modify HDH report (HDHCLIN0) to print notes 80 chars wide
.                    If note line length exceeds 80 chars print an extra line
.          V9.12.03  01/07/2009  Mike Laming   CAR 196463
.                    Modify HDH report (HDHCLIN0) to print Head and Tail details
.          V9.12.02  11/05/2009  Peter Vela    CAR 196060
.                    Changed Patient Notes to Medical History for HDH
.          V9.12.01  08/05/2009  Peter Vela    CAR 196060
.                    Changed Hospital Details in HDH
.          V9.11.02  01/05/2009  Peter Vela    CAR 189171
.                    Changed Hospital Details for HDH
.          V9.11.01  01/05/2009  Peter Vela    CAR 189171
.                    Added HDHCLINO
.          V9.04.01  20/06/2005  Ebon Clements CAR 59678
.                    Added DOCTNT00 - doctor notes from VISMTXFD
.          V9.04.00  30/05/2005  Ebon Clements CAR 59676
.                    Added CLINTX00 - clinical notes from VISMTXFD
.          V9.03.02  07/07/2004  Peter Vela    CAR 50310
.                    Removed all unwanted carriage returns from St. Vincents
.                    Clinical Details report
.                    Put St. Vincents management notes in a different section
.                    Placed St. Vincents Clinical notes after Emergency Diag
.          V9.03.01  25/02/2004  Jill Habasque CAR 47769
.                    Added hardcoded layout for HWK
.          V9.03.02  30/06/2003  Tonii  CAR 38920 (SV133928)
.                    Added printing of UR number
.                    Fixed printing of Patient Notes
.                    Changed display of "Seen By Date & Time"
.          V9.03.01  14/05/2003  Tonii  CAR 38920
.                    Fixed alignment of data in EMRCLIN0 for 
.                    St Vincent's 
.------------------------------------------------------------
.                V6.06.00  09/03/1999  Katie Nelson
.                Created Program
.
.------------------------------------------------------------
. Linup routine from AAEMR5.PBL
.------------------------------------------------------------
LINUPMR5  DISPLAY   *P1:24,*EL,*P40:24,*V2LON,"** Generating Lineup **";
          CALL      OPNPRINT
          PRINT     *N,*N:
                    *N,*9,"XXXXXXXXXXXXXXXXXXXX":
                    *37,"XXXXXXXXXXXXXXXXXXXX",*58,"XXXX",*65,"999":
                    *71,"99999999":
                    *N,*N:
                    *17,"XXXXXXXXXXXXXXXXXXXXXXXXX":
                    *N,*17,"XXXXXXXXXXXXXXXXXXXXXXXXX",*43,"XXXXXXXXXXXXXXX":
                    *63,"XXXX",*70,"XXXXXXXXXXXX":
                    *N,*N:
                    *9,"XXXXXXXXXXXXXXXXXXXX",*N:
                    *9,"XXXXXXXXXXXXXXXXXXXXXXXXX",*N:       *** pkadd1
                    *9,"XXXXXXXXXXXXXXXXXXXXXXXXX",*27,"XXXXXXXXXXXXXXX":
                    *43,"XXXX",*N,*N:
                    *9,"XXXXXXXXXXXXXXXXXXXX",*40,"XXXXXXXXXXXXXXXXXXXX",*N:
                    *16,"XXXXXXXXXX":          **
                    *35,"XXXXXXXXXXXXXXXXXXXXXXXXX":
                    *67,"XX/XX/XXXX",*78,"XXXXX",*N,*N:   *** emvidate emvitime
                    *50,"XXXXXXXXXXXXXX",*N,*N,*N:      *** poccup
                    *20,"X",*25,"XX/XX/XXXX",*36,"99999999",*49,"XXXX":
                    *55,"XXXX",*62,"XXXX",*75,"XXX":
                    *N,*N,*N,*46,"XXXXXXXXXXXXX",*70,"XXXXX":     *** emvidoct
                    *N,*14,"XXXX":
                    *N,*9,"XXXXXXXXXXXXXXXXXXXX":
                    *N,*N,*N,*N,*14,"XXXX":
                    *N,*9,"XXXXXXXXXXXXXXXXXXXX":
                    *N,*N,*N,*N,*14,"XXXX":
                    *N,*9,"XXXXXXXXXXXXXXXXXXXX":
                    *N,*N,*N,*N,*N,*N,*N,*N:
                    *N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N:
                    *N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N;
.
          CALL      CLSPRINT
          RETURN
.
.------------------------------------------------------------
.  Print
.------------------------------------------------------------
SETLMR5   MOVE      SP1,DIM1
          MOVE      SP20,DIM11
          MOVE      "58",PAGEMAXL            * could use a parameter (?)
          MOVE      ZERO,PASSNO
          MOVE      ZERO,CLNO
.
          MOVE      EMVIADMN,KEY8            * Read the EMR Visit File
          CALL      RDEMVIS1
          BRANCH    OVRCD,OVERCOND
.
          MOVE      SP4,SRC4
          MOVE      SP4,SIT4
          MOVE      SP4,MODE4
          MOVE      SP4,HIGH14
          MOVE      SP4,HIGH24
          MOVE      SP5,HIGH34
.
          PACK      KEY5 WITH CODEH1,PHIGH1
          CALL      RDCODE1
          BRANCH    OVRCD OF HIG2
          MOVE      THCSCOD,HIGH14
HIG2      PACK      KEY5 WITH CODEH2,PHIGH2
          CALL      RDCODE1
          BRANCH    OVRCD OF HIG3
          MOVE      THCSCOD,HIGH24
HIG3      MATCH     SP3,PHIGH3
          GOTO      SRC IF EQUAL
          PACK      KEY5 WITH CODEH3,PHIGH3
          CALL      RDCODE1
          BRANCH    OVRCD OF SRC
          MOVE      OTHER,HIGH34
.
SRC       PACK      KEY5 WITH CODES,EMVISRCE
          CALL      RDCODE1
          BRANCH    OVRCD OF SIT
          MOVE      THCSCOD,SRC4
SIT       PACK      KEY5 WITH CODEES,EMVISITN
          CALL      RDCODE1
          BRANCH    OVRCD OF MODE
          MOVE      THCSCOD,SIT4
.
MODE      PACK      KEY5 WITH CODEEA,EMVIMODE
          CALL      RDCODE1
          BRANCH    OVRCD OF PREV
          MOVE      THCSCOD,MODE4
.
PREV      MATCH     SP3,EMVIPREV
          GOTO      TMSEEN IF EQUAL
.
          PACK      KEY5 WITH CODEEP,EMVIPREV
          CALL      RDCODE1
          BRANCH    OVRCD OF TMSEEN
.
          MOVE      TDESC,EMRPREV
.
TMSEEN    MOVE      EMVIDRTM,EMRSEEN
.
          MOVE      EMVIDOCT,KEY6
          CALL      RDDOCT1
          BRANCH    OVRCD OF SPADOC
          MOVE      DGNAME,DIM1
          MOVE      DSNAME,DIM11
          PACK      EMRDOCT WITH DIM1,DOT,DIM11
          RESET     EMRDOCT
KAEINS    PACK      EMRINSUR WITH CODEU1,EMVIINSR
          RESET     EMRINSUR
.
          MOVE      EMRINSUR,KEY5
          CALL      RDCODE1
          BRANCH    OVRCD OF SPADESC
          MOVE      TDESC,EMRDESC
KAEURN    RESET     KEY8
.
          MOVE      EMVIURNO,PURNO         *** Change when PATMA1A1 uses 8 pos.
          MOVE      PURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD OF SPAPAT
          MOVE      KEY8,EMRURNO
          MOVE      PSNAME,QSNAME
          MOVE      PGNAME,QGNAME
          MOVE      PTITL,QTITL
          MOVE      PSAGE,QSAGE
          MOVE      PADD1,QADD1
          MOVE      PADD2,QADD2
          MOVE      PSUBRB,QSUBRB
          MOVE      PADD4,QADD4
          MOVE      PPOST,QPOST
          MOVE      PTELEP,QTELEP
          MOVE      PSEX,QSEX
          MOVE      PMEDI,QMEDI
          MOVE      POCCUP,QOCCUP
          MOVE      PLOCDOC,QLOCDOC
          MOVE      SP3,DCASE
          BRANCH    PCASE OF YESCASE
          GOTO      KRESP
.
YESCASE   MOVE      ASK,DCASE
.
KRESP     MOVE      EMVIADMN,PKADMN
          MOVE      PKADMN,KEY8
.
          CALL      RDRESP1              *** read patresp  
          COMPARE   ONE,OVRCD
          CALL      SPARESP IF EQUAL
.
          MOVE      SP3,WCDESC
          MOVE      SP3,VADESC
.
          UNPACK    SP5 WITH TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
          PACK      KEY5 WITH CODECL,EMVICOMP
          CALL      RDCODE1
.
          MOVE      ZERO,FORM2
GETCLL4   ADD       ONE,FORM2
          COMPARE   SIX,FORM2
          GOTO      ENDCLM4 IF NOT LESS
.
          LOAD      ANS USING FORM2 OF TCINDC1,TCINDC2,TCINDC3:
                    TCINDC4,TCINDC5
.
          CMATCH    ANSW TO ANS
          GOTO      CLWCO4 IF EQUAL
          CMATCH    ANSM TO ANS
          GOTO      CLMAB4 IF EQUAL
.
          GOTO      GETCLL4
.
CLWCO4    MOVE      "WCA",WCDESC
          GOTO      GETCLL4
.
CLMAB4    MOVE      "VA ",VADESC
          GOTO      GETCLL4
.
ENDCLM4   RETURN
.------------------------------------------------------------
. Get ACC Number
.------------------------------------------------------------
SETACC5   OPEN      EOCLNKA3,"eoclnkaf"
          OPEN      EOCREFA1,"eocrefaf"
          MOVE      SP70,ECRFACCN
          PACK      KEY23,DEMVIADM,SP70
          CALL      RSECLNK3
SETACC10  CALL      RKECLNK3
          BRANCH    OVRCD,SETACC99
.
          MATCH     DEMVIADM,DECLNVIS
          GOTO      SETACC99 IF NOT EQUAL
.
          PACK      KEY13,ECLNURNO,DECLNEPN,SP70
          CALL      RDECREF1
          IF        OVRCD=1
            MOVE      SP70,ECRFACCN
          ENDIF
.
SETACC99  RETURN
.
.------------------------------------------------------------
. Print Form     
.------------------------------------------------------------
PRTMR5    CALL      OPNPRINT
.
          UNPACK    PKNAME,KEY60
          PRINT     *N,*N:
                    *N,*9,QSNAME:
                    *37,QGNAME,*58,QTITL,*65,QSAGE,*71,EMRURNO,*79,DCASE:
                    *N,*N,*17,QADD1:
                    *N,*17,QADD2,*43,QSUBRB,*63,QPOST,*70,QTELEP,*N,*N:
                    *9,KEY60,*70,VADESC,*74,WCDESC,*N:
                    *9,PKADD1,*N:
                    *9,PKADD2,*35,PKSUBR,*51,PKPOST,*N,*N:
                    *9,EMRDESC,*40,EMVIEMPL,*N:
                    *16,QMEDI,*40,PSUBRB; 
          UNPACK    EMVIDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *67,CPCDATE,*78,EMVITIME,*N,*N:
                    *9,QLOCDOC,*50,QOCCUP,*N,*N:
                    *20,QSEX;
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *25,CPCDATE,*36,EMVIADMN,*49,SRC4,*55,SIT4,*62,MODE4:
                    *75,EMRPREV:
                    *N,*N,*N,*46,EMRDOCT,*70,EMRSEEN:
                    *N,*N,*15,HIGH24:
                    *N,*25,HIGH34:
                    *N,*N,*N:
                    *N,*N,*N,*N,*N:
                    *N,*15,HIGH14:
                    *N,*N,*N,*N,*N,*N,*N,*N,*N:
                    *N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N:
                    *N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N;


.
          CALL      CLSPRINT
          RETURN
.------------------------------------------------------------
. Print Form     
.------------------------------------------------------------
EMRCLIN0  CALL      OPNPRINT
.
          PRINT    *9,"St Vincent's Hospital",*50,"U/R Number  :",EMVIURNO,*N:
                   *9,"Emergency Care Centre",*50,"Visit Number:",EMVIADMN,*N:
                   *9,"Medical Summary";
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *50,QSNAME,*65,QGNAME,*N:
                    *9,"Princess Street, Fitzroy",*50,QSEX,*58,CPCDATE,*N:
                    *9,"9288-4375 / 9288-4373",*50,QADD2,*N:
                    *50,QSUBRB,*65,QPOST,*N:
                    *50,"Ph : ",*56,QTELEP,*N
.
          PRINT     *N:
                    *N:
                    *9,"Referring Doctors",*N:
                    *9,"=================",*N;
                    PACK      KEY10,PMPXRHC1,SP70
                    CALL      RDPMHCP1
                    IF        OVRCD=0
                      MOVE      PMHCTITL,FMTTITLE
                      MOVE      PMHCGNAM,FMTGNAME
                      MOVE      PMHCSNAM,FMTSNAME
                      CALL      DOCNM000
                    ELSE
                      MOVE      SP70,DOCFNAME
                    ENDIF
          PRINT     *9,DOCFNAME,*N:
                    *N:
                    *9,"TRIAGE:",*N:
                    *N:
                    *9,"Comments        : ",*27,EMVICOM1,*N;           *C-50310
                    MATCH   SP70,EMVICOM2 
                    IF      !@EQUAL 
          PRINT     *27,EMVICOM2,*N;                                   *C-50310
                    ENDIF
                    MATCH   SP70,EMVICOM3 
                    IF      !@EQUAL 
          PRINT     *27,EMVICOM3,*N;                                   *C-50310
                    ENDIF
                    MATCH   SP70,EMVICOM4 
                    IF      !@EQUAL 
          PRINT     *27,EMVICOM4,*N;                                   *C-50310
                    ENDIF
                    MATCH   SP70,EMVICOM5 
                    IF      !@EQUAL 
          PRINT     *27,EMVICOM5,*N;                                   *C-50310
                    ENDIF
                    MATCH   SP70,EMVICOM6 
                    IF      !@EQUAL 
          PRINT     *27,EMVICOM6,*N;                                   *C-50310
                    ENDIF 
                    PACK      KEY5,CODEAA,EMVITRIG
                    CALL      RDCODE1                      * code on file ?
          PRINT     *9,"Triage Category : ",*27,TDESC,*N;              *C-50310
                    UNPACK    EMVIDATE,CCENT,CYEAR,CMON,CDAY
                    CALL      PACDATE
          PRINT     *9,"Attendance Date : ",*27,CPCDATE:               *C-50310
                    *40,"Time : ",*48,EMVITIME,*N;
.                    UNPACK    EMVIDRDT,CCENT,CYEAR,CMON,CDAY
.                    CALL      PACDATE
.         PRINT      *9,"Seen By Date    : ",*27,CPCDATE:              *C-50310
.                    *40,"Time : ",*48,EMVIDRTM,*N;
                    CALL      SEENDC00
         PRINT      *N:
                    *9,"Patient Notes",*N:
                    *9,"=============",*N;
                    CALL   DISPNT00
.         PRINT     *N:                                                *D-50310
.                   *9,"Clinical Notes",*N:
.                   *9,"=============",*N;
.                   CALL   CLINOT00
          PRINT     *N,*N:                                             *I-50310
                    *9,"Management Notes",*N:
                    *9,"=============",*N;
                    CALL   MANNOT00
          PRINT     *N,*N:
                    *9,"Emerg Diagnosis",*N:
                    *9,"===============",*N:
                    *N;
                    CALL   CLIDIA00
.
          PRINT     *N,*N:                                             *I-50310
                    *9,"Clinical Notes",*N:
                    *9,"=============",*N;
                    CALL   CLINTX00
.
          PRINT     *N,*N:
                    *9,"Doctor Notes",*N:
                    *9,"=============",*N;
                    CALL   DOCTNT00
.
                    PACK      KEY10,EMVIDOCT,SP70
                    CALL      RDPMHCP1
                    IF        OVRCD=0
                      MOVE      PMHCTITL,FMTTITLE
                      MOVE      PMHCGNAM,FMTGNAME
                      MOVE      PMHCSNAM,FMTSNAME
                      CALL      DOCNM000
                    ELSE
                      MOVE      SP70,DOCFNAME
                    ENDIF

          PRINT     *N:
                    *50,"Doctor  : ",*60,DOCFNAME                      *C-50310
.
          PRINT     *N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N;
.
          CALL      CLSPRINT
EMRCLIN9  RETURN
.------------------------------------------------------------
. Print HWK Form
.------------------------------------------------------------
HWKCLIN0  CALL      OPNPRINT
.
          PRINT    *1,"Hawke's Bay District Health Board":
                   *42,"U/R Number  :",EMVIURNO,*N:
                   *1,"Emergency Discharge",*42,"Visit Number:",EMVIADMN,*N:
                   *42,"ACC Number  :",ECRFACCN;
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *N,*42,QSNAME,*58,QGNAME,*N:
                    *42,QSEX,*52,CPCDATE,*N:
                    *42,QADD1,*N:
                    *42,QADD2,*N:
                    *42,QSUBRB,*N:
                    *42,QADD4,*58,QPOST,*N:
                    *42,"Ph : ",*50,QTELEP,*N
.
          PRINT     *N:
                    *N:
                    *1,"Referring Doctors",*N:
                    *1,"=================",*N;
                    PACK      KEY10,PMPXRHC1,SP70
                    CALL      RDPMHCP1
                    IF        OVRCD=0
                      MOVE      PMHCTITL,FMTTITLE
                      MOVE      PMHCGNAM,FMTGNAME
                      MOVE      PMHCSNAM,FMTSNAME
                      CALL      DOCNM000
                    ELSE
                      MOVE      SP70,DOCFNAME
                    ENDIF
          PRINT     *1,DOCFNAME,*N:
                    *N:
                    *1,"TRIAGE:",*N:
                    *N:
                    *1,"Comments        : ",*18,EMVICOM1,*N;
                    MATCH   SP70,EMVICOM2
                    IF      !@EQUAL
          PRINT     *18,EMVICOM2,*N;
                    ENDIF
                    MATCH   SP70,EMVICOM3
                    IF      !@EQUAL
          PRINT     *18,EMVICOM3,*N;
                    ENDIF
                    MATCH   SP70,EMVICOM4
                    IF      !@EQUAL
          PRINT     *18,EMVICOM4,*N;
                    ENDIF
                    MATCH   SP70,EMVICOM5
                    IF      !@EQUAL
          PRINT     *18,EMVICOM5,*N;
                    ENDIF
                    MATCH   SP70,EMVICOM6
                    IF      !@EQUAL
          PRINT     *18,EMVICOM6,*N;
                    ENDIF
                    PACK      KEY5,CODEAA,EMVITRIG
                    CALL      RDCODE1                      * code on file ?
          PRINT     *1,"Triage Category : ",*18,TDESC,*N;
                    UNPACK    EMVIDATE,CCENT,CYEAR,CMON,CDAY
                    CALL      PACDATE
          PRINT     *1,"Attendence Date : ",*18,CPCDATE:
                    *32,"Time : ",*40,EMVITIME,*N;
.                    UNPACK    EMVIDRDT,CCENT,CYEAR,CMON,CDAY
.                    CALL      PACDATE
.         PRINT      *1,"Seen By Date    : ",*18,CPCDATE:
.                    *32,"Time : ",*40,EMVIDRTM,*N;
                    CALL      SEENDC00
         PRINT      *N:
                    *1,"Medical History",*N:
                    *1,"===============",*N;
                    CALL   DISPNT00
          PRINT     *N:
                    *1,"Patient Notes",*N:
                    *1,"=============",*N;
                    CALL   PATNOT00
          PRINT     *N,*N:
                    *1,"Emerg Diagnosis",*N:
                    *1,"===============",*N:
                    *N;
                    CALL   CLIDIA00

                    PACK      KEY10,EMVIDOCT,SP70
                    CALL      RDPMHCP1
                    IF        OVRCD=0
                      MOVE      PMHCTITL,FMTTITLE
                      MOVE      PMHCGNAM,FMTGNAME
                      MOVE      PMHCSNAM,FMTSNAME
                      CALL      DOCNM000
                    ELSE
                      MOVE      SP70,DOCFNAME
                    ENDIF

          PRINT     *N:
                    *42,"Doctor  : ",*51,DOCFNAME
.
          PRINT    *N,*N,*N,*N:
                   *1,"An Appointment has been made for the Clinic in ____":
                   "days/weeks.  Please ",*N:
                   *1," see your GP in ___days/weeks for followup.  If ":
                   "your wound becomes red, ",*N:
                   *1,"hot or sore please call your GP or the Emergency ":
                   "Department on ",*N:
                   *1,"06 8788109 ext 2623.",*N,*N:
                   *1,"ALWAYS CALL 111 IN AN EMERGENCY"
.
          CALL      CLSPRINT
HWKCLIN9  RETURN
.------------------------------------------------------------
. Clinical Notes from VISCMTFD
.------------------------------------------------------------ 
CLINOT00  PACK      KEY14,EMVIADMN,SP70    * clinical notes
          CALL      RSVSCMT1               * position in file ?
CLINOT10  CALL      RKVSCMT1               * read next record
          BRANCH    OVRCD,CLINOT99         * eof - field blank
          MATCH     DEMVIADM,VSCTVIST      * same visit still ?
          GOTO      CLINOT99 IF NOT EQUAL
          MATCH     "HWKCLI",STATNCOD
          IF        @EQUAL
            PRINT     *N:
                      *1,VSCTDATA;
          ELSE 
            MATCH     "001",VSCTTYPE                                   *I-50310
            IF        @EQUAL
              PRINT     *N:
                        *9,VSCTDATA;
            ENDIF
          ENDIF
.
          GOTO      CLINOT10
.
CLINOT99  RETURN
.------------------------------------------------------------ 
. Clinical Notes from VISMTXFD
.------------------------------------------------------------ 
CLINTX00  PACK      KEY20,EMVIADMN,ZERO,ZERO,ONE,SP70    * clinical notes
          CALL      RSVSMTX1               * position in file ?
CLINTX10  CALL      RKVSMTX1               * read next record
          BRANCH    OVRCD,CLINTX99         * eof - field blank
.
          MATCH     DEMVIADM,VSMTVISN      * same visit still ?
          GOTO      CLINTX99 IF NOT EQUAL
.
          MATCH     "001",VSMTTYPE                         
          IF        @EQUAL
            PRINT     *N:
                      *9,VSMTCMNT;
          ELSE
            GOTO      CLINTX99
          ENDIF
.
          GOTO      CLINTX10
.
CLINTX99  RETURN
.------------------------------------------------------------          *I-50310
MANNOT00  PACK      KEY14,DEMVIADM,SP70    * Management notes
          CALL      RSVSCMT1               * position in file ?
MANNOT10  CALL      RKVSCMT1               * read next record
          BRANCH    OVRCD,MANNOT99         * eof - field blank
          MATCH     DEMVIADM,VSCTVIST      * same visit still ?
          GOTO      MANNOT99 IF NOT EQUAL
.
          MATCH     "002",VSCTTYPE
          IF        @EQUAL
            MOVE      "1",LINSREQD
            CALL      CHKPOF00             * check for Page Overflow  *I-196463
            IF        PASSNO = 0 | PASSNO = 2
              MATCH     "HDHCLN",STATNCOD       * Hawkesbury Dist.Health (HDH)
              IF        @EQUAL
                PRINT     *N,*1,VSCTDATA;
              ELSE
                PRINT     *N,*9,VSCTDATA;
              ENDIF
            ENDIF
          ENDIF
.
          GOTO      MANNOT10
.
MANNOT99  RETURN
.------------------------------------------------------------
PATNOT00  PACK      KEY14,EMVIURNO,SP70    * patient notes
          CALL      RSPMMTX1               * position in file ?
PATNOT10  CALL      RKPMMTX1               * read next record
          BRANCH    OVRCD,PATNOT99         * eof - field blank
          MATCH     EMVIURNO,PMMTURNO      * same patient still ?
          GOTO      PATNOT99 IF NOT EQUAL
          PRINT     *N:
                    *1,PMMTCMNT;

          GOTO      PATNOT10
.
PATNOT99  RETURN
.------------------------------------------------------------
.         Notes Display in the text area
.------------------------------------------------------------
DISPNT00  MOVE      EMVIURNO,OBMDURNO
          PACK      KEY14,OBMDURNO,Z70
          CALL      RSOBMDT1
DISPNT10  CALL      RPOBMDT1
          BRANCH    OVRCD,DISPNT99
.
          MATCH     EMVIURNO,OBMDURNO
          GOTO      DISPNT99 IF NOT EQUAL
.
          MATCH     "1",OBMDDELT
          GOTO      DISPNT10 IF EQUAL
.
          MATCH     "HDHCLN",STATNCOD       * Hawkesbury Dist.Health (HDH)
          IF        @EQUAL
            MATCH     EMVIDATE,OBMDDATE     * Only print medical history dated
            GOTO      DISPNT10 IF LESS      * on or after the arrival date
.
            IF        EMVISTAT=2 | EMVISTAT=3
              MATCH     OBMDDATE,EMVIDDAT   * Only print medical history dated
              GOTO      DISPNT10 IF LESS    * on or before the discharge date
            ENDIF
          ENDIF
.
          MATCH     SP70,OBMDDATE
          IF        !@EQUAL
            UNPACK    OBMDDATE,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      TEMPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          CLEAR     MEDNOTES
          APPEND    TEMPDATE,MEDNOTES
          APPEND    "  ",MEDNOTES
          APPEND    OBMDTIME,MEDNOTES
          APPEND    "  ",MEDNOTES
          APPEND    OBMDUSER,MEDNOTES
          APPEND    "  ",MEDNOTES
          APPEND    OBMDOCCG,MEDNOTES
          RESET     MEDNOTES
.
          MATCH     "HWKCLI",STATNCOD
          IF        @EQUAL
            PRINT     *N:
                      *1,MEDNOTES;
          ELSE 
            MOVE      "1",LINSREQD
            CALL      CHKPOF00             * check for Page Overflow  *I-196463
            IF        PASSNO = 0 | PASSNO = 2
              MATCH     "HDHCLN",STATNCOD       * Hawkesbury Dist.Health (HDH)
              IF        @EQUAL
                PRINT     *N,*1,MEDNOTES;
              ELSE
                PRINT     *N,*9,MEDNOTES;
              ENDIF
            ENDIF
          ENDIF
.
          PACK      KEY17,EMVIURNO,OBMDNOTE,SP70
          CALL      RSOBMTX1
DISPNT20  CALL      RKOBMTX1
          BRANCH    OVRCD,DISPNT99
.
          MATCH     OBMTURNO,OBMDURNO
          GOTO      DISPNT10 IF NOT EQUAL              * was DISPNT99 *C-196463
.
          MATCH     OBMTNOTE,OBMDNOTE
          GOTO      DISPNT10 IF NOT EQUAL
.
          MATCH     "HWKCLI",STATNCOD
          IF        @EQUAL
            PRINT     *N:
                      *1,OBMTCMNT;
            GOTO      DISPNT20
          ENDIF
.
          MATCH     "HDHCLN",STATNCOD      * Only print 80 character per line
          IF        @EQUAL
            UNPACK    OBMTCMNT,KEY80,KEY20
            MOVE      "1",LINSREQD
            CALL      CHKPOF00             * check for Page Overflow  *I-196463
            IF        PASSNO = 0 | PASSNO = 2
              PRINT     *N,*1,KEY80;
            ENDIF
            MATCH     SP70,KEY20
            IF        !@EQUAL
              MOVE      "1",LINSREQD
              CALL      CHKPOF00           * check for Page Overflow  *I-196463
              IF        PASSNO = 0 | PASSNO = 2
                PRINT     *N,*1,KEY20;
              ENDIF
            ENDIF
            GOTO      DISPNT20
          ENDIF
.
          MOVE      "1",LINSREQD
          CALL      CHKPOF00             * check for Page Overflow  *I-196463
          IF        PASSNO = 0 | PASSNO = 2

            PRINT     *N,*9,OBMTCMNT;
          ENDIF
          GOTO      DISPNT20
.
DISPNT99 RETURN

CLIDIA00  PACK      KEY8,EMVIADMN,SP70
          CALL      RDEMDCM1
          BRANCH    OVRCD,CLIDIA99       
.
          MOVE      ZERO,F1
          REPEAT
            ADD       ONE,F1
            LOAD      DIM50A,F1,EMDCTXT1,EMDCTXT2,EMDCTXT3,EMDCTXT4,EMDCTXT5:
                                EMDCTXT6,EMDCTXT7,EMDCTXT8
            MATCH     SP70,DIM50A
            IF        !@EQUAL
              CALL      CHKPOF00       * check for Page Overflow      *I-196463
              IF        PASSNO = 0 | PASSNO = 2
                MATCH     "HDHCLN",STATNCOD       * Hawkesbury Dist.Health (HDH)
                IF        @EQUAL
                  PRINT     *1,DIM50A,*N;
                ELSE
                  PRINT     *9,DIM50A,*N;
                ENDIF
              ENDIF
            ENDIF
          UNTIL     F1 = 8
.
CLIDIA99  RETURN
.
.---------------------------------------------------------------------------
SEENDC00  MOVE      SP70,SAVDOC
          MOVE      "4",LINSREQD                                      *I-196463
.
          PACK      KEY22,DEMVIADM,SP70
          CALL      RSEMHIS1
SEENDC10  CALL      RKEMHIS1
          BRANCH    OVRCD,SEENDC99
          MATCH     DEMVIADM,EMHIVIS
          GOTO      SEENDC99 IF NOT EQUAL
.
          MATCH     EMHIDOC,SAVDOC
          GOTO      SEENDC10 IF EQUAL
.
          PACK      KEY10,EMHIDOC,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
          ELSE
            MOVE      SP70,DOCFNAME
          ENDIF
.
.                                                                     *C-196463
          CALL      CHKPOF00           * check for Page Overflow      *I-196463
          IF        PASSNO = 0 | PASSNO = 2
            MATCH     "HDHCLN",STATNCOD       * Hawkesbury Dist.Health (HDH)
            IF        @EQUAL
              PRINT     *1,"Seen By Doctor  : ",*19,DOCFNAME;          *C-50310
            ELSE
              PRINT     *9,"Seen By Doctor  : ",*27,DOCFNAME;          *C-50310
            ENDIF
.
            UNPACK    EMHIDSD,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            UNPACK    EMHIDST,HOUR,MIN,SEC
            PACK      DOCTIME,HOUR,COLON,MIN,COLON,SEC
            REP       " 0",DOCTIME
.
            MOVE      "2",LINSREQD                                    *I-196463
            CALL      CHKPOF00           * check for Page Overflow    *I-196463
            MATCH     "HDHCLN",STATNCOD       * Hawkesbury Dist.Health (HDH)
            IF        @EQUAL
              PRINT     *N,*1,"Seen By Date    : ",*19,CPCDATE:
                        *32,"Time : ",*40,DOCTIME,*N,*N;
            ELSE
              PRINT     *N,*9,"Seen By Date    : ",*27,CPCDATE:
                        *40,"Time : ",*48,DOCTIME,*N,*N;
            ENDIF
            MOVE      EMHIDOC,SAVDOC
          ENDIF
.
          GOTO      SEENDC10

SEENDC99  MOVE      "0",LINSREQD                                      *I-196463
          RETURN
.
.---------------------------------------------------------------------------
SPADOC    PACK      EMRDOCT WITH SP10,SP3
          GOTO      KAEINS
.
SPADESC   MOVE      SP20,EMRDESC
          GOTO      KAEURN
.
SPAPAT    MOVE      SP8,EMRURNO
          MOVE      SP20,QSNAME
          PACK      QGNAME WITH SP20,SP5
          MOVE      ZERO,QSAGE
          PACK      QADD1 WITH SP20,SP5
          PACK      QADD2 WITH SP20,SP5
          PACK      QSUBRB WITH SP10,SP5
          MOVE      SP4,QPOST
          PACK      QTELEP WITH SP6,SP6
          MOVE      SP1,QSEX
          MOVE      SP10,QMEDI
          PACK      QOCCUP WITH SP10,SP4
          RESET     QOCCUP
          MOVE      SP3,WCDESC
          MOVE      SP3,VADESC
          GOTO      KRESP
.
SPARESP   PACK      PKNAME,SP70,SP20
          MOVE      SP30,PKADD1
          MOVE      SP30,PKADD2
          MOVE      SP30,PKSUBR
          MOVE      SP4,PKPOST
          MOVE      SP12,PKTELEP
          MOVE      SP12,PKTELEB
          MOVE      SP10,PKRELP
.
          MOVE      SP30,WCNAME
          MOVE      "---",VADESC
          MOVE      "---",WCDESC
          RETURN
.------------------------------------------------------------
. Doctors Notes from VISMTXFD
.------------------------------------------------------------
DOCTNT00  PACK      KEY20,EMVIADMN,ZERO,ZERO,SEVEN,SP70    * doctor notes
          CALL      RSVSMTX1               * position in file ?
DOCTNT10  CALL      RKVSMTX1               * read next record
          BRANCH    OVRCD,DOCTNT99         * eof - field blank
.
          MATCH     DEMVIADM,VSMTVISN      * same visit still ?
          GOTO      DOCTNT99 IF NOT EQUAL
.
          MATCH     "007",VSMTTYPE
          IF        @EQUAL
            MATCH     "HDHCLN",STATNCOD       * Hawkesbury Dist.Health (HDH)
            IF        @EQUAL
              PRINT     *N:
                        *1,VSMTCMNT;
            ELSE
              PRINT     *N:
                        *9,VSMTCMNT;
            ENDIF
          ELSE
            GOTO      DOCTNT99
          ENDIF
.
          GOTO      DOCTNT10
.
DOCTNT99  RETURN
.------------------------------------------------------------
. HDH Print Form
.------------------------------------------------------------
HDHCLIN0  CALL      OPNPRINT
          MOVE      ONE,PASSNO              * Pass 1 = count, Pass 2 = print
          MOVE      ZERO,PAGETOTL
.
HDHCLIN1  MOVE      ZERO,CLNO
          MOVE      ONE,PAGENUMB
          MOVE      ZERO,PRINTEND
.
          BRANCH    PASSNO,HDHCLI20         * bypass header on pass 1
.
HDHCLI10  PRINT    *1,"Hawkesbury District Health Service":
                   *42,"U/R Number  :",EMVIURNO,*N:
                   *1,"Emergency Department",*42,"Visit Number:",EMVIADMN,*N;
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *1,"2 Day Street Windsor NSW 2756":
                    *42,QSNAME,*58,QGNAME,*N:
                    *1,"Phone: 4560-5555 / 4560-5508",*42,QSEX,*50,CPCDATE,*N:
                    *42,QADD1,*N:
                    *42,QSUBRB,*57,QPOST,*N:
                    *42,"Ph : ",*48,QTELEP,*N
.
.                                           * set up header for following pages
          CLEAR     DIM80A
          MOVE      "Name : ",KEY7
          APPEND    KEY7,DIM80A
          SQUEEZE   QGNAME
          APPEND    QGNAME,DIM80A
          APPEND    SP1,DIM80A
          APPEND    QSNAME,DIM80A
          MOVE      "     DOB : ",KEY11
          APPEND    KEY11,DIM80A
          APPEND    CPCDATE,DIM80A
          MOVE      "          SEX : ",KEY16
          APPEND    KEY16,DIM80A
          APPEND    QSEX,DIM80A
          RESET     DIM80A
.
          PRINT     *N,*N,*1,"Referring Doctors",*N,*1,"=================",*N;
.
          PACK      KEY10,PMPXRHC1,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
          ELSE
            MOVE      SP70,DOCFNAME
          ENDIF
.
HDHCLI20  MOVE      "15",LINSREQD           * total lines in heading
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *1,DOCFNAME,*N,*N,*1,"TRIAGE:",*N,*N:
                      *1,"Comments        : ",*19,EMVICOM1,*N;
          ENDIF
.
          MOVE      ZERO,F1                 * TRIAGE: information
          REPEAT
            ADD       ONE,F1
            LOAD      DIM50A,F1,EMVICOM2,EMVICOM3,EMVICOM4,EMVICOM5,EMVICOM6
            MATCH     SP70,DIM50A
            IF        !@EQUAL
              CALL      CHKPOF00            * check for Page Overflow
              IF        PASSNO = 2
                PRINT     *19,DIM50A,*N;
              ENDIF
            ENDIF
          UNTIL     F1 = 5
.
          PACK      KEY5,CODEAA,EMVITRIG
          CALL      RDCODE1                 * code on file ?
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *1,"Triage Category : ",*19,TDESC,*N;
          ENDIF
.
          UNPACK    EMVIDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *1,"Attendance Date : ",*19,CPCDATE:
                      *32,"Time : ",*40,EMVITIME,*N;
          ENDIF
.
.                                           * heading info is in SEENDC00
          CALL      SEENDC00                * "Seen By Doctor/By Date" routine
.
.
          MOVE      "4",LINSREQD            * Management Notes heading
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *N,*1,"Management Notes",*N,*1,"=============",*N;
          ENDIF
          CALL      MANNOT00                * "Management Notes" routine
.
          MOVE      "3",LINSREQD
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *N,*N,*1,"Medical History",*N,*1,"=============",*N;
          ENDIF
          CALL      DISPNT00                * "Medical History" routine
.
          MOVE      "5",LINSREQD
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *N,*N,*1,"Emerg Diagnosis",*N,*1,"===============",*N,*N;
          ENDIF
          CALL      CLIDIA00                * "Emerg Diagnosis" routine
.
          MOVE      "4",LINSREQD
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *N,*N,*1,"Clinical Notes",*N,*1,"=============",*N;
          ENDIF
          CALL      DISNOT00
.                                                           * start   *I-215117
          MOVE      "4",LINSREQD
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *N,*N,*1,"Procedures",*N,*1,"=============",*N;
          ENDIF
          CALL      PROCS000                                * end     *I-215117
.
          MOVE      "4",LINSREQD
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *N,*N,*1,"Doctor Notes",*N,*1,"=============",*N;
          ENDIF
          CALL      DOCTNT00
.
          MOVE      SP70,DOCFNAME
          PACK      KEY10,EMVIDOCT,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
          ENDIF
          CALL      CHKPOF00                * check for Page Overflow *I-196463
          IF        PASSNO = 2
            PRINT     *N,*42,"Doctor  : ",*52,DOCFNAME
          ENDIF
.
          MOVE      ONE,PRINTEND
          CALL      CHKPOF00                * print the final tail data
.
          IF        PASSNO = 1
            MOVE      TWO,PASSNO
            GOTO      HDHCLIN1
          ENDIF
.
.
HDHCLIN9  CALL      CLSPRINT
          RETURN
.------------------------------------------------------------
. Clinical Notes from VISMTXFD with Date and Time
.------------------------------------------------------------
DISNOT00  MOVE      "001",NOTEFILT
.
          PACK      KEY17,EMVIADMN,NOTEFILT,SP70
          CALL      RSVSMDT1
DISNOT10  CALL      RKVSMDT1
          BRANCH    OVRCD,DISNOT99
.
          MATCH     DEMVIADM,VSMDVISN
          GOTO      DISNOT99 IF NOT EQUAL
.
          MATCH     NOTEFILT,VSMDTYPE
          GOTO      DISNOT99 IF NOT EQUAL
.
          MATCH     "1",VSMDDELT
          GOTO      DISNOT10 IF EQUAL
.
          MOVE      SP70,TEMPDATE
          MATCH     SP70,VSMDDATE
          IF        !@EQUAL
            UNPACK    VSMDDATE,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      TEMPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          CLEAR     VISNOTES
          APPEND    TEMPDATE,VISNOTES
          APPEND    "  ",VISNOTES
          APPEND    VSMDTIME,VISNOTES
          APPEND    "  ",VISNOTES
          PACK      KEY10,VSMDUSER,SP70
          CALL      RDWBSE1
          IF        OVRCD<>1
            APPEND    WBSENAM,VISNOTES
          ELSE
            APPEND    VSMDUSER,VISNOTES
          ENDIF
.
          CALL      WEBU0000                * Restore scheduling web user id
.
.         APPEND    "  ",VISNOTES
.         APPEND    VSMDOCCG,VISNOTES
          RESET     VISNOTES
.
          STRIP     VISNOTES
          MOVELPTR  VISNOTES,LENGTH
.
          SFORMAT   VAR,LENGTH
          MOVE      VISNOTES,VAR
...       WRITE     HTMLFILE;VAR
.
          CALL      CHKPOF00                * check for Page Overflow *I-196463
          IF        PASSNO = 2
            MATCH     "HDHCLN",STATNCOD       * Hawkesbury Dist.Health (HDH)
            IF        @EQUAL
              PRINT     *N,*1,VAR;
            ELSE
              PRINT     *N,*9,VAR;
            ENDIF
          ENDIF
          SFORMAT   VAR,127
.
          PACK      KEY20,VSMDVISN,VSMDTYPE,VSMDNOTE,SP70
          CALL      RSVSMTX1
DISNOT20  CALL      RKVSMTX1
          BRANCH    OVRCD,DISNOT10
.
          MATCH     VSMDVISN,VSMTVISN
          GOTO      DISNOT10 IF NOT EQUAL
.
          MATCH     VSMDTYPE,VSMTTYPE
          GOTO      DISNOT10 IF NOT EQUAL
.
          MATCH     VSMDNOTE,VSMTNOTE
          GOTO      DISNOT10 IF NOT EQUAL
.
          STRIP     VSMTCMNT
          MOVELPTR  VSMTCMNT,LENGTH
.
          SFORMAT   VAR,LENGTH
          MOVE      VSMTCMNT,VAR
.
          MATCH     "HDHCLN",STATNCOD       * Only print 80 chars on a line
          GOTO      DISNOT50 IF NOT EQUAL   * at HDH
.
          IF        LENGTH>80
            SFORMAT   VAR,EIGHTY
            MOVE      VSMTCMNT,VAR

            CALL      CHKPOF00              * check for Page Overflow *I-196463
            IF        PASSNO = 2
              PRINT     *N,*1,VAR;
            ENDIF
            SFORMAT   VAR,127
.
            ASSIGN    (LENGTH-EIGHTY),LENGTH
.
            BUMP     VSMTCMNT,EIGHTY
.
            SFORMAT   VAR,LENGTH
            MOVE      VSMTCMNT,VAR
          ENDIF
.
DISNOT50  CALL      CHKPOF00              * check for Page Overflow *I-196463
          IF        PASSNO = 2
            MATCH     "HDHCLN",STATNCOD       * Hawkesbury Dist.Health (HDH)
            IF        @EQUAL
              PRINT     *N,*1,VAR;
            ELSE
              PRINT     *N,*9,VAR;
            ENDIF
          ENDIF
          SFORMAT   VAR,127
.
          GOTO      DISNOT20
.
DISNOT99 RETURN
.
.*****************************************************************************
.                                   PROCS000                          *I-215117
.                          Emergency Procedure Codes
.*****************************************************************************
PROCS000  MOVE      SP10,KEY10
          MOVE      "004",KEY3              * "004" = "procedure" EMVCTYPE
.
          PACK      KEY17,EMVIADMN,KEY3,SP70
          CALL      RSEMVCD2
PROCS100  CALL      RKEMVCD2
          BRANCH    OVRCD,PROCS999
.
          MATCH     DEMVIADM,EMVCVIST
          GOTO      PROCS999 IF NOT EQUAL
          MATCH     KEY3,EMVCTYPE
          GOTO      PROCS999 IF NOT EQUAL
.
          CALL      LOKCOD00                * Lookup Code to get description
...       BRANCH    EXIT,PROCS999
.
          MOVE      SP7,KEY7
          MATCH     "000",EMVCSEQU
          IF        @EQUAL
            MOVE      "Primary",KEY7
          ENDIF
.
          CALL      CHKPOF00                * check for Page Overflow *I-196463
          IF        PASSNO = 2
            STRIP     CODEDESC
            MATCH     "HDHCLN",STATNCOD       * Hawkesbury Dist.Health (HDH)
            IF        @EQUAL
              PRINT     *N,*1,EMVCMNCD,SP1,*+,CODEDESC,SP6,KEY7;
            ELSE
              PRINT     *N,*9,EMVCMNCD,SP1,*+,CODEDESC,SP6,KEY7;
            ENDIF
          ENDIF
.
          GOTO      PROCS100
.
.
PROCS999  RETURN
.
.*****************************************************************************
.*                                 CHKPOF00                           *I-196463
.*          Page Overflow routing to count Pages for "Page n of nn"
.*
.*****************************************************************************
CHKPOF00  MOVE      ZERO,EXIT
          BRANCH    PRINTEND,CHKPOF80       * End of printing - print final tail
.
          COMPARE   THREE,LINSREQD          * Headings must be 3 or more lines
          IF        @LESS
            ADD       LINSREQD,CLNO         * this is an ordinary print line
            COMPARE   PAGEMAXL,CLNO
            GOTO      CHKPOF99 IF LESS      * Print line will fit on page
          ELSE
.                                      * a Heading has been detected - see if
.                                      * Heading + 2 lines can be printed
            ASSIGN    (CLNO + LINSREQD + 2),F3
            IF        F3 < PAGEMAXL
              ADD       LINSREQD,CLNO
              GOTO      CHKPOF99            * Heading will fit on page
            ENDIF
          ENDIF
.
.                                           * Page overflow has been detected
.
CHKPOF50  REPEAT                            * pad out the page
            IF        PASSNO = 2
              PRINT     *N;
            ENDIF
            ADD       ONE,CLNO
          UNTIL     CLNO > PAGEMAXL
.
          MOVE      SP20,DIM15A
          IF        LINSREQD < 4
            MOVE      "Continued ....",DIM15A
          ENDIF
.
          IF        PASSNO = 2
            MOVE      PAGENUMB,DIM3A        * print page break
            MOVE      PAGETOTL,DIM3B
            SQUEEZE   DIM3B
            MATCH     "FORMAT HEADER",DIM80A
            IF        @EQUAL
              MOVE      CTIMEIS,KEY5
              PRINT     *64,DIM15A,*N,*N:
                        *20,"Page ",DIM3A," of ",DIM3B," Printed ",CDATE," at ",KEY5;
              UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
              CALL      PACDATE
              MOVE      QSNAME,PACSNAME
              MOVE      QGNAME,PACGNAME
              MOVE      QTITL,PACTITLE
              MOVE      TWO,PACFRMT
              CALL      PACNAME
              STRIP     PACFNAME
              PRINT     *F,*25,*V2LON,*ULON,"EMERGENCY CLINICAL NOTES",*HOFF,*N:
                        *N,*42,*V2LON,"U/R Number  :",EMVIURNO,*N:
                        *42,"Visit Number:",EMVIADMN,*N:
                        *42,*ULON,*+,PACFNAME,*-,*HOFF,*N:
                        *42,*V2LON,QSEX,*50,CPCDATE,*HOFF,*N;
            ELSE
              PRINT     *64,DIM15A,*N,*N:
                        *35,"Page ",DIM3A," of ",DIM3B;
              PRINT     *F,*N,DIM80A,*N,*N,*N;  * new page & header line
            ENDIF
          ENDIF
.
          ADD       ONE,PAGENUMB            * now start next page
          MATCH     "FORMAT HEADER",DIM80A
          IF        @EQUAL
            ASSIGN    (LINSREQD + 5),CLNO     * 2 heading lines + any Heading
          ELSE
            ASSIGN    (LINSREQD + 2),CLNO     * 2 heading lines + any Heading
          ENDIF
.
          GOTO      CHKPOF99
.
.
.                                           * print trailer on the last page
CHKPOF80  MOVE      ZERO,EXIT
          REPEAT                            * pad out the page
            IF        PASSNO = 2
              PRINT     *N;
            ENDIF
            ADD       ONE,CLNO
          UNTIL     CLNO > PAGEMAXL
.
          IF        PASSNO = 2
            MOVE      PAGENUMB,DIM3A        * print final "Page n of nn"
            MOVE      PAGETOTL,DIM3B
            SQUEEZE   DIM3B
            MATCH     "FORMAT HEADER",DIM80A
            IF        @EQUAL
              MOVE      CTIMEIS,KEY5
              PRINT     *20,"Page ",DIM3A," of ",DIM3B," Printed ",CDATE," at ",KEY5;
            ELSE
              PRINT     *35,"Page ",DIM3A," of ",DIM3B
            ENDIF
          ENDIF
.
CHKPOF99  IF        PASSNO = 1
            MOVE      PAGENUMB,PAGETOTL
          ENDIF
          MOVE      ONE,LINSREQD
          RETURN
+
.------------------------------------------------------------
. Healthscope Print Form
.------------------------------------------------------------
HSPFP000  CALL      IBACLOCK
          PACK      CDATE,CDD,SLASH,CMM,SLASH,CCC,CYY
          CLOCK     TIME,CTIMEIS
          CALL      OPNPRINT
          MOVE      ONE,PASSNO              * Pass 1 = count, Pass 2 = print
          MOVE      ZERO,PAGETOTL
          MOVE      ZERO,PAGENUMB
          MOVE      SP70,EMSTDESC                * Site code description
          MOVE      "60",PAGEMAXL
          PACK      KEY3,EMVISITE,SP70
          MATCH     SP70,KEY3
          IF        !@EQUAL
            MOVE      "Invalid Code",EMSTDESC
            CALL      RDEMSIT1
          ENDIF
          MOVE      "HDHCLN",STATNCOD       * Hawkesbury Dist.Health (HDH) for Notes to print at *1
.
          MOVE      SP70,PTHSNAME  * DIM       50       50       4   Hospital Name
          MOVE      SP70,PTHSADD1  * DIM       35       35      54   Hospital Address line 1
          MOVE      SP70,PTHSADD2  * DIM       35       35      89   Hospital Address line 2
          MOVE      SP70,PTHSADD3  * DIM       35       35     124   Hospital Address line 3
          MOVE      SP70,PTHSADD4  * DIM       35       35     159   Hospital Address line 4
          MOVE      SP70,PTHSPCOD  * DIM       8         8     194   Hospital Post Code
          MOVE      SP70,PTHSTELE  * DIM       20       20     202   Hospital Telephone number
          MOVE      SP70,PTHSFAXN  * DIM       20       20     222   Hospital Fax Number
          READ      CONTROLF,TWO;*4,CNAME
          READ      CONTROLF,TEN3;CHADD1,CHADD2,CHPOST
          MATCH     SP70,EMSTHSNO
          IF        @EQUAL
            MOVE      CNAME,PTHSNAME
            MOVE      CHADD1,PTHSADD1
            MOVE      CHADD2,PTHSADD2
            MOVE      CHPOST,PTHSADD3
            MOVE      SP70,PTHSADD4
            MOVE      SP70,PTHSPCOD
          ELSE
            MOVE      EMSTHSNO,KEY3
            CALL      RDPTHSP1                          * hospital on file ?
            MATCH     SP70,PTHSNAME
            IF        @EQUAL
              MOVE      CNAME,PTHSNAME
            ENDIF
            MATCH     SP70,PTHSADD1
            IF        @EQUAL
              MOVE      CHADD1,PTHSADD1
              MOVE      CHADD2,PTHSADD2
              MOVE      CHPOST,PTHSADD3
              MOVE      SP70,PTHSADD4
              MOVE      SP70,PTHSPCOD
            ENDIF
          ENDIF
.
HSPFP100  MOVE      ZERO,CLNO
          MOVE      ONE,PAGENUMB
          MOVE      ZERO,PRINTEND
.
          BRANCH    PASSNO,HSPFP200         * bypass header on pass 1
.
          MOVE      PTHSNAME,KEY40
          PRINT     *25,*V2LON,*ULON,"EMERGENCY CLINICAL NOTES",*HOFF,*N:
                    *N,*V2LON,*1,KEY40:
                    *42,"U/R Number  :",EMVIURNO,*N:
                    *1,"Emergency Department",*42,"Visit Number:",EMVIADMN:
                    *HOFF,*N;
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          STRIP     PTHSADD4
          PACK      KEY40,PTHSADD4,SP1,PTHSPCOD
          MOVE      QSNAME,PACSNAME
          MOVE      QGNAME,PACGNAME
          MOVE      QTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          STRIP     PACFNAME
          PRINT     *1,*V2LON,PTHSADD1:
                    *42,*ULON,*+,PACFNAME,*-,*HOFF,*N:
                    *1,*V2LON,PTHSADD2,*42,QSEX,*50,CPCDATE,*N:
                    *1,PTHSADD3,*42,QADD1,*N:
                    *1,KEY40,*42,QSUBRB,*57,QPOST,*N,*HOFF
.
.                    *1,"Phone:",PTHSTELE," Fax:",PTHSFAXN,*HOFF,*N 
.
.                                           * set up header for following pages
          MOVE      "FORMAT HEADER",DIM80A
.
          PRINT     *N,*N,*1,*V2LON,"Referring Doctor":
                       *N,*1,"========================================================================",*HOFF,*N;
.
          PACK      KEY10,EMVIREFG,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
          ELSE
            MOVE      SP70,DOCFNAME
          ENDIF
          PRINT     *1,DOCFNAME
          PRINT     *N,*N,*1,*V2LON,"Local GP",*52,"Fax ":
                      *N,*1,"========================================================================",*HOFF,*N;
.
          PACK      KEY8,DEMVIADM,SP70
          CALL      RDEMCLI1
          IF        OVRCD=0
.
            MATCH     SP70,EMCILMCD
            GOTO      HSPFP150 IF EQUAL
.

            PACK      KEY10,EMCILMCD,SP70
            CALL      RDPMHCP1
            IF        OVRCD=0
              MOVE      PMHCTITL,FMTTITLE
              MOVE      PMHCGNAM,FMTGNAME
              MOVE      PMHCSNAM,FMTSNAME
              CALL      DOCNM000
            ELSE
              MOVE      SP70,DOCFNAME
              MOVE      SP70,PMHCFAXN
            ENDIF
          ELSE

HSPFP150  PACK       KEY8,EMVIURNO,SP70
          CALL       RDPMPX21
          IF         OVRCD=0
.
            MATCH      SP70,PMPXRHC1
            GOTO       HSPFP175 IF EQUAL
.
            PACK      KEY10,PMPXRHC1,SP70
            CALL      RDPMHCP1
            BRANCH    OVRCD,HSPFP175
.
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
.
          ELSE
HSPFP175    MOVE      SP70,DOCFNAME
            MOVE      SP70,PMHCFAXN
          ENDIF

          ENDIF
.
HSPFP200  MOVE      "22",LINSREQD           * total lines in heading
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *1,DOCFNAME,*52,PMHCFAXN:
                      *N,*N,*1,*V2LON,"Triage Notes ":
                      *N,*1,"========================================================================",*HOFF,*N:
                      *1,EMVICOM1,*N;
          ENDIF
.
          MOVE      ZERO,F1                 * TRIAGE: information
          REPEAT
            ADD       ONE,F1
            LOAD      DIM50A,F1,EMVICOM2,EMVICOM3,EMVICOM4,EMVICOM5,EMVICOM6
            MATCH     SP70,DIM50A
            IF        !@EQUAL
              MOVE      "1",LINSREQD
              CALL      CHKPOF00            * check for Page Overflow
              IF        PASSNO = 2
                PRINT     *1,DIM50A,*N;
              ENDIF
            ENDIF
          UNTIL     F1 = 5
.
          PACK      KEY5,CODEAA,EMVITRIG
          CALL      RDCODE1                 * code on file ?
          MOVE      "2",LINSREQD
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *N,*1,*V2LON,"Triage Category : ",*HOFF,*19,TDESC,*N;
          ENDIF
.
          UNPACK    EMVIDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      "1",LINSREQD
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *1,*V2LON,"Attendance Date : ",*HOFF,*19,CPCDATE:
                      *32,*V2LON,"Time : ",*HOFF,*40,EMVITIME,*N;
          ENDIF
.
          CALL      DOCSEN00                * "Seen By Doctor/By Date" routine
.
          MOVE      "3",LINSREQD
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *N,*N,*V2LON,*1,"Past History":
                      *N,*1,"========================================================================",*HOFF;
          ENDIF
          CALL      PASHIS00                * "Medical History" routine
.
          MOVE      "3",LINSREQD
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *N,*N,*V2LON,*1,"Clinical Notes ":
                      *N,*1,"========================================================================",*HOFF;
          ENDIF
          CALL      DISNOT00
.
          MOVE      "3",LINSREQD
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *N,*N,*V2LON,*1,"Emergency Diagnosis ":
                      *N,*1,"========================================================================",*HOFF;
          ENDIF
          CALL      HSPDIA00                * "Emerg Diagnosis" routine
.                                                           * start   *I-215117
          MOVE      "3",LINSREQD            * Management Notes heading
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *N,*N,*V2LON,*1,"Management Notes ":
                      *N,*1,"========================================================================",*HOFF;
          ENDIF
          CALL      HSPNOT00                * "Management Notes" routine
.
          MOVE      "1",LINSREQD            * Management Notes heading
          CALL      CHKPOF00                * check for Page Overflow
          MOVE      "2",LINSREQD            * Management Notes heading
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *N,*N,*V2LON,*1,"Alerts ":
                      *N,*1,"========================================================================",*HOFF;
          ENDIF
          CALL      PRTALT00                * Alerts
.
          MOVE      ONE,PRINTEND
          CALL      CHKPOF00                * print the final tail data
.
          IF        PASSNO = 1
            MOVE      TWO,PASSNO
            GOTO      HSPFP100
          ENDIF
.
          CALL      CLSPRINT
HSPFP999  RETURN
+
.******************************************************************************
.*                                WACLN000               Called by: PRINTEMR  *
.*        WA Emergency Print Form routine.                                    *
.******************************************************************************
WACLN000  CALL      IBACLOCK
          PACK      CDATE,CDD,SLASH,CMM,SLASH,CCC,CYY
          CLOCK     TIME,CTIMEIS
          CALL      OPNPRINT
          MOVE      ONE,PASSNO              * Pass 1 = count, Pass 2 = print
          MOVE      ZERO,PAGETOTL
          MOVE      ZERO,PAGENUMB
          MOVE      SP70,EMSTDESC           * Site code description
          MOVE      "64",PAGEMAXL
          PACK      KEY3,EMVISITE,SP70
          MATCH     SP70,KEY3
          IF        !@EQUAL
            MOVE      "Invalid Code",EMSTDESC
            CALL      RDEMSIT1
          ENDIF
          MOVE      "WAPCLN",STATNCOD       * WA Health for Notes to print at *1
.
WACLN100  MOVE      ZERO,CLNO
          MOVE      ONE,PAGENUMB
          MOVE      ZERO,PRINTEND
.
          BRANCH    PASSNO,WACLN200         * bypass header on pass 1
.
          CLEAR     DIM80A
          MOVE      PAGENUMB,HDCURPAG
          MOVE      PAGETOTL,HDTOTPAG
          CALL      PRPGHD00                * new page & header lines
.
.         Set up header for the following pages
.
          MOVE      "FORMAT HEADER",DIM80A
.
WACLN200  MOVE      "17",LINSREQD           * total lines in heading
          CALL      CHKWOF00                * check for Page Overflow
.
          MOVE      ZERO,SAVNOTEN           * reset note number value
          CALL      WANOTE00                * get Clinical Notes
.
          MOVE      SAVKEY17,KEY17
          CALL      RDVSMDT1                * get last visit note details
          BRANCH    OVRCD,WACLN999
.
          PACK      KEY10,VSMDUSER,SP70
          CALL      RDWBSE1
          BRANCH    OVRCD,WACLN999
.
          MOVE      "3",LINSREQD       * note accounting details & signing line
          CALL      CHKWOF00
          IF        PASSNO = 2
            UNPACK    VSMDDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            STRIP     WBSEUID
            STRIP     WBSENAM
            PRINT     *1,CPCDATE," ",VSMDTIME," ",*+,WBSEUID,*-," ",WBSENAM:
                      *N,*N:
                      *1,"__________________________________________________":
                      *N;
          ENDIF
.
WACLN300  MOVE      ONE,PRINTEND
          CALL      CHKWOF00                * print the final tail data
.
          IF        PASSNO = 1
            MOVE      TWO,PASSNO            * now do the printing
            GOTO      WACLN100
          ENDIF
.
          CALL      CLSPRINT
WACLN999  RETURN
+
.******************************************************************************
.*                                PRPGHD00               Called by: WACLN000  *
.*        Prints a new page with header lines.                                *
.*                                                                            *
.*        Parameters:                                                         *
.*                  HDCURPAG  -   Current page                                *
.*                  HDTOTPAG  -   Total number of pages                       *
.******************************************************************************
PRPGHD00  UNPACK    EMVIDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      HDCURPAG,DIM3A
          MOVE      HDTOTPAG,DIM3B
          SQUEEZE   DIM3A
          SQUEEZE   DIM3B
          MOVE      EMVIURNO,PRINTURN
          MOVE      EMVIADMN,PRINTADM
          SQUEEZE   PRINTURN
          SQUEEZE   PRINTADM
          PRINT     *F,*1,*HOFF,"EDCDATTNOTES",*N:
                    *1,"Page ",*+,DIM3A,*-," of ",DIM3B,*N,*N:
                    *1,"U/R Number        : ",PRINTURN,*N:
                    *1,"Visit Number      : ",PRINTADM,*N:
                    *1,"Visit Date        : ",CPCDATE," ",EMVITIME,*N;
.
.         Patient is known?
.
          PACK      KEY8,EMVIADMN,SP70
          CALL      RDEMUNK1
          BRANCH    OVRCD,PRPGHD10
.
.         Unknown patient...
.
          MOVE      EMUNDET1,DISPSURN
          MOVE      EMUNDET2,DISPFRST
          MOVE      SP70,DISPSECN
          UNPACK    EMUNBDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      EMUNSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
          MOVE      DSEXDE13,DISPSEX
          UNPACK    SP100,PADD1,PADD2
          UNPACK    SP100,PSUBRB,PADD4,PPOST
          GOTO      PRPGHD20
.
.         UR has been allocated...
.
PRPGHD10  MOVE      PTMASNAM,DISPSURN
          MOVE      PMPXFNAM,DISPFRST
          MOVE      PMPXSNAM,DISPSECN
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      QSEX,DSEXCHAR
          CALL      SEXDSC00                * get sex description (in IBACOMN)
          MOVE      DSEXDE13,DISPSEX
.
PRPGHD20  PRINT     *1,"Surname           : ",DISPSURN,*N:
                    *1,"First Given Name  : ",DISPFRST,*N: 
                    *1,"Second Given Name : ",DISPSECN,*N:
                    *1,"Date of Birth     : ",CPCDATE,*N:
                    *1,"Sex               : ",DISPSEX,*N;
.
.         N.B. If we don't want to print the address lines below for 0 U/R
.         patients then we have to adjust the LINSREQD value for the page
.         heading (to be fewer than 17 lines) so that CHKWOF00 prints 
.         the page footer at the correct line number...
.
.         MATCH     "0",PRINTURN
.         IF        !@EQUAL 
            PRINT     *1,PADD1,*N,PADD2,*N,PSUBRB,*N,PADD4,*N,PPOST,*N;
.         ENDIF
          PRINT     *1,"________________________________________________________________________________",*N;

PRPGHD99  RETURN
+
.******************************************************************************
.*                                WANOTE00               Called by: WACLN000  *
.*        Clinical Notes (WA) from VISMTXFD.                                  *
.******************************************************************************
WANOTE00  PACK      KEY20,EMVIADMN,ZERO,ZERO,ONE,SP70      * Clinical notes
          CALL      RSVSMTX1                * position in file ?
WANOTE10  CALL      RKVSMTX1                * read next record
          BRANCH    OVRCD,WANOTE99          * eof - field blank
.
          MATCH     DEMVIADM,VSMTVISN       * same visit still ?
          GOTO      WANOTE99 IF NOT EQUAL
.
          MATCH     "001",VSMTTYPE
          GOTO      WANOTE99 IF NOT EQUAL
.
          PACK      KEY17,VSMTVISN,VSMTTYPE,VSMTNOTE,SP70
          CALL      RDVSMDT1
          BRANCH    OVRCD,WANOTE99
.
          MATCH     "1",VSMDDELT            * deleted note?
          GOTO      WANOTE10 IF EQUAL       * yes; skip
.
          COMPARE   SAVNOTEN,ZERO
          GOTO      WANOTE20 IF EQUAL
.
          MOVE      VSMTNOTE,F6
          COMPARE   F6,SAVNOTEN             * different note number
          IF        !@EQUAL
            MOVE      SAVKEY17,KEY17
            CALL      RDVSMDT1              * get previous note read
            BRANCH    OVRCD,WANOTE99
.
            PACK      KEY10,VSMDUSER,SP70
            CALL      RDWBSE1
            BRANCH    OVRCD,WANOTE99
.
            MOVE      "3",LINSREQD          * accounting details & signing line
            CALL      CHKWOF00              * check for Page Overflow
.
            UNPACK    VSMDDATE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            STRIP     WBSEUID
            STRIP     WBSENAM
.
            IF        PASSNO = 2
              PRINT     *1,CPCDATE," ",VSMDTIME," ",*+,WBSEUID,*-," ",WBSENAM:
                        *N,*N:
                        *1,"__________________________________________________":
                        *N;
            ENDIF
          ENDIF
.
WANOTE20  MOVE      VSMTUNIQ,F3
          IF        F3=1
            PACK      SAVKEY17,VSMTVISN,VSMTTYPE,VSMTNOTE,SP70
            MOVE      "1",LINSREQD          * display note number
            CALL      CHKWOF00              * check for Page Overflow
            IF        PASSNO = 2
              SQUEEZE   VSMTNOTE
              PRINT     *1,"Note ",*+,VSMTNOTE,*-,":",*N;
            ENDIF
          ENDIF
.         
.         Note line...
.
          MOVE      "1",LINSREQD
          CALL      CHKWOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *1,VSMTCMNT,*N;
          ENDIF
          MOVE      VSMTNOTE,SAVNOTEN
.
          GOTO      WANOTE10
.
WANOTE99  RETURN
+
.******************************************************************************
.*                                 CHKWOF00              Called by: WACLN000  *
.*        Checks page overflow (WA) to count pages for "Page n of nn".        *
.******************************************************************************
CHKWOF00  MOVE      ZERO,EXIT
          BRANCH    PRINTEND,CHKWOF80       * End of printing - print final tail
.
          COMPARE   TEN7,LINSREQD           * Headings must be 17 or more lines
          IF        @LESS
            ADD       LINSREQD,CLNO         * this is an ordinary print line
            COMPARE   PAGEMAXL,CLNO
            GOTO      CHKWOF99 IF LESS      * Print line will fit on page
          ELSE
.
.         A Heading has been detected...
.         See if Heading + 3 lines (tail for prev page) can be printed?
.
            ASSIGN    (CLNO + LINSREQD + 3),F3
            IF        F3 < PAGEMAXL
              ADD       LINSREQD,CLNO
              GOTO      CHKWOF99            * Heading will fit on page
            ENDIF
          ENDIF
.
.         Page overflow has been detected...
.
CHKWOF50  REPEAT                            * pad out the page
            IF        PASSNO = 2
              PRINT     *N;
            ENDIF
            ADD       ONE,CLNO
          UNTIL     CLNO > PAGEMAXL
.
          MOVE      SP20,DIM15A
          IF        LINSREQD < 4
            MOVE      "Continued ....",DIM15A
          ENDIF
.
          IF        PASSNO = 2
.
.           Overflow tail 
.
            MOVE      PAGENUMB,DIM3A        * print page break
            SQUEEZE   DIM3A
            PRINT     *64,DIM15A,*N:
                      *35,"END OF PAGE ",DIM3A;
.
            ASSIGN    (PAGENUMB + 1),HDCURPAG
            MOVE      PAGETOTL,HDTOTPAG
            CALL      PRPGHD00              * new page & header lines
          ENDIF
.
          ADD       ONE,PAGENUMB            * now start next page
          ASSIGN    (LINSREQD + 17),CLNO    * add to line count (inc header)
          CLEAR     DIM80A
.
          GOTO      CHKWOF99
.
.         Print trailer on the final page...
.
CHKWOF80  MOVE      ZERO,EXIT
          REPEAT                            * pad out the page
            IF        PASSNO = 2
              PRINT     *N;
            ENDIF
            ADD       ONE,CLNO
          UNTIL     CLNO > PAGEMAXL
.
          IF        PASSNO = 2
            MOVE      PAGENUMB,DIM3A        * print final "END OF PAGE n"
            SQUEEZE   DIM3A
            PRINT     *35,"END OF PAGE ",DIM3A;
          ENDIF
.
CHKWOF99  IF        PASSNO = 1
            MOVE      PAGENUMB,PAGETOTL
          ENDIF
          MOVE      ONE,LINSREQD
          RETURN
+
.------------------------------------------------------------
. Print Notes for Medical History - Healthscope layout
.------------------------------------------------------------
PASHIS00  MOVE      EMVIURNO,OBMDURNO
          PACK      KEY14,OBMDURNO,SP70
          CALL      RSOBMDT1
PASHIS10  CALL      RKOBMDT1
          BRANCH    OVRCD,PASHIS99
          MATCH     EMVIURNO,OBMDURNO
          GOTO      PASHIS99 IF NOT EQUAL
          MATCH     "1",OBMDDELT
          GOTO      PASHIS10 IF EQUAL
.
          PACK      KEY17,EMVIURNO,OBMDNOTE,SP70
          CALL      RSOBMTX1
PASHIS20  CALL      RKOBMTX1
          BRANCH    OVRCD,PASHIS10
          MATCH     OBMTURNO,OBMDURNO
          GOTO      PASHIS10 IF NOT EQUAL              * was PASHIS99 *C-196463
          MATCH     OBMTNOTE,OBMDNOTE
          GOTO      PASHIS10 IF NOT EQUAL
.
          UNPACK    OBMTCMNT,KEY50,KEY51
          MOVE      "1",LINSREQD
          CALL      CHKPOF00             * check for Page Overflow  *I-196463
          IF        PASSNO = 0 | PASSNO = 2
            PRINT     *N,*1,KEY50;
          ENDIF
          MATCH     SP70,KEY51
          IF        !@EQUAL
            MOVE      "1",LINSREQD
            CALL      CHKPOF00           * check for Page Overflow  *I-196463
            IF        PASSNO = 0 | PASSNO = 2
              PRINT     *N,*1,KEY51;
            ENDIF
          ENDIF
          GOTO      PASHIS20
.
PASHIS99 RETURN
.------------------------------------------------------------
. Print Alerts Healthscope layout
.------------------------------------------------------------
PRTALT00  PACK      KEY16,EMVIURNO,SP70
          CALL      RSPTALR1
PRTALT10  CALL      RKPTALR1
          BRANCH    OVRCD,PRTALT99
          MATCH     PTALURNO,EMVIURNO
          GOTO      PRTALT99 IF NOT EQUAL

          PACK      KEY5,PTALCATG,SP70
          CALL      RDCODE1
          BRANCH    OVRCD,PRTALT10
          PACK      KEY21,TDESC,SP70
.
          PACK      KEY5,PTALCATG,PTALCODE
          CALL      RDCODE1
          BRANCH    OVRCD,PRTALT10
.
          MATCH     ANSP,TCINDC3
          GOTO      PRTALT10 IF NOT EQUAL   * not a printable alert
.
          MOVE      "1",LINSREQD
          CALL      CHKPOF00             * check for Page Overflow  *I-196463
          IF        PASSNO = 0 | PASSNO = 2
            PRINT     *N,*1,KEY21,TDESC;
          ENDIF
.
          PACK      KEY19,PTALURNO,PTALCATG,PTALCODE,PTALCNTR,SP70
          CALL      RSPMALN1
PRTALT15  CALL      RKPMALN1
          BRANCH    OVRCD,PRTALT10
          MATCH     PTALURNO,PMANURNO
          GOTO      PRTALT10 IF NOT EQUAL
          MATCH     PTALCATG,PMANACAT
          GOTO      PRTALT10 IF NOT EQUAL
          MATCH     PTALCODE,PMANACOD
          GOTO      PRTALT10 IF NOT EQUAL
          MATCH     PTALCNTR,PMANCNTR
          GOTO      PRTALT10 IF NOT EQUAL
          MOVE      PMANCOMM,ALRCOMM
.
          MOVE      "1",LINSREQD
          CALL      CHKPOF00
          IF        PASSNO = 0 | PASSNO = 2
            PRINT     *N,*5,PMANCOMM;
          ENDIF
          GOTO      PRTALT15
.
PRTALT99 RETURN
.------------------------------------------------------------ 
. Clinical Notes from VISMTXFD
.------------------------------------------------------------ 
HSPNOT00  PACK      KEY20,EMVIADMN,ZERO,ZERO,TWO,SP70    * clinical notes
          CALL      RSVSMTX1               * position in file ?
HSPNOT10  CALL      RKVSMTX1               * read next record
          BRANCH    OVRCD,HSPNOT99         * eof - field blank
          MATCH     DEMVIADM,VSMTVISN      * same visit still ?
          GOTO      HSPNOT99 IF NOT EQUAL
          MATCH     "002",VSMTTYPE                         
          GOTO      HSPNOT99 IF NOT EQUAL
.
          MOVE      "1",LINSREQD
          CALL      CHKPOF00             * check for Page Overflow  *I-196463
          IF        PASSNO = 0 | PASSNO = 2
            PRINT     *N,*1,VSMTCMNT;
          ENDIF
          GOTO      HSPNOT10
HSPNOT99  RETURN
.
.---------------------------------------------------------------------------
DOCSEN00  MOVE      SP70,SAVDOC
          PACK      KEY22,DEMVIADM,SP70
          CALL      RSEMHIS1
DOCSEN10  CALL      RKEMHIS1
          BRANCH    OVRCD,DOCSEN99
          MATCH     DEMVIADM,EMHIVIS
          GOTO      DOCSEN99 IF NOT EQUAL
          MATCH     EMHIDOC,SAVDOC
          GOTO      DOCSEN10 IF EQUAL
.
          PACK      KEY10,EMHIDOC,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
          ELSE
            MOVE      SP70,DOCFNAME
          ENDIF
          MOVE      "3",LINSREQD                                      *I-196463
          CALL      CHKPOF00           * check for Page Overflow      *I-196463
          IF        PASSNO = 0 | PASSNO = 2
            UNPACK    EMHIDSD,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            UNPACK    EMHIDST,HOUR,MIN,SEC
            PACK      DOCTIME,HOUR,COLON,MIN,COLON,SEC
            REP       " 0",DOCTIME
            PRINT     *1,*V2LON,"Seen By Doctor  : ",*19,DOCFNAME;    *C-50310
            PRINT     *N,*1,*V2LON,"Seen By Date    : ",*HOFF,*19,CPCDATE:
                        *32,*V2LON,"Time : ",*HOFF,*40,DOCTIME,*N;
          ENDIF
          MOVE      EMHIDOC,SAVDOC
.
          GOTO      DOCSEN10

DOCSEN99  RETURN
.-------------------------------------------------------
. Emergency Diagnosis
.-------------------------------------------------------
HSPDIA00  MOVE      ZERO,FORM1A
          PACK      KEY14,EMVIADMN,ZERO,ZERO,FIVE,SP70
          CALL      RSEMVCD1
HSPDIA10  CALL      RKEMVCD1
          BRANCH    OVRCD,HSPDIA99
.
          MATCH     DEMVIADM,EMVCVIST
          GOTO      HSPDIA99 IF NOT EQUAL
.
          MATCH     "005",EMVCTYPE
          GOTO      HSPDIA99 IF NOT EQUAL
.
          MATCH     SP70,EMVCFTXT
          IF        !@EQUAL
            UNPACK    EMVCFTXT,DIM50,DIM50A
          ELSE
            PACK      KEY9,EMVCMNCD,SP70
            CALL      RDEMICD1
            BRANCH    OVRCD,HSPDIA10
            UNPACK    EMICDESC,DIM50,DIM50A
          ENDIF
.
          MATCH     SP70,DIM50
          IF        !@EQUAL
            MOVE      "1",LINSREQD
            CALL      CHKPOF00                * check for Page Overflow
            IF        PASSNO = 2
              PRINT     *N,DIM50;
            ENDIF
          ENDIF
.
          MATCH     SP70,DIM50A
          IF        !@EQUAL
            MOVE      "1",LINSREQD
            CALL      CHKPOF00                * check for Page Overflow
            IF        PASSNO = 2
              PRINT     *N,DIM50A;
            ENDIF
          ENDIF
.
          GOTO      HSPDIA10
.
HSPDIA99  RETURN
.------------------------------------------------------------
. Healthscope Print Form
.------------------------------------------------------------
HSPF2000  CALL      IBACLOCK
          PACK      CDATE,CDD,SLASH,CMM,SLASH,CCC,CYY
          CLOCK     TIME,CTIMEIS
          CALL      OPNPRINT
          MOVE      ONE,PASSNO              * Pass 1 = count, Pass 2 = print
          MOVE      ZERO,PAGETOTL
          MOVE      ZERO,PAGENUMB
          MOVE      SP70,EMSTDESC                * Site code description
          MOVE      "60",PAGEMAXL
          PACK      KEY3,EMVISITE,SP70
          MATCH     SP70,KEY3
          IF        !@EQUAL
            MOVE      "Invalid Code",EMSTDESC
            CALL      RDEMSIT1
          ENDIF
          MOVE      "HSPCL2",STATNCOD       * Hawkesbury Dist.Health (HDH) for Notes to print at *1
.
          MOVE      SP70,PTHSNAME  * DIM       50       50       4   Hospital Name
          MOVE      SP70,PTHSADD1  * DIM       35       35      54   Hospital Address line 1
          MOVE      SP70,PTHSADD2  * DIM       35       35      89   Hospital Address line 2
          MOVE      SP70,PTHSADD3  * DIM       35       35     124   Hospital Address line 3
          MOVE      SP70,PTHSADD4  * DIM       35       35     159   Hospital Address line 4
          MOVE      SP70,PTHSPCOD  * DIM       8         8     194   Hospital Post Code
          MOVE      SP70,PTHSTELE  * DIM       20       20     202   Hospital Telephone number
          MOVE      SP70,PTHSFAXN  * DIM       20       20     222   Hospital Fax Number
          READ      CONTROLF,TWO;*4,CNAME
          READ      CONTROLF,TEN3;CHADD1,CHADD2,CHPOST
          MATCH     SP70,EMSTHSNO
          IF        @EQUAL
            MOVE      CNAME,PTHSNAME
            MOVE      CHADD1,PTHSADD1
            MOVE      CHADD2,PTHSADD2
            MOVE      CHPOST,PTHSADD3
            MOVE      SP70,PTHSADD4
            MOVE      SP70,PTHSPCOD
          ELSE
            MOVE      EMSTHSNO,KEY3
            CALL      RDPTHSP1                          * hospital on file ?
            MATCH     SP70,PTHSNAME
            IF        @EQUAL
              MOVE      CNAME,PTHSNAME
            ENDIF
            MATCH     SP70,PTHSADD1
            IF        @EQUAL
              MOVE      CHADD1,PTHSADD1
              MOVE      CHADD2,PTHSADD2
              MOVE      CHPOST,PTHSADD3
              MOVE      SP70,PTHSADD4
              MOVE      SP70,PTHSPCOD
            ENDIF
          ENDIF
.
HSPF2100  MOVE      ZERO,CLNO
          MOVE      ONE,PAGENUMB
          MOVE      ZERO,PRINTEND
.
          BRANCH    PASSNO,HSPF2200         * bypass header on pass 1
.
          MOVE      PTHSNAME,KEY40
          PRINT     *25,*V2LON,*ULON,"EMERGENCY CLINICAL NOTES",*HOFF,*N:
                    *N,*V2LON,*1,KEY40:
                    *42,"U/R Number  :",EMVIURNO,*N:
                    *1,"Emergency Department",*42,"Visit Number:",EMVIADMN:
                    *HOFF,*N;
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          STRIP     PTHSADD4
          PACK      KEY40,PTHSADD4,SP1,PTHSPCOD
          MOVE      QSNAME,PACSNAME
          MOVE      QGNAME,PACGNAME
          MOVE      QTITL,PACTITLE
          MOVE      TWO,PACFRMT
          CALL      PACNAME
          STRIP     PACFNAME
          PRINT     *1,*V2LON,PTHSADD1:
                    *42,*ULON,*+,PACFNAME,*-,*HOFF,*N:
                    *1,*V2LON,PTHSADD2,*42,QSEX,*50,CPCDATE,*N:
                    *1,PTHSADD3,*42,QADD1,*N:
                    *1,KEY40,*42,QSUBRB,*57,QPOST,*N,*HOFF
.
.                    *1,"Phone:",PTHSTELE," Fax:",PTHSFAXN,*HOFF,*N
.
.                                           * set up header for following pages
          MOVE      "FORMAT HEADER",DIM80A
.
          PRINT     *N,*N,*1,*V2LON,"Referring Doctor":
                       *N,*1,"========================================================================",*HOFF,*N;
.
          PACK      KEY10,EMVIREFG,SP70
          CALL      RDPMHCP1
          IF        OVRCD=0
            MOVE      PMHCTITL,FMTTITLE
            MOVE      PMHCGNAM,FMTGNAME
            MOVE      PMHCSNAM,FMTSNAME
            CALL      DOCNM000
          ELSE
            MOVE      SP70,DOCFNAME
          ENDIF
          PRINT     *1,DOCFNAME
          PRINT     *N,*N,*1,*V2LON,"Local GP",*52,"Fax ":
                      *N,*1,"========================================================================",*HOFF,*N;
.
          PACK      KEY8,DEMVIADM,SP70
          CALL      RDEMCLI1
          IF        OVRCD=0
            PACK      KEY10,EMCILMCD,SP70
            CALL      RDPMHCP1
            IF        OVRCD=0
              MOVE      PMHCTITL,FMTTITLE
              MOVE      PMHCGNAM,FMTGNAME
              MOVE      PMHCSNAM,FMTSNAME
              CALL      DOCNM000
            ELSE
              MOVE      SP70,DOCFNAME
              MOVE      SP70,PMHCFAXN
            ENDIF
          ENDIF
.
.
HSPF2200  MOVE      "22",LINSREQD           * total lines in heading
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *1,DOCFNAME,*52,PMHCFAXN:
                      *N,*N,*1,*V2LON,"Triage Notes ":
                      *N,*1,"========================================================================",*HOFF,*N:
                      *1,EMVICOM1,*N;
          ENDIF
.
          MOVE      ZERO,F1                 * TRIAGE: information
          REPEAT
            ADD       ONE,F1
            LOAD      DIM50A,F1,EMVICOM2,EMVICOM3,EMVICOM4,EMVICOM5,EMVICOM6
            MATCH     SP70,DIM50A
            IF        !@EQUAL
              MOVE      "1",LINSREQD
              CALL      CHKPOF00            * check for Page Overflow
              IF        PASSNO = 2
                PRINT     *1,DIM50A,*N;
              ENDIF
            ENDIF
          UNTIL     F1 = 5
.
          PACK      KEY5,CODEAA,EMVITRIG
          CALL      RDCODE1                 * code on file ?
          MOVE      "2",LINSREQD
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *N,*1,*V2LON,"Triage Category : ",*HOFF,*19,TDESC,*N;
          ENDIF
.
          UNPACK    EMVIDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      "1",LINSREQD
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *1,*V2LON,"Attendance Date : ",*HOFF,*19,CPCDATE:
                      *32,*V2LON,"Time : ",*HOFF,*40,EMVITIME,*N;
          ENDIF
.
          CALL      DOCSEN00                * "Seen By Doctor/By Date" routine
.
          MOVE      "3",LINSREQD
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *N,*N,*V2LON,*1,"Past History":
                      *N,*1,"========================================================================",*HOFF;
          ENDIF
          CALL      PASHIS00                * "Medical History" routine
.
          MOVE      "3",LINSREQD
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *N,*N,*V2LON,*1,"Clinical Notes ":
                      *N,*1,"========================================================================",*HOFF;
          ENDIF
          CALL      DISNT200
.
          MOVE      "3",LINSREQD
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *N,*N,*V2LON,*1,"Emergency Diagnosis ":
                      *N,*1,"========================================================================",*HOFF;
          ENDIF
          CALL      HSPDIA00                * "Emerg Diagnosis" routine
.                                                           * start   *I-215117
          MOVE      "3",LINSREQD            * Management Notes heading
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *N,*N,*V2LON,*1,"Management Notes ":
                      *N,*1,"========================================================================",*HOFF;
          ENDIF
          CALL      HSPNOT00                * "Management Notes" routine
.
          MOVE      "1",LINSREQD            * Management Notes heading
          CALL      CHKPOF00                * check for Page Overflow
          MOVE      "2",LINSREQD            * Management Notes heading
          CALL      CHKPOF00                * check for Page Overflow
          IF        PASSNO = 2
            PRINT     *N,*N,*V2LON,*1,"Alerts ":
                      *N,*1,"========================================================================",*HOFF;
          ENDIF
          CALL      PRTALT00                * Alerts
.
          MOVE      ONE,PRINTEND
          CALL      CHKPOF00                * print the final tail data
.
          IF        PASSNO = 1
            MOVE      TWO,PASSNO
            GOTO      HSPF2100
          ENDIF
.
          CALL      CLSPRINT
HSPF2999  RETURN
+
.------------------------------------------------------------
. Clinical Notes from PMSMTXFD with Date and Time
.------------------------------------------------------------
DISNT200  PACK      KEY14,EMVIURNO,SP70
          CALL      RSPMMDT1
DISNT210  CALL      RKPMMDT1
          BRANCH    OVRCD,DISNT299
.
          MATCH     EMVIURNO,PMMDURNO
          GOTO      DISNT299 IF NOT EQUAL
.
          MATCH     "1",PMMDDELT
          GOTO      DISNT210 IF EQUAL
.
          MOVE      SP70,TEMPDATE
          MATCH     SP70,PMMDDATE
          IF        !@EQUAL
            UNPACK    PMMDDATE,CCENT,CYEAR,CMON,CDAY
            MOVE      CMON,F2
            LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
            PACK      TEMPDATE,CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR
          ENDIF
.
          CLEAR     VISNOTES
          APPEND    TEMPDATE,VISNOTES
          APPEND    "  ",VISNOTES
          APPEND    PMMDTIME,VISNOTES
          APPEND    "  ",VISNOTES
          APPEND    PMMDUSER,VISNOTES
          APPEND    "  ",VISNOTES
          APPEND    PMMDOCCG,VISNOTES
          RESET     VISNOTES
.
          STRIP     VISNOTES
          MOVELPTR  VISNOTES,LENGTH
.
          SFORMAT   VAR,LENGTH
          MOVE      VISNOTES,VAR
.
          CALL      CHKPOF00                * check for Page Overflow *I-196463
          IF        PASSNO = 2
            PRINT     *N,*1,VAR;
          ENDIF
          SFORMAT   VAR,127
.
          PACK      KEY17,PMMDURNO,PMMDNOTE,SP70
          CALL      RSPMMTX1
DISNT220  CALL      RKPMMTX1
          BRANCH    OVRCD,DISNT210
.
          MATCH     PMMTURNO,PMMDURNO
          GOTO      DISNT210 IF NOT EQUAL
.
          MATCH     PMMTNOTE,PMMDNOTE
          GOTO      DISNT210 IF NOT EQUAL
.
          STRIP     PMMTCMNT
          MOVELPTR  PMMTCMNT,LENGTH
.
          SFORMAT   VAR,LENGTH
          MOVE      PMMTCMNT,VAR
.
DISNT250  CALL      CHKPOF00              * check for Page Overflow *I-196463
          IF        PASSNO = 2
            PRINT     *N,*1,VAR;
          ENDIF
          SFORMAT   VAR,127
.
          GOTO      DISNT220
.
DISNT299 RETURN
.------------------------------------------------------------
. Print Form
.------------------------------------------------------------
BHSCLIN0  CALL      OPNPRINT
.
         PRINT    *9,"Ballarat Health Services",*50,"U/R Number  :",EMVIURNO,*N:
                  *9,"Emergency Department",*50,"Visit Number:",EMVIADMN,*N:
                  *9,"Cnr Mair St and Drummond St North";
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          PRINT     *50,QSNAME,*65,QGNAME,*N:
                    *9,"Ballarat VIC 3350",*50,QSEX,*58,CPCDATE,*N:
                    *9,"Phone: 5320-4747 or 5320-4396",*50,QADD2,*N:
                    *9,"Fax: 5320-4826",*50,QSUBRB,*65,QPOST,*N:
                    *50,"Ph : ",*56,QTELEP,*N
.
          PRINT     *N:
                    *N:
                    *9,"Referring Doctors",*N:
                    *9,"=================",*N;
                    PACK      KEY10,PMPXRHC1,SP70
                    CALL      RDPMHCP1
                    IF        OVRCD=0
                      MOVE      PMHCTITL,FMTTITLE
                      MOVE      PMHCGNAM,FMTGNAME
                      MOVE      PMHCSNAM,FMTSNAME
                      CALL      DOCNM000
                    ELSE
                      MOVE      SP70,DOCFNAME
                    ENDIF
          PRINT     *9,DOCFNAME,*N:
                    *N:
                    *9,"TRIAGE:",*N:
                    *N:
                    *9,"Comments        : ",*27,EMVICOM1,*N;           *C-50310
                    MATCH   SP70,EMVICOM2
                    IF      !@EQUAL
          PRINT     *27,EMVICOM2,*N;                                   *C-50310
                    ENDIF
                    MATCH   SP70,EMVICOM3
                    IF      !@EQUAL
          PRINT     *27,EMVICOM3,*N;                                   *C-50310
                    ENDIF
                    MATCH   SP70,EMVICOM4
                    IF      !@EQUAL
          PRINT     *27,EMVICOM4,*N;                                   *C-50310
                    ENDIF
                    MATCH   SP70,EMVICOM5
                    IF      !@EQUAL
          PRINT     *27,EMVICOM5,*N;                                   *C-50310
                    ENDIF
                    MATCH   SP70,EMVICOM6
                    IF      !@EQUAL
          PRINT     *27,EMVICOM6,*N;                                   *C-50310
                    ENDIF
                    PACK      KEY5,CODEAA,EMVITRIG
                    CALL      RDCODE1                      * code on file ?
          PRINT     *9,"Triage Category : ",*27,TDESC,*N;              *C-50310
                    UNPACK    EMVIDATE,CCENT,CYEAR,CMON,CDAY
                    CALL      PACDATE
          PRINT     *9,"Attendance Date : ",*27,CPCDATE:               *C-50310
                    *40,"Time : ",*48,EMVITIME,*N;
.                    UNPACK    EMVIDRDT,CCENT,CYEAR,CMON,CDAY
.                    CALL      PACDATE
.         PRINT      *9,"Seen By Date    : ",*27,CPCDATE:              *C-50310
.                    *40,"Time : ",*48,EMVIDRTM,*N;
                    CALL      SEENDC00
         PRINT      *N:
                    *9,"Patient Notes",*N:
                    *9,"=============",*N;
                    CALL   DISPNT00
.         PRINT     *N:                                                *D-50310
.                   *9,"Clinical Notes",*N:
.                   *9,"=============",*N;
.                   CALL   CLINOT00
          PRINT     *N,*N:                                             *I-50310
                    *9,"Management Notes",*N:
                    *9,"=============",*N;
                    CALL   MANNOT00
          PRINT     *N,*N:
                    *9,"Emerg Diagnosis",*N:
                    *9,"===============",*N:
                    *N;
                    CALL   CLINOT00
.
          PRINT     *N,*N:                                             *I-50310
                    *9,"Clinical Notes",*N:
                    *9,"=============",*N;
                    CALL   CLINTX00
.
          PRINT     *N,*N:
                    *9,"Doctor Notes",*N:
                    *9,"=============",*N;
                    CALL   DOCTNT00
.
                    PACK      KEY10,EMVIDOCT,SP70
                    CALL      RDPMHCP1
                    IF        OVRCD=0
                      MOVE      PMHCTITL,FMTTITLE
                      MOVE      PMHCGNAM,FMTGNAME
                      MOVE      PMHCSNAM,FMTSNAME
                      CALL      DOCNM000
                    ELSE
                      MOVE      SP70,DOCFNAME
                    ENDIF

          PRINT     *N:
                    *50,"Doctor  : ",*60,DOCFNAME                      *C-50310
.
          PRINT     *N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N;
.
          CALL      CLSPRINT
BHSCLIN9  RETURN

