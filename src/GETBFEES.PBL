GETBFEES  
. ******************************************************************************
.* System         : Patient Billing                                           *
.* Include Name   : GETBFEES.PBL                                              *
.* Author         : J.Tan                                                     *
.* Date           : 02/12/2010                                                *
.* Function       : Routine to get a valid bed fees                           *
.*                                                                            *
.* Mods           : V10.08.01 24/08/2016 J.Tan   TSK 0321532                  *
.*                  Mods to use variable F3 instead of COUNT                  *
.*                                                                            *
.* Requires       : KEY27 - key for bed fees record                           *
.*                  TOTDAYS - Number of days                                  *
.*                  ASTAT - Admission Status                                  *
.*                  ADATE - Admission Date                                    *
.*                  DDATE - Discharge Date                                    *
.*                  CNTRCEFR - Contract Effective date details                *
.*                  CEFFDATE - Effective date calculation                     *
.*                                                                            *
.* Returns        : CONTRCID - Contract ID                                    *
.*                  BFPAT - Patient Portion                                   *
.*                  BFHF  - Health fund Portion                               *
.*                  EXIT  - 0 Found valid bed fees                            *
.*                        - 1 No valid bed fees                               *
.*                        - 2 Default record found                            *
. ******************************************************************************
GETF0000  OPEN      PATBFEE1,"patbfeef"
          OPEN      PMSCIDA1,"pmscidaf"
.
          MOVE      ZERO,F2
          MOVE      ZERO,F3
          UNPACK    KEY27,SBFCLM,SBFHFUND,SBFHFTAB,SBFATYPE,SBFBTYPE
          PACK      DIM23 WITH SBFCLM,SBFHFUND,SBFHFTAB,SBFATYPE,SBFBTYPE
.
          COMPARE   NINE,CFEETYP
          GOTO      GETF2000 IF EQUAL        * Johns Hopkins 
.
          COMPARE   THREE,CNTRCEFR
          GOTO      GETF6400 IF EQUAL        * Invalid Contract Effective detail
.
          MOVE      SP70,CONTRCID
          MOVE      SP70,PTHFBCAT
          PACK      KEY14,SBFHFUND,SBFHFTAB
          CALL      RDFUND1
          PACK      KEY18,SBFCLM,SBFHFUND,PTHFBCAT,SBFATYPE,SBFBTYPE,SP70
.
GETF1000  PACK      KEY27,KEY18,SP30
          CALL      RDSBFEE1                * Position on & read the bed fee
GETF1200  CALL      RDKBFEE1                  file
          BRANCH    OVRCD,GETF6200
.
          PACK      DIM18,BFCLM,BFHFUND,PTBFHTYP,BFATYPE,BFBTYPE,SP70
          MATCH     KEY18,DIM18
          GOTO      GETF6200 IF NOT EQUAL
.
          MATCH     SP70,CONTRCID
          GOTO      GETF1400 IF NOT EQUAL
.
          MOVE      PTBFCNID,KEY6         * Contract ID
          CALL      VALCONTR              * Validate the Contract ID
          BRANCH    EXIT,GETF1200         * not valid
          MOVE      PTBFCNID,CONTRCID     * Contract ID
          GOTO      GETF3000
.
GETF1400  MATCH     PTBFCNID,CONTRCID
          GOTO      GETF6200 IF NOT EQUAL
          GOTO      GETF3000
.
.         Get bed fees for Johns Hopkins Singapore Bed fees
.
GETF2000  OPEN      JHSBFEA1,"jhsbfeaf"
          PACK      KEY26 WITH DIM23,SP70
          CALL      RSJHBFE1
GETF2200  CALL      RKJHBFE1
          BRANCH    OVRCD,GETF6200
.
          PACK      DIM23A WITH BFCLM,BFHFUND,BFHFTAB,BFATYPE,BFBTYPE
          MATCH     DIM23,DIM23A
          GOTO      GETF6200 IF NOT EQUAL
.
GETF3000  ADD       ONE,F3
.
.         For a daycase patient, BFENDDY should be zero
.
          COMPARE   THREE,ASTAT
          GOTO      GETF4000 IF NOT EQUAL
          MATCH     ADATE,DDATE
          GOTO      GETF4000 IF NOT EQUAL
          COMPARE   ZERO,BFENDDY
          GOTO      GETF6200 IF NOT EQUAL
          GOTO      GETF8000
.
GETF4000  COMPARE   ZERO,TOTDAYS
          GOTO      GETF5000 IF NOT EQUAL
.
          COMPARE   ZERO,BFENDDY
          GOTO      GETF6000 IF EQUAL
          GOTO      GETF8000
.
GETF5000  COMPARE   TOTDAYS,BFENDDY
          GOTO      GETF6000 IF LESS
          GOTO      GETF8000
.
.         Save bed fees
.
GETF6000  MOVE      BFPAT,SBFPAT
          MOVE      BFHF,SBFHF
          MOVE      BFENDDY,SBFENDDY
          GOTO      GETF1200
.
.         Use default bed fees
.
GETF6200  COMPARE   ZERO,F3
          GOTO      GETF6600 IF NOT EQUAL
.
          ADD       ONE,F2
          BRANCH    F2,GETF7000,GETF7200
GETF6400  MOVE      ONE,EXIT
          GOTO      GETF6800
.
GETF6600  MOVE      TWO,EXIT                 * Found misc.item but invalid
.
GETF6800  MOVE      ZERO,SBFPAT
          MOVE      ZERO,SBFHF
          MOVE      ZERO,SBFENDDY
          MOVE      SP70,CONTRCID
          GOTO      GETF9999
.
GETF7000  PACK      DIM23,SBFCLM,SP6,SP8,SBFATYPE,SBFBTYPE,SP70
          PACK      KEY18,SBFCLM,SP6,SP3,SBFATYPE,SBFBTYPE,SP70
          COMPARE   NINE,CFEETYP
          GOTO      GETF2000 IF EQUAL         * JH Bed fees
          GOTO      GETF1000
.
GETF7200  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY;*193,PTCNDCLM
          CALL      FCLM0000        * Check Hospital claim code charging
          PACK      DIM23,PTCNDCLM,SP6,SP8,SBFATYPE,SBFBTYPE,SP70
          PACK      KEY18,PTCNDCLM,SP6,SP3,SBFATYPE,SBFBTYPE,SP70
          COMPARE   NINE,CFEETYP
          GOTO      GETF2000 IF EQUAL         * JH Bed fees
          GOTO      GETF1000
.
GETF8000  MOVE      BFPAT,SBFPAT
          MOVE      BFHF,SBFHF
          MOVE      BFENDDY,SBFENDDY
          MOVE      ZERO,EXIT
GETF9999  RETURN
+
.*******************************************************************************
.         Get the default charging claim code for multi hospital
.*******************************************************************************
FCLM0000  IF        IBCNMHOS=1
            OPEN      PATHSPA1,"pathspaf"
            MOVE      PMVXMHOS,KEY3
            CALL      RDPTHSP1
            IF        OVRCD=0
              MOVE      PTHSDCLM,PTCNDCLM     * default claim code charging
            ENDIF
          ENDIF
          MATCH     SP70,PTCNDCLM
          IF        @EQUAL
            MOVE      "0  ",PTCNDCLM          * Use zero claim code
          ENDIF
FCLM9999  RETURN
+
.*******************************************************************************
          INC       VALCONTR                 * Validate Contract ID
+
