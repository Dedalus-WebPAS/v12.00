.***************************************************************************
.* System    :   Patient Management System                                 *
.* Program   :   CONXVEMD                                                  *
.* Desc      :   Delete data loaded for the VEMD Conversion                *
.***************************************************************************
.* Date      :   27/06/2001                                                *
.* Author    :   Mike Laming                                               *
.* Function  :   This program will read the conversion file convvemd.txt   *
.*               to delete all data loaded to EMRVISFD, EMRVDGFD and       *
.*               EMRVPRFD as part of the VEMD Conversion.                  *
.* Mods      :                                                             *
.*               V9.04.01 01/06/2005 Peter Vela CAR 60166                  *
.*                        Recompiled for EMRCONFD                          *
.*                                                                         *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
          INC       EMRCONFD/INC                 * EMR Control file
          INC       EMRPROFD/INC                 * Emergency Procedure codes
          INC       EMRVISFD/INC                 * Visit Details
          INC       EMRVDGFD/INC                 * Visit Diagnosis Details
          INC       EMRVPRFD/INC                 * Visit Procedure Details
          INC       PATCODFD/INC
          INC       PATMA1FD/INC                 * Patient Master
.
.
.Layout of convvemd.txt
.---------------------
VEMDPD   FILE      ASCII, FIXED=285
.
.Name     Type    Length  Pos  Description
.----     ----    ------  ---  -----------
XESTID    DIM       3     1    Site Identifier
XVISTNUM  DIM       8     4    Visit Number
XPATNTID  DIM       10    12   Patient Identifier
XMEDINUM  DIM       10    22   Medicare Number
XMEDICOD  DIM       2     32   Medicare Suffix
XPATSEX   DIM       1     34   Sex
XBRTHDAT  DIM       8     35   Date of Birth
XBPLACE   DIM       4     43   Country of Birth
XABORIG   DIM       1     47   Indigenous Status
XPRELANG  DIM       2     48   Preferred Language
XSUBURB   DIM       20    50   Locality	
XPOSTCOD  DIM       4     70   Post code
XARRMODE  DIM       2     74   Arrival Transport Mode (Cat EA)
XREFDBY   DIM       2     76   Referred by
XTSOURCE  DIM       3     78   Transfer Source (Cat S)
XVISTYP   DIM       2     81   Type of Visit
XCOMPST   DIM       1     83   Compensable Status (Cat CL)
XAMBCASE  DIM       6     84   Ambulance Case Number
XARRDATE  DIM       8     90   Arrival Date
XARRTIME  DIM       4     98   Arrival Time
XTRIDATE  DIM       8     102  Triage Date
XTRITIME  DIM       4     110  Triage Time
XTRIGCAT  DIM       1     114  Triage Category
XNURSDAT  DIM       8     115  First Seen By Treating Nurse Date
XNURSTIM  DIM       4     123  First Seen By Treating Nurse Time
XDOCTDAT  DIM       8     127  First Seen By Treating Doctor Date
XDOCTTIM  DIM       4     135  First Seen By Treating Doctor Time
XPROCDUR  DIM       2     139  Procedures
XDEPDATE  DIM       8     141  Departure Date
XDEPTIME  DIM       4     149  Departure Time
XDEPSTAT  DIM       1     153  Departure Status (Cat ED)
XTDESTN   DIM       3     154  Transfer Destination
XREFDEPT  DIM       2     157  referred to on Departure
XTREASON  DIM       1     159  Reason for Transfer
XESCORT   DIM       1     160  Escort Source
XDEPTMOD  DIM       2     161  Departure Transport Mode
XPRIDIAG  DIM       6     163  Primary Diagnosis
XADDDIAG  DIM       6     169  Additional Diagnosis 1
XINJURY   DIM       2     175  Nature of Main Injury
XBREGION  DIM       2     177  Body Region
XINJEVNT  DIM       100   179  Description of Injury Event
XINJCAUS  DIM       2     279  Injury Cause
XHINTENT  DIM       2     281  Human Intent
XINJPLAC  DIM       1     283  Place Where Injury Occurred (Cat AP)
XINJACTV  DIM       1     284  Activity When Injured
.        End of record    285
.
BYPVEMD   FORM      1
COUNT     FORM      6
COUNT1    FORM      3
COUNTIN   FORM      7                                                  *I-90201
COUNTR    FORM      3
CONTVIS   FORM      6
CONTVPR   FORM      6
CONTVDG   FORM      6
DATAERR   FORM      1                                                  *I-90202
DISPVAR   FORM      6
UPDTVIS   FORM      1
.
CH02      DIM       2
CH04      DIM       4
CODE      DIM       2
CURDTE    DIM       8
DIM8      DIM       8
LINNO     DIM       3
PRTPT2    DIM       92
.                    ....*....1....*....2....*....3....*....4..
SP127     INIT      "                                          ":
                    "                                          ":
                    "                                           "
XPURNO    DIM       8
.
PRGID     INIT      "CONXVEMD"
PRGNAM    INIT      "Delete VEMD data from EMR Visit Details"
VERSION   INIT      "V12.00.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and check files
          BRANCH    EXIT,ML9999            * if conversion files don't exist
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
.
          CALL      OPEN0000               * Open file
          CALL      PROC0000               * process the details
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and check files. *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      VEMDPD,"convvemd"
          MOVE      OVRCD,BYPVEMD
          TRAPCLR   IO
.
          MOVE      ZERO,EXIT
.                                           * If no conversion files exist
          IF        BYPVEMD=1
            DISPLAY   *P1:23,*EL,*B,"File convvemd.txt does not exist ":
                      "- Process Terminated":  
                      *P1:24,"Hit <",*V2LON,"ENTER",*HOFF,"> to continue ";
            KEYIN     ANS;
            MOVE      ONE,EXIT
          ENDIF 
.
          PACK      CURDTE,CCC,CYY,CMM,CDD  * Set up the current date for use
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.****************************************************************************

OPTN0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:4,"This program deletes VEMD conversion data":
                    "from the EMR Tables";
          KEYIN     *P1:24,*EL,"Ok to Continue (",*V2LON,"Y",*HOFF,"/":
                    *V2LON,"N",*HOFF,") ? ",*V2LON,ANS;
.
          REP       UPPLOW,ANS
          MATCH     ANSY,ANS
          GOTO      OPTN9000 IF EQUAL
.
          MATCH     ANSN,ANS
          GOTO      OPTN9500 IF EQUAL
.
          BEEP
          GOTO      OPTN0000
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
OPTN9999  RETURN
+
+
.**************************************************************************
.*                               OPEN0000              Called by: ML0000  *
.*                              Open Files                                *
.**************************************************************************
.
OPEN0000  MOVE      ZERO,OVRCD                   * 
.
          DISPLAY   *P56:24,*EL,"Opening ":
                    *P64:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
.                                                * Visit Details
          DISPLAY   *P64:24,"emrvisaf";
          OPEN      EMRVISA1,"emrvisaf"
.                                                * Visit Diagnosis details
          DISPLAY   *P64:24,"emrvdgaf";
          OPEN      EMRVDGA1,"emrvdgaf"
.                                                * Visit Procedures details
          DISPLAY   *P64:24,"emrvpraf";
          OPEN      EMRVPRA1,"emrvpraf"
.                                                * Emergency Procedure Codes
          DISPLAY   *P64:24,"emrproaf";
          OPEN      EMRPROA1,"emrproaf"
.                                                * Codes Table
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"controlf";
          OPEN      CONTROLF,"controlf"
.
OPEN9999  RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                     Process the conversion files                       *
.**************************************************************************
.
.
PROC0000  CLOCK     TIME,CTIMEIS
          MOVE      ZERO,CPAGENO
          MOVE      ONE,CNOUNDLN
          MOVE      ZERO,COUNTIN                                       *I-90201
          MOVE      ZERO,CONTVIS
          MOVE      ZERO,CONTVDG
          MOVE      ZERO,CONTVPR
.
          MOVE      ONE,COUNT
          DISPLAY   *P1:14,"* Processing ",*V2LON,"convvemd.txt ",*HOFF," *":
                    *P1:16,"Started : ",*V2LON,CTIMEIS,*HOFF:
                    *P1:18,*EL,"Records    : ";

PROC1000  MOVE      ZERO,OVRCD
          MOVE      ZERO,DATAERR                                       *I-90202
          TRAP      FERR0000 IF FORMAT                                 *I-90202
.
          READ      VEMDPD,SEQ;XESTID,XVISTNUM,XPATNTID,XMEDINUM,XMEDICOD:
                               XPATSEX,XBRTHDAT,XBPLACE,XABORIG,XPRELANG:
                               XSUBURB,XPOSTCOD,XARRMODE,XREFDBY,XTSOURCE:
                               XVISTYP,XCOMPST,XAMBCASE,XARRDATE,XARRTIME:
                               XTRIDATE,XTRITIME,XTRIGCAT,XNURSDAT,XNURSTIM:
                               XDOCTDAT,XDOCTTIM,XPROCDUR,XDEPDATE,XDEPTIME:
                               XDEPSTAT,XTDESTN,XREFDEPT,XTREASON,XESCORT:
                               XDEPTMOD,XPRIDIAG,XADDDIAG,XINJURY,XBREGION:
                               XINJEVNT,XINJCAUS,XHINTENT,XINJPLAC,XINJACTV
.
          TRAPCLR   FORMAT                                             *I-90202
          GOTO      PROC9999 IF OVER        * Check for EOF
          BRANCH    DATAERR,PROC1000        * Get next record          *I-90202
.
          ADD       ONE,COUNTIN                                        *I-90201
          ADD       ONE,COUNT
          ASSIGN    COUNT % 10,DISPVAR
          IF        DISPVAR = 0
            DISPLAY   *P14:18,COUNT;
          ENDIF
.
. ------------------------------------------- Start processing the input record
.
          MOVE      XVISTNUM,KEY8
          CALL      RDEMVIS1                * Check against Visit Master
          IF        OVRCD > 0
            GOTO      PROC1000
          ENDIF
.
          MOVE      XVISTNUM,EMVIADMN
          UNPACK    XPATNTID,CH02,XPURNO
          MOVE      XPURNO,EMVIURNO
.
. ------------------                        * Procedures 
          CALL      FNDP0000
. ------------------                        * Primary ED diagnosis
          CALL      FNDD0000
. ------------------                        * Delete the EMR Visit Detail
          CALL      DEEMVIS1
          ADD       ONE,CONTVIS
.
          GOTO      PROC1000
.
.
PROC9999  MOVE      ZERO,OVRCD
          CLOCK     TIME,CTIMEIS
          DISPLAY   *P1:16,"* Records deleted from 'emrvisaf' - ",CONTVIS:
                    *P1:17,"*         deleted from 'emrvdgaf' - ",CONTVDG:
                    *P1:18,"*         deleted from 'emrvpraf' - ",CONTVPR;
.
          DISPLAY   *P1:20,"  Records processed - ",*V2LON,COUNT;
          KEYIN     *P40:21,"Finished : ",*V2LON,*DV,CTIMEIS:
                    *P1:24,*EL,*HOFF,"No more records.  Tap ",*V2LON,"<ENTER>":
                    *HOFF," to exit ... ",*EOFF,ANS;
          RETURN
+
.******************************************************************************
.*                                     FNDD0000                               *
.*                            Translate Diagnosis Details                     *
.******************************************************************************
FNDD0000  PACK      KEY17,XVISTNUM,XPRIDIAG                             
          CALL      RDEMVDG1
          IF        OVRCD = 0
            CALL      DEEMVDG1
            ADD       ONE,CONTVDG
          ENDIF
.
          MATCH     SP6,XADDDIAG
          GOTO      FNDD9999 IF EQUAL
.         
FNDD1000  PACK      KEY17,XVISTNUM,XADDDIAG                             
          CALL      RDEMVDG1
          IF        OVRCD = 0
            CALL      DEEMVDG1
            ADD       ONE,CONTVDG
          ENDIF
.
FNDD9999  RETURN
+
.******************************************************************************
.*                                     FNDP0000                               *
.*                            Translate Emergency Procedures                  *
.******************************************************************************
FNDP0000  OPEN      EMRPROA1,"emrproaf"     * Position at start of Table
.
FNDP1000  MOVE      ZERO,OVRCD
          READKS    EMRPROA1;EMPRCODE,EMPRDESC,EMPRHDPE,EMPRSPAR
.
          IF        @OVER > 0
            GOTO      FNDP9999
          ENDIF          
.
          UNPACK    EMPRHDPE,CH02,CH04
          MATCH     XPROCDUR,CH02
          GOTO      FNDP1000 IF NOT EQUAL
.
.                                           * A Valid Procedure Code exists
.                                           * See if Procedure Details exist
.                                           * (No need to Update one)        
          PACK      KEY17,XVISTNUM,EMPRCODE
          CALL      RDEMVPR1
          IF        OVRCD = 0
            CALL      DEEMVPR1
            ADD       ONE,CONTVPR
          ENDIF
.
FNDP9999  RETURN
+
.******************************************************************************
.*                                     FERR0000                               *
.*                   Print TRAP data errors in "convvemd.txt"                 *
.******************************************************************************
.
FERR0000  MOVE      ONE,DATAERR
          PRINT     *1,"| ",XVISTNUM,*13,"| ",XPATNTID,*26,"| ":
                    "Data error occurred in this record - Record No. :":
                    COUNTIN,"   - Record bypassed",*132,"|"
          READ      VEMDPD,SEQ;XESTID       * Bypass rest of record
.
FERR9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       EMRPROIO/INC                 * Emergency Procedure codes
          INC       EMRVISIO/INC                 * Visit Details
          INC       EMRVDGIO/INC                 * Visit Diagnosis Details
          INC       EMRVPRIO/INC                 * Visit Procedure Details
          INC       PATCODIO/INC
          INC       PATMA1IO/INC                 * Patient Master
          
