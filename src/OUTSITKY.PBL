.****************************************************************************
.*                             OUTSITKY                                     *
.*                        Get O/P site code                                 *
.*         Returns: EXIT  0 = OK                                            *
.*                        1 = nothing or exitchar entered                   *
.*        V10.05.00 29/12/2014  Steve Armstrong  CAR 305263                 *
.*                  Created include.                                        *
.****************************************************************************
.
OUTSITKY  KEYIN     *PECOL:EVERT,*EL,*V2LON,SITECODE
.
          CALL      CKSIT000                * see what was entered
          BRANCH    EXIT,SITKY100:          * ? entered
                         SITKY750:          * valid code
                         SITKY900:          * nothing entered
                         SITKY900           * exitchar
.
          GOTO      OUTSITKY                * invalid code
.
SITKY100  MOVE      ZERO,HLEF
          CALL      GETSCR00
SITKY120  CALL      OUTSITDS                * display codes on file
.
SITKY150  KEYIN     *P1:24,*EL,"O/P Site: ":
                    *P11:24,*V2LON,SITECODE
.
          CALL      CKSIT000                * see what was entered
          BRANCH    EXIT,SITKY120:          * ? entered
                         SITKY700:          * valid code
                         SITKY800:          * nothing entered
                         SITKY800           * exitchar
.
          GOTO      SITKY150
.
SITKY700  CALL      PUTSCR00                * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,SITECODE
SITKY750  MOVE      ZERO,EXIT
          GOTO      SITKY999
.
SITKY800  CALL      FRESCR00
SITKY900  MOVE      ONE,EXIT
.
SITKY999  RETURN
+
.***************************************************************************
.*                                  CKSIT000           Called by: OUTSITKY *
.*                      See what was input                                 *
.*      Returns: EXIT  0 = invalid                                         *
.*                     1 = "?"                                             *
.*                     2 = valid                                           *
.*                     3 = nothing                                         *
.*                     4 = exitchar                                        * 
.***************************************************************************
.
CKSIT000  PACK      SITECODE,SITECODE,SP6
.
          MATCH     SP6,SITECODE                 * anything entered ?
          GOTO      CKSIT900 IF EQUAL            * no
.
          MATCH     EXITCHAR,SITECODE            * exitchar entered ?
          GOTO      CKSIT700 IF EQUAL            * yes
.
          MATCH     QUEST,SITECODE               * ? entered ?
          GOTO      CKSIT800 IF EQUAL            * yes
.
.         Code entered
.
          PACK      KEY6,SITECODE
          CALL      RDSITA1                      * code on file ?
          BRANCH    OVRCD,CKSIT600               * no
.
          MOVE      TWO,EXIT                     * code entered
          GOTO      CKSIT999
.
CKSIT600  DISPLAY   *P1:24,*EL,*B,"O/P Site not on file. ";
          CALL      HITENTER
          MOVE      ZERO,EXIT
          GOTO      CKSIT999
.
CKSIT700  MOVE      FOUR,EXIT
          GOTO      CKSIT999
.
CKSIT800  MOVE      ONE,EXIT
          GOTO      CKSIT999
.
CKSIT900  MOVE      THREE,EXIT
.
CKSIT999  RETURN
+
