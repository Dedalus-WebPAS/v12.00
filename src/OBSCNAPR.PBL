.------------------------------------------------------------------------------
. Output Table of Clinical Notes
.
. Modification : 
.
.------------------------------------------------------------------------------
. V11.00.01 10/09/2020  Thanh T           TSK 0881752
.           Changed WRITE000 to fix wrte out class=icon for ClinicalNote.gif 
. V10.08.01 02.11.2016  Ebon Clements     TSK 0305048
.           Added clinical note enquiry functionality
.                13.06.2002 Harvinder SRF No. - 12797
.                           Modified to display "End of Notes"
.                           Modified to do the filtering on the basis of
.                           Occupational Group and Note Type
.                15.12.1999 B.G.Ackland
.                           Make Security Based on CLIWEB06 not CLIWEB99
.
.------------------------------------------------------------------------------
          DEFPROC   CLNTAB00
.
          INC       OBSCNAFD/INC
          INC       OBSCNCFD/INC
.
.
. Variable Declarations
.
CATOCG    INIT      "w0"
CRTSUMM   DIM       1
USERNAME  DIM       30
OCCGROUP  DIM       20
TOTALREC  FORM      4
LASTNOTE  FORM      4
RECORDNO  FORM      2
NOTESKEY  DIM       12
LINKTYP   DIM       1
DIM2A     DIM       2
DIM2B     DIM       2
DISPNUM   FORM      4
FILENAMA  DIM       8
SAVTEMP   DIM       3
.------------------------------------------------------------
. Mainline Code
.------------------------------------------------------------
MAIN0000  CALL      INIT0000
          CALL      CLNT0000
.
MAIN9999  GOTO      CLNTAB99
.------------------------------------------------------------
INIT0000  OPEN      OBSCNAA1,"obscnaaf"
          OPEN      OBSCNAA2,"obscnaaf"
          OPEN      OBSCNBA1,"obscnbaf"
          OPEN      OBSCNCA1,"obscncaf"
          OPEN      OBSCNCA2,"obscncaf"
.          
          UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
          UNPACK    DIM127,D1,LINKTYP,DIM127
          UNPACK    DIM127,D1,LINKNUM,DIM127
          UNPACK    DIM127,D1,CRTSUMM,DIM127
          MOVE      LINKNUM,RECORDNO
          MOVE      LINKTEM,SAVTEMP
.
INIT9999  RETURN
.------------------------------------------------------------
CLNT0000  MOVE      ZERO,DISPNUM            * Number Output
          MOVE      ZERO,TOTALREC           * Number Processed
.
          PACK      KEY12,ADMISSNO,Z70
          CALL      RSOBCA1                 * Positional Read
CLNT3000  CALL      RPOBCA1                 * Read Prior Record
          BRANCH    OVRCD,CLNT9000
.
          MATCH     ADMISSNO,OBCAVIS
          GOTO      CLNT9000 IF NOT EQUAL
.
          MATCH     CLNOT001,SP70           * Display for all Note Types
          IF        !@EQUAL
            MATCH     CLNOT001,OBCATYP      * Filter Note Types
            GOTO      CLNT3000 IF NOT EQUAL
          ENDIF
.
          MATCH     WBSEC008,SP70           * Display for all Occ. Groups
          IF        !@EQUAL
            MATCH     WBSEC008,OBCAOCG      * Filter Occ. Groups
            GOTO      CLNT3000 IF NOT EQUAL
          ENDIF
.
          ADD       ONE,TOTALREC
          IF        TOTALREC>=STARTNOT
            ADD       ONE,DISPNUM
            CALL      WRITE000            * Write HTML Code to list each Note
          ENDIF
.
          IF        DISPNUM<RECORDNO
            GOTO      CLNT3000
          ELSE
            GOTO      CLNT9999
          ENDIF
.
CLNT9000  WRITE     HTMLFILE;"<tr bgcolor=#FF0000>":
                               "<td colspan=4 align=center>":
                               "<b>End of Notes</td>"
.
CLNT9999  RETURN
.----------------------------------------------------
WRITE000  PACK      NOTESKEY,ADMISSNO,OBCACID,SP20   
          REP       " +",SAVTEMP
          REP       " +",URNUMBER
          REP       " +",ADMISSNO
          REP       " +",NOTESKEY
.
          UNPACK    OBCADTE,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
.
          UNPACK    OBCATME,DIM2,DIM1,DIM2A
          MOVE      DIM2,F2
          IF        F2>=12
            MOVE      "pm",DIM2B
            IF        F2>=13
              ASSIGN    F2-12,F2
            ENDIF
          ELSE
            MOVE    "am",DIM2B
          ENDIF
          MOVE      F2,DIM2
.
          PACK      KEY10,OBCAUID,SP10      *** Pack Key with User ID
          CALL      RDWBSE1
          PACK      KEY5,CATOCG,WBSEOCG
          CALL      RDCODE1
          MATCH     "0",LINKTYP
          IF        @EQUAL
            MOVE      WBSENAM,USERNAME
            MOVE      TDESC,OCCGROUP
          ENDIF
          MATCH     "1",LINKTYP
          IF        @EQUAL
            MOVE      WBSEUID,USERNAME
            MOVE      ACODE,OCCGROUP
          ENDIF
.
          PACK      KEY3,OBCATYP            * Key is Type of Note
          CALL      RDOBCC1                 * Read Clinical Note Type
          IF        OVRCD=0
            MOVE      OBCCTMO,LINKTEM       * Output Template ID
          ELSE
            MOVE      SAVTEMP,LINKTEM      * Use Default Template 
          ENDIF
.        
          WRITE     HTMLFILE;"<tr><td><a href='javascript:ShowNote(#"",LINKPRG:
                             ".pbl?reportno=",LINKREP:
                             "&template=",LINKTEM:
                             "&urnumber=",URNUMBER:
                             "&admissno=",ADMISSNO:
                             "&nzteskey=",NOTESKEY,"#")'>"
          
          WRITE     HTMLFILE;"<img src=../images/ClinicalNote.gif ":
                             "class=Icon></a><b>",OBCCDES,"</td>":
                             "<td>"
.
          MATCH     "1",OBCAMDL            *** If ALL of Note is Deleted
          IF        @EQUAL
            WRITE   HTMLFILE;"<strike>";
          ENDIF
.
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR,SP2:
                             DIM2,DIM1,DIM2A,SP1,DIM2B,SP2,USERNAME:
                             "</td><td><b>"
.
          MATCH     "1",CRTSUMM
          IF        @EQUAL
            GOTO      WRITE100
          ENDIF
.
          MATCH     "1",OBCAMDL            *** If ALL of Note is Deleted
          IF        @EQUAL
            WRITE   HTMLFILE;"<strike>",OCCGROUP
          ELSE
            WRITE   HTMLFILE;OCCGROUP
          ENDIF

WRITE100  WRITE     HTMLFILE;"</td></tr><tr><td></td>"
.
          WRITE     HTMLFILE;"<td colspan=2>"
.
          MATCH     "1",OBCAMDL            *** If ALL of Note is Deleted
          GOTO      WRITE500 IF EQUAL
.
          MATCH     "1",OBCAP1D
          IF        !@EQUAL
            MATCH     SP70,OBCAPT1
            IF        !@EQUAL
              WRITE     HTMLFILE;"<img src=../images/NoteItem.gif> ":
                                 OBCAPT1,"<br>"
            ENDIF
          ENDIF
.
          MATCH     "1",OBCAP2D
          IF        !@EQUAL
            MATCH     SP70,OBCAPT2
            IF        !@EQUAL
              WRITE     HTMLFILE;"<img src=../images/NoteItem.gif> ":
                                 OBCAPT2,"<br>"
            ENDIF
          ENDIF
.
          MATCH     "1",OBCAP3D
          IF        !@EQUAL
            MATCH     SP70,OBCAPT3
            IF        !@EQUAL
              WRITE     HTMLFILE;"<img src=../images/NoteItem.gif> ":
                                 OBCAPT3,"<br>"
            ENDIF
          ENDIF
.
          MATCH     "1",OBCAP4D
          IF        !@EQUAL
            MATCH     SP70,OBCAPT4
            IF        !@EQUAL
              WRITE     HTMLFILE;"<img src=../images/NoteItem.gif> ":
                                 OBCAPT4
            ENDIF
          ENDIF
.
WRITE500  WRITE     HTMLFILE;"</td></tr>"
.
          REP       "+ ",SAVTEMP
          REP       "+ ",URNUMBER
          REP       "+ ",ADMISSNO
          REP       "+ ",NOTESKEY
.
WRITE999  RETURN
.
          INC       OBSCNAIO/INC
          INC       OBSCNCIO/INC
          INC       IBAOVRIO/INC
.
CLNTAB99  ENDPROC
.
