.******************************************************************************
.*  Include Name  : PATTBLKY.PBL                                              *
.*  Description   : Keyin of a Health fund Table                              *
.*  Author        : Wilma Flintstone                                          *
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  MHFUND   - Health Fund                                    *
.*                  CKYIDEF4 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory   (0=No  1=Yes)             *
.*                  CKYIMODE - is keyin insert mode (0=No  1=Yes)             *
.*                                                                            *
.*  Returns       : HFTABL   - Keyed in code                                  *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.*        V5.08.02  19/07/2000  Dean Cramer   SRF 6710                        *
.*                  Alter variables for match,move and apend actions on       *
.*                  Health fund table                                         *
.*                  increase in patmchgf.                                     *
.*        V5.08.01  06/06/2000  Jill Habasque                                 *
.*                  Changed from key10 to key14 for the health fund file      *
.*                  17/03/99 J.Tan                                            *
.*                  Check for active/inactive                                 *
.*                                                                            *
.******************************************************************************
PATTBLKY
HFTBL010  MATCH     SP8,CKYIDEF8                  * using a default ?
          GOTO      HFTBL050 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN4:      * no default used
                    *PECOL:EVERT,*V2LON,HFTABL:
                    *PECOL:EVERT,*DV,HFTABL
          GOTO      HFTBL080
.
HFTBL050  MOVE      CKYIDEF8,HFTABL
          KEYIN     *PECOL:EVERT,*DV,CKYIDEF8:     * default used
                    *PECOL:EVERT,*V2LON,*RV,HFTABL:
                    *PECOL:EVERT,*DV,HFTABL
.
.         check what was entered
.
HFTBL080  CALL      TBLCK000
          BRANCH    EXIT,HFTBL100,HFTBL705,HFTBL805,HFTBL905
.                         "?"      valid    nothing exitchar
          GOTO      HFTBL010
.
.         a "?" was input
.
HFTBL100  MOVE      "0",HLEF                * save all of the screen
          CALL      GETSCR00                * save screen
.
HFTBL105  MOVE      ONE,CNEWDS
          PACK      KEY6,HFTABL,SP6
          CALL      PATTBLDS
.
.         now ask for code while leaving displayed codes on screen
.
HFTBL150  MATCH     SP8,CKYIDEF8                  * using a default
          GOTO      HFTBL200 IF NOT EQUAL
.
          KEYIN     *P1:24,*EL,*DV,"Code    : ":
                    *P11:24,*DV,UNDLN8:      * no default used
                    *P11:24,*V2LON,HFTABL:
                    *P11:24,*DV,HFTABL
          GOTO      HFTBL300
.
HFTBL200  MOVE      CKYIDEF8,HFTABL
          KEYIN     *P1:24,*EL,*DV,"Code    : ":
                    *P11:24,*DV,HFTABL:     * default used
                    *P11:24,*V2LON,*RV,HFTABL:
                    *P11:24,*DV,HFTABL
.
.         repeat the validation of the code
.         check what was entered
.
HFTBL300  CALL      TBLCK000
          BRANCH    EXIT,HFTBL105,HFTBL700,HFTBL800,HFTBL900
.                         "?"      valid    nothing exitchar
.
          GOTO      HFTBL150
.
.         a valid code was entered 
. 
HFTBL700  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,HFTABL
.
HFTBL705  MOVE      FALSE,EXIT                    * valid input
          GOTO      HFTBL999 
.
.         nothing was input
.
HFTBL800  CALL      PUTSCR00                      * restore screen
          DISPLAY   *PECOL:EVERT,*V2LON,HFTABL
.
HFTBL805  MOVE      SP20,HFNAME                    * blank out description
          MOVE      TRUE,EXIT                     * Nothing or Spaces entered
          GOTO      HFTBL999 
.
.         EXITCHAR input
.
HFTBL900  CALL      FRESCR00                      * free the screen saved
.
HFTBL905  MOVE      TWO,EXIT                      * EXITCHAR
.
HFTBL999  RETURN
+
.***************************************************************************
.*                              TBLCK000             Called by: PATFNDKY   *
.*                        check what was entered                           *
.*          Returns:  EXIT  = 0   invalid                                  *
.*                          = 1   "?"                                      *
.*                          = 2   valid                                    *
.*                          = 3   nothing                                  *
.*                          = 4   exitchar                                 *
.***************************************************************************
TBLCK000  ENDSET    HFTABL
          APPEND    SP8,HFTABL
          RESET     HFTABL
.
          MATCH     EXITCHAR,HFTABL
          GOTO      TBLCK600 IF EQUAL
.
          MATCH     SP8,HFTABL
          GOTO      TBLCK700 IF EQUAL
.
. check for a "?"
.
          MATCH     QUEST,HFTABL
          GOTO      TBLCK800 IF EQUAL
.
. a code was entered so validate
. 
TBLCK500  PACK      KEY14,MHFUND,HFTABL,SP10
          CALL      RDFUND1
          IF        CKYIMODE=ZERO
            BRANCH    OVRCD,TBLCK900
          ELSE
            COMPARE   ZERO,OVRCD
            GOTO      TBLCK900 IF EQUAL
          ENDIF
.
          COMPARE   ONE,PHFTACT
          IF        @EQUAL
            DISPLAY   *P1:24,*EL,*V2LON,"Inactive Code. ";
            CALL      HITENTER
            MOVE      ZERO,EXIT
            GOTO      TBLCK999
          ENDIF
.
          MOVE      TWO,EXIT
          GOTO      TBLCK999
.
. exitchar entered
.
TBLCK600  MOVE      FOUR,EXIT
          GOTO      TBLCK999
.
. nothing entered
.
TBLCK700  BRANCH    CKYIMAND,TBLCK950             * mandatory
          MOVE      THREE,EXIT
          MOVE      SP20,HFNAME                    * clear desc field
          GOTO      TBLCK999
.
. "?" entered
.
TBLCK800  MOVE      ONE,EXIT
          GOTO      TBLCK999
.
. invalid code entered
.
TBLCK900  
          IF        CKYIMODE=0
            DISPLAY   *P1:24,*EL,*B,"Invalid code.  ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
          MOVE      ZERO,EXIT
          GOTO      TBLCK999
.
TBLCK950  BEEP
          MOVE      ZERO,EXIT
.
TBLCK999  RETURN
.
          INC       PATTBLDS
.
+
