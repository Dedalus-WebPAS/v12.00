.**************************************************************************
.*                             DPRTDSPL              Called by:           *
.*              Display's departments available printers                  *
.*                                                                        *
.**************************************************************************
. 
DSPR0000  MOVE      ONE,CPAGENO
          MOVE      SEVEN,CVERT
          MOVE      FIVE,CCOL
          MOVE      ZERO,FIELD
.
          PACK      KEY5,ANS2,SP5
          CALL      RDICOD1                    * positional read
          BRANCH    OVRCD,DSPR9000
          DISPLAY   *P41:4,*EF,PRXDESC,*P4:6,"Code",SP2,"Description"
DSPR1000  CALL      RDKICOD1                   * read next record
          BRANCH    OVRCD,DSPR5000
.
          MATCH     ANS2,PRXCATA               * correct department ?
          GOTO      DSPR5000 IF NOT EQUAL
.
          ADD       ONE,CVERT
          COMPARE   TWENTY2,CVERT
          IF        @EQUAL
            COMPARE   FIFTY,CCOL               * page full ?
            IF        @EQUAL
              GOTO      DSPR4000
            ELSE
              MOVE      FIFTY,CCOL
              MOVE      SEVEN,CVERT
              DISPLAY   *P48:6,"Code",SP2,"Description"
              GOTO      DSPR1000
            ENDIF
          ELSE
            GOTO      DSPR3000
          ENDIF
.
DSPR3000  ADD       ONE,FIELD
          DISPLAY   *PCCOL:CVERT,*V2LON,PRXCODE,SP3,*HOFF,PRXDESC
          PACK      TMPKEY[FIELD],PRXCATA,PRXCODE
          GOTO      DSPR1000
.
.  new page needed
.
DSPR4000  BRANCH    CPAGENO,DSPR4500
.
.  middle pages  :  Next & First options
.
DSPR4100  MOVE      "1",CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,DSPR9999:              * exit
                         DSPR9999:              * search again
                         DSPR7000:              * next
                         DSPR8000               * previous
          GOTO      KEYA0000
.
.  first page  :  Next option
.
DSPR4500  MOVE      "2",CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,DSPR9999:              * exit 
                         DSPR9999:              * search again
                         DSPR7000:              * next
                         DSPR5900               * previous
          GOTO      KEYA0000
.
.  end of data 
.
DSPR5000  BRANCH    CPAGENO,DSPR5500
.
.  middle pages  :  First option
.
DSPR5100  MOVE      "3",CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,DSPR9999:              * exit
                         DSPR9999:              * search again
                         DSPR5900:              * next
                         DSPR8000               * previous
          GOTO      KEYA0000
.
.  first page  :  No options
.
DSPR5500  MOVE      "4",CALLPOSN
          CALL      KEYA0000
          BRANCH    EXIT,DSPR9999:              * exit
                         DSPR9999:              * search again
                         DSPR5900:              * next
                         DSPR5900               * previous
          GOTO      KEYA0000
.
DSPR5900  BEEP
DSPR5910  BRANCH    CALLPOSN,DSPR4100,DSPR4500,DSPR5100,DSPR5500
.
.   number entered
.
DSPR7000  MOVE      EIGHT,CVERT
          MOVE      FIVE,CCOL
          ADD       ONE,CPAGENO
          DISPLAY   *P1:CVERT,*EF
          MOVE      ZERO,FIELD
          GOTO      DSPR3000
.
.   previous screen selected
.
DSPR8000  MOVE      TMPKEY[1],KEY5
          CALL      RDICOD1                   * reread first record on screen
          MOVE      ZERO,FORM2                * initialise count
          WHILE     FORM2 <= TWENTY8
            CALL      RDPICOD1                * read previous record
            ADD       ONE,FORM2               * increment count
          DO
.
          MOVE      EIGHT,CVERT
          MOVE      FIVE,CCOL
          SUB       ONE,CPAGENO
          MOVE      ZERO,FIELD
          DISPLAY   *P1:CVERT,*EF
          GOTO      DSPR3000
.
DSPR9000  DISPLAY   *P1:5,*EF,*P1:24,*EL,"Invalid department code. ";
          CALL      HITENTER
          MOVE      TWO,EXIT
.
DSPR9999  RETURN



KEYA0000  DISPLAY   *P1:24,*EF,"e(",*V2LON,ANSX,*HOFF,")it, ":
                    "next (",*V2LON,ANSD,*HOFF,")epartment";
          MOVE      TWENTY6,CCOL
          IF        CALLPOSN = 1|CALLPOSN = 2
            DISPLAY   ", (",*V2LON,ANSN,*HOFF,")ext";
            ADD       EIGHT,CCOL
          ENDIF
          IF        CALLPOSN = 1|CALLPOSN = 3
            DISPLAY   ", p(",*V2LON,ANSR,*HOFF,")evious";
            ADD       TEN2,CCOL
          ENDIF
.
          DISPLAY   " :  ";
          ADD       FIVE,CCOL
.
KEYA1000  KEYIN     *PCCOL:24,*DV,UNDLN1:
                    *PCCOL:24,*V2LON,*JR,DIM1:
                    *PCCOL:24,*DV,DIM1
          UNPACK    DIM1,ANS
          REP       UPPLOW,ANS
.
          MATCH     ANSX,ANS                  * exit selected ?
          GOTO      KEYA9100 IF EQUAL
.
          MATCH     ANSD,ANS                  * search again selected ?
          GOTO      KEYA9200 IF EQUAL
.
          MATCH     ANSN,ANS                  * next selected ?
          GOTO      KEYA9300 IF EQUAL
.
          MATCH     ANSR,ANS                  * previous selected ?
          GOTO      KEYA9400 IF EQUAL
.
KEYA1500  BEEP
          GOTO      KEYA1000
.
KEYA9100  MOVE      ONE,EXIT                  * exit
          GOTO      KEYA9999
.
KEYA9200  MOVE      TWO,EXIT                  * search again
          GOTO      KEYA9999
.
KEYA9300  MOVE      THREE,EXIT                * next
          GOTO      KEYA9999
.
KEYA9400  MOVE      FOUR,EXIT                 * previous
          GOTO      KEYA9999
.
KEYA9999  RETURN
+













