.******************************************************************************
.*                                  PATITMRD                                  *
.*                         Get MBS Item record                                *
.*                                                                            *
.* Requires:   KEY17 populated with -                                         *
.*                      - MBS Code                                            *
.*                      - Service Date (defaults to current date)             *
.*                                                                            *
.* Returns:    EXIT  0 = valid MBS Item in "patitemn"                         *
.*                   1 = Item not found                                       *
.*             WKITCODE - MBS Item Code                                       *
.*             WKITDATE - Service Date                                        *
.*             WKITINAC - Item Inactive flag - 0=Active, 1=Inactive           *
.*             WKITEXDT - Item date range -    0=OK,     1=out of range       *
.*                                                                            *
.* Modifications:                                                             *
.******************************************************************************
PATITMRD
          MOVE      ZERO,EXIT
          MOVE      ZERO,WKITINAC           * initialise Inactive flag
          MOVE      ZERO,WKITEXDT           * date range error flag
.
          UNPACK    KEY17,WKITCODE,WKITDATE
          MATCH     SP8,WKITDATE            * check blank date
          IF        @EQUAL
            PACK      WKITDATE,CCC,CYY,CMM,CDD,SP10 * default to current date
            REP       " 0",WKITDATE
            PACK      KEY17,WKITCODE,WKITDATE,SP10
          ENDIF

          UNPACK    KEY17,KEY9,KEY8        * set up for validation
          CALL      RDITEM1
          IF        OVRCD = 0
            MATCH     "I",PTITACTV          * Inactive ?
            GOTO      ITMRE900 IF NOT EQUAL * no
            MOVE      ONE,WKITINAC          * set Inactive flag
          ENDIF
.
          PACK      KEY17,KEY9,KEY8
          CALL      RDSITEM1
          CALL      RDPITEM1
          BRANCH    OVRCD,ITMRE200
.
          PACK      D9,ITEMNO
          MATCH     KEY9,D9
          GOTO      ITMRE200 IF NOT EQUAL
          GOTO      ITMRE400
.
.         Check for future date
.
ITMRE200  PACK      KEY17,KEY9,KEY8
          CALL      RDSITEM1
          CALL      RDKITEM1
          BRANCH    OVRCD,ITMRE990
.
          PACK      D9,ITEMNO
          MATCH     KEY9,D9
          GOTO      ITMRE990 IF NOT EQUAL
          GOTO      ITMRE980                * out of date range
.
ITMRE400  MOVE      ZERO,WKITINAC           * reset Inactive flag
.
          MATCH     "I",PTITACTV            * Inactive ?
          GOTO      ITMRE970 IF EQUAL       * yes
.
          MATCH     PTITEFDT,WKITDATE       * effective date => serv. date ?
          GOTO      ITMRE980 IF LESS        * yes
.
          MATCH     SP8,PTITETDT
          IF        !@EQUAL
            MATCH     KEY8,PTITETDT         * service Date => Eff.To Date ?
            GOTO      ITMRE980 IF LESS
          ENDIF
.
ITMRE900  MOVE      ZERO,EXIT
          GOTO      ITMRE999
.
ITMRE970  MOVE      ONE,WKITINAC            * set Inactive flag
          GOTO      ITMRE990
.
ITMRE980  MOVE      ONE,WKITEXDT            * set Date Range error flag
ITMRE990  MOVE      ONE,EXIT
          UNPACK    SP70,IDESC,IALPHA,IMISGRP,ICLSS
          MOVE      ZERO,QIAMT
.
ITMRE999  MOVE      EXIT,OVRCD              * just in case I miss one.
          RETURN
