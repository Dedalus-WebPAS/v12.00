.PATDATYP.PBL
.
         DEFPROC    PATDATYP
.
         INC        PATTRNFD/INC
.
.******************************************************************************
.        PATDATYP : Calculate the number of bed days for Admission Type (Catg A)
.                   indicator 12 (SAVIND12) = Spaces for Qualified
.                   indicator 12 (SAVIND12) = U for Unqualified
.
.        Parameter: FROMDATE - start date
.                   TODATE  - ending date
.                   SAVIND12- value of Indicator 12 of Catg A
.        Return   : NBRDAYS - Number of bed days
.
.        Modifications : V10.01.00 27/11/2012 J.Tan  CAR 262673
.                       Created new include to calculate bed days for Qualified
.                       or Unqualified days
.         V12.00.01 23/05/2025  J.Tan          TSK 0955096
.                   Added Alphanumeric visit number changes
.
.*******************************************************************************
DBTP0000 OPEN       PATTRAN2,"pattranf"
         MOVE       SP10,WDATE1             * initialise the start date
         MOVE       SP10,WDATE2             * initialise the end date
         MOVE       ZERO,NBRDAYS
         PACK       SAVIND12,SAVIND12,SP70
.
         COMPARE    THREE,ASTAT             * discharged ?
         GOTO       DBTP1000 IF NOT EQUAL   * no.
.
         MATCH      ADATE,DDATE             * day case patient ?
         GOTO       DBTP1000 IF NOT EQUAL   * no.
.
         PACK       KEY30,AADMNO,FROMDATE,Z70
         CALL       RDSTRAN2
         CALL       RDPTRAN2
         BRANCH     OVRCD,DBTP9000
         MATCH      TADMN,AADMNO            * same admission ?
         GOTO       DBTP9000 IF NOT EQUAL  * No.
.
         PACK       KEY5,ANSA,SP1,TATYPE
         CALL       RDCODE1
         IF         OVRCD=0
           MATCH      SAVIND12,TCINDC12            * Unqualified days?
           GOTO       DBTP9000 IF NOT EQUAL
         ENDIF
.
         MATCH      ADATE,TODATE
         GOTO       DBTP9000 IF LESS
.
         MOVE       ONE,NBRDAYS             * no of bed days is one for daycase
         GOTO       DBTP9000
.
DBTP1000 PACK       KEY30,AADMNO,FROMDATE,SP20,SP10
         CALL       RDSTRAN2
DBTP1200 CALL       RDKTRAN2
         BRANCH     OVRCD,DBTP4000
.
         MATCH      TADMN,AADMNO            * same admission ?
         GOTO       DBTP4000 IF NOT EQUAL
.
         REP        " 0",TDATE
         MOVE       TDATE,STDATE
         MATCH      ANSA,TMOVE              * Admission record ?
         GOTO       DBTP2000 IF EQUAL
.
         MATCH      ANSR,TMOVE              * Return record ?
         GOTO       DBTP2000 IF EQUAL
.
         CMATCH     ANSC,TMOVE              * Change record ?
         GOTO       DBTP2200 IF EQUAL
.
         GOTO       DBTP3000
.
.        Admission/Return record
.
DBTP2000 MATCH      TDATE,TODATE
         GOTO       DBTP9000 IF LESS
.
         PACK       KEY5,ANSA,SP1,TATYPE
         CALL       RDCODE1
         IF         OVRCD=0
           MATCH      SAVIND12,TCINDC12  * Unqualified days?
           GOTO       DBTP1200 IF NOT EQUAL
         ENDIF
.
         MOVE       TDATE,WDATE1            * set from date
         GOTO       DBTP1200                * get next trans record
.
.        Change
.
DBTP2200 MATCH      TDATE,TODATE
         IF         @LESS
           MATCH      SP6,WDATE1              * Check with the fromdate
           GOTO       DBTP9000 IF EQUAL
           MOVE       TODATE,WDATE2           * set to date
           CALL       CBDY0000                * Calculate bed days
           GOTO       DBTP9000
         ENDIF
.
         PACK       KEY5,ANSA,SP1,TATYPE
         CALL       RDCODE1
         IF         OVRCD=0
           MATCH      SAVIND12,TCINDC12          * Unqualified days
           GOTO       DBTP2400 IF NOT EQUAL
         ENDIF
.
.        Change to the selected bed type
.
         MATCH      SP8,WDATE1            * check if it was a real change of
         GOTO       DBTP1200 IF NOT EQUAL   * bed type
.
         MOVE       TDATE,WDATE1          * set fromdate of the change bedtype
         GOTO       DBTP1200
.
.        Change to a different bed type, check if it was from selected bedtype

DBTP2400 MATCH      SP8,WDATE1           
         GOTO       DBTP1200 IF EQUAL
.
         MOVE       TDATE,WDATE2
         CALL       CBDY0000                * Calculate bed days
         GOTO       DBTP1200                * get next trans record
.
.        Discharge/Leave record always on the same bed type as previous record
.
DBTP3000 MATCH      SP8,WDATE1
         GOTO       DBTP3200 IF EQUAL       * Was not on selected bed type
.
         MATCH      TDATE,TODATE
         IF         @LESS
           MOVE       TODATE,WDATE2         * set to end date
         ELSE
           MOVE       TDATE,WDATE2          * Set to date
         ENDIF
         REP        " 0",WDATE2
         CALL       CBDY0000                * Calculate bed days
.
DBTP3200 CMATCH     "D",TMOVE
         GOTO       DBTP1200 IF NOT EQUAL   * get next record
         GOTO       DBTP9000
.
.        check the last record
.
DBTP4000 MATCH      SP8,WDATE1
         GOTO       DBTP9000 IF EQUAL       * Was not on selected bed type
.
         MOVE       TODATE,WDATE2
         REP        " 0",WDATE2
         CALL       CBDY0000                * Calculate bed days
         GOTO       DBTP9000
.
DBTP9000 CLOSE      PATTRAN2
DBTP9999 GOTO       ENDP9999
+
.******************************************************************************
.        CBDY0000 - Calculate dates difference
.******************************************************************************
CBDY0000 PACK       CDYSFDTE,WDATE1
         PACK       CDYSTDTE,WDATE2
         MATCH      FROMDATE,WDATE1
         IF         @LESS
           MOVE       FROMDATE,WDATE1
         ENDIF
         CALL       CALCDAYS
         MOVE       CDYSDAYS,CALDAYS
         SUB        ONE,CALDAYS
         ADD        CALDAYS,NBRDAYS
         MOVE       SP10,WDATE1
         MOVE       SP10,WDATE2
CBDY9999 RETURN
+
         INC       IBAOVRIO/INC
         INC       PATTRNIO/INC
.
ENDP9999 ENDPROC
