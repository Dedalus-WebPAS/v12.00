.------------------------------------------------------------
. File          :  PATOICPR.PBL
.
. Function      :  Update Keyword Index File for ICD9 Operation Code
.
. Parameters    :  OPCODE
.
. Modifications :
.-----------------------------------------------------------------------------
. V9.02.01  08/08/2002  Pat Dirito
.           Changed read from RDICO1 to RD10T9D1 as RDICO1 uses ICD10
.           parameter
.-----------------------------------------------------------------------------
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Procedure to Update
.  a Key Word Index Table that can be used for searching 
.  Masterfiles. The procedure should be called from the 
.  Add, Update and Delete functions in the Masterfile 
.  Maintenance Function.
.  
.  A Key Word Index Table is Defined in the following Manner
.                              
.  PTOIICD    DIM       xx    * Code Field Segment 1
.  PTOIICD    DIM       xx    * Code Field Segment n
.  PTOIKWD     DIM       15    * Key Word
.  PTOISPA     DIM       10    * Spare
.
.  Index 1   PTOIICD, PTOIICD, PTOIKWD
.  Index 2   PTOIKWD, PTOIICD, PTOIICD
.
.  To convert this Procedure the following should be performed
.    Global Change PAT to the System ID eg :%s/PAT/PAT/g
.    Global Change OIC to the File   ID eg :%s/OIC/DKI/g
.    Global Change PTOI to the IO Call ID eg :%s/PTOI/PTDK/g
.    Modify the KEYINDEX defintion to combined Code Field Lengths eg 6
.    Global Change KEY9 to the Key for the Master File Table eg KEY6 
.    Global Change KEY24 to the Key for the Key Word Table eg KEY21
.    Change xxxxxxx to the Code Fields eg DCODEhh
.    Change the GENR0000 routine to call the BWORD000 for each
.    string of key words to be indexed for the Coded Field
.    eg MOVE    DSNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    DGNAME,KEYWORDS
.       CALL    BWORD000
.       MOVE    TDESC,KEYWORDS
.       CALL    BWORD000
.------------------------------------------------------------
          DEFPROC   PATOICUP
.
          INC       PATOICFD/INC
.
KEYINDEX  DIM       9         * Code Index in Table
KEYWORDS  DIM       60        * String Containing the Key Words to be Indexed
SIXTY     FORM      "60"
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
.------------------------------------------------------------
. Generate Word Index
.------------------------------------------------------------
GENR0000  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     PATOICA1,"patoicaf"
          TRAPCLR  IO
          BRANCH   OVRCD,GENR9999
.
          MOVE     OPCODE,KEYINDEX
          CALL     CLEAR000           * Remove Current Key Word Index
          BRANCH   EXIT,GENR9999
.
          MOVE     OPCODE,KEY9        * Validate Record on File
          CALL     RD10T9O1           * Read ICD9 Procedure File 
          BRANCH   OVRCD,GENR1900     * If Not then it Must be a Delete/Clear
.
          MOVE     OPCODE,KEYINDEX
          MOVE     OPCODE,KEYWORDS
          CALL     BWORD000
.
          MOVE     ODESC,KEYWORDS
          CALL     BWORD000
.
GENR1900  CLOSE    PATOICA1
.
GENR9999  GOTO     PATOICEP
.------------------------------------------------------------
. Clear Current Key Words
.------------------------------------------------------------
CLEAR000  PACK     KEY24,KEYINDEX,SP70
          CALL     RPPTOI1
          CALL     RKPTOI1
          BRANCH   OVRCD,CLEAR999
          MATCH    KEYINDEX,PTOIICD
          GOTO     CLEAR999 IF NOT EQUAL
.
          PACK     KEY24,PTOIICD,PTOIKWD,SP70
          CALL     DEPTOI1
          GOTO     CLEAR000
.
CLEAR999  RETURN
.-------------------------------------------------
. Determine Words to Index
.-------------------------------------------------
BWORD000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      BWORD000 IF NOT EOS
            GOTO      BWORD999               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWORDS,SP30      * reset form pointer
          MOVE      KEY60,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      BWORD100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Save this Word
.
BWORD100  MOVE      KEYWORDS,KEY15
          PACK      KEY15,KEY15,SP70
          REP       UPPLOW,KEY15             * Always Upper Case
          PACK      KEY24,KEYINDEX,KEY15
          UNPACK    KEY24,PTOIICD,PTOIKWD
          CALL      RDPTOI1
          IF        OVRCD=1
            CALL      WRPTOI1
          ENDIF
          GOTO      BWORD190
.
.         Check for any more words
.
BWORD190  SETLPTR   KEYWORDS,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY60,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY60,KEYWORDS
          GOTO      BWORD000
.
BWORD999  RETURN
.
          INC       PATOICIO/INC
          INC       IBAOVRIO/INC
.
PATOICEP  ENDPROC                          * End of Procedure
.
