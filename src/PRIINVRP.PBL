.******************************************************************************
.*                                                                            *
.*                                 PRIINVRP                                   *
.*                                 --------                                   *
.*                                                                            *
.*    Procedure tor Reprint Bulk Private Practice Invoice - COMWEB70 (ibapol) *
.*    Copied from Reprint Invoice PRIWEB02                                    *
.*                                                                            *
.******************************************************************************
.*                                                                            *
.*    AUTHOR        :  J.Tan                                                  *
.*                                                                            *
.*    DATE          :  31/03/2023                                             *
.*                                                                            *
.*    Parameter     : KEY8 - Private Practice Invoice number                  *
.*                                                                            *
.*    MODIFICATIONS :                                                         *
.*                                                                            *
.******************************************************************************
.*        V12.00.01 24/07/2025  J.Tan          TSK 0962500                    *
.*                  Mod Cash to check for U/R number                          *
.*        V11.04.01 09/07/2024  J.Tan            TSK 0934944                  *
.*                  Mod to check Referral Provider no from a linked Practice  *
.*        V11.03.00 31/03/2023 J.Tan           TSK 0847250                    *
.*                  Created routine to re print invoice                       *
.******************************************************************************
          DEFPROC   PRIINVRP
.
          INC       PRIWEBVR/INC                 * for PRIINVPR
          INC       PRIVRDEB/INC                 * for PRIKYDEB
          INC       PRICONFD/INC
.
          INC       PRIDTRFD/INC
          INC       PRIHREFD/INC
          INC       PRIHTRFD/INC
          INC       PRIDOCFD/INC
          INC       PRIMGPFD/INC
.
          INC       PRISTAFD/INC
          INC       PRIFINFD/INC
          INC       PRIFIGFD/INC
          INC       HICITIFD/INC
          INC       PRIHISFD/INC
          INC       PRITESFD/INC
          INC       PRIHOLFD/INC                 * account hold file
          INC       PRIBULFD/INC
          INC       PRICNOFD/INC
          INC       PRIEXCFD/INC
          INC       PRIVAFFD/INC
          INC       PRIWCOFD/INC
          INC       PRIMABFD/INC
          INC       PRIHDBFD/INC
          INC       PRIPRAFD/INC
          INC       PATPERFD/INC
          INC       PATMESFD/INC
          INC       PRIAUIFD/INC                 * cash deposits invoiced audit
.
DIM28     DIM       28
FSTAMNT   FORM      12.2
FLAGMESS  FORM      1
HEADFLAG  FORM      1
SAVMESS   DIM       3
PRINTPDF  DIM       1
PRDTR030  DIM       8      * Linked Visit Number
MESSCODE  DIM       3      * invoice message code
CHKMPRAC  FORM      "1"
PATNAME   DIM       25
PATADD3   DIM       21
FORM122   FORM      12.2
FSTDATE   DIM       8
TEMPUNQ   FORM      8
EPRAC     DIM       6
SPRAC     DIM       6
.
REPRNT00  OPEN      PRIDTRA3,"pridtraf"          * Debtor Transaction file
          OPEN      PRIPRAC1,"pripracf"
          OPEN      PRIWCOM1,"priwcomf"
          OPEN      PRIMABD1,"primabdf"
          OPEN      PRIVAFD1,"privafdf"
          OPEN      PRIHREF1,"prihreff"
          OPEN      PRIDOCT1,"pridoctf"
          OPEN      PATMESG1,"patmesgf"
          OPEN      RCPDTEA3,"rcpdteaf"
          OPEN      RCPBNKA1,"rcpbnkaf"
          OPEN      PRIHDBT2,"prihdbtf"          * holding header file
.
          READ      CONTROLF,THIRTY4;*206,PRCNPRIH:
                                     *212,PRCNPRIT
          READ      CONTROLF,THIRTY3;*71,PRCNNAME,PRCNTELE,PRCNADDR,PRCNJOUR:
                                    *170,PRCNLAYT
.
          MOVE      ONE,VSCANPMI
          MOVE      ONE,COPINV
          MOVE      URNUMBER,VDEBTOR
          CALL      RDPRIN1
          BRANCH    OVRCD,REPRNT90
.
          OPEN      PRIDTRA3,"pridtraf"          * Debtor Transaction file
          MOVE      ZERO,NBNKDEPS
          MOVE      ZERO,CURRDEP
          CALL      LDDETAIL                 * Load PMI details into vars
          CALL      SETPRA00                 * Set PRFA
          MOVE      ZERO,FLAGBULK
          MOVE      ONE,WEBFLAG
.
          CALL      SINVH000                     * check for template header
          BRANCH    EXIT,REPRNT99
          CALL      SINVT000                     * check for template tail
          BRANCH    EXIT,REPRNT99
.
          MOVE      PRINNUMB,PRCNINVN
          MOVE      "Error - invalid practice",PRPRDESC
          MOVE      PRINPRAC,KEY6
          CALL      RDPRPR1                 * read the practice file
.
          COMPARE   ZERO,PRPRTIME           * see if we want the service time
          GOTO      REPRNT05 IF EQUAL         on the invoice
          CALL      GETS0000                * Get service time
.
.------ get the claim code details if they exist ------
.
REPRNT05  CALL      GETC0000                * get the claim code details if
.                                             they exist
          CALL      OPNPRINT
.
          MOVE      ZERO,HEADFLAG
          MOVE      ZERO,FLAGMESS
          MOVE      SP70,VRFDOCT
          MOVE      SP8,SAVRDATE
          UNPACK    SP70,VRFDCNAM,VRFDOCT,VRFDPROV,VREFERD
.
          CALL      GIND0000                * Get default patient indicator
.
          PACK      KEY24,VDEBTOR,VSCANPMI,PRINNUMB,SP6
          CALL      RSPRDT3                 * position on the debtors
.                                             transaction file
.------ read through the debtors transaction file ------
.
REPRNT10  CALL      RKPRDT3
          BRANCH    OVRCD,REPRNT70
.
          MATCH     VDEBTOR,PRDTDEBT        * compare debtors
          GOTO      REPRNT70 IF NOT EQUAL
.
          COMPARE   VSCANPMI,PRDTSCAN       * compare scan flags
          GOTO      REPRNT70 IF NOT EQUAL
.
          MATCH     PRINNUMB,PRDTINVN       * match invoice numbers
          GOTO      REPRNT70 IF NOT EQUAL
.
          IF        PRDTRTYP=1
            COMPARE   TWO,PRDTIFLG
            GOTO      REPRNT10 IF EQUAL     * IMC details
          ENDIF
.
          IF        HEADFLAG=0
            MOVE      PRINCLAM,TMPCLAIM
            PACK      KEY27,PRDTUNIQ,PRDTPRAC,PRDTDOCT,PRDTCODE,SP70
            CALL      RDPRHR1
            IF        OVRCD=0
              MOVE      PRHRUNIQ,KEY8
              CALL      RDPRHD2
              IF        OVRCD=1
                MOVE      SP70,PRHDHFND
              ENDIF
            ENDIF
            CALL      PIHED000                * print the invoice header
            MOVE      ONE,HEADFLAG
          ENDIF
.
          BRANCH    FLAGMESS,REPRNT20       * skip if we have the message code
          MOVE      PRDTMESS,SAVMESS
          MOVE      ONE,FLAGMESS
.
.------ match referring doctors ------
.
REPRNT20  MATCH     PRDTREFD,VRFDOCT        * same ref. doctor?
          GOTO      REPRNT30 IF NOT EQUAL   * no
.
          MATCH     PRDTRDTE,SAVRDATE       * same ref. date?
          GOTO      REPRNT60 IF EQUAL       * yes, details same as previous
.
REPRNT30  MATCH     SP70,PRDTREFD           * see if we have a referring doctor
          GOTO      REPRNT60 IF EQUAL
          MOVE      PRDTOPS,TMPOPS
          MOVE      SP10,REFDATE
.
          MATCH     SP8,PRDTRDTE            * see if we have a referring date
          GOTO      REPRNT40 IF EQUAL
.
          UNPACK    PRDTRDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * pack the date into required format
          MOVE      CPCDATE,REFDATE
.
.------ set up the referral details ------
.
REPRNT40  MOVE      PRDTREFT,VREFERD
          MOVE      PRDTREFD,VRFDOCT
          MOVE      PRDTRDTE,SAVRDATE
          MOVE      "                           ",VRFDCNAM
          MOVE      SP10,VRFDPROV
.
          MOVE      SP10,RPERIOD
          PACK      KEY5,ANSR,ANSF,PRDTRFPD
          CALL      RDCODE1
          IF        OVRCD<>1
            MOVE      TDESC,RPERIOD
          ENDIF
.
          PACK      KEY27,PRDTUNIQ,PRDTPRAC,PRDTDOCT,PRDTCODE,SP70
          CALL      RDPRHR1
          IF        OVRCD=0
            MATCH     SP70,PRHRREFD
            IF        !@EQUAL & !@EOS
              OPEN      PMSHCLA1,"pmshclaf"
              PACK      KEY20,PRHRREFD,PRHRHCPP,SP70
              CALL      RDPMHCL1
              IF        OVRCD=0
                MATCH     SP70,PMHLPRV1
                IF        !@EQUAL
                  MOVE      PMHLPRV1,VRFDPROV  * provider from a Linked Practice
                ENDIF
              ENDIF
            ENDIF
          ENDIF
.
          PACK      KEY10,VRFDOCT,SP10
          CALL      RDPMHCP1                * read the patient doctor file
          BRANCH    OVRCD,REPRNT50
          MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          MOVE      PMHCTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME                 * pack the doctors name
          MOVE      PACFNAME,VRFDCNAM
          MATCH     SP70,VRFDPROV
          IF        @EQUAL
            MOVE      PMHCPRV1,VRFDPROV
          ENDIF
.
.------ print the referring doctor details ------
.
REPRNT50  MOVE      SP70,VREFRPER
          PACK      KEY27,PRDTUNIQ,PRDTPRAC,PRDTDOCT,PRDTCODE,SP70
          CALL      RDPRHR1
          IF        OVRCD=0
            MATCH     SP70,PRHRRFPD
            IF        !@EQUAL & !@EOS
              PACK      KEY5,ANSR,ANSF,PRHRRFPD,SP70
              CALL      RDCODE1
              IF        OVRCD=0
                MOVE      TDESC,VREFRPER
              ENDIF
            ENDIF
          ENDIF
          CALL      PRDOC000
.
.------ set up the rest of the details to print for the current transaction ---
.
REPRNT60  MOVE      ONEHUND,PRDOFEEP
          PACK      KEY22,PRDTPRAC,PRDTDOCT,PRDTCLAM,PRDTCODE
          CALL      RDPRDO1                 * read the practice doctor file
.
          CALL      PRINV000                * print the current transaction
          MOVE      TRUE,FLAGSOME
          GOTO      REPRNT10
.
REPRNT70  MOVE      ZERO,NBNKDEPS
          PACK      SAVKEY12,SP4,PRINNUMB
          PACK      KEY29,SAVKEY12,SP70
          CALL      RSRCDTE3
REPRNT72  CALL      RKRCDTE3
          BRANCH    OVRCD,REPRNT78
.
          MATCH     SAVKEY12,RCDTINVN       * Same invoice number?
          GOTO      REPRNT78 IF NOT EQUAL
.
          MATCH     PRINDEBT,RCDTURNO       * Same Debtor number?
          GOTO      REPRNT72 IF NOT EQUAL
.
          PACK      KEY12,RCDTRECN,SP70
          CALL      RDRCBNK1
          BRANCH    OVRCD,REPRNT72
.
          MATCH     "0",RCBNSTAT        * Cash Not banked?
          GOTO      REPRNT72 IF NOT EQUAL
.
          ADD       RCDTAMNT,NBNKDEPS
          GOTO      REPRNT72
.
REPRNT78
..         COMPARE   ZERO,NBNKDEPS
.          GOTO      REPRNT80 IF EQUAL
.          CALL      CNTIV000                * check for invoice continuation
.          IF        IBCNUGST=2 & PTCNINVB=1
.            PRINT     *13,"Non-Banked deposits",*122,NBNKDEPS
.          ELSE
.            PRINT     *13,"Non-Banked deposits",*68,NBNKDEPS
.          ENDIF
.          ADD       NBNKDEPS,PINVPAID
.          ADD       PINVPAID,PINVTOT
.
REPRNT80  CALL      PCRDN000                * Print credit note before cash
          PACK      PMLINE1 WITH SP30,SP30,SP5
          PACK      PMLINE2 WITH SP30,SP30,SP5
          MOVE      SAVMESS,KEY3
          CALL      RDMESG1                 * read the message file
          CALL      PRITL000                * print the invoice tail
.
.         MATCH     "1",PRINTPDF
.         IF        @EQUAL
.           PACK      SELPRINT,Z70
.         ENDIF
          CALL      CLSPRINT
.
.         MATCH     "1",PRINTPDF
.         IF        @EQUAL
.           CALL      NEWCOR00
.           BRANCH    EXIT,REPRNT99
.         ENDIF
.
.         CALL      LADV0000                * Print Lodgement Advice - PCS
.         CALL      LADW0000                * Print Lodgement Advice - PCIW
.         CALL      DB4F0000                * DB4 Form DBS
.         CALL      DB4W0000                * DB4 Form BBSW
.
          MOVE      ZERO,EXIT
          GOTO      REPRNT99
.
REPRNT90  MOVE      ONE,EXIT
          GOTO      REPRNT99
.
REPRNT99  GOTO      NPRNT999
+
.------------------------------------------------------------------------------
.         Load debtors details
.------------------------------------------------------------------------------
LDDETAIL  MOVE      ONE,OVRCD
          MOVE      SP30,PSNAME
          MOVE      SP30,PGNAME
          MOVE      SP30,FNSNAME
          MOVE      SP70,VDEBNAME
          MOVE      VDEBTOR,PURNO
          MOVE      PURNO,PRHDDEBT
          MOVE      PURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,LDDET900
.
.         Load display variables
.
          MOVE      PSNAME,FNSNAME
          MOVE      SP30,VDEBNAME
          MATCH     SP30,PSNAME
          GOTO      LDDET100 IF NOT EQUAL
          MOVE      "[Missing Surname]",PSNAME
LDDET100  MOVE      PSNAME,VDEBNAME
          RESET     VDEBNAME,30
LDDET200  CMATCH    SP1,VDEBNAME
          GOTO      LDDET300 IF NOT EQUAL
          BUMP      VDEBNAME,-1
          GOTO      LDDET200
LDDET300  APPEND    ",",VDEBNAME
          APPEND    SP1,VDEBNAME
          APPEND    PGNAME,VDEBNAME
          RESET     VDEBNAME
.
          MOVE      PBDEBT,VBADEBT
          MOVE      PADD1,VDEBADD1
          MOVE      PADD2,VDEBADD2
          MOVE      PSUBRB,VDEBADD3
          MOVE      PADD4,VDEBADD4
          MOVE      PPOST,VDEBPOST
          MOVE      PTELEP,VDEBTELP
          MOVE      PTELEB,VDEBTELB
.
          MOVE      SP10,VDOB
          MATCH     SP8,PBDATE                   * any dob ?
          GOTO      LDDET800 IF EQUAL            * no
.
          UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,VDOB
.
LDDET800  MOVE      ZERO,OVRCD
LDDET900  RETURN
+
.------------------------------------------------------------------------------
.         Set PRFA details for reprint invoice
.------------------------------------------------------------------------------
SETPRA00  MOVE      PRINNAMR,TMPRESP
          MOVE      PRINPRA1,TMPRADD1
          MOVE      PRINPRA2,TMPRADD2
          MOVE      PRINPRA3,TMPRADD3
          MOVE      PRINPRA4,TMPRADD4
          MOVE      PRINPOST,TMPRPOST
          MOVE      PRINTELP,TMPRPTEL
          MOVE      PRINTELB,TMPRBTEL
          MOVE      PRINMPHN,TMPRMPHN
          MOVE      PRINRELP,TMPRREL
          MOVE      PRINDOCT,VSVDOCT
          MOVE      "                              ",VSVDCNAM
          MOVE      "                              ",PRIMTYPE
          MOVE      SP10,VSVDPROV
.
          UNPACK    SP70,VSVDCNAM,VSVDPROV,PRIMTYPE
          PACK      KEY10,PRINDOCT,SP10
          CALL      RDPMHCP1                * read the doctors file
          BRANCH    OVRCD,SETPRA99
.
          MOVE      PMHCSNAM,PACSNAME
          MOVE      PMHCGNAM,PACGNAME
          MOVE      PMHCTITL,PACTITLE
          MOVE      ONE,PACFRMT
          CALL      PACNAME                 * pack the doctors name
          MOVE      PACFNAME,VSVDCNAM
          MOVE      PMHCPRV1,VSVDPROV
.
          MATCH     SP3,PMHCSPC1            * see if the doctors type exists
          GOTO      SETPRA99 IF EQUAL
.
          PACK      KEY5,CODEDT,PMHCSPC1
          CALL      RDCODE1                 * read the patient codes file
          BRANCH    OVRCD,SETPRA99
          MOVE      TDESC,PRIMTYPE
.
SETPRA99  RETURN
+
.**********************************************************************
.*                                GETS0000                            *
.*       Routine to get the service time for the invoice from the     *
.*       first transaction on the debtors transaction file            *
.**********************************************************************
GETS0000  PACK      KEY24,VDEBTOR,VSCANPMI,PRINNUMB,SP6
          CALL      RSPRDT3                 * get the first record on the
          CALL      RKPRDT3                   debtors transaction file
          BRANCH    OVRCD,GETS9000
.
          MATCH     VDEBTOR,PRDTDEBT        * compare debtor numbers
          GOTO      GETS9000 IF NOT EQUAL
.
          COMPARE   VSCANPMI,PRDTSCAN       * compare scan flags
          GOTO      GETS9000 IF NOT EQUAL
.
          MATCH     PRINNUMB,PRDTINVN       * match invoice numbers
          GOTO      GETS9999 IF EQUAL
.
.------ we could not get the first debtors transaction record ------
.
.
GETS9000  MOVE      SP6,PRDTSTIM
.
GETS9999  RETURN
+
.**********************************************************************
.*                               GETC0000                             *
.*     Routine to get the claim details to print if required          *
.**********************************************************************
GETC0000  MOVE      ZERO,COUNTR
          MOVE      SP3,CLAIMTYP
          MOVE      SP20,CLAIMNUM
          MOVE      SP10,CLAIMDTE
          MOVE      SP30,EMPLNAME
.
          MATCH     SP3,PRINCLAM            * see if the claim type exists
          GOTO      GETC9999 IF EQUAL
.
          PACK      KEY5,CODECL,PRINCLAM
          CALL      RDCODE1                 * read the patient codes file
          BRANCH    OVRCD,GETC9999
.
.------ check the next patient indicator ------
.
GETC1000  ADD       ONE,COUNTR
.
          COMPARE   SIX,COUNTR              * see if no more indicators to
          GOTO      GETC9999 IF EQUAL         process
.
          LOAD      INDICATR,COUNTR,TCINDC1,TCINDC2,TCINDC3,TCINDC4,TCINDC5
.
          CMATCH    ANSW,INDICATR           * see if we have a workers
          GOTO      GETC2000 IF EQUAL         compensation claim
.
          CMATCH    ANSM,INDICATR           * see if we have a M.A.B. claim
          GOTO      GETC3000 IF EQUAL
.
          CMATCH    ANSV,INDICATR           * see if we have a veterans affairs
          GOTO      GETC4000 IF EQUAL         claim
.
          GOTO      GETC1000
.
. ------- Workers Compensation -------
.
GETC2000  PACK      KEY16,PRCNINVN,PRINCNUM
          CALL      RDPRWC1                 * read the workers compensation
          BRANCH    OVRCD,GETC1000            claim file
.
          MATCH     SP3,CLAIMTYP
          GOTO      GETC1000 IF NOT EQUAL
          MOVE      "WC ",CLAIMTYP
          MOVE      PRWCCLMN,CLAIMNUM
          MOVE      PRWCNAME,EMPLNAME
.
          MATCH     SP8,PRINADTE
          GOTO      GETC1000 IF EQUAL
.
          UNPACK    PRINADTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * pack the date
          MOVE      CPCDATE,CLAIMDTE
          GOTO      GETC1000
.
. ------- Motor Accident Board -------
.
GETC3000  PACK      KEY16,PRCNINVN,PRINCNUM
          CALL      RDPRMA1                 * read the transport accident
          BRANCH    OVRCD,GETC1000            commission file
.
          MATCH     SP3,CLAIMTYP
          GOTO      GETC1000 IF NOT EQUAL
          MOVE      "TAC",CLAIMTYP
          MOVE      PRMAREFN,CLAIMNUM
.
          MATCH     SP8,PRINADTE
          GOTO      GETC1000 IF EQUAL
.
          UNPACK    PRINADTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * pack the date
          MOVE      CPCDATE,CLAIMDTE
          GOTO      GETC1000
.
. ------- Veterans Affairs -------
.
GETC4000  PACK      KEY16,PRCNINVN,PRINCNUM
          CALL      RDPRVA1                 * read the veterans affairs file
          BRANCH    OVRCD,GETC1000
.
          MATCH     SP3,CLAIMTYP
          GOTO      GETC1000 IF NOT EQUAL
          MOVE      "VA ",CLAIMTYP
          MOVE      PRVACLAM,CLAIMNUM
          GOTO      GETC1000
.
GETC9999  RETURN
+
.------------------------------------------------------------------------------
.         Get Patient Indicator from DTR file
.------------------------------------------------------------------------------
GIND0000  MOVE      SP70,TMPPIND
          PACK      KEY24,VDEBTOR,VSCANPMI,PRINNUMB,SP6
          CALL      RSPRDT3                 * position on the debtors
GIND1000  CALL      RKPRDT3
          BRANCH    OVRCD,GIND9999
.
          MATCH     VDEBTOR,PRDTDEBT        * compare debtors
          GOTO      GIND9999 IF NOT EQUAL
.
          COMPARE   VSCANPMI,PRDTSCAN       * compare scan flags
          GOTO      GIND9999 IF NOT EQUAL
.
          MATCH     PRINNUMB,PRDTINVN       * match invoice numbers
          GOTO      GIND9999 IF NOT EQUAL
.
          COMPARE   ONE,PRDTRTYP
          GOTO      GIND1000 IF NOT EQUAL   * not an Item Transaction recordtype
.
          MOVE      PRDTCODE,TMPPIND
GIND9999  RETURN
+
.------------------------------------------------------------------------------
.         Print credit note line for re-print invoice
.------------------------------------------------------------------------------
PCRDN000  BRANCH    FLAGBULK,PCRDN999       * skip if bulk billing the patient
          MOVE      ZERO,FORM12P2
          MOVE      ZERO,FORM122
          MOVE      ZERO,CREDTOT
          OPEN      PRICNOA3,"pricnoaf"
          PACK      KEY16,PRINNUMB,SP70
          CALL      RSPRCNO3
PCRDN100  CALL      RKPRCNO3
          BRANCH    OVRCD,PCRDN200
          MATCH     PRINNUMB,PRCOINVN
          GOTO      PCRDN200 IF NOT EQUAL
.
          ADD       PRCOAMNT,FORM12P2     * Total credit
          SUB       PRCOGSTM,FORM12P2     * exclude GST
          ADD       PRCOGSTM,FORM122     * Total GST
          GOTO      PCRDN100
.
PCRDN200  MOVE      FORM12P2,CREDTOT
          ADD       FORM122,CREDTOT
          COMPARE   ZERO,CREDTOT
          GOTO      PCRDN999 IF EQUAL   * No credit note
.
          CALL      CNTIV000                * check for invoice continuation
          IF        PRCNLAYT=2
            PRINT     *23,"Credit Note":    * Cabrini
                      *N,*64,CREDTOT
            ADD       ONE,VLINENO
            GOTO      PCRDN300
          ELSE
            PRINT     *12,"Credit Note";
          ENDIF
.
          IF        IBCNUGST=2 & PTCNINVB=1
            MOVE      CREDTOT,FORM8P2
            PRINT     *122,FORM8P2
            GOTO      PCRDN300
          ENDIF
          IF        PRCNLAYT=1
            MOVE      CREDTOT,FORM7P2
            PRINT     *68,FORM7P2
          ELSE
            IF        PRCNLAYT=0 | PRCNLAYT=2 | PRCNLAYT=4 | PRCNLAYT=5
              MOVE      CREDTOT,FORM8P2
              PRINT     *68,FORM8P2
            ELSE
              PRINT     *68,CREDTOT
            ENDIF
          ENDIF
.
PCRDN300  ADD       CREDTOT,PINVTOT
          ADD       ONE,VLINENO
PCRDN999  RETURN
+
.****************************************************************************
.*                                GPIND000                                  *
.*                       Get the patient  indicator                         *
.****************************************************************************
GPIND000  MOVE      SP1,TCINDC1
.
          PACK      TMPPIND,TMPPIND,SP3
          MATCH     SP3,TMPPIND
          GOTO      GPIND999 IF EQUAL
.
          PACK      KEY5,CODEGI,TMPPIND
          CALL      RDCODE1
          BRANCH    OVRCD,GPIND999
.
GPIND999  RETURN
+
.------------------------------------------------------------------------------
.
.
.
          INC       PRIINVHL
          INC       PRIINVTL
          INC       PRIINVHT
.
          INC       PRIDTRIO/INC
          INC       PRIHREIO/INC
          INC       PRIHTRIO/INC
          INC       PRIDOCIO/INC
          INC       PRIMGPIO/INC
.
          INC       PRISTAIO/INC
          INC       PRIFINIO/INC
          INC       PRIFIGIO/INC
          INC       HICITIIO/INC
          INC       PRIHISIO/INC
          INC       PRITESIO/INC
          INC       PRIHOLIO/INC                 * account hold file
          INC       PRIBULIO/INC
          INC       PRICNOIO/INC
          INC       PRIEXCIO/INC
          INC       PRIVAFIO/INC
          INC       PRIWCOIO/INC
          INC       PRIMABIO/INC
          INC       PRIHDBIO/INC
          INC       PRIPRAIO/INC
          INC       PATPERIO/INC
          INC       PATMESIO/INC
          INC       PRIAUIIO/INC                 * cash deposits invoiced audit
.
          INC       IBAOVRIO/INC
.
NPRNT999  ENDPROC

