.******************************************************************************
.* System         : Patient Billing                                           *
.* Include Name   : ABFOTDET.PBL                                              *
.* Author         : J.Tan/Peter Vela                                          *
.* Date           : 11/07/2019  Task 0857392                                  *
.* Modification   :                                                           *
.*       V12.00.01 14/05/2025  Ebon Clements  TSK 0955096                     *
.*                 Alphanueric visit number changes                            *
.*       V11.03.01 30/05/2023  J.Tan          TSK 0923703                     *
.*                 Mod to 6 decimal places in NWAU calculations               *
.*       V11.01.01 22/08/2022  J.Tan           TSK 0906597                    *
.*                 Mod to display ABF/NWAU calculation (claim code Indc2=N)   *
.*       V10.15.01 10/06/2020  J.Tan           TSK 0886178                    *
.*                 Mod to display ABF Calculations                            *
.*       V10.14.01 24/09/2019 J.Tan      TSK 0857392                          *
.*                 Mod to check if ABF invoice raised                         *
.*       V10.14.02 08/10/2019 J.Tan       TSK 0857392                         *
.*                 Mod to display Clinic ID and Description                   *
.*       V10.15.01 31/12/2019  J.Tan      TSK 0886176                         *
.*                 Mod checking for INVOICNO variable                         *
.*                                                                            *
.******************************************************************************
ABFOTDET  MOVE      ZERO,PWAMOUNT
          MOVE      ZERO,ANMCAMNT
          MOVE      ZERO,AINDAMNT
          MOVE      ZERO,ARESAMNT
          MOVE      ZERO,ATAAMNTS
          MOVE      ZERO,NEPAMNTS
          MOVE      ZERO,ABFCHRGE
          MOVE      ZERO,NWAUAMNT
.
          READ      CONTROLF,HUND28;*105,PTCNUABF
          MATCH     "1",PTCNUABF
          GOTO      ABFO9999 IF NOT EQUAL   * Not using ABF
.
          PACK      KEY5,ANSC,ANSL,OBACOMP
          CALL      RDCODE1
          BRANCH    OVRCD,ABFO9999
.
          BRANCH    NWAUFLAG,ABFO0100      * NWAU Calculations
.
          MATCH     "A",TCINDC2
          GOTO      ABFO9999 IF NOT EQUAL  * Not an ABF Funding
          GOTO      ABFO0200
.
ABFO0100  MATCH     "N",TCINDC2              * Check for NWAU Calculations
          GOTO      ABFO9000 IF NOT EQUAL    * Not an ABF Funding
.
          GOTO      ABFO0300
.
ABFO0200  PACK      KEY5,ANSN,ANSC,OTBBTCOD  * check Tier 2 code
          CALL      RDCODE1
          BRANCH    OVRCD,ABFO9999
          MATCH     "A",TCINDC1
          GOTO      ABFO9999 IF NOT EQUAL
.
ABFO0300  MOVE      ZERO,CALCFLAG
          MATCH     "20210101",OBADATE
          GOTO      ABFO0800 IF LESS
.
          MOVE      ONE,CALCFLAG
          MATCH     "20220701",OBADATE
          GOTO      ABFO0800 IF LESS
.
          MOVE      TWO,CALCFLAG           * 2023/2024 ABF calculation
.
.         Check if an Invoice number was selected
.
ABFO0800  PACK      INVOICNO,INVOICNO,SP70
          MATCH     SP70,INVOICNO
          GOTO      ABFO3000 IF NOT EQUAL
.
.         Check if ABF Invoice raised already
.
          OPEN      PATINVR3,"patinvrf"
          PACK      KEY16,OBAOUTNO,Z70
          CALL      RSPTINV3
ABFO1000  CALL      RPPTINV3
          BRANCH    OVRCD,ABFO2000
.
          MATCH     PINVADM,OBAOUTNO
          GOTO      ABFO2000 IF NOT EQUAL
.
          REP       " 0",PTINCRST
          MATCH     "0",PTINCRST
          GOTO      ABFO1000 IF NOT EQUAL  * Credit note
.
          COMPARE   THREE,PTINCMIX
          GOTO      ABFO1000 IF NOT EQUAL
.
          MOVE      PINVNO,INVOICNO
          GOTO      ABFO3000          * found an ABF Invoice - do not calc.ABF
.
ABFO2000  MOVE      ZERO,PROGFLAG
          MOVE      ZERO,NWAUAMNT
          PROC      ABFOTINV          * Calculate ABF Invoice
          MOVE      SP70,INVOICNO
.
ABFO3000  OPEN      PMSABFA3,"pmsabfaf"
          OPEN      PMSABFA3,"pmsabfaf"
.
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE                 * Format the admin date
          REP       " 0",CPCDATE
          PACK      KEY16,CPCDATE,SP20
.
          WRITE     HTMLFILE;"<table border=1 width=100%>":
                             "<tr><td width=30%>Attended On:</td>":
                             "<td width=70%><b>&nbsp;",KEY16,"</b></td>":
                             "</tr>"; 
.
          MOVE      SP70,TDESC
          MATCH     SP70,OBACOMP
          IF        !@EQUAL & !@EOS
            PACK      KEY5,ANSC,ANSL,OBACOMP,SP70
            CALL      RDCODE1
            IF        OVRCD=1
              MOVE      SP70,TDESC
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td>Claim Code:</td>":
                               "<td><b>&nbsp;",OBACOMP,"</b>":
                               " - <b>",TDESC,"</b></td>":
                             "</tr>";
.
          PACK      KEY19,OBAOUTNO,ZERO,THIRTY2,INVOICNO,SP70
          CALL      RDPMABF3
          IF        OVRCD=ONE
            CALL      CLPMSABF
          ENDIF
.
          CALL      GCLN0000         * Get Clinic ID
          BRANCH    EXIT,ABFO9999
.
          WRITE     HTMLFILE;"<tr><td>Tier 2:</td>":
                             "<td><b>&nbsp;",PMABCDES,"</b>  ( ",KEY40,")":
                             "</td>":
                             "</tr>";
.
          PACK      KEY19,OBAOUTNO,ZERO,TWENTY1,INVOICNO,SP70
          CALL      RDPMABF3
          IF        OVRCD=ONE
            CALL      CLPMSABF
          ENDIF
.
          MOVE      PMABADJV,FORM10P4
          MOVE      PMABADJV,AINDAMNT
          STRIP     PMABCDES
.
          WRITE     HTMLFILE;"<tr><td>Indigenous Status:</td>":
                            "<td><b>&nbsp;",FORM10P4," ( ",PMABCDES,")</b></td>":
                             "</tr>";
.
          PACK      KEY19,OBAOUTNO,ZERO,THIRTY3,INVOICNO,SP70
          CALL      RDPMABF3
          IF        OVRCD=ONE
            CALL      CLPMSABF
          ENDIF
.
          UNPACK    PMABCDES,KEY3
          MOVE      PMABADJV,FORM10P4
          MOVE      PMABADJV,ANMCAMNT
.
          MATCH     "Yes",KEY3
          IF        @EQUAL
            WRITE     HTMLFILE;"<tr><td>Multidisciplinary Clinic:</td>":
                               "<td><b>&nbsp;",FORM10P4," ( ",KEY3,")</b></td>":
                               "</tr>";
          ELSE
            WRITE     HTMLFILE;"<tr><td>Multidisciplinary Clinic:</td>":
                               "<td><b>&nbsp;",KEY3,"</b></td>":
                               "</tr>";
          ENDIF
.
.         Patient Remoteness Area Adjustment
.
          MOVE      SP70,DIM3
          MOVE      ZERO,FORM10P4
          PACK      KEY19,OBAOUTNO,ZERO,TEN6,INVOICNO,SP70
          CALL      RDPMABF3
          IF        OVRCD=ONE
            CALL      CLPMSABF
          ELSE
            MOVE      PMABADJV,FORM10P4
            MOVE      PMABADJV,ARESAMNT
            MOVE      "R2 ",DIM3
          ENDIF
.
          IF        PMABADJV = ZERO
            PACK      KEY19,OBAOUTNO,ZERO,TEN7,INVOICNO,SP70
            CALL      RDPMABF3
            IF        OVRCD=ONE
              CALL      CLPMSABF
            ELSE
              MOVE      PMABADJV,FORM10P4
              MOVE      PMABADJV,ARESAMNT
              MOVE      "RA3",DIM3
            ENDIF
          ENDIF
.
          IF        PMABADJV = ZERO
            PACK      KEY19,OBAOUTNO,ZERO,TEN9,INVOICNO,SP70
            CALL      RDPMABF3
            IF        OVRCD=ONE
              CALL      CLPMSABF
            ELSE
              MOVE      PMABADJV,FORM10P4
              MOVE      PMABADJV,ARESAMNT
              MOVE      "RA4",DIM3
            ENDIF
          ENDIF
.
          WRITE     HTMLFILE;"<tr><td>Remote Area Adjustment:</td>";
.
          MATCH     SP70,DIM3
          IF        @EQUAL
            WRITE     HTMLFILE;"<td><b>&nbsp;",FORM10P4,"</b></td>";
          ELSE
            WRITE     HTMLFILE;"<td><b>&nbsp;",FORM10P4,"(",DIM3,")</b></td>";
          ENDIF
.
          WRITE     HTMLFILE;"</tr>";
.
          MOVE      SP70,DIM3
          MOVE      ZERO,FORM10P4
          PACK      KEY19,OBAOUTNO,ZERO,THIRTY6,INVOICNO,SP70
          CALL      RDPMABF3
          IF        OVRCD=ONE
            CALL      CLPMSABF
          ELSE
            MOVE      "RA3",DIM3
          ENDIF
.
          IF        PMABADJV=ZERO
            PACK      KEY19,OBAOUTNO,ZERO,THIRTY7,INVOICNO,SP70
            CALL      RDPMABF3
            IF        OVRCD=ONE
              CALL      CLPMSABF
            ELSE
              MOVE      "RA4",DIM3
            ENDIF
          ENDIF
.
          MOVE      PMABADJV,FORM10P4
          MOVE      PMABADJV,ATAAMNTS
.
          WRITE     HTMLFILE;"<tr><td>Hospital Remoteness Area:</td>";
.
          MATCH     SP70,DIM3
          IF        @EQUAL
            WRITE     HTMLFILE;"<td><b>&nbsp;",FORM10P4,"</b></td>";
          ELSE
            WRITE     HTMLFILE;"<td><b>&nbsp;",FORM10P4,"(",DIM3,")</b></td>";
          ENDIF
          WRITE     HTMLFILE;"</tr>";
.
          PACK      KEY19,OBAOUTNO,ZERO,FIFTY3,INVOICNO,SP70
          CALL      RDPMABF3
          IF        OVRCD=ONE
            CALL      CLPMSABF
          ELSE
            MATCH     SP70,INVOICNO
            IF        !@EQUAL
              MOVE      SP70,KEY19
              UNPACK    PMABCDES,KEY19      * get NWAU amount with 6 decimal
              SQUEEZE   KEY19
              MOVE      KEY19,NWAUAMNT
            ENDIF
          ENDIF
          MOVE      PMABADJV,FORM10P4
.
          WRITE     HTMLFILE;"<tr><td>NWAU:</td>":
                             "<td><b>&nbsp;",FORM10P4,"</b></td>":
                             "</tr>";
.
          PACK      KEY19,OBAOUTNO,ZERO,SIXTY2,INVOICNO,SP70
          CALL      RDPMABF3
          IF        OVRCD=ONE
            CALL      CLPMSABF
          ENDIF
          MOVE      PMABADJV,FORM10P4
          MOVE      PMABADJV,PWAMOUNT
          WRITE     HTMLFILE;"<tr><td width=30%>Price Weight:</td>":
                             "<td width=70%><b>&nbsp;",FORM10P4,"</b></td>":
                     "</tr>";
.
          PACK      KEY19,OBAOUTNO,ZERO,SIXTY1,INVOICNO,SP70
          CALL      RDPMABF3
          IF        OVRCD=ONE
            CALL      CLPMSABF
          ENDIF
          MOVE      PMABADJV,FORM10P4
          MOVE      PMABADJV,NEPAMNTS
          WRITE     HTMLFILE;"<tr><td width=30%>NEP:</td>":
                             "<td width=70%><b>&nbsp;",FORM10P4,"</b></td>":
                     "</tr></table>";
.
          CALL      OCAL00000                 * Display ABF calculation
          GOTO      ABFO9999
.
ABFO9000  WRITE     HTMLFILE;"<tr><center><b>ABF/NWAU Details ":
                             "are not applicable to this event</b></td>":
                             "</center></tr>";
.
ABFO9999  RETURN
+
.*******************************************************************************
.         Display ABF Calculation
.*******************************************************************************
OCAL0000  CALL      OTBF0000                  * Calculate ABF amount
          MOVE      ABFCHRGE,FORM12P2
.
          BRANCH    CALCFLAG,OCAL1000:        * 2020 Calculation
                             OCAL2000         * 2023/2024
.
.         {PW x (1 + Anmdc) x (1 + Aind + Ares)} x NEP
.
          WRITE     HTMLFILE;"<table border=1 width=100%>":
                             "<tr>&nbsp;</tr><tr><td>":
                      "ABF Calculation:</td></tr>":
                      "<tr><td>":
                      "= {PW x (1 + Anmdc) x ":
                      "(1 + AInd + Ares )} x ":
                      "NEP</td></tr>";
.
          WRITE     HTMLFILE;"<tr><td>":
                      "= { ",PWAMOUNT," x (1 + ",ANMCAMNT," ) x ":
                      "(1 + ",AINDAMNT," + ",ARESAMNT," )} x ":
                      "$",NEPAMNTS," </td></tr>";
.
          GOTO      OCAL8000
.
.         New ABF calculation
.
.         {PW x APaed x (1 + ARes + AInd+ ANMC) )} x NEP
.
OCAL1000  WRITE     HTMLFILE;"<table border=1 width=100%>":
                             "<tr>&nbsp;</tr><tr><td>":
                      "ABF Calculation:</td></tr>":
                      "<tr><td>":
                      "= {PW x APaed x ":
                      "(1 + ARes + AInd + ANMC)} x ":
                      "NEP</td></tr>";
.
          WRITE     HTMLFILE;"<tr><td>":
                      "= { ",PWAMOUNT," x 1 x (1 + ",ARESAMNT," + ":
                      AINDAMNT," + ",ANMCAMNT," )} x ":
                      "$",NEPAMNTS,"</td></tr>";
          GOTO      OCAL8000
.
.         2023/2024
.
OCAL2000  WRITE     HTMLFILE;"<table border=1 width=100%>":
                             "<tr>&nbsp;</tr><tr><td>":
                      "ABF Calculation:</td></tr>":
                      "<tr><td>":
                      "= {PW x APaed x ":
                      "(1 + ARes + AInd + ANMC) x (1 + ATreat)}":
                      " x NEP</td></tr>";
.
          WRITE     HTMLFILE;"<tr><td>":
                      "= { ",PWAMOUNT," x 1 x (1 + ",ARESAMNT," + ":
                      AINDAMNT," + ",ANMCAMNT," )} x (1 + ",ATAAMNTS,")}":
                      " x $",NEPAMNTS,"</td></tr>";
          GOTO      OCAL8000
.
OCAL8000  WRITE    HTMLFILE;"<tr><td>":
                      "= $",FORM12P2,"</td></tr><table>"
.
OCAL9999  RETURN
+
.******************************************************************************
.         Get Clinic ID
.******************************************************************************
GCLN0000  MOVE      ZERO,EXIT
          PACK      KEY40,SP70
.
          OPEN      PATVISA1,"patvisaf"
          PACK      KEY8,OBAOUTNO
          CALL      RDVISA1
          BRANCH    OVRCD,GCLN9000
.
          CALL      COTFIL00
.
          PACK      KEY36,OBAOUTNO,PVIDATE,SP70
          CALL      RDSBOKA6
          CALL      RDKBOKA6
          BRANCH    OVRCD,GCLN9000
.
          MATCH     OBAOUTNO,PVIBILL     * Check if same Admission
          GOTO      GCLN9000 IF NOT EQUAL
.
          PACK      KEY20,OBASITE,OBACLIN,OBADATE,SP70
          CALL      RDCLIA1
          COMPARE   ZERO,OVRCD
          GOTO      GCLN2000 IF EQUAL
.
          PACK      KEY20,OBASITE,OBACLIN,OBADATE,Z70
          CALL      RDSCLIA1
          CALL      RDPCLIA1
          BRANCH    OVRCD,GCLN9000
.
          MATCH     OBASITE,OCASITE
          GOTO      GCLN9000 IF NOT EQUAL    * different site
.
          MATCH     OBACLIN,OCACLIN
          GOTO      GCLN9000 IF NOT EQUAL    * different clinic
.
GCLN2000  MOVE      "-",KEY1
          PACK      KEY40,OCACLIN,KEY1,OCADESC,SP70
          MOVE      ZERO,EXIT
          GOTO      GCLN9999
.
GCLN9000  MOVE      ONE,EXIT
GCLN9999  RETURN
+
.*******************************************************************************
. COTFIL00 - Check that the correct outpatient site prefix files are open
.            according to the visit files site code. If the site is invalid
.            or blank just open the default site of out.                        
.*******************************************************************************
COTFIL00  MATCH     SP70,PVISITE             * Does the visit have a site
          GOTO      COTFIL50 IF NOT EQUAL
.
COTFIL10  MATCH     "out",CFILEPRE           * Is out currently open
          IF        !@EQUAL
            MOVE      "out",OSTFILE          * open default prefix of out
            MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
            CALL      OPNOUT00               * Open out Outpatient Files
          ENDIF
.
          GOTO      COTFIL99
.
.
COTFIL50  MOVE      PVISITE,KEY6
          CALL      RDSITA1
          BRANCH    OVRCD,COTFIL10           * error invalid site code
.
          MATCH     OSTFILE,CFILEPRE         * Save Current File Prefix
          IF        !@EQUAL
            MOVE      OSTFILE,CFILEPRE       * Save Current File Prefix
            CALL      OPNOUT00               * Open prefixed Outpatient Files
          ENDIF
.
          GOTO      COTFIL99
.
COTFIL99  RETURN
+
.*******************************************************************************
.         Calculate ABF amount
.*******************************************************************************
OTBF0000  BRANCH    CALCFLAG,OTBF1200,OTBF1000
.
.         {[PW x (1 + Anmdc) x (1 + Aind + ARes) x (1 + ATreat) x NEP
.
          MOVE      ONE,F106B
          ADD       ANMCAMNT,F106B           * + A_NMC
.
          MOVE      ONE,F106A
          ADD       AINDAMNT,F106A           * A_Ind
          ADD       ARESAMNT,F106A           * A_Res
.
          MOVE      PWAMOUNT,FORM106         * PW
          MULT      F106A,FORM106            * x (1+A_Ind+A_res)
          MULT      F106B,FORM106            * x (1+A_NMC)
.
          GOTO      OTBF8000
.
.         New ABF calculation
.         {PW x APaed x (1 + ARes + AInd+ ANMC) x (1 + ATreat)} x NEP
.         Note: APaed=1
.
OTBF1000  MATCH     SP70,INVOICNO
          IF        !@EQUAL
            MOVE      NWAUAMNT,FORM106       * NWAU amount in 6 decimal places
            GOTO      OTBF8000
          ENDIF
.
OTBF1200  MOVE      ONE,F106A
          ADD       ARESAMNT,F106A           * A_Res
          ADD       AINDAMNT,F106A           * + A_Ind
          ADD       ANMCAMNT,F106A           * + A_NMC
.
          MOVE      ONE,F106B                * 1
          ADD       ATAAMNTS,F106B           * + ATreat
.
          MOVE      PWAMOUNT,FORM106         * PW
          MULT      F106A,FORM106            * x (1+A_Ind+A_res+A_NMC)
          MULT      F106B,FORM106            * x (1 + ATreat)
.
OTBF8000  IF        FORM106<0
            MOVE      ZERO,FORM106
          ENDIF
          MULT      NEPAMNTS,FORM106       * NEP
          MOVE      FORM106,ABFCHRGE       * ABF Charge
.
OTBF9999  RETURN
+
.
