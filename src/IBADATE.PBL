. *****************************************************************************
. *                                                                           *
. * Include Name :  IBADATE.PBL                                               *
. *                                                                           *
. * Function     :  Subroutine Include for                                    *
. *                 Standard Date Library                                     *
. *                                                                           *
. * Subroutines  :  KEYDATE  : Keyin a date (DMCY or MDCY)                    *
. *                 KEYMONTH : Keyin a month  (MM/CCYY)                       *
. *                 KEYDAY   : Keyin a day (Internal)                         *
. *                 KEYMON   : Keyin a month (Internal)                       *
. *                 KEYYEAR  : Keyin a year (Internal)                        *
. *                 DSPDATE  : Display a date                                 *
. *                 DSPMONTH : Display a month                                *
. *                 UDLDATE  : Display underlines for date                    *
. *                 UDLMONTH : Display underlines for mnth                    *
. *                 PACDATE  : Pack up a date (with slash)                    *
. *                 PACMONTH : Pack up a month(with slash)                    *
. *                 PRTDATE  : Print a date                                   *
. *                                                                           *
. * Files needed :  IBACOMM.INC    IBA Special Variables                      *
. *                                                                           *
. * Modifications:                                                            *
. *       V6.05.02  16/11/1998  Steve Armstrong                               *
. *                 Mods to treat CCENTRY as "1" to always include century in *
.*                  keyin (Y2K mods).                                         *
. *       V6.05.01  20/08/1998    J.Tan                                       *
. *                 Mods zero filled for century/year                         *
. *       V5.03.01  24/10/1995    Justin Coates  (SUHT mods)                  *
. *                 added the use of CUNKDAY and IBCNUDAY for KEYDATE         *
.******************************************************************************
. *               V5.00  14/07/89 Graeme Williams                             *
. *                        Include Re-written                                 *
. *               V5.01.01 11/01/89 Graeme Williams                           *
. *                        Added CCANLDTE as a new paramtr                    *
. *                        It was needed to be able to add                    *
. *                        a new feature to surname srchs.                    *
. *                        SRF 103238                                         *
. *               V5.01.02 30/04/92 Graeme Williams                           *
. *                        Use *NOMASK on keyins with                         *
. *                        default values                                     *
. *                                                                           *
. *****************************************************************************
.
. *****************************************************************************
. * KEYDATE    : Keyin a date (American or Australian Format)                 *
. *                                                                           *
. * Parameters : ERRA     (From COMMON) 0 = DD/MM/CCYY, 1 = DD/MM/CCYY        *
. *              CHIGHLT  0 = Highlights on for keyin of variables            *
. *                       1 = No highlighting in keyin of variables           *
. *              CDEFDTE  0 = Each section of the date (day, month, year) must*
. *                           be entered even if there is default values      *
. *                       1 = Accepting the first section of the date (day in *
. *              CCANLDTE 0 = Default dates cannot be cancelled.              *
. *                       1 = Default dates can be cancelled by a space       *
. *                           Australia, Month in USA) will automatically     *
. *                           accept the entire date (only applies when there *
. *                           is a default date)                              *
. *              CUNKDAY  0 = Dont allow 99 for day                           *
. *                       1 = Allow 99 for day                                *
. *              CCOL     Column to start keyin of date                       *
. *              CVERT    Line to keyin date on                               *
. *              CDAY     Default Day, blank if no default date               *
. *              CMON     Default Month, blank if no default date             *
. *              CYEAR    Default Year, blank if no default date              *
. *              CCENT    Default Century, if blank will default to the       *
. *                       current century (CCC from common)                   *
. * Returns    : CQUEST   0 = No "?" option selected                          *
. *                       1 = "?" option selected, OVRCD will be set to "1"   *
. *              OVRCD    0 = Date entered successfully                       *
. *                       1 = No date entered, OR "?" option selected         *
. *              IDAY     Input day as a FORM 2                               *
. *              CDAY     Input day as a DIM 2 (zero filled)                  *
. *              IMON     Input month as a FORM 2                             *
. *              CMON     Input month as a DIM 2 (zero filled)                *
. *              IYEAR    Input year as a FORM 2                              *
. *              CYEAR    Input year as a DIM 2 (zero filled)                 *
. *              ICENT    Input century as a FORM 2                           *
. *              CCENT    Input century as a DIM 2 (zero filled)              *
. *                                                                           *
. *****************************************************************************
.
KEYDATE   OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*45,FORM1  * ibcnuday
          IF        FORM1 <> ONE 
            MOVE      ZERO,CUNKDAY             * unknown day not allowed
          ENDIF
.
          MOVE      ZERO,OVRCD               * Initialise exit condition
          CALL      DSPDATE                  * Display default date if exists
          CALL      UDLDATE                  * Display underlines if no date
.
.         Branch to different keyins depending on date format (DMY or MDY)
.
CNTKDT1   BRANCH    ERRA,USADATE
.
.         Australian date format (DDMMYY or DDMMCCYY)
.
          MOVE      CCOL,CCOL1               * Column for day keyin
          MOVE      CCOL,CCOL2
          ADD       THREE,CCOL2              * Column for month keyin
          MOVE      CCOL,CCOL3
          ADD       SIX,CCOL3                * Column for year keyin
.
.         Initialisation completed. Now start the actual keyin of the date
.
KEYAUS1   CALL      KEYDAY                   * Key in the day
          BRANCH    OVRCD,STOPDATE           * No day entered, or "?" entered
.
.         If nothing was entered, and CDEFDTE is "1", then don't bother going
.         through the rest of the date. The way to check this is by checking
.         if both CDEFDTE and CDCHG are "1". The sneaky way we are doing this
.         is to multiply them together and check if the result is "1". This
.         can only be true if both of them are "1".
.
          MULT      CDEFDTE,CDCHG            * Check if they accept entire date
.
          BRANCH    CDCHG,CDATERET           * Date accepted.
.
.         Key in the rest of the date
.
KEYAUS2   CALL      KEYMON                   * Key in the month
          BRANCH    OVRCD,KEYAUS1            * No month entered, rekey day
.
KEYAUS3   CALL      KEYYEAR                  * Key in the year
          BRANCH    OVRCD,KEYAUS2:           * No year entered, rekey month
                          KEYDATE            * Invalid date, rekey entire date
.
.         Date entered successfully
.
CDATERET  MOVE      ZERO,CUNKDAY             * always set to 0
          RETURN
.
.         American date format (MMDDYY or MMDDCCYY)
.
USADATE   MOVE      CCOL,CCOL2               * Column for month keyin
          MOVE      CCOL,CCOL1
          ADD       THREE,CCOL1              * Column for day keyin
          MOVE      CCOL,CCOL3
          ADD       SIX,CCOL3                * Column for year keyin
.
.         Initialisation completed. Now start the actual keyin of the date
.
KEYUSA1   CALL      KEYMON                   * Key in the month
          BRANCH    OVRCD,STOPDATE           * No month entered, or "?" entered
.
.         If nothing was entered, and CDEFDTE is "1", then don't bother going
.         through the rest of the date. The way to check this is by checking
.         if both CDEFDTE and CDCHG are "1". The sneaky way we are doing this
.         is to multiply them together and check if the result is "1". This
.         can only be true if both of them are "1".
.
          MULT      CDEFDTE,CDCHG            * Check if they accept entire date
.
          BRANCH    CDCHG,CDATERET           * Date accepted.
.
.         Key in the rest of the date
.
KEYUSA2   CALL      KEYDAY                   * Key in the day
          BRANCH    OVRCD,KEYUSA1            * No day entered, rekey month
.
KEYUSA3   CALL      KEYYEAR                  * Key in the year
          BRANCH    OVRCD,KEYUSA2:           * No year entered, rekey day
                          KEYDATE            * Invalid date, rekey entire date
          RETURN
.
.       REDISPLAY UDLDATE AS SPACES.. NEW DATE EOS ON KEYIN
.       OVRCD FLAG IS SET    (BY KEYDAY OR KEYMON)
.
STOPDATE  DISPLAY    *PCCOL:CVERT,SP10
          RETURN
.
ENDKDATE  UNPACK     SP8,CDAY,CMON,CYEAR,CCENT
          GOTO       OVERCOND
.
. *****************************************************************************
. * KEYMONTH   : Keyin a month/year                                           *
. *                                                                           *
. * Parameters : CHIGHLT  0 = Highlights on for keyin of variables            *
. *                       1 = No highlighting in keyin of variables           *
. *              CDEFDTE  0 = Each section of the date (month, year) must be  *
. *                           entered even if there is default values         *
. *                       1 = Accepting the month of the date will            *
. *                           automatically accept the entire date (only      *
. *                           applies when there is a default date)           *
. *              CCOL     Column to start keyin of month                      *
. *              CVERT    Line to keyin month on                              *
. *              CMON     Default Month, blank if no default date             *
. *              CYEAR    Default Year, blank if no default date              *
. *              CCENT    Default Century, if blank will default to the       *
. *                       current century (CCC from common)                   *
. * Returns    : CQUEST   0 = No "?" option selected                          *
. *                       1 = "?" option selected, OVRCD will be set to "1"   *
. *              OVRCD    0 = Date entered successfully                       *
. *                       1 = No date entered, OR "?" option selected         *
. *              IMON     Input month as a FORM 2                             *
. *              CMON     Input month as a DIM 2 (zero filled)                *
. *              IYEAR    Input year as a FORM 2                              *
. *              CYEAR    Input year as a DIM 2 (zero filled)                 *
. *              ICENT    Input century as a FORM 2                           *
. *              CCENT    Input century as a DIM 2 (zero filled)              *
. *                                                                           *
. *****************************************************************************
.
KEYMONTH  MOVE      ONE,IDAY                 * Dummy for date check in KEYYEAR
          MOVE      IDAY,XDAY                * Dummy for date check in KEYYEAR
.
          MOVE      ZERO,OVRCD               * Initialise exit condition
.
          CALL      DSPMONTH                 * Display the month if it exists
.
          CALL      UDLMONTH                 * Display underlines if no month
.
.         Initialise keyin positions
.
CNTKMT1   MOVE      CCOL,CCOL2               * Column for month keyin
          MOVE      CCOL,CCOL3
          ADD       THREE,CCOL3              * Column for year keyin
.
.         Initialisation completed. Now keyin actual data
.
KEYMTH0   CALL      KEYMON                   * Key in the month
          BRANCH    OVRCD,STOPDATE           * No month entered OR "?" entered
.
.         If nothing was entered, and CDEFDTE is "1", then don't bother going
.         through the rest of the date. The way to check this is by checking
.         if both CDEFDTE and CDCHG are "1". The sneaky way we are doing this
.         is to multiply them together and check if the result is "1". This
.         can only be true if both of them are "1".
.
          MULT      CDEFDTE,CDCHG            * Check if they accept entire date
.
          BRANCH    CDCHG,CDATERET           * Date accepted.
.
          CALL      KEYYEAR                  * Key in the year
          BRANCH    OVRCD,KEYMTH0:           * No year entered, rekey month
                          KEYMONTH           * Invalid date, rekey entire date
          RETURN
.
. *****************************************************************************
. * KEYDAY     : Keyin a day                                                  *
. *                                                                           *
. * Parameters : CHIGHLT  0 = Highlights on for keyin of variables            *
. *                       1 = No highlighting in keyin of variables           *
. *              CCANLDTE 0 = Blanks not allowed as input                     *
. *                       1 = Blanks as input will cancel a default value.    *
. *              CCOL1    Column to start keyin of day                        *
. *              CVERT    Line to keyin date on                               *
. *              CDAY     Default Day, blank if no default day                *
. * Returns    : CQUEST   0 = No "?" option selected                          *
. *                       1 = "?" option selected, OVRCD will be set to "1"   *
. *              OVRCD    0 = Day entered successfully                        *
. *                       1 = No day entered, OR "?" option selected          *
. *              IDAY     Input day as a FORM 2                               *
. *              XDAY     Input day as a DIM 2 (zero filled)                  *
. *                                                                           *
. *****************************************************************************
.
KEYDAY    MOVE      ZERO,OVRCD               * Initialise exit condition
          MOVE      ZERO,CDCHG               * Initialise the "Any Change" flag
          MOVE      ZERO,CQUEST              * Initialise the "?" flag
          MOVE      SP2,XDAY                 * Initialise day entered
.
.         Check if we have a default day to worry about
.
          MATCH     SP2,CDAY                 * Is there a default day ?
          GOTO      RETKDAY IF NOT EQUAL     * Yes.
.
.         No default day.
.
          DISPLAY   *PCCOL1:CVERT,UNDLN2     * Display underlines
.
.         Keyin a new day, with or without highlighting as appropriate
.
RETKDAY   BRANCH    CHIGHLT,KEYDAYN          * CHIGHLT=1 means no highlighting
.
.         Keyin day with highlighting
.
          KEYIN     *PCCOL1:CVERT,*NOMASK,*V2LON,*ZF,*JR,*+,XDAY;
          GOTO      KEYDAYO                  * Continue processing
.
.         Keyin day without highlighting
.
KEYDAYN   KEYIN     *PCCOL1:CVERT,*NOMASK,*JR,*ZF,*+,XDAY;
.
.         Continue processing a new day
.
KEYDAYO   RESET     XDAY                     * Was anything entered ?
          GOTO      KEYDAY8 IF EOS           * No. Set exit condition
.
          MATCH     "0?",XDAY                * Was a "?" entered ?
          GOTO      CNOQST IF EQUAL          * Yes. Set the appropriate flag
.
          MATCH     SP2,XDAY                 * Was a blank entered ?
          GOTO      KEYDAYS IF EQUAL         * Yes. Check CCANLDTE parameter.
          MATCH     "0 ",XDAY                * Was a blank entered ?
          GOTO      KEYDAYS IF EQUAL         * Yes. Check CCANLDTE parameter.
.
          TYPE      XDAY                     * Is the day numeric
          GOTO      INVDAY IF NOT EQUAL      * No. Invalid day
.
          MOVE      XDAY,IDAY                * Convert to a form field
.
.         Validate the day
.
          COMPARE   ZERO,IDAY                * Was a zero input ?
          GOTO      KEYDAY8 IF EQUAL         * Yes. Treat as if nothing input
.
          COMPARE   ONE,IDAY                 * Validate day against low value
          GOTO      INVDAY IF LESS           * Invalid day ( < 1 )
.
          IF        CUNKDAY = 1 & IDAY = 99
            GOTO      KEYDAYV                  * user entered 99 and is allowed
          ENDIF
.
          COMPARE   "32",IDAY                * Validate day against high value
          GOTO      INVDAY IF NOT LESS       * Invalid day ( > 31 )
.
.         We have a valid day
.
KEYDAYV   MOVE      IDAY,XDAY                * Change the number back to a DIM
          REP       " 0",XDAY                * Zero fill the dim field
.
.         Redisplay the day (with or without highlighting)
.
CONTDAY   BRANCH    CHIGHLT,CONTDAYN         * Using highlighting ?
.
          DISPLAY   *PCCOL1:CVERT,*V2LON,XDAY;
          RETURN
.
CONTDAYN  DISPLAY   *PCCOL1:CVERT,XDAY;
          RETURN
.
.         A blank entered as the date. Check the value of CCANLDTE to see
.         if the entire date should be cancelled.
.
KEYDAYS   COMPARE   ZERO,CCANLDTE
          GOTO      INVDAY IF EQUAL
.
          UNPACK    SP6,CDAY,CMON,CYEAR
          GOTO      KEYDAY9
.
.         An invalid day
.
INVDAY
..          DISPLAY   *P1:24,*V2LON,*EL,*B,"Invalid Day ",*W2,*P1:24,*EL;
          MOVE      "Invalid Day",DDISPMSG
          CALL      DATEMSGC
          GOTO      KEYDAY
.
.         <ENTER> has hit for the day. If we have a default day, then use
.         that and set CDCHG to say nothing was changed. If we don't have
.         a default day, set OVRCD to say nothing was input.
.
KEYDAY8   MATCH     SP2,CMON                 * Do we have a default day ?
          GOTO      KEYDAY9 IF EQUAL         * No. Take appropriate action
.
.         Use the default date
.
          MOVE      CDAY,XDAY                * Default day (as a DIM)
          REP       " 0",XDAY                * Zero fill the day
          MOVE      CDAY,IDAY                * Default day (as a FORM)
          MOVE      ONE,CDCHG                * Nothing was changed.
          GOTO      CONTDAY                  * Redisplay the day
.
.         No default day and nothing entered
.
KEYDAY9   MOVE      ONE,OVRCD                * Indicator to say nothing entered
          RETURN
.
. *****************************************************************************
. * KEYMON     : Keyin a month                                                *
. *                                                                           *
. * Parameters : CHIGHLT  0 = Highlights on for keyin of variables            *
. *                       1 = No highlighting in keyin of variables           *
. *              CCANLDTE 0 = Blanks not allowed as input                     *
. *                       1 = Blanks as input will cancel a default value.    *
. *              CCOL1    Column to start keyin of month                      *
. *              CVERT    Line to keyin date on                               *
. *              CMON     Default month, blank if no default month            *
. * Returns    : CQUEST   0 = No "?" option selected                          *
. *                       1 = "?" option selected, OVRCD will be set to "1"   *
. *              OVRCD    0 = Month entered successfully                      *
. *                       1 = No month entered, OR "?" option selected        *
. *              IMON     Input month as a FORM 2                             *
. *              XMON     Input month as a DIM 2 (zero filled)                *
. *                                                                           *
. *****************************************************************************
.
KEYMON    MOVE      ZERO,OVRCD               * Initialise exit condition
          MOVE      ZERO,CDCHG               * Initialise the "Any Change" flag
          MOVE      ZERO,CQUEST              * Initialise the "?" flag
          MOVE      SP2,XMON                 * Initialise day entered
.
.         Check if we have a default month to worry about
.
          MATCH     SP2,CMON                 * Is there a default month ?
          GOTO      RETKMON IF NOT EQUAL     * Yes.
.
.         No default month.
.
          DISPLAY   *PCCOL2:CVERT,UNDLN2     * Display underlines
.
.         Keyin a new month, with or without highlighting as appropriate
.
RETKMON   BRANCH    CHIGHLT,KEYMONN          * CHIGHLT=1 means no highlighting
.
.         Keyin month with highlighting
.
          KEYIN     *PCCOL2:CVERT,*NOMASK,*V2LON,*ZF,*JR,*+,XMON;
          GOTO      KEYMONO                  * Continue processing
.
.         Keyin month without highlighting
.
KEYMONN   KEYIN     *PCCOL2:CVERT,*NOMASK,*JR,*ZF,*+,XMON;
.
.         Continue processing a new month
.
KEYMONO   RESET     XMON                     * Was anything entered ?
          GOTO      KEYMON8 IF EOS           * No. Set exit condition
.
          MATCH     "0?",XMON                * Was a "?" entered ?
          GOTO      CNOQST IF EQUAL          * Yes. Set the appropriate flag
.
          MATCH     SP2,XMON                 * Was a space entered ?
          GOTO      KEYMONS IF EQUAL         * Yes. Check CCANLDTE parameter.
          MATCH     "0 ",XMON                * Was a blank entered ?
          GOTO      KEYMONS IF EQUAL         * Yes. Check CCANLDTE parameter.
.
          TYPE      XMON                     * Is the month numeric
          GOTO      INVMON IF NOT EQUAL      * No. Invalid day
.
          MOVE      XMON,IMON                * Convert to a form field
.
.         Validate the month
.
          COMPARE   ZERO,IMON                * Was a zero input ?
          GOTO      KEYMON8 IF EQUAL         * Yes. Treat as if nothing input
.
          COMPARE   ONE,IMON                 * Validate month against low value
          GOTO      INVMON IF LESS           * Invalid month ( < 1 )
.
          COMPARE   TEN3,IMON                * Validate month against hi value
          GOTO      INVMON IF NOT LESS       * Invalid month ( > 12 )
.
.         We have a valid month
.
          MOVE      IMON,XMON                * Change the number back to a DIM
          REP       " 0",XMON                * Zero fill the dim field
.
.         Redisplay the month (with or without highlighting)
.
CONTMON   BRANCH    CHIGHLT,CONTMONN         * Using highlighting ?
.
          DISPLAY   *PCCOL2:CVERT,*V2LON,XMON;
          RETURN
.
CONTMONN  DISPLAY   *PCCOL2:CVERT,XMON;
          RETURN
.
.         A blank entered as the date. Check the value of CCANLDTE to see
.         if the entire date should be cancelled.
.
KEYMONS   COMPARE   ZERO,CCANLDTE
          GOTO      INVMON IF EQUAL
.
          UNPACK    SP6,CDAY,CMON,CYEAR
          GOTO      KEYMON9
.
.         An invalid month
.
INVMON
..          DISPLAY   *P1:24,*V2LON,*EL,*B,"Invalid Month ",*W2,*P1:24,*EL;
          MOVE      "Invalid Month",DDISPMSG
          CALL      DATEMSGC
          GOTO      KEYMON
.
.         <ENTER> has hit for the month. If we have a default month, then use
.         that and set CDCHG to say nothing was changed. If we don't have
.         a default month, set OVRCD to say nothing was input.
.
KEYMON8   MATCH     SP2,CMON                 * Do we have a default month ?
          GOTO      KEYMON9 IF EQUAL         * No. Take appropriate action
.
.         Use the default date
.
          MOVE      CMON,XMON                * Default month (as a DIM)
          REP       " 0",XMON                * Zero fill the month
          MOVE      CMON,IMON                * Default month (as a FORM)
          MOVE      ONE,CDCHG                * Nothing was changed.
          GOTO      CONTMON                  * Redisplay the month
.
.         No default month and nothing entered
.
KEYMON9   MOVE      ONE,OVRCD                * Indicator to say nothing entered
          RETURN
.
.------------------------------------------------------------
.         KEYIN THE YEAR OF THE DATE
.------------------------------------------------------------
.
KEYYEAR   MOVE       ZERO,OVRCD
          MOVE       CYEAR,XYEAR
          MOVE       CCENT,XCENT
          PACK       C4YEAR WITH CCENT,CYEAR
          REP        " 0",C4YEAR
.
.         Keyin the year as a CCYY, using CYEAR as the default century
.         if only two digits were entered
.
          MATCH      SP2,CYEAR
          GOTO       RETKCEN IF NOT EQUAL
.
.         No default year, display underlines
.
          DISPLAY    *PCCOL3:CVERT,UNDLN4
.
.         Keyin the century into a DIM4, and check for the number of digits
.         entered to determine whether or not to use the default century
.
          BRANCH     CHIGHLT OF KEYCENTN
.
          KEYIN      *PCCOL3:CVERT,*NOMASK,*V2LON,*DE,*JR,C4YEAR;
          GOTO       KEYCENTO
.
KEYCENTN  KEYIN      *PCCOL3:CVERT,*NOMASK,*DE,*JR,C4YEAR;
          GOTO       KEYCENTO
.
.         Use the default century and year
.
RETKCEN   BRANCH     CHIGHLT OF RETKCENN
.
          KEYIN      *PCCOL3:CVERT,*NOMASK,*V2LON,*DE,*RV,*JR,C4YEAR;
          GOTO       KEYCENTO
.
RETKCENN  KEYIN      *PCCOL3:CVERT,*NOMASK,*DE,*RV,*JR,C4YEAR;
KEYCENTO  RESET      C4YEAR
          GOTO       OVERCOND IF EOS
.
          UNPACK     C4YEAR WITH XCENT,XYEAR
          MATCH      SP2,XCENT
          GOTO       KEYCENTT IF NOT EQUAL
.
          MOVE       CCC,XCENT
.
KEYCENTT  REP        " 0",XYEAR
          MOVE       XYEAR,IYEAR
          REP        " 0",XCENT
          MOVE       XCENT,ICENT
.
.         Check for a valid year
.
          COMPARE    ZERO,IYEAR
          GOTO       INVYEAR IF LESS
.
.         Display the year
.
          BRANCH     CHIGHLT OF CONTCENN
.
          DISPLAY    *PCCOL3:CVERT,*V2LON,XCENT,XYEAR;
          GOTO       ENDYEARO
.
CONTCENN  DISPLAY    *PCCOL3:CVERT,XCENT,XYEAR;
          GOTO       ENDYEARO
.
.         Error message
.
INVYEAR
..          DISPLAY    *P1:24,*V2LON,*EL,*B,"Invalid Year ",*W2,*P1:24,*EL;
          MOVE      "Invalid Year",DDISPMSG
          CALL      DATEMSGC
          GOTO       KEYYEAR
.
.         We have completed entering the date, perform the final checking
.
ENDYEARO  MOVE        XDAY,IDAY
          MOVE        XMON,IMON
          MOVE        XYEAR,IYEAR
          MOVE        XCENT,ICENT
.
.         Check the number of days for this month
.         if entered 99 then allow this
.
          COMPARE     "99",IDAY
          GOTO        ENDCYMD IF EQUAL      * have entered 99 for day
.
          BRANCH      IMON OF M31,M28,M31,M30,M31,M30:
                              M31,M31,M30,M31,M30,M31
.
M31       COMPARE     THIRTY2,IDAY
          GOTO        ENDCYMD IF LESS
          GOTO        INVDMY
.
M30       COMPARE     THIRTY1,IDAY
          GOTO        ENDCYMD IF LESS
          GOTO        INVDMY
.
M28       COMPARE     TWENTY9,IDAY
          GOTO        ENDCYMD IF LESS
.
.         We are checking February, so check for a leap year
.
          MOVE        IYEAR,CLYEAR
          DIVIDE      FOUR,CLYEAR
          MULT        FOUR,CLYEAR
.
          COMPARE     IYEAR,CLYEAR
          GOTO        INVDMY IF NOT EQUAL
.
.         Check for day 29 as this is a leap year
.
          COMPARE     TWENTY9,IDAY
          GOTO        ENDCYMD IF EQUAL
          MOVE        TWENTY9,IDAY
          GOTO        INVDMYL
.
INVDMY    LOAD      IDAY USING IMON OF THIRTY1,TWENTY8,THIRTY1:
                                       THIRTY,THIRTY1,THIRTY:
                                       THIRTY1,THIRTY1,THIRTY:
                                       THIRTY1,THIRTY,THIRTY1
.
INVDMYL   CLEAR      DDISPMSG
          APPEND     "Month only has ",DDISPMSG
          APPEND     IDAY,DDISPMSG
          APPEND     " days ",DDISPMSG
          RESET      DDISPMSG
.
          CALL       DATEMSGC
          GOTO       OVERLOCK
.
ENDCYMD   MOVE       XDAY,CDAY
          MOVE       XMON,CMON
          MOVE       XYEAR,CYEAR
          MOVE       XCENT,CCENT
          RETURN
.
.         Subroutine to set the change indicator
.
CNOCHG    MOVE       ONE,CDCHG
          RETURN
.
.         subroutine to set question mark indicator
.
CNOQST    MOVE       ONE,CQUEST               *** ? OPTION
          GOTO       OVERCOND      *** MUST BE SET
.
. *****************************************************************************
. * DSPDATE    : Display a date into the appropriate format (Aust or USA)     *
. *                                                                           *
. * Parameters : ERRA     (From COMMON) 0 = DD/MM/CCYY, 1 = DD/MM/CCYY        *
. *              CHIGHLT  0 = Highlighting, 1 = No Highlighting               *
. *              CDAY     Day                                                 *
. *              CMON     Month                                               *
. *              CYEAR    Year                                                *
. *              CCENT    Century                                             *
. *                                                                           *
. *****************************************************************************
.
DSPDATE   MATCH      SP2,CDAY
          RETURN     IF EQUAL
          MATCH      "00",CDAY
          RETURN     IF EQUAL
.
          CALL       PACDATE
.
          BRANCH     CHIGHLT OF DSPAUSC
.
          DISPLAY    *PCCOL:CVERT,*V2LON,CPCDATE;
          RETURN
.
DSPAUSC   DISPLAY    *PCCOL:CVERT,CPCDATE;
          RETURN
.
. *****************************************************************************
. * DSPMONTH   : Display a month and year                                     *
. *                                                                           *
. * Parameters : CHIGHLT  0 = Highlighting, 1 = No Highlighting               *
. *              CMON     Month                                               *
. *              CYEAR    Year                                                *
. *              CCENT    Century                                             *
. *                                                                           *
. *****************************************************************************
.
DSPMONTH  MATCH      SP2,CMON
          RETURN     IF EQUAL
          MATCH      "00",CMON
          RETURN     IF EQUAL
.
          CALL       PACMONTH
.
          BRANCH     CHIGHLT OF DSPMTHC
.
          DISPLAY    *PCCOL:CVERT,*V2LON,CPCDATE;
          RETURN
.
DSPMTHC   DISPLAY    *PCCOL:CVERT,CPCDATE;
          RETURN
.
. *****************************************************************************
. * UDLDATE    : Underline date with the appropriate format (Cent or no Cent) *
. *                                                                           *
. * Parameters : CDAY     Day                                                 *
. *              CMON     Month                                               *
. *              CYEAR    Year                                                *
. *              CCENT    Century                                             *
. *                                                                           *
. *****************************************************************************
.
UDLDATE   MATCH      SP2,CDAY
          RETURN     IF NOT EQUAL
.
          DISPLAY    *PCCOL:CVERT,UNDLN2,SLASH,UNDLN2,SLASH,UNDLN4;
          RETURN
.
. *****************************************************************************
. * UDLMONTH   : Display underlines for a month input                         *
. *                                                                           *
. * Parameters : CMON     Month                                               *
. *              CYEAR    Year                                                *
. *              CCENT    Century                                             *
. *                                                                           *
. *****************************************************************************
.
UDLMONTH  MATCH      SP2,CMON
          RETURN     IF NOT EQUAL
.
          DISPLAY    *PCCOL:CVERT,UNDLN2,SLASH,UNDLN4;
          RETURN
.
. *****************************************************************************
. * PACDATE    : Pack up a date into the appropriate format (Aust or USA)     *
. *                                                                           *
. * Parameters : ERRA     (From COMMON) 0 = DD/MM/CCYY, 1 = DD/MM/CCYY        *
. *              CDAY     Day                                                 *
. *              CMON     Month                                               *
. *              CYEAR    Year                                                *
. *              CCENT    Century                                             *
. * Returns    : CPDATE   Packed date without century (Blank if no date)      *
. *              CPCDATE  Packed date with century (Blank if no date)         *
. *                                                                           *
. *****************************************************************************
.
PACDATE   MOVE       SP20,CPDATE
          MOVE       SP20,CPCDATE
          MATCH      SP2,CDAY
          RETURN     IF EQUAL
          MATCH      "00",CDAY
          RETURN     IF EQUAL
          BRANCH     ERRA OF PACUSA
.
          PACK       CPDATE WITH CDAY,SLASH,CMON,SLASH,CYEAR
          REP        " 0",CPDATE
          PACK       CPCDATE WITH CDAY,SLASH,CMON,SLASH,CCENT,CYEAR
          REP        " 0",CPCDATE
          RETURN
.
PACUSA    PACK       CPDATE WITH CMON,SLASH,CDAY,SLASH,CYEAR
          REP        " 0",CPDATE
          PACK       CPCDATE WITH CMON,SLASH,CDAY,SLASH,CCENT,CYEAR
          REP        " 0",CPCDATE
          RETURN
.
. *****************************************************************************
. * PACMONTH   : Pack up a month and year                                     *
. *                                                                           *
. * Parameters : CMON     Month                                               *
. *              CYEAR    Year                                                *
. *              CCENT    Century                                             *
. * Returns    : CPDATE   Packed date without century (Blank if no date)      *
. *              CPCDATE  Packed date with century (Blank if no date)         *
. *                                                                           *
. *****************************************************************************
.
PACMONTH  PACK       CPDATE WITH CMON,SLASH,CYEAR,SP10
          PACK       CPCDATE WITH CMON,SLASH,CCENT,CYEAR,SP10
          RETURN
.
. *****************************************************************************
. * PRTDATE    : Print a date at a given column                               *
. *                                                                           *
. * Parameters : ERRA     (From COMMON) 0 = DD/MM/CCYY, 1 = DD/MM/CCYY        *
. *              CDAY     Day                                                 *
. *              CMON     Month                                               *
. *              CYEAR    Year                                                *
. *              CCENT    Century                                             *
. *              CCOL     Position to print date                              *
. *                                                                           *
. *****************************************************************************
.
PRTDATE   CALL       PACDATE
          PRINT      *CCOL,CPDATE;
          RETURN
.
DATEMSGC  BEEP
          GETVAR     DLINE24A,DATTR24A,1,24
          RESET      DDISPMSG
          STRIP      DDISPMSG
          ENDSET     DDISPMSG
          APPEND     SP1,DDISPMSG
          RESET      DDISPMSG
          DISPLAY    *P1:24,*EL,*+,DDISPMSG;
.
          DISPLAY    *P1:24,*W2
.
          PUTVAR     DLINE24A,DATTR24A,1,24
          RETURN
.
