. *----------------------------------------------------------------------
. * Include Name  :   PMSUPGCD.PBL
. *
. * Function      :   Common functions to calculate the number of visit 
. *                   for a program record - pmsupgaf pmsupoaf
. *----------------------------------------------------------------------
.  Modifications Done :
.
.  V12.00.01  21/05/2025  Ebon Clements TSK 0955096
.             Alphanueric visit number changes
.  V11.04.01  10/05/2024  Nikitha B     TSK 0945137
.             Added LOOPCNTR to keep the server alive at COUT4000 and CINP4000.
.  V10.08.01  15/06/2016  Ebon Clements  TSK 0318672
.             Added count OP appointments by associated number functionality
.  V10.03.01  19/02/2013  Jill Parkinson CAR 281115
.             Changed CINP0000 to use afundh,afndtb instead of pfundh,pfndtb
.  V10.00.02  21/07/2010  Ebon Clements CAR 207040
.             Fixed OP visits extra funding required count
.             Added option to include the new visit in count
.  V10.00.01  09/06/2010  Ebon Clements CAR 207040
.             Fixed IP visits extra funding required count
.  V9.12.00   01/03/2010  Ebon Clements CAR 207040
.             Created include
. *----------------------------------------------------------------------
.
.******************************************************************************
. Count number of IP visits for a valid program record
.******************************************************************************
CINP0000  MOVE      ZERO,IPVCMAXI           * Max IP Visits
          MOVE      PMUGMAXI,IPVCMAXI
.
          MOVE      ZERO,IPVCWARN           * IP Visit Count Warning
          MOVE      PMUGWARI,IPVCWARN
.
          MOVE      ZERO,IPVCOUNT           * IP Visit Count
          MOVE      PMUGMANI,IPVCOUNT       * Add manual IP visits to count
.
          IF        NEWVISIT=1
            ADD       ONE,IPVCOUNT          * Include new pre adm in count
          ENDIF
.
          MOVE      ZERO,IPVEXTRA           * Extra Funding required in X Visits
.
          MOVE      ZERO,LOOPCNTR
.
          PACK      KEY24,PURNO,PMUGSDAT,SP70
          CALL      RDSVISA2
CINP4000  CALL      RDKVISA2
          BRANCH    OVRCD,CINP5000
.
          MATCH     PURNO,PVIURNO
          GOTO      CINP5000 IF NOT EQUAL
.
          MATCH     "PATWEB",PRGID
          IF        @EQUAL
            ADD       ONE,LOOPCNTR
            IF        (LOOPCNTR > 20)
              WRITE      HTMLFILE;"// Keep Alive -  ",LOOPCNTR
              MOVE       ZERO,LOOPCNTR
            ENDIF
          ENDIF
.
          MATCH     SP70,PMUGEDAT
          IF        !@EQUAL
            MATCH     PVIDATE,PMUGEDAT
            GOTO      CINP5000 IF LESS
          ENDIF
.
          COMPARE   THREE,PVITYPE                * IP Visit
          GOTO      CINP4000 IF NOT EQUAL
.
          PACK      KEY8,PVIBILL
          CALL      RDPTMIS1
          BRANCH    OVRCD,CINP4000
.
          BRANCH    ASTAT,CINP4000,CINP4500,CINP4500,CINP4500,CINP4000
          GOTO      CINP4000
.
CINP4500  MATCH     PMUGATYP,ATYPE
          GOTO      CINP4000 IF NOT EQUAL
.
          MATCH     SP70,PMUGCLAM
          IF        !@EQUAL
            MATCH     PMUGCLAM,ACLAIM
            GOTO      CINP4000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,PMUGFUND
          IF        !@EQUAL
            MATCH     PMUGFUND,AFUNDH
            GOTO      CINP4000 IF NOT EQUAL
.
            PACK      KEY14 WITH AFUNDH,AFNDTB,SP10
            CALL      RDFUND1
            BRANCH    OVRCD,CINP4000
.
            MATCH     PMUGTABT,PTHFBCAT
            GOTO      CINP4000 IF NOT EQUAL
          ENDIF
.
          ADD       ONE,IPVCOUNT            * Add to IP count
          GOTO      CINP4000
.
CINP5000  IF        IPVCWARN<>ZERO
            IF        (IPVCOUNT>=IPVCWARN) & (IPVCOUNT < IPVCMAXI)
              ASSIGN    (IPVCMAXI-IPVCOUNT),IPVEXTRA
              GOTO      CINP9999
            ENDIF
          ENDIF
.
          IF        IPVCMAXI<>ZERO
            IF        IPVCOUNT>=IPVCMAXI
              ASSIGN    (ZERO-ONE),IPVEXTRA      * (Set value to -1)
            ENDIF
          ENDIF
.
CINP9999  RETURN
.
.******************************************************************************
. Count number of OP visits for a valid program if there are no OP breakdowns
.******************************************************************************
COUT0000  MOVE      ZERO,OPVCMAXI           * Max OP Visits
          MOVE      PMUGMAXO,OPVCMAXI
.
          MOVE      ZERO,OPVCWARN           * OP Visit Count Warning
          MOVE      PMUGWARO,OPVCWARN
.
          MOVE      ZERO,OPVCOUNT           * OP Visit Count
          MOVE      PMUGMANO,OPVCOUNT       * Add manual IP visits to count
.
          IF        NEWVISIT=1
            MATCH     "1",OTCNCASS       * Count bookings by associated number
            IF        @EQUAL
              MOVE      ZERO,F6
              MATCH     SP70,VALDSTYP
              IF        !@EQUAL
                PACK      KEY5,ANSC,ANSV,VALDSTYP,SP70
                CALL      RDCODE1
                IF        OVRCD<>1
                  MOVE      TCFORM6,F6
                ENDIF
              ENDIF
              ADD       F6,OPVCOUNT      * Add to OP count
            ELSE
              ADD       ONE,OPVCOUNT     * Add to OP count
            ENDIF
          ENDIF
.
          MOVE      ZERO,OPVEXTRA           * Extra funding required in X visits
.
          MATCH     "1",PMUGBRKO            * Breakdown OP visits
          GOTO      COUT9999 IF EQUAL
.
          MOVE      ZERO,LOOPCNTR
.
          PACK      KEY24,PURNO,PMUGSDAT,SP70
          CALL      RDSVISA2
COUT4000  CALL      RDKVISA2
          BRANCH    OVRCD,COUT5000
.
          MATCH     PURNO,PVIURNO
          GOTO      COUT5000 IF NOT EQUAL
.
          MATCH     "PATWEB",PRGID
          IF        @EQUAL
            ADD       ONE,LOOPCNTR
            IF        (LOOPCNTR > 20)
              WRITE      HTMLFILE;"// Keep Alive -  ",LOOPCNTR
              MOVE       ZERO,LOOPCNTR
            ENDIF
          ENDIF
.
          MATCH     SP70,PMUGEDAT
          IF        !@EQUAL
            MATCH     PVIDATE,PMUGEDAT
            GOTO      COUT5000 IF LESS
          ENDIF
.
          COMPARE   TWO,PVITYPE                  * OP Visit
          GOTO      COUT4000 IF NOT EQUAL
.
          CALL      COTFIL00                     * Open correct outpatient files
.
          PACK      KEY36,PVIBILL,SP70
          CALL      RDSBOKA6
          CALL      RDKBOKA6
          BRANCH    OVRCD,COUT4000
.
          MATCH     PVIBILL,OBAOUTNO
          GOTO      COUT4000 IF NOT EQUAL
.
          COMPARE   FOUR,OBASTAT                 * Attended only
          GOTO      COUT4000 IF NOT EQUAL
.
          PACK      KEY8,PVIBILL,SP70
          CALL      RDBOKB1
          BRANCH    OVRCD,COUT4000
.
          MATCH     PMUGATYP,OBACLASS
          GOTO      COUT4000 IF NOT EQUAL
.
          MATCH     SP70,PMUGCLAM
          IF        !@EQUAL
            MATCH     PMUGCLAM,OBACOMP
            GOTO      COUT4000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,PMUGFUND
          IF        !@EQUAL
            MATCH     SP70,OTBBFUND
            IF        @EQUAL
              MATCH     PMUGFUND,PFUNDH
              GOTO      COUT4000 IF NOT EQUAL
.
              PACK      KEY14,PFUNDH,PFNDTB,SP70
            ELSE
              MATCH     PMUGFUND,OTBBFUND
              GOTO      COUT4000 IF NOT EQUAL
.
              PACK      KEY14,OTBBFUND,OTBBTBLE,SP70
            ENDIF
            CALL      RDFUND1
            BRANCH    OVRCD,COUT4000
.
            MATCH     PMUGTABT,PTHFBCAT
            GOTO      COUT4000 IF NOT EQUAL
          ENDIF
.
          MATCH     "1",OTCNCASS       * Count bookings by associated number
          IF        @EQUAL
            MOVE      ZERO,F6
            MATCH     SP70,OBAVISIT
            IF        !@EQUAL
              PACK      KEY5,ANSC,ANSV,OBAVISIT,SP70
              CALL      RDCODE1
              IF        OVRCD<>1
                MOVE      TCFORM6,F6
              ENDIF
            ENDIF
            ADD       F6,OPVCOUNT      * Add to OP count
          ELSE
            ADD       ONE,OPVCOUNT     * Add to OP count
          ENDIF
.
          GOTO      COUT4000
.
COUT5000  IF        OPVCWARN<>ZERO 
            IF        (OPVCOUNT>=OPVCWARN) & (OPVCOUNT < OPVCMAXI)
              ASSIGN    (OPVCMAXI-OPVCOUNT),OPVEXTRA
              GOTO      COUT9999
            ENDIF
          ENDIF
.
          IF        OPVCMAXI<>ZERO
            IF        OPVCOUNT>=OPVCMAXI
              ASSIGN    (ZERO-ONE),OPVEXTRA      * (Set value to -1)
            ENDIF
          ENDIF
.
COUT9999  RETURN
.******************************************************************************
. Count number of visits for a valid OP program breakdown record
.******************************************************************************
COBK0000  MOVE      ZERO,OPVCMAXI           * Max OP Visits
          MOVE      PMUOMAXO,OPVCMAXI
.
          MOVE      ZERO,OPVCWARN           * OP Visit Count Warning
          MOVE      PMUOWARO,OPVCWARN
.
          MOVE      ZERO,OPVCOUNT           * OP Visit Count
          MOVE      PMUOMANO,OPVCOUNT       * Add manual IP visits to count
.
          IF        NEWVISIT=1
            MATCH     "1",OTCNCASS       * Count bookings by associated number
            IF        @EQUAL
              MOVE      ZERO,F6
              MATCH     SP70,VALDSTYP
              IF        !@EQUAL
                PACK      KEY5,ANSC,ANSV,VALDSTYP,SP70
                CALL      RDCODE1
                IF        OVRCD<>1
                  MOVE      TCFORM6,F6
                ENDIF
              ENDIF
              ADD       F6,OPVCOUNT      * Add to OP count
            ELSE
              ADD       ONE,OPVCOUNT     * Add to OP count
            ENDIF

          ENDIF
.
          MOVE      ZERO,OPVEXTRA           * Extra funding required in X visits
.
          PACK      KEY24,PURNO,PMUGSDAT,SP70
          CALL      RDSVISA2
COBK4000  CALL      RDKVISA2
          BRANCH    OVRCD,COBK5000
.
          MATCH     PURNO,PVIURNO
          GOTO      COBK5000 IF NOT EQUAL
.
          MATCH     SP70,PMUOEDAT
          IF        !@EQUAL
            MATCH     PVIDATE,PMUOEDAT
            GOTO      COBK5000 IF LESS
          ENDIF
.
          COMPARE   TWO,PVITYPE                  * OP Visit
          GOTO      COBK4000 IF NOT EQUAL
.
          CALL      COTFIL00                     * Open correct outpatient files
.
          PACK      KEY36,PVIBILL,SP70
          CALL      RDSBOKA6
          CALL      RDKBOKA6
          BRANCH    OVRCD,COBK4000
.
          MATCH     PVIBILL,OBAOUTNO
          GOTO      COBK4000 IF NOT EQUAL
.
          COMPARE   FOUR,OBASTAT                 * Attended only
          GOTO      COBK4000 IF NOT EQUAL
.
          PACK      KEY8,PVIBILL,SP70
          CALL      RDBOKB1
          BRANCH    OVRCD,COBK4000
.
          MATCH     PMUOATYP,OBACLASS
          GOTO      COBK4000 IF NOT EQUAL
.
          MATCH     SP70,PMUOCLAM
          IF        !@EQUAL
            MATCH     PMUOCLAM,OBACOMP
            GOTO      COBK4000 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,PMUOFUND
          IF        !@EQUAL
            MATCH     SP70,OTBBFUND
            IF        @EQUAL
              MATCH     PMUOFUND,PFUNDH
              GOTO      COBK4000 IF NOT EQUAL
.
              PACK      KEY14,PFUNDH,PFNDTB,SP10
            ELSE
              MATCH     PMUOFUND,OTBBFUND
              GOTO      COBK4000 IF NOT EQUAL
.
              PACK      KEY14,OTBBFUND,OTBBTBLE,SP10
            ENDIF
            CALL      RDFUND1
            BRANCH    OVRCD,COBK4000
.
            MATCH     PMUOTABT,PTHFBCAT
            GOTO      COBK4000 IF NOT EQUAL
          ENDIF
.
          MATCH     PMUOVTYP,OBAVISIT
          GOTO      COBK4000 IF NOT EQUAL
.
          MATCH     "1",OTCNCASS       * Count bookings by associated number
          IF        @EQUAL
            MOVE      ZERO,F6
            MATCH     SP70,OBAVISIT
            IF        !@EQUAL
              PACK      KEY5,ANSC,ANSV,OBAVISIT,SP70
              CALL      RDCODE1
              IF        OVRCD<>1
                MOVE      TCFORM6,F6
              ENDIF
            ENDIF
            ADD       F6,OPVCOUNT      * Add to OP count
          ELSE
            ADD       ONE,OPVCOUNT     * Add to OP count
          ENDIF
.
          GOTO      COBK4000
.
COBK5000  IF        OPVCWARN<>ZERO 
            IF        (OPVCOUNT>=OPVCWARN) & (OPVCOUNT < OPVCMAXI)
              ASSIGN    (OPVCMAXI-OPVCOUNT),OPVEXTRA
              GOTO      COBK9999
            ENDIF
          ENDIF
.
          IF        OPVCMAXI<>ZERO
            IF        OPVCOUNT>=OPVCMAXI
              ASSIGN    (ZERO-ONE),OPVEXTRA      * (Set value to -1)
            ENDIF   
          ENDIF
.
COBK9999  RETURN
