.------------------------------------------------------------
.  File          :  PMSHKIPR.PBL
.
.  Function      :  Update Keyword Index File for HCP Table
.
.  Parameters    :  PMHCHCPC - Key to HCP Table
.
.  Modifications :  
.                   V10.11.01 08/08/2017 Tracey Nguyen   TSK 0299206
.                             Added HCPSW000 & GENSNDEX to derive Sound-Ex 
.                             keywords for HCP Surname and Given Name         
.                   V10.05.01 25/07/2014 Ebon Clements CAR 295504
.                             Added preferred name to keywords
.                   V9.09.01  07/01/2008 Ebon Clements CAR 146550
.                             Added HCP code as a keyword
.
.  Notes
.  -----
.  This Skeleton Routine is to Create a Procedure to Update
.  a Key Word Index Table that can be used for searching 
.  Masterfiles. The procedure should be called from the 
.  Add, Update and Delete functions in the Masterfile 
.  Maintenance Function.
.  
.  A Key Word Index Table is Defined in the following Manner
.                              
.  PTDKxxxx    DIM       xx    * Code Field Segment 1
.  PTDKyyyy    DIM       xx    * Code Field Segment n
.  PMHKKKWD     DIM       15    * Key Word
.  PTDKSPA     DIM       10    * Spare
.
.  Index 1   PTDKxxxx, PTDKyyyy, PMHKKKWD
.  Index 2   PMHKKKWD, PTDKxxxx, PTDKyyyy
.
.  To convert this Procedure the following should be performed
.    Global Change PAT to the System ID eg :%s/PAT/PAT/g
.    Global Change CKI to the File   ID eg :%s/CKI/CKI/g
.    Global Change PTDK to the IO Call ID eg :%s/PTDK/PTDK/g
.    Modify the KEYINDEX defintion to combined Code Field Lengths eg 6
.    Global Change KEY21 to the Key for the Key Word Table eg KEY21
.    Change xxxxxxx to the Code Fields eg DCODEhh
.    Change the HCGE0000 routine to call the HCPBW000 for each
.    string of key words to be indexed for the Coded Field
.    eg MOVE    DSNAME,KEYWORDS
.       CALL    HCPBW000
.       MOVE    DGNAME,KEYWORDS
.       CALL    HCPBW000
.       MOVE    TDESC,KEYWORDS
.       CALL    HCPBW000
.------------------------------------------------------------
          DEFPROC   PMSHKIUP
.
          INC       PMSHKIFD/INC
          INC       GENSNDVR/INC      * Vars for GENSNDEX.PBL 
.
KEYINDEX  DIM       10        * Code Index in Table
KEYWORDS  DIM       60        * String Containing the Key Words to be Indexed
SIXTY     FORM      "60"
KEYWRDNO  FORM      2
KEYWRD00  DIM       60
KEYWRD01  DIM       15
KEYWRD02  DIM       15
KEYWRD03  DIM       15
KEYWRD04  DIM       15
KEYWRD05  DIM       15
KEYWRD06  DIM       15
KEYWRD07  DIM       15
KEYWRD08  DIM       15
KEYWRD09  DIM       15
KEYWRD10  DIM       15
.
Z1        INIT      "~"

.------------------------------------------------------------
. Generate Word Index
.------------------------------------------------------------
HCGE0000  MOVE     ZERO,OVRCD
          TRAP     OVERCOND IF IO
          OPEN     PMSHKIA1,"pmshkiaf"
          TRAPCLR  IO
          BRANCH   OVRCD,HCGE9999
.
          MOVE     PMHCHCPC,KEYINDEX
          CALL     HCPCL000           * Remove Current Key Word Index
          BRANCH   EXIT,HCGE9999
.
          MOVE     PMHCHCPC,KEY10         * Validate Record on File
          CALL     RDPMHCP1
          BRANCH   OVRCD,HCGE1900     * If Not then it Must be a Delete/Clear
.
          MOVE     PMHCHCPC,KEYINDEX
          MOVE     PMHCSNAM,KEYWORDS
          CALL     HCPBW000
.
          MOVE     PMHCGNAM,KEYWORDS
          CALL     HCPBW000
.
          MOVE     PMHCHCPC,KEYWORDS
          CALL     HCPBW000
.
          MATCH    SP70,PMHCPRFN
          IF       !@EQUAL
            MOVE     PMHCPRFN,KEYWORDS      * Preferred name
            CALL     HCPBW000
          ENDIF
.
HCGE1050  MOVE     PMHCPRV1,KEYWORDS
          CALL     HCPBW000
.
.         TSK 0299206 - Soundex
.
HCGE1100  MOVE     PMHCSNAM,KEYWORDS        * HCP Surname
          CALL     HCPSW000
.
          MOVE     PMHCGNAM,KEYWORDS        * HCP Given Names
          CALL     HCPSW000
.
HCGE1900  CLOSE    PMSHKIA1
.
HCGE9999  GOTO     PMSHKIEP
.------------------------------------------------------------
. Clear Current Key Words
.------------------------------------------------------------
HCPCL000  PACK     KEY25,KEYINDEX,SP70
          CALL     RSPMHKI1
          CALL     RKPMHKI1
          BRANCH   OVRCD,HCPCL999
          MATCH    KEYINDEX,PMHKHCPC
          GOTO     HCPCL999 IF NOT EQUAL
.
          PACK     KEY25,PMHKHCPC,PMHKKKWD,SP70
          CALL     DEPMHKI1
          GOTO     HCPCL000
.
HCPCL999  RETURN
.-------------------------------------------------
. Determine Words to Index
.-------------------------------------------------
HCPBW000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      HCPBW000 IF NOT EOS
            GOTO      HCPBW999               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWORDS,SP30      * reset form pointer
          MOVE      KEY60,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      HCPBW100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handly form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Save this Word
.
HCPBW100  MOVE      KEYWORDS,KEY15
          PACK      KEY15,KEY15,SP70
          REP       UPPLOW,KEY15             * Always Upper Case
          PACK      KEY25,KEYINDEX,KEY15
          UNPACK    KEY25,PMHKHCPC,PMHKKKWD
          CALL      RDPMHKI1
          IF        OVRCD=1
            CALL      WRPMHKI1
          ENDIF
          GOTO      HCPBW190
.
.         Check for any more words
.
HCPBW190  SETLPTR   KEYWORDS,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY60,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY60,KEYWORDS
          GOTO      HCPBW000
.
HCPBW999  RETURN
.
. TSK 0299206  
.-----------------------------------------------------------------------------
. Determine Words to Index with Soundex algorithm
.-----------------------------------------------------------------------------
HCPSW000  CMATCH    SP1,KEYWORDS             * Eliminate leading blanks
          IF        @EQUAL
            BUMP      KEYWORDS
            GOTO      HCPSW000 IF NOT EOS
            GOTO      HCPSW999               * entire name if blank
          ENDIF
          PACK      KEY60,KEYWORDS,SP30      * reset form pointer
          MOVE      KEY60,KEYWORDS
.
          SCAN      SP1,KEYWORDS             * Locate the first blank
          GOTO      HCPSW100 IF NOT EQUAL
          BUMP      KEYWORDS,-1              * go back 1 to end of name
          MOVEFPTR  KEYWORDS,CCITEM          * another handy form field
          RESET     KEYWORDS                 * reset the form pointer
          SETLPTR   KEYWORDS,CCITEM          * set logical length to end of name
.
.         Derive and save this sound-ex key word
.
HCPSW100  MOVE      KEYWORDS,SXKEYIN         * Keyword in to convert to Soundex
          CALL      GSNDX000                 * Derive Sound-Ex key
          PACK      KEY15,Z1,SXKEYOUT,SP70   * Add "~" to indicate Sound-Ex key
          PACK      KEY25,KEYINDEX,KEY15
          UNPACK    KEY25,PMHKHCPC,PMHKKKWD  * Write the Sound-Ex key
          CALL      RDPMHKI1
          IF        OVRCD=1
            CALL      WRPMHKI1
          ENDIF
          GOTO      HCPSW190
.
.         Check for any more words
.
HCPSW190  SETLPTR   KEYWORDS,SIXTY         * reset logical length
          ADD       ONE,CCITEM             * position of 1st blank
          RESET     KEYWORDS,CCITEM        * reset to this point
          PACK      KEY60,KEYWORDS,SP30    * reset form pointer
          MOVE      KEY60,KEYWORDS
          GOTO      HCPSW000
.
HCPSW999  RETURN
.
.------------------------------------------------------------------------------
          INC       PMSHKIIO/INC
          INC       IBAOVRIO/INC
          INC       GENSNDEX               * Routine to generate Soundex key
.
PMSHKIEP  ENDPROC                          * End of Procedure
.
