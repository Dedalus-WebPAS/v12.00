.******************************************************************************
.*  Include Name  : PMSHCPKY.PBL                                              *
.*  Description   : Keyin of a Doctor file entry                              *
.*  Author        : Jeni Tan
.*                                                                            *
.*  Parameters    : ECOL     - column of keyin                                *
.*                  EVERT    - row of keyin                                   *
.*                  CKYIDEF8 - default value (blank if no default)            *
.*                  CKYIMAND - is keyin mandatory (0=No  1=Yes)               *
.*                  CKYINACT - check doctor status                            *
.*                  CKYINACT VALUE                         VALID DRSTAT       *
.*                  ===================================================       *
.*                     0   No validation                        ALL           *
.*                     1   Validate all doctor types            0 or 2        *
.*                     2   Validate Attending doctors only      0             *
.*                     3   Validate Referring doctors only      0 or 2        *
.*                                                                            *
.*  Returns       : PMHCHCPC  - Keyed in code                                 *
.*                  EXIT = 0   Everything Ok.                                 *
.*                       = 1   Nothing or Spaces Input                        *
.*                       = 2   EXITCHAR input                                 *
.*                                                                            *
.*  On return     : You must display the description yourself.                *
.*                                                                            *
.* Modifications  :                                                           *
.* 15/08/2003  Jill Habasque CAR 40347                                        *
.*             Changed to use CKYIDEF8 instead of CKYIDEF6                    *
.*                                                                            *
.******************************************************************************
PMSHCPKY  
PHCKY010  MATCH     SP8,CKYIDEF8                  * using a default ?
          GOTO      PHCKY050 IF NOT EQUAL   
.
          KEYIN     *PECOL:EVERT,*DV,UNDLN10:      * no default used
                    *PECOL:EVERT,*V2LON,PMHCHCPC:
                    *PECOL:EVERT,*DV,PMHCHCPC
          GOTO      PHCKY080
.
PHCKY050  MOVE      CKYIDEF8,PMHCHCPC
          KEYIN     *PECOL:EVERT,*DV,PMHCHCPC:     * default used
                    *PECOL:EVERT,*V2LON,*RV,PMHCHCPC:
                    *PECOL:EVERT,*DV,PMHCHCPC
.
.         check what was entered
.
PHCKY080  CALL      DHCCK000
          BRANCH    EXIT,PHCKY100,PHCKY705,PHCKY805,PHCKY905
.                         "?"      valid    nothing exitchar
          GOTO      PHCKY010
.
.         a "?" was input
.
PHCKY100  MOVE      PMHCHCPC,KEY10
          PROC      HPMHKA00
          BRANCH    OVRCD,PHCKY010
          IF        CKYIMODE=1
            CALL      REXISTS
            GOTO      PHCKY010
          ENDIF
          GOTO      PHCKY080
.
. ------- valid doctor entered ------------------------
.
PHCKY705  MOVE      FALSE,EXIT                    * valid input
          DISPLAY   *PECOL:EVERT,*V2LON,PMHCHCPC
          MOVE      PMHCHCPC,DOCCODE
          GOTO      PHCKY999 
.
.         nothing was input
.
PHCKY805  MOVE      TRUE,EXIT                     * Nothing or Spaces entered
          GOTO      PHCKY999 
.
.         EXITCHAR input
.
PHCKY905  MOVE      TWO,EXIT                      * EXITCHAR
.
PHCKY999  RETURN
+
.***************************************************************************
.*                              DHCCK000             Called by: PMSHCPKY   *
.*                        check what was entered                           *
.*          Returns:  EXIT  = 0   invalid                                  *
.*                          = 1   "?"                                      *
.*                          = 2   valid                                    *
.*                          = 3   nothing                                  *
.*                          = 4   exitchar                                 *
.***************************************************************************
DHCCK000  ENDSET    PMHCHCPC
          APPEND    SP10,PMHCHCPC
          RESET     PMHCHCPC
.
          MATCH     EXITCHAR,PMHCHCPC
          GOTO      DHCCK600 IF EQUAL
.
          MATCH     SP10,PMHCHCPC
          GOTO      DHCCK700 IF EQUAL
.
.         check for a "?"
.
          MATCH     QUEST,PMHCHCPC
          GOTO      DHCCK800 IF EQUAL
.
. a code was entered so validate
. 
DHCCK500  PACK      KEY10,PMHCHCPC,SP10
          CALL      RDPMHCP1
.
          IF        CKYIMODE=0
            BRANCH    OVRCD,DHCCK900
          ELSE
            COMPARE   ZERO,OVRCD
            GOTO      DHCCK900 IF EQUAL
          ENDIF 
.
.         Make sure dr. is active   
.
          IF        CKYINACT=1
            MATCH     "1",PMHCSTTS
            IF        @EQUAL
              DISPLAY   *P1:24,*EL,*B,"Inactive Doctor Code.  ";
              CALL      HITENTER
              GOTO      DHCCK910
            ENDIF
          ENDIF
.
          IF        CKYINACT=2
            MATCH     "1",PMHCSTTS
            IF        @EQUAL
              DISPLAY   *P1:24,*EL,*B,"Inactive Attending Doctor.  ";
              CALL      HITENTER
              GOTO      DHCCK910
            ENDIF
          ENDIF
.
          IF        CKYINACT=3
            MATCH     "1",PMHCSTTS
            IF        @EQUAL
              DISPLAY   *P1:24,*EL,*B,"Inactive Referring Doctor.  ";
              CALL      HITENTER
              GOTO      DHCCK910
            ENDIF
          ENDIF
.
          MOVE      TWO,EXIT
          GOTO      DHCCK999
.
. exitchar entered
.
DHCCK600  MOVE      FOUR,EXIT
          UNPACK    SP30,PMHCSNAM,PMHCTITL
          MOVE      SP30,PMHCSNAM
          MOVE      SP10,PMHCHCPC                * clear code
          MOVE      SP10,DOCCODE                 * clear code
          GOTO      DHCCK999
.
. nothing entered
.
DHCCK700  BRANCH    CKYIMAND,DHCCK950             * mandatory
.
          UNPACK    SP30,PMHCSNAM,PMHCTITL
          MOVE      SP30,PMHCGNAM
          MOVE      SP10,DOCCODE
          MOVE      THREE,EXIT
          GOTO      DHCCK999
.
. "?" entered
.
DHCCK800  MOVE      ONE,EXIT
          GOTO      DHCCK999
.
. invalid code entered
.
DHCCK900  IF        CKYIMODE=0
            DISPLAY   *P1:24,*EL,*B,"Invalid Doctor Code.  ";
            CALL      HITENTER
          ELSE
            CALL      REXISTS
          ENDIF
.
DHCCK910  MOVE      ZERO,EXIT
          GOTO      DHCCK999
.
DHCCK950  BEEP
          MOVE      ZERO,EXIT
.
DHCCK999  RETURN
...............................................................................
.
          INC       PMSHKIKY
.
