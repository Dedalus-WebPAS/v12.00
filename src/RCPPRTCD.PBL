.**********************************************************************
.   Routine to Print Receipt - Used by RCPWEB01 (Re-print Receipts).
.                              Used by RCPWEB03 (Input Receipts).
.
.   Modifications : 
.------------------------------------------------------------------------------
.     V12.00.02 27/08/2025  Alina Bhari      TSK 0954453
.               Added stattionary template variable for patient name            .               - XRCP0600,XRCP1460
.     V12.00.01 20/05/2025  Ebon Clements    TSK 0955096
.               Alphanueric visit number changes
.     V11.04.01 07/10/2024  Thanh T          TSK 0952281
.               Changed PRNT1520 to use PATMA1AF.PTMASNAM first if it contains
.               data, else use PATMA1AF.PSNAME 
.------------------------------------------------------------------------------
.     V11.03.01 02/10/2023  Nikitha B        TSK 0927699
.               Added  Added CAPPRVNO and PTCNHABN for Hospital Approval Number .               and  ABN Number for single and multi hospital.
.     V11.02.02 08/04/2022  Jacob Jackson    TSK 0864505
.               Remove label in branch statement which was
.               pointing to non-existent entry
.     V11.02.01 24/01/2022  Jacob Jackson    TSK 0964505
.               Added preferred name option fields -
.               XRCP1370, XRCP1380, XRCP1390, XRCP1400, XRCP1410
.     V10.14.01 20/03/2019  J.Tan            TSK 0857392
.               Changed RCP Transaction count from DIM3 to DIM5
.     V10.07.02 29/01/2016  J.Tan         CAR 303942
.               Mods for Payment types
.     V10.07.01 11/12/2015  J.Tan             CAR 303942
.               Mods for Additional payment types
.     V10.06.01 18/06/2015  Ebon Clements     CAR 317589
.               Fixed key pack on extra contacts read - XCON0000 
.     V10.04.02 20/06/2014  J.Tan             CAR 302000
.               Fixed printing deposit type description (overlap)
.     V10.04.01 14/01/2013  Patrick Adair     CAR 265844
.               Added variables for PRFA name and address
.     V10.03.02 07/12/2011  J.Tan             CAR 254776
.               Removed address line 2 from the new full PRFA address field
.     V10.03.01 29/11/2011  J.Tan             CAR 254776
.               Added a variable for Patient full address
.     V10.02.02 22/08/2010  J.Tan             CAR 235370
.               Added variables for NHI Given name and Surname
.     V10.02.01 28/07/2010  J.Tan             CAR 244159
.               Mods for Credit Card Surcharge Register 89
.     V10.00.01 21/07/2010  J.Tan             CAR 226011
.               Fixed printing name for HWK Sundry debtor register 
.     V9.12.04  23/10/2009  Ebon Clements     CAR 185901
.               Print debtor number in ur field if sundry debtors - PRNT1350
.     V9.12.03  09/07/2009  J.Tan             CAR 199760
.               Added ED receipt header and tail template
.     V9.12.02  24/06/2009  Jill Habasque     CAR 191626
.               Added RCDTVIST to be printed on templated receipt body
.     V9.12.01  26/05/2009  J.Tan             CAR 197291
.               Mods printing credit card description
.     V9.11.01  28/01/2009  Peter Vela        CAR 188375
.               Added PRFA for Inpatient Deposits (Register 97)
.     V9.10.02  11/09/2008  J.Tan             CAR 151983
.               Fixed PRFA for Sundry Debtors (DBDBNA1)
.     V9.10.01  26/05/2008  J.Tan             
.               Fixed PRFA for receipt from PP Invoice
.     V9.09.10  26/03/2008  Peter Vela        CAR 163975
.               Added PMI address variables to stationery templated rec header
.     V9.09.09  19/03/2008  Peter Vela        CAR 163975
.               Modified to print templated Header and Tail for HFORMAT = 1
.     V9.09.08  21/02/2008  Mike Laming       CAR 161012
.               Added printing "Cancelled" receipts to Receipt Header routines
.     V9.09.07  20/02/2008  Peter Vela        CAR 162070
.               Fixed date printing for private practice receipts and sundry
.               debtor receipts
.     V9.09.06  02/01/2008  J.Tan             CAR 134035
.               Mods to print Internal receipts comments
.     V9.09.05  16/10/2007  J.Tan             CAR 151560
.               Mods print position for payment type again for SJOG
.     V9.09.04  15/10/2007  J.Tan             CAR 151560
.               Mods print position for payment type
.     V9.09.03  08/10/2007  J.Tan             CAR 151560
.               Mods to print Method of payment and fixed printing invoice date
.     V9.09.02  27/09/2007  J.Tan             CAR 151260
.               Fixed printing Deposits for template format
.     V9.09.01  30/07/2007  Ebon Clements     CAR 146302
.               Fixed print of PRFA for private prac deposits - HFORMAT=9
.     V9.08.07  18/04/2007  J.Tan             CAR 130467
.               Mods JH name to have Surname followed by Given name
.     V9.08.06  20/02/2007  Ebon Clements     CAR 132148
.               Changed private prac rec to get PRFA from the invoice
.               if it is populated for HFORMAT=9. PRNT1255
.     V9.08.05  15/12/2006 Sandra Barcham     CAR 127625
.               Fix print deposit for JH
.     V9.08.04  13/12/2006  J.Tan             CAR 127625
.               Mods to Singapore HRN
.     V9.08.03  08/12/2006  J.Tan             CAR 127625
.               Mods for JH print receipts
.     V9.08.02  04/12/2006  J.Tan             CAR 126773
.               Mods JH to print payment details
.     V9.08.01  16/11/2006  Steve Armstrong   CAR 124160
.               Added Singapore HRN
.     V9.07.03  01/09/2006 Ebon Clements  CAR 111214
.               Set receipt date CRCPDATE to RCBNTDAT not the currect date
.     V9.07.02  24/08/2006 Peter Vela CAR 117188
.               Removed defaulting of PATHSP variables at PRNT4220
.               Modified table reading of PATHSP at PRNT1025
.     V9.07.01  27/07/2006 Peter Vela CAR 114217
.               Added REGDESC to receipt trans lines (RTRN0000)
.     V9.06.04  17/05/2006 Peter Vela CAR 105533
.               Populated variables for HFORMAT=2 (Template) Register 1 (Inpat)
.     V9.06.03  10/05/2006 Peter Vela CAR 104751
.               Added PRNTADD4 Address Line 4
.               Added defaults for HFORMAT=2 (template) pathspaf.
.     V9.06.01  26/04/2006 J.Tan      CAR 102287
.               Added fields for Multi hospital details
.     V9.05.01  26/03/2006 Peter Vela CAR 61299
.               Increased field no size from DIM3 to DIM5  
.     V9.04.10  31/10/2005 J.Tan  CAR 49925
.               Mods receipt layout 2 - using header/tail template with the
.               length of body from system parameter
.     V9.04.09  12/10/2005 J.Tan  CAR 67445
.               Fixed printing receipt for Anne Caudle Layout (CLNO)
.     V9.04.08  27/05/2005 J.Tan  CAR 61641
.               Added field for page number
.     V9.04.07  19/05/2005 J.Tan  CAR 61641
.               Mods for Ballarat layout
.     V9.04.06  07/12/2004 Lina Vo    CAR 55180
.               Fixed pendlebury to print 24 lines for body.  Also make program
.               consistent with V9.03.
.     V9.04.05  30/11/2004 Guomin Fu  CAR 55180
.               Mods for Pendlebury receipt layout(HFORMAT=4)
.     V9.04.03  16/11/2004 Lina Vo
.               Fixed HFORMAT=5 to only print one transaction line (HTRN0500).
.     V9.04.02  17/09/2004 Lina Vo
.               Put in extra checks for HSYS3 and HSYS6 and to not read files
.                   V9.04.01 26/08/2004 Lina Vo CAR 53012
.                   Add HFORMAT=1 for Holy Spirit
.                   V9.03.01 26/05/04 J.Tan  CAR 50150
.                   Fixed RCH print receipt
.                   21/01/04 J.Tan
.                   Fixed printing receipts for emr.
.                   01/03/2004 Lina Vo    CAR 47921
.                   Removed use of PRIDBTFD & IO.
.                   22/04/2004 Lina Vo             
.                   Fixed print layout to be same as V9.02
.
.   Fields   : CMDLINE   DIM       50
.              CRCPDATE  DIM       8
.              CODEUR    INIT      "U/R"
.              CODEPP    INIT      "P/P"
.              CODEIR    INIT      "I/R"
.              CODESP    INIT      "S/P"
.              CODESR    INIT      "S/R"
.              CURRROW   FORM      3
.              CURRROW   FORM      3
.              DIM3      DIM       3
.              DIM8      DIM       8
.              DIM12     DIM       12
.              DIM40     DIM       40
.              FORM3     FORM      3
.              FORM8     FORM      8
.              MAXCLNO   FORM      2
.              MRCNNOHS  FORM      2
.              NMPNUMB   DIM       20
.              RECEIPTN  DIM       12
.              PATNAME   DIM       30                * formatted patient name
.              PNAM15    DIM       15
.              PRNTADD1  DIM       20
.              PRNTADD2  DIM       20
.              PRNTSUBR  DIM       20
.              PRNTADD4  DIM       20
.              PRNTPOST  DIM       4
.              PRNTNAME  DIM       30
.              PRNTRNAM  DIM       30
.              PRNTPNAM  DIM       30
.              PRNTPNUM  DIM       8
.              PRNTSTRT  FORM      3
.              REF10     DIM       10
.              SHREGIS   DIM       26              * short register decsription
.              TMPLTFLG  FORM      1
.              TAILLENG  FORM      3
.              VARIABLE  DIM       127
.              XXDPATH   DIM       120
.
.   Requires : READ      CONTROLF,THIRTY6;*106,HFORMAT
.              INC       COMDEPFD & IO
.              INC       DEBFDTFD & IO
.              INC       DEBFTHFD & IO
.              INC       DEBDBTFD & IO
.              INC       IBATMDFD & IO
.              INC       IBATMHFD 
.              INC       NHIMASIO & IO
.              INC       RCPBNKFD & IO
.              INC       RCPDTEFD & IO
.              INC       RCPREGFD & IO
.              INC       PATDPTHV.INC
.              INC       PATHOSFD & IO
.              INC       PATINVFD & IO
.              INC       PATVISFD & IO
.              INC       PATMA1FD & IO
.              INC       PATMI1FD & IO
.              INC       PATNIDFD & IO
.              INC       PATRE1FD & IO
.              INC       PMSPX2FD & IO
.              INC       PRIINVFD & IO
.              INC       PRIDBTFD & IO
.              INC       PATDPATH
.              INC       PMIGTNID
.
.**************************************************************************
+
.**************************************************************************
.         Print the receipt depending on Format
.**************************************************************************
PRTN0000  BRANCH    HFORMAT,PRTN1100,PRTN1000,PRTN1300,PRTN1000,PRTN1000:
                            PRTN1000,PRTN1000,PRTN1000,PRTN1000
.
PRTN1000  CALL      PRNT0000                * all others
          CLOSE     PATVISA1
          OPEN      PATVISA1,"patvisaf"
.
          CLOSE     PATINVR1
          OPEN      PATINVR1,"patinvrf"
.
          CLOSE     PATRE1A1
          OPEN      PATRE1A1,"patre1af"
          GOTO      PRTN9999
.
PRTN1100  CALL      PRINTHS1                * for Holy Spirit
          GOTO      PRTN9999
.
PRTN1300  CALL      RWAR0000                * for warburton
          GOTO      PRTN9999
.
PRTN9999  RETURN
+
.------------------------------------------------------------------------------
.                         Re-print Receipt
.------------------------------------------------------------------------------
PRNT0000  PACK      DIM12,SP20
          PACK      DIM8,SP10                * srf 635035
          MOVE      ZERO,CLNO                * Nothing printed yet
          MOVE      SP70,CDATE
          MOVE      ZERO,MPGEFLAG
          MOVE      ZERO,CPAGENO
.
          MOVE      SP70,PPRAADD1
          MOVE      SP70,PPRAADD2
          MOVE      SP70,PPRASUBR
          MOVE      SP70,PPRAPOST
.
          MOVE      SP70,PRFANAME
          MOVE      SP70,PRFAADD1
          MOVE      SP70,PRFAADD2
          MOVE      SP70,PRFAADD3
          MOVE      SP70,PRFAADD4
          MOVE      SP70,PRFAPOST
.
          PACK      KEY12,RECEIPTN,SP20      * For this receipt ...
          CALL      RDRCBNK1
          BRANCH    OVRCD,PRNT9999
.                                                                     *I-161012
          PACK      CANCLMSG,SP70
          MATCH     "1",RCBNSTAT
          IF        @EQUAL
            MOVE      "THIS  RECEIPT  HAS  BEEN  CANCELLED",CANCLMSG
          ENDIF
.
          MOVE      RCBNTDAT,CRCPDATE        * Receipt Date
.
          MOVE      SP70,PRNTPTYP
          MOVE      SP70,PRNTPNAM
          PACK      KEY15,RECEIPTN,SP20      * For this receipt ...
          CALL      RSRCBDT1
          CALL      RKRCBDT1
          BRANCH    OVRCD,PRNT0800
          CALL      SPAY0000                 * Set payment type
.
PRNT0800  PACK      KEY17,RECEIPTN,SP20
          CALL      RSRCDTE1                 * Go through all transactions
PRNT1000  CALL      RKRCDTE1
          BRANCH    OVRCD,PRNT9100
          MATCH     RCDTRECN,RECEIPTN        * Same receipt still ?
          GOTO      PRNT9100 IF NOT EQUAL     * No - pad out receipt
.
          MOVE      RCDTREGI,KEY3
          CALL      RDREGA1
          BRANCH    OVRCD,PRNT1000
.
          COMPARE   ZERO,CLNO
          GOTO      PRNT4000 IF NOT EQUAL
          PACK      PRNTRNAM,SP30
.
.         Get invoice details
.
          UNPACK    RCDTINVN,KEY4,PINVNO
          PACK      KEY8,PINVNO                * Invoice number is the key
.
          MATCH     SP70,RCDTURNO
          IF        !@EQUAL
            CALL      XCON0000               * Check Extra Contact details
          ENDIF
.                                            * PatBill,PrivPrac,Sundry Debtors
          IF        HFORMAT=5|HFORMAT=6|HFORMAT=7|HFORMAT=8|HFORMAT=9|HFORMAT=4|HFORMAT=2
            PACK      PATNAME,RCDTNAME,SP30
            PACK      PRNTNAME,RCDTNAME,SP30
            PACK      PRNTRNAM,RCDTNAME,SP30
            PACK      PRNTADD1,RCDTADD1,SP30
            PACK      PRNTADD2,RCDTADD2,SP30
            PACK      PRNTSUBR,RCDTADD3,SP30
            PACK      PRNTADD4,RCDTADD4,SP30
            PACK      PRNTPOST,RCDTPOST,SP5
          ENDIF
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,ZERO;*43,IBCNMHOS
          READ      CONTROLF,THIRTY6;*223,RCCNPAYM
.
          COMPARE   ONE,IBCNMHOS
          GOTO      PRNT1050 IF NOT EQUAL
.
          UNPACK    SP70,PTHSHOSP,PTHSNAME
          UNPACK    SP70,PTHSADD1,PTHSADD2
          UNPACK    SP70,PTHSADD3,PTHSADD4
          UNPACK    SP70,PTHSPCOD,PTHSTELE,PTHSFAXN
.
          IF        HFORMAT=2
.           MATCH     SP70,RCBNMHOS
.           IF        !@EQUAL & !@EOS
.             OPEN      PATHSPA1,"pathspaf"
.             PACK      KEY3,RCBNMHOS,SP70
.             CALL      RDPTHSP1
            MATCH     SP70,RCBNCDRW
            IF        !@EQUAL & !@EOS
              PACK      KEY6,RCBNCDRW,SP70
              CALL      RDCIDA1
              BRANCH    OVRCD,PRNT1025
.
              OPEN      PATHSPA1,"pathspaf"
              PACK      KEY3,RCCIHOSP,SP70
              CALL      RDPTHSP1 
              BRANCH    OVRCD,PRNT1025
.
            ELSE
PRNT1025      MATCH     SP70,RCBNCUID
              IF        !@EQUAL & !@EOS
                PACK      KEY10,RCBNCUID,SP70
                CALL      RDWBSE1
                BRANCH    OVRCD,PRNT1050
.
                OPEN      PATHSPA1,"pathspaf"
                PACK      KEY3,WBSEHOSP,SP70
                CALL      RDPTHSP1
.
              ENDIF
            ENDIF
          ENDIF
.
PRNT1050  PACK      DIM12,SP20
          PACK      PRNTPNUM,SP20
          BRANCH    REGIBACD,PRNT1100,PRNT1200,PRNT1000,PRNT1000,PRNT1350:
                             PRNT1100
.
          COMPARE   "89",REGIBACD            * Credit Card surcharge
          GOTO      PRNT1500 IF EQUAL
          COMPARE   "92",REGIBACD             * GST Sundry Cash Sales   *I-90301
          GOTO      PRNT1500 IF EQUAL
          COMPARE   "94",REGIBACD             * Sundry debtor
          GOTO      PRNT1500 IF EQUAL
          COMPARE   "95",REGIBACD            * Sundry Cash Sales
          GOTO      PRNT1500 IF EQUAL
          COMPARE   "90",REGIBACD            * Cash Deposits (emr)
          GOTO      PRNT1500 IF EQUAL
          COMPARE   "91",REGIBACD            * Cash Deposits (out)
          GOTO      PRNT1500 IF EQUAL
          COMPARE   "96",REGIBACD            * Cash Deposits (Priv. Practice)
          GOTO      PRNT1230 IF EQUAL
          COMPARE   "97",REGIBACD            * Cash Deposits (Patient)
          GOTO      PRNT1500 IF EQUAL
          COMPARE   "98",REGIBACD            * Internal Receipting
          GOTO      PRNT1400 IF EQUAL
          COMPARE   "99",REGIBACD            * check for Suspense
          GOTO      PRNT1000 IF NOT EQUAL
.
.
          PACK      PATNAME,RCDTNAME,SP30
          PACK      PRNTNAME,RCDTNAME,SP20
          PACK      PRNTADD1,RCDTADD1,SP30
          PACK      PRNTADD2,RCDTADD2,SP30
          PACK      PRNTSUBR,RCDTADD3,SP30
          PACK      PRNTADD4,RCDTADD4,SP30
          PACK      PRNTPOST,RCDTPOST,SP30
          PACK      PTMXCXMP,SP30
          PACK      PTMXCARD,SP30
          MOVE      SP20,NMPNUMB
          MOVE      SP20,DIM8
          UNPACK    RCDTTDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          GOTO      PRNT4000
.
. ------- patient system ------- (or for Nursing Home System as well)
.
PRNT1100  MOVE      RCDTMDHS,FORM2            * get correct DPATH to scan
          LOAD      XXDPATH,FORM2,HDPATH1,HDPATH2,HDPATH3,HDPATH4
.
          MOVE      "patvisaf",OPFILE
          MOVE      TWENTY,FILENUM
          CALL      OMDFN000                * open visit file in correct dbase
          IF        EXIT=1
            OPEN      PATVISA1,"patvisaf"
          ENDIF
.
          MOVE      "patinvrf",OPFILE
          MOVE      FIFTY1,FILENUM
          CALL      OMDFN000                * open invoice file in correct dbase
          IF        EXIT=1
            OPEN      PATINVR1,"patinvrf"
          ENDIF
.
          MOVE       "patre1af",OPFILE
          MOVE      FORTY6,FILENUM
          CALL      OMDFN000                * open resp file in correct dbase
          IF        EXIT=1
            OPEN      PATRE1A1,"patre1af"
          ENDIF
.
          CALL      RDINV1                   * Patient Billing invoice
          BRANCH    OVRCD,PRNT1600     * check for deposit trans of nursing home
.
          UNPACK    PINVDTE,CCENT,CYEAR,CMON,CDAY  * Invoice Date
.
.         MOVE      PINVADM,KEY8
.         CALL      RDVISA1
.         IF        OVRCD = ZERO
.           UNPACK    PVIDATE,CCENT,CYEAR,CMON,CDAY  * Invoice Date
.         ENDIF
          CALL      PACDATE
          PACK      DIM12,CODEUR,SP1,PINVUR  * U/R number
          PACK      PRNTPNUM,PINVUR
.
.         Get patient name
.
          MOVE      "Unknown patient",PATNAME
          MOVE      PINVUR,KEY8
          CALL      RDMAST1                * get patient master record
          BRANCH    OVRCD,PRNT4000
.
          MOVE      PADD1,PPRAADD1
          MOVE      PADD2,PPRAADD2
          MOVE      PSUBRB,PPRASUBR
          MOVE      PPOST,PPRAPOST
.
          IF        HFORMAT = 4
            MOVE      SP4,PACTITLE          * format patient name
            MOVE      PSNAME,PACSNAME
            MOVE      PGNAME,PACGNAME
            MOVE      TWO,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,PATNAME
          ENDIF
.
          MOVE      SP20,NMPNUMB
          CALL      PMIGTNID
.
          IF        HFORMAT = 5 | HFORMAT = 7 | HFORMAT=8 | HFORMAT=4
            UNPACK    PINVDTE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      PTITL,PACTITLE
            MOVE      PSNAME,PACSNAME
            MOVE      PGNAME,ANS
            MOVE      ANS,PACGNAME
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,PATNAME
            PACK      PRNTNAME,PATNAME,SP30
.
            MOVE      PURNO,DIM8
            PACK      PRNTPNUM,PURNO
            PACK      KEY8,PINVADM
            CALL      RDRESP1                * Read the PRA file to get suburb
            IF        OVRCD<>1
              PACK      PRNTADD1,PKADD1,SP30
              PACK      PRNTADD2,PKADD2,SP30
              PACK      PRNTSUBR,PKSUBR,SP30
              PACK      PRNTADD4,PKADD4,SP30
              PACK      RCDTPOST,PKPOST,SP30
              PACK      PRNTPOST,PKPOST,SP30  * I-55180
            ENDIF
          ENDIF
.
          IF        HFORMAT=6
            UNPACK    PINVDTE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      PTITL,PACTITLE
            MOVE      PSNAME,PACSNAME
            MOVE      PGNAME,ANS
            MOVE      ANS,PACGNAME
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,PATNAME
            MOVE      PACFNAME,RCDTREFR
            MOVE      PURNO,DBDBDEB
            PACK      KEY8,PINVADM
            CALL      RDRESP1                * Read the PRA file to get suburb
            IF        OVRCD<>1
              PACK      PATNAME,PKNAME,SP30
              PACK      PRNTADD1,PKADD1,SP30
              PACK      PRNTADD2,PKADD2,SP30
              PACK      PRNTSUBR,PKSUBR,SP30
              PACK      PRNTADD4,PKADD4,SP30
              PACK      RCDTPOST,PKPOST,SP30
            ENDIF
          ENDIF
.
          IF        HFORMAT = 9
            UNPACK    PINVDTE,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
            MOVE      SP4,PACTITLE
            MOVE      PSNAME,PACSNAME
            MOVE      PGNAME,ANS
            MOVE      ANS,PACGNAME
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,PATNAME
            PACK      PRNTNAME,PATNAME,SP30
            MOVE      PURNO,DIM8
            PACK      PRNTPNUM,PURNO
.
            MOVE      PINVUR,KEY8
            CALL      RESC0000
          ENDIF
.
.         Checking if using Header/Tail template format
.
          COMPARE   TWO,HFORMAT
          GOTO      PRNT4000 IF NOT EQUAL      * Not using template variables
.
          UNPACK    PINVDTE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      SP4,PACTITLE
.
.         Johns Hopkins will have Surname followed by Given Name
.
          IF        CFEETYP=9
            MOVE      PGNAME,PACSNAME
            MOVE      PSNAME,PACGNAME
          ELSE
            MOVE      PSNAME,PACSNAME
            MOVE      PGNAME,ANS
            MOVE      ANS,PACGNAME
          ENDIF
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME
.
          PACK      PRNTNAME,PATNAME,SP30
          PACK      PRNTRNAM,PATNAME,SP30
          MOVE      PURNO,DIM8
          PACK      PRNTPNUM,PURNO
.
          PACK      KEY8,PINVADM
          CALL      RDRESP1                * Read the PRA file to get suburb
          IF        OVRCD<>1
            PACK      PRNTRNAM,PKNAME,SP30
            PACK      PRNTADD1,PKADD1,SP30
            PACK      PRNTADD2,PKADD2,SP30
            PACK      PRNTSUBR,PKSUBR,SP30
            PACK      PRNTADD4,PKADD4,SP30
            PACK      PRNTPOST,PKPOST,SP30
.
.           If there is no Extra Contact details for PRFA, use PRFA details
.
            MATCH     SP70,PRFANAME
            IF        @EQUAL
              PACK      PRFANAME,PKNAME,SP70
              PACK      PRFAADD1,PKADD1,SP70
              PACK      PRFAADD2,PKADD2,SP70
              PACK      PRFAADD3,PKSUBR,SP70
              PACK      PRFAADD4,PKADD4,SP70
              PACK      PRFAPOST,PKPOST,SP70
            ENDIF
          ELSE
            MOVE      PINVUR,KEY8
            CALL      RESC0000
          ENDIF
          GOTO      PRNT4000                  * Print transaction
.
. ------- private practice system -------
.
PRNT1200  COMPARE   ONE,HSYS3
          GOTO      PRNT1000 IF NOT EQUAL
.
          CALL      RDPRIN1                  * Private Practice invoice
          BRANCH    OVRCD,PRNT1000
.
          UNPACK    PRINDATE,CCENT,CYEAR,CMON,CDAY  * Invoice Date
          CALL      PACDATE
          MOVE      PRINDATE,PINVDTE
.
          MOVE      PRINNUMB,PINVADM
          BRANCH    PRINSCAN,PRNT1250         * This is a PMI record
          PACK      DIM12,CODEPP,SP1,PRINDEBT * Debtor number
          PACK      PRNTPNUM,PRINDEBT
.
.         Get patient name
.
          MOVE      "Unknown patient",PATNAME
          MOVE      PRINDEBT,KEY8
          GOTO      PRNT1240
.
.         Private practice Deposit
.
PRNT1230  COMPARE   ONE,HSYS3
          GOTO      PRNT1000 IF NOT EQUAL
.
          UNPACK    RCDTTDAT,CCENT,CYEAR,CMON,CDAY  * transaction Date
          CALL      PACDATE
          MOVE      ZERO,FORM1
          MOVE      RCDTSFLG,FORM1
          BRANCH    FORM1,PRNT1245       * PMI flag
.
          PACK      DIM12,CODEPP,SP1,RCDTURNO * Debtor number
          PACK      PRNTPNUM,SP20
          MOVE      "Unknown patient",PATNAME
          MOVE      RCDTURNO,KEY8
          MOVE      SP4,PACTITLE
.
PRNT1240  
.          CALL      RDPRDB1                * get debtor master record
.          BRANCH    OVRCD,PRNT4000
.
.          MOVE      SP4,PACTITLE          * format debtor name
.          MOVE      PRDBSNAM,PACSNAME
.          MOVE      PRDBGNAM,PACGNAME
.          MOVE      ONE,PACFRMT
.          CALL      PACNAME
.          MOVE      PACFNAME,PATNAME
.
          PACK      PRNTNAME,PATNAME,SP20
.
.          PACK      PRNTRNAM,PRDBRNAM,SP20
.          PACK      PRNTADD1,PRDBRAD1,SP20
.          PACK      PRNTADD2,PRDBRAD2,SP20
.          PACK      PRNTSUBR,PRDBRAD3,SP20
.          PACK      PRNTADD4,PRDBRAD4,SP20
.          PACK      PRNTPOST,PRDBRPOS,SP20
.
          PACK      PRNTRNAM,SP70
          PACK      PRNTADD1,SP70
          PACK      PRNTADD2,SP70
          PACK      PRNTSUBR,SP70
          PACK      PRNTADD4,SP70
          PACK      PRNTPOST,SP70
          GOTO      PRNT4000                  * Print transaction
.
.         Private practice deposit - PMI flag
.
PRNT1245  PACK      DIM12,CODEUR,SP1,RCDTURNO * U/R number
          PACK      PRNTPNUM,RCDTURNO
          MOVE      "Unknown patient",PATNAME
          MOVE      ZEROVISN,PINVADM
          MOVE      SP70,PRINNAMR
          MOVE      RCDTURNO,KEY8
          GOTO      PRNT1255
.
PRNT1250  PACK      DIM12,CODEUR,SP1,PRINDEBT * U/R number
          PACK      PRNTPNUM,PRINDEBT
.
          MOVE      "Unknown patient",PATNAME
          MOVE      PRINDEBT,KEY8
PRNT1255  CALL      RDMAST1                * get patient master record
          BRANCH    OVRCD,PRNT4000
.
          MOVE      PTITL,PACTITLE
          IF        CFEETYP=9
            MOVE      PGNAME,PACSNAME
            MOVE      PSNAME,PACGNAME
          ELSE
            MOVE      PSNAME,PACSNAME
            MOVE      PGNAME,ANS
            MOVE      ANS,PACGNAME
          ENDIF
.
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PATNAME
          PACK      PRNTNAME,PATNAME,SP20
          PACK      PRNTRNAM,PATNAME,SP20
          PACK      PRNTADD1,PADD1,SP30
          PACK      PRNTADD2,PADD2,SP30
          PACK      PRNTSUBR,PSUBRB,SP30
          PACK      PRNTADD4,PADD4,SP30
          PACK      PRNTPOST,PPOST,SP30
          MOVE      PADD1,PPRAADD1
          MOVE      PADD2,PPRAADD2
          MOVE      PSUBRB,PPRASUBR
          MOVE      PPOST,PPRAPOST
.
          MATCH     SP70,PRINNAMR
          IF        !@EQUAL
            PACK      PRNTRNAM,PRINNAMR,SP30
            PACK      PRNTADD1,PRINPRA1,SP30
            PACK      PRNTADD2,PRINPRA2,SP30
            PACK      PRNTSUBR,PRINPRA3,SP30
            PACK      PRNTADD4,PRINPRA4,SP30
            PACK      PRNTPOST,PRINPOST,SP30
          ENDIF
.
          IF    HFORMAT=9
             MOVE      SP4,PACTITLE
             MOVE      PSNAME,PACSNAME
             MOVE      PGNAME,ANS
             MOVE      ANS,PACGNAME
             MOVE      ONE,PACFRMT
             CALL      PACNAME
             MOVE      PACFNAME,PATNAME
             MOVE      PACFNAME,RCDTREFR
             MOVE      PURNO,DBDBDEB
.
             COMPARE   "96",REGIBACD            * Cash Deposits (Priv. Practice)
             IF        @EQUAL
               MOVE      PURNO,KEY8
               CALL      RESC0000
               GOTO      PRNT4000               * Print transaction
             ENDIF
.
             MATCH     SP70,PRINNAMR
             IF        !@EQUAL
               PACK      PRNTRNAM,PRINNAMR,SP30
               PACK      PRNTADD1,PRINPRA1,SP30
               PACK      PRNTADD2,PRINPRA2,SP30
               PACK      PRNTSUBR,PRINPRA3,SP30
               PACK      PRNTADD4,PRINPRA4,SP30
               PACK      PRNTPOST,PRINPOST,SP30
              ELSE
               MOVE      PURNO,KEY8
               CALL      RESC0000
             ENDIF
          ENDIF
          GOTO      PRNT4000                  * Print transaction
.
. ------- new sundry debtors receipt -------
.
PRNT1350  COMPARE   ONE,HSYS6
          GOTO      PRNT1000 IF NOT EQUAL
.
          MOVE      RCDTINVN,FORM8
          PACK      KEY12,SP4,FORM8
          REP       " 0",KEY12
          PACK      KEY21,ONE,KEY12,SP30
          CALL      RSDBFH6
          CALL      RKDBFH6
          BRANCH    OVRCD,PRNT1000
          MATCH     "1",DBFHDTY
          GOTO      PRNT1000 IF NOT EQUAL
          MATCH     KEY12,DBFHDOC
          GOTO      PRNT1000 IF NOT EQUAL
.
          PACK      KEY8,DBFHDEB,SP30
          CALL      RDDBDB1
          BRANCH    OVRCD,PRNT1000
.
          PACK      PRNTPNUM,RCDTURNO,SP70
          PACK      PATNAME,DBDBNA1,SP30
.         PACK      PRNTNAME,DBDBNA1,SP20
          PACK      PRNTNAME,SP70
          PACK      PRNTRNAM,DBDBNA1,SP70
          PACK      PRNTADD1,DBDBAD1,SP30
          PACK      PRNTADD2,DBDBAD2,SP30
          PACK      PRNTSUBR,DBDBAD3,SP30
          PACK      PRNTADD4,DBDBAD4,SP30
          PACK      PRNTPOST,DBDBPC,SP30
          PACK      PTMXCXMP,SP30
          PACK      PTMXCARD,SP30
          MOVE      SP20,NMPNUMB
          MOVE      DBFHDEB,DIM8
          UNPACK    DBFHDDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          GOTO      PRNT4000
.
. ------- internal receipting -------
.
PRNT1400  MOVE      SP8,CPDATE               * Internal Receipting
          PACK      DIM12,CODEIR,SP10
          GOTO      PRNT4000
.
. ------- System codes 95, 96 & 97 --------
.
PRNT1500  IF       REGIBACD=90
            GOTO     PRNT1520             * cash deposit (emr)
          ENDIF
.
          IF       REGIBACD=91
            GOTO     PRNT1520             * cash deposit (out)
          ENDIF
.
          IF       REGIBACD=97
            GOTO     PRNT1520             * cash deposit (pat)
          ENDIF
.
          MOVE      SP8,CPDATE
          GOTO      PRNT4000
.
PRNT1520  PACK      PRNTPNUM,RCDTURNO
          MOVE      RCDTURNO,KEY8
          CALL      RDMAST1                * get patient master record
          BRANCH    OVRCD,PRNT4000
.
          MOVE      PADD1,PPRAADD1
          MOVE      PADD2,PPRAADD2
          MOVE      PSUBRB,PPRASUBR
          MOVE      PPOST,PPRAPOST
.
.         Johns Hopkins will have Surname followed by Given Name
.
.          MOVE      SP4,PACTITLE
.          IF        CFEETYP=9
.            MOVE      PGNAME,PACSNAME
.            MOVE      PSNAME,PACGNAME
.          ELSE
.            MOVE      PSNAME,PACSNAME
.            MOVE      PGNAME,ANS
.            MOVE      ANS,PACGNAME
.          ENDIF
.          MOVE      ONE,PACFRMT
.          CALL      PACNAME
.          MOVE      PACFNAME,PATNAME
.          PACK      PRNTNAME,PATNAME,SP30
.
          MOVE      SP4,PACTITLE
          IF        CFEETYP=9
            MOVE      PGNAME,PACSNAME
            MOVE      PSNAME,PACGNAME
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,PATNAME
            PACK      PRNTNAME,PATNAME,SP30
            GOTO      PRNT1530
          ENDIF
.
          MATCH     SP70,PTMASNAM
          IF        @EQUAL
            MOVE      PSNAME,PACSNAME
            MOVE      PGNAME,ANS
            MOVE      ANS,PACGNAME
            MOVE      ONE,PACFRMT
            CALL      PACNAME
            MOVE      PACFNAME,PATNAME
            PACK      PRNTNAME,PATNAME,SP30
            GOTO      PRNT1530
          ENDIF
.
          MOVE      PGNAME,ANS
          PACK      PRNTNAME,ANS,SP1,PTMASNAM,SP70
          MOVE      PRNTNAME,PATNAME
.
PRNT1530  MOVE      PURNO,KEY8
          MOVE      PURNO,DIM8
.
          MOVE     SP70,PKNAME
          IF       REGIBACD=97
            PACK      PRNTPNUM,PURNO
            PACK      KEY8,RCDTVIST
            CALL      RDRESP1                * Read the PRA file to get suburb
            IF        OVRCD<>1
              MATCH     SP70,PKNAME
              IF        @EQUAL
                PACK      PRNTRNAM,PRNTNAME,SP30
              ELSE
                PACK      PRNTRNAM,PKNAME,SP30
              ENDIF
              PACK      PRNTADD1,PADD1,SP30
              PACK      PRNTADD2,PADD2,SP30
              PACK      PRNTSUBR,PSUBRB,SP30
              PACK      PRNTADD4,PADD4,SP30
              PACK      PRNTPOST,PPOST,SP30
            ELSE
              CALL      RESC0000
            ENDIF
          ELSE
            CALL      RESC0000
          ENDIF
.
.         If Extra Contact for PRFA NOT exists,then use details from patre1af
.
          MATCH     SP70,PRFANAME
          IF        @EQUAL
            MATCH     SP70,PKNAME
            IF        !@EQUAL
              PACK      PRFANAME,PKNAME,SP70
              PACK      PRFAADD1,PKADD1,SP70
              PACK      PRFAADD2,PKADD2,SP70
              PACK      PRFAADD3,PKSUBR,SP70
              PACK      PRFAADD4,PKADD4,SP70
              PACK      PRFAPOST,PKPOST,SP70
            ENDIF
          ENDIF
.
          PACK      DIM12,CODEUR,SP1,PURNO
          GOTO      PRNT4000
.
. print deposit transaction if any, for Nursing Home System only.
.
PRNT1600   IF        REGIBACD=6
            UNPACK    RCDTTDAT,CCENT,CYEAR,CMON,CDAY
            CALL      PACDATE
..            MOVE      CPCDATE,CDATE
            GOTO      PRNT4000
          ENDIF
          GOTO      PRNT1000
.
.         Print only 8 transactions (9 for ACC & WGH)
.
PRNT4000  BRANCH    HFORMAT,PRNT4100,PRNT4200,PRNT4100,PRNT4400:
                            PRNT4100,PRNT4400,PRNT4500,PRNT4600:
                            PRNT4500
.
PRNT4100  BRANCH    CLNO,PRNT5000,PRNT5000,PRNT5000,PRNT5000,PRNT5000:
                         PRNT5000,PRNT5000,PRNT9000
          GOTO      PRNT4900
.
.         Receipt using Header/tail template 
.
PRNT4200  COMPARE   ZERO,CLNO
          GOTO      PRNT4220 IF EQUAL
          MOVE      MAXCLNO,FORM2
          COMPARE   FORM2,CLNO
          GOTO      PRNT9000 IF NOT LESS
          GOTO      PRNT4240
.
PRNT4220  
.         OPEN      CONTROLF,"controlf"
.         READ      CONTROLF,ZERO;*43,IBCNMHOS   * Multi Hospital
.         COMPARE   ONE,IBCNMHOS
.         GOTO      PRNT4230 IF NOT EQUAL
.
.         PACK      KEY8,PINVADM,SP70
.         OPEN      PMSVX1A1,"pmsvx1af"
.         CALL      RDPMVX11
.         BRANCH    OVRCD,PRNT4230
.
.         MATCH     SP70,PMVXMHOS
.         IF        !@EQUAL & !@EOS
.           OPEN      PATHSPA1,"pathspaf"
.           MOVE      PMVXMHOS,KEY3
.           CALL      RDPTHSP1
.           COMPARE   ZERO,OVRCD
.           GOTO      PRNT4235 IF EQUAL
.         ENDIF
.
PRNT4230  
.         UNPACK    SP70,PTHSHOSP,PTHSNAME
.         UNPACK    SP70,PTHSADD1,PTHSADD2
.         UNPACK    SP70,PTHSADD3,PTHSADD4
.         UNPACK    SP70,PTHSPCOD,PTHSTELE,PTHSFAXN
.
PRNT4235  CALL      RHED0000      * print receipt header
.
PRNT4240  CALL      RTRN0000      * print transaction
          IF        REGIBACD=98
            CALL      IRCMN000    * Check internal receipt comments
          ENDIF
          GOTO      PRNT1000
.
PRNT4400  BRANCH    CLNO,PRNT5000,PRNT5000,PRNT5000,PRNT5000,PRNT5000:
                         PRNT5000,PRNT5000,PRNT5000,PRNT5000,PRNT5000:
                         PRNT5000,PRNT5000,PRNT5000,PRNT5000,PRNT5000:
                         PRNT5000,PRNT5000,PRNT5000,PRNT5000,PRNT5000:
                         PRNT5000,PRNT5000,PRNT5000,PRNT9000          
          GOTO      PRNT4900
.
PRNT4500  BRANCH    CLNO,PRNT5000,PRNT5000,PRNT5000,PRNT5000,PRNT5000:
                         PRNT5000,PRNT5000,PRNT5000,PRNT5000,PRNT5000:
                         PRNT5000,PRNT5000,PRNT5000,PRNT5000,PRNT5000:
                         PRNT5000,PRNT5000,PRNT5000,PRNT5000,PRNT5000:
                         PRNT9000
          GOTO      PRNT4900
.
.         Ballarat receipt
.
PRNT4600  COMPARE   ZERO,CLNO
          GOTO      PRNT4900 IF EQUAL
          MOVE      MAXCLNO,FORM2
          SUB       ONE,FORM2
          COMPARE   FORM2,CLNO
          GOTO      PRNT9000 IF NOT LESS
          GOTO      PRNT5000
.
.         Print receipt header
.
PRNT4900   CALL      RHED0000
           MOVE      ZERO,OVRCD
           CALL      RSBHS000        * print on the right side
.
.         Print transaction
.
PRNT5000  CALL      RTRN0000
          GOTO      PRNT1000
.
.         have another item to print but no space on current receipt
.         so print spaces until end of page and then do a header
.
PRNT9000  CALL      RTAL0000
          IF        HFORMAT=8 | HFORMAT=2
            MOVE      ONE,MPGEFLAG
            ADD       ONE,CPAGENO
            MOVE      ONE,HEADTAIL    * Flagged to Tail
            CALL      XRCP0000        * Print Receipt Tail
            PRINT     " "
            MOVE      ZERO,MPGEFLAG
          ENDIF
          MOVE      ZERO,CLNO                * Clear counter
          GOTO      PRNT4000                  * Print receipt header
.
.         have finished the receipt
.
PRNT9100  CALL      REND0000
          IF        HFORMAT=8 | HFORMAT=4 | HFORMAT=2
            ADD       ONE,CPAGENO
            MOVE      ONE,HEADTAIL    * Flagged to Tail
            CALL      XRCP0000        * Print Receipt Tail
            PRINT     " "
            GOTO      PRNT9999
          ENDIF
          IF        HFORMAT=SEVEN | HFORMAT=9
            PRINT     *N,*N,*N,*65,RCBNTOTP:
                      *N,*N,*N,*N,*N,*N,*N,*N,*N:
                      *N,*N,*53,DIM8,*68,RECEIPTN:
                      *N,*14,PATNAME,*N,*14,PRNTADD1,*N,*14,PRNTADD2:
                      *N,*14,PRNTSUBR:
                      *N,*34,PRNTPOST,*N,*65,RCBNTOTP
            GOTO      PRNT9999
          ENDIF
          IF        HFORMAT <> FIVE
            CALL      RTAL0000
          ELSE
            PRINT     *65,RCBNTOTP,*F;
          ENDIF
.
PRNT9999  RETURN
.
.*********************************************************************
.         Print Internal receipt comments if any
.*********************************************************************
IRCMN000  PACK      KEY20,RCDTRECN,RCDTTCNT,SP70
          CALL      RSRCCMN1
IRCMN100  CALL      RKRCCMN1
          BRANCH    OVRCD,IRCMN999
.
          MATCH     RCDTRECN,RCCMRECN        * Same receipt number?
          GOTO      IRCMN999 IF NOT EQUAL
          MATCH     RCDTTCNT,RCCMTCNT        * Same Transaction count?
          GOTO      IRCMN999 IF NOT EQUAL
.
          MATCH     SP70,RCCMLNDA
          GOTO      IRCMN100 IF EQUAL        * Skip blank line
.
          COMPARE   ZERO,CLNO
          GOTO      IRCMN200 IF EQUAL
          MOVE      MAXCLNO,FORM2
          COMPARE   FORM2,CLNO
          GOTO      IRCMN900 IF NOT LESS
          GOTO      IRCMN400
.
IRCMN200  CALL      RHED0000      * print receipt header
.
IRCMN400  MOVE      SP70,KEY50
          UNPACK    RCCMLNDA,KEY50
          PRINT     *12,KEY50
          ADD       ONE,CLNO
          GOTO      IRCMN100
.
.         print spaces until end of page and then do a header
.
IRCMN900  CALL      RTAL0000
          IF        HFORMAT=8 | HFORMAT=2
            MOVE      ONE,MPGEFLAG
            ADD       ONE,CPAGENO
            MOVE      ONE,HEADTAIL    * Flagged to Tail
            CALL      XRCP0000        * Print Receipt Tail
            PRINT     " "
            MOVE      ZERO,MPGEFLAG
          ENDIF
          MOVE      ZERO,CLNO                * Clear counter
          GOTO      IRCMN200                  * Print receipt header
.
IRCMN999  RETURN
+
.*********************************************************************
.         SPAY0000 - Set payment type
.*********************************************************************
SPAY0000  MOVE      "Other Payment",PRNTPTYP     * default
          IF        CFEETYP<>9
            MOVE      RCBDPAYD,PRNTPNAM          * payee name
          ENDIF
.
          MOVE      ZERO,F1
          MOVE      RCBDPTYP,F1
          MOVE      F1,F2
          MOVE      SP70,PRNTPTYP
          MOVE      "Py",KEY2
          PACK      KEY5,KEY2,RCBDPCOD
          CALL      RDCODE1
          IF        OVRCD=0
            SQUEEZE   PTCDNHCP
            MOVE      PTCDNHCP,F2
            UNPACK    TDESC,PRNTPTYP
          ENDIF
.
          IF           F2=2
            IF         CFEETYP=9
              MOVE       RCBDCHQN,PRNTPNAM       * cheque number
            ELSE
              MOVE       RCBDDRAW,PRNTPNAM
            ENDIF
          ENDIF
.
          IF        F2=3
            PACK       KEY5,ANSC,ANSR,RCBDCRDT
            CALL       RDCODE1
            IF         OVRCD=0
              MOVE       TDESC,PRNTPTYP
            ENDIF
            MOVE       RCBDCRDT,PRNTCTYP
            IF         CFEETYP=9
              IF         OVRCD=0
                MOVE        TDESC,PRNTPNAM           * Name of credit card
              ENDIF
            ENDIF
          ENDIF
.
          IF          F2=4
            IF        CFEETYP=9
              MOVE      RCBDSTRF,PRNTPNAM            * statement reference
            ENDIF
          ENDIF
.
          COMPARE   "6",F2
          GOTO      SPAY9999 IF NOT EQUAL
.
.         COMPARE   NINE,CFEETYP
.         GOTO      SPAY9999 IF NOT EQUAL     * Not for JH receipts
.
          MOVE      "Cash  ",PRNTPNAM
          MATCH     "2",RCBDEFTT
          GOTO      SPAY9999 IF NOT EQUAL     * not credit
.
          MOVE      "Credit",PRNTPNAM
          MATCH     SP70,RCBDCRDT
          GOTO      SPAY9999 IF EQUAL         * not by credit card
.
          PACK      KEY5,ANSC,ANSR,RCBDCRDT
          CALL      RDCODE1
          BRANCH    OVRCD,SPAY9999
.
          MOVE      "EFT",KEY3
          PACK      PRNTPTYP,KEY3,SLASH,TDESC,SP70
.
          STRIP     TDESC
          RESET     PRNTPNAM
          APPEND    "-",PRNTPNAM
          APPEND    TDESC,PRNTPNAM           * Name of credit card
          APPEND    SP1,PRNTPNAM
          APPEND    RCBDSTRF,PRNTPNAM        * Ref.
          APPEND    SP70,PRNTPNAM
          RESET     PRNTPNAM
.
SPAY9999  RETURN
.
.*********************************************************************
.         Print payment details for JH
.*********************************************************************
PRBDT000  COMPARE   NINE,CFEETYP
          GOTO      PRBDT100 IF EQUAL       * Print payment details for JH
.
          MATCH     "1",RCCNPAYM          * Check parameter to print payment
          GOTO      PRBDT999 IF NOT EQUAL
.
PRBDT100  COMPARE   ZERO,CLNO
          GOTO      PRBDT999 IF EQUAL           * No payment
          PACK      KEY15,RECEIPTN,SP20
          CALL      RSRCBDT1
PRBDT200  CALL      RKRCBDT1
          BRANCH    OVRCD,PRBDT999
.
          MATCH     RECEIPTN,RCBDRECN
          GOTO      PRBDT999 IF NOT EQUAL      * Different receipt number
.
          MOVE      MAXCLNO,FORM2
          COMPARE   FORM2,CLNO
          GOTO      PRBDT300 IF LESS
.
          CALL      RTAL0000
          MOVE      ONE,MPGEFLAG
          ADD       ONE,CPAGENO
          MOVE      ONE,HEADTAIL    * Flagged to Tail
          CALL      XRCP0000        * Print Receipt Tail
          PRINT     " "
          MOVE      ZERO,MPGEFLAG
          MOVE      ZERO,CLNO                * Clear counter
.
PRBDT300  MOVE      SP70,PRNTPTYP
          MOVE      SP70,PRNTPNAM
          CALL      SPAY0000                 * Set payment type
          IF        CFEETYP=9
            PRINT     *1,PRNTPTYP,*20,PRNTPNAM,*50,RCBDPAMT
          ELSE
            UNPACK    PRNTPTYP,KEY14
            PRINT     *5,KEY14,*20,PRNTPNAM,*50,RCBDPAMT
          ENDIF
          ADD       ONE,CLNO
          GOTO      PRBDT200
.
PRBDT999  RETURN
+
.*********************************************************************
.         Subroutine to get Responsible Person if not in patre1af
.*********************************************************************
RESC0000  MOVE      ZERO,FORM1
          CALL      RDPMPX21
          IF        OVRCD=1
             MOVE     SIX,FORM1
           ELSE
             MOVE     PMPXCONP,FORM1
          ENDIF
          COMPARE   ZERO,FORM1
          IF        @EQUAL
             MOVE     SIX,FORM1
          ENDIF
          BRANCH    FORM1,RESC1000,RESC2000,RESC3000,RESC4000,RESC5000,RESC6000
.
RESC1000  MOVE      PMPXMTLE,PACTITLE
          MOVE      PMPXMSNA,PACSNAME
          MOVE      PMPXMGNA,ANS
          MOVE      ANS,PACGNAME
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PRNTRNAM
          PACK      PRNTADD1,PMPXMAD1,SP30
          PACK      PRNTADD2,PMPXMAD2,SP30
          PACK      PRNTSUBR,PMPXMAD3,SP30
          PACK      PRNTADD4,PMPXMAD4,SP30
          PACK      PRNTPOST,PMPXMPOS,SP30
          GOTO      RESC9999
.
RESC2000  MOVE      PMPXFTLE,PACTITLE
          MOVE      PMPXFSNA,PACSNAME
          MOVE      PMPXFGNA,ANS
          MOVE      ANS,PACGNAME
          MOVE      ONE,PACFRMT
          CALL      PACNAME
          MOVE      PACFNAME,PRNTRNAM
          PACK      PRNTADD1,PMPXFAD1,SP30
          PACK      PRNTADD2,PMPXFAD2,SP30
          PACK      PRNTSUBR,PMPXFAD3,SP30
          PACK      PRNTADD4,PMPXFAD4,SP30
          PACK      PRNTPOST,PMPXFPOS,SP30
          GOTO      RESC9999
.
RESC3000  PACK      PRNTRNAM,PNKNAME,SP30
          PACK      PRNTADD1,PNKADD1,SP30
          PACK      PRNTADD2,PNKADD2,SP30
          PACK      PRNTSUBR,PNKSUBR,SP30
          PACK      PRNTADD4,PNKADD4,SP30
          PACK      PRNTPOST,PNKPOST,SP30
          GOTO      RESC9999
.
RESC4000  PACK      PRNTRNAM,PTMXECNM,SP30
          PACK      PRNTADD1,PTMXECA1,SP30
          PACK      PRNTADD2,PTMXECA2,SP30
          PACK      PRNTSUBR,PTMXECA3,SP30
          PACK      PRNTADD4,PTMXECA4,SP30
          PACK      PRNTPOST,PTMXECPC,SP30
          GOTO      RESC9999
.
RESC5000  PACK      PRNTRNAM,PTMXNRNM,SP30
          PACK      PRNTADD1,PTMXNRA1,SP30
          PACK      PRNTADD2,PTMXNRA2,SP30
          PACK      PRNTSUBR,PTMXNRA3,SP30
          PACK      PRNTADD4,PTMXNRA4,SP30
          PACK      PRNTPOST,PTMXNRPC,SP30
.
RESC6000  PACK      PRNTRNAM,PATNAME,SP30
          PACK      PRNTADD1,PADD1,SP30
          PACK      PRNTADD2,PADD2,SP30
          PACK      PRNTSUBR,PSUBRB,SP30
          PACK      PRNTADD4,PADD4,SP30
          PACK      PRNTPOST,PPOST,SP30
.
RESC9999  RETURN
.
.*********************************************************************
.*                  RWAR0000                    Called by : PRTN0000 *
.*        Print Receipt for Warburton                                *
.*********************************************************************
RWAR0000  MOVE      ZERO,CLNO                * Nothing printed yet
.
          MOVE      SP1,RCBNSTAT             * Get the Receipt info   *I-161012
          PACK      KEY12,RECEIPTN,SP20
          CALL      RDRCBNK1
.
          PACK      CANCLMSG,SP70
          MATCH     "1",RCBNSTAT
          IF        @EQUAL
            MOVE      "THIS  RECEIPT  HAS  BEEN  CANCELLED",CANCLMSG
          ENDIF
.
          PACK      KEY17,RECEIPTN,SP20
          CALL      RSRCDTE1                 * Go through all transactions
.
RWAR1000  CALL      RSRCDTE1
          BRANCH    OVRCD,RWAR4600
.
          MATCH     RCDTRECN,RECEIPTN        * Same receipt still ?
          GOTO      RWAR4600 IF NOT EQUAL     * No - pad out receipt
.
.         Get invoice details
.
          PACK      KEY8,RCDTINVN,SP8          * Invoice number is the key
          UNPACK    RCDTTDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,CDATE
.
          MOVE      RCDTREGI,KEY3
          CALL      RDREGA1
          BRANCH    OVRCD,RWAR1000
.
          BRANCH    REGIBACD,RWAR1100,RWAR1200,RWAR1000
.                          PatBill  PrivPrac Sundry Debtors (ignore old)
.
          COMPARE   "98",REGIBACD              * Internal Receipting
          GOTO      RWAR1400 IF EQUAL
          GOTO      RWAR1000                  * Otherwise ignore
.
RWAR1100  CALL      RDINV1                   * Patient Billing invoice
          BRANCH    OVRCD,RWAR1000           * invalid invoice number
          UNPACK    PINVDTE,CCENT,CYEAR,CMON,CDAY  * Invoice Date
          CALL      PACDATE
          MOVE      PINVUR,DIM8              * UR number
          GOTO      RWAR4000                 * Print transaction
.
RWAR1200  CALL      RDPRIN1                  * Private Practice invoice
          BRANCH    OVRCD,RWAR1000           * invalid invoice number
          UNPACK    PRINDATE,CCENT,CYEAR,CMON,CDAY  * Invoice Date
          CALL      PACDATE
          MOVE      PRINDATE,PINVDTE
          MOVE      PRINDEBT,DIM8            * UR number
          GOTO      RWAR4000                 * Print transaction
.
RWAR1400  MOVE      SP2,XDAY                 * Internal Receipting
          MOVE      SP2,XMON
          MOVE      SP2,XYEAR
          MOVE      SP2,XCENT
          MOVE      SP10,CPDATE              * clear date
          MOVE      ZERO,DIM8                * no UR number
          GOTO      RWAR4000
.
.         Print only 29 transactions (0-28)
.         when CLNO =  1 ; one line has been printed
.         when CLNO = 29 ; 29 lines have been printed
.
RWAR4000  PACK      DIM12,RCDTREFR,SP30
          COMPARE   "29",CLNO
          GOTO      RWAR4400 IF NOT LESS    * finished a page
.
          COMPARE   ZERO,CLNO
          GOTO      RWAR4200 IF NOT EQUAL   * normal line
.
.         Print receipt header (line 15 & 18)
.         (assumes we are on line 1, so 14 *N gets to line 15)
.
          PRINT     *N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*68,RECEIPTN:
                    *N,*N,*N,*7,"RECEIPT",*68,CDATE,*N,*N,*15,CANCLMSG,*N,*N
.
.         Print transaction (lines 23 - 51)
.
RWAR4200  PRINT     *3,RCDTREGI,*10,RCDTINVN,*20,RCDTREFR:
                    *44,DIM8,*57,CPDATE,*68,RCDTAMNT
          ADD       ONE,CLNO
          GOTO      RWAR1000
.
.         receipt has gone over a page (line 53)
.
RWAR4400  PRINT     *N,"  continued on next page ",*N,*N,*N,*N,*N,*N,*N,*N,*N:
                    *N,*N,*N,*N,*N;
          MOVE      ZERO,CLNO                * Clear counter
          GOTO      RWAR4000                 * Print receipt header
.
.         at end of current receipt
.
RWAR4600  COMPARE   "29",CLNO
          GOTO      RWAR5000 IF NOT LESS    * finished receipt
.
.         print blank lines till end of receipt
.
          PRINT     *1,SP10                 * Print another line
          ADD       ONE,CLNO                * Increment counter
          GOTO      RWAR4600
.
.         print the total at end of receipt (line 61) and leave at line 1 of new
.         receipt (line 67 of current one)
.
RWAR5000  PRINT     *N,*N,*N,*N,*N,*N,*N,*N,*N,*68,RCBNTOTP,*N,*N,*N,*N,*N,*N;
.
RWAR9995  CALL      CLSPRINT                 * Print out receipt
.
RWAR9999  RETURN
+
.*********************************************************************
.         print the receipt header
.*********************************************************************
RHED0000  UNPACK    RCBNTDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPDATE,CDATE
          BRANCH    HFORMAT,RHED0200,RHED0200,RHED0100,RHED0400,RHED0500:
                            RHED0600,RHED0700,RHED0800,RHED0900
.
RHED0100  PRINT     *N,*72,CDAY,SLASH,CMON,SLASH,CYEAR:
                    *N,*N,*72,RECEIPTN,*N,*N,*26,PRNTNAME:
                    *69,RCBNTOTP,*N,*26,RCDTADD1:
                    *N,*26,RCDTADD2,*43,RCDTPOST,*N,*N,*15,CANCLMSG
          GOTO      RHED9999
.
RHED0200  MOVE      ZERO,HEADTAIL      * template header & tail for Pendlebury
          CALL      XRCP0000           * Print Receipt Header
          MATCH     "1",RCBNSTAT                                      *I-161012
          IF        @EQUAL
            PRINT     *N,*15,CANCLMSG
          ELSE
            PRINT     *N;
          ENDIF
          GOTO      RHED9999
.
RHED0400  MOVE      ZERO,HEADTAIL      * template header & tail for Pendlebury
          CALL      XRCP0000           * Print Receipt Header
.... D-55180
....      PRINT     *N,*82,CDAY,SLASH,CMON,SLASH,CYEAR:
....                *N,*N,*26,PRNTNAME,*82,RECEIPTN,*N,*26,RCDTADD1:
....                *N,*26,RCDTADD2,*47,RCDTPOST,*79,RCBNTOTP,*N,*N
.... END D
          MATCH     "1",RCBNSTAT                                      *I-161012
          IF        @EQUAL
            PRINT     *N,*15,CANCLMSG
          ENDIF
          GOTO      RHED9999
.
. ------- layout 5 - Hawkes Bay -------
.
RHED0500  MATCH     SP70,PRNTNAME
          IF        @EQUAL
            PRINT     *1,PRNTRNAM,*32,PURNO 
          ELSE
            PRINT     *1,PRNTNAME,*32,PURNO 
          ENDIF
.
          MATCH     "RCPWEB03",PRGID
          IF        @EQUAL
            PRINT     *N,*1,PRNTADD1,*44,"RECEIPT       ",*65,RECEIPTN 
          ELSE
            PRINT     *N,*1,PRNTADD1,*44,"RECEIPT (COPY)",*65,RECEIPTN 
          ENDIF
          PRINT     *N,*1,PRNTADD2,*65,CDATE: 
                    *N,*1,PRNTSUBR,*33,PRNTPOST:
                    *N,*N,*N,*10,PTMXCXMP,*35,PTMXCARD,*63,NMPNUMB:
                    *N,*N,*N,*1,"Register",*35,"Invoice No.",*50,"  Date  ":
                    *70,"Amount",*N,*15,CANCLMSG                      *C-161012
          GOTO      RHED9999
.
. ------- layout 6 - West Gippsland -------
.
RHED0600  PRINT     *N,*N:
                    *N,*N,*13,PRNTNAME,*65,CDATE:
                    *N,*13,PRNTADD1:
                    *N,*13,PRNTADD2,*65,RECEIPTN:
                    *N,*13,PRNTSUBR
          MATCH     "RCPWEB03",PRGID
          IF        @EQUAL
            PRINT     *N,*38,PRNTPOST,*65,RCBNTOTP,*N,*N,*24,"RECEIPT       "
          ELSE
            PRINT     *N,*38,PRNTPOST,*65,RCBNTOTP,*N,*N,*24,"RECEIPT (COPY)"
          ENDIF
          PRINT     *N,*N,*15,CANCLMSG,*N                             *C-161012
          GOTO      RHED9999
.
. ------- layout 7 - West Wimmera -------
.
RHED0700  MATCH     "RCPWEB03",PRGID
          IF        @EQUAL
            PRINT     *N,*N,*60,"RECEIPT       " 
          ELSE
            PRINT     *N,*N,*60,"RECEIPT (COPY)" 
          ENDIF
          PRINT     *N,*N,*N,*N,*53,RECEIPTN,*70,CDATE:
                    *N,*N,*N,*N,*N,*14,PRNTNAME:
                    *N,*14,PRNTADD1,*57,DIM12:
                    *N,*14,PRNTADD2:
                    *N,*14,PRNTSUBR:
                    *N,*34,PRNTPOST:
                    *N,*N,*N,*N,*N,*N,*15,CANCLMSG,*N,*N              *C-161012
          GOTO      RHED9999
.
. ------- layout 9 - Ballarat Health Services -------
.
RHED0800  MOVE      ZERO,HEADTAIL      * Flagged to Header
          CALL      XRCP0000           * Print Receipt Header
          MATCH     "1",RCBNSTAT                                      *I-161012
          IF        @EQUAL
            PRINT     *N,*15,CANCLMSG
          ENDIF
.
          GOTO      RHED9999
.
RHED0900  MATCH     "RCPWEB03",PRGID
          IF        @EQUAL
            PRINT      *1,"CREATEFORMRCHRECEIPT":
                       *N,*1,"rchreceipt":
                       *N,*65,"RECEIPT       ": 
                       *N,PRNTPNAM:
                       *N,*N,*N,*53,RECEIPTN,*70,CDATE:
                       *N,*N,*14,PRNTRNAM:
                       *N,*N,*N,*14,PRNTNAME:
                       *N,*14,PRNTADD1,*57,DIM12:
                       *N,*14,PRNTADD2:
                       *N,*14,PRNTSUBR:
                       *N,*34,PRNTPOST:
                       *N,*N,*N,*N,*N,*N,*15,CANCLMSG,*N,*N           *C-161012
          ELSE
            PRINT      *1,"CREATEFORMRCHRECEIPT":
                       *N,*1,"rchreceipt": 
                       *N,*65,"RECEIPT (COPY)": 
                       *N,PRNTPNAM:
                       *N,*N,*N,*53,RECEIPTN,*70,CDATE:
                       *N,*N,*14,PRNTRNAM:
                       *N,*N,*N,*14,PRNTNAME:
                       *N,*14,PRNTADD1,*57,DIM12:
                       *N,*14,PRNTADD2:
                       *N,*14,PRNTSUBR:
                       *N,*34,PRNTPOST:
                       *N,*N,*N,*N,*N,*N,*15,CANCLMSG,*N,*N           *C-161012
          ENDIF
          GOTO      RHED9999
.
RHED9999  RETURN
+
.*********************************************************************
.         print the transaction
.*********************************************************************
RTRN0000  MOVE      "Unknown register",REGDESC
          MOVE      RCDTREGI,KEY3
          CALL      RDREGA1
          MOVE      REGDESC,SHREGIS
.
          UNPACK    RCDTTDAT,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          BRANCH    HFORMAT,RTRN0100,RTRN0200,RTRN0100,RTRN0400,RTRN0500:
                            RTRN0600,RTRN0700,RTRN0800,RTRN0900
.
.                                                       * added reg 92 *I-90301
.
RTRN0100  IF        REGIBACD=90 | REGIBACD=91 | REGIBACD=95 | REGIBACD=96 | REGIBACD=97 | REGIBACD=92 | REGIBACD=89
            MOVE      RCDTREFR,REF10
            PRINT     *10,RCDTREGI,*21,REF10,*33,DIM12:
                      *50,CPDATE,*62,RCDTAMNT
          ELSE
            PRINT     *10,RCDTREGI,*21,RCDTINVN,*33,DIM12:
                      *50,CPDATE,*62,RCDTAMNT
          ENDIF
          GOTO      RTRN9999
.
.         Header and Tail template
.
RTRN0200 IF REGIBACD=95 | REGIBACD=92 | REGIBACD=94 | REGIBACD=98 | REGIBACD=99 | REGIBACD=89
            IF        CFEETYP=9
              PRINT     CPCDATE,*16,RCDTREFR,*65,RCDTAMNT
            ELSE
.             PRINT     CPCDATE,*12,RCDTREGI,*16,RCDTREFR,*65,RCDTAMNT
              UNPACK    REGDESC,KEY25
              PRINT     CPCDATE,*12,KEY25,*40,RCDTREFR,*65,RCDTAMNT
            ENDIF
          ELSE
            IF      REGIBACD=96 | REGIBACD=97 | REGIBACD=90 | REGIBACD=91
              IF        CFEETYP=9
                UNPACK    REGDESC,KEY20
                PRINT     *16,KEY20:
                          *40,"(",RCDTVIST,")",*65,RCDTAMNT
              ELSE
.               PRINT     *12,RCDTREGI,*16,REGDESC:
.                         *65,RCDTAMNT
                PACK       KEY5,ANSD,ANSP,RCDTDPTY,SP70
                CALL       RDCODE1
                IF         OVRCD=1
                  MOVE       SP70,TDESC
                ENDIF
                UNPACK    REGDESC,KEY20
                UNPACK    TDESC,KEY15
                PRINT     *1,CPCDATE,*12,RCDTVIST,*21,KEY20,*49,KEY15:
                          *65,RCDTAMNT
              ENDIF
            ELSE
.
              IF        REGIBACD=5
                PACK      KEY21,ONE,RCDTINVN,RCDTURNO,SP70
                CALL      RDDBFH6
                IF        OVRCD=1
                  UNPACK    RCDTTDAT,CCENT,CYEAR,CMON,CDAY
                ELSE
                  UNPACK    DBFHDDAT,CCENT,CYEAR,CMON,CDAY
                ENDIF
                CALL      PACDATE
                PRINT     CPCDATE,*12,RCDTREGI,*16,RCDTINVN,*29,REGDESC,*65,RCDTAMNT
                GOTO      RTRN0300
              ENDIF
.
              IF        REGIBACD=2
                UNPACK    RCDTINVN,KEY4,KEY8
                CALL      RDPRIN1
                IF        OVRCD=1
                  UNPACK    RCDTTDAT,CCENT,CYEAR,CMON,CDAY
                ELSE
                  UNPACK    PRINDATE,CCENT,CYEAR,CMON,CDAY
                ENDIF
                CALL      PACDATE
                PRINT     CPCDATE,*12,RCDTREGI,*16,RCDTINVN,*29,REGDESC,*65,RCDTAMNT
                GOTO      RTRN0300
              ENDIF
.
              UNPACK    RCDTINVN,KEY4,KEY8
              CALL      RDINV1
              UNPACK    PINVDTE,CCENT,CYEAR,CMON,CDAY  * Invoice Date
              CALL      PACDATE
              IF        CFEETYP=9
                PRINT     CPCDATE,*16,RCDTINVN,*29,REGDESC,*65,RCDTAMNT
              ELSE
                PRINT     CPCDATE,*12,RCDTREGI,*16,RCDTINVN,*29,REGDESC,*65,RCDTAMNT
              ENDIF
.
            ENDIF
          ENDIF
RTRN0300  ADD       ONE,CLNO
          GOTO      RTRN9999
.
RTRN0400  MOVE      RCDTAMNT,FORM8P2
          UNPACK    PRNTPTYP,KEY14
          PRINT     *3,CPDATE,*13,KEY14,*28,PRNTCTYP,*32,RCBDPAYD:
                    *52,RCDTREGI,*55,RCDTINVN,*68,FORM8P2
.... D - CAR 55180
....      MOVE      PATNAME,PNAM15
....      MOVE      RCDTAMNT,FORM8P2
....      PRINT     *1,SHREGIS,*28,RCDTINVN,*37,DIM12,*50,RCDTREFR,*71,PNAM15:
....                *87,FORM8P2
.... End D
          ADD       ONE,CLNO
          GOTO      RTRN9999
.
. ----- layout 5 - Hawkes Bay -------
.
RTRN0500  PRINT     *1,SHREGIS,*35,RCDTINVN,*50,CPDATE,*65,RCDTAMNT
          GOTO      RTRN9999
.
. ----- layout 6 - West Gippsland -------
.
RTRN0600  PRINT     *1,RCDTREGI,*10,RCDTREFR,*30,DBDBDEB,*41,RCDTINVN:
                    *52,CPCDATE,*64,RCDTAMNT
          GOTO      RTRN9999
.
. ----- layout 7 - West Wimmera & layout 8 RCH -------
.
.                                                       * added reg 92 *I-90301
.
RTRN0700  IF       REGIBACD = 95 | REGIBACD = 96 | REGIBACD = 97 | REGIBACD = 92 | REGIBACD = 89
            PRINT     CPCDATE,*12,RCDTREGI,*16,RCDTREFR,*65,RCDTAMNT
          ELSE
            PRINT     CPCDATE,*12,RCDTREGI,*16,RCDTINVN,*65,RCDTAMNT
          ENDIF
          GOTO      RTRN9999
.
. ----- layout 8 - Ballarat Health Services -------
.
.                                                       * added reg 92 *I-90301
.
RTRN0800  IF       REGIBACD = 95 | REGIBACD = 96 | REGIBACD = 97 | REGIBACD = 92 | REGIBACD = 89
            PRINT     CPCDATE,*12,RCDTREGI,*16,RCDTREFR,*89,RCDTAMNT;
          ELSE
            PRINT     CPCDATE,*12,RCDTREGI,*16,RCDTINVN,*89,RCDTAMNT;
          ENDIF
          ADD       ONE,CLNO
          MOVE      ZERO,OVRCD
          CALL      RSBHS000        * print on the right side
          GOTO      RTRN9999
.
.
. ----- layout 9 - RCH  -------
.
.                                                       * added reg 92 *I-90301
.
RTRN0900  IF      REGIBACD=95 | REGIBACD=92 | REGIBACD=94 | REGIBACD=98 | REGIBACD=99 | REGIBACD = 89
            PRINT     CPCDATE,*12,RCDTREGI,*16,RCDTREFR,*65,RCDTAMNT
          ELSE
            IF      REGIBACD=96 | REGIBACD=97 | REGIBACD=90 | REGIBACD=91
              PRINT     *12,RCDTREGI,*65,RCDTAMNT
            ELSE
              UNPACK    PINVDTE,CCENT,CYEAR,CMON,CDAY  * Invoice Date
              CALL      PACDATE
              PRINT     CPCDATE,*12,RCDTREGI,*16,RCDTINVN,*65,RCDTAMNT
            ENDIF
          ENDIF
          GOTO      RTRN9999
RTRN9999  RETURN
+
.*********************************************************************
.         print tail when going over a page
.*********************************************************************
RTAL0000  BRANCH    HFORMAT,RTAL0800,RTAL0800,RTAL0100,RTAL0400,RTAL0100:
                            RTAL0600,RTAL0700,RTAL0800,RTAL0900
.
RTAL0100  PRINT     *N,*N,*N
          GOTO      RTAL9999
.
RTAL0400  PRINT     *N,*N,*N,*N
          GOTO      RTAL9999
.
RTAL0600  PRINT     *N,*N,*N,*N,*N,*N,*N,*N,*N,*N,*N
          gOTO      RTAL9999
.
RTAL0700  PRINT     *N,*N,*69,"CARRIED FWD":
                    *N,*N,*N,*N,*N,*N,*N,*N,*N,*N:
                    *N,*N,*N,*N,*N
          GOTO      RTAL9999
.
RTAL0800  MOVE      ZERO,CURRROW              * current row counter
.         CALL      XCRF0000                   * Position Carried Forward
          GOTO      RTAL9999
.
RTAL0900  PRINT     *N,*N,*69,"CARRIED FWD":
                    *N,*N,*N,*N,*N,*N,*N,*N,*N,*N:
                    *N,*N,*N,*N,*N
          GOTO      RTAL9999
RTAL9999  RETURN
+
.*********************************************************************
.         print the end of the receipt
.*********************************************************************
REND0000  BRANCH    HFORMAT,REND0210,REND0200,REND0100,REND0400,REND0100:
                            REND0400,REND0500,REND0600,REND0500
.
REND0100  BRANCH    CLNO,REND9000,REND9000,REND9000,REND9000,REND9000:
                         REND9000,REND9000,REND9999
          GOTO      REND9000
.
REND0200  CALL      PRBDT000               * Print payment details
.
REND0210  COMPARE   MAXCLNO,CLNO           * check for length of body invoice
          GOTO      REND9999 IF NOT LESS
          PRINT     *N;
          ADD       ONE,CLNO
          GOTO      REND0210
.
REND0400  BRANCH    CLNO,REND9000,REND9000,REND9000,REND9000,REND9000:
                         REND9000,REND9000,REND9000,REND9000,REND9000:
                         REND9000,REND9000,REND9000,REND9000,REND9000:
                         REND9000,REND9000,REND9000,REND9000,REND9000:
                         REND9000,REND9000,REND9000,REND9999
          GOTO      REND9000
.
REND0500  BRANCH    CLNO,REND9000,REND9000,REND9000,REND9000,REND9000:
                         REND9000,REND9000,REND9000,REND9000,REND9000:
                         REND9000,REND9000,REND9000,REND9000,REND9000:
                         REND9000,REND9000,REND9000,REND9000,REND9000:
                         REND9999
          GOTO      REND9000
.
REND0600  ADD       ONE,CLNO
          MOVE      ONE,OVRCD
          CALL      RSBHS000        * print on the right side
          GOTO      REND9999
.
REND9000  PRINT     *N;                      * Print another line
          ADD       ONE,CLNO                 * Increment counter
          GOTO      REND9999
.
REND9999  RETURN
.------------------------------------------------------------------------------
. Print something on the right hand side of Stationery
. Requires : MAXCLNO
.            OVERCD = 1 - loop thru until MAXCLNO reach
.                     0 - just find the appropriate position to print
.------------------------------------------------------------------------------
RSBHS000  
          COMPARE   ZERO,CLNO
          GOTO      RSBHS020 IF EQUAL
          COMPARE   ONE,CLNO
          GOTO      RSBHS040 IF EQUAL
.
          COMPARE   TEN1,CLNO
          GOTO      RSBHS060 IF EQUAL
.
          COMPARE   TWENTY1,CLNO
          GOTO      RSBHS080 IF EQUAL
.
          COMPARE   MAXCLNO,CLNO
          GOTO      RSBHS010 IF NOT LESS
.
          PRINT     *N;
RSBHS010  BRANCH    OVRCD,RSBHS990
          GOTO      RSBHS999
.
RSBHS020  PRINT     *N;
          BRANCH    OVRCD,RSBHS990
          GOTO      RSBHS999
.
RSBHS040  PRINT     *114,PRNTPNUM
          BRANCH    OVRCD,RSBHS990
          GOTO      RSBHS999
.
RSBHS060  PRINT     *116,RECEIPTN
          BRANCH    OVRCD,RSBHS990
          GOTO      RSBHS999
.
RSBHS080  PRINT     *116,RCBNTOTP
          BRANCH    OVRCD,RSBHS990
          GOTO      RSBHS999
.
RSBHS990  ADD       ONE,CLNO
.
          COMPARE   MAXCLNO,CLNO
          GOTO      RSBHS999 IF NOT LESS
          GOTO      RSBHS000
.
RSBHS999  RETURN
+
.------------------------------------------------------------------------------
. Set Template Files
. Returns  : TMPLTFLG : 0 - do template, 1 - don't template
.            TAILLENG
.------------------------------------------------------------------------------
STMPL000  MOVE      ONE,TMPLTFLG
          MOVE      ZERO,TAILLENG
.
          READ      CONTROLF,THIRTY6;*205,RCCNRCHD,RCCNRCTL
          READ      CONTROLF,SEVENTY8;*52,AECNAERH,*58,AECNAERT
.
          CALL      CSYS0000              * check if this is an ED receipt
          BRANCH    EXIT,STMPL020         * first record is not for EMR visit
.
          MATCH     "000000",AECNAERH     * Emergency Receipt header
          GOTO      STMPL020 IF EQUAL
          MATCH     SP70,AECNAERH         * Emergency Receipt header
          GOTO      STMPL020 IF EQUAL
.
          MATCH     "000000",AECNAERT     * Emergency Receipt tail
          GOTO      STMPL020 IF EQUAL
          MATCH     SP70,AECNAERT         * Emergency Receipt tail
          GOTO      STMPL020 IF EQUAL
.
          MOVE      AECNAERH,RCCNRCHD
          MOVE      AECNAERT,RCCNRCTL
          GOTO      STMPL100
.
STMPL020  MATCH     "000000",RCCNRCHD
          GOTO      STMPL050 IF EQUAL
.
          MATCH     SP6,RCCNRCHD
          GOTO      STMPL100 IF NOT EQUAL
.
STMPL050  MATCH     "000000",RCCNRCTL
          GOTO      STMPL999 IF EQUAL
.
          MATCH     SP6,RCCNRCTL
          GOTO      STMPL999 IF EQUAL
.
STMPL100  PACK      KEY6,RCCNRCHD,SP70
          CALL      RDIBTH1               * read header detail file
          BRANCH    OVRCD,STMPL900
.
          PACK      KEY6,RCCNRCTL,SP70
          CALL      RDIBTH1               * read header detail file
          BRANCH    OVRCD,STMPL910
.
          MOVE      IBTHLENG,TAILLENG
.
          GOTO      STMPL990
.
.------ stationery code not on file ------
.
STMPL900  MOVE     "User Defined Stationery Header Code not on File. ",WEBTITLE
          CALL      WEBERR00     * Output File & Write HTML Pg Header
          GOTO      STMPL999
.
.
STMPL910  MOVE      "User Defined Stationery Tail Code not on File. - ",WEBTITLE
          CALL      WEBERR00     * Output File & Write HTML Pg Header
          GOTO      STMPL999
.
STMPL990  MOVE      ZERO,TMPLTFLG
.
STMPL999  RETURN
.------------------------------------------------------------------------------
. Print Receipt Header or Tail Templating
. Requires : HEADTAIL : 0 - Head , 1 - Tail
.------------------------------------------------------------------------------
XRCP0000  MOVE      ONE,CURRROW              * Initialise current row counter
.
          IF        HEADTAIL=0
            PACK      KEY6,RCCNRCHD,SP70
            CALL      RDIBTH1               * read header detail file
.
            PACK      KEY12,RCCNRCHD,SP70
          ELSE
            PACK      KEY6,RCCNRCTL,SP70
            CALL      RDIBTH1               * read header detail file
.
            PACK      KEY12,RCCNRCTL,SP70
          ENDIF
          CALL      RSIBDET1
          CALL      RKIBDET1
          BRANCH    OVRCD,XRCP8000
.
          IF        HEADTAIL=0
            MATCH     RCCNRCHD,IBDTSCOD        * Same form ?
            GOTO      XRCP8000 IF NOT EQUAL    * No - pad out rest of form
            GOTO      XRCP0100
          ELSE
            MATCH     RCCNRCTL,IBDTSCOD        * Same form ?
            GOTO      XRCP8000 IF NOT EQUAL    * No - pad out rest of form
            GOTO      XRCP0100
          ENDIF
.
XRCP0100  COMPARE   CURRROW,IBDTROW          * Current row up to required row ?
          GOTO      XRCP0400 IF EQUAL        * Yes - print data
          PRINT     *N;                      * Else print 1 extra line
          ADD       ONE,CURRROW              * Increment counter
          GOTO      XRCP0100                 * Check counter again
.
. ------- Read through the details file -------
.
XRCP0200  CALL      RKIBDET1                 * Read next detail record
          BRANCH    OVRCD,XRCP8000           * No more records - pad out form
.
          BRANCH    HEADTAIL,XRCP0250        * Header or Tail ?
.
          MATCH     RCCNRCHD,IBDTSCOD        * Same form ?
          GOTO      XRCP8000 IF NOT EQUAL    * No - pad out rest of form
          GOTO      XRCP0300
.
XRCP0250  MATCH     RCCNRCTL,IBDTSCOD        * Same form ?
          GOTO      XRCP8000 IF NOT EQUAL    * No - pad out rest of form
.
XRCP0300  COMPARE   CURRROW,IBDTROW          * Are we at the required row ?
          GOTO      XRCP0400 IF EQUAL        * Yes - print data
          PRINT     *N;                      * Else pad out to required row
          ADD       ONE,CURRROW              * One more row printed
          GOTO      XRCP0300                 * Check row count
.
. ------- Print actual data lines -------
.
XRCP0400  BRANCH    IBDTINDC,XRCP0500:       * Text field
                             XRCP0600        * Data field
.
. ------- Print text field -------
.
XRCP0500  RESET     IBDTTXFL,IBDTPLEN
          LENSET    IBDTTXFL
          RESET     IBDTTXFL
.
          IF        IBDTPLEN=7
            MOVE      IBDTTXFL,DIM40
            REP       UPPLOW,DIM40
            MATCH     "RECEIPT",DIM40
            IF        @EQUAL
              RESET     IBDTTXFL,IBDTPLEN
              LENSET    IBDTTXFL
              MATCH     "RCPWEB01",PRGID
              IF        @EQUAL
                APPEND    " (COPY)",IBDTTXFL
              ENDIF
              RESET     IBDTTXFL
            ENDIF
          ENDIF
.
          PRINT     *IBDTCOL,*+,IBDTTXFL;    * Print data
          MOVE      IBDTROW,CURRROW          * Row printed is current row
          GOTO      XRCP0200                 * Read next record
.
. ------- Print data field -------
.
XRCP0600  MOVE      IBDTTXFL,DIM5            * Data number 1st 3 chars of field
          MOVE      DIM5,FORM5               * Move to a FORM field for BRANCH
          PACK      VARIABLE,SP30,SP30,SP30,SP30,SP10  * Initialise variable
.
          BRANCH    HEADTAIL,XRCP0650        * Header or Tail ?
.
          BRANCH    FORM5,XRCP1010,XRCP1020,XRCP1030,XRCP1040,XRCP1050:   * 5
                          XRCP1060,XRCP1070,XRCP1080,XRCP1090,XRCP1100:   *10
                          XRCP1110,XRCP1120,XRCP1130,XRCP1140,XRCP1150:   *15
                          XRCP1160,XRCP1170,XRCP1180,XRCP1190,XRCP1200:   *20
                          XRCP1210,XRCP1220,XRCP1230,XRCP1240,XRCP1250:   *25
                          XRCP1260,XRCP1270,XRCP1280,XRCP1290,XRCP1300:   *30
                          XRCP1310,XRCP1320,XRCP1330,XRCP1340,XRCP1350:   *35
                          XRCP1360,XRCP1370,XRCP1380,XRCP1390,XRCP1400:   *40
                          XRCP1410,XRCP1420,XRCP1430,XRCP1440,XRCP1450:   *45
                          XRCP1460
          GOTO      XRCP0200                 * ignoring, Read next record
.
XRCP0650  BRANCH    FORM5,XRCP1010,XRCP1020,XRCP1030,XRCP1040,XRCP1050:   * 5
                          XRCP1060,XRCP1070,XRCP1080,XRCP0710,XRCP0720:   *10
                          XRCP0730,XRCP1180,XRCP1190,XRCP1200,XRCP1370:   *15
                          XRCP1380,XRCP1390,XRCP1400,XRCP1410,XRCP1420:   *20
                          XRCP1430,XRCP1440,XRCP1450
          GOTO      XRCP0200                 * ignoring, Read next record
.
. Tail Items/Fields below
.
XRCP0710  BRANCH    MPGEFLAG,XRCP0712
          MOVE      RCBNTOTP,VARIABLE        * Unformated Total Receipt
          MOVE      TEN1,PRNTSTRT
          SUB       IBDTPLEN,PRNTSTRT
          GOTO      XRCP6000                 * C-55180
.
XRCP0712  MOVE      "C/FWD",VARIABLE
          GOTO      XRCP6000                 * C-55180
.
.... I-55180
XRCP0720  MOVE      RCBNCDRW,VARIABLE        * Cash Drawer
          GOTO      XRCP5000
.
XRCP0730  MOVE      CPAGENO,KEY3             * Page no
          PACK      VARIABLE,KEY3,SP70
          GOTO      XRCP5000
.
. Header Items/Fields below
.
XRCP1010  MOVE      RECEIPTN,VARIABLE        * Receipt Number
          GOTO      XRCP5000
.
XRCP1020  UNPACK    CRCPDATE,CCENT,CYEAR,CMON,CDAY    * Receipt date
          CALL      PACDATE
          IF        IBDTPLEN = 10
            MOVE      CPCDATE,VARIABLE
          ELSE
            MOVE      CPDATE,VARIABLE
          ENDIF
          GOTO      XRCP5000
.
XRCP1030  MOVE      PRNTNAME,VARIABLE        * Patient/Debtor Name
          GOTO      XRCP5000
.
XRCP1040  MOVE      PRNTADD1,VARIABLE        * Patient Address Line 1
          GOTO      XRCP5000
.
XRCP1050  MOVE      PRNTADD2,VARIABLE        * Patient Address Line 2
          GOTO      XRCP5000
.
XRCP1060  MOVE      PRNTSUBR,VARIABLE        * Patient Suburb
          GOTO      XRCP5000
.
XRCP1070  MOVE      PRNTPOST,VARIABLE        * Patient Post Code
          GOTO      XRCP5000
.
XRCP1080  MOVE      PRNTPNUM,VARIABLE        * U/R, Private, or Debtor Number
          GOTO      XRCP5000
.
XRCP1090  MOVE      PTHSHOSP,VARIABLE        * Hospital ID
          GOTO      XRCP5000
.
XRCP1100  MOVE      PTHSNAME,VARIABLE        * Hospital Name
          GOTO      XRCP5000
.
XRCP1110  MOVE      PTHSADD1,VARIABLE        * Hospital Address 1
          GOTO      XRCP5000
.
XRCP1120  MOVE      PTHSADD2,VARIABLE        * Hospital Address 2
          GOTO      XRCP5000
.
XRCP1130  MOVE      PTHSADD3,VARIABLE        * Hospital Address 3
          GOTO      XRCP5000
.
XRCP1140  MOVE      PTHSADD4,VARIABLE        * Hospital Address 4
          GOTO      XRCP5000
.
XRCP1150  MOVE      PTHSPCOD,VARIABLE        * Hospital Postcode
          GOTO      XRCP5000
.
XRCP1160  MOVE      PTHSTELE,VARIABLE        * Hospital Telephone no
          GOTO      XRCP5000
.
XRCP1170  MOVE      PTHSFAXN,VARIABLE        * Hospital Fax no
          GOTO      XRCP5000
.
XRCP1180  MOVE      PRNTADD4,VARIABLE        * Patient Address Line 4
          GOTO      XRCP5000
.
XRCP1190  MOVE      PRNTRNAM,VARIABLE        * Person Responsible for Account
          GOTO      XRCP5000
.
XRCP1200  PROC      GETALTID                 * Singapore HRN
          GOTO      XRCP5000
.
XRCP1210  MOVE      PPRAADD1,VARIABLE        * PMI Address Line 1
          GOTO      XRCP5000
.
XRCP1220  MOVE      PPRAADD2,VARIABLE        * PMI Address Line 2
          GOTO      XRCP5000
.
XRCP1230  MOVE      PPRASUBR,VARIABLE        * PMI Address Line 3
          GOTO      XRCP5000
.
XRCP1240  MOVE      PPRAPOST,VARIABLE        * PMI Address Line 4
          GOTO      XRCP5000
.
XRCP1250  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*138,PTCNNHII
          IF        PTCNNHII=1
            OPEN      NHIMASA2,"nhimasaf"
            MOVE      PURNO,KEY8
            CALL      RDNHMAS2
            IF        OVRCD=0
              MOVE      NHMAGIV1,VARIABLE     * NHI Given Name
            ENDIF
          ENDIF
          GOTO      XRCP5000
.
XRCP1260  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*138,PTCNNHII
          IF        PTCNNHII=1
            OPEN     NHIMASA2,"nhimasaf"
            MOVE     PURNO,KEY8
            CALL     RDNHMAS2
            IF       OVRCD=0
              MOVE     SP70,PACTITLE
              CALL     PACNHIN
              MOVE     PACFNAME,VARIABLE      * Formatted NHI Name
            ENDIF
          ENDIF
          GOTO      XRCP5000
.
XRCP1270  MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      SP70,PACTITLE
          MOVE      TWO,PACFRMT            * format - Surname, Given name
          CALL      PACNAME
          MOVE      PACFNAME,VARIABLE      * Formatted Patient's Name
          GOTO      XRCP5000
.
XRCP1280  OPEN      CONTROLF,"controlf"
          READ      CONTROLF,TWENTY1;*138,PTCNNHII
          IF        PTCNNHII=1
            OPEN     NHIMASA2,"nhimasaf"
            MOVE     PURNO,KEY8
            CALL     RDNHMAS2
            IF       OVRCD=0
              MOVE     NHMASURN,VARIABLE     * NHI Surname Name
            ENDIF
          ENDIF
          GOTO     XRCP5000
.
XRCP1290  MOVE      PSNAME,PACSNAME
          MOVE      PGNAME,PACGNAME
          MOVE      SP70,PACTITLE
          MOVE      ONE,PACFRMT            * format - Given name,Surname
          CALL      PACNAME
          MOVE      PACFNAME,VARIABLE      * Formatted Patient's Name
          GOTO      XRCP5000
.
XRCP1300  CALL      PADR0000                 * Patient full address
          GOTO      XRCP5000
.
XRCP1310  MATCH     SP70,PRFANAME
          IF        @EQUAL
            PACK      PRFANAME,PRNTNAME      * default to patient name
          ENDIF
          MOVE      PRFANAME,VARIABLE        * PRFA Name
          GOTO      XRCP5000
.
XRCP1320  MATCH     SP70,PRFAADD1
          IF        @EQUAL
            PACK      PRFAADD1,PRNTADD1
          ENDIF
          MOVE      PRFAADD1,VARIABLE
          GOTO      XRCP5000
.
XRCP1330  MATCH     SP70,PRFAADD2
          IF        @EQUAL
            PACK      PRFAADD2,PRNTADD2
          ENDIF
          MOVE      PRFAADD2,VARIABLE
          GOTO      XRCP5000
.
XRCP1340  MATCH     SP70,PRFAADD3
          IF        @EQUAL
            PACK      PRFAADD3,PRNTSUBR
          ENDIF
          MOVE      PRFAADD3,VARIABLE
          GOTO      XRCP5000
.
XRCP1350  MATCH     SP70,PRFAADD4
          IF        @EQUAL
            PACK      PRFAADD4,PRNTADD4
          ENDIF
          MOVE      PRFAADD4,VARIABLE
          GOTO      XRCP5000
.
XRCP1360  MATCH     SP70,PRFAPOST
          IF        @EQUAL
            PACK      PRFAPOST,PRNTPOST
          ENDIF
          MOVE      PRFAPOST,VARIABLE
          GOTO      XRCP5000
.
XRCP1370  PACK      VARIABLE,PMPXPFSN,SP70   * Preferred Surname
          GOTO      XRCP5000
.
XRCP1380  PACK      VARIABLE,PMPXPFGN,SP70   * Preferred Given Name
          GOTO      XRCP5000
.
XRCP1390  PACK      VARIABLE,PSNAME,SP70     * Pref Surname instead of Actual SN
          MATCH     "1",PTCNPFSN
          IF        @EQUAL
            MATCH     SP70,PMPXPFSN
            IF        !@EQUAL & !@EOS
              PACK      VARIABLE,PMPXPFSN,SP70,SP10
            ENDIF
          ENDIF
          GOTO      XRCP5000
.
XRCP1400  PACK      VARIABLE,PGNAME,SP70     * Pref GN instead of Actual GN
          MATCH     "1",PTCNPFGN
          IF        @EQUAL
            MATCH     SP70,PMPXPFGN
            IF        !@EQUAL & !@EOS
              PACK      VARIABLE,PMPXPFGN,SP70,SP10
            ENDIF
          ENDIF
          GOTO      XRCP5000
.
XRCP1410  PACK      VARIABLE,PTMASNAM,SP70   * Pref Surname instead of 40char SN
          MATCH     "1",PTCNPFSN
          IF        @EQUAL
            MATCH     SP70,PMPXPFSN
            IF        !@EQUAL & !@EOS
              PACK      VARIABLE,PMPXPFSN,SP70,SP10
            ENDIF
          ENDIF
          GOTO      XRCP5000
.
XRCP1420  PACK      VARIABLE,CAPPRVNO,SP70   * Hospital Provider Approval Number
          GOTO      XRCP5000
.
XRCP1430  PACK      VARIABLE,PTCNHABN,SP70   * Hospital ABN Number
          GOTO      XRCP5000
.
XRCP1440  PACK      VARIABLE,PTHSAPPR,SP70   * Hospital Approval Number MultiHos
          GOTO      XRCP5000
.
XRCP1450  PACK      VARIABLE,PTHSABNN,SP70   * Hospital ABN Number MultiHosp
          GOTO      XRCP5000
.
XRCP1460  MOVE      SP70,VARIABLE           * Patient Name
          MOVE      PTITL,PACTITLE          * Patient Title
          MOVE      PGNAME,PACGNAME         * Patient given name
          MOVE      PSNAME,PACSNAMX         * Patient surname
          MOVE      THREE,PACFRMT           * Title,Given name and Surname
          CALL      PACNAME
          PACK      KEY80,PACFNAMX,SP70
          MOVE      KEY80,VARIABLE          * Formatted patient Name
          GOTO      XRCP5000
.
.
. ------- Format the variable -------
.
XRCP5000  RESET     VARIABLE,IBDTPLEN
          LENSET    VARIABLE
          RESET     VARIABLE
          PRINT     *IBDTCOL,*+,VARIABLE;
          GOTO      XRCP0200                 * Read next record
.
. ------- Formatting for Form fields and right justified variables -------
.
XRCP6000  COMPARE   ZERO,PRNTSTRT          * Don't bump if printing full length
          GOTO      XRCP7000 IF EQUAL
.
          BUMP      VARIABLE,PRNTSTRT      * bump to starting position
.
. ------- Print the variable -------
.
XRCP7000  PRINT     *IBDTCOL,*+,VARIABLE;
          GOTO      XRCP0200                 * Read next record
.
.------ end of invoice reached ------
.
XRCP8000  MOVE      ZERO,EXIT
.
.------ print new-lines until the end of page is reached ------
.
XRCP9000  COMPARE   IBTHLENG,CURRROW         * test page length
          GOTO      XRCP9999 IF NOT LESS
.
          PRINT     *N;
          ADD       ONE,CURRROW
          MOVE      ONE,EXIT
          GOTO      XRCP9000
.
XRCP9999  MOVE      ZERO,CLNO                * Set column number
          RETURN                             * Finished
.------------------------------------------------------------------------------
. Patient Full address
.------------------------------------------------------------------------------
PADR0000  CLEAR     VARIABLE
          APPEND    PRNTSUBR,VARIABLE
          RESET     VARIABLE
          STRIP     VARIABLE
          ENDSET    VARIABLE
          APPEND    SP1,VARIABLE
          APPEND    PRNTADD4,VARIABLE
          RESET     VARIABLE
          STRIP     VARIABLE
          ENDSET    VARIABLE
          APPEND    SP1,VARIABLE
          APPEND    PRNTPOST,VARIABLE
          RESET     VARIABLE
          STRIP     VARIABLE
          ENDSET    VARIABLE
          APPEND    SP70,VARIABLE
          RESET     VARIABLE
PADR9999  RETURN
+
.------------------------------------------------------------------------------
. Position Receipt - Carried forward
.------------------------------------------------------------------------------
XCRF0000  COMPARE   CURRROW,TAILLENG         * Current row up to required row ?
          GOTO      XCRF9900 IF EQUAL        * Yes - print data
          GOTO      XCRF9999 IF LESS         * out of the loop
          IF        CURRROW=0
            IF        PTCNINVB=2
              PRINT     *93,"CARRIED FWD";
            ELSE
              PRINT     *69,"CARRIED FWD";
            ENDIF
          ELSE
            PRINT     *N;                      * Else print 1 extra line
          ENDIF
          ADD       ONE,CURRROW              * Increment counter
          GOTO      XCRF0000                 * Check counter again
.
XCRF9900  PRINT     " "
.
XCRF9999  RETURN
+
.***************************************************************************
.*  CSYS0000  :  Check the first record of receipt is for Emergency visit  *
.***************************************************************************
CSYS0000  MOVE      RECEIPTN,FORM12
          MOVE      FORM12,RECEIPTN
          PACK      KEY17,RECEIPTN,SP20
          CALL      RSRCDTE1                 * Go through all transactions
CSYS1000  CALL      RKRCDTE1
          BRANCH    OVRCD,CSYS9800
          MATCH     RCDTRECN,RECEIPTN        * Same receipt still ?
          GOTO      CSYS9800 IF NOT EQUAL    * No - pad out receipt
.
          MOVE      RCDTVIST,KEY8
          CALL      RDVISA1
          BRANCH    OVRCD,CSYS9800
          COMPARE   ONE,PVITYPE              * Emergency visit?
          GOTO      CSYS9800 IF NOT EQUAL
          MOVE      ZERO,EXIT
          GOTO      CSYS9999
.
CSYS9800  MOVE      ONE,EXIT
CSYS9999  RETURN
+
.***************************************************************************
.         Check Extra Contact Details for PRFA
.***************************************************************************
XCON0000  OPEN      PMSCEXA1,"pmscexaf"
          PACK      KEY14,RCDTURNO,SP70
          CALL      RSPMCEX1
XCON1000  CALL      RKPMCEX1
          BRANCH    OVRCD,XCON9999
.
          MATCH     PMCEURNO,RCDTURNO         * Same U/R
          GOTO      XCON9999 IF NOT EQUAL
.
          MOVE      "tc",KEY2
          PACK      KEY5,KEY2,PMCETYPE
          CALL      RDCODE1
          BRANCH    OVRCD,XCON1000
.
          MATCH     "6",TCINDC1
          GOTO      XCON1000 IF NOT EQUAL   * Not a PRFA
.
          MOVE      PMCETITL,PACTITLE          * format patient name
          MOVE      PMCESNAM,PACSNAMX
          MOVE      PMCEGNAM,PACGNAME
          MOVE      THREE,PACFRMT
          CALL      PACNAME
          PACK      PRFANAME,PACFNAMX,SP70
          PACK      PRFAADD1,PMCEADD1,SP70
          PACK      PRFAADD2,PMCEADD2,SP70
          PACK      PRFAADD3,PMCEADD3,SP70
          PACK      PRFAADD4,PMCEADD4,SP70
          PACK      PRFAPOST,PMCEPOST,SP70
XCON9999  RETURN
+
.***************************************************************************
.*  OTRF0000  :  Open all required visit based files                       *
.***************************************************************************
OTRF0000  IF        FILENUM = 20
            CLOSE     PATVISA1
            OPEN      PATVISA1,PATH
          ENDIF
          IF        FILENUM = 51
            CLOSE     PATINVR1
            OPEN      PATINVR1,PATH
          ENDIF
          IF        FILENUM = 46
            CLOSE     PATRE1A1
            OPEN      PATRE1A1,PATH
          ENDIF
.
OTRF9999  RETURN
+
.***************************************************************************
.  This routine formats a name into the requested format from NHI name.
.     Surname dim25 and first given name dim20
.---------------------------------------------------------------------------
.
PACNHIN   PACK      PACFNAME,SP30,SP30       * Initialise to blanks.
.
.         Title Given_names Surname
.
PACNH100  CLEAR     PACFNAME
          APPEND    PACTITLE,PACFNAME        * Append title
.
.         Find the end of the title
.
PACNH150  CMATCH    SP1,PACFNAME
          GOTO      PACNH200 IF NOT EQUAL
.
          BUMP      PACFNAME,-1              * Try the previous character
          GOTO      PACNH150 IF NOT EOS
.
.         No title. Go directly to adding the given name
.
          CLEAR     PACFNAME                 * Make sure the name is cleared
          GOTO      PACNH210
.
.         Found the end of the title. Add the given name
.
PACNH200  APPEND    SP1,PACFNAME             * Add a blank after the title
PACNH210  APPEND    NHMAGIV1,PACFNAME        * Append the given names
.
.         Find the end of the given names
.
PACNH250  CMATCH    SP1,PACFNAME
          GOTO      PACNH300 IF NOT EQUAL
.
          BUMP      PACFNAME,-1              * Try the previous character
          GOTO      PACNH250 IF NOT EOS
.
.         No title or given name. Go directly to adding the surname
.
          CLEAR     PACFNAME                 * Make sure the name is cleared
          GOTO      PACNH310
.
.         Found the end of the given names. Add the surname
.
PACNH300  APPEND    SP1,PACFNAME             * Add a blank after the title
PACNH310  APPEND    NHMASURN,PACFNAME        * Append the surname
          APPEND    SP30,PACFNAME            * Pad out with spaces
          APPEND    SP30,PACFNAME            * Pad out with spaces
          RESET     PACFNAME
          GOTO      PACNH999
.
PACNH9999 RETURN
+
          INC       PRINTHS1                * print for Holy Spirit
