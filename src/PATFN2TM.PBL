. *----------------------------------------------------------------------
. * Include Name  :   PATFN2TM.INC
. *
. * Function      :   CGI variables for PATFN2FD - patfhaf.dat
. *
. * V11.02.00  05/04/2022 J.Tan   TSK 0814566
. *                       Created TM
. *----------------------------------------------------------------------
.
.------------------------------------------------------------------------
.   Clear Parameters 
.------------------------------------------------------------------------
.
CPTFNX00  MOVE      SP70,FNEXT001
          MOVE      ZERO,FNEXT002
          MOVE      ZERO,FNEXT003
          MOVE      ZERO,FNEXT004
          MOVE      SP70,FNEXT005
          MOVE      SP70,FNEXT006
          MOVE      SP70,FNEXT007
CPTFNX99  RETURN
.
.------------------------------------------------------------
.   Set Parameters 
.------------------------------------------------------------
STHFNX00  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STHFNX01,STHFNX02,STHFNX03,STHFNX04,STHFNX05:
                       STHFNX06,STHFNX07
.
          MOVE      ONE,EXIT
          GOTO      STHFNX99
.
STHFNX01  SQUEEZE   QRYDATA
          MOVE      QRYDATA,FNEXT001
          PACK      FNEXT001,FNEXT001,SP70
          GOTO      STHFNX99
.
STHFNX02  MOVE      QRYDATA,FNEXT002
          GOTO      STHFNX99
.
STHFNX03  MOVE      QRYDATA,FNEXT003
          GOTO      STHFNX99
.
STHFNX04  SQUEEZE   QRYDATA
          MOVE      ZERO,FORM2
          MOVE      QRYDATA,FORM2
          MOVE      FORM2,FNEXT004
          GOTO      STHFNX99
.
STHFNX05  MOVE      QRYDATA,FNEXT005
          GOTO      STHFNX99
.
STHFNX06  MOVE      QRYDATA,FNEXT006
          GOTO      STHFNX99
.
STHFNX07  PACK      QRYDATA,QRYDATA,SP70
          MATCH     SP70,QRYDATA
          GOTO      STHFNX99 IF EQUAL
.
          UNPACK    QRYDATA,CDAY,KEY1,MTHNAM3,KEY1,CCENT,CYEAR
          CALL      SETMTH00                *Set CMON from MTHNAM3
          PACK      FNEXT007,CCENT,CYEAR,CMON,CDAY
          REP       " 0",FNEXT007
          GOTO      STHFNX99
.
STHFNX99  RETURN
.
.------------------------------------------------------------
. Display fields
.------------------------------------------------------------
DFNX0000  BRANCH    FLDITMNO,DFNX0100,DFNX0200,DFNX0300,DFNX0400,DFNX0500:
                             DFNX0600,DFNX0700
          GOTO      DFNX9999
.
DFNX0100  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,DFNX0110
.
          WRITE     HTMLFILE;PTFNHFND;
          GOTO      DFNX9999
.
DFNX0110  PACK      HFNAME,SP70
          PACK      KEY14,PTFNHFND,ZERO8,SP70
          CALL      RDFUND1
.
          WRITE     HTMLFILE;HFNAME;
          GOTO      DFNX9999
.
DFNX0200  WRITE     HTMLFILE;PTFNBMVH;
          GOTO      DFNX9999
.
DFNX0300  WRITE     HTMLFILE;PTFNMVRT;
          GOTO      DFNX9999
.
DFNX0400  WRITE     HTMLFILE;PTFNHRMV;
          GOTO      DFNX9999
.
DFNX0500  UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,DFNX0510
          WRITE     HTMLFILE;PTFNMVBC;
          GOTO      DFNX9999
.
DFNX0510  PACK      MDESC,SP70
          PACK      KEY29,PTFNMVBC,Z70
          CALL      RDSMCHG4
          CALL      RDPMCHG4
          BRANCH    OVRCD,DFNX9999
          MATCH     PTFNMVBC,MCHARGE
          GOTO      DFNX9999 IF NOT EQUAL
.
          WRITE     HTMLFILE;MDESC;
          GOTO      DFNX9999
.
DFNX0600  COMPARE   ONE,IBCNMHOS
          GOTO      DFNX9999 IF NOT EQUAL
.
          UNPACK    DIM127,D1,KEY1,DIM127
          MOVE      ZERO,F1
          MOVE      KEY1,F1
          BRANCH    F1,DFNX0610,DFNX0620
.
          WRITE     HTMLFILE;PTFNHOSP;
          GOTO      DFNX9999
.
DFNX0610  MATCH     SP70,PTFNHOSP
          IF        @EQUAL
            WRITE     HTMLFILE;"All";
          ELSE
            PACK      KEY3,PTFNHOSP,SP70
            CALL      RDPTHSP1
            BRANCH    OVRCD,DFNX9999
            WRITE     HTMLFILE;PTHSNAME;           * hospital name
          ENDIF
          GOTO      DFNX9999
.
DFNX0620  MOVE      ONE,F1
          CALL      HSPSEL00           * Hospital Selection List
          GOTO      DFNX9999
.
DFNX0700  MATCH     PTFNEDAT,SP70
          GOTO      DFNX9999 IF EQUAL
.
          UNPACK    PTFNEDAT,CCENT,CYEAR,CMON,CDAY
          MOVE      CMON,F2
          LOAD      MTHNAM3,F2,JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
          WRITE     HTMLFILE;CDAY,SP1,MTHNAM3,SP1,CCENT,CYEAR;
          GOTO      DFNX9999
.
DFNX9999  RETURN
+
