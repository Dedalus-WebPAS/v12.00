.-----------------------------------------------------------------------
.
.     Include Name : PMSVSCTM.PBL
.
.     Function     : File Definition include for Patient System
.                    Clinical Coding Value Set Codes
.
.     Modifications:
.        V11.01.03  20.09.2021  DA Sarkies     TSK 0905305
.                   Changed value that is passed back on the search is   
.                   pmvcfnam to desctext
.        V11.01.02  09.09.2021  DA Sarkies     TSK 0905305
.                   Changed description to ECDG Subcategory Descripition 
.                   Included second description feild loaded from DB and sent
.                   to front end.
.        V11.01.01  08.09.2021  DA Sarkies     TSK 0905305
.                   Added check in CODHDK00 to check for ECC and generate
.                   Added check in CODRWK00 to display contents of table
.        V11.00.02  21/12/2020  Tracey Nguyen  TSK 0888079
.                   Added table heading for when PTVSC001 is not 'BRA'
.                   - CODHDK00 
.        V11.00.01  11/11/2020  Tracey Nguyen  TSK 0888079
.                   Added new tag for Search by Keyword 
.                   - VSCD1800,CODTBK00,CODHDK00,CODRWK00
.                   - VSCD1900,CODPVK00
.                   - VSCD2000,CODNXK00
.        V10.11.00  14/11/2017  Don Nguyen     TSK 0816814
.                   Created Include
.
.------------------------------------------------------------
.   Clear Parameters for PMSVSTTD
.------------------------------------------------------------
CPVSC000  MOVE      SP70,PTVSC001
          MOVE      SP70,PTVSC002
          MOVE      SP70,PTVSC003
          MOVE      SP70,PTVSC004
          MOVE      SP70,PTVSC005
          MOVE      SP70,PTVSC006
          MOVE      SP70,PTVSC007
.
CPVSC999  RETURN
.------------------------------------------------------------
.  Initialize's file data (Value Set ID Maintenace)
.-----------------------------------------------------------
CLRCOD00  MOVE      SP70,PMVCVSID
          MOVE      SP70,PMVCCODE
          MOVE      SP70,PMVCPTRM
          MOVE      SP70,PMVCFNAM
          MOVE      SP70,PMVCACTV
.
CLRCOD99  RETURN
.-----------------------------------------------------------------
.  Moves cgi variables into file variables (Value Set ID maintenance)
.-----------------------------------------------------------------
VSCSAV00  MOVE      PTVSC001,PMVCVSID
          MOVE      PTVSC002,PMVCCODE
          MOVE      PTVSC003,PMVCPTRM
          MOVE      PTVSC004,PMVCFNAM
          MOVE      PTVSC005,PMVCACTV
.
VSCSAV99  RETURN
.-------------------------------------------------------------------------------
.  Set Parameters for Value Set ID Codes Maintenance
.-------------------------------------------------------------------------------
STVSC000  MOVE      ZERO,EXIT
          UNPACK    QRYNAME,KEY5,KEY3
          MOVE      KEY3,F3
          BRANCH    F3,STVSC001,STVSC002,STVSC003,STVSC004,STVSC005,STVSC006:
                       STVSC007
.
          GOTO      STVSC999
.
STVSC001  MOVE      QRYDATA,PTVSC001
          PACK      PTVSC001,PTVSC001,SP70
          GOTO      STVSC999
.
STVSC002  MOVE      QRYDATA,PTVSC002
          PACK      PTVSC002,PTVSC002,SP70
          GOTO      STVSC999
.
STVSC003  MOVE      QRYDATA,PTVSC003
          GOTO      STVSC999
.
STVSC004  MOVE      QRYDATA,PTVSC004
          GOTO      STVSC999
.
STVSC005  MOVE      QRYDATA,PTVSC005
          GOTO      STVSC999
.
STVSC006  MOVE      QRYDATA,PTVSC006
          GOTO      STVSC999
.
STVSC007  MOVE      QRYDATA,PTVSC007
          GOTO      STVSC999
.
STVSC999  RETURN
+
.------------------------------------------------------------
.  Checks mandatory fields (Value Set ID Maintenance)
.------------------------------------------------------------
CHKVSC00  RESET     PTVSC001
          MATCH     SP70,PTVSC001
          GOTO      CHKVSC90 IF EQUAL
.
          RESET     PTVSC002
          MATCH     SP70,PTVSC002
          GOTO      CHKVSC91 IF EQUAL
.
.         RESET     PTVSC003
.         MATCH     SP70,PTVSC003
.         GOTO      CHKVSC92 IF EQUAL
.
.         RESET     PTVSC004
.         MATCH     SP70,PTVSC004
.         GOTO      CHKVSC93 IF EQUAL
.
          GOTO      CHKVSC98
.
CHKVSC90  MOVE      "Value Set ID Code is mandatory",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKVSC99
.
CHKVSC91  MOVE      "Value Set Name is mandatory",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKVSC99
CHKVSC92  MOVE      "Preferred Term is mandatory",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKVSC99
.
CHKVSC93  MOVE      "Fully Specified Name is mandatory",WEBTITLE
          CALL      WEBERR00
          MOVE      ONE,EXIT
          GOTO      CHKVSC99
.
CHKVSC98  MOVE      ZERO,EXIT
CHKVSC99  RETURN
+
.-----------------------------------------------------------
.  Value Set ID Codes Maintenance
.------------------------------------------------------------
VSCD0000  BRANCH    FLDITMNO,VSCD0100,VSCD0200,VSCD0300,VSCD0400,VSCD0500:
                             VSCD0600,VSCD0700,VSCD0800,VSCD0900,VSCD1000:
                             VSCD1100,VSCD1200,VSCD1300,VSCD1400,VSCD1500:
                             VSCD1600,VSCD1700,VSCD1800,VSCD1900,VSCD2000
          GOTO      VSCD9999
.
VSCD0100  WRITE     HTMLFILE;PMVCVSID;   * Value Set ID
          GOTO      VSCD9999
.
VSCD0200  WRITE     HTMLFILE;PMVCCODE;   * Value Set ID Code
          GOTO      VSCD9999
.
VSCD0300  WRITE     HTMLFILE;PMVCPTRM;   * Preferred Term
          GOTO      VSCD9999
.
VSCD0400  WRITE     HTMLFILE;PMVCFNAM;   * Fully Specified Name
          GOTO      VSCD9999
.
VSCD0500  WRITE     HTMLFILE;PMVCACTV;   * Active Flag (0 or Blank = No
          GOTO      VSCD9999             *              1 = Yes)
.
VSCD0600  MOVE      FILTVSID,CODSYSID
          CALL      VSCSEL00
          GOTO      VSCD9999
.
VSCD0700  CALL      VSCTAB00             * Table of Value Set ID/Codes
          GOTO      VSCD9999
.
VSCD0800  WRITE     HTMLFILE;FILTVACT;   * Active Filter
          GOTO      VSCD9999
.
VSCD0900  WRITE     HTMLFILE;FILTVSID;   * Value Set ID Filter
          GOTO      VSCD9999
.
VSCD1000  WRITE     HTMLFILE;FILTVSCD;   * Value Set Code Filter
          GOTO      VSCD9999
.
VSCD1100  WRITE     HTMLFILE;FILTVSPT;   * Value Set Preferred Term Filter
          GOTO      VSCD9999
.
VSCD1200  PACK      KEY3,PMVCVSID,SP70
          CALL      RDPMVST1
          IF        OVRCD=0
            WRITE     HTMLFILE;PMVTVSNM; * Value Set ID Name
          ENDIF     
          GOTO      VSCD9999
.
VSCD1300  CALL      CODTAB00             * Table of Codes for a Value Set ID
          GOTO      VSCD9999
.
VSCD1400  CALL      CODPRV00             * Link to Prev set of Codes
          GOTO      VSCD9999
.
VSCD1500  CALL      CODNXT00             * Link to Next set of Codes
          GOTO      VSCD9999
.
VSCD1600  WRITE     HTMLFILE;NORECORD;   * No. of records to display
          GOTO      VSCD9999
.
VSCD1700  WRITE     HTMLFILE;PTVSC001;   * Coding System ID
          GOTO      VSCD9999
.
VSCD1800  CALL      CODTBK00             * Value Set Codes keyword search
          GOTO      VSCD9999
.
VSCD1900  CALL      CODPVK00             * Link to Prev keyword search
          GOTO      VSCD9999
.
VSCD2000  CALL      CODNXK00             * Link to Next set keyword search
          GOTO      VSCD9999

VSCD9999  RETURN
+
.------------------------------------------------------------
.  Value Set ID Selection List
.------------------------------------------------------------
VSCSEL00  PACK      KEY3,SP70
          CALL      RSPMVST1
VSCSEL10  CALL      RKPMVST1
          BRANCH    OVRCD,VSCSEL99
.
          MATCH     SP70,PMVTVSID
          GOTO      VSCSEL10 IF EQUAL
.
          PACK      KEY5,QUOTE,PMVTVSID,QUOTE
.
          MATCH     CODSYSID,PMVTVSID
          IF        @EQUAL
            WRITE     HTMLFILE;"<option value=",KEY5," selected>":
                               PMVTVSNM,"</option>"
          ELSE
            WRITE     HTMLFILE;"<option value=",KEY5,">",PMVTVSNM,"</option>"
          ENDIF
          GOTO      VSCSEL10
.
VSCSEL99  RETURN
+
.-----------------------------------------------------------
.  Value Set ID Codes Table (Sort List)
.-----------------------------------------------------------
VSCTAB00  UNPACK    DIM127,D1,LINKPRG,DIM127
          UNPACK    DIM127,D1,LINKREP,DIM127
          UNPACK    DIM127,D1,LINKTEM,DIM127
.
          MATCH     SP70,FILTVSID           * Value Set ID specified?
          GOTO      VSCTAB99 IF EQUAL       * bail if blank
.
          MOVE      FILTVSID,PTVSC001
          PACK      PTVSC001,PTVSC001,SP70
.
          MOVE      FILTVSCD,PTVSC002
          PACK      PTVSC002,PTVSC002,SP70
.         
          MATCH     SP70,FILTVSPT           * Key Words?
          GOTO      VSCTAB10 IF EOS
          GOTO      VSCTAB20 IF NOT EQUAL   * Key Words Search
.
VSCTAB10  STRIP     FILTVSCD                * Strip trailing space from Code
.
.         List by Value Set ID & Code
.
          PACK      KEY33,PTVSC001,PTVSC002,SP70
          CALL      RSPMVSC1
VSCTAB11  CALL      RKPMVSC1
          BRANCH    OVRCD,VSCTAB99
.
          MATCH     PMVCVSID,PTVSC001
          GOTO      VSCTAB99 IF NOT EQUAL
.
          MATCH     SP70,FILTVSCD
          IF        !@EQUAL
            MATCH     PMVCCODE,FILTVSCD
            GOTO      VSCTAB99 IF NOT EQUAL
          ENDIF
.
          MATCH     SP70,FILTVACT           * Filter as per Active status
          IF        !@EQUAL
            MATCH     FILTVACT,PMVCACTV
            GOTO      VSCTAB11 IF NOT EQUAL
          ENDIF
.
          CALL      VSCROW00      * output sort list row
          GOTO      VSCTAB11
.
.         List by Keyword
.
VSCTAB20  MOVE      FILTVSPT,KEYWORDS
          CALL      GETWRD00                * Break up KEYWORDS for search
.
          STRIP     KEYWRD01
          PACK      KEY83,PTVSC001,KEYWRD01,SP70
          CALL      RSPMUKI2
VSCTAB21  CALL      RKPMUKI2
          BRANCH    OVRCD,VSCTAB90
.
          CALL      CHKUKI00                * Check key word index file
          BRANCH    OVRCD,VSCTAB21,VSCTAB90
.
          PACK      KEY33,PMUKVSID,PMUKCODE,SP70
          CALL      RDPMVSC1
          BRANCH    OVRCD,VSCTAB21
.
          MATCH     SP70,FILTVACT           * Filter as per Active status
          IF        !@EQUAL
            MATCH     FILTVACT,PMVCACTV
            GOTO      VSCTAB21 IF NOT EQUAL
          ENDIF
.
          CALL      VSCROW00      * output sort list row
          GOTO      VSCTAB21
.
VSCTAB90
VSCTAB99  RETURN
+
.-----------------------------------------------------------
. Output Sort List Row
.-----------------------------------------------------------
VSCROW00  PACK      KEY3,PMVCVSID,SP70
          CALL      RDPMVST1
          BRANCH    OVRCD,VSCROW99
.
          CLEAR     HTMLLINK
          APPEND    "javascript:ShowDetail49('",HTMLLINK
          APPEND    PMVCVSID,HTMLLINK
          APPEND    "','",HTMLLINK
          APPEND    PMVCCODE,HTMLLINK
          APPEND    "');",HTMLLINK
          RESET     HTMLLINK
.
          MOVE      "Yes",KEY3
          MATCH     "1",PMVCACTV
          IF        !@EQUAL
            MOVE      "No ",KEY3
          ENDIF
.
          WRITE     HTMLFILE;"t.addRow(#"",*+,HTMLLINK,*-,"#",":     * 0
                             "#"",*+,PMVTVSNM,*-,"#",":              * 1
                             "#"",*+,PMVCCODE,*-,"#",":              * 2
                             "#"",*+,PMVCPTRM,*-,"#",":              * 3
                             "#"",*+,PMVCFNAM,*-,"#",":              * 4
                             "#"",KEY3,"#""                          * 5
          WRITE     HTMLFILE;");"
.
VSCROW99  RETURN
+
.-----------------------------------------------------------
.  Value Set ID Codes Table
.-----------------------------------------------------------
CODTAB00  CALL      CODHED00           * Output Table Heading
.
          IF        NORECORD=0
            MOVE      "10",NORECORD    * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          PACK      PTVSC002,PTVSC002,SP70
.
          PACK      KEY33,PTVSC001,PTVSC002,SP70
          CALL      RSPMVSC1
          CALL      RPPMVSC1
CODTAB10  CALL      RKPMVSC1
          BRANCH    OVRCD,CODTAB99          * end of file
.
          MATCH     PMVCVSID,PTVSC001       * check the Value Set ID
          GOTO      CODTAB99 IF NOT EQUAL
.
          MATCH     "1",PMVCACTV
          GOTO      CODTAB10 IF NOT EQUAL
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      CODTAB10
          ENDIF
.
          CALL      CODROW00                * Display Row
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      CODTAB10 IF NOT EQUAL
.
CODTAB99  RETURN
+
.------------------------------------------------------------
. Value Set ID Codes Table Heading
.------------------------------------------------------------
CODHED00  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=15%>":
                               "Code</td>":
                               "<td class=HeadingCell width=35%>":
                               "Description</td>":
                             "</tr>"
.
CODHED99  RETURN
+
.------------------------------------------------------------
. Value Set ID Codes Table Row
.------------------------------------------------------------
CODROW00  WRITE     HTMLFILE;"<tr><td nowrap><a href='javascript:UpdateParent(":
                             "#"",PMVCCODE,"#",#"",PMVCPTRM,"#")'>":
                             "<img src='../images/MaintenanceIcon.gif'":
                             " hspace=4 border=0 align=absmiddle></a>":
                             PMVCCODE,"</td><td>&nbsp;",PMVCPTRM,"</td>":
                             "</tr>"
.
          GOTO      CODROW99
.
CODROW99  RETURN
+
.----------------------------------------------------------------------------
. Value Set Codes Search - by Keyword
.----------------------------------------------------------------------------
CODTBK00  CALL      CODHDK00         * Output table heading desc format
.
          IF        NORECORD=0
            MOVE      "10",NORECORD    * Set Default No Records to Display
          ENDIF
          MOVE      ZERO,RECNUMB
          MOVE      ZERO,DISNUMB
.
          MOVE      FILTVSPT,KEYWORDS
          CALL      GETWRD00                * Break up KEYWORDS for search
.
          STRIP     KEYWRD01
          PACK      KEY83,PTVSC001,KEYWRD01,SP70
          CALL      RSPMUKI2
          CALL      RPPMUKI2
CODTBK10  CALL      RKPMUKI2
          BRANCH    OVRCD,CODTBK99
.
          CALL      CHKUKI00                * Check key word index file
          BRANCH    OVRCD,CODTBK10,CODTBK99
.
          PACK      KEY33,PMUKVSID,PMUKCODE,SP70
          CALL      RDPMVSC1
          BRANCH    OVRCD,CODTBK10
.
          MATCH     "1",PMVCACTV
          GOTO      CODTBK10 IF NOT EQUAL
.
          ADD       ONE,RECNUMB
          IF        STARTNUM>=RECNUMB
            GOTO      CODTBK10
          ENDIF
.
          CALL      CODRWK00              * Display Row
.
          ADD       ONE,DISNUMB
          COMPARE   NORECORD,DISNUMB
          GOTO      CODTBK10 IF NOT EQUAL
.
CODTBK99  RETURN

.------------------------------------------------------------
. Value Set ID Codes Table Heading - desc format
.------------------------------------------------------------
CODHDK00  MATCH     "BRA",PTVSC001         * Billing Reason for Admission
          GOTO      CODHDK10 IF EQUAL
.
          MATCH     "ECC",PTVSC001         * ECDG Subcategory (Value Set ID
          GOTO      CODHDK20 IF EQUAL      * Code for Value Set ECC
.
.-------- Value Code Set Search (for keywords search) table heading
          WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=50%>":
                               "Description</td>":
                               "<td class=HeadingCell width=50%>":
                               "Fully Specified Name</td>":
                             "</tr>"
          GOTO      CODHDK99
.
.-------- Billing Reason for Admission table heading
CODHDK10  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=50%>":
                               "Description</td>":
                               "<td class=HeadingCell width=50%>":
                               "Intended Stay</td>":
                             "</tr>"
          GOTO      CODHDK99
.
.-------- ECDG Reason for Admission table heading
CODHDK20  WRITE     HTMLFILE;"<tr>":
                               "<td class=HeadingCell width=20%>":
                               "Code</td>":
                               "<td class=HeadingCell width=80%>":
                               "Description</td>":
                             "</tr>"
          GOTO      CODHDK99
.
CODHDK99  RETURN
+
.------------------------------------------------------------
. Value Set ID Codes Table Row - desc format
.------------------------------------------------------------
CODRWK00  MATCH     "ECC",PTVSC001         * ECDG Subcategory (Value Set ID
          GOTO      CODRWK10 IF EQUAL      * Code for Value Set ECC
.
          WRITE     HTMLFILE;"<tr><td nowrap><a href='javascript:UpdateParent(":
                             "#"",PMVCCODE,"#",#"",PMVCPTRM,"#")'>":
                             "<img src='../images/MaintenanceIcon.gif'":
                             " class=Icon></a> &nbsp;":
                             PMVCPTRM,"</td><td>&nbsp;",PMVCFNAM,"</td>":
                             "</tr>"
          GOTO      CODRWK99
.
CODRWK10  WRITE     HTMLFILE;"<tr><td nowrap><a href='javascript:UpdateParent(":
                             "#"",PMVCCODE,"#",#"",PMVCFNAM,"#"":
                             ",#"",PMVCFNAM,"#")'>":
                             "<img src='../images/MaintenanceIcon.gif'":
                             " class=Icon></a> &nbsp;":
                             PMVCCODE,"</td><td>&nbsp;",PMVCPTRM,"</td>":
                             "</tr>"
          GOTO      CODRWK99
.
CODRWK99  RETURN
+
.------------------------------------------------------------
. Link to Previous set of Codes
.------------------------------------------------------------
CODPRV00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          REPLACE   " +",PTVSC001
          REPLACE   " +",PTVSC002
.
          WRITE     HTMLFILE;"patweb87.pbl":
                             "?reportno=",F2:
                             "&template=",FLDPARA:
                             "&startnum=",KEY3:
                             "&norecord=",KEY2:
                             "&ptvsc001=",PTVSC001:
                             "&ptvsc002=",PTVSC002;
.
          REPLACE   "+ ",PTVSC001
          REPLACE   "+ ",PTVSC002
.
CODPRV99  RETURN
+
.------------------------------------------------------------
. Link to Next set of Codes
.------------------------------------------------------------
CODNXT00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          REPLACE   " +",PTVSC001
          REPLACE   " +",PTVSC002
.
          WRITE     HTMLFILE;"patweb87.pbl":
                             "?reportno=",F2:
                             "&template=",FLDPARA:
                             "&startnum=",KEY3:
                             "&norecord=",KEY2:
                             "&ptvsc001=",PTVSC001:
                             "&ptvsc002=",PTVSC002;
.
          REPLACE   "+ ",PTVSC001
          REPLACE   "+ ",PTVSC002
.
CODNXT99  RETURN
+
.------------------------------------------------------------
. Link to Previous set of keyword search
.------------------------------------------------------------
CODPVK00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM-NORECORD,F3
          IF        F3<0
            MOVE      ZERO,F3
          ENDIF
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          REPLACE   " +",PTVSC001
          REPLACE   " +",FILTVSPT
.
          WRITE     HTMLFILE;"patweb87.pbl":
                             "?reportno=",F2:
                             "&template=",FLDPARA:
                             "&startnum=",KEY3:
                             "&norecord=",KEY2:
                             "&ptvsc001=",PTVSC001:
                             "&filtvspt=",FILTVSPT;
.
          REPLACE   "+ ",PTVSC001
          REPLACE   "+ ",FILTVSPT
.
CODPVK99  RETURN
+
.------------------------------------------------------------
. Link to Next set of keyword search
.------------------------------------------------------------
CODNXK00  MOVE      NORECORD,KEY2
          REP       " +",KEY2
          ASSIGN    STARTNUM+NORECORD,F3
          MOVE      F3,KEY3
          REP       " +",KEY3
          UNPACK    DIM127,D1,FLDPARA,DIM127
          MOVE      REPORTNO,F2
.
          REPLACE   " +",PTVSC001
          REPLACE   " +",FILTVSPT
.
          WRITE     HTMLFILE;"patweb87.pbl":
                             "?reportno=",F2:
                             "&template=",FLDPARA:
                             "&startnum=",KEY3:
                             "&norecord=",KEY2:
                             "&ptvsc001=",PTVSC001:
                             "&filtvspt=",FILTVSPT;
.
          REPLACE   "+ ",PTVSC001
          REPLACE   "+ ",FILTVSPT
.
CODNXK99  RETURN
.------------------------------------------------------------
. Check Value Set Key Word record
.------------------------------------------------------------
CHKUKI00  MATCH     KEYWRD01,PMUKKKWD
          GOTO      CHKUKI92 IF NOT EQUAL
          IF        KEYWRDNO>1
            PACK      SAVKEY83,PTVSC001,PMUKKKWD,PMUKCODE,SP70
            CALL      CHKUKW00
            BRANCH    EXIT,CHKUKI91
            MOVE      SAVKEY83,KEY83
            CALL      RDPMUKI2
          ENDIF
.
          GOTO      CHKUKI90
.
CHKUKI90  MOVE      ZERO,OVRCD
          GOTO      CHKUKI99
.
CHKUKI91  MOVE      ONE,OVRCD
          GOTO      CHKUKI99
.
CHKUKI92  MOVE      TWO,OVRCD
CHKUKI99  RETURN
+
.------------------------------------------------------------
. Check for matching Value Set Key Word
.------------------------------------------------------------
CHKUKW00  MOVE      ONE,F2
          MOVE      ZERO,EXIT
          PACK      SAVKEY30,PMUKCODE,SP70
.
CHKUKW10  COMPARE   KEYWRDNO,F2           * Loop through each Key Word in Search
          GOTO      CHKUKW99 IF EQUAL     * Exit OK
.
          ADD       ONE,F2
          LOAD      KEYWRDXX,F2,KEYWRD01,KEYWRD02,KEYWRD03,KEYWRD04,KEYWRD05:
                                KEYWRD06,KEYWRD07,KEYWRD08,KEYWRD09,KEYWRD10
.
          PACK      KEY83,PTVSC001,PMUKCODE,KEYWRDXX,SP70
          CALL      RDPMUKI1              * Check for Exact Match
          COMPARE   ZERO,OVRCD            * Ok so Continue Loop
          GOTO      CHKUKW10 IF EQUAL
.
          CALL      RKPMUKI1              * Not Exact so Read Next Record
          BRANCH    OVRCD,CHKUKW90        * No Match So Exit No Match
.
          MATCH     PTVSC001,PMUKVSID
          GOTO      CHKUKW90 IF NOT EQUAL
.
          PACK      KEY30,PMUKCODE,SP70
          MATCH     SAVKEY30,KEY30        * Match Key Fields
          GOTO      CHKUKW90 IF NOT EQUAL * No Match So Exit
.
          STRIP     KEYWRDXX
          MATCH     PMUKKKWD,KEYWRDXX     * Check Key Word
          GOTO      CHKUKW90 IF NOT EQUAL * No Match So Exit
.
          GOTO      CHKUKW10              * Check Next Search Word
.
CHKUKW90  MOVE      ONE,EXIT
CHKUKW99  RETURN
+
