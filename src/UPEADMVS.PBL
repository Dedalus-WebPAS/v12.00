.------------------------------------------------------------------------------
. Update eAdmission Visit Number
. 
. Modifications
. V12.00.01 19.05.2025 Ebon Clements  TSK 0960463
.           Fixed population of last updated date and user
. V10.06.01 02.04/2015 B.G.Ackland    CAR 312482
.           Move CSC Portal Document to Patient Correspondence
. V10.05.01  14/10/2014  Peter Vela     CAR 301518
.            Added Clinical Documents audit functionality OBSAUDFD
. V10.04.01  06/02/2014  Patrick Adair  CAR 293445
.            amended to allow addition of documents to processed patient
.                                        Car 293780
.            amended to set clinical documents icon flag
. V10.03.01  30/07/2013  Patrick Adair  CAR 289389
.            Updated to cater for new documents workflow requirements
. V10.03.00  28/05/2013  Patrick Adair  CAR 285158
.            Update eAdmission file Visit number for PreAdmission and Bookings
.------------------------------------------------------------------------------
          DEFPROC   UPEADMVS
.
          INC       PATMA1FD/INC
          INC       PMSEHBFD/INC
          INC       OBSAUDFD/INC
          INC       OBSECOFD/INC
          INC       OBSPCOFD/INC
          INC       OBSPCTFD/INC
.
CMDLINE   DIM       200
DISPEAID  DIM       20
DISPECCD  DIM       8
DISPPCCD  DIM       8
FROMFILE  DIM       100
STORURNO  DIM       8
STORVISN  DIM       8
TOFILENM  DIM       100
CORSFILE  DIM       20
CMDMOVE   INIT      "mv "
.
DASH      INIT      "-"
.
UPEADM10  OPEN      PATMA1A1,"patma1af"
          OPEN      PATMX1A1,"patmx1af"
          OPEN      OBSAUDA1,"obsaudaf"
          OPEN      OBSPCOA1,"obspcoaf"
          OPEN      OBSPCTA1,"obspctaf"
.
          BRANCH    F1,UPEADM20
.
. Update eAdmission Interface Tables and Load Documents for Visit
.
          OPEN      PMSEHBA1,"pmsehbaf"
          OPEN      OBSECOA1,"obsecoaf"
          PACK      KEY20,ADMISNID,SP20
          CALL      RDPMEHB1
          BRANCH    OVRCD,UPEADM90
.
          MOVE      KEY1,PMHBSTAT
          MOVE      KEY8,PMHBVISN
          CALL      UPPMEHB1
          CALL      MVHS0000
          GOTO      UPEADM90
. 
. Add Correspondacne File from CSC eAdmissions
.
UPEADM20  OPEN      CONTROLF,"controlf"
          MOVE      KEY8,PMHBVISN
          CALL      ADDCOR00   
          GOTO      UPEADM95
.
UPEADM90  CLOSE     PMSEHBA1
          CLOSE     OBSECOA1
.
UPEADM95  CLOSE     OBSPCOA1
          CLOSE     OBSPCTA1
          GOTO      UPEADM99
+
.------------------------------------------------------------------------------
. Rename eAdmission Documents from ADMISNID to URNO, copy details to OBSPCOAF
. and update URNO on OBSECOAF
.------------------------------------------------------------------------------
MVHS0000  PACK      KEY24,PMHBEAID,SP70
          CALL      RSOBECO1
MVHS1000  CALL      RKOBECO1
          BRANCH    OVRCD,MVHS9999
.
          MATCH     PMHBEAID,OBECEAID
          GOTO      MVHS9999 IF NOT EQUAL
.
          PACK      KEY20,PMHBURNO,PMHBVISN,Z70
          CALL      RSOBPC1
          CALL      RPOBPC1
          BRANCH    OVRCD,MVHS2000
.
          MATCH     PMHBURNO,OBPCURNO
          IF        !@EQUAL
            GOTO      MVHS2000
          ENDIF
.
          MATCH     PMHBVISN,OBPCCVIS
          IF        @EQUAL
            MOVE      OBPCCPID,FORM4
            ADD       ONE,FORM4
            MOVE      FORM4,OBPCCPID
            GOTO      MVHS2100
          ENDIF
.
MVHS2000  MOVE      "   1",OBPCCPID
.
MVHS2100  CALL      MVDC0000
          MATCH     "0",TASKID
          GOTO      MVHS9000 IF NOT EQUAL
.
          CALL      IBACLOCK
          PACK      DIM8,CCC,CYY,CMM,CDD
          REP       " 0",DIM8
          PACK      TODAY,DIM8,SP70
          CLOCK     TIME,CTIMEIS
.                                           * Write PCO record
          PACK      OBPCURNO,PMHBURNO
          PACK      OBPCCVIS,PMHBVISN
          PACK      OBPCINDT,OBECINDT
          PACK      OBPCCTYP,OBECCTYP
          PACK      OBPCINTM,OBECINTM
          PACK      OBPCFEXT,OBECFEXT
          PACK      OBPCDTLU,TODAY
          PACK      OBPCTMLU,CTIMEIS
          MOVE      WBSEUID,OBPCLUID        * Last Web User ID
          PACK      OBPCINUS,SP70
          PACK      OBPCMDEL,SP70
          PACK      OBPCSLID,OBECSLID
          PACK      OBPCSLEV,SP70
          PACK      OBPCCOM1,SP70
          PACK      OBPCCOM2,SP70
          PACK      OBPCCFRM,SP70
          PACK      OBPCCTOO,SP70
          PACK      OBPCUDF1,SP70
          PACK      OBPCUDF2,SP70
          PACK      OBPCUYN1,SP70
          PACK      OBPCUYN2,SP70
          PACK      OBPCUDD1,SP70
          PACK      OBPCUDD2,SP70
          PACK      OBPCACAT,SP70
          PACK      OBPCACOD,SP70
          PACK      OBPCEAID,PMHBEAID
.
          PACK      KEY20,OBPCURNO,OBPCCVIS,OBPCCPID
          CALL      WROBPC1
.
          MOVE      ANSA,OBAUTYPE           * Add
          CALL      OBSAUD00                * Clinical Documents Audit
.
          PACK      KEY24,PMHBEAID,OBECCPID,SP70
          MOVE      PMHBURNO,OBECURNO
          CALL      UPOBECO1
.
          PACK      KEY8,PMHBURNO           * validate UR
          CALL      RDMAST1
          BRANCH    OVRCD,MVHS1000
.
          MATCH     "1",PTMXSIN4
          IF        !@EQUAL
            PACK      KEY8,PMHBURNO         * validate UR
            CALL      RLPTMAS1
            BRANCH    OVRCD,MVHS1000,MVHS1000
            MOVE      "1",PTMXSIN4
            CALL      UPMAST1
            CALL      UUPTMAS1
          ENDIF
.
MVHS9000  GOTO      MVHS1000
.
MVHS9999  RETURN
+
.------------------------------------------------------------------------------
. Move files to new directories with new file name with new file name
.------------------------------------------------------------------------------
MVDC0000  MOVE      PMHBVISN,STORVISN
          MOVE      PMHBEAID,DISPEAID
          MOVE      OBPCCPID,DISPPCCD
          MOVE      OBECCPID,DISPECCD
          REP       " 0",STORVISN
          REP       " 0",DISPEAID
          REP       " 0",DISPPCCD
          REP       " 0",DISPECCD
.
          PACK      KEY4,OBECSLID,SP70
          CALL      RDOBPT1
          STRIP     OBPTSDIR
.
          PACK      FROMFILE,OBPTSDIR,DISPEAID,DASH,DISPECCD,SP70
          PACK      TOFILENM,OBPTSDIR,STORVISN,DASH,DISPPCCD,SP70
          STRIP     FROMFILE
          STRIP     TOFILENM
.
          CLEAR     CMDLINE
          APPEND    "eadhspdf.us1 ",CMDLINE
          APPEND    FROMFILE,CMDLINE           * old filename
          APPEND    ".pdf",CMDLINE
          APPEND    " ",CMDLINE
          APPEND    TOFILENM,CMDLINE           * new filename
          APPEND    ".pdf",CMDLINE
          RESET     CMDLINE
          EXECUTE   CMDLINE,TASKID
.
MVDC9999  RETURN
.
.------------------------------------------------------------
. Clinical Documents Audit: Add, Update, View and Delete
. Input - OBAUTYPE (A,U,V,D)
.------------------------------------------------------------
OBSAUD00  MOVE      OBPCURNO,OBAUURNO
          MOVE      OBPCCVIS,OBAUVISN
          MOVE      OBPCCPID,OBAUCORR
          CALL      IBACLOCK
          PACK      OBAUDATE,CCC,CYY,CMM,CDD
          REP       " 0",OBAUDATE
          PACK      OBAUTIME,CTIMEIS
          MOVE      SP70,OBAUWEBU
          MOVE      SP70,OBAUREAS
          PACK      OBAUSPAR,SP70
.
          MATCH     ANSU,OBAUTYPE
          IF        @EQUAL
            MOVE      OBPCUDF1,OBAUREAS
          ENDIF
.
          PACK      KEY47,OBAUURNO,OBAUVISN,OBAUCORR,OBAUDATE:
                          OBAUTIME,OBAUWEBU,OBAUTYPE,SP70
          CALL      RAOBAUD1
          IF        OVRCD=1
            CALL      WROBAUD1
          ENDIF
.
OBSAUD99  RETURN
+
.--------------------------------------------------------
. Add Correspondance File to Visit
.--------------------------------------------------------
ADDCOR00  PACK      KEY20,URNUMBER,PMHBVISN,Z70
          CALL      RSOBPC1
          CALL      RPOBPC1
          BRANCH    OVRCD,ADDCOR10
.
          MATCH     URNUMBER,OBPCURNO
          IF        !@EQUAL
            GOTO      ADDCOR10
          ENDIF
.
          MATCH     PMHBVISN,OBPCCVIS
          IF        @EQUAL
            MOVE      OBPCCPID,FORM4
            ADD       ONE,FORM4
            MOVE      FORM4,OBPCCPID
            GOTO      ADDCOR20
          ENDIF
.
ADDCOR10  MOVE      "   1",OBPCCPID
ADDCOR20  READ      CONTROLF,NINTY1;*219,CWEBCLOC
          PACK      KEY4,CWEBCLOC
          CALL      RDOBPT1
          BRANCH    OVRCD,ADDCOR99
.
          MOVE      OBPTSLID,OBPCSLID       * Storage Location ID
          PACK      OBPCURNO,URNUMBER       * U/R
          PACK      OBPCCVIS,PMHBVISN       * Visit
.
          SCAN      ".",CORSP001            * Must be valid file with extension .pdf
          GOTO      ADDCOR99 IF NOT EQUAL
          MOVE      CORSP001,OBPCFEXT
          RESET     CORSP001
.
          STRIP     OBPTSDIR
          PACK      CORSFILE,OBPCCVIS,DASH,OBPCCPID,OBPCFEXT 
          REP       " 0",CORSFILE
          PACK      CMDLINE,CMDMOVE,CORSP001,SP1,OBPTSDIR,CORSFILE
          EXECUTE   CMDLINE,TASKID
.
          CALL      IBACLOCK
          PACK      DIM8,CCC,CYY,CMM,CDD
          REP       " 0",DIM8
          PACK      TODAY,DIM8,SP70
          CLOCK     TIME,CTIMEIS
.                                           * Write PCO record
          PACK      OBPCINDT,TODAY
          PACK      OBPCCTYP,CORSP003
          PACK      OBPCINTM,CTIMEIS
          MOVE      WBSEUID,OBPCINUS        * Web User ID
          PACK      OBPCDTLU,TODAY
          PACK      OBPCTMLU,CTIMEIS
          MOVE      WBSEUID,OBPCLUID        * Last Web User ID
          MOVE      "Patient Online Preadmission",OBPCCOM1
          MOVE      "Patient Portal",OBPCCFRM
.
          PACK      OBPCMDEL,SP70
          PACK      OBPCSLEV,SP70
          PACK      OBPCCOM2,SP70
          PACK      OBPCCTOO,SP70
          PACK      OBPCUDF1,SP70
          PACK      OBPCUDF2,SP70
          PACK      OBPCUYN1,SP70
          PACK      OBPCUYN2,SP70
          PACK      OBPCUDD1,SP70
          PACK      OBPCUDD2,SP70
          PACK      OBPCACAT,SP70
          PACK      OBPCACOD,SP70
.
          PACK      KEY20,OBPCURNO,OBPCCVIS,OBPCCPID
          CALL      WROBPC1
.
          MOVE      ANSA,OBAUTYPE           * Add
          CALL      OBSAUD00                * Clinical Documents Audit
.
          PACK      KEY8,URNUMBER           * validate UR
          CALL      RDMAST1
          BRANCH    OVRCD,ADDCOR99
.
          MATCH     "1",PTMXSIN4
          IF        !@EQUAL
            PACK      KEY8,URNUMBER         * validate UR
            CALL      RLPTMAS1
            BRANCH    OVRCD,ADDCOR99,ADDCOR99
            MOVE      "1",PTMXSIN4
            CALL      UPMAST1
            CALL      UUPTMAS1
          ENDIF
.
ADDCOR99  RETURN
.
          INC       PATMA1IO/INC
          INC       PMSEHBIO/INC
          INC       OBSAUDIO/INC
          INC       OBSECOIO/INC
          INC       OBSPCOIO/INC
          INC       OBSPCTIO/INC
          INC       IBAOVRIO/INC
.
UPEADM99  ENDPROC
